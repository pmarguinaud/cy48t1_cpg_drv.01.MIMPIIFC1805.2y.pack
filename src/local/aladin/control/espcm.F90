SUBROUTINE ESPCM(YDGEOMETRY,YDMODEL,YDFIELDS,CDCONF,LDESPCL,LDIAU,KCPLSPEC2,KCPLSPEC2V)

!**** *ESPCM* - Interface for spectral calculations in LAM model.

!     Purpose.
!     --------

!**   Interface.
!     ----------
!        *CALL* *ESPCM(CDCONF)

!     Explicit arguments :
!     --------------------
!      INPUT:
!       CDCONF         - configuration of work.
!                        CDCONF='0': nothing done
!                        CDCONF='A': SI scheme and horizontal diffusion done.
!                        CDCONF='I': SI scheme done; no horizontal diffusion.
!       LDESPCL        - T if spectral nudging at this time step.
!       LDIAU          - T if IAU at this time step.  
!       KCPLSPEC2      - local spectral dimension for spectral nudging.
!       KCPLSPEC2V     - full-columns local spectral dimension for spectral nudging.

!     Implicit arguments :
!     --------------------
!       See #include below.

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS
!        Documentation about FULL-POS.

!     Author.
!     -------
!        Unknown   * ALADIN project *
!        Original : 87-11-24 (routine SPCM in Arpege/IFS)

!     Modifications.
!     --------------
!        I. Santos, I. Martinez: (Mar 2010). Variable map factor for RTM.
!        K. Yessad (Jan 2011): new architecture for LBC modules and set-up.
!        R. El Khatib and F. Voitus (Mar 2011): optimisations.
!        K. Yessad (Feb 2012): simplifications for tests and CDCONF.
!        K. Yessad (May 2012): externalisation of coupling.
!        R. El Khatib 18-Jul-2012 Fullpos move away
!        A. Bogatchev 12-04-2013 phasing cy40, coherence with modified modules
!                                and renamed namelists and functions
!        B. Bochenek (Oct 2013): LESWLS,EWB1,EWB2 removed
!        B. Bochenek (Apr 2014) Phasing to CY40R2, Add optional YDSP
!        P. Brousseau (Apr 2014) : IAU
!        B. Bochenek (Apr 2015): Phasing: move some variables.
!        O. Marsden: June 2015 CY42 YRGMV, YRGFL, YRSURF, YRGMV5, and YRGFL5 are now passed by argument
!        K. Yessad (Dec 2016): Prune obsolete options.
!        K. Yessad (June 2017): Introduce NHQE model.
!        K. Yessad (June 2018): Alternate NHEE SI scheme elimination.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE TYPE_MODEL, ONLY : MODEL
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCT0   , ONLY : LR3D, LNHDYN, LNHEE, LNHQE
USE YOMCT3   , ONLY : NSTEP
USE YOMDYNA  , ONLY : YRDYNA
USE YOMMP0   , ONLY : MYSETN, MYSETV
USE YOMLUN   , ONLY : NULOUT
USE YOMSP    , ONLY : SPVOR_FLT, SPDIV_FLT
USE YEMLBC_INIT,ONLY : LSPTENC
USE OML_MOD  , ONLY : OML_GET_NUM_THREADS
USE SPECTRAL_FIELDS_DATA, ONLY: SPECTRAL_FIELD
!
USE FIELDS_MOD, ONLY: FIELDS
!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(IN)  :: YDGEOMETRY
TYPE(MODEL),INTENT(INOUT):: YDMODEL
!
TYPE(FIELDS),INTENT(INOUT):: YDFIELDS
!
CHARACTER(LEN=1),   INTENT(IN) :: CDCONF 
LOGICAL,            INTENT(IN) :: LDESPCL
LOGICAL,            INTENT(IN) :: LDIAU
INTEGER(KIND=JPIM), INTENT(IN) :: KCPLSPEC2
INTEGER(KIND=JPIM), INTENT(IN) :: KCPLSPEC2V

!     ------------------------------------------------------------------

REAL(KIND=JPRB) :: ZSPVORG(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPDIVG(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPVORG_FLT(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPDIVG_FLT(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPTG  (YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPSPDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2V_NH)
REAL(KIND=JPRB) :: ZSPSVDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2V_NH)
REAL(KIND=JPRB) :: ZSPNHXG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2V_NHX)
REAL(KIND=JPRB) :: ZSPSPG (MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF))
REAL(KIND=JPRB) :: ZSPGFLG(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF), &
 & MAX(1,YDMODEL%YRML_GCONF%YGFL%NUMSPFLDS))
REAL(KIND=JPRB) :: ZSPTNDSI_VORG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH),&
 &  ZSPTNDSI_DIVG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDSI_TG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDSI_SPDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH),&
 &  ZSPTNDSI_SVDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDHD_VORG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH),&
 &  ZSPTNDHD_DIVG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDHD_TG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDHD_SPDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH),&
 &  ZSPTNDHD_SVDG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDHD_SNHXG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH)
REAL(KIND=JPRB) :: ZSPTNDHD_GFLG(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRMP%NSPEC2VDDH,1)
REAL(KIND=JPRB) :: ZSPVOR3(YDGEOMETRY%YRDIMV%NFLEVL,KCPLSPEC2), ZSPVOR3G(YDGEOMETRY%YRDIMV%NFLEVG,KCPLSPEC2V)
REAL(KIND=JPRB) :: ZSPDIV3(YDGEOMETRY%YRDIMV%NFLEVL,KCPLSPEC2), ZSPDIV3G(YDGEOMETRY%YRDIMV%NFLEVG,KCPLSPEC2V)
REAL(KIND=JPRB) :: ZSPT3(YDGEOMETRY%YRDIMV%NFLEVL,KCPLSPEC2), ZSPT3G(YDGEOMETRY%YRDIMV%NFLEVG,KCPLSPEC2V)
REAL(KIND=JPRB) :: ZSPGFL3(YDGEOMETRY%YRDIMV%NFLEVL,KCPLSPEC2,YDMODEL%YRML_GCONF%YGFL%NUMSPFLDS)
REAL(KIND=JPRB) :: ZSPGFL3G(YDGEOMETRY%YRDIMV%NFLEVG,KCPLSPEC2V,YDMODEL%YRML_GCONF%YGFL%NUMSPFLDS)
REAL(KIND=JPRB) :: ZSPSP3(KCPLSPEC2), ZSPSP3G(KCPLSPEC2V)
REAL(KIND=JPRB) :: ZSPBUFG(YDGEOMETRY%YRDIMV%NFLEVG,MAX(YDGEOMETRY%YRMP%NSPEC2V,YDGEOMETRY%YRMP%NSPEC2VF),3)

LOGICAL :: LLONEM
LOGICAL :: LLESPCL, LLDOSI, LLDOHD, LLIAU
LOGICAL :: LLDO_PARTSI(3,0:YDMODEL%YRML_DYN%YRDYN%NITERHELM+1)

INTEGER(KIND=JPIM) :: IOFF, IM, IEND, IMLOC, ISTA, JMLOCF, ISPEC, ISPEC2V
INTEGER(KIND=JPIM) :: ITHRDS, IBLKS, JSTA
INTEGER(KIND=JPIM) :: IPART_2A, IPART_2B2C, IPART_2D, JITER
INTEGER(KIND=JPIM) :: IDIMV, IDIMH

REAL(KIND=JPRB) :: ZHOOK_HANDLE
REAL(KIND=JPRB),ALLOCATABLE :: ZZPS(:)
!----POINTERS FOR ASSOCIATION --------------------------
INTEGER(KIND=JPIM),POINTER :: NEK0, NEK1, NEN1, NEN2
REAL(KIND=JPRB),POINTER :: SPNUDVOR, SPNUDDIV, SPNUDT, SPNUDQ, SPNUDSP, RNUTENC
!     ------------------------------------------------------------------

#include "espcpl.h"
#include "espsc2r.h"

#include "abor1.intfb.h"
#include "espchor.intfb.h"
#include "espcsi.intfb.h"
#include "espiau.intfb.h"
#include "espnhsi.intfb.h"
#include "espnheesi.intfb.h"
#include "espnhqesi.intfb.h"
#include "espnhqesi_prodskap.intfb.h"
#include "trmtos.intfb.h"
#include "trstom.intfb.h"
#include "espfilt.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ESPCM',0,ZHOOK_HANDLE)
!     ------------------------------------------------------------------
ASSOCIATE(YDML_DIAG=>YDMODEL%YRML_DIAG, YDML_GCONF=>YDMODEL%YRML_GCONF, YDML_DYN=>YDMODEL%YRML_DYN, &
 & YRML_LBC=>YDMODEL%YRML_LBC, YRELBC_FIELDS=>YDFIELDS%YRELBC_FIELDS, YRSP=>YDFIELDS%YRSPEC,&
 & YDLAP=>YDGEOMETRY%YRLAP, YDELAP=>YDGEOMETRY%YRELAP)
ASSOCIATE( &
 & NS3D=>YDML_GCONF%YRDIMF%NS3D, LIMPF=>YDML_DYN%YRDYN%LIMPF, NITERHELM=>YDML_DYN%YRDYN%NITERHELM, YDMP=>YDGEOMETRY%YRMP, &
 & LESIDG=>YDML_DYN%YREDYN%LESIDG, &
 & NISNAX=>YDGEOMETRY%YREDIM%NISNAX, &
 & NFLEVL=>YDGEOMETRY%YRDIMV%NFLEVL, NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, &
 & NSPEC2=>YDGEOMETRY%YRDIM%NSPEC2, NSMAX=>YDGEOMETRY%YRDIM%NSMAX, NDLON=>YDGEOMETRY%YRDIM%NDLON, &
 & NDGLG=>YDGEOMETRY%YRDIM%NDGLG, &
 & NPTRMF=>YDGEOMETRY%YRMP%NPTRMF, NSPSTAF=>YDGEOMETRY%YRMP%NSPSTAF, NSPEC2V=>YDGEOMETRY%YRMP%NSPEC2V, &
 & NSPEC2VF=>YDGEOMETRY%YRMP%NSPEC2VF, NPTRSV=>YDGEOMETRY%YRMP%NPTRSV, NPTRSVF=>YDGEOMETRY%YRMP%NPTRSVF, &
 & NSPEC2VDDH=>YDGEOMETRY%YRMP%NSPEC2VDDH, NSPEC2V_NH=>YDGEOMETRY%YRMP%NSPEC2V_NH, &
 & NSPEC2V_NHX=>YDGEOMETRY%YRMP%NSPEC2V_NHX, YQ=>YDML_GCONF%YGFL%YQ, YQ_NL=>YDML_GCONF%YGFL%YQ_NL,&
 & NSTOP=>YDML_GCONF%YRRIP%NSTOP, &
 & SPTNDHD_DIV=>YDML_DIAG%YRSPDDH%SPTNDHD_DIV, SPTNDHD_GFL=>YDML_DIAG%YRSPDDH%SPTNDHD_GFL, &
 & SPTNDHD_SNHX=>YDML_DIAG%YRSPDDH%SPTNDHD_SNHX, SPTNDHD_SPD=>YDML_DIAG%YRSPDDH%SPTNDHD_SPD, &
 & SPTNDHD_SVD=>YDML_DIAG%YRSPDDH%SPTNDHD_SVD, SPTNDHD_T=>YDML_DIAG%YRSPDDH%SPTNDHD_T, &
 & SPTNDHD_VOR=>YDML_DIAG%YRSPDDH%SPTNDHD_VOR, SPTNDSI_DIV=>YDML_DIAG%YRSPDDH%SPTNDSI_DIV, &
 & SPTNDSI_SPD=>YDML_DIAG%YRSPDDH%SPTNDSI_SPD, SPTNDSI_SVD=>YDML_DIAG%YRSPDDH%SPTNDSI_SVD, &
 & SPTNDSI_T=>YDML_DIAG%YRSPDDH%SPTNDSI_T, SPTNDSI_VOR=>YDML_DIAG%YRSPDDH%SPTNDSI_VOR, &
 & LRSIDDH=>YDML_DIAG%YRLDDH%LRSIDDH, LRHDDDH=>YDML_DIAG%YRLDDH%LRHDDDH)
!     ------------------------------------------------------------------

! LESIDG: like LSIDG but for ALADIN in the case where:
!  - large domain where the variations of the mapping factor M are large.
!  - type of projections where M is quasi constant on a latitude and
!    M can be written as a low order polynomial of mu=sin(latitude).

IF (LESIDG.OR.LIMPF) THEN
  LLONEM=.TRUE.
  ISPEC=NSPEC2VF
ELSE
  LLONEM=.FALSE.
  ISPEC=NSPEC2V
ENDIF
! ky: ISPEC2V must be equal to ISPEC but that needs to be checked!!!
ISPEC2V=MAX(NSPEC2V,NSPEC2VF)
IDIMV=UBOUND(YRSP%DIV,1)
IDIMH=UBOUND(YRSP%DIV,2)

IF (LLONEM) THEN
  IOFF=NPTRSVF(MYSETV)-1
ELSE
  IOFF=NPTRSV(MYSETV)-1
ENDIF

IPART_2A=1
IPART_2B2C=2
IPART_2D=3
IF (LNHDYN.AND.LNHQE.AND.(.NOT.YRDYNA%LNHQE_SIHYD)) THEN
  IF (NITERHELM==0) THEN
    ! Predictor:
    LLDO_PARTSI(IPART_2A,0)=.TRUE.
    LLDO_PARTSI(IPART_2B2C,0)=.TRUE.
    LLDO_PARTSI(IPART_2D,0)=.FALSE.
    ! Corrector:
    LLDO_PARTSI(IPART_2A,1)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,1)=.FALSE.
    LLDO_PARTSI(IPART_2D,1)=.TRUE.
  ELSEIF (NITERHELM>0) THEN
    ! Predictor:
    LLDO_PARTSI(IPART_2A,0)=.TRUE.
    LLDO_PARTSI(IPART_2B2C,0)=.TRUE.
    LLDO_PARTSI(IPART_2D,0)=.FALSE.
    ! Intermediate iterations:
    LLDO_PARTSI(IPART_2A,1:NITERHELM)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,1:NITERHELM)=.TRUE.
    LLDO_PARTSI(IPART_2D,1:NITERHELM)=.FALSE.
    ! Corrector:
    LLDO_PARTSI(IPART_2A,NITERHELM+1)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,NITERHELM+1)=.FALSE.
    LLDO_PARTSI(IPART_2D,NITERHELM+1)=.TRUE.
  ENDIF
ELSE
  LLDO_PARTSI(:,:)=.TRUE.
ENDIF

IF (CDCONF == 'A'.OR.CDCONF == 'I') THEN

 IF (LR3D) THEN
  ALLOCATE(ZZPS(NSPEC2))
  IF(YDMP%NPSP==1)ZZPS=YRSP%SP

  CALL GSTATS(54,0)

  IF (YDML_GCONF%YGFL%NUMSPFLDS > 0) THEN
    CALL TRMTOS(YDGEOMETRY,PSPVOR=YRSP%VOR,PSPDIV=YRSP%DIV,PSPT=YRSP%T,&
       & PSPSPD=YRSP%SPD,PSPSVD=YRSP%SVD,PSPSNHX=YRSP%NHX,&
       & PSPGFL=YRSP%GFL,PSPSP=ZZPS,&
       & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,&
       & PSPSPDG=ZSPSPDG,PSPSVDG=ZSPSVDG,PSPSNHXG=ZSPNHXG,&
       & PSPGFLG=ZSPGFLG,PSPSPG=ZSPSPG,LDFULLM=LLONEM)
  ELSE
    CALL TRMTOS(YDGEOMETRY,PSPVOR=YRSP%VOR,PSPDIV=YRSP%DIV,PSPT=YRSP%T,&
        & PSPSPD=YRSP%SPD,PSPSVD=YRSP%SVD,PSPSNHX=YRSP%NHX,&
        & PSPSP=ZZPS,&
        & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,&
        & PSPSPDG=ZSPSPDG,PSPSVDG=ZSPSVDG,PSPSNHXG=ZSPNHXG,&
        & PSPSPG=ZSPSPG,LDFULLM=LLONEM)
  ENDIF
  IF(YRDYNA%LGRADSP) THEN
    CALL TRMTOS(YDGEOMETRY,PSPVOR=SPVOR_FLT,PSPDIV=SPDIV_FLT,&
      & PSPVORG=ZSPVORG_FLT,PSPDIVG=ZSPDIVG_FLT,LDFULLM=LLONEM)
  ENDIF

  CALL GSTATS(54,2)

  CALL GSTATS(9,0)

  ! * SEMI-IMPLICIT SCMEME.
  LLDOSI=(CDCONF == 'A'.OR.CDCONF == 'I')

  IF (LLDOSI) THEN
    IF (LLONEM) THEN

      ! Each zonal wave number has to be treated separately
      CALL GSTATS(1029,0)

      IF (LNHEE.AND.(.NOT.YRDYNA%LSI_NHEE)) THEN

        DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
          IM=YDLAP%MYMS(JMLOCF)
          ISTA=NSPSTAF(IM)
          IEND=ISTA+4*(NISNAX(IM)+1)-1
          CALL ESPNHSI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,YDML_DYN%YREDYN,IM,JMLOCF,ISTA,IEND,LLONEM,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
           & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
        ENDDO

      ELSEIF (LNHEE.AND.YRDYNA%LSI_NHEE) THEN

        DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
          IM=YDLAP%MYMS(JMLOCF)
          ISTA=NSPSTAF(IM)
          IEND=ISTA+4*(NISNAX(IM)+1)-1
          CALL ESPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,IM,JMLOCF,ISTA,IEND,LLONEM,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
           & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
        ENDDO

      ELSEIF (LNHQE.AND.(.NOT.YRDYNA%LNHQE_SIHYD)) THEN

        DO JITER=0,NITERHELM+1

          IF (JITER == 0) THEN
            ZSPBUFG(:,:,1)=ZSPDIVG(:,:)
          ENDIF
          CALL ESPNHQESI_PRODSKAP(LLONEM,YDGEOMETRY,YDFIELDS%YRGMV,IDIMV,IDIMH,ISPEC2V,ZSPBUFG(1,1,1),ZSPBUFG(1,1,3))

          DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
            IM=YDLAP%MYMS(JMLOCF)
            ISTA=NSPSTAF(IM)
            IEND=ISTA+4*(NISNAX(IM)+1)-1
            CALL ESPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,&
             & IM,JMLOCF,ISTA,IEND,LLONEM,LLDO_PARTSI(1,JITER),&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,ISTA:IEND,1:3),&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
          ENDDO

        ENDDO ! JITER

      ELSE

        DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
          IM=YDLAP%MYMS(JMLOCF)
          ISTA=NSPSTAF(IM)
          IEND=ISTA+4*(NISNAX(IM)+1)-1
          CALL ESPCSI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,YDML_DYN%YREDYN,IM,JMLOCF,ISTA,IEND,LLONEM, &
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,&
           & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG)
        ENDDO
        IF(LNHQE.AND.YRDYNA%LNHQE_SIHYD) ZSPSPDG(:,:)=0.0_JPRB

      ENDIF

      CALL GSTATS(1029,1)

    ELSE

      ! All spectral columns are done in one go

      IF (NSPEC2V > 0) THEN
        IM=-999
        IMLOC=-999

        IF (LNHEE.AND.(.NOT.YRDYNA%LSI_NHEE)) THEN

!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
          ITHRDS=OML_GET_NUM_THREADS()
          IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
          DO JSTA=1,NSPEC2V,IBLKS
            IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
            CALL ESPNHSI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,YDML_DYN%YREDYN,IM,IMLOC,JSTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
          ENDDO
!$OMP END DO
!$OMP END PARALLEL

        ELSEIF (LNHEE.AND.YRDYNA%LSI_NHEE) THEN

!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
          ITHRDS=OML_GET_NUM_THREADS()
          IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
          DO JSTA=1,NSPEC2V,IBLKS
            IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
            CALL ESPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,IM,IMLOC,JSTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
          ENDDO
!$OMP END DO
!$OMP END PARALLEL

        ELSEIF (LNHQE.AND.(.NOT.YRDYNA%LNHQE_SIHYD)) THEN

          DO JITER=0,NITERHELM+1

            IF (JITER == 0) THEN
              ZSPBUFG(:,:,1)=ZSPDIVG(:,:)
            ENDIF
            CALL ESPNHQESI_PRODSKAP(LLONEM,YDGEOMETRY,YDFIELDS%YRGMV,IDIMV,IDIMH,ISPEC2V,ZSPBUFG(1,1,1),ZSPBUFG(1,1,3))

!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
            ITHRDS=OML_GET_NUM_THREADS()
            IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
            DO JSTA=1,NSPEC2V,IBLKS
              IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
              CALL ESPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,&
               & IM,IMLOC,JSTA,IEND,LLONEM,LLDO_PARTSI(1,JITER),&
               & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,JSTA:IEND,1:3),&
               & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
            ENDDO
!$OMP END DO
!$OMP END PARALLEL

          ENDDO ! JITER

        ELSE

!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
          ITHRDS=OML_GET_NUM_THREADS()
          IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
          DO JSTA=1,NSPEC2V,IBLKS
            IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
            CALL ESPCSI(YDMODEL%YRCST,YDGEOMETRY,YDML_DIAG%YRLDDH,YDML_GCONF%YRRIP,YDML_DYN%YRDYN,YDML_DYN%YREDYN,IM,IMLOC,JSTA,IEND,LLONEM, &
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG)
          ENDDO
!$OMP END DO
!$OMP END PARALLEL
          IF(LNHQE.AND.YRDYNA%LNHQE_SIHYD) ZSPSPDG(:,:)=0.0_JPRB

        ENDIF

      ENDIF ! NSPEC2V > 0
    ENDIF ! LLONEM
  ENDIF ! LLDOSI

  ! * SPECTRAL NUDGING.
  LLESPCL=LDESPCL.AND.(CDCONF == 'A'.OR.CDCONF == 'I')
  IF (LLESPCL) THEN

    ! ky: ESPSC2R is externalisable part of spectral nudging.
    CALL ESPSC2R(NS3D,NSPEC2,NFLEVL,NSTOP,YQ%MPSP,YRELBC_FIELDS%NOFFGT3BSP,NSTEP,&
     & YQ_NL%LSP,YRML_LBC%LSPNUSPDL,LSPTENC,YRML_LBC%RNUDTFRAC,YRML_LBC%RNUTENC,YRELBC_FIELDS%GT3SPBUF,YRML_LBC%EWB,&
     & ZSPVOR3,ZSPDIV3,ZSPT3,ZSPGFL3,ZSPSP3)

    IF (ANY(YRML_LBC%LNUDSPGFL)) THEN
      IF (YRML_LBC%LSPNUSPDL) THEN
        CALL TRMTOS(YDGEOMETRY,PSPVOR=ZSPVOR3,PSPDIV=ZSPDIV3,PSPT=ZSPT3,&
         & PSPGFL=ZSPGFL3,PSPSP=ZSPSP3,LDSELECT3D=YRML_LBC%LNUDSPGFL,&
         & PSPVORG=ZSPVOR3G,PSPDIVG=ZSPDIV3G,PSPTG=ZSPT3G,&
         & PSPGFLG=ZSPGFL3G,PSPSPG=ZSPSP3G)
      ELSE
        CALL TRMTOS(YDGEOMETRY,PSPVOR=ZSPVOR3,PSPDIV=ZSPDIV3,PSPT=ZSPT3,&
         & PSPGFL=ZSPGFL3,LDSELECT3D=YRML_LBC%LNUDSPGFL,&
         & PSPVORG=ZSPVOR3G,PSPDIVG=ZSPDIV3G,PSPTG=ZSPT3G,&
         & PSPGFLG=ZSPGFL3G)
      ENDIF
    ELSE
      IF (YRML_LBC%LSPNUSPDL) THEN
        CALL TRMTOS(YDGEOMETRY,PSPVOR=ZSPVOR3,PSPDIV=ZSPDIV3,PSPT=ZSPT3,&
         & PSPSP=ZSPSP3,&
         & PSPVORG=ZSPVOR3G,PSPDIVG=ZSPDIV3G,PSPTG=ZSPT3G,&
         & PSPSPG=ZSPSP3G)
      ELSE
        CALL TRMTOS(YDGEOMETRY,PSPVOR=ZSPVOR3,PSPDIV=ZSPDIV3,PSPT=ZSPT3,&
         & PSPVORG=ZSPVOR3G,PSPDIVG=ZSPDIV3G,PSPTG=ZSPT3G)
      ENDIF
    ENDIF

    IF (LLONEM) THEN
      !   Each zonal wave number has to be treated separately
!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOCF,IM,ISTA,IEND)
      DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
        IM=YDLAP%MYMS(JMLOCF)
        ISTA=NSPSTAF(IM)
        IEND=ISTA+4*(NISNAX(IM)+1)-1
        IF (ISPEC > 0) THEN
          ! ky: ESPCPL is externalisable part of spectral nudging.
          CALL ESPCPL(NDLON,NDGLG,NFLEVG,ISTA,IEND,IOFF,&
           & YDLAP%NVALUE,YDELAP%MVALUE,YQ%MPSP,&
           & YRML_LBC%NEK0,YRML_LBC%NEK1,YRML_LBC%NEN1,YRML_LBC%NEN2,YRML_LBC%LSPNUSPDL,YQ_NL%LSP,&
           & YRML_LBC%SPNUDVOR,YRML_LBC%SPNUDDIV,YRML_LBC%SPNUDT,YRML_LBC%SPNUDQ,YRML_LBC%SPNUDSP,&
           & ZSPVOR3G,ZSPDIV3G,ZSPT3G,ZSPGFL3G,ZSPSP3G,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPGFLG,ZSPSPG)
        ENDIF
      ENDDO
!$OMP END PARALLEL DO
    ELSE
      !   All spectral columns are done in one go
      IF (NSPEC2V > 0) THEN
        IM=-999
!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
        ITHRDS=OML_GET_NUM_THREADS()
        IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
        DO JSTA=1,NSPEC2V,IBLKS
          IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
          IF (ISPEC > 0) THEN
            ! ky: ESPCPL is externalisable part of spectral nudging.
            CALL ESPCPL(NDLON,NDGLG,NFLEVG,JSTA,IEND,IOFF,&
             & YDLAP%NVALUE,YDELAP%MVALUE,YQ%MPSP,&
             & YRML_LBC%NEK0,YRML_LBC%NEK1,YRML_LBC%NEN1,YRML_LBC%NEN2,YRML_LBC%LSPNUSPDL,YQ_NL%LSP,&
             & YRML_LBC%SPNUDVOR,YRML_LBC%SPNUDDIV,YRML_LBC%SPNUDT,YRML_LBC%SPNUDQ,YRML_LBC%SPNUDSP,&
             & ZSPVOR3G,ZSPDIV3G,ZSPT3G,ZSPGFL3G,ZSPSP3G,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPGFLG,ZSPSPG)
          ENDIF
        ENDDO
!$OMP END DO
!$OMP END PARALLEL
      ENDIF
    ENDIF

  ENDIF ! LLESPCL

  ! * HORIZONTAL DIFFUSION.
  LLDOHD=(CDCONF == 'A')

  IF (LLDOHD) THEN
    IF (LLONEM) THEN
      !   Each zonal wave number has to be treated separately
!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOCF,IM,ISTA,IEND)
      DO JMLOCF=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
        IM=YDLAP%MYMS(JMLOCF)
        ISTA=NSPSTAF(IM)
        IEND=ISTA+4*(NISNAX(IM)+1)-1
        IF (ISPEC > 0) THEN
          IF( YRDYNA%LGRADSP ) THEN
            CALL ESPFILT(YDGEOMETRY,YDML_DYN%YREDYN,ISTA,IEND,IOFF,&
              &ZSPVORG_FLT,ZSPDIVG_FLT,ZSPSPDG)
          ENDIF
          CALL ESPCHOR(YDGEOMETRY,YDMODEL,ISTA,IEND,IOFF,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPDG,ZSPSVDG,ZSPNHXG,ZSPGFLG,ZSPSPG,&
           & ZSPTNDHD_VORG,ZSPTNDHD_DIVG,ZSPTNDHD_TG,&
           & ZSPTNDHD_SPDG,ZSPTNDHD_SVDG,ZSPTNDHD_SNHXG,ZSPTNDHD_GFLG)
        ENDIF
      ENDDO
!$OMP END PARALLEL DO
    ELSE
      !   All spectral columns are done in one go
      IF (NSPEC2V > 0) THEN
        IM=-999
!$OMP PARALLEL&
!$OMP& PRIVATE(ITHRDS,IBLKS,JSTA,IEND)
        ITHRDS=OML_GET_NUM_THREADS()
        IBLKS=(NSPEC2V-1+ITHRDS)/ITHRDS
!$OMP DO SCHEDULE(STATIC)
        DO JSTA=1,NSPEC2V,IBLKS
          IEND=MIN(NSPEC2V,JSTA+IBLKS-1)
          IF (ISPEC > 0) THEN
            IF( YRDYNA%LGRADSP ) THEN
              CALL ESPFILT(YDGEOMETRY,YDML_DYN%YREDYN,JSTA,IEND,IOFF,&
                &ZSPVORG_FLT,ZSPDIVG_FLT,ZSPSPDG)
            ENDIF
            CALL ESPCHOR(YDGEOMETRY,YDMODEL,JSTA,IEND,IOFF,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPDG,ZSPSVDG,ZSPNHXG,ZSPGFLG,ZSPSPG,&
             & ZSPTNDHD_VORG,ZSPTNDHD_DIVG,ZSPTNDHD_TG,&
             & ZSPTNDHD_SPDG,ZSPTNDHD_SVDG,ZSPTNDHD_SNHXG,ZSPTNDHD_GFLG)
          ENDIF
        ENDDO
!$OMP END DO
!$OMP END PARALLEL
      ENDIF
    ENDIF ! LLONEM
  ENDIF ! LLDOHD

  CALL GSTATS(9,2)

  CALL GSTATS(54,3)

  ! ky: this call to TRSTOM has to be moved later between ESPCSI/ESPNHSI
  !     and ESPCHOR to be consistent with what is done in SPCM.
  IF (YDML_GCONF%YGFL%NUMSPFLDS > 0) THEN
    CALL TRSTOM(YDGEOMETRY,PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,&
       & PSPSPDG=ZSPSPDG,PSPSVDG=ZSPSVDG,PSPSNHXG=ZSPNHXG,&
       & PSPGFLG=ZSPGFLG,PSPSPG=ZSPSPG,&
       & PSPVOR=YRSP%VOR,PSPDIV=YRSP%DIV,PSPT=YRSP%T,&
       & PSPSPD=YRSP%SPD,PSPSVD=YRSP%SVD,PSPSNHX=YRSP%NHX,&
       & PSPGFL=YRSP%GFL,PSPSP=ZZPS,LDFULLM=LLONEM,LDNEEDPS=.TRUE.)
  ELSE
    CALL TRSTOM(YDGEOMETRY,PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,&
       & PSPSPDG=ZSPSPDG,PSPSVDG=ZSPSVDG,PSPSNHXG=ZSPNHXG,&
       & PSPSPG=ZSPSPG,&
       & PSPVOR=YRSP%VOR,PSPDIV=YRSP%DIV,PSPT=YRSP%T,&
       & PSPSPD=YRSP%SPD,PSPSVD=YRSP%SVD,PSPSNHX=YRSP%NHX,&
       & PSPSP=ZZPS,LDFULLM=LLONEM,LDNEEDPS=.TRUE.)
  ENDIF
  IF(YRDYNA%LGRADSP) THEN
    CALL TRSTOM(YDGEOMETRY,PSPVORG=ZSPVORG_FLT,PSPDIVG=ZSPDIVG_FLT,&
     & PSPVOR=SPVOR_FLT,PSPDIV=SPDIV_FLT,LDFULLM=LLONEM,LDNEEDPS=.FALSE.)
  ENDIF
  IF (LRSIDDH) THEN
    CALL TRSTOM(&
     & YDGEOMETRY,PSPVORG=ZSPTNDSI_VORG,PSPDIVG=ZSPTNDSI_DIVG,PSPTG=ZSPTNDSI_TG,&
     & PSPSPDG=ZSPTNDSI_SPDG,PSPSVDG=ZSPTNDSI_SVDG,&
     & PSPVOR=SPTNDSI_VOR,PSPDIV=SPTNDSI_DIV,PSPT=SPTNDSI_T,&
     & PSPSPD=SPTNDSI_SPD,PSPSVD=SPTNDSI_SVD,LDFULLM=LLONEM)
  ENDIF
  IF (LRHDDDH) THEN
    IF (YDML_GCONF%YGFL%NUMSPFLDS > 0) THEN
      CALL TRSTOM(&
       & YDGEOMETRY,PSPVORG=ZSPTNDHD_VORG,PSPDIVG=ZSPTNDHD_DIVG,PSPTG=ZSPTNDHD_TG,&
       & PSPGFLG=ZSPTNDHD_GFLG,&
       & PSPSPDG=ZSPTNDHD_SPDG,PSPSVDG=ZSPTNDHD_SVDG,PSPSNHXG=ZSPTNDHD_SNHXG,&
       & PSPVOR=SPTNDHD_VOR,PSPDIV=SPTNDHD_DIV,PSPT=SPTNDHD_T,&
       & PSPSPD=SPTNDHD_SPD,PSPSVD=SPTNDHD_SVD,PSPSNHX=SPTNDHD_SNHX,&
       & PSPGFL=SPTNDHD_GFL,LDFULLM=LLONEM,LDNEEDPS=.FALSE.)
    ELSE
      CALL TRSTOM(&
       & YDGEOMETRY,PSPVORG=ZSPTNDHD_VORG,PSPDIVG=ZSPTNDHD_DIVG,PSPTG=ZSPTNDHD_TG,&
       & PSPSPDG=ZSPTNDHD_SPDG,PSPSVDG=ZSPTNDHD_SVDG,PSPSNHXG=ZSPTNDHD_SNHXG,&
       & PSPVOR=SPTNDHD_VOR,PSPDIV=SPTNDHD_DIV,PSPT=SPTNDHD_T,&
       & PSPSPD=SPTNDHD_SPD,PSPSVD=SPTNDHD_SVD,PSPSNHX=SPTNDHD_SNHX,&
       & LDFULLM=LLONEM,LDNEEDPS=.FALSE.)
    ENDIF
  ENDIF
  IF(YDMP%NPSP==1)YRSP%SP=ZZPS
  DEALLOCATE(ZZPS)

  ! * IAU.
  LLIAU=LDIAU.AND.(CDCONF == 'A'.OR.CDCONF == 'I')
  IF (LLIAU) THEN
    CALL ESPIAU(YDGEOMETRY%YRMP,YDML_GCONF%YGFL,YRSP)
  ENDIF
  
  CALL GSTATS(54,1)
  CALL GSTATS(9,3)

  CALL GSTATS(9,1)

 ELSE

  CALL ABOR1('ESPCM MUST NOT BE CALLED FOR THIS VALUE OF NCONF')

 ENDIF ! LR3D

ELSE

  IF (CDCONF /= '0') THEN
    WRITE (NULOUT,*) 'CDCONF(9:9) = ', CDCONF
    CALL ABOR1('ESPCM: CONFIGURATION NOT ALLOWED: CHECK CDCONF')
  ENDIF

ENDIF ! CDCONF

END ASSOCIATE
END ASSOCIATE
!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ESPCM',1,ZHOOK_HANDLE)
END SUBROUTINE ESPCM
