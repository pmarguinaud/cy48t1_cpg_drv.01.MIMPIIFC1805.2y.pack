SUBROUTINE ESEIMPLS(YDGEOMETRY,YDMODEL,YDFIELDS,KDIM,PGM,PGT3GMV,PGT3GMVS)
 
!**** *ESEIMPLS*

!      Compute semi-implicit correction terms for large-scale fields

!     Purpose :
!     -------
 
!** Interface : 
!   ---------- 
!              *CALL* *ESEIMPLS(....) 

!              Explicit arguments : 
!              ------------------- 
!              KDIM    : first dimension for all fields
!              PGM     : map factor
!              PGT3GMV : coupler for GMV fields
!              PGT3GMVS: coupler for GMVS fields

!          Implicit arguments :
!          -------------------

!     Method.
!     ------

!     Externals.
!     ---------

!     Reference.
!     ---------
!     Documentation of ARPEGE/ALADIN

!     Author.
!     -------
!     G. Radnoti
!      Original: 94-01-18

!     Modifications.
!     --------------
!      K. Yessad (Aug 2009): cleaning
!      I. Santos and I. Martinez (Mar 2010): RTM variable map factor.
!      K. Yessad (May 2010): gather some non externalisable code.
!      K. Yessad (Jan 2011): new architecture for LBC modules and set-up.
!      A. Bogatchev 11-04-2014 phasing cy40 coherence with modified modules
!                              and renamed namelists and functions
!      B. Bochenek (Apr 2015): Phasing: move some variables.
!      O. Marsden: June 2015 CY42 YRGMV, YRGFL, YRSURF, YRGMV5, and YRGFL5 are now passed by argument
!      M. Hortal (Dec 2014): specify the first dimension of all fields.
!      K. Yessad (Dec 2016): Prune obsolete options.
!      K. Yessad (June 2017): Vertical-dependent SITRA.
!      K. Yessad (June 2017): Introduce NHQE model.
!      R. El khatib 30-Apr-2019 Fix erroneous bounds violation
!----------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE TYPE_MODEL , ONLY : MODEL
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCT0   , ONLY : LNHDYN, LNHEE, LNHQE
USE YOMCST   , ONLY : RG       ,RD

!
USE FIELDS_MOD, ONLY : FIELDS
USE YOMCVER      , ONLY : LVERTFE
!---------------------------------------------------------------------- 

IMPLICIT NONE 

TYPE(GEOMETRY), INTENT(INOUT)    :: YDGEOMETRY
TYPE(MODEL), INTENT(INOUT)       :: YDMODEL
TYPE(FIELDS), INTENT(INOUT)       :: YDFIELDS
INTEGER(KIND=JPIM),INTENT(IN)    :: KDIM
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGT3GMV(KDIM,YDGEOMETRY%YRDIMV%NFLEVG,YDFIELDS%YRELBC_FIELDS%YYTGMVCPL%NDIMT)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGT3GMVS(KDIM,YDFIELDS%YRELBC_FIELDS%YYTGMVSCPL%NDIMT)

!---------------------------------------------------------------------- 

INTEGER(KIND=JPIM) :: IPROFS, JLEV, JROF

REAL(KIND=JPRB) :: ZUSIC(KDIM,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB) :: ZVSIC(KDIM,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB) :: ZTSIC(KDIM,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB) :: ZPDSIC(KDIM,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB) :: ZVDSIC(KDIM,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB) :: ZSPSIC(KDIM) 

REAL(KIND=JPRB) :: ZSIT_O(KDIM,YDGEOMETRY%YRDIMV%NFLEVG), ZSID_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITL_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG), ZSIDL_O (KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITM_I (KDIM,YDGEOMETRY%YRDIMV%NFLEVG), ZSIDM_O (KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSISP_O(KDIM      )
REAL(KIND=JPRB) :: ZSISPL_I(KDIM      ), ZSISPM_I(KDIM       )
REAL(KIND=JPRB) :: ZSIPD_O(KDIM,YDGEOMETRY%YRDIMV%NFLEVG), ZSIPD9_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIPDL_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIVD(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIVD_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIVD_O(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIPDM_I(KDIM,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZDT, ZBDT, ZDIM

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!---------------------------------------------------------------------- 

#include "siseve.intfb.h"
#include "sidd.intfb.h"
#include "sigam.intfb.h"
#include "siptp.intfb.h"
#include "sitnu.intfb.h"
#include "silkap.intfb.h"

!---------------------------------------------------------------------- 

IF (LHOOK) CALL DR_HOOK('ESEIMPLS',0,ZHOOK_HANDLE)

ASSOCIATE(YRELBC_FIELDS=>YDFIELDS%YRELBC_FIELDS, YDDYN=>YDMODEL%YRML_DYN%YRDYN, YDEDYN=>YDMODEL%YRML_DYN%YREDYN, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP)
ASSOCIATE( &
 & SITR=>YDDYN%SITR, &
 & TDT=>YDRIP%TDT, &
 & LESIDG=>YDEDYN%LESIDG, &
 & NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, &
 & RSTRET=>YDGEOMETRY%YRGEM%RSTRET)
!---------------------------------------------------------------------- 
 
!     1. SEMI IMPLICIT CORRECTION ON LARGE SCALE FIELDS

! * Compute linear terms from PGT3GMV and PGT3GMVS, put them in Z[X]SIC.

ZDT=TDT
ZBDT=YDDYN%RBT

ZDIM = RG*RG/(RD*SITR)

IF (LESIDG) THEN
!$OMP PARALLEL DO
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSID_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MDIV)
      ZSITL_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MTL)
      ZSITM_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MTM)
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
ELSE
!$OMP PARALLEL DO
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSID_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MDIV)&
       & * RSTRET * RSTRET /(PGM(JROF)*PGM(JROF))
      ZSITL_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MTL)
      ZSITM_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MTM)
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
ENDIF

IF(LNHEE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSIPD9_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPD)
      ZSIVD_I (JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSVD)
      ZSIPDL_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPDL)
      ZSIPDM_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPDM)
    ENDDO
  ENDDO
ELSEIF(LNHQE.AND.(.NOT.YDMODEL%YRML_DYN%YRDYNA%LNHQE_SIHYD)) THEN
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSIPD9_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPD)
      ZSIPDL_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPDL)
      ZSIPDM_I(JROF,JLEV)=-PGT3GMV(JROF,JLEV,YRELBC_FIELDS%YYTGMVCPL%MSPDM)
    ENDDO
  ENDDO
ENDIF

DO JROF=1,KDIM
  ZSISPL_I(JROF)=-PGT3GMVS(JROF,YRELBC_FIELDS%YYTGMVSCPL%MSPL)
  ZSISPM_I(JROF)=-PGT3GMVS(JROF,YRELBC_FIELDS%YYTGMVSCPL%MSPM)
ENDDO

IPROFS=KDIM
IF(LNHEE) THEN

  CALL SIPTP(YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSID_I,ZSIVD_I,&
   & ZSIPD_O,ZSIT_O,ZSISP_O,IPROFS)
  CALL SIDD(LVERTFE, YDMODEL%YRML_DYN%YRDYNA,YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDL_O,ZSIVD,&
   & ZSIPDL_I,ZSITL_I,ZSISPL_I,IPROFS)
  CALL SIDD(LVERTFE, YDMODEL%YRML_DYN%YRDYNA,YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDM_O,ZSIVD,&
   & ZSIPDM_I,ZSITM_I,ZSISPM_I,IPROFS)
  CALL SISEVE(YDMODEL%YRML_DYN%YRDYNA,YDGEOMETRY,YDDYN,KDIM,1,ZSIPD9_I,ZSIVD_O,IPROFS)
!$OMP PARALLEL DO
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSIVD_O(JROF,JLEV)=ZDIM*ZSIVD_O(JROF,JLEV)
    ENDDO
  ENDDO
!$OMP END PARALLEL DO

ELSEIF(LNHQE.AND.(.NOT.YDMODEL%YRML_DYN%YRDYNA%LNHQE_SIHYD)) THEN

  CALL SITNU(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSID_I,ZSIT_O,ZSISP_O,IPROFS)
  CALL SIGAM(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDL_O,ZSITL_I,ZSISPL_I,IPROFS,NFLEVG)
  CALL SIGAM(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDM_O,ZSITM_I,ZSISPM_I,IPROFS,NFLEVG)
!$OMP PARALLEL DO
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZSIDL_O(JROF,JLEV)=ZSIDL_O(JROF,JLEV)+RD*SITR*ZSIPDL_I(JROF,JLEV)
      ZSIDM_O(JROF,JLEV)=ZSIDM_O(JROF,JLEV)+RD*SITR*ZSIPDM_I(JROF,JLEV)
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
  CALL SILKAP(YDMODEL%YRCST,YDGEOMETRY,YDDYN,YDMODEL%YRML_DYN%YRDYNA%LNHQE_C2,KDIM,1,IPROFS,ZSIPD9_I,ZSIVD_O,PMULFAC=ZDIM)

ELSE

  CALL SITNU(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSID_I,ZSIT_O,ZSISP_O,IPROFS)
  CALL SIGAM(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDL_O,ZSITL_I,ZSISPL_I,IPROFS,NFLEVG)
  CALL SIGAM(LVERTFE, YDMODEL%YRCST,YDGEOMETRY,YDDYN,KDIM,1,ZSIDM_O,ZSITM_I,ZSISPM_I,IPROFS,NFLEVG)

ENDIF

!$OMP PARALLEL DO
DO JLEV=1,NFLEVG
  DO JROF=1,KDIM
    ZUSIC(JROF,JLEV)= -ZBDT*ZSIDL_O(JROF,JLEV)*ZDT*0.5_JPRB
    ZVSIC(JROF,JLEV)= -ZBDT*ZSIDM_O(JROF,JLEV)*ZDT*0.5_JPRB
    ZTSIC(JROF,JLEV)= -ZBDT*ZSIT_O(JROF,JLEV)*ZDT*0.5_JPRB
  ENDDO
ENDDO
!$OMP END PARALLEL DO

IF(LNHEE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZPDSIC(JROF,JLEV)= -ZBDT*ZSIPD_O(JROF,JLEV)*ZDT*0.5_JPRB
    ENDDO
  ENDDO
ENDIF
IF(LNHDYN.AND.(.NOT.YDMODEL%YRML_DYN%YRDYNA%LNHQE_SIHYD)) THEN
  DO JLEV=1,NFLEVG
    DO JROF=1,KDIM
      ZVDSIC(JROF,JLEV)= -ZBDT*ZSIVD_O(JROF,JLEV)*ZDT*0.5_JPRB
    ENDDO
  ENDDO
ENDIF

DO JROF=1,KDIM
  ZSPSIC(JROF)= -ZBDT*ZSISP_O(JROF)*ZDT*0.5_JPRB
ENDDO

  ! * Increment PGT3GMV and PGT3GMVS with Z[X]SIC.
   
!$OMP PARALLEL
!$OMP WORKSHARE
PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MU)=PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MU)&
 & +ZUSIC(1:KDIM,1:NFLEVG)
PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MV)=PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MV)&
 & +ZVSIC(1:KDIM,1:NFLEVG)
PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MT)=PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MT)&
 & +ZTSIC(1:KDIM,1:NFLEVG)
!$OMP END WORKSHARE NOWAIT
 
IF (LNHEE) THEN
!$OMP WORKSHARE
  PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MSPD)=PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MSPD)&
   & +ZPDSIC(1:KDIM,1:NFLEVG)
!$OMP END WORKSHARE NOWAIT
ENDIF
IF (LNHDYN.AND.(.NOT.YDMODEL%YRML_DYN%YRDYNA%LNHQE_SIHYD)) THEN
!$OMP WORKSHARE
  PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MSVD)=PGT3GMV(1:KDIM,1:NFLEVG,YRELBC_FIELDS%YYTGMVCPL%MSVD)&
   & +ZVDSIC(1:KDIM,1:NFLEVG)
!$OMP END WORKSHARE NOWAIT
ENDIF
  
!$OMP WORKSHARE
PGT3GMVS(1:KDIM,YRELBC_FIELDS%YYTGMVSCPL%MSP)=PGT3GMVS(1:KDIM,YRELBC_FIELDS%YYTGMVSCPL%MSP)+ZSPSIC(1:KDIM)
!$OMP END WORKSHARE
!$OMP END PARALLEL

!---------------------------------------------------------------------- 

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ESEIMPLS',1,ZHOOK_HANDLE)
END SUBROUTINE ESEIMPLS
