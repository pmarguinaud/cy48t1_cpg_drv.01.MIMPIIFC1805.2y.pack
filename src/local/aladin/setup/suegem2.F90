SUBROUTINE SUEGEM2(YDGEOMETRY,PGM,PGNORX,PGNORY,PGELAM,PGELAT)

!**** *SUEGEM2*  - Initialize geometry parameters in LAM models: part 2

!     Purpose.
!     --------
!        Initialize geometry and biperiodicisation keys (YOMGEM, YOMGC): part 2
!        LAM version of SUGEM2.

!        Initialize geometry and does bi-periodicisation of some geometric quantities.
!        In particuliar, fills YDGEOMETRY%YRGSGEOM and YDGEOMETRY%YRCSGEOM
!        * Parts 1 to 6 do modifications on PGNORX,PGNORY,PGELAM,PGELAT,
!          and do intermediate calculations which will be useful to fill
!          YDGEOMETRY%YRGSGEOM and YDGEOMETRY%YRCSGEOM. Some of these calculations have no
!          equivalent in global models.
!        * Part 7 fills YDGEOMETRY%YRGSGEOM.
!        * Part 8 fills YDGEOMETRY%YRCSGEOM.
!        * Part 9 fills SLHDP.

!**    INTERFACE : CALL SUEGEM2
!      ---------

!      EXTERNALS :
!      ----------
!                  ETIBIHI   - FOR DOUBLY-PERIODICISATION
!                  EREESPE   - FOR GO TO SPECRAL SPACE
!                  ESPEREE   - FOR GO TO REAL SPACE

!      EXPLICIT ARGUMENTS :
!      ------------------
!                  NONE.

!      REFERENCES :
!      ----------

!      AUTHOR :
!      ------
!       V. IVANOVICI         09.07.1992
!       Initial name: SUEBIG

!      MODIFICATION :
!      ------------
!       Modified : 2002-04-11 A. Bogatchev: Remove reading of namelist NAMDYN
!       Modified : 2005-03-01 F. Bouyssel (REFLRHC, TEQH)
!       A. Stanesic   18-Apr-2008 Externalise biperiodicisation
!       K. Yessad (Jul 2011): new structures GSGEOM, CSGEOM + cleanings
!       K. Yessad (Jul 2011): all YDGEOMETRY%YRGSGEOM and YDGEOMETRY%YRCSGEOM attributes now set-up there.
!       K. Yessad (Dec 2011): renaming into SUEGEM2; make it the mirror of SUGEM2.
!       S. Martinez (Mar 2012): introduce SUEBIG modifications from Daan Degrauwe
!                               (Boyd periodization - CY38T1)
!       R. El Khatib 23-Mar 2012 Avoid Null pointer
!       T. Dalkilic  28-Aug-2012 Externalization of Truncation part(SUEMAPF)
!       O. Marsden: June 2015 CY42 YRGMV, YRGFL, YRSURF, YRGMV5, and YRGFL5 are now passed by argument
!       P. Sekula:  17-02-2020  Allocate natively NPROMA-blocked copy of geometry
!      ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RPI      ,ROMEGA   ,RA
USE YOMCT0   , ONLY : LRPLANE  ,NCONF
USE YOMLUN   , ONLY : NULOUT   
USE YOMMP0   , ONLY : LOUTPUT  ,MY_REGION_EW

!      ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT)    :: YDGEOMETRY
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(0:YDGEOMETRY%YRDIM%NDLSUR,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGNORX(0:YDGEOMETRY%YRDIM%NDLSUR,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGNORY(0:YDGEOMETRY%YRDIM%NDLSUR,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGELAM(0:YDGEOMETRY%YRDIM%NDLSUR,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGELAT(0:YDGEOMETRY%YRDIM%NDLSUR,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG) 

!      ------------------------------------------------------------------

REAL(KIND=JPRB), ALLOCATABLE :: ZREV(:) 
REAL(KIND=JPRB), ALLOCATABLE :: ZREVC(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZREV1(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZREVC1(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZREV2(:)
REAL(KIND=JPRB) :: ZSNM(YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB) :: ZGP(YDGEOMETRY%YRGEM%NGPTOT,1,3),ZSPM(1,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB) :: ZR1(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZR2(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZR3(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGEMU (0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGSQM2 (0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGESLO (0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGECLO (0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGOMVRL(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZGOMVRM(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)

REAL(KIND=JPRB) :: ZZRCORI(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRCORIC(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGEMU(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGSQM2(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGELAM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGELAT(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGECLO(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGESLO(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGOMVRL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGOMVRM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDLCL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDMCL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDMCM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGAW(YDGEOMETRY%YRGEM%NGPTOT)
INTEGER(KIND=JPIM) :: IIGPLAT(YDGEOMETRY%YRGEM%NGPTOT)
INTEGER(KIND=JPIM) :: IIUNIQUEGP(YDGEOMETRY%YRGEM%NGPTOT)

REAL(KIND=JPRB) :: ZZRCOLON(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRSILON(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRINDX(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRINDY(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRATATH(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRATATX(YDGEOMETRY%YRGEM%NGPTOT)

INTEGER(KIND=JPIM) :: IENDLAT, IENDLON, IGLG, IROF, ISTLAT, ISTLON, JGL, JLON, ILSUR1
INTEGER(KIND=JPIM) :: JKGLO, IEND, IBL
INTEGER(KIND=JPIM) :: ILONTOT, ILON1, IADD 

REAL(KIND=JPRB) :: ZEPS, ZGMMAX, ZGMMIN, ZGMMOY, ZGMST, ZLAM, ZMAG, ZMAGH,&
 & ZNORM, ZNORMH, ZSINLAT, ZXGEO ,ZZZGAW
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!      ------------------------------------------------------------------

#include "etibihie.h"

#include "abor1.intfb.h"
#include "ereespe.intfb.h"
#include "esperee.intfb.h"
#include "bipincishift.intfb.h"
#include "suemapf.intfb.h"
#include "sugem2_yrgsgeom.intfb.h"

!      ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUEGEM2',0,ZHOOK_HANDLE)

ASSOCIATE( &
 & LBIPINCI=>YDGEOMETRY%YREDIM%LBIPINCI, NBIPINCIX=>YDGEOMETRY%YREDIM%NBIPINCIX, &
 & NBIPINCIY=>YDGEOMETRY%YREDIM%NBIPINCIY, &
 & SLHDP=>YDGEOMETRY%YRGEM%SLHDP, NGPTOT=>YDGEOMETRY%YRGEM%NGPTOT, NLOENG=>YDGEOMETRY%YRGEM%NLOENG, &
 & NGPTOTG=>YDGEOMETRY%YRGEM%NGPTOTG, RSTRET=>YDGEOMETRY%YRGEM%RSTRET, &
 & NDGLG=>YDGEOMETRY%YRDIM%NDGLG, NSMAX=>YDGEOMETRY%YRDIM%NSMAX, NDGENL=>YDGEOMETRY%YRDIM%NDGENL, &
 & NPROMA=>YDGEOMETRY%YRDIM%NPROMA, NGPBLKS=>YDGEOMETRY%YRDIM%NGPBLKS, NDLON=>YDGEOMETRY%YRDIM%NDLON, &
 & NDGSAG=>YDGEOMETRY%YRDIM%NDGSAG, NDGENG=>YDGEOMETRY%YRDIM%NDGENG, NDLSM=>YDGEOMETRY%YRDIM%NDLSM, &
 & NMSMAX=>YDGEOMETRY%YRDIM%NMSMAX, NDGUNL=>YDGEOMETRY%YRDIM%NDGUNL, NDGUNG=>YDGEOMETRY%YRDIM%NDGUNG, &
 & NDGUXG=>YDGEOMETRY%YRDIM%NDGUXG, NDLUNG=>YDGEOMETRY%YRDIM%NDLUNG, NDLUXG=>YDGEOMETRY%YRDIM%NDLUXG, &
 & NSTA=>YDGEOMETRY%YRMP%NSTA, NPTRFLOFF=>YDGEOMETRY%YRMP%NPTRFLOFF, MYLATS=>YDGEOMETRY%YRMP%MYLATS, &
 & NONL=>YDGEOMETRY%YRMP%NONL, &
 & LMAP=>YDGEOMETRY%YREGEO%LMAP, RLAT_ACAD=>YDGEOMETRY%YREGEO%RLAT_ACAD, ELX=>YDGEOMETRY%YREGEO%ELX, &
 & ELY=>YDGEOMETRY%YREGEO%ELY)
!      ------------------------------------------------------------------

!      1.INITIALISATION OF TRUNCATION ORDER FOR MAP FACTOR :
!      ---------------------------------------------------

ZEPS =1.E-10_JPRB

!     ------------------------------------------------------------------

!      2.DOUBLY_PERIODICISATION AND FIT OF GEOGRAPHICAL POSITION :
!      ---------------------------------------------------------

DO JGL=NDGUNG,NDGUXG
  DO JLON=NDLUNG,NDLUXG
    ZR1(JLON,JGL)=COS(PGELAT(JLON,JGL))*COS(PGELAM(JLON,JGL))
    ZR2(JLON,JGL)=COS(PGELAT(JLON,JGL))*SIN(PGELAM(JLON,JGL))
    ZR3(JLON,JGL)=SIN(PGELAT(JLON,JGL))
  ENDDO
ENDDO

IF (LBIPINCI) THEN
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,-NBIPINCIX,-NBIPINCIY,ZR1)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,-NBIPINCIX,-NBIPINCIY,ZR2)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,-NBIPINCIX,-NBIPINCIY,ZR3)
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,0,NDLSM,&
   & ZR1,.TRUE.,.TRUE.,0)
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,0,NDLSM,&
   & ZR2,.TRUE.,.TRUE.,0)
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,0,NDLSM,&
   & ZR3,.TRUE.,.TRUE.,0)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,NBIPINCIX,NBIPINCIY,ZR1)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,NBIPINCIX,NBIPINCIY,ZR2)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,NBIPINCIX,NBIPINCIY,ZR3)
ELSE
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,0,NDLSM,ZR1,.TRUE.,.TRUE.,0)  
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,0,NDLSM,ZR2,.TRUE.,.TRUE.,0)  
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,0,NDLSM,ZR3,.TRUE.,.TRUE.,0)  
ENDIF

DO JGL=NDGUNG,NDGUXG
  DO JLON=NDLUXG+1,NDLON
    ZMAGH= ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZMAG = ZMAGH+ZR3(JLON,JGL)*ZR3(JLON,JGL)
    ZNORMH = SQRT(MAX(1.E-12_JPRB,ZMAGH))
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGELAT(JLON,JGL)=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR3(JLON,JGL)/ZNORM)))
    ZLAM=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORMH)))
    IF(ZR1(JLON,JGL)/ZNORMH >= 0) THEN
      PGELAM(JLON,JGL)=ZLAM
    ELSE
      PGELAM(JLON,JGL)=RPI - ZLAM
    ENDIF
  ENDDO
ENDDO

DO JGL=NDGUXG+1,NDGLG
  DO JLON=1,NDLON
    ZMAGH= ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZMAG = ZMAGH+ZR3(JLON,JGL)*ZR3(JLON,JGL)
    ZNORMH = SQRT(MAX(1.E-12_JPRB,ZMAGH))
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGELAT(JLON,JGL)=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR3(JLON,JGL)/ZNORM)))
    ZLAM=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORMH)))
    IF(ZR1(JLON,JGL)/ZNORMH >= 0) THEN
      PGELAM(JLON,JGL)=ZLAM
    ELSE
      PGELAM(JLON,JGL)=RPI - ZLAM
    ENDIF
  ENDDO
ENDDO

IF ((NDLUNG-1) >= 1) CALL ABOR1('SUEGEM2 2a: NOT YET IMPLEMENTED')

DO JGL=NDGUNG,NDGUXG
  DO JLON=1,NDLUNG-1
    ZMAGH= ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZMAG = ZMAGH+ZR3(JLON,JGL)*ZR3(JLON,JGL)
    ZNORMH = SQRT(MAX(1.E-12_JPRB,ZMAGH))
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGELAT(JLON,JGL)=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR3(JLON,JGL)/ZNORM)))
    ZLAM=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORMH)))
    IF(ZR1(JLON,JGL)/ZNORMH >= 0) THEN
      PGELAM(JLON,JGL)=ZLAM
    ELSE
      PGELAM(JLON,JGL)=RPI - ZLAM
    ENDIF
  ENDDO
ENDDO

IF ((NDGUNL-1) >= 1) CALL ABOR1('SUEGEM2 2b: NOT IMPLEMENTED - ERROR ')

DO JGL=1,NDGUNG-1
  DO JLON=1    ,NDLON
    IROF=IROF+1
    ZMAGH= ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZMAG = ZMAGH+ZR3(JLON,JGL)*ZR3(JLON,JGL)
    ZNORMH = SQRT(MAX(1.E-12_JPRB,ZMAGH))
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGELAT(JLON,JGL)=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR3(JLON,JGL)/ZNORM)))
    ZLAM=ASIN(MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORMH)))
    IF(ZR1(JLON,JGL)/ZNORMH >= 0) THEN
      PGELAM(JLON,JGL)=ZLAM
    ELSE
      PGELAM(JLON,JGL)=RPI - ZLAM
    ENDIF
  ENDDO
ENDDO

DO JGL=NDGSAG,NDGENG
  DO JLON=1,NDLON
    ZSINLAT=SIN(PGELAT(JLON,JGL))
    ZGEMU (JLON,JGL)=ZSINLAT
    ZGSQM2(JLON,JGL)=SQRT(MAX(0.0_JPRB,1.0_JPRB-ZSINLAT*ZSINLAT))
    ZGESLO(JLON,JGL)=SIN(PGELAM(JLON,JGL))
    ZGECLO(JLON,JGL)=COS(PGELAM(JLON,JGL))
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!      3.COMPUTATION AND FIT OF CORIOLIS PARAMETER :
!      ---------------------------------------------

ALLOCATE(ZREV(NGPTOTG))
ALLOCATE(ZREVC(NGPTOTG))
ALLOCATE(ZREV1(NGPTOT))
ALLOCATE(ZREVC1(NGPTOT))
ALLOCATE(ZREV2(NGPTOT))
IROF=0
ILSUR1=0
ZREV(:)=0.0_JPRB
ZREVC(:)=0.0_JPRB
DO JGL=NDGSAG,NDGENG
  DO JLON=1,NDLON
    IROF=IROF+1
    ZREV(IROF)=2.0_JPRB*ROMEGA*ZGEMU(JLON,JGL)
    ZREVC(IROF)=2.0_JPRB*ROMEGA*ZGSQM2(JLON,JGL)
  ENDDO
  IROF=IROF+ILSUR1
ENDDO
! copy of DM-global stuff to DM-local by PLM & GR
ISTLAT=1
IENDLAT=NDGENL

IROF=1
DO JGL=ISTLAT,IENDLAT
  ILON1=0
  ILONTOT=NDLON
  IGLG=MYLATS(JGL)
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    IADD=(IGLG-1)*ILONTOT+JLON+ILON1
    ZREV1(IROF)=ZREV(IADD)
    ZREVC1(IROF)=ZREVC(IADD)
    IROF=IROF+1
  ENDDO
ENDDO
IF (NCONF /= 923) THEN
  CALL EREESPE(YDGEOMETRY,1,1,ZSNM,ZREV1)
  CALL ESPEREE(YDGEOMETRY,1,1,ZSNM,ZREV1)
  CALL EREESPE(YDGEOMETRY,1,1,ZSNM,ZREVC1)
  CALL ESPEREE(YDGEOMETRY,1,1,ZSNM,ZREVC1)
ENDIF

!     ------------------------------------------------------------------

!      4.DOUBLY-PERIODICISATION AND FIT OF MAP FACTOR :
!      ----------------------------------------------

ZREV(:)=0.0_JPRB
IROF=0
ILSUR1=0
DO JGL=NDGUNG,NDGUXG
  DO JLON=NDLUNG,NDLUXG
    IROF=IROF+1+(NDLUNG-1)
    ZREV(IROF)=PGM(JLON,JGL)
  ENDDO
  IROF=IROF+ILSUR1+(NDLON-NDLUXG)
ENDDO
IF (LBIPINCI) THEN
  CALL BIPINCISHIFT(1,NDLON,1,NDGLG,-NBIPINCIX,-NBIPINCIY,ZREV)
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,1,NDLON,&
   & ZREV,.TRUE.,.TRUE.,0)
  CALL BIPINCISHIFT(1,NDLON,1,NDGLG,NBIPINCIX,NBIPINCIY,ZREV)
ELSE
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,1,NDLON,ZREV,.TRUE.,.TRUE.,0)
ENDIF
! copy of DM-global stuff to DM-local by PLM & GR
ISTLAT=1
IENDLAT=NDGENL

IROF=1
DO JGL=ISTLAT,IENDLAT
  ILON1=0
  ILONTOT=NDLON
  IGLG=MYLATS(JGL)
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    IADD=(IGLG-1)*ILONTOT+JLON+ILON1
    ZREV2(IROF)=ZREV(IADD)
    IROF=IROF+1
  ENDDO
ENDDO

!*        4.1 Truncation
!         ---------------

IF (NCONF /= 923) THEN
  CALL SUEMAPF(ZREV2)
ENDIF

!*     4.2 Find maximum, minimum and mean value: setting RSTRET
!      --------------------------------------------------------

ZGMMAX=-99999999.
ZGMMIN=999999999.
ZGMMOY=0.0_JPRB
ZGMST = 0.0_JPRB
DO JLON=1,NGPTOTG
  ZGMMOY=ZGMMOY+ZREV(JLON)*ZREV(JLON)
  ZGMST =ZGMST+ZREV(JLON)
  ! care is taken that the tail of ZREV array is zero if LMESSP (G.R.)!!
  IF (ZREV(JLON) < ZGMMIN.AND.ZREV(JLON) > ZEPS) ZGMMIN=ZREV(JLON)
  IF (ZREV(JLON) > ZGMMAX) ZGMMAX=ZREV(JLON)
ENDDO
ZGMMOY = ZGMMOY/ZGMST
RSTRET = ZGMMAX

!*    print values
IF (LOUTPUT) THEN
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' GM MAX = ',ZGMMAX 
  WRITE (NULOUT,*) ' GM MIN = ',ZGMMIN 
  WRITE (NULOUT,*) ' GM MOY = ',ZGMMOY
  WRITE (NULOUT,*) ' SUM GM = ',ZGMST
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' FINAL RSTRET = ',RSTRET
  WRITE (NULOUT,*) ' '
ENDIF

!     ------------------------------------------------------------------

!      5.SET THE GEOGRAPHICAL NORTH UNIT VECTOR :
!      ------------------------------------------

DO JGL=NDGUNG,NDGUXG
  DO JLON=NDLUNG,NDLUXG
    ZR1(JLON,JGL)=PGNORX(JLON,JGL)
    ZR2(JLON,JGL)=PGNORY(JLON,JGL)
  ENDDO
ENDDO

IF (LBIPINCI) THEN
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,-NBIPINCIX,-NBIPINCIY,ZR1)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,-NBIPINCIX,-NBIPINCIY,ZR2)
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,0,NDLSM,&
   & ZR1,.TRUE.,.TRUE.,0)  
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG-2*NBIPINCIX,NDGUXG-2*NBIPINCIY,0,NDLSM,&
   & ZR2,.TRUE.,.TRUE.,0)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,NBIPINCIX,NBIPINCIY,ZR1)
  CALL BIPINCISHIFT(0,NDLSM,NDLON,NDGLG,NBIPINCIX,NBIPINCIY,ZR2)
ELSE
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,0,NDLSM,&
   & ZR1,.TRUE.,.TRUE.,0)  
  CALL ETIBIHIE(NDLON,NDGLG,1,NDLUXG,NDGUXG,0,NDLSM,&
   & ZR2,.TRUE.,.TRUE.,0)  
ENDIF

DO JGL=NDGUNG,NDGUXG
  DO JLON=NDLUXG+1,NDLON
    ZMAG = ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGNORX(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR1(JLON,JGL)/ZNORM))
    PGNORY(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORM))
  ENDDO
ENDDO

DO JGL=NDGUXG+1, NDGLG
  DO JLON=1    ,NDLON
    ZMAG = ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGNORX(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR1(JLON,JGL)/ZNORM))
    PGNORY(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORM))
  ENDDO
ENDDO

DO JGL=NDGUNG,NDGUXG
  DO JLON=1,NDLUNG-1
    ZMAG = ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGNORX(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR1(JLON,JGL)/ZNORM))
    PGNORY(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORM))
  ENDDO
ENDDO

DO JGL=1,NDGUNG-1
  DO JLON=1    ,NDLON
    ZMAG = ZR1(JLON,JGL)*ZR1(JLON,JGL)+ZR2(JLON,JGL)*ZR2(JLON,JGL)
    ZNORM  = SQRT(MAX(1.E-12_JPRB,ZMAG ))
    PGNORX(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR1(JLON,JGL)/ZNORM))
    PGNORY(JLON,JGL)=MIN(1.0_JPRB,MAX(-1.0_JPRB,ZR2(JLON,JGL)/ZNORM))
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!      6.SET THE VALUES FOR GOMVRL AND GOMVRM :
!      --------------------------------------

DO JGL=NDGSAG,NDGENG
  DO JLON=1    ,NDLON
    ZXGEO = 2.0_JPRB * ROMEGA * RA * ZGSQM2(JLON,JGL)
    ZGOMVRL(JLON,JGL) =   ZXGEO * PGNORY(JLON,JGL)
    ZGOMVRM(JLON,JGL) = - ZXGEO * PGNORX(JLON,JGL)
  ENDDO
ENDDO

!     ------------------------------------------------------------------
    
!*       7.    Initialize YDGEOMETRY%YRGSGEOM:
!              --------------------
  
! ky: the .NOT.LELAM counterpart of these calculations is in part 2 of SUGEM2.

ISTLAT=1
IENDLAT=NDGENL

IROF=1
DO JGL=ISTLAT,IENDLAT
  IGLG=MYLATS(JGL)
  ZZZGAW=1.0_JPRB/(REAL(NDGLG,JPRB)*REAL(NLOENG(IGLG),JPRB))
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    ZZRCORI(IROF)=ZREV1(IROF)
    ZZRCORIC(IROF)=ZREVC1(IROF)
    ZZGEMU(IROF)=ZGEMU(JLON,IGLG)
    ZZGSQM2(IROF)=ZGSQM2(JLON,IGLG)
    ZZGELAM(IROF)=PGELAM(JLON,IGLG)
    ZZGELAT(IROF)=PGELAT(JLON,IGLG)
    ZZGECLO(IROF)=ZGECLO(JLON,IGLG)
    ZZGESLO(IROF)=ZGESLO(JLON,IGLG)
    ZZGM(IROF)=ZREV2(IROF)
    ZZGOMVRL(IROF)=ZGOMVRL(JLON,IGLG)
    ZZGOMVRM(IROF)=ZGOMVRM(JLON,IGLG)
    ZZGNORDL(IROF)=PGNORX(JLON,IGLG)
    ZZGNORDM(IROF)=PGNORY(JLON,IGLG)
    IF (LRPLANE) THEN
      ! GNORDLCL,GNORDMCL,GNORDMCM not used, filled with zeros
      ZZGNORDLCL(IROF)=0.0_JPRB
      ZZGNORDMCL(IROF)=0.0_JPRB
      ZZGNORDMCM(IROF)=0.0_JPRB
    ELSE
      ! GNORDLCL,GNORDMCL,GNORDMCM used, provisionally filled with zeros
      ! but the right formula remains to be coded.
      ZZGNORDLCL(IROF)=0.0_JPRB
      ZZGNORDMCL(IROF)=0.0_JPRB
      ZZGNORDMCM(IROF)=0.0_JPRB
    ENDIF
    ZZGAW(IROF)=ZZZGAW
    IIGPLAT(IROF)=IGLG
    IIUNIQUEGP(IROF)=NDLON*(IGLG-1)+JLON
    IROF=IROF+1
  ENDDO
ENDDO

! Special case where a latitude is specified for radiation
! but the Coriolis Parameter is set to 0 in the dynamics
IF ( (NINT(RLAT_ACAD) /= -999999) ) THEN
  ZZRCORI(:)=0.0_JPRB
  ZZRCORIC(:)=0.0_JPRB
ENDIF
! Print min/max value of Coriolis parameter
IF ( LOUTPUT ) THEN
  WRITE(NULOUT,*) 'MINVAL(RCORI)=',MINVAL(ZZRCORI)
  WRITE(NULOUT,*) 'MAXVAL(RCORI)=',MAXVAL(ZZRCORI)
ENDIF

CALL SUGEM2_YRGSGEOM (YDGEOMETRY, ZZRCORI, ZZRCORIC, ZZGEMU, ZZGSQM2, ZZGELAM, &
& ZZGELAT, ZZGECLO, ZZGESLO, ZZGM, ZZGOMVRL, ZZGOMVRM, ZZGNORDL, ZZGNORDM, ZZGNORDLCL, &
& ZZGNORDMCL, ZZGNORDMCM, ZZGAW, IIGPLAT, IIUNIQUEGP)

!     ------------------------------------------------------------------

!*       8.    Initialize YDGEOMETRY%YRCSGEOM:
!              --------------------

! ky: the .NOT.LELAM counterpart of these calculations is in part 3 of SUGEM2.
!     LAM calculation of RATATH and RATATX needs YDGEOMETRY%YRGSGEOM_NB%GM.
!     LAM calculation of RCOLON and RSILON needs PGELAM.
!     RINDX and RINDY are required only if LRPLANE=T. but they will be computed
!     anytime.

ISTLAT=1
IENDLAT=NDGENL

IROF=1
DO JGL=ISTLAT,IENDLAT
  IGLG=MYLATS(JGL)
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    ZZRCOLON(IROF)=COS(PGELAM(JLON,IGLG))
    ZZRSILON(IROF)=SIN(PGELAM(JLON,IGLG))
    IROF=IROF+1
  ENDDO
ENDDO

IROF=1
DO JGL =ISTLAT,IENDLAT
  IGLG=MYLATS(JGL)
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    ZZRINDX(IROF)=REAL(JLON,JPRB)
    ZZRINDY(IROF)=REAL(IGLG,JPRB)
    IROF=IROF+1
  ENDDO
ENDDO

IF (NCONF /= 923 .AND. LMAP) THEN
 ! Compute spectral map factor
  ZGP(1:NGPTOT,1,1)=YDGEOMETRY%YRGSGEOM_NB%GM(1:NGPTOT)
  ! Compute derivatives of map factor
  CALL EREESPE(YDGEOMETRY,1,1,ZSPM,ZGP(1,1,1))
  CALL ESPEREE(YDGEOMETRY,1,1,ZSPM,ZGP(1,1,1),PREELL=ZGP(1,1,2),PREELM=ZGP(1,1,3))
  ZZRATATH(1:NGPTOT)= 2.0_JPRB*ZGP(1:NGPTOT,1,2)
  ZZRATATX(1:NGPTOT)=-2.0_JPRB*ZGP(1:NGPTOT,1,3)
ELSE
  ZZRATATH(1:NGPTOT)=0.0_JPRB
  ZZRATATX(1:NGPTOT)=0.0_JPRB
ENDIF


! Allocate YDGEOMETRY%YRCSGEOM_NB:
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RCOLON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RSILON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDX(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDY(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATH(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATX(NGPTOT))

! Allocate natively NPROMA-blocked copy of geometry

ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RCOLON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RSILON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDX(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDY(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATH(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATX(NPROMA,NGPBLKS))

! Allocate YDGEOMETRY%YRCSGEOM:
ALLOCATE(YDGEOMETRY%YRCSGEOM(NGPBLKS))

! Copy items to non-blocked form of YDGEOMETRY%YRCSGEOM
YDGEOMETRY%YRCSGEOM_NB%RCOLON(:)=ZZRCOLON(:)
YDGEOMETRY%YRCSGEOM_NB%RSILON(:)=ZZRSILON(:)
YDGEOMETRY%YRCSGEOM_NB%RINDX(:)=ZZRINDX(:)
YDGEOMETRY%YRCSGEOM_NB%RINDY(:)=ZZRINDY(:)
YDGEOMETRY%YRCSGEOM_NB%RATATH(:)=ZZRATATH(:)
YDGEOMETRY%YRCSGEOM_NB%RATATX(:)=ZZRATATX(:)
  
! Point to items in YDGEOMETRY%YRCSGEOM   
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM(IBL)%RCOLON=>YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RSILON=>YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDX =>YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDY =>YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATH=>YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATX=>YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

! Copy blocks into native NPROMA-allocated arrays
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM_B%RCOLON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RSILON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDX (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDY (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATH(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATX(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

!     ------------------------------------------------------------------

!*      9.   Resolution correction for SLHD
!            ------------------------------

! ky: the .NOT.LELAM counterpart of these calculations is in part 4 of SUGEM2.

IF ( (NMSMAX == 0) .AND. (NSMAX > 0) ) THEN
  SLHDP=ELY/REAL(NSMAX)
ELSEIF ( (NMSMAX > 0) .AND. (NSMAX == 0) ) THEN
  SLHDP=ELX/REAL(NMSMAX)
ELSEIF ( (NMSMAX > 0) .AND. (NSMAX > 0) ) THEN
  SLHDP=SQRT( ELX*ELX/(REAL(NMSMAX)*REAL(NMSMAX)) +&
   & ELY*ELY/(REAL(NSMAX)*REAL(NSMAX)) )
ELSE
  ! 1D column; SLHDP not used in this case.
  SLHDP=1.0_JPRB
ENDIF

!     ------------------------------------------------------------------

! *     10. Deallocations

IF (ALLOCATED(ZREV)) DEALLOCATE(ZREV)
IF (ALLOCATED(ZREVC)) DEALLOCATE(ZREVC)
IF (ALLOCATED(ZREV1)) DEALLOCATE(ZREV1)
IF (ALLOCATED(ZREVC1)) DEALLOCATE(ZREVC1)
IF (ALLOCATED(ZREV2)) DEALLOCATE(ZREV2)

!      ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUEGEM2',1,ZHOOK_HANDLE)
END SUBROUTINE SUEGEM2
