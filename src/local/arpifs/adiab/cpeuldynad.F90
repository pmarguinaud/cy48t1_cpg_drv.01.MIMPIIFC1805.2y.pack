SUBROUTINE CPEULDYNAD(YDGEOMETRY,YDGMV,YDGMV5,YGFL,YDML_DYN,YDPHY,KST,KPROF,PBETADT,PDT,&
 & KIBL,PGFL,PKENE,PCTY0,&
 & PRES0,PRDELP,PATND,&
 & PGFLT1,PGMVT1,PGMVT1S,PGMV,PGMVS,&
 & PB2,&
 & PGMV5,PGFL5,PCTY5,PRES5,PRDELP5)

!**** *CPEULDYNAD* - adjoint dynamic grid point calculations for Eulerian model.

!     Purpose.
!     --------
!     Adjoint Dynamic computations in grid-point space for Eulerian model.

!     The following options present in the direct code are not yet coded in AD:
!     - LRUBC=T and NDPSFI=1.
!     - LNHDYN=T.
!     - LPC_FULL=T.
!     - LVERTFE=T.

!**   Interface.
!     ----------
!        *CALL* *CPEULDYNAD(...)

!        Explicit arguments :
!        --------------------

!        INPUT:
!          KST     - first element of work.
!          KPROF   - depth of work.
!          PBETADT - BETADT or 0 according to configuration.
!          PDT     - SL2TL or first timestep: timestep; otherwise 2*timestep.
!          KIBL    - index into YRCSGEOM/YRGSGEOM isntances in YDGEOMETRY

!        INPUT/OUTPUT OTHER THAN "...5" VARIABLES (corresponding to
!        INPUT variables in the TL code) + INPUT geometrical variables:
!          PGFL    - GFL variables at t and t-dt.
!          PKENE   - kinetic energy.
!          PCTY0   - contains vertical velocities, vertical integral of divergence at t.
!          PRES0   - pressure on an interlayer at t.
!          PRDELP  - 1/(pressure depth of layers) at t.
!          PATND   - adiabatic Lagrangian tendencies.

!        INPUT or INOUT (corresponding to INOUT variables in the TL code):
!          PGFLT1  - t+dt term for the GFL variables equations.
!          PGMVT1  - GMV variables at t+dt.
!          PGMVT1S - GMVS variables at t+dt.
!          PGMV    - GMV variables at t and t-dt.
!          PGMVS   - GMVS variables at t and t-dt.

!        INPUT/OUTPUT (corresponding to OUTPUT variables in the TL code):
!          PB2     - "SLBUF2" buffer.

!        INPUT "...5" VARIABLES (trajectory):
!          PGMV5   - GMV variables.
!          PGFL5   - GFL variables.
!          PCTY5   - contains vertical velocities, vertical integral of divergence.
!          PRES5   - pressure on an interlayer (trajectory).
!          PRDELP5 - 1/(pressure depth of layers) (trajectory).

!        Implicit arguments : none.
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF*
!      Original : 90-03-22 (initial name = CPDYNAD)

!     Modifications.
!     --------------
!   M.Hamrud      01-Oct-2003 CY28 Cleaning
!   -- name = CPEULDYNAD --
!   K.Yessad     : 20-03-2006: update according to the direct code
!     routine CPEULDYN and rename it into CPEULDYNAD.
!   M.Jidane 09-04-2006 : Compute R with q variables under key
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad (Nov 2009): cleanings, DT/Dt now pre-computed in CPG_GP_AD.
!   K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!   K. Yessad (Nov 2011): various contributions.
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   K. Yessad (July 2014): Move some variables.
!     ------------------------------------------------------------------

USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCT0             , ONLY : LSPRT, LNHDYN
USE YOMDYNA            , ONLY : YRDYNA
USE YOMCVER            , ONLY : LVERTFE
USE YOMCST             , ONLY : RD, RV, RETV
USE YOMPHY             , ONLY : TPHY
USE YOM_YGFL           , ONLY : TYPE_GFLD
USE INTDYN_MOD         , ONLY : YYTTND, YYTCTY5, YYTCTY0

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV5
TYPE(MODEL_DYNAMICS_TYPE),INTENT(IN):: YDML_DYN
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBETADT
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKENE(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRES0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRDELP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%YT1%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMV5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV5%YT5%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM5) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCTY5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY5%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRES5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZR0   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR5   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSID  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIDL (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIDM (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSISP (YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSISPL(YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSISPM(YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSIT  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITL (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITM (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT0L (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT0M (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT5L (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT5M (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZFM, ZED, ZED5

INTEGER(KIND=JPIM) :: IPROFS, JLEV, JROF,JGFL
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "sigamad.intfb.h"
#include "sitnuad.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPEULDYNAD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), YDDYN=>YDML_DYN%YRDYN, &
 & YDPTRSLB2=>YDML_DYN%YRPTRSLB2,YDEDYN=>YDML_DYN%YREDYN)

ASSOCIATE(NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NDIM5=>YGFL%NDIM5, &
 & NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, YQ=>YGFL%YQ, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & LIMPF=>YDDYN%LIMPF, LSIDG=>YDDYN%LSIDG, &
 & LESIDG=>YDEDYN%LESIDG, &
 & RSTRET=>YDGEM%RSTRET, &
 & NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, YT0=>YDGMV%YT0, &
 & YT1=>YDGMV%YT1, YT9=>YDGMV%YT9, &
 & YT5=>YDGMV5%YT5, NDPSFI=>YDPHY%NDPSFI, &
 & MSLB2SPSI=>YDPTRSLB2%MSLB2SPSI, MSLB2TSI=>YDPTRSLB2%MSLB2TSI, &
 & MSLB2USI=>YDPTRSLB2%MSLB2USI, MSLB2VSI=>YDPTRSLB2%MSLB2VSI, &
 & NFLDSLB2=>YDPTRSLB2%NFLDSLB2)
!     ------------------------------------------------------------------

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (NDPSFI == 1) THEN
  CALL ABOR1(' CPEULDYNAD: NDPSFI=1 not yet coded in EUL AD model')
ENDIF
IF (YRDYNA%LRUBC) THEN
  CALL ABOR1(' CPEULDYNAD: LRUBC=T not yet coded in EUL AD model')
ENDIF
IF (LNHDYN) THEN
  CALL ABOR1(' CPEULDYNAD: LNHDYN=T not yet coded in EUL AD model')
ENDIF
IF (YRDYNA%LPC_FULL) THEN
  CALL ABOR1(' CPEULDYNAD: LPC_FULL=T not yet coded in EUL AD model')
ENDIF
IF (LVERTFE) THEN
  CALL ABOR1(' CPEULDYNAD: LVERTFE=T not yet coded in EUL AD model')
ENDIF

!     ------------------------------------------------------------------

!*       1.    INTERMEDIATE QUANTITIES FOR EQUATIONS RHS
!              -----------------------------------------

! ky: not fully consistent with the direct code but consistent with the
!     cy33 version of cpeuldyntl.F90 + cpeuldynad.F90.
ZFM = 0.5_JPRB

! dimension for calls to SI routines
IPROFS=KPROF-KST+1

!     ------------------------------------------------------------------

!*       7+8.  Non linear tendency at time "t" and temporal advance.
!              -----------------------------------------------------

! * Continuity equation:
PCTY0(KST:KPROF,NFLEVG,YYTCTY0%M_PSDVBC)=PCTY0(KST:KPROF,NFLEVG,YYTCTY0%M_PSDVBC)&
 & -PDT/PRES5(KST:KPROF,NFLEVG)*PGMVT1S(KST:KPROF,YT1%MSP)  
PRES0(KST:KPROF,NFLEVG)=PRES0(KST:KPROF,NFLEVG)&
 & +PDT*PCTY5(KST:KPROF,NFLEVG,YYTCTY5%M_PSDVBC)&
 & /(PRES5(KST:KPROF,NFLEVG)*PRES5(KST:KPROF,NFLEVG))*PGMVT1S(KST:KPROF,YT1%MSP)  

! * Temperature equation:
DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PATND(JROF,JLEV,YYTTND%M_TNDT)=PATND(JROF,JLEV,YYTTND%M_TNDT)+PDT*PGMVT1(JROF,JLEV,YT1%MT)
  ENDDO
ENDDO

! * Momentum equation:
DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PATND(JROF,JLEV,YYTTND%M_TNDU)=PATND(JROF,JLEV,YYTTND%M_TNDU)+PDT*PGMVT1(JROF,JLEV,YT1%MU)
    PATND(JROF,JLEV,YYTTND%M_TNDV)=PATND(JROF,JLEV,YYTTND%M_TNDV)+PDT*PGMVT1(JROF,JLEV,YT1%MV)
    PKENE(JROF,JLEV)=PKENE(JROF,JLEV)&
     & -PDT*YDCSGEOM%RATATH(JROF)*PGMVT1(JROF,JLEV,YT1%MV)&
     & +PDT*YDCSGEOM%RATATX(JROF)*PGMVT1(JROF,JLEV,YT1%MU)
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!*       6.    SEMI-IMPLICIT FOR PC SCHEME ONLY.
!              ---------------------------------

!nyc IF (LPC_FULL) THEN
!nyc   Not yet coded!
!nyc ENDIF

!     ------------------------------------------------------------------

!*       5.    SEMI-IMPLICIT CORRECTION.
!              -------------------------

!        5.0     Set to zero

DO JLEV=1,NFLEVG
  DO JROF=1,KPROF
    ZR0  (JROF,JLEV)=0.0_JPRB
    ZSID (JROF,JLEV)=0.0_JPRB
    ZSITL(JROF,JLEV)=0.0_JPRB
    ZSITM(JROF,JLEV)=0.0_JPRB
    ZTT0L(JROF,JLEV)=0.0_JPRB
    ZTT0M(JROF,JLEV)=0.0_JPRB
  ENDDO
ENDDO

DO JROF=1,KPROF
  ZSISPL(JROF)=0.0_JPRB
  ZSISPM(JROF)=0.0_JPRB
ENDDO

!        5.0b    Trajectory code to compute ZR5, ZTT5L, ZTT5M.
!                (matching with code computed in part 3.0 of CPEULDYNTL). 
!                k.y.: has to be put later in a new routine CPEULDYN5
!                      called at the beginning of CPEULDYNTL and CPEULDYNAD.

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZR5(JROF,JLEV)=RD+(RV-RD)*PGFL5(JROF,JLEV,YQ%MP5)
    ENDDO
    IF (YQ%LCDERS) THEN
      DO JROF=KST,KPROF
        ZTT5L(JROF,JLEV)=RD*&
         & (PGMV5(JROF,JLEV,YT5%MTL)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
         & *PGFL5(JROF,JLEV,YQ%MP5L))/ZR5(JROF,JLEV)  
        ZTT5M(JROF,JLEV)=RD*&
         & (PGMV5(JROF,JLEV,YT5%MTM)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
         & *PGFL5(JROF,JLEV,YQ%MP5M))/ZR5(JROF,JLEV)  
      ENDDO
    ELSE
      DO JROF=KST,KPROF
        ZTT5L(JROF,JLEV)=RD*PGMV5(JROF,JLEV,YT5%MTL)/ZR5(JROF,JLEV)
        ZTT5M(JROF,JLEV)=RD*PGMV5(JROF,JLEV,YT5%MTM)/ZR5(JROF,JLEV)
      ENDDO
    ENDIF
  ENDDO
ELSE
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZTT5L(JROF,JLEV)=PGMV5(JROF,JLEV,YT5%MTL)
      ZTT5M(JROF,JLEV)=PGMV5(JROF,JLEV,YT5%MTM)
    ENDDO
  ENDDO
ENDIF

!        5.3     Fill P[X]SIC arrays

DO JROF=KST,KPROF
  ZSISP (JROF)=0.0_JPRB
  ZSISP(JROF)=ZSISP(JROF)-PBETADT*PDT*ZFM*PB2(JROF,MSLB2SPSI)
  PB2(JROF,MSLB2SPSI)=0.0_JPRB
ENDDO

DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    ZSIT (JROF,JLEV)=0.0_JPRB
    ZSIT (JROF,JLEV)= ZSIT (JROF,JLEV)-PBETADT*PDT*ZFM*PB2(JROF,MSLB2TSI-1+JLEV)
    PB2(JROF,MSLB2TSI-1+JLEV)= 0.0_JPRB
    ZSIDM(JROF,JLEV)=0.0_JPRB
    ZSIDM(JROF,JLEV)= ZSIDM(JROF,JLEV)-PBETADT*PDT*ZFM*PB2(JROF,MSLB2VSI-1+JLEV)
    PB2(JROF,MSLB2VSI-1+JLEV)= 0.0_JPRB
    ZSIDL(JROF,JLEV)=0.0_JPRB
    ZSIDL(JROF,JLEV)= ZSIDL(JROF,JLEV)-PBETADT*PDT*ZFM*PB2(JROF,MSLB2USI-1+JLEV)
    PB2(JROF,MSLB2USI-1+JLEV)= 0.0_JPRB
  ENDDO
ENDDO

!        5.2     Do SI calculations
!                This should match what is in LASSIEAD/LANHSIAD

! * For "spectral RT" option, adjust semi-implicit term in
!   T-equation to compensate for later multiplication by R/Rd

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZSIT(JROF,JLEV)=RD*ZSIT(JROF,JLEV)/ZR5(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

IF (LIMPF) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
       & -2.0_JPRB*YDGSGEOM%RCORI(JROF)*ZSIDM(JROF,JLEV)
      PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
       & +2.0_JPRB*YDGSGEOM%RCORI(JROF)*ZSIDL(JROF,JLEV)
      PGMV(JROF,JLEV,YT9%MU)=PGMV(JROF,JLEV,YT9%MU)&
       & +YDGSGEOM%RCORI(JROF)*ZSIDM(JROF,JLEV)
      PGMV(JROF,JLEV,YT9%MV)=PGMV(JROF,JLEV,YT9%MV)&
       & -YDGSGEOM%RCORI(JROF)*ZSIDL(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

CALL SIGAMAD(YDGEOMETRY,YDDYN,NPROMA,1,ZSIDM(KST,1),ZSITM(KST,1),ZSISPM(KST),IPROFS,NFLEVG)
CALL SIGAMAD(YDGEOMETRY,YDDYN,NPROMA,1,ZSIDL(KST,1),ZSITL(KST,1),ZSISPL(KST),IPROFS,NFLEVG)
CALL SITNUAD(YDGEOMETRY,YDDYN,NPROMA,1,ZSID(KST,1),ZSIT(KST,1),ZSISP(KST),IPROFS)

!        5.1     Prepare input arrays for SI calculations

! * GMVS variables:
DO JROF=KST,KPROF
  PGMVS(JROF,YT9%MSPM)=PGMVS(JROF,YT9%MSPM)+ZSISPM(JROF)
  PGMVS(JROF,YT0%MSPM)=PGMVS(JROF,YT0%MSPM)-2.0_JPRB*ZSISPM(JROF)
  ZSISPM(JROF)=0.0_JPRB
  PGMVS(JROF,YT9%MSPL)=PGMVS(JROF,YT9%MSPL)+ZSISPL(JROF)
  PGMVS(JROF,YT0%MSPL)=PGMVS(JROF,YT0%MSPL)-2.0_JPRB*ZSISPL(JROF)
  ZSISPL(JROF)=0.0_JPRB
ENDDO

! * GMV variables:

DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PGMV(JROF,JLEV,YT9%MTM)=PGMV(JROF,JLEV,YT9%MTM)+ZSITM(JROF,JLEV)
    PGMV(JROF,JLEV,YT0%MTM)=PGMV(JROF,JLEV,YT0%MTM)-2.0_JPRB*ZSITM(JROF,JLEV)
    PGMV(JROF,JLEV,YT9%MTL)=PGMV(JROF,JLEV,YT9%MTL)+ZSITL(JROF,JLEV)
    PGMV(JROF,JLEV,YT0%MTL)=PGMV(JROF,JLEV,YT0%MTL)-2.0_JPRB*ZSITL(JROF,JLEV)
  ENDDO
  IF (LSIDG) THEN
    DO JROF=KST,KPROF
      PGMV(JROF,JLEV,YT9%MDIV)=PGMV(JROF,JLEV,YT9%MDIV)+ZSID(JROF,JLEV)
      PGMV(JROF,JLEV,YT0%MDIV)=PGMV(JROF,JLEV,YT0%MDIV)-2.0_JPRB*ZSID(JROF,JLEV)
    ENDDO
  ELSEIF (LESIDG) THEN
    DO JROF=KST,KPROF
      PGMV(JROF,JLEV,YT9%MDIV)=PGMV(JROF,JLEV,YT9%MDIV)&
       & +YDGSGEOM%GMAPPA(JROF)/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))*ZSID(JROF,JLEV)
      PGMV(JROF,JLEV,YT0%MDIV)=PGMV(JROF,JLEV,YT0%MDIV)&
       & -2.0_JPRB*YDGSGEOM%GMAPPA(JROF)/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))*ZSID(JROF,JLEV)
    ENDDO
  ELSE
    DO JROF=KST,KPROF
      PGMV(JROF,JLEV,YT9%MDIV)=PGMV(JROF,JLEV,YT9%MDIV)&
       & +RSTRET*RSTRET/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))*ZSID(JROF,JLEV)  
      PGMV(JROF,JLEV,YT0%MDIV)=PGMV(JROF,JLEV,YT0%MDIV)&
       & -2.0_JPRB*RSTRET*RSTRET/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))*ZSID(JROF,JLEV)  
    ENDDO
  ENDIF
ENDDO

!     ------------------------------------------------------------------

!*       4.    HORIZONTAL ADVECTION.
!              ---------------------

! * T,V,U,GFL:

DO JGFL=1,NUMFLDS
  IF(YCOMP(JGFL)%LCDERS .AND. YCOMP(JGFL)%LADV) THEN
    DO JLEV=1,NFLEVG
      DO JROF=KST,KPROF
        PGFL(JROF,JLEV,YCOMP(JGFL)%MPL)=PGFL(JROF,JLEV,YCOMP(JGFL)%MPL)&
         & -PDT*PGMV5(JROF,JLEV,YT5%MU)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)-PDT*&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5L)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        PGFL(JROF,JLEV,YCOMP(JGFL)%MPM)=PGFL(JROF,JLEV,YCOMP(JGFL)%MPM)&
         & -PDT*PGMV5(JROF,JLEV,YT5%MV)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)-PDT*&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5M)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
      ENDDO
    ENDDO
  ENDIF
ENDDO

DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF

    ZTT0L(JROF,JLEV)=ZTT0L(JROF,JLEV)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MU)*PGMVT1(JROF,JLEV,YT1%MT)
    PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
     & -PDT*ZTT5L(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MT)
    ZTT0M(JROF,JLEV)=ZTT0M(JROF,JLEV)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MV)*PGMVT1(JROF,JLEV,YT1%MT)
    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & -PDT*ZTT5M(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MT)

    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MDIV)*PGMVT1(JROF,JLEV,YT1%MV)
    PGMV(JROF,JLEV,YT0%MDIV)=PGMV(JROF,JLEV,YT0%MDIV)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MV)*PGMVT1(JROF,JLEV,YT1%MV)  
    PGMV(JROF,JLEV,YT0%MVL)=PGMV(JROF,JLEV,YT0%MVL)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MU)*PGMVT1(JROF,JLEV,YT1%MV)  
    PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MVL)*PGMVT1(JROF,JLEV,YT1%MV)
    PGMV(JROF,JLEV,YT0%MUL)=PGMV(JROF,JLEV,YT0%MUL)&
     & +PDT*PGMV5(JROF,JLEV,YT5%MV)*PGMVT1(JROF,JLEV,YT1%MV)  
    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & +PDT*PGMV5(JROF,JLEV,YT5%MUL)*PGMVT1(JROF,JLEV,YT1%MV)

    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & +PDT*PGMV5(JROF,JLEV,YT5%MVOR)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV,YT0%MVOR)=PGMV(JROF,JLEV,YT0%MVOR)&
     & +PDT*PGMV5(JROF,JLEV,YT5%MV)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV,YT0%MVL)=PGMV(JROF,JLEV,YT0%MVL)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MV)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MVL)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV,YT0%MUL)=PGMV(JROF,JLEV,YT0%MUL)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MU)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
     & -PDT*PGMV5(JROF,JLEV,YT5%MUL)*PGMVT1(JROF,JLEV,YT1%MU)  

  ENDDO
ENDDO

!     ------------------------------------------------------------------

!*       3.    CASE "LSPRT".
!              -------------

! *  Convert gradients of 'TV' to gradients of T if necessary.
!    If no gradient of Q approximate (for EUL NMI - still needed??)

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    IF (YQ%LCDERS) THEN
      IF (.NOT. YRDYNA%LDRY_ECMWF) THEN
        DO JROF=KST,KPROF
          ZR0(JROF,JLEV)=ZR0(JROF,JLEV)-RD*(PGMV5(JROF,JLEV,YT5%MTL)-RETV*&
           & PGMV5(JROF,JLEV,YT5%MT)*PGFL5(JROF,JLEV,YQ%MP5L))*ZTT0L(JROF,JLEV)&
           & /(ZR5(JROF,JLEV)*ZR5(JROF,JLEV))
          ZR0(JROF,JLEV)=ZR0(JROF,JLEV)-RD*(PGMV5(JROF,JLEV,YT5%MTM)-RETV*&
           & PGMV5(JROF,JLEV,YT5%MT)*PGFL5(JROF,JLEV,YQ%MP5M))*ZTT0M(JROF,JLEV)&
           & /(ZR5(JROF,JLEV)*ZR5(JROF,JLEV))
        ENDDO
      ENDIF
      DO JROF=KST,KPROF
        PGMV(JROF,JLEV,YT0%MTL)=PGMV(JROF,JLEV,YT0%MTL)&
         & +RD*ZTT0L(JROF,JLEV)/ZR5(JROF,JLEV)
        PGFL(JROF,JLEV,YQ%MPL)=PGFL(JROF,JLEV,YQ%MPL)&
         & -RD*RETV*PGMV5(JROF,JLEV,YT5%MT)*ZTT0L(JROF,JLEV)/ZR5(JROF,JLEV)  
        PGMV(JROF,JLEV,YT0%MT)=PGMV(JROF,JLEV,YT0%MT)&
         & -RD*RETV*PGFL5(JROF,JLEV,YQ%MP5L)&
         & *ZTT0L(JROF,JLEV)/ZR5(JROF,JLEV)  
        PGMV(JROF,JLEV,YT0%MTM)=PGMV(JROF,JLEV,YT0%MTM)&
         & +RD*ZTT0M(JROF,JLEV)/ZR5(JROF,JLEV)
        PGFL(JROF,JLEV,YQ%MPM)=PGFL(JROF,JLEV,YQ%MPM)&
         & -RD*RETV*PGMV5(JROF,JLEV,YT5%MT)*ZTT0M(JROF,JLEV)/ZR5(JROF,JLEV)  
        PGMV(JROF,JLEV,YT0%MT)=PGMV(JROF,JLEV,YT0%MT)&
         & -RD*RETV*PGFL5(JROF,JLEV,YQ%MP5M)&
         & *ZTT0M(JROF,JLEV)/ZR5(JROF,JLEV)  
      ENDDO
    ELSE
      IF (.NOT. YRDYNA%LDRY_ECMWF) THEN
        DO JROF=KST,KPROF
          ZR0(JROF,JLEV)=ZR0(JROF,JLEV)-RD*PGMV5(JROF,JLEV,YT5%MTL)&
           & *ZTT0L(JROF,JLEV)/(ZR5(JROF,JLEV)*ZR5(JROF,JLEV))
          ZR0(JROF,JLEV)=ZR0(JROF,JLEV)-RD*PGMV5(JROF,JLEV,YT5%MTM)&
           & *ZTT0M(JROF,JLEV)/(ZR5(JROF,JLEV)*ZR5(JROF,JLEV))
        ENDDO
      ENDIF
      DO JROF=KST,KPROF
        PGMV(JROF,JLEV,YT0%MTL)=PGMV(JROF,JLEV,YT0%MTL)&
         & +RD*ZTT0L(JROF,JLEV)/ZR5(JROF,JLEV)
        PGMV(JROF,JLEV,YT0%MTM)=PGMV(JROF,JLEV,YT0%MTM)&
         & +RD*ZTT0M(JROF,JLEV)/ZR5(JROF,JLEV)
      ENDDO
    ENDIF
  ENDDO
  IF (.NOT. YRDYNA%LDRY_ECMWF) THEN
    DO JLEV=1,NFLEVG
      DO JROF=KST,KPROF
        PGFL(JROF,JLEV,YQ%MP)=PGFL(JROF,JLEV,YQ%MP)+(RV-RD)*ZR0(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF
ELSE
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      PGMV(JROF,JLEV,YT0%MTL)=PGMV(JROF,JLEV,YT0%MTL)+ZTT0L(JROF,JLEV)
      PGMV(JROF,JLEV,YT0%MTM)=PGMV(JROF,JLEV,YT0%MTM)+ZTT0M(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*       2.    VERTICAL ADVECTION.
!              -------------------

! * GFL,T,V,U:
!   Bottom layer.
!   The vertical advection at bottom level is not yet modified when NDPSI=1,
!   since Physics outputs are needed.

! * GFL,T,V,U:
!   Layers 1 to nflevg-1.

DO JGFL=1,NUMFLDS
  IF(YCOMP(JGFL)%LADV) THEN
    DO JLEV=NFLEVG-1,1,-1
      DO JROF=KST,KPROF
        ZED5=PDT*0.5_JPRB*PCTY5(JROF,JLEV,YYTCTY5%M_EVEL)
        ZED=0.0_JPRB

        PGFL(JROF,JLEV,YCOMP(JGFL)%MP)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP)&
         & +ZED5*PRDELP5(JROF,JLEV+1)*PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)  
        PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP)=PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP)&
         & -ZED5*PRDELP5(JROF,JLEV+1)*PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)  
        ZED=ZED+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*PRDELP5(JROF,JLEV+1)*PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)  
        PRDELP(JROF,JLEV+1)=PRDELP(JROF,JLEV+1)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED5*PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)  

        PGFL(JROF,JLEV,YCOMP(JGFL)%MP)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP)&
         & +ZED5*PRDELP5(JROF,JLEV)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP)=PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP)&
         & -ZED5*PRDELP5(JROF,JLEV)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        ZED=ZED+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*PRDELP5(JROF,JLEV)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        PRDELP(JROF,JLEV)=PRDELP(JROF,JLEV)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED5*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  

        PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)=PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)+PDT*0.5_JPRB*ZED

      ENDDO
    ENDDO
  ENDIF
ENDDO

DO JLEV=NFLEVG-1,1,-1
  DO JROF=KST,KPROF
    ZED5=PDT*0.5_JPRB*PCTY5(JROF,JLEV,YYTCTY5%M_EVEL)
    ZED=0.0_JPRB

    PGMV(JROF,JLEV,YT0%MT)=PGMV(JROF,JLEV,YT0%MT)&
     & +ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MT)  
    PGMV(JROF,JLEV+1,YT0%MT)=PGMV(JROF,JLEV+1,YT0%MT)&
     & -ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MT)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MT)  
    PRDELP(JROF,JLEV+1)=PRDELP(JROF,JLEV+1)&
     & +(PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED5*PGMVT1(JROF,JLEV+1,YT1%MT)  

    PGMV(JROF,JLEV,YT0%MT)=PGMV(JROF,JLEV,YT0%MT)&
     & +ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MT)  
    PGMV(JROF,JLEV+1,YT0%MT)=PGMV(JROF,JLEV+1,YT0%MT)&
     & -ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MT)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MT)  
    PRDELP(JROF,JLEV)=PRDELP(JROF,JLEV)&
     & +(PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED5*PGMVT1(JROF,JLEV,YT1%MT)  

    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & +ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MV)  
    PGMV(JROF,JLEV+1,YT0%MV)=PGMV(JROF,JLEV+1,YT0%MV)&
     & -ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MV)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MV)  
    PRDELP(JROF,JLEV+1)=PRDELP(JROF,JLEV+1)&
     & +(PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED5*PGMVT1(JROF,JLEV+1,YT1%MV)  

    PGMV(JROF,JLEV,YT0%MV)=PGMV(JROF,JLEV,YT0%MV)&
     & +ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MV)  
    PGMV(JROF,JLEV+1,YT0%MV)=PGMV(JROF,JLEV+1,YT0%MV)&
     & -ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MV)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MV)  
    PRDELP(JROF,JLEV)=PRDELP(JROF,JLEV)&
     & +(PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED5*PGMVT1(JROF,JLEV,YT1%MV)  

    PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
     & +ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MU)  
    PGMV(JROF,JLEV+1,YT0%MU)=PGMV(JROF,JLEV+1,YT0%MU)&
     & -ZED5*PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MU)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *PRDELP5(JROF,JLEV+1)*PGMVT1(JROF,JLEV+1,YT1%MU)  
    PRDELP(JROF,JLEV+1)=PRDELP(JROF,JLEV+1)&
     & +(PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED5*PGMVT1(JROF,JLEV+1,YT1%MU)  

    PGMV(JROF,JLEV,YT0%MU)=PGMV(JROF,JLEV,YT0%MU)&
     & +ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MU)  
    PGMV(JROF,JLEV+1,YT0%MU)=PGMV(JROF,JLEV+1,YT0%MU)&
     & -ZED5*PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MU)  
    ZED=ZED+(PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *PRDELP5(JROF,JLEV)*PGMVT1(JROF,JLEV,YT1%MU)  
    PRDELP(JROF,JLEV)=PRDELP(JROF,JLEV)&
     & +(PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED5*PGMVT1(JROF,JLEV,YT1%MU)  

    PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)=PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)+PDT*0.5_JPRB*ZED

  ENDDO
ENDDO

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPEULDYNAD',1,ZHOOK_HANDLE)
END SUBROUTINE CPEULDYNAD

