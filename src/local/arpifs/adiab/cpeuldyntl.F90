SUBROUTINE CPEULDYNTL(YDGEOMETRY,YDGMV,YDGMV5,YGFL,YDML_DYN,YDPHY,KST,KPROF,PBETADT,PDT,&
 & KIBL,PGFL,PKENE,PCTY0,&
 & PRES0,PRDELP,PATND,&
 & PGFLT1,PGMVT1,PGMVT1S,PGMV,PGMVS,&
 & PB2,&
 & PGMV5,PGFL5,PCTY5,PRES5,PRDELP5)

!**** *CPEULDYNTL* - tangent linear dynamic grid point calculations
!                    for Eulerian model.

!     Purpose.
!     --------
!     Tangent Linear Dynamic computations in grid-point space for Eulerian model.

!     The following options present in the direct code are not yet coded in TL:
!     - LRUBC=T and NDPSFI=1. 
!     - LNHDYN=T.
!     - LPC_FULL=T.
!     - LVERTFE=T.

!**   Interface.
!     ----------
!        *CALL* *CPEULDYNTL(...)

!        Explicit arguments :
!        --------------------

!        INPUT (OTHER THAN "...5" VARIABLES):
!          KST     - first element of work.
!          KPROF   - depth of work.
!          PBETADT - BETADT or 0 according to configuration.
!          PDT     - SL2TL or first timestep: timestep; otherwise 2*timestep.
!          KIBL    - index into YRCSGEOM/YRGSGEOM instances in YDGEOMETRY
!          PGFL    - GFL variables at t and t-dt.
!          PKENE   - kinetic energy.
!          PCTY0   - contains vertical velocities, vertical integral of divergence at t.
!          PRES0   - pressure on an interlayer at t.
!          PRDELP  - 1/(pressure depth of layers) at t.
!          PATND   - adiabatic Lagrangian tendencies.

!        INPUT/OUTPUT:
!          PGFLT1  - t+dt term for the GFL equations. 
!          PGMVT1  - GMV variables at t+dt.
!          PGMVT1S - GMVS variables at t+dt.
!          PGMV    - GMV variables at t and t-dt.
!          PGMVS   - GMVS variables at t and t-dt.

!        OUTPUT:
!          PB2     - "SLBUF2" buffer.

!        INPUT "...5" VARIABLES (trajectory):
!          PGMV5   - GMV variables.
!          PGFL5   - GFL variables.
!          PCTY5   - contains vertical velocities, vertical integral of divergence.
!          PRES5   - pressure on an interlayer (trajectory).
!          PRDELP5 - 1/(pressure depth of layers) (trajectory).

!        Implicit arguments : none.
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF*
!      Original : 90-03-22 (initial name = CPDYNTL)

!     Modifications.
!     --------------
!   M.Hamrud      01-Oct-2003 CY28 Cleaning
!   -- name = CPEULDYNTL --
!   K.Yessad     : 20-03-2006: update according to the direct code
!    routine CPEULDYN and rename it into CPEULDYNTL. 
!   M.Jidane  08-04-2006 : Reintro of R dep. on q variables under key
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad (Dec 2008): remove dummy CDLOCK
!   K. Yessad (Nov 2009): cleanings, DT/Dt now pre-computed in CPG_GP_TL.
!   K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!   K. Yessad (Nov 2011): various contributions.
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   K. Yessad (July 2014): Move some variables.
!     ------------------------------------------------------------------

USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCT0             , ONLY : LSPRT, LNHDYN
USE YOMDYNA            , ONLY : LRUBC, LPC_FULL, LDRY_ECMWF
USE YOMCVER            , ONLY : LVERTFE
USE YOMCST             , ONLY : RD, RV, RETV, YRCST
USE YOMPHY             , ONLY : TPHY
USE YOM_YGFL           , ONLY : TYPE_GFLD
USE INTDYN_MOD         , ONLY : YYTTND, YYTCTY5, YYTCTY0

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV5
TYPE(MODEL_DYNAMICS_TYPE),INTENT(IN):: YDML_DYN
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBETADT
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKENE(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRES0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%YT1%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMV5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV5%YT5%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM5) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCTY5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY5%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRES5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZR0   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR5   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSID  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIDL (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSIDM (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSISP (YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSISPL(YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSISPM(YDGEOMETRY%YRDIM%NPROMA       )
REAL(KIND=JPRB) :: ZSIT  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITL (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSITM (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT0L (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT0M (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT5L (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT5M (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZFM, ZED, ZED5

INTEGER(KIND=JPIM) :: IPROFS, JLEV, JROF,JGFL
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "sigam.intfb.h"
#include "sitnu.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPEULDYNTL',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 & YDMP=>YDGEOMETRY%YRMP,  YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, &
 & YDSTA=>YDGEOMETRY%YRSTA, YDLAP=>YDGEOMETRY%YRLAP, YDCSGLEG=>YDGEOMETRY%YRCSGLEG,  &
 & YDVSPLIP=>YDGEOMETRY%YRVSPLIP, YDVSLETA=>YDGEOMETRY%YRVSLETA, YDHSLMER=>YDGEOMETRY%YRHSLMER,  &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), &
  & YDSPGEOM=>YDGEOMETRY%YSPGEOM, YDDYN=>YDML_DYN%YRDYN,YDPTRSLB2=>YDML_DYN%YRPTRSLB2,  &
  & YDEDYN=>YDML_DYN%YREDYN)

ASSOCIATE(NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NDIM5=>YGFL%NDIM5, &
 & NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, YQ=>YGFL%YQ, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & LIMPF=>YDDYN%LIMPF, LSIDG=>YDDYN%LSIDG, &
 & LESIDG=>YDEDYN%LESIDG, &
 & RSTRET=>YDGEM%RSTRET, &
 & NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, YT0=>YDGMV%YT0, &
 & YT1=>YDGMV%YT1, YT9=>YDGMV%YT9, &
 & YT5=>YDGMV5%YT5, NDPSFI=>YDPHY%NDPSFI, &
 & MSLB2SPSI=>YDPTRSLB2%MSLB2SPSI, MSLB2TSI=>YDPTRSLB2%MSLB2TSI, &
 & MSLB2USI=>YDPTRSLB2%MSLB2USI, MSLB2VSI=>YDPTRSLB2%MSLB2VSI, &
 & NFLDSLB2=>YDPTRSLB2%NFLDSLB2)
!     ------------------------------------------------------------------

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (NDPSFI == 1) THEN
  CALL ABOR1(' CPEULDYNTL: NDPSFI=1 not yet coded in EUL TL model')
ENDIF
IF (LRUBC) THEN
  CALL ABOR1(' CPEULDYNTL: LRUBC=T not yet coded in EUL TL model')
ENDIF
IF (LNHDYN) THEN
  CALL ABOR1(' CPEULDYNTL: LNHDYN=T not yet coded in EUL TL model')
ENDIF
IF (LPC_FULL) THEN
  CALL ABOR1(' CPEULDYNTL: LPC_FULL=T not yet coded in EUL TL model')
ENDIF
IF (LVERTFE) THEN
  CALL ABOR1(' CPEULDYNTL: LVERTFE=T not yet coded in EUL TL model')
ENDIF

!     ------------------------------------------------------------------

!*       1.    INTERMEDIATE QUANTITIES FOR EQUATIONS RHS
!              -----------------------------------------

! ky: not fully consistent with the direct code but consistent with the
!     cy33 version of cpeuldyntl.F90.
ZFM = 0.5_JPRB

! dimension for calls to SI routines
IPROFS=KPROF-KST+1

!     ------------------------------------------------------------------

!*       2.    VERTICAL ADVECTION.
!              -------------------

! * U,V,T,GFL
!   Layers 1 to nflevg-1.

DO JLEV=1,NFLEVG-1
  DO JROF=KST,KPROF
    ZED=PDT*0.5_JPRB*PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)
    ZED5=PDT*0.5_JPRB*PCTY5(JROF,JLEV,YYTCTY5%M_EVEL)

    PGMVT1(JROF,JLEV,YT1%MU)=PGMVT1(JROF,JLEV,YT1%MU)+&
     & (PGMV(JROF,JLEV,YT0%MU)-PGMV(JROF,JLEV+1,YT0%MU))&
     & *ZED5*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED5*PRDELP(JROF,JLEV)  
    PGMVT1(JROF,JLEV+1,YT1%MU)=PGMVT1(JROF,JLEV+1,YT1%MU)+&
     & (PGMV(JROF,JLEV,YT0%MU)-PGMV(JROF,JLEV+1,YT0%MU))&
     & *ZED5*PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED *PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MU)-PGMV5(JROF,JLEV+1,YT5%MU))&
     & *ZED5*PRDELP (JROF,JLEV+1)  

    PGMVT1(JROF,JLEV,YT1%MV)=PGMVT1(JROF,JLEV,YT1%MV)+&
     & (PGMV(JROF,JLEV,YT0%MV)-PGMV(JROF,JLEV+1,YT0%MV))&
     & *ZED5*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED5*PRDELP(JROF,JLEV)  
    PGMVT1(JROF,JLEV+1,YT1%MV)=PGMVT1(JROF,JLEV+1,YT1%MV)+&
     & (PGMV(JROF,JLEV,YT0%MV)-PGMV(JROF,JLEV+1,YT0%MV))&
     & *ZED5*PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED *PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MV)-PGMV5(JROF,JLEV+1,YT5%MV))&
     & *ZED5*PRDELP (JROF,JLEV+1)  

    PGMVT1(JROF,JLEV,YT1%MT)=PGMVT1(JROF,JLEV,YT1%MT)+&
     & (PGMV(JROF,JLEV,YT0%MT)-PGMV(JROF,JLEV+1,YT0%MT))&
     & *ZED5*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED*PRDELP5(JROF,JLEV)+&
     & (PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED5*PRDELP(JROF,JLEV)  
    PGMVT1(JROF,JLEV+1,YT1%MT)=PGMVT1(JROF,JLEV+1,YT1%MT)+&
     & (PGMV(JROF,JLEV,YT0%MT)-PGMV(JROF,JLEV+1,YT0%MT))&
     & *ZED5*PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED *PRDELP5(JROF,JLEV+1)+&
     & (PGMV5(JROF,JLEV,YT5%MT)-PGMV5(JROF,JLEV+1,YT5%MT))&
     & *ZED5*PRDELP (JROF,JLEV+1)  

  ENDDO
ENDDO

DO JGFL=1,NUMFLDS
  IF(YCOMP(JGFL)%LADV) THEN
    DO JLEV=1,NFLEVG-1
      DO JROF=KST,KPROF
        ZED=PDT*0.5_JPRB*PCTY0(JROF,JLEV,YYTCTY0%M_EVEL)
        ZED5=PDT*0.5_JPRB*PCTY5(JROF,JLEV,YYTCTY5%M_EVEL)
        PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)&
         & +(PGFL(JROF,JLEV,YCOMP(JGFL)%MP)-PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP))&
         & *ZED5*PRDELP5(JROF,JLEV)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED*PRDELP5(JROF,JLEV)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED5*PRDELP(JROF,JLEV)  
        PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)=PGFLT1(JROF,JLEV+1,YCOMP(JGFL)%MP1)&
         & +(PGFL(JROF,JLEV,YCOMP(JGFL)%MP)-PGFL(JROF,JLEV+1,YCOMP(JGFL)%MP))&
         & *ZED5*PRDELP5(JROF,JLEV+1)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED*PRDELP5(JROF,JLEV+1)+(&
         & PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5)-PGFL5(JROF,JLEV+1,YCOMP(JGFL)%MP5)&
         & )*ZED5*PRDELP(JROF,JLEV+1)  
      ENDDO
    ENDDO
  ENDIF
ENDDO
  
! * U,V,T,GFL:
!   Bottom layer.
!   The vertical advection at bottom level is not yet modified when NDPSI=1,
!   since Physics outputs are needed.

!     ------------------------------------------------------------------

!*       3.    CASE "LSPRT".
!              -------------

! *  Convert gradients of 'TV' to gradients of T if necessary.
!    If no gradient of Q approximate (for EUL NMI - still needed??)

!        3.0     Trajectory code to compute ZR5, ZTT5L, ZTT5M.

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZR5(JROF,JLEV)=RD+(RV-RD)*PGFL5(JROF,JLEV,YQ%MP5)
    ENDDO
    IF (YQ%LCDERS) THEN
      DO JROF=KST,KPROF
        ZTT5L(JROF,JLEV)=&
         & RD*(PGMV5(JROF,JLEV,YT5%MTL)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
         & *PGFL5(JROF,JLEV,YQ%MP5L))/ZR5(JROF,JLEV)  
        ZTT5M(JROF,JLEV)=&
         & RD*(PGMV5(JROF,JLEV,YT5%MTM)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
         & *PGFL5(JROF,JLEV,YQ%MP5M))/ZR5(JROF,JLEV)  
      ENDDO
    ELSE
      DO JROF=KST,KPROF
        ZTT5L(JROF,JLEV)=RD*PGMV5(JROF,JLEV,YT5%MTL)/ZR5(JROF,JLEV)
        ZTT5M(JROF,JLEV)=RD*PGMV5(JROF,JLEV,YT5%MTM)/ZR5(JROF,JLEV)
      ENDDO
    ENDIF
  ENDDO
ELSE
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZTT5L(JROF,JLEV)=PGMV5(JROF,JLEV,YT5%MTL)
      ZTT5M(JROF,JLEV)=PGMV5(JROF,JLEV,YT5%MTM)
    ENDDO
  ENDDO
ENDIF

!        3.1     TL code.

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    IF (.NOT. LDRY_ECMWF) THEN
      DO JROF=KST,KPROF
        ZR0(JROF,JLEV)=(RV-RD)*PGFL(JROF,JLEV,YQ%MP)
      ENDDO
    ENDIF
  ENDDO
  DO JLEV=1,NFLEVG
    IF (YQ%LCDERS) THEN
      DO JROF=KST,KPROF
        ZTT0L(JROF,JLEV)=RD*(+((PGMV(JROF,JLEV,YT0%MTL)&
         & -RETV*(PGMV5(JROF,JLEV,YT5%MT)*PGFL(JROF,JLEV,YQ%MPL)&
         & +PGMV(JROF,JLEV,YT0%MT)*PGFL5(JROF,JLEV,YQ%MP5L)))/ZR5(JROF,JLEV)))  
        ZTT0M(JROF,JLEV)=RD*(+((PGMV(JROF,JLEV,YT0%MTM)&
         & -RETV*(PGMV5(JROF,JLEV,YT5%MT)*PGFL(JROF,JLEV,YQ%MPM)&
         & +PGMV(JROF,JLEV,YT0%MT)*PGFL5(JROF,JLEV,YQ%MP5M)))/ZR5(JROF,JLEV)))  
      ENDDO
      IF (.NOT. LDRY_ECMWF) THEN
        DO JROF=KST,KPROF
          ZTT0L(JROF,JLEV)=ZTT0L(JROF,JLEV)&
            & +RD*(-(PGMV5(JROF,JLEV,YT5%MTL)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
            & *PGFL5(JROF,JLEV,YQ%MP5L))*ZR0(JROF,JLEV)&
            & /(ZR5(JROF,JLEV)*ZR5(JROF,JLEV)))
          ZTT0M(JROF,JLEV)=ZTT0M(JROF,JLEV)&
            & +RD*(-(PGMV5(JROF,JLEV,YT5%MTM)-RETV*PGMV5(JROF,JLEV,YT5%MT)&
            & *PGFL5(JROF,JLEV,YQ%MP5M))*ZR0(JROF,JLEV)&
            & /(ZR5(JROF,JLEV)*ZR5(JROF,JLEV)))
        ENDDO
      ENDIF
    ELSE
      DO JROF=KST,KPROF
        ZTT0L(JROF,JLEV)=RD*(PGMV(JROF,JLEV,YT0%MTL)/ZR5(JROF,JLEV))
        ZTT0M(JROF,JLEV)=RD*(PGMV(JROF,JLEV,YT0%MTM)/ZR5(JROF,JLEV))
      ENDDO
      IF (.NOT. LDRY_ECMWF) THEN
        DO JROF=KST,KPROF
          ZTT0L(JROF,JLEV)=ZTT0L(JROF,JLEV)+RD*(-PGMV5(JROF,JLEV,YT5%MTL)&
           & *ZR0(JROF,JLEV)/(ZR5(JROF,JLEV)*ZR5(JROF,JLEV)))
          ZTT0M(JROF,JLEV)=ZTT0M(JROF,JLEV)+RD*(-PGMV5(JROF,JLEV,YT5%MTM)&
           & *ZR0(JROF,JLEV)/(ZR5(JROF,JLEV)*ZR5(JROF,JLEV)))
        ENDDO
      ENDIF
    ENDIF
  ENDDO
ELSE
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZTT0L(JROF,JLEV)=PGMV(JROF,JLEV,YT0%MTL)
      ZTT0M(JROF,JLEV)=PGMV(JROF,JLEV,YT0%MTM)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*       4.    HORIZONTAL ADVECTION.
!              ---------------------

! * U,V,T,GFL:

DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF

    PGMVT1(JROF,JLEV,YT1%MU)=PGMVT1(JROF,JLEV,YT1%MU)&
     & +PDT*( PGMV5(JROF,JLEV,YT5%MVOR)*PGMV(JROF,JLEV,YT0%MV)&
     & +PGMV(JROF,JLEV,YT0%MVOR)*PGMV5(JROF,JLEV,YT5%MV)&
     & -PGMV5(JROF,JLEV,YT5%MV)*PGMV(JROF,JLEV,YT0%MVL)&
     & -PGMV(JROF,JLEV,YT0%MV)*PGMV5(JROF,JLEV,YT5%MVL)&
     & -PGMV5(JROF,JLEV,YT5%MU)*PGMV(JROF,JLEV,YT0%MUL)&
     & -PGMV(JROF,JLEV,YT0%MU)*PGMV5(JROF,JLEV,YT5%MUL)&
     & )  

    PGMVT1(JROF,JLEV,YT1%MV)=PGMVT1(JROF,JLEV,YT1%MV)&
     & +PDT* (-PGMV5(JROF,JLEV,YT5%MDIV)*PGMV(JROF,JLEV,YT0%MV)&
     & -PGMV(JROF,JLEV,YT0%MDIV)*PGMV5(JROF,JLEV,YT5%MV)&
     & -PGMV5(JROF,JLEV,YT5%MU)*PGMV(JROF,JLEV,YT0%MVL)&
     & -PGMV(JROF,JLEV,YT0%MU)*PGMV5(JROF,JLEV,YT5%MVL)&
     & +PGMV5(JROF,JLEV,YT5%MV)*PGMV(JROF,JLEV,YT0%MUL)&
     & +PGMV(JROF,JLEV,YT0%MV)*PGMV5(JROF,JLEV,YT5%MUL)&
     & )  

    PGMVT1(JROF,JLEV,YT1%MT)=PGMVT1(JROF,JLEV,YT1%MT)&
     & -PDT*(ZTT0L(JROF,JLEV)*PGMV5(JROF,JLEV,YT5%MU)&
     & +ZTT5L(JROF,JLEV)*PGMV(JROF,JLEV,YT0%MU)&
     & +ZTT0M(JROF,JLEV)*PGMV5(JROF,JLEV,YT5%MV)&
     & +ZTT5M(JROF,JLEV)*PGMV(JROF,JLEV,YT0%MV))

  ENDDO
ENDDO

DO JGFL=1,NUMFLDS
  IF(YCOMP(JGFL)%LCDERS .AND. YCOMP(JGFL)%LADV) THEN
    DO JLEV=1,NFLEVG
      DO JROF=KST,KPROF
        PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=&
         & PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)-PDT*&
         & (PGFL(JROF,JLEV,YCOMP(JGFL)%MPL)*PGMV5(JROF,JLEV,YT5%MU)&
         & +PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5L)*PGMV(JROF,JLEV,YT0%MU)&
         & +PGFL(JROF,JLEV,YCOMP(JGFL)%MPM)*PGMV5(JROF,JLEV,YT5%MV)&
         & +PGFL5(JROF,JLEV,YCOMP(JGFL)%MP5M)*PGMV(JROF,JLEV,YT0%MV))  
      ENDDO
    ENDDO
  ENDIF
ENDDO

!     ------------------------------------------------------------------

!*       5.    SEMI-IMPLICIT CORRECTION.
!              -------------------------

! ky: part 5 is purely linear and normally should be identical to part 5
!     of cpeuldyn.F90, for the blocks which are not under LSPRT.

!        5.1     Prepare input arrays for SI calculations

! * GMV variables:

DO JLEV=1,NFLEVG
  IF (LSIDG) THEN
    DO JROF=KST,KPROF
      ZSID(JROF,JLEV)=PGMV(JROF,JLEV,YT9%MDIV)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MDIV)
    ENDDO
  ELSEIF (LESIDG) THEN
    DO JROF=KST,KPROF
      ZSID(JROF,JLEV)=&
       & (PGMV(JROF,JLEV,YT9%MDIV)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MDIV))&
       & *YDGSGEOM%GMAPPA(JROF)/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))  
    ENDDO
  ELSE
    DO JROF=KST,KPROF
      ZSID(JROF,JLEV)=&
       & (PGMV(JROF,JLEV,YT9%MDIV)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MDIV))&
       & *RSTRET*RSTRET/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))  
    ENDDO
  ENDIF
  DO JROF=KST,KPROF
    ZSITL(JROF,JLEV)=PGMV(JROF,JLEV,YT9%MTL)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MTL)
    ZSITM(JROF,JLEV)=PGMV(JROF,JLEV,YT9%MTM)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MTM)
  ENDDO
ENDDO

! * GMVS variables:
DO JROF=KST,KPROF
  ZSISPL(JROF)=PGMVS(JROF,YT9%MSPL)-2.0_JPRB*PGMVS(JROF,YT0%MSPL)
  ZSISPM(JROF)=PGMVS(JROF,YT9%MSPM)-2.0_JPRB*PGMVS(JROF,YT0%MSPM)
ENDDO

!        5.2     Do SI calculations
!                This should match what is in LASSIE/LANHSI

CALL SITNU(YRCST,YDGEOMETRY,YDDYN,NPROMA,1,ZSID(KST,1),ZSIT(KST,1),ZSISP(KST),IPROFS)
CALL SIGAM(YRCST,YDGEOMETRY,YDDYN,NPROMA,1,ZSIDL(KST,1),ZSITL(KST,1),ZSISPL(KST),IPROFS,NFLEVG)
CALL SIGAM(YRCST,YDGEOMETRY,YDDYN,NPROMA,1,ZSIDM(KST,1),ZSITM(KST,1),ZSISPM(KST),IPROFS,NFLEVG)

IF (LIMPF) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZSIDL(JROF,JLEV)=ZSIDL(JROF,JLEV)&
       & -YDGSGEOM%RCORI(JROF)*(PGMV(JROF,JLEV,YT9%MV)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MV))
      ZSIDM(JROF,JLEV)=ZSIDM(JROF,JLEV)&
       & +YDGSGEOM%RCORI(JROF)*(PGMV(JROF,JLEV,YT9%MU)-2.0_JPRB*PGMV(JROF,JLEV,YT0%MU))
    ENDDO
  ENDDO
ENDIF

! * For "spectral RT" option, adjust semi-implicit term in
!   T-equation to compensate for later multiplication by R/Rd

IF (LSPRT) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF
      ZSIT(JROF,JLEV)=RD*ZSIT(JROF,JLEV)/ZR5(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!        5.3     Fill PB2(MSLB2[X]SI) arrays

DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PB2(JROF,MSLB2USI-1+JLEV)=-PBETADT*ZSIDL(JROF,JLEV)*PDT*ZFM
    PB2(JROF,MSLB2VSI-1+JLEV)=-PBETADT*ZSIDM(JROF,JLEV)*PDT*ZFM
    PB2(JROF,MSLB2TSI-1+JLEV)=-PBETADT*ZSIT(JROF,JLEV)*PDT*ZFM
  ENDDO
ENDDO

DO JROF=KST,KPROF
  PB2(JROF,MSLB2SPSI)= -PBETADT*ZSISP(JROF)*PDT*ZFM
ENDDO

!     ------------------------------------------------------------------

!*       6.    SEMI-IMPLICIT FOR PC SCHEME ONLY.
!              ---------------------------------

!nyc IF (LPC_FULL) THEN
!nyc   Not yet coded, but should be identical to part 6 of CPEULDYN
!nyc   (except for code under LSPRT) because this is linear code!
!nyc ENDIF

!     ------------------------------------------------------------------

!*       7+8.  Non linear tendency at time "t" and temporal advance.
!              -----------------------------------------------------

! * Momentum equation.
DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PGMVT1(JROF,JLEV,YT1%MU)=PGMVT1(JROF,JLEV,YT1%MU)+PDT*PATND(JROF,JLEV,YYTTND%M_TNDU)&
     & +PDT*(PKENE(JROF,JLEV)*YDCSGEOM%RATATX(JROF))
    PGMVT1(JROF,JLEV,YT1%MV)=PGMVT1(JROF,JLEV,YT1%MV)+PDT*PATND(JROF,JLEV,YYTTND%M_TNDV)&
     & -PDT*(PKENE(JROF,JLEV)*YDCSGEOM%RATATH(JROF))
  ENDDO
ENDDO

! * Temperature equation.
DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF
    PGMVT1(JROF,JLEV,YT1%MT)=PGMVT1(JROF,JLEV,YT1%MT)+PDT*PATND(JROF,JLEV,YYTTND%M_TNDT)
  ENDDO
ENDDO

! * Continuity equation.
PGMVT1S(KST:KPROF,YT1%MSP)=PGMVT1S(KST:KPROF,YT1%MSP)&
 & -PDT*PCTY0(KST:KPROF,NFLEVG,YYTCTY0%M_PSDVBC)/PRES5(KST:KPROF,NFLEVG)&
 & +PDT*PCTY5(KST:KPROF,NFLEVG,YYTCTY5%M_PSDVBC)*PRES0(KST:KPROF,NFLEVG)&
 & /(PRES5(KST:KPROF,NFLEVG)*PRES5(KST:KPROF,NFLEVG))  

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPEULDYNTL',1,ZHOOK_HANDLE)
END SUBROUTINE CPEULDYNTL

