#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG(YDGEOMETRY,YDMF_PHYS,YDCPG_DYN0,YDCPG_DYN9,YDMF_PHYS_SURF,YDVARS,YDMODEL,YDFIELDS,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & LDCONFX,LD_DFISTEP,LDFSTEP,LDDIAB,LDSLPHY,LDUSEPB1,&
 & KGPCOMP,KSTGLO,KIBL,&
 & PDT,PDTPHY,PTE,PBETADT,YDSL,PGFLSLP,PSAVTEND,PGMVTNDHD_DDH,PGFLTNDHD_DDH,PGPSDT2D,PSD_PF,&
 & PGRADH_PHY,&
 & PFORCEU,PFORCEV,PFORCET,PFORCEQ,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGMV,PGMVS,PGFL,PGFLPC,PGFLPT,&
 & PSP_SB,PSP_SG,PSP_RR,PSP_CL,PSD_VF,PSD_VP,PSD_VV,PSD_VH,PSD_VK,PSD_VA,PSD_VC,&
 & PSD_VD,PSD_SFL,PSD_SFO,PSD_XA,PSD_DI,PSD_XP,PSD_XP2,&
 & PEMTD,PEMTU,PTRSW,PRMOON,PGMVTNDSI_DDH,&
 & PGDEOSI,PGUEOSI,PGMU0,PGMU0_MIN,PGMU0_MAX,&
 & PGDEOTI,PGDEOTI2,PGUEOTI,PGUEOTI2,PGEOLT,PGEOXT,&
 & PGRPROX,PGMIXP,PGFLUXC,PGRSURF,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & KSETTLOFF,PB1,PB2,PGMVT1,PGMVT1S,PGFLT1,PEXTRA,&
 & PGMVTNDSL_DDH,PGFLTNDSL_DDH,PTRAJ_PHYS,PTRAJ_SLAG,YDDDH)

!**** *CPG* - Grid point calculations.

!     Purpose.
!     --------
!           Grid point calculations.

!**   Interface.
!     ----------
!        *CALL* *CPG(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        LDCONFX      : T if this call to CPG is done only to do some diagnostics like CFU,XFU
!                       (LDCONFX=T <==> former CDCONF='X')
!        LD_DFISTEP      : 'D' -> DFI computations
!        LDFSTEP      : .T. if first step.
!        LDDIAB       : .T. if complete physics is activated.
!        LDSLPHY      : .T. if ECMWF split physics.
!        LDUSEPB1     : .T. if updating PB1
!        KGPCOMP      : total number of grid points:
!                       ndglg*ndlon in global / ndguxg*ndlon in LAM
!                       used for model computations.
!        KSTGLO       : global offset.
!        KIBL         : index into the YRGSGEOM/YRCSGEOM geometry types.
!        PDT          : For a leap-frog scheme (three time level scheme):
!                       'dt' at the first time-step, '2 dt' otherwise.
!                       For a 2TL SL scheme: timestep 'dt'.
!        PDTPHY       : timestep used in the physics.
!        PTE          : 1. or 0. according to different configurations.
!        PBETADT      : BETADT or 0. according to different configurations.
!        YDSL         : SL_STRUCT definition
!        PGFLSLP      : GFL array for use in semi-lagrangian physics
!        PSAVTEND     : tendencies buffer for split ECMWF physics.
!        PGMVTNDHD_DDH: tendencies of horizontal diffusion scheme for GMV.
!        PGFLTNDHD_DDH: tendencies of horizontal diffusion for spectrally treated GFL.
!        PGPSDT2D     : cf. YGPSDT%GP2D in YOMSPSDT (buffer for stochastic physics).
!        PGRADH_PHY   : Horizontal Gradients for Physics

!     INPUT/OUTPUT:
!     -------------
!        PGMV         : upper air GMV variables at t and t-dt.
!        PGMVS        : surface GMV variables at t and t-dt.
!        PGFL         : unified_treatment grid-point fields at t
!        PGFLPC       : unified_treatment grid-point fields at t (3TL PC only)
!        PGFLPT       : tendency of X variable from phy.
!        PSP_SB       : soil prognostic quantities for the different reservoirs
!        PSP_SG       : surface snow prognostic quantities
!        PSP_RR       : surface prognostic quantities 
!        PSP_CL       : cls diagnostic fields
!        PSD_VF       : climatological/geographical diagnostic fields
!        PSD_VP       : deep soil diagnostic fields
!        PSD_VV       : vegetation diagnostic fields
!        PSD_VH       : convective cloud diagnostic fields
!        PSD_VK       : convective cloud prognostic fields
!        PSD_VA       : aerosol diagnostic fields
!        PSD_VC       : climatological ozone profiles diagnostic fields
!        PSD_VD       : diagnostic fields
!        PSD_SFL      : SFLUX diagnostic fields
!        PSD_SFO      : SFORC diagonstic fields
!        PSD_PF       : precip fraction 
!        PSD_XP       : precipitation type diagnostic
!        PSD_XP2      : precipitation type diagnostic
!        PEMTD        : downward longwave emissivity
!        PEMTU        : upward longwave emissivity
!        PTRSW        : shortwave transmissivity
!        PRMOON       : Moon radiation
!        PGMVTNDSI_DDH: tendencies of semi-implicit scheme.
!        PGDEOSI      : DESCENDING INCREMENTAL OPTICAL DEPTHS, SOLAR
!        PGUEOSI      : ASCENDING  INCREMENTAL OPTICAL DEPTHS, SOLAR
!        PGMU0        : COSINE OF SOLAR ZENITH ANGLE, APPROXIMATE ACTUAL VALUE
!        PGMU0_MIN    : COSINE OF SOLAR ZENITH ANGLE, MIN VALUE
!        PGMU0_MAX    : COSINE OF SOLAR ZENITH ANGLE, MAX VALUE
!        PGDEOTI      : descending incremental optical depths, dB/dT(T0) weights
!        PGDEOTI2     : descending incremental optical depths, B weights with
!                       linear T_e correction
!        PGUEOTI      : ascending incremental optical depths, dB/dT(T0) weights
!        PGUEOTI2     : ascending incremental optical depths, B weights with
!                       linear T_e correction
!        PGEOLT       : local optical depths, dB/dT(T0) weights
!        PGEOXT       : maximum optical depths for EBL-EAL, dB/dT(T0) weights
!        PGRPROX      : correction term for adjacent exchanges
!        PGMIXP       : non-statistical weights for bracketing
!        PGFLUXC      : out of bracket part of EBL, resp. EBL-EAL flux
!        PGRSURF      : corrective ratio for surface CTS contribution
!        YDDDH        : diagnostic superstructure

!     OUTPUT:
!     -------
!        PB1          : "SLBUF1" buffer for interpolations in SL scheme.
!        PB2          : "SLBUF2" buffer.
!        PGMVT1       : upper air GMV variables buffer.
!        PGMVT1S      : surface GMV variables buffer.
!        PGFLT1       : GFL variables buffer.
!        PEXTRA       : additional quantity for diagnostics.
!        PGMVTNDSL_DDH: GMV(t+dt,F)-GMV(t or t-dt,O) for DDH
!        PGFLNDSL_DDH : GFL(t+dt,F)-GFL(t or t-dt,O) for DDH

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation vol 2 ch 1 and vol 3 ch 6

!     Author.
!     -------
!      Philippe Courtier  *DMN*
!      Original : 90-03-19

! Modifications
! -------------
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!    4-Mar-2008 Y. Seity : Cleaning IR and WV similated sat pictures
!                            (replaced by Fullpos Calculations)
!   K. Yessad (Sep 2008): prune enhanced diffusion (lfrein).
!   18-Nov-2008 R.Brozkova: OpenMP bugfix 
!   K. Yessad (Dec 2008): remove dummy CDLOCK + cleanings
!   18-May-2009 S. Riette : mean cls wind added 
!   F. Vana  15-Oct-2009: NSPLTHOI option
!   15-October-2009 Y. Bouteloup : Store radiative cloud water and ice in GFL (YIRAD and YLRAD)
!   K. Yessad (Nov 2009): cleanings, DT/Dt now pre-computed in CPG_GP.
!   K. Yessad (Nov 2009): prune lpc_old.
!   24-Feb-2010 S. Riette : max gust avec NXGSTPERIOD seconds
!   11-Mai-2010 F. Bouyssel : RINDX, RINDY in argument
!   L. Bengtsson-Sedlar & F. Vana  18-Feb-2011 : CA arrays for MF physics
!   K. Yessad (Jan 2011): introduce INTDYN_MOD structures + cleanings.
!   F. Vana  21-Feb-2011: horiz. turbulence, N[x]LAG=4, diffus. on phys. tend.
!   G.Mozdzynski (Jan 2011): OOPS cleaning, use of derived type SL_STRUCT
!   G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM
!   2011-09-07, J.M. Piriou and E. Bazile: open/close output LFA files for 1D model MUSC (LMUSCLFA).
!   K. Yessad (Fev 2012): various modifications.
!   R. El Khatib 16-Mar-2012 Automatic allocations + cleanings
!   F. Bouttier  Jul 2012: pass stochastic physics pattern ZGPSDT2D
!   M. Ahlgrimm  31-Oct-2011 add rain, snow and PEXTRA to DDH output
!   T. Wilhelmsson 29-Mar-2012 Remove LCPG_SPLIT option and move OpenMP loop to CPG_DRV
!   F. Vana  28-Nov-2013 : Redesigned trajectory handling.
!   M. Diamantakis Dec 2013: add KSETTLOFF - may be used when LSETTLSVF=T
!   M. Ahlgrimm Apr 2014: Add lake variables and precip fraction to DDH output
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   2013-11, J. Masek: Passing intermittency arrays for ACRANEB2.
!   K. Yessad (July 2014): Move some variables.
!   O. Marsden (May 2016): Replace NPROMA9 by NPROMA for dimensions, as NPROMA9 not compatible with some routines called
!   F.Taillefer (June 2016): add MF assim CLS arrays in CPG_DIA
!   K. Yessad (Dec 2016): Prune obsolete options.
!   2017-09, J. Masek: Shifted dimensioning of PGMU0.
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!   R. El Khatib 05-Jun-2018 computation of periods moved from cnt4 (OOPS refactoring)
!   M. Diamantakis (June 2018): Extra arguments for multiple (Atlas) grid advection scheme
!   R. Brozkova (Sept 2018) : Dataflow for global normal irradiance and mean
!                             radiant temperature.
!   F. Vana  Oct-2018: More optimal trigger of SLPHY computation.
!   I. Etchevers (Jan 2019) : PSD_XP for precipitation type diagnostic
!   2019-09, M. Hrastinski: Dataflow for TKE and TTE terms in ALARO DDH (ZFTCNS)
! End Modifications
!-------------------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
!
USE FIELDS_MOD         , ONLY : FIELDS
!
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_DYN_TYPE_MOD   , ONLY : CPG_DYN_TYPE
USE MFPHYS_SURFACE_TYPE_MOD,ONLY : MFPHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCT0             , ONLY : LSLAG
USE YOMDYNA            , ONLY : LSLDIA, LPC_FULL, LPC_CHEAP
USE INTDYN_MOD         , ONLY : YYTTND   ,YYTHW0   ,YYTCTY0  ,&
 &                              YYTRCP0  ,YYTRCP9  ,YYTXYB0_PHY,YYTXYB9_PHY,YYTXYB0  ,YYTXYB9
USE EINT_MOD           , ONLY : SL_STRUCT
USE YOMSPSDT           , ONLY : YSPPT
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TYPE, TRAJ_SLAG_TYPE
USE DDH_MIX            , ONLY : TYP_DDH, SETDDH, CLEANDDH

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MF_PHYS_TYPE),INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MFPHYS_SURF_TYPE),INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
!
TYPE(FIELDS)      ,INTENT(INOUT) :: YDFIELDS
!
LOGICAL           ,INTENT(IN)    :: LDCONFX
LOGICAL           ,INTENT(IN)    :: LD_DFISTEP
LOGICAL           ,INTENT(IN)    :: LDFSTEP
LOGICAL           ,INTENT(IN)    :: LDDIAB
LOGICAL           ,INTENT(IN)    :: LDSLPHY
LOGICAL           ,INTENT(IN)    :: LDUSEPB1
INTEGER(KIND=JPIM),INTENT(IN)    :: KGPCOMP
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO
INTEGER(KIND=JPIM),INTENT(OUT)   :: KSETTLOFF(YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDTPHY
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTE
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBETADT
TYPE(SL_STRUCT),   INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFLSLP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIMSLP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSAVTEND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRSLPHY%NVTEND)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVTNDHD_DDH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
 & 2+YDMODEL%YRML_GCONF%YRDIMF%NFTHER)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFLTNDHD_DDH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGPSDT2D(YDGEOMETRY%YRDIM%NPROMA,YSPPT%YGPSDT(1)%NG2D)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEU(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCET(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEQ(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_PF(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_PFD%NLEVS,YDFIELDS%YRSURF%YSD_PFD%NDIM) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGRADH_PHY(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_MF%YRARPHY%NGRADIENTS,YDGEOMETRY%YRDIMV%NFLEVG)      ! HORIZONTAL GRADIENTS FOR PHYSICS
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDFIELDS%YRGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRGMV%NDIMGMVS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLPC(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIMPC)
REAL(KIND=JPRB),   INTENT(INOUT) :: PGFLPT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIMPT)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SB(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSP_SBD%NLEVS,YDFIELDS%YRSURF%YSP_SBD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SG(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSP_SGD%NLEVS,YDFIELDS%YRSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_RR(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_CL(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSP_CLD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VF(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VP(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VPD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VV(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VVD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VH(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VHD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VK(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VKD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VA(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VAD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VC(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VCD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VD(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_VDD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_SFL(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_SFLD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_SFO(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_SFOD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_XA(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_XAD%NLEVS,YDFIELDS%YRSURF%YSD_XAD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_DI(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_DID%NLEVS,YDFIELDS%YRSURF%YSD_DID%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_XP(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_XPD%NLEVS,YDFIELDS%YRSURF%YSD_XPD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_XP2(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRSURF%YSD_XP2D%NLEVS,YDFIELDS%YRSURF%YSD_XP2D%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEMTD(YDGEOMETRY%YRDIM%NPROMA,1:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEMTU(YDGEOMETRY%YRDIM%NPROMA,1:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTRSW(YDGEOMETRY%YRDIM%NPROMA,1:YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRMOON(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVTNDSI_DDH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
 & YDMODEL%YRML_DIAG%YRMDDH%NDIMSIGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGDEOSI(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,2)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGUEOSI(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,2)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMU0(YDGEOMETRY%YRDIM%NPROMM,0:YDMODEL%YRML_PHY_MF%YRPHY%NSORAYFR-1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMU0_MIN(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMU0_MAX(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGDEOTI(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGDEOTI2(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGUEOTI(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGUEOTI2(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOLT(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOXT(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGRPROX(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMIXP(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLUXC(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGRSURF(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB1(YDSL%NASLB1,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDFIELDS%YRGMV%YT1%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDFIELDS%YRGMV%YT1%NDIMS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEXTRA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NVEXTRDYN)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGMVTNDSL_DDH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
 & 2+YDMODEL%YRML_GCONF%YRDIMF%NFTHER)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGFLTNDSL_DDH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NUMFLDS)
TYPE (TRAJ_PHYS_TYPE),INTENT(INOUT) :: PTRAJ_PHYS
TYPE (TRAJ_SLAG_TYPE),INTENT(INOUT) :: PTRAJ_SLAG
TYPE(TYP_DDH)        , INTENT(INOUT) :: YDDDH
!     ------------------------------------------------------------------
! - derivatives of orography.
REAL(KIND=JPRB) :: ZOROGL(YDGEOMETRY%YRDIM%NPROMA)                    ! cf. POROGL in CPG_GP
REAL(KIND=JPRB) :: ZOROGM(YDGEOMETRY%YRDIM%NPROMA)                    ! cf. POROGM in CPG_GP
! - other auxiliary quantities at time 0.
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPRE0F(:,:)                   
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZRCP0(:,:,:)                   
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPHIF0(:,:)                   
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPRE0(:,:)                    ! cf. PRE0 in CPG_GP
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZCTY0(:,:,:)                  
REAL(KIND=JPRB) :: ZPRE0L(YDGEOMETRY%YRDIM%NPROMA)                    ! cf. PRE0L in CPG_GP
REAL(KIND=JPRB) :: ZPRE0M(YDGEOMETRY%YRDIM%NPROMA)                    ! cf. PRE0M in CPG_GP
REAL(KIND=JPRB) :: ZNHPRE0F(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)          ! cf. PNHPRE0F in CPG_GP
REAL(KIND=JPRB) :: ZNHPRE0H(YDGEOMETRY%YRDIM%NPROMNH,0:YDGEOMETRY%YRDIMV%NFLEVG)        ! cf. PNHPRE0H in CPG_GP
REAL(KIND=JPRB) :: ZPRE0_PHY(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)        ! true pressure for physics (/= ZPRE0 in NH, = in H)
REAL(KIND=JPRB) :: ZPRE0F_PHY(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)         ! true pressure for physics (/= ZPRE0F in NH, = in H)
REAL(KIND=JPRB) :: ZRT0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)              ! cf. PRT0L in CPG_GP
REAL(KIND=JPRB) :: ZRT0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)              ! cf. PRT0M in CPG_GP
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZXYB0(:,:,:)
!REAL(KIND=JPRB) :: ZUVH0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW0%NDIM)! cf. PUVH0 in CPG_GP
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZUVH0(:,:,:)
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPHI0(:,:)                                      ! cf. PHI0 in CPG_GP
REAL(KIND=JPRB) :: ZPHI0FL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)            ! cf. PHI0FL in CPG_GP
REAL(KIND=JPRB) :: ZPHI0FM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)            ! cf. PHI0FM in CPG_GP
REAL(KIND=JPRB) :: ZGWFT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PGWFT0 in CPG_GP
REAL(KIND=JPRB) :: ZKENE0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PKENE0 in CPG_GP
REAL(KIND=JPRB) :: ZW0_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)            ! for 3Dturb or AROME
REAL(KIND=JPRB) :: ZW0L_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)           ! for 3Dturb
REAL(KIND=JPRB) :: ZW0M_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)           ! for 3Dturb
REAL(KIND=JPRB) :: ZNHX0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)              ! cf. PNHXT0 in CPG_GP
REAL(KIND=JPRB) :: ZNHY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)            ! cf. PNHYT0 in CPG_GP
REAL(KIND=JPRB) :: ZQCHA0L(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)           ! cf. PQCHA0L in CPG_GP
REAL(KIND=JPRB) :: ZQCHA0M(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)           ! cf. PQCHA0M in CPG_GP
REAL(KIND=JPRB) :: ZDBBC(YDGEOMETRY%YRDIM%NPROMNH)                    ! cf. PDBBC in CPG_GP
REAL(KIND=JPRB) :: ZRDPHI(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)            ! cf. PRDPHI in CPG_GP
REAL(KIND=JPRB) :: ZGWT0(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PGWT0 in CPG_GP
REAL(KIND=JPRB) :: ZGWS(YDGEOMETRY%YRDIM%NPROMNH)                     ! cf. PGWS in CPG_GP
REAL(KIND=JPRB) :: ZGWFL(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PGWFL in CPG_GP
REAL(KIND=JPRB) :: ZGWFM(YDGEOMETRY%YRDIM%NPROMNH,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PGWFM in CPG_GP
REAL(KIND=JPRB) :: ZPREHYD0F_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPREHYD0_PHY(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXYB0_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB0_PHY%NDIM)
! - adiabatic Lagrangian tendencies
REAL(KIND=JPRB) :: ZATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
! - miscellaneous NPROMA-dimensioned arrays.
REAL(KIND=JPRB) :: ZKOZO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG*YDMODEL%YRML_PHY_G%YRDPHY%NVCLIS+1)     ! cf. PKOZO in CPG_GP
REAL(KIND=JPRB) :: ZNEB(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)               ! cf. PNEB in MF_PHYS
REAL(KIND=JPRB) :: ZQLI(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)               ! cf. PQLI in CPG_GP
REAL(KIND=JPRB) :: ZQICE(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)              ! cf. PQICE in CPG_GP
REAL(KIND=JPRB) :: ZQRAIN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PQRAIN in CPG_GP
REAL(KIND=JPRB) :: ZQSNOW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PQSNOW in CPG_GP
REAL(KIND=JPRB) :: ZRH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)                ! cf. PRH in MF_PHYS
REAL(KIND=JPRB) :: ZQS(YDGEOMETRY%YRDIM%NPROMA)                       ! cf. PQS in MF_PHYS
REAL(KIND=JPRB) :: ZQSOL(YDGEOMETRY%YRDIM%NPROMA)                     ! cf. PQSOL in MF_PHYS
REAL(KIND=JPRB) :: ZDHSF(YDGEOMETRY%YRDIM%NPROMA)                     ! cf. PDHSF in CPG_DIA
REAL(KIND=JPRB) :: ZDHCV(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_DIAG%YRMDDH%NDHCVSUN)   ! cf. PDHCV in CPG_DIA
REAL(KIND=JPRB) :: ZCLCT(YDGEOMETRY%YRDIM%NPROMA)                     ! cf. PCLCT in MF_PHYS

! - quantities at time 9
REAL(KIND=JPRB) :: ZW9_PHY(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)            ! for AROME Physics
REAL(KIND=JPRB) :: ZPRE9_PHY(YDGEOMETRY%YRDIM%NPROMM9,0:YDGEOMETRY%YRDIMV%NFLEVG)       ! true pressure for physics (/= ZPRE9 in NH, = in H)
REAL(KIND=JPRB) :: ZPRE9F_PHY(YDGEOMETRY%YRDIM%NPROMM9,YDGEOMETRY%YRDIMV%NFLEVG)        ! true pressure for physics (/= ZPRE9F in NH, = in H)
REAL(KIND=JPRB) :: ZPREHYD9F_PHY(YDGEOMETRY%YRDIM%NPROMM9,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPREHYD9_PHY(YDGEOMETRY%YRDIM%NPROMM9,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXYB9_PHY(YDGEOMETRY%YRDIM%NPROMM9,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB9_PHY%NDIM)
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPRE9(:,:)                                     ! cf. PRE9 in CPG_GP
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPRE9F(:,:)                                    ! cf. PRE9F in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB) :: ZNHPRE9F(YDGEOMETRY%YRDIM%NPROMNH9,YDGEOMETRY%YRDIMV%NFLEVG)        ! cf. PNHPRE9F in CPG_GP
REAL(KIND=JPRB) :: ZNHPRE9H(YDGEOMETRY%YRDIM%NPROMNH9,0:YDGEOMETRY%YRDIMV%NFLEVG)      ! cf. PNHPRE9H in CPG_GP
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZXYB9(:,:,:)
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZRCP9(:,:,:)                                   ! cf. PRCP9 in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPHI9(:,:)                                     ! cf. PHI9 in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZPHIF9(:,:)                                    ! cf. PHIF9 in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB), POINTER, CONTIGUOUS :: ZWRL9(:,:)                                    
REAL(KIND=JPRB) :: ZGWFT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)            ! cf. PGWFT9 in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB) :: ZGWT9(YDGEOMETRY%YRDIM%NPROMNH9,YDGEOMETRY%YRDIMV%NFLEVG)           ! cf. PGWT9 in CPG_GP
REAL(KIND=JPRB) :: ZNHX9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)             ! cf. PNHXT9 in CPG_GP, was declared with NPROMA9
REAL(KIND=JPRB) :: ZNHY9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)           ! cf. PNHYT9 in CPG_GP, was declared with NPROMA9
! - other quantities.
REAL(KIND=JPRB) :: ZGPAR(YDGEOMETRY%YRDIM%NPROMM,YDMODEL%YRML_PHY_MF%YRPARAR%NGPAR+1)             ! cf. PGPAR in CPG_GP
REAL(KIND=JPRB) :: ZSLBUF1AU(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)        ! local version of SLBUF1
REAL(KIND=JPRB) :: ZLSM(YDGEOMETRY%YRDIM%NPROMA)                      ! cf. PLSM in CPG_DYN
REAL(KIND=JPRB) :: ZTSOL(YDGEOMETRY%YRDIM%NPROMA)                     ! cf. PTSOL in CPG_DYN

! Terms from prognostic TKE and TTE equations for ALARO DDH.
REAL(KIND=JPRB) :: ZFTCNS(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,6)

REAL(KIND=JPRB), TARGET :: ZAUX3D(0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_DIAG%YRMDDH%NDHIDH,YDDDH%NFIELDS3D_AUTO)
REAL(KIND=JPRB), TARGET :: ZAUX2D(YDMODEL%YRML_DIAG%YRMDDH%NDHIDH,YDDDH%NFIELDS2D_AUTO)
REAL(KIND=JPRB), TARGET :: ZAUXSM(YDGEOMETRY%YRDIM%NPROMA,1,YDGEOMETRY%YRDIMV%NFLEVG,13)

INTEGER(KIND=JPIM) :: IDDHI(YDGEOMETRY%YRDIM%NPROMA)                  ! cf. KDDHI in CPG_GP

INTEGER(KIND=JPIM) :: JROF, JFLD
INTEGER(KIND=JPIM) :: IEND, IENDC, IST, ISTC
INTEGER(KIND=JPIM) :: IBL
INTEGER(KIND=JPIM) :: ICEND, ICENDE
INTEGER(KIND=JPIM) :: IGL1, IGL2
INTEGER(KIND=JPIM) :: IOFF
INTEGER(KIND=JPIM) :: ISLB1GFL9,ISLB1T9,ISLB1V9,ISLB1U9,ISLB1VD9

#ifdef __INTEL_COMPILER
INTEGER(KIND=JPIM), PARAMETER :: JPREFETCH = 8
#else
INTEGER(KIND=JPIM), PARAMETER :: JPREFETCH = 0
#endif

LOGICAL :: LLGET
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "cpdysldia.intfb.h"
#include "cpg_dia.intfb.h"
#include "cpg_dyn.intfb.h"
#include "cpg_end.intfb.h"
#include "cpg_gp.intfb.h"
#include "cpg_pt_ulp.intfb.h"
#include "cp_ptrslb1.intfb.h"
#include "ec_phys_lslphy.intfb.h"
#include "gpiniddh.intfb.h"
#include "mf_phys_prep.intfb.h"
#include "mf_phys.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB, &
 & YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP, YDCSGLEG=>YDGEOMETRY%YRCSGLEG,  YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), &
 & YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), YDVSPLIP=>YDGEOMETRY%YRVSPLIP, YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, YDSPGEOM=>YDGEOMETRY%YSPGEOM, YDDYN=>YDMODEL%YRML_DYN%YRDYN, &
 & YDPTRSLB1=>YDMODEL%YRML_DYN%YRPTRSLB1,YDPTRSLB2=>YDMODEL%YRML_DYN%YRPTRSLB2,YDSIMPHL=>YDMODEL%YRML_PHY_MF%YRSIMPHL, &
 & YDMDDH=>YDMODEL%YRML_DIAG%YRMDDH,YDECUCONVCA=>YDMODEL%YRML_PHY_EC%YRECUCONVCA,YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,  &
 & YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH,YGFL=>YDMODEL%YRML_GCONF%YGFL, &
 & YDPARAR=>YDMODEL%YRML_PHY_MF%YRPARAR,YDDIMF=>YDMODEL%YRML_GCONF%YRDIMF, &
 & YDSLPHY=>YDMODEL%YRML_PHY_G%YRSLPHY,YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY,YDARPHY=>YDMODEL%YRML_PHY_MF%YRARPHY)

ASSOCIATE(NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NDIMPC=>YGFL%NDIMPC, &
 & NDIMPT=>YGFL%NDIMPT, NDIMSLP=>YGFL%NDIMSLP, NUMFLDS=>YGFL%NUMFLDS, &
 & NDGENL=>YDDIM%NDGENL, NPROMA=>YDDIM%NPROMA, NPROMA9=>YDDIM%NPROMA9, &
 & NPROMM=>YDDIM%NPROMM, &
 & NPROMM9=>YDDIM%NPROMM9, NPROMNH=>YDDIM%NPROMNH, NPROMNH9=>YDDIM%NPROMNH9, &
 & NFTHER=>YDDIMF%NFTHER, &
 & NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA, &
 & NTSSG=>YDDPHY%NTSSG, NVCLIS=>YDDPHY%NVCLIS, NVEXTRDYN=>YDDPHY%NVEXTRDYN, &
 & NCURRENT_ITER=>YDDYN%NCURRENT_ITER, NSITER=>YDDYN%NSITER, SLHDA=>YDDYN%SLHDA, &
 & SLHDD0=>YDDYN%SLHDD0, &
 & RCUCONVCA=>YDECUCONVCA%RCUCONVCA, RNLCONVCA=>YDECUCONVCA%RNLCONVCA, &
 & LCUCONV_CA=>YDECUCONVCA%LCUCONV_CA, &
 & NGPTOT=>YDGEM%NGPTOT, &
 & GM=>YDGSGEOM%GM, NGPLAT=>YDGSGEOM%NGPLAT, &
 & NDIMGMV=>YDFIELDS%YRGMV%NDIMGMV, NDIMGMVS=>YDFIELDS%YRGMV%NDIMGMVS, YT0=>YDFIELDS%YRGMV%YT0, &
 & YT1=>YDFIELDS%YRGMV%YT1, &
 & LSDDH=>YDLDDH%LSDDH, LFLEXDIA=>YDLDDH%LFLEXDIA, LDDH_OMP=>YDLDDH%LDDH_OMP, &
 & NDHCVSUN=>YDMDDH%NDHCVSUN, NDIMSIGMV=>YDMDDH%NDIMSIGMV, NDHIDH=>YDMDDH%NDHIDH, &
 & MSLB1GFLP9=>YDPTRSLB1%MSLB1GFLP9, MSLB1SP9=>YDPTRSLB1%MSLB1SP9, &
 & MSLB1TP9=>YDPTRSLB1%MSLB1TP9, MSLB1UP9=>YDPTRSLB1%MSLB1UP9, &
 & MSLB1VP9=>YDPTRSLB1%MSLB1VP9, NFLDSLB1=>YDPTRSLB1%NFLDSLB1, &
 & MSLB2VVEL=>YDPTRSLB2%MSLB2VVEL, NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & NVTEND=>YDSLPHY%NVTEND, &
 & YSD_PFD=>YDFIELDS%YRSURF%YSD_PFD, YSD_SFLD=>YDFIELDS%YRSURF%YSD_SFLD, &
 & YSD_SFOD=>YDFIELDS%YRSURF%YSD_SFOD, YSD_VAD=>YDFIELDS%YRSURF%YSD_VAD, YSD_VCD=>YDFIELDS%YRSURF%YSD_VCD, &
 & YSD_VDD=>YDFIELDS%YRSURF%YSD_VDD, YSD_VF=>YDFIELDS%YRSURF%YSD_VF, YSD_VFD=>YDFIELDS%YRSURF%YSD_VFD, &
 & YSD_VHD=>YDFIELDS%YRSURF%YSD_VHD, YSD_VKD=>YDFIELDS%YRSURF%YSD_VKD, &
 & YSD_VPD=>YDFIELDS%YRSURF%YSD_VPD, YSD_VVD=>YDFIELDS%YRSURF%YSD_VVD, &
 & YSD_XAD=>YDFIELDS%YRSURF%YSD_XAD, YSP_RR=>YDFIELDS%YRSURF%YSP_RR, YSP_RRD=>YDFIELDS%YRSURF%YSP_RRD, &
 & YSP_CL=>YDFIELDS%YRSURF%YSP_CL, YSP_CLD=>YDFIELDS%YRSURF%YSP_CLD, &
 & YSP_SBD=>YDFIELDS%YRSURF%YSP_SBD, YSP_SGD=>YDFIELDS%YRSURF%YSP_SGD, &
 & YSD_XPD=>YDFIELDS%YRSURF%YSD_XPD, YSD_XP2D=>YDFIELDS%YRSURF%YSD_XP2D,&
 & LSIMPH=>YDSIMPHL%LSIMPH, &
 & NSORAYFR=>YDPHY%NSORAYFR, LMPHYS=>YDPHY%LMPHYS)
!     ------------------------------------------------------------------

!*       3.    READ BUFFERS, COMPUTE AUXILIARY QUANTITIES.
!              -------------------------------------------

ZPHIF0             => YDCPG_DYN0%PHIF
ZRCP0              => YDCPG_DYN0%RCP%ZVIEW
ZPRE0F             => YDCPG_DYN0%PREF
ZCTY0 (1:,0:,1:)   => YDCPG_DYN0%CTY%ZVIEW
ZPHI0 (1:, 0:)     => YDCPG_DYN0%PHI
ZPRE0 (1:, 0:)     => YDCPG_DYN0%PRE
ZXYB0              => YDCPG_DYN0%XYB%ZVIEW
ZUVH0 (1:, 0:, 1:) => YDCPG_DYN0%UVH%ZVIEW

ZPHIF9             => YDCPG_DYN9%PHIF
ZRCP9              => YDCPG_DYN9%RCP%ZVIEW
ZPRE9F             => YDCPG_DYN9%PREF
ZWRL9              => YDCPG_DYN9%WRL
ZPHI9 (1:, 0:)     => YDCPG_DYN9%PHI
ZPRE9 (1:, 0:)     => YDCPG_DYN9%PRE
ZXYB9              => YDCPG_DYN9%XYB%ZVIEW


ZGPAR(:,:)=0.0_JPRB

IGL1=1
IGL2=NDGENL
IST=1
ISTC=1

IOFF=KSTGLO
IBL=(KSTGLO-1)/NPROMA+1
ICEND=MIN(NPROMA,KGPCOMP-KSTGLO+1)
ICENDE=MIN(NPROMA,NGPTOT-KSTGLO+1)
IEND=ICEND
IENDC=ICENDE

CALL CPG_GP(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDMF_PHYS_SURF,YDVARS,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & YDMODEL,YDFIELDS,LD_DFISTEP,IST,IEND,ISTC,IENDC,IBL,KSTGLO,LDFSTEP,&
 & LDDIAB,PDT,PTE,&
 & KIBL,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,PGMV,PGMVS,&
 & PGMVTNDSL_DDH,PGFLTNDSL_DDH,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & ZOROGL,ZOROGM,&
 & ZPRE0,ZPRE0L,ZPRE0M,ZPRE0F,ZNHPRE0F,ZNHPRE0H,ZUVH0,&
 & ZPHI0,ZPHIF0,ZPHI0FL,ZPHI0FM,&
 & ZRCP0,ZCTY0,ZGWFT0,ZKENE0,ZRT0L,ZRT0M,&
 & ZPRE9,ZPRE9F,ZNHPRE9F,ZNHPRE9H,&
 & ZPHI9,ZPHIF9,ZRCP9,ZGWFT9,&
 & ZGPAR,PB2,&
 & PGMVT1,PGMVT1S,PGFLT1,&
 & ZKOZO,ZQS,ZQICE,ZQLI,ZQRAIN,ZQSNOW,&
 & ZATND,ZDBBC,ZRDPHI,ZGWT0,ZGWT9,ZGWS,ZGWFL,ZGWFM,&
 & ZNHX0,ZNHX9,ZNHY0,ZNHY9,ZQCHA0L,ZQCHA0M,PEXTRA,YDDDH)

!     ------------------------------------------------------------------

!*       4.    PHYSICS + DIAGNOSTICS
!              ---------------------

!*       4.2    Sets-up some quantities for DDH.
!               A subset of these quantities may be modified by the MF-physics.

IF (LSDDH) THEN
  IF (NCURRENT_ITER == 0) CALL GPINIDDH(YDGEOMETRY,YDMDDH,IST,IEND,IDDHI,ZDHSF,ZDHCV,ZNEB,ZCLCT,KSTGLO)
  IF (LFLEXDIA.AND.LDDH_OMP) THEN
    CALL SETDDH(YDMDDH,YDDDH,IST,IEND,NPROMA,NFLEVG,NCURRENT_ITER,ZDHSF,IDDHI,ZAUX3D,ZAUX2D,ZAUXSM)
  ENDIF
ENDIF

!*       4.1    Fill ZSLBUF1AU.

IF (LDUSEPB1) THEN
  DO JFLD=1,NFLDSLB1
    ZSLBUF1AU(1:ICEND,JFLD)=0.0_JPRB
  ENDDO
ENDIF

!     ------------------------------------------------------------------
! -- need probably a test on LEPHYS here.

!*       4.3    ECMWF-PHYSICS.

!*       4.3.1   Call ECMWF unlagged physics.

! CALL EC_PHYS(...) to plug here.

!*       4.3.2   Call ECMWF split physics.

!     ------------------------------------------------------------------

IF (LDSLPHY.AND.((LPC_FULL.AND.(NCURRENT_ITER == NSITER)).OR.(.NOT.LPC_FULL))) THEN
  CALL EC_PHYS_LSLPHY(&
   & YDGEOMETRY,YDSLPHY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,IST,IEND,LDFSTEP,PDTPHY,&
   & ZSLBUF1AU(1,MSLB1UP9),ZSLBUF1AU(1,MSLB1VP9),&
   & ZSLBUF1AU(1,MSLB1TP9),ZSLBUF1AU(1,MSLB1GFLP9),&
   & PSAVTEND,PGFLSLP)
ENDIF

IF (LCUCONV_CA) THEN
! Quick fix. The arrays should rather be dimensionned with (nproma,ngpblks). REK
  YDMF_PHYS%CUCONVCA(1:ICEND)=RCUCONVCA(IOFF:IOFF+ICEND-1)
  YDMF_PHYS%NLCONVCA(1:ICEND)=RNLCONVCA(IOFF:IOFF+ICEND-1)
ENDIF
!     ------------------------------------------------------------------

!*       4.4    MF-PHYSICS.

IF( (LMPHYS.OR.LSIMPH) .AND. (NCURRENT_ITER == 0) ) THEN  

!*       4.4.1   Call MF unlagged physics (predictor only if PC scheme).

  CALL MF_PHYS_PREP(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDFIELDS%YRGMV,&
   & YDMODEL%YRML_PHY_MF%YRARPHY,IST,IEND,KIBL,ZOROGL,ZOROGM,&
   & PGMV,ZRCP0(:,:,YYTRCP0%M_R),ZRCP9(:,:,YYTRCP9%M_CP),&
   & ZGWFT0,ZGWFT9,ZGWFL,ZGWFM,&
   & ZNHPRE0F,ZNHPRE9F,ZNHPRE0H,ZNHPRE9H,&
   & ZPRE0F,ZPRE9F,ZPRE0,ZPRE9,&
   & ZXYB0,ZXYB9,&
   & ZW0_PHY,ZW9_PHY,ZW0L_PHY,ZW0M_PHY,ZPRE0F_PHY,ZPRE9F_PHY,ZPRE0_PHY,ZPRE9_PHY,&
   & ZPREHYD0F_PHY,ZPREHYD9F_PHY,ZPREHYD0_PHY,ZPREHYD9_PHY,&
   & ZXYB0_PHY,ZXYB9_PHY)

  CALL MF_PHYS(YDGEOMETRY,YDMF_PHYS,YDCPG_DYN0,YDCPG_DYN9,YDMF_PHYS_SURF,YDVARS,YDFIELDS%YRGMV,YDFIELDS%YRSURF,YDFIELDS%YRCFU,YDFIELDS%YRXFU,YDMODEL,&
   & IBL,KGPCOMP,IST,IEND,IGL1,IGL2,KSTGLO,&
   & LDCONFX,PDTPHY,&
   & KIBL,ZOROGL,ZOROGM,&
   & PGMV,PGMVS,PGFL,&
   & ZW0_PHY,ZW0L_PHY,ZW0M_PHY,ZRCP0,ZPHI0,ZPHIF0,&
   & ZPREHYD0_PHY,ZPREHYD0F_PHY,ZPRE0_PHY,ZPRE0F_PHY,ZXYB0_PHY,&
   & ZW9_PHY,ZRCP9,ZPHI9,ZPHIF9,&
   & ZPREHYD9_PHY,ZPREHYD9F_PHY,ZPRE9_PHY,ZPRE9F_PHY,ZXYB9_PHY,&
   & ZKOZO,PGPSDT2D,PGRADH_PHY,&
   & PFORCEU,PFORCEV,PFORCET,PFORCEQ, &
   & ZCTY0,&
   & ZSLBUF1AU,PB2,&
   & PGMVT1,PGMVT1S,PGFLT1,&
   & PEMTD,PEMTU,PTRSW,PRMOON,ZGPAR,&
   & PGDEOSI,PGUEOSI,PGMU0,PGMU0_MIN,PGMU0_MAX,&
   & PGDEOTI,PGDEOTI2,PGUEOTI,PGUEOTI2,PGEOLT,PGEOXT,&
   & PGRPROX,PGMIXP,PGFLUXC,PGRSURF,&
   & ZDHSF,ZCLCT,&
   & ZNEB,&
   & ZQICE,ZQLI,ZQS,&
   & ZRH,&
   & ZQSOL,&
   & PTRAJ_PHYS,YDDDH,ZFTCNS)

ENDIF

!*       4.4.2   Store phy. tends.

IF ( LPC_FULL.AND.(.NOT.LPC_CHEAP).AND.LSLAG.AND.(LMPHYS.OR.LSIMPH) ) THEN

  CALL CP_PTRSLB1(YDDYN,YDPTRSLB1,ISLB1U9,ISLB1V9,ISLB1T9,ISLB1VD9,ISLB1GFL9)

  ! * currently valid only for SL2TL advection scheme.
  !   Remarks KY:
  !   - PC scheme with Eulerian scheme: physics is passed differently
  !     from predictor to corrector step, and no call of CPG_PT_ULP is done.
  !   - LPC_CHEAP: physics is passed differently from predictor to
  !     corrector step (in LAPINEB), and no call of CPG_PT_ULP is done.
  !   - pressure departure variable: its diabatic tendency is currently
  !     assumed to be zero and it is ignored.
  LLGET=(NCURRENT_ITER > 0)
  CALL CPG_PT_ULP(YDGEOMETRY,YDFIELDS%YRGMV,YGFL,IST,IEND,LLGET,NFLSA,NFLEN,&
   & PTENDU=ZSLBUF1AU(1,ISLB1U9),&
   & PTENDV=ZSLBUF1AU(1,ISLB1V9),&
   & PTENDT=ZSLBUF1AU(1,ISLB1T9),&
   & PTENDVD=ZSLBUF1AU(1,ISLB1VD9),&
   & PTENDGFL=ZSLBUF1AU(1,ISLB1GFL9),&
   & PTENDSP=ZSLBUF1AU(1,MSLB1SP9),&
   & PGFLPT=PGFLPT,&
   & PGFLT1=PGFLT1,PGFL=PGFL,&
   & PGMV=PGMV,PGMVS=PGMVS)

ENDIF

!     ------------------------------------------------------------------

!*       4.5    DIAGNOSTICS.
!*              - for dynamics, uses outputs of CPG_GP (MF and ECMWF).
!*              - for physics, uses outputs of MF_PHYS (MF only).

IF (NCURRENT_ITER == 0) THEN
  CALL CPG_DIA(YDGEOMETRY,YDMF_PHYS,YDCPG_DYN0,YDCPG_DYN9,YDMF_PHYS_SURF,YDVARS,YDFIELDS%YRGMV,YDFIELDS%YRSURF,YDFIELDS%YRCFU,YDFIELDS%YRXFU,YDMODEL,IST,IEND,ISTC,IENDC,KSTGLO,IDDHI,&
   & LDCONFX,LDDIAB,LDFSTEP,LD_DFISTEP,&
   & KIBL,ZOROGL,ZOROGM,&
   & PGMV,PGFL,&
   & ZPRE0,ZPRE0F,ZXYB0,&
   & ZPHI0,ZPHIF0,ZPHI0FL,ZPHI0FM,ZRCP0,&
   & ZCTY0,PB2(1,MSLB2VVEL),ZKENE0,&
   & ZRT0L,ZRT0M,ZPRE0L,ZPRE0M,&
   & ZNHPRE0F,ZNHPRE0H,ZQCHA0L,ZQCHA0M,&
   & ZDHSF,&
   & ZCLCT,&
   & ZNEB,&
   & ZQICE,ZQLI,ZQRAIN,ZQSNOW,ZQS,PSD_XA,&
   & ZRH,&
   & ZGPAR,&
   & PSD_PF,&
   & PGMVTNDSI_DDH,PGMVTNDHD_DDH,PGFLTNDHD_DDH,ZATND,&
   & ZDHCV,&
   & YDDDH,ZFTCNS)
ENDIF

IF (LSLDIA .AND. (NCURRENT_ITER == NSITER)) THEN
  ! * Calculate and print SL dynamics diagnostics.
  CALL CPDYSLDIA(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDDPHY,YDMODEL%YRML_GCONF,NPROMA,LDFSTEP,IST,IEND,NFLEVG,&
   & KIBL,&
   & PGMVS(1,YT0%MSP),ZXYB0(:,:,YYTXYB0%M_DELP),ZPRE0F,&
   & PGMV(1,1,YT0%MT),PGFL,PEXTRA)
ENDIF

!     ------------------------------------------------------------------

!*       5.    DYNAMICS.
!              ---------

!*       5.1   Call dynamics.

IF (YSD_VF%YLSM%LSET) THEN
  ZLSM(IST:IEND)=PSD_VF(IST:IEND,YSD_VF%YLSM%MP)
ELSE
  ZLSM(IST:IEND)=0.0_JPRB
ENDIF
IF (YSP_RR%YT%LSET) THEN
  ZTSOL(IST:IEND)=PSP_RR(IST:IEND,YSP_RR%YT%MP0)
ELSE
  ZTSOL(IST:IEND)=PGMV(IST:IEND,NFLEVG,YT0%MT)
ENDIF

CALL CPG_DYN(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDMF_PHYS_SURF,YDVARS,YDFIELDS%YRGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & YDMODEL,IST,IEND,PBETADT,PDT,&
 & SLHDA(IOFF),SLHDD0(IOFF),&
 & KIBL,ZOROGL,ZOROGM,&
 & ZPRE0,ZXYB0(:,:,YYTXYB0%M_RDELP),ZUVH0,&
 & ZPHI0,ZPHIF0,ZRCP0,&
 & ZCTY0,ZKENE0,&
 & ZLSM,ZTSOL,ZQSOL,&
 & ZATND,ZDBBC,ZRDPHI,ZGWT0,ZGWT9,&
 & ZNHX0,ZNHX9,ZNHY0,ZNHY9,&
 & PGFL,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & KSETTLOFF,PGFLPC,ZGWS,PGMV,PGMVS,&
 & ZSLBUF1AU,PB2,PGMVT1,PGMVT1S,PGFLT1,PGMVTNDSI_DDH,PTRAJ_SLAG,ZWRL9)

!*       5.2   Update PB1, first fields with prefetch (Intel only)

IF (LDUSEPB1) THEN
#ifdef __INTEL_COMPILER
  ! optim: prefetch does not help on nslcore, prefer to prefetch 1 level
  JFLD = MIN(NFLDSLB1,JPREFETCH)
  DO JROF=KSTGLO,KSTGLO+ICEND-1,4
    CALL MM_PREFETCH(PB1(YDSL%NSLCORE(JROF),JFLD),3)
  ENDDO
#endif

  DO JFLD=1,NFLDSLB1-JPREFETCH
    !DIR$ NEXTSCALAR
    !DIR$ IVDEP
    !DIR$ NOINTERCHANGE
    !DIR$ LOOP COUNT AVG(16)
    DO JROF=KSTGLO,KSTGLO+ICEND-1
#ifdef __INTEL_COMPILER
      ! Prefetch next field from PB1 to L2, same point
      CALL MM_PREFETCH(PB1(YDSL%NSLCORE(JROF),JFLD+JPREFETCH),3)
#endif
      PB1(YDSL%NSLCORE(JROF),JFLD)=ZSLBUF1AU(JROF-KSTGLO+1,JFLD)
    ENDDO
  ENDDO

  DO JFLD=MAX(NFLDSLB1-JPREFETCH+1,1),NFLDSLB1
    !DIR$ IVDEP
    !DIR$ LOOP COUNT AVG(16)
    DO JROF=KSTGLO,KSTGLO+ICEND-1
      PB1(YDSL%NSLCORE(JROF),JFLD)=ZSLBUF1AU(JROF-KSTGLO+1,JFLD)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*       6.    FINAL PART OF NON-LAGGED GRID-POINT COMPUTATIONS.
!              -------------------------------------------------

CALL CPG_END(YDGEOMETRY,YDFIELDS%YRGMV,YDFIELDS%YRSURF,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_GCONF,YDDYN,YDMODEL%YRML_PHY_MF,&
 & IST,IEND,ISTC,IENDC,IBL,KSTGLO,LDCONFX,LDDIAB,&
 & GM(1),ZQS,PSP_RR,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,ZGPAR,PGMV,PGMVS,PTRAJ_PHYS)

!     ------------------------------------------------------------------

!     ------------------------------------------------------------------

!*       7.    DDH CLEANING.
!              -------------------------------------------------

IF (LSDDH.AND.LFLEXDIA.AND.LDDH_OMP) THEN
    CALL CLEANDDH(YDMODEL%YRML_DIAG%YRTDDH,YDDDH,KSTGLO,NCURRENT_ITER)
ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG',1,ZHOOK_HANDLE)
END SUBROUTINE CPG
