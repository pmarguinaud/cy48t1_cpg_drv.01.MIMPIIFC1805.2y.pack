SUBROUTINE CPG2AD(YDGEOMETRY,YDGMV,YDML_GCONF,YDML_DYN,CDCONF,KNUMB,&
 & PB1,PB2,PGMV,PGMVS,&
 & KIBL,&
 & PUT0,PVT0,PUT0L,PVT0L,PDIVT0,PVORT0,&
 & PSPT0,PSPT0L,PSPT0M,&
 & PUT9,PVT9,PDIVT9,&
 & PSPT9,PSPT9L,PSPT9M,&
 & PUT1,PVT1,PSPT1,&
 & PUT5,PVT5,PUT5L,PVT5L,PDIVT5,PVORT5,&
 & PSPT5,PSPT5L,PSPT5M)  

!**** *CPG2AD* - Grid point calculations, 2-D model - adjoint.

!     Purpose.
!     --------
!     adjoint grid point calculations in dynamics.

!**   Interface.
!     ----------
!        *CALL* *CPG2AD(...)

!        Explicit arguments :
!        --------------------

!        CDCONF    : configuration of work                       (I)
!        KNUMB     : number of elements of arrays for which
!                    computations are performed (MP version)     (I)
!        PB1       : Buffer for interpolations                   (I)
!        PB2       : Buffer for t+dt quantities                  (I)
!        KIBL      : index into YRGSGEOM instance in YDGEOMETRY  (I)
!        PUT0      : zonal wind time t                           (I/O)
!        PVT0      : meridian wind time t                        (I/O)
!        PUT0L     : zonal derivative of zonal wind time t       (I/O)
!        PVT0L     : zonal derivative of meridian wind time t    (I/O)
!        PDIVT0    : divergence  time t                          (I/O)
!        PVORT0    : vorticity  time t                           (I/O)
!        PSPT0     : equivalent height time t                    (I/O)
!        PSPT0L    : zonal derivative of PSPT0                   (I/O)
!        PSPT0M    : meridian derivative of PSPT0                (I/O)
!        PUT5      : zonal wind time t (trajectory)              (I/O)
!        PVT5      : meridian wind time t (trajectory)           (I/O)
!        PUT5L     : zonal deriv of zonal wind time t (traj)     (I/O)
!        PVT5L     : zonal deriv of meridian wind time t (traj)  (I/O)
!        PDIVT5    : divergence  time t (trajectory)             (I/O)
!        PVORT5    : vorticity  time t (trajectory)              (I/O)
!        PSPT5     : equivalent height time t (trajectory)       (I)
!        PSPT5L    : zonal derivative of PSPT5                   (I/O)
!        PSPT5M    : meridian derivative of PSPT5                (I/O)

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!      Is called by SCAN2MAD.

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF*
!      Original : 88-02-04

!     Modifications.
!     --------------
!      Modified 01-07-11 by K. YESSAD: relaxation of thin layer hyp for SL2TL.
!      Modified 01-08-30 by K. YESSAD: pruning and some other cleanings.
!      Modified 03-2002  J.Vivoda  interface to SC2...9 routines
!      Modified 02-09-30 by P. Smolikova (iterface to SC2...9 for d4 in NH)
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      K. Yessad Nov 2008: rationalisation of dummy argument interfaces
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      K. Yessad (July 2014): Move some variables.
!     ------------------------------------------------------------------

USE MODEL_DYNAMICS_MOD     , ONLY : MODEL_DYNAMICS_TYPE
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE YOMGMV                 , ONLY : TGMV
USE PARKIND1               , ONLY : JPIM, JPRB
USE YOMHOOK                , ONLY : LHOOK, DR_HOOK
USE YOMCT0                 , ONLY : LRSHW, LRVEQ, LSLAG, LTWOTL
USE YOMCT2                 , ONLY : NSTOP2
USE YOMCT3                 , ONLY : NSTEP
USE YOMDYNA                , ONLY : YRDYNA

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)               ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)                   ,INTENT(INOUT) :: YDGMV
TYPE(MODEL_DYNAMICS_TYPE)    ,INTENT(INOUT) :: YDML_DYN
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT) :: YDML_GCONF
CHARACTER(LEN=1)  ,INTENT(IN)    :: CDCONF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KNUMB 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB1(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB1%NFLDSLB1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB2%NFLDSLB2) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,1,YDGMV%NDIMGMV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS) 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT0(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT0(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT0L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT0L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIVT0(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVORT0(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT0(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT0L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT0M(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT9(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT9(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIVT9(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT9(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT9L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT9M(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUT1(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVT1(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSPT1(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT5L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT5L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIVT5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVORT5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSPT5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT5L(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT5M(YDGEOMETRY%YRDIM%NPROMA) 
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZSPNLT9 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZBDT(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZDUM(YDGEOMETRY%YRDIM%NPROMA)
INTEGER(KIND=JPIM) :: IEND, IENDC, IST, ISTC, JROF  
LOGICAL :: LLFSTEP, LLLSTEP, LLRSTEP
REAL(KIND=JPRB) :: ZBT, ZDT, ZTE
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "gptf2ad.intfb.h"
#include "lacdynshwad.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG2AD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 &  YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), &
 & YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), YDOROG=>YDGEOMETRY%YROROG(KIBL), YDDYN=>YDML_DYN%YRDYN, &
 & YDPTRSLB1=>YDML_DYN%YRPTRSLB1,YDPTRSLB2=>YDML_DYN%YRPTRSLB2, &
 & YDRIP=>YDML_GCONF%YRRIP)

ASSOCIATE(NPROMA=>YDDIM%NPROMA, &
 & BETADT=>YDDYN%BETADT, LIMPF=>YDDYN%LIMPF, LSIDG=>YDDYN%LSIDG, &
 & SIVP=>YDDYN%SIVP, &
 & RSTRET=>YDGEM%RSTRET, &
 & NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, &
 & MSLB1SP0=>YDPTRSLB1%MSLB1SP0, MSLB1SP9=>YDPTRSLB1%MSLB1SP9, &
 & MSLB1U0=>YDPTRSLB1%MSLB1U0, MSLB1U9=>YDPTRSLB1%MSLB1U9, &
 & MSLB1UR0=>YDPTRSLB1%MSLB1UR0, MSLB1V0=>YDPTRSLB1%MSLB1V0, &
 & MSLB1V9=>YDPTRSLB1%MSLB1V9, MSLB1VR0=>YDPTRSLB1%MSLB1VR0, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1, &
 & MSLB2URL=>YDPTRSLB2%MSLB2URL, MSLB2USI=>YDPTRSLB2%MSLB2USI, &
 & MSLB2VRL=>YDPTRSLB2%MSLB2VRL, MSLB2VSI=>YDPTRSLB2%MSLB2VSI, &
 & NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & TDT=>YDRIP%TDT)
!     ------------------------------------------------------------------

!*       1.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

IF    (CDCONF == 'A')THEN
  ZDT=TDT
  ZTE=1.0_JPRB
ELSE
  ZDT=0.0_JPRB
  ZTE=0.0_JPRB
  CALL ABOR1(' CPG2AD - UNKNOWN CONFIGURATION ')
ENDIF

IF(NSTEP > 0) THEN
  LLFSTEP=.FALSE.
ELSE
  LLFSTEP=.TRUE.
ENDIF
IF(NSTEP > 0) THEN
  LLRSTEP=.FALSE.
ELSE
  LLRSTEP=.TRUE.
ENDIF
IF(NSTEP < NSTOP2-1) THEN
  LLLSTEP=.FALSE.
ELSE
  LLLSTEP=.TRUE.
ENDIF

IST =1
IEND=KNUMB
ISTC =1
IENDC=KNUMB

!     ------------------------------------------------------------------

!*       2.    INTERFACE TO GLOBAL ARRAYS (FIRST PART)
!              ---------------------------------------

!     ------------------------------------------------------------------

!*       7.    TIME FILTER (PART 1)
!              --------------------

!     ------------------------------------------------------------------

!*       6.    MAP FACTOR
!              -----------

DO JROF=IST,IEND
  PUT0  (JROF)=PUT0  (JROF)/ YDGSGEOM%GM(JROF)
  PVT0  (JROF)=PVT0  (JROF)/ YDGSGEOM%GM(JROF)
  PUT0L (JROF)=PUT0L (JROF)/(YDGSGEOM%GM(JROF)**2)
  PVT0L (JROF)=PVT0L (JROF)/(YDGSGEOM%GM(JROF)**2)
  PDIVT0(JROF)=PDIVT0(JROF)/(YDGSGEOM%GM(JROF)**2)
  PVORT0(JROF)=PVORT0(JROF)/(YDGSGEOM%GM(JROF)**2)
  PSPT0L(JROF)=PSPT0L(JROF)/ YDGSGEOM%GM(JROF)
  PSPT0M(JROF)=PSPT0M(JROF)/ YDGSGEOM%GM(JROF)
  IF (.NOT.YRDYNA%LELTRA) THEN
    PUT9  (JROF)=PUT9  (JROF)/ YDGSGEOM%GM(JROF)
    PVT9  (JROF)=PVT9  (JROF)/ YDGSGEOM%GM(JROF)
  ENDIF
  IF (.NOT.LTWOTL) THEN
    PDIVT9(JROF)=PDIVT9(JROF)/(YDGSGEOM%GM(JROF)**2)
    PSPT9L(JROF)=PSPT9L(JROF)/ YDGSGEOM%GM(JROF)
    PSPT9M(JROF)=PSPT9M(JROF)/ YDGSGEOM%GM(JROF)
  ENDIF
ENDDO
DO JROF=IST,IEND
  PUT5  (JROF)=PUT5  (JROF)* YDGSGEOM%GM(JROF)
  PVT5  (JROF)=PVT5  (JROF)* YDGSGEOM%GM(JROF)
  PUT5L (JROF)=PUT5L (JROF)*(YDGSGEOM%GM(JROF)**2)
  PVT5L (JROF)=PVT5L (JROF)*(YDGSGEOM%GM(JROF)**2)
  PDIVT5(JROF)=PDIVT5(JROF)*(YDGSGEOM%GM(JROF)**2)
  PVORT5(JROF)=PVORT5(JROF)*(YDGSGEOM%GM(JROF)**2)
  PSPT5L(JROF)=PSPT5L(JROF)* YDGSGEOM%GM(JROF)
  PSPT5M(JROF)=PSPT5M(JROF)* YDGSGEOM%GM(JROF)
ENDDO

!     ------------------------------------------------------------------

!*       3.    SHALLOW WATER MODEL.
!              --------------------

IF(LRSHW)THEN

  IF(.NOT.LSLAG) THEN

!*        3.1  EULERIAN SCHEME.

!*        3.1.2   SEMI-IMPLICIT ADJUSTMENT FOR EULERIAN SCHEME.

    ZBT=BETADT*0.5_JPRB*ZDT*ZTE
    DO JROF=IST,IEND
      IF (LSIDG) THEN
        ZBDT(JROF)=ZBT*SIVP(1)
      ELSE
        ZBDT(JROF)=ZBT*SIVP(1)*RSTRET*RSTRET/(YDGSGEOM%GM(JROF)*YDGSGEOM%GM(JROF))
      ENDIF
    ENDDO

    DO JROF=IST,IEND
      PDIVT0(JROF)=PDIVT0(JROF)+PSPT1(JROF)*ZBDT(JROF)*2.0_JPRB
      PDIVT9(JROF)=PDIVT9(JROF)-PSPT1(JROF)*ZBDT(JROF)
      PSPT0L(JROF)=PSPT0L(JROF)+PUT1(JROF)*ZBT*2.0_JPRB
      PSPT9L(JROF)=PSPT9L(JROF)-PUT1(JROF)*ZBT
      PSPT0M(JROF)=PSPT0M(JROF)+PVT1(JROF)*ZBT*2.0_JPRB
      PSPT9M(JROF)=PSPT9M(JROF)-PVT1(JROF)*ZBT
    ENDDO

    IF (LIMPF) THEN
      DO JROF=IST,IEND
        PVT0(JROF)=PVT0(JROF)-YDGSGEOM%RCORI(JROF)*PUT1(JROF)*ZBT*2.0_JPRB
        PVT9(JROF)=PVT9(JROF)+YDGSGEOM%RCORI(JROF)*PUT1(JROF)*ZBT
        PUT0(JROF)=PUT0(JROF)+YDGSGEOM%RCORI(JROF)*PVT1(JROF)*ZBT*2.0_JPRB
        PUT9(JROF)=PUT9(JROF)-YDGSGEOM%RCORI(JROF)*PVT1(JROF)*ZBT
      ENDDO
    ENDIF

!*        3.1.1   EXPLICIT STEP FOR EULERIAN SCHEME.

    DO JROF=IST,IEND
      PUT9  (JROF)=PUT9  (JROF)+PUT1(JROF)*ZTE
      PVORT0(JROF)=PVORT0(JROF)+PUT1(JROF)*ZDT*PVT5  (JROF)
      PVT0  (JROF)=PVT0  (JROF)+PUT1(JROF)*ZDT*PVORT5(JROF)
      PVT0L (JROF)=PVT0L (JROF)-PUT1(JROF)*ZDT*PVT5  (JROF)
      PVT0  (JROF)=PVT0  (JROF)-PUT1(JROF)*ZDT*PVT5L (JROF)
      PUT0L (JROF)=PUT0L (JROF)-PUT1(JROF)*ZDT*PUT5  (JROF)
      PUT0  (JROF)=PUT0  (JROF)-PUT1(JROF)*ZDT*PUT5L (JROF)
      PUT0  (JROF)=PUT0  (JROF)+&
       & PUT1(JROF)*ZDT*PUT5  (JROF)*YDCSGEOM%RATATX(JROF)  
      PVT0  (JROF)=PVT0  (JROF)+&
       & PUT1(JROF)*ZDT*PVT5  (JROF)*YDCSGEOM%RATATX(JROF)  
      PVT0  (JROF)=PVT0  (JROF)+PUT1(JROF)*ZDT*YDGSGEOM%RCORI(JROF)
      PSPT0L(JROF)=PSPT0L(JROF)-PUT1(JROF)*ZDT

      PVT9  (JROF)=PVT9  (JROF)+PVT1(JROF)*ZTE
      PDIVT0(JROF)=PDIVT0(JROF)-PVT1(JROF)*ZDT*PVT5  (JROF)
      PVT0  (JROF)=PVT0  (JROF)-PVT1(JROF)*ZDT*PDIVT5(JROF)
      PVT0L (JROF)=PVT0L (JROF)-PVT1(JROF)*ZDT*PUT5  (JROF)
      PUT0  (JROF)=PUT0  (JROF)-PVT1(JROF)*ZDT*PVT5L (JROF)
      PUT0L (JROF)=PUT0L (JROF)+PVT1(JROF)*ZDT*PVT5  (JROF)
      PVT0  (JROF)=PVT0  (JROF)+PVT1(JROF)*ZDT*PUT5L (JROF)
      PUT0  (JROF)=PUT0  (JROF)-&
       & PVT1(JROF)*ZDT*PUT5  (JROF)*YDCSGEOM%RATATH(JROF)  
      PVT0  (JROF)=PVT0  (JROF)-&
       & PVT1(JROF)*ZDT*PVT5  (JROF)*YDCSGEOM%RATATH(JROF)  
      PUT0  (JROF)=PUT0  (JROF)-PVT1(JROF)*ZDT*YDGSGEOM%RCORI(JROF)
      PSPT0M(JROF)=PSPT0M(JROF)-PVT1(JROF)*ZDT

      PSPT9 (JROF)=PSPT9 (JROF)+PSPT1(JROF)*ZTE
      PUT0  (JROF)=PUT0  (JROF)-&
       & PSPT1(JROF)*ZDT*PSPT5L(JROF)  
      PSPT0L(JROF)=PSPT0L(JROF)-PSPT1(JROF)*ZDT*PUT5  (JROF)
      PVT0  (JROF)=PVT0  (JROF)-&
       & PSPT1(JROF)*ZDT*PSPT5M(JROF)  
      PSPT0M(JROF)=PSPT0M(JROF)-PSPT1(JROF)*ZDT*PVT5  (JROF)
      PDIVT0(JROF)=PDIVT0(JROF)-&
       & PSPT1(JROF)*ZDT*PSPT5 (JROF)  
      PSPT0 (JROF)=PSPT0 (JROF)-PSPT1(JROF)*ZDT*PDIVT5(JROF)
    ENDDO

  ELSE

!*        3.2  SEMI-LAGRANGIAN SCHEME.

    CALL LACDYNSHWAD(YDGEM,YDDYN,CDCONF,NPROMA,IST,IEND,ZDT,&
     & PUT9,PVT9,PUT0,PVT0,PSPT0,PDIVT0,PSPT0L,PSPT0M,&
     & PSPT5,PDIVT5,&
     & YDGSGEOM,YDOROG%OROG,YDOROG%OROGL,YDOROG%OROGM,&
     & ZSPNLT9,&
     & PB1(1,MSLB1U9),PB1(1,MSLB1V9),PB1(1,MSLB1SP9),&
     & PB1(1,MSLB1U0),PB1(1,MSLB1V0),PB1(1,MSLB1SP0),&
     & PSPT1,&
     & PB1(1,MSLB1UR0),PB1(1,MSLB1VR0),&
     & PB2(1,MSLB2USI),PB2(1,MSLB2VSI),&
     & PB2(1,MSLB2URL),PB2(1,MSLB2VRL))

  ENDIF

!     ------------------------------------------------------------------

!*       4.    VORTICITY EQUATION MODEL.
!              -------------------------

ELSEIF(LRVEQ)THEN
  DO JROF=IST,IEND
    PSPT0 (JROF)=0.0_JPRB
    PSPT0L(JROF)=0.0_JPRB
    PSPT0M(JROF)=0.0_JPRB
    PSPT9 (JROF)=0.0_JPRB
    PSPT9L(JROF)=0.0_JPRB
    PSPT9M(JROF)=0.0_JPRB
    PUT9  (JROF)=PUT9  (JROF)+PUT1(JROF)*ZTE
    PVORT0(JROF)=PVORT0(JROF)+PUT1(JROF)*ZDT*PVT5  (JROF)
    PVT0  (JROF)=PVT0  (JROF)+PUT1(JROF)*ZDT*PVORT5(JROF)
    PVT0L (JROF)=PVT0L (JROF)-PUT1(JROF)*ZDT*PVT5  (JROF)
    PVT0  (JROF)=PVT0  (JROF)-PUT1(JROF)*ZDT*PVT5L (JROF)
    PUT0L (JROF)=PUT0L (JROF)-PUT1(JROF)*ZDT*PUT5  (JROF)
    PUT0  (JROF)=PUT0  (JROF)-PUT1(JROF)*ZDT*PUT5L (JROF)
    PVT0  (JROF)=PVT0  (JROF)+PUT1(JROF)*ZDT*YDGSGEOM%RCORI(JROF)

    PVT9  (JROF)=PVT9  (JROF)+PVT1(JROF)*ZTE
    PDIVT0(JROF)=PDIVT0(JROF)-PVT1(JROF)*ZDT*PVT5  (JROF)
    PVT0  (JROF)=PVT0  (JROF)-PVT1(JROF)*ZDT*PDIVT5(JROF)
    PVT0L (JROF)=PVT0L (JROF)-PVT1(JROF)*ZDT*PUT5  (JROF)
    PUT0  (JROF)=PUT0  (JROF)-PVT1(JROF)*ZDT*PVT5L (JROF)
    PUT0L (JROF)=PUT0L (JROF)+PVT1(JROF)*ZDT*PVT5  (JROF)
    PVT0  (JROF)=PVT0  (JROF)+PVT1(JROF)*ZDT*PUT5L (JROF)
    PUT0  (JROF)=PUT0  (JROF)-PVT1(JROF)*ZDT*YDGSGEOM%RCORI(JROF)
    PUT0  (JROF)=PUT0  (JROF)-PVT1(JROF)*ZDT*&
     & PUT5  (JROF)&
     & *YDCSGEOM%RATATH(JROF)  
    PUT0  (JROF)=PUT0  (JROF)+PUT1(JROF)*ZDT*&
     & PUT5  (JROF)&
     & *YDCSGEOM%RATATX(JROF)  
    PVT0  (JROF)=PVT0  (JROF)-PVT1(JROF)*ZDT*&
     & PVT5  (JROF)&
     & *YDCSGEOM%RATATH(JROF)  
    PVT0  (JROF)=PVT0  (JROF)+PUT1(JROF)*ZDT*&
     & PVT5  (JROF)&
     & *YDCSGEOM%RATATX(JROF)  
  ENDDO

ENDIF

!     ------------------------------------------------------------------

!*       2.    INTERFACE TO GLOBAL ARRAYS (SECOND PART)
!              ----------------------------------------

!*       2.3   MAP FACTOR

DO JROF=IST,IEND
  PUT0  (JROF)=PUT0  (JROF)* YDGSGEOM%GM(JROF)
  PVT0  (JROF)=PVT0  (JROF)* YDGSGEOM%GM(JROF)
  PUT0L (JROF)=PUT0L (JROF)*(YDGSGEOM%GM(JROF)**2)
  PVT0L (JROF)=PVT0L (JROF)*(YDGSGEOM%GM(JROF)**2)
  PDIVT0(JROF)=PDIVT0(JROF)*(YDGSGEOM%GM(JROF)**2)
  PVORT0(JROF)=PVORT0(JROF)*(YDGSGEOM%GM(JROF)**2)
  PSPT0L(JROF)=PSPT0L(JROF)* YDGSGEOM%GM(JROF)
  PSPT0M(JROF)=PSPT0M(JROF)* YDGSGEOM%GM(JROF)
  IF (.NOT.YRDYNA%LELTRA) THEN
    PUT9  (JROF)=PUT9  (JROF)* YDGSGEOM%GM(JROF)
    PVT9  (JROF)=PVT9  (JROF)* YDGSGEOM%GM(JROF)
  ENDIF
  IF (.NOT.LTWOTL) THEN
    PDIVT9(JROF)=PDIVT9(JROF)*(YDGSGEOM%GM(JROF)**2)
    PSPT9L(JROF)=PSPT9L(JROF)* YDGSGEOM%GM(JROF)
    PSPT9M(JROF)=PSPT9M(JROF)* YDGSGEOM%GM(JROF)
  ENDIF
ENDDO

!*       2.2   TIME FILTER (PART 2).

IF (.NOT.LTWOTL) THEN
  CALL GPTF2AD(YDGEOMETRY,YDGMV,YDML_GCONF,YDDYN,IST,IEND,LLRSTEP,PGMV,PGMVS,ZDUM)
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG2AD',1,ZHOOK_HANDLE)
END SUBROUTINE CPG2AD
