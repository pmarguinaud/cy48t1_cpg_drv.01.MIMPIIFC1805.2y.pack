#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG5(YDGEOMETRY,YDML_GCONF,YDML_DYN,KNUMB,PB15,PB2,PTRAJ_SLAG)

!**** *CPG5* - Grid point calculations - 3-D model.

!     Purpose.
!     --------
!           Grid point calculations in dynamics - 3-D MODEL.
!     ****  Either repeats some trajectory calculations for
!     ****  the adjoint of the semi-Lagrangian scheme - or
!     ****  reads them back from additional trajectory storage.

!**   Interface.
!     ----------
!        *CALL* *CPG5(...)

!        Explicit arguments :
!        --------------------

!        KNUMB     : number of elements of arrays for which          
!                    computations are performed (MP version)     (I)
!        PB15      : SL buffer for quantities to be interpolated (O)
!        PB2       : Buffer for t+dt quantities                  (O)
!        PTRAJ_SLAG : Trajectory from NL model                   (I)

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!        Calls RDSLTRAJ to get stored trajectory information.
!        Calls LACDYN if SL scheme.
!        Is called by SCAN2MAD

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Clive Temperton *ECMWF* (after CPG25)
!      Original : 99-10-19

!     Modifications.
!     --------------
!      C.Temperton: 01-01-29: Call to RDSLTRAJ (for case LSPRT=.T.)
!      Y.Tremolet: 01-03-29: New module for trajectory
!      K. YESSAD 04-JAN-2002: pruning (rm obsolete NVLAG=1, PCL05, PX05)
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      K.YESSAD (Jan 2004): adapt code to any value of NFLSA.
!      K. Yessad (Dec 2008): remove useless dummy arguments
!      F. Vana  13-Jan-2009: KAPPA5 for TL/AD of SLHD
!      F. Vana  28-Nov-2013 : Redesigned trajectory handling.
!      K. Yessad (July 2014): Move some variables.
!      F. Vana  13-Feb-2014: Specific buffer for KAPPAT.
!      F. Vana  17-Feb-2015: Fix for GFL fields trajectory
!      F. Vana  21-Nov-2017: Option LSLDP_CURV
!      F. Vana  July 2018: RK4 scheme for trajectory research.
!     ------------------------------------------------------------------

USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMDYNA  , ONLY : YRDYNA
USE YOMCT0   , ONLY : LSLAG, LSPRT
USE YOMVWRK  , ONLY : NTRSLTYPE
USE YOMTRAJ  , ONLY : TRAJ_SLAG_TYPE, LPRTTRAJ
USE YOMLUN   , ONLY : NULOUT

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)       ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_DYNAMICS_TYPE),INTENT(INOUT):: YDML_DYN
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT):: YDML_GCONF
INTEGER(KIND=JPIM)   ,INTENT(IN)    :: KNUMB 
REAL(KIND=JPRB)      ,INTENT(OUT)   :: PB15(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB15%NFLDSLB15) 
REAL(KIND=JPRB)      ,INTENT(OUT)   :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB2%NFLDSLB2) 
TYPE (TRAJ_SLAG_TYPE),INTENT(IN)    :: PTRAJ_SLAG

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IST, IEND, JLEV, JGFL, JJOFF, JJGFL
REAL(KIND=JPRB)    :: ZTSTEP

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG5',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP, &
 &  YDDYN=>YDML_DYN%YRDYN, &
 &  YDPTRSLB2=>YDML_DYN%YRPTRSLB2,YDRIP=>YDML_GCONF%YRRIP,YGFL=>YDML_GCONF%YGFL,YDPTRSLB15=>YDML_DYN%YRPTRSLB15)
ASSOCIATE(NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA, &
 & L2TLFF=>YDDYN%L2TLFF, LADVF=>YDDYN%LADVF, LSLHDHEAT=>YDDYN%LSLHDHEAT, &
 & MSLB1C05=>YDPTRSLB15%MSLB1C05, MSLB1C95=>YDPTRSLB15%MSLB1C95, &
 & MSLB1GFL95=>YDPTRSLB15%MSLB1GFL95, MSLB1SP95=>YDPTRSLB15%MSLB1SP95, &
 & MSLB1T05=>YDPTRSLB15%MSLB1T05, MSLB1T95=>YDPTRSLB15%MSLB1T95, &
 & MSLB1U05=>YDPTRSLB15%MSLB1U05, MSLB1U95=>YDPTRSLB15%MSLB1U95, &
 & MSLB1UR05=>YDPTRSLB15%MSLB1UR05, MSLB1V05=>YDPTRSLB15%MSLB1V05, &
 & MSLB1ZR05=>YDPTRSLB15%MSLB1ZR05, &
 & MSLB1V95=>YDPTRSLB15%MSLB1V95, MSLB1VR05=>YDPTRSLB15%MSLB1VR05, &
 & MSLB1WR05=>YDPTRSLB15%MSLB1WR05, NFLDSLB15=>YDPTRSLB15%NFLDSLB15, &
 & MSLB2KAPPA5=>YDPTRSLB2%MSLB2KAPPA5, MSLB2KAPPAT5=>YDPTRSLB2%MSLB2KAPPAT5, &
 & MSLB2Q15=>YDPTRSLB2%MSLB2Q15, MSLB2T15=>YDPTRSLB2%MSLB2T15, &
 & MSLB2U15=>YDPTRSLB2%MSLB2U15, MSLB2URL5=>YDPTRSLB2%MSLB2URL5, &
 & MSLB2V15=>YDPTRSLB2%MSLB2V15, MSLB2VRL5=>YDPTRSLB2%MSLB2VRL5, &
 & MSLB2WRL5=>YDPTRSLB2%MSLB2WRL5, NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & MSLB1UR005=>YDPTRSLB15%MSLB1UR005, MSLB1VR005=>YDPTRSLB15%MSLB1VR005, &
 & MSLB1ZR005=>YDPTRSLB15%MSLB1ZR005, MSLB1WR005=>YDPTRSLB15%MSLB1WR005, &
 & LSLDP_RK=>YDDYN%LSLDP_RK, LSLDP_CURV=>YDDYN%LSLDP_CURV,TSTEP=>YDRIP%TSTEP)
!     ------------------------------------------------------------------

!       * Preliminary fill buffers with zero.

IF ((.NOT.LSLAG).AND.(.NOT.LSPRT)) THEN
  ! This routine should not be called if LSLAG=F and LSPRT=F.
  CALL ABOR1(' There is no point to call CPG5!')
ENDIF

IST =1
IEND=KNUMB

!cdir collapse
PB15(:,:)=0.0_JPRB
!cdir collapse
PB2(:,:)=0.0_JPRB


!       * Read stored quantities from NL model

IF (NTRSLTYPE == 2) THEN

  ZTSTEP=TSTEP
  IF (ZTSTEP==0._JPRB) ZTSTEP=1._JPRB

  PB15(IST:IEND,MSLB1U95+1-NFLSA:MSLB1U95+NFLEVG-NFLSA)=PTRAJ_SLAG%PUL95(IST:IEND,1:NFLEVG)
  PB15(IST:IEND,MSLB1V95+1-NFLSA:MSLB1V95+NFLEVG-NFLSA)=PTRAJ_SLAG%PVL95(IST:IEND,1:NFLEVG)
  PB15(IST:IEND,MSLB1T95+1-NFLSA:MSLB1T95+NFLEVG-NFLSA)=PTRAJ_SLAG%PTL95(IST:IEND,1:NFLEVG)
  DO JGFL=1,NUMFLDS
    IF(YCOMP(JGFL)%LADV) THEN
      JJOFF=(NFLEN-NFLSA+1)*(YCOMP(JGFL)%MP_SL1-1)-NFLSA
      ! Find the corresponding NL field in GFL buffers (assuming %MP are not redefined)
      DO JJGFL=1,SIZE(PTRAJ_SLAG%IGFLL95)
        IF (YCOMP(JGFL)%MP == PTRAJ_SLAG%IGFLL95(JJGFL)) THEN
          PB15(IST:IEND,MSLB1GFL95+1+JJOFF:MSLB1GFL95+NFLEVG+JJOFF) =&
           & PTRAJ_SLAG%PGFLL95(IST:IEND,1:NFLEVG,JJGFL)
          EXIT  ! we are done
        ENDIF
      ENDDO
    ENDIF
  ENDDO
  PB15(IST:IEND,MSLB1C95+1-NFLSA:MSLB1C95+NFLEVG-NFLSA)=PTRAJ_SLAG%PCL95(IST:IEND,1:NFLEVG)*ZTSTEP
  PB15(IST:IEND,MSLB1U05+1-NFLSA:MSLB1U05+NFLEVG-NFLSA)=PTRAJ_SLAG%PUL05(IST:IEND,1:NFLEVG)*ZTSTEP
  PB15(IST:IEND,MSLB1V05+1-NFLSA:MSLB1V05+NFLEVG-NFLSA)=PTRAJ_SLAG%PVL05(IST:IEND,1:NFLEVG)*ZTSTEP
  PB15(IST:IEND,MSLB1T05+1-NFLSA:MSLB1T05+NFLEVG-NFLSA)=PTRAJ_SLAG%PTL05(IST:IEND,1:NFLEVG)*ZTSTEP
  IF (LADVF.AND.L2TLFF) THEN
    PB2(IST:IEND,MSLB2U15:MSLB2U15+NFLEVG-1)=PTRAJ_SLAG%PUT15(IST:IEND,1:NFLEVG)
    PB2(IST:IEND,MSLB2V15:MSLB2V15+NFLEVG-1)=PTRAJ_SLAG%PVT15(IST:IEND,1:NFLEVG)
  ENDIF
  IF (LSPRT) THEN
    PB2(IST:IEND,MSLB2T15:MSLB2T15+NFLEVG-1)=PTRAJ_SLAG%PTT15(IST:IEND,1:NFLEVG)
    PB2(IST:IEND,MSLB2Q15:MSLB2Q15+NFLEVG-1)=PTRAJ_SLAG%PQT15(IST:IEND,1:NFLEVG)
  ENDIF
  PB15(IST:IEND,MSLB1UR05+1-NFLSA:MSLB1UR05+NFLEVG-NFLSA)=PTRAJ_SLAG%PURL05(IST:IEND,1:NFLEVG)
  PB15(IST:IEND,MSLB1VR05+1-NFLSA:MSLB1VR05+NFLEVG-NFLSA)=PTRAJ_SLAG%PVRL05(IST:IEND,1:NFLEVG)
  IF (LSLDP_CURV) THEN
    PB15(IST:IEND,MSLB1ZR05+1-NFLSA:MSLB1ZR05+NFLEVG-NFLSA)=PTRAJ_SLAG%PZRL05(IST:IEND,1:NFLEVG)
  ENDIF
  PB15(IST:IEND,MSLB1WR05+1-NFLSA:MSLB1WR05+NFLEVG-NFLSA)=PTRAJ_SLAG%PWRL05(IST:IEND,1:NFLEVG)
  IF (LSLDP_RK) THEN
    PB15(IST:IEND,MSLB1UR005+1-NFLSA:MSLB1UR005+NFLEVG-NFLSA)=PTRAJ_SLAG%PURL005(IST:IEND,1:NFLEVG)
    PB15(IST:IEND,MSLB1VR005+1-NFLSA:MSLB1VR005+NFLEVG-NFLSA)=PTRAJ_SLAG%PVRL005(IST:IEND,1:NFLEVG)
    IF (LSLDP_CURV) THEN
      PB15(IST:IEND,MSLB1ZR005+1-NFLSA:MSLB1ZR005+NFLEVG-NFLSA)=PTRAJ_SLAG%PZRL005(IST:IEND,1:NFLEVG)
    ENDIF
    PB15(IST:IEND,MSLB1WR005+1-NFLSA:MSLB1WR005+NFLEVG-NFLSA)=PTRAJ_SLAG%PWRL005(IST:IEND,1:NFLEVG)
  ENDIF
  PB2(IST:IEND,MSLB2URL5:MSLB2URL5+NFLEVG-1)=PTRAJ_SLAG%PURL5(IST:IEND,1:NFLEVG)
  PB2(IST:IEND,MSLB2VRL5:MSLB2VRL5+NFLEVG-1)=PTRAJ_SLAG%PVRL5(IST:IEND,1:NFLEVG)
  PB2(IST:IEND,MSLB2WRL5:MSLB2WRL5+NFLEVG-1)=PTRAJ_SLAG%PWRL5(IST:IEND,1:NFLEVG)
  IF (YRDYNA%LSLHD) THEN
    IF (YRDYNA%LSLHD_STATIC) THEN
      ! In case of static SLHD (= different two standard SL interpolations)
      ! KAPPA becomes trivial, hence no need to save it from NL model.
      PB2(IST:IEND,MSLB2KAPPA5:MSLB2KAPPA5+NFLEVG-1)=1._JPRB
    ELSE
      PB2(IST:IEND,MSLB2KAPPA5:MSLB2KAPPA5+NFLEVG-1)=PTRAJ_SLAG%PKAPPA5(IST:IEND,1:NFLEVG)
    ENDIF
    IF (LSLHDHEAT) THEN
      IF (YRDYNA%LSLHD_STATIC) THEN
        PB2(IST:IEND,MSLB2KAPPAT5:MSLB2KAPPAT5+NFLEVG-1)=1._JPRB
      ELSE
        PB2(IST:IEND,MSLB2KAPPAT5:MSLB2KAPPAT5+NFLEVG-1)=PTRAJ_SLAG%PKAPPAT5(IST:IEND,1:NFLEVG)
      ENDIF
    ENDIF
  ENDIF
  PB15(IST:IEND,MSLB1SP95)=PTRAJ_SLAG%PX95(IST:IEND)


  ! Copy SP95 to C05 (multiple copies):
  DO JLEV=1,NFLEVG
    PB15(IST:IEND,MSLB1C05-NFLSA+JLEV)=PB15(IST:IEND,MSLB1SP95)
  ENDDO

  IF (LPRTTRAJ.AND.PTRAJ_SLAG%LASTCHUNK) WRITE(NULOUT,*)'GREPTRAJ READ TRAJ_SLAG in CPG5'

ELSE

  CALL ABOR1(' CPG5 - Wrong value of NTRSLTYPE ')

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG5',1,ZHOOK_HANDLE)
END SUBROUTINE CPG5
