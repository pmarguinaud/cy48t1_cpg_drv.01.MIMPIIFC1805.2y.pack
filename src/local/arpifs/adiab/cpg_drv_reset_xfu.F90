SUBROUTINE CPG_DRV_RESET_XFU (YDCPG_OPTS, YDFIELDS, YDMODEL)

USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK

USE TYPE_MODEL         , ONLY : MODEL
USE FIELDS_MOD         , ONLY : FIELDS
USE YOMCT1             , ONLY : N1HIS
USE YOMCT0             , ONLY : NFRHIS  ,NHISTS  ,NHISTSMIN
USE YOMCT3             , ONLY : NSTEP
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_OPTS_TYPE
!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(CPG_OPTS_TYPE), INTENT(IN)  :: YDCPG_OPTS
TYPE(MODEL)         ,INTENT(INOUT) :: YDMODEL
!
TYPE(FIELDS)        ,INTENT(INOUT) :: YDFIELDS
!     ------------------------------------------------------------------
! - IRAZTS : CONTROLS INSTANTANEOUS FLUX RESET
INTEGER(KIND=JPIM) :: IRAZTS(NSTEP/YDFIELDS%YRXFU%NFRRAZ:NSTEP/YDFIELDS%YRXFU%NFRRAZ)
! - IHISTS : ARRAY CONTAINING TRAJECTORY TIME STEPS
INTEGER(KIND=JPIM), ALLOCATABLE :: IHISTS(:)
INTEGER(KIND=JPIM) :: JJ, IFRGST, IFRGST2, IPERIOD,IFRVISI,IFRVISI2,IFRPRECIP,IFRPRECIP2
LOGICAL :: LLMEAN, LLMLPP

REAL(KIND=JPRB)    :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "monio_t.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_DRV_RESET_XFU', 0, ZHOOK_HANDLE)
ASSOCIATE(YDRIP=>YDMODEL%YRML_GCONF%YRRIP, YDXFU=>YDFIELDS%YRXFU, YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY)
ASSOCIATE(NSTART=>YDRIP%NSTART,TSTEP=>YDRIP%TSTEP, NSTOP=>YDRIP%NSTOP, &
& LRESET=>YDXFU%LRESET, LRESET_GST=>YDXFU%LRESET_GST, LRESET_GST2=>YDXFU%LRESET_GST2, LXXGST=>YDXFU%LXXGST,                          &
& LXXGST2=>YDXFU%LXXGST2, LRESET_PRECIP=>YDXFU%LRESET_PRECIP, LRESET_PRECIP2=>YDXFU%LRESET_PRECIP2, LRESET_VISI=>YDXFU%LRESET_VISI,  &
& LRESET_VISI2=>YDXFU%LRESET_VISI2, LXXDIAGH=>YDXFU%LXXDIAGH, MEANPERIOD=>YDXFU%MEANPERIOD, NFRRAZ=>YDXFU%NFRRAZ,                    &
& NMEANSTEPS=>YDXFU%NMEANSTEPS, NXGSTPERIOD=>YDXFU%NXGSTPERIOD, NXGSTPERIOD2=>YDXFU%NXGSTPERIOD2, LXMWINDCLS=>YDXFU%LXMWINDCLS,      &
& NVISIPERIOD=>YDXFU%NVISIPERIOD, NVISIPERIOD2=>YDXFU%NVISIPERIOD2, NDPRECPERIOD=>YDPHY%NDPRECPERIOD,                                &
& NDPRECPERIOD2=>YDPHY%NDPRECPERIOD2,LDPRECIPS=>YDPHY%LDPRECIPS, LDPRECIPS2=>YDPHY%LDPRECIPS2, LXVISI=>YDXFU%LXVISI, LXVISI2=>YDXFU%LXVISI2)

!*    XFU CONTROL UPDATE

IF (.NOT.YDCPG_OPTS%LCONFX) THEN

  ! INSTANTANEOUS FLUX RESET
  CALL MONIO_T(NSTEP, YDRIP, IRAZTS(NSTEP:), YDXFU%N1RAZ, NFRRAZ, YDXFU%NRAZTS)
  LRESET=YDCPG_OPTS%LFSTEP.OR.(IRAZTS(NSTEP/NFRRAZ) == 1.AND.MOD(NSTEP,NFRRAZ) == 0)

  IF (LXMWINDCLS) THEN !WE WANT TO COMPUTE MEAN CLS WIND
    IPERIOD=MAX(0,NINT(REAL(MEANPERIOD,JPRB)/TSTEP)-1)
    ! HISTORY EVENTS FOR ATMOSPHERE
    ALLOCATE(IHISTS(NSTEP/NFRHIS:(NSTEP+1+IPERIOD)/NFRHIS))
    CALL MONIO_T(NSTEP/NFRHIS, YDRIP, IHISTS(NSTEP/NFRHIS:), N1HIS, NFRHIS, NHISTS, KN___TSMIN=NHISTSMIN&
    & )
    !IF WE SAVE HISTORY FILE THIS TIME STEP, WE REINIT MEAN CALCUL
    LLMLPP =IHISTS(NSTEP/NFRHIS) == 1.AND.MOD(NSTEP,NFRHIS) == 0
    IF(NSTART/=0.AND..NOT.YDMODEL%YRML_AOC%YRMCC%LMULTIYR)THEN
      LLMLPP = LLMLPP.AND.NSTEP/=NSTART
    ENDIF
    IF(LLMLPP) THEN
      NMEANSTEPS=0
    ENDIF
    !IF WE WILL SAVE HISTORY FILE IN THE NEXT MEANPERIOD SECONDS
    LLMEAN=.FALSE.
    DO JJ=0,IPERIOD
      IF(NSTEP+JJ+1<=NSTOP) THEN
        IF(IHISTS((NSTEP+JJ+1)/NFRHIS)==1.AND.MOD(NSTEP+JJ+1,NFRHIS) == 0) THEN
          LLMEAN=.TRUE.
        ENDIF
      ENDIF
    ENDDO
    IF (LLMEAN) THEN
      NMEANSTEPS=NMEANSTEPS+1
    ENDIF
    !IF WE RESET FLUXES WE WILL TRUNC THE MEAN PERIOD, SO NMEANSTEPS=1
    IF (NMEANSTEPS/=0 .AND. LRESET) THEN
      NMEANSTEPS=1
    ENDIF
  ENDIF

  !IF WE WANT TO COMPUTE MAX GUST OVER A DEFINED PERIOD (NXGSTPERIOD)
  IF((LXXGST.OR.LXXDIAGH) .AND. NXGSTPERIOD>0) THEN
    !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT MAX CALCULATION
    IFRGST=NINT(REAL(NXGSTPERIOD,JPRB)/TSTEP)
    LRESET_GST=.FALSE.
    IF(MOD(NSTEP,IFRGST)==0) LRESET_GST=.TRUE.
  ENDIF

  IF(LXXGST2.AND.NXGSTPERIOD2>0) THEN
    !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT MAX CALCULATION
    IFRGST2=NINT(REAL(NXGSTPERIOD2,JPRB)/TSTEP)
    LRESET_GST2=.FALSE.
    IF(MOD(NSTEP,IFRGST2)==0) LRESET_GST2=.TRUE.
  ENDIF

  IF(LXVISI .AND. NVISIPERIOD>0) THEN
    !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT 
    !CALCULATION
    IFRVISI=NINT(REAL(NVISIPERIOD,JPRB)/TSTEP)
    !WRITE(20,*),'IFRVISI=',IFRVISI,NVISIPERIOD,TSTEP,JSTEP
    LRESET_VISI=.FALSE.
    IF(MOD(NSTEP,IFRVISI)==0) LRESET_VISI=.TRUE.

  ENDIF
  IF(LXVISI2 .AND.NVISIPERIOD2>0) THEN
    !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT MAX
    !CALCULATION
    IFRVISI2=NINT(REAL(NVISIPERIOD2,JPRB)/TSTEP)
    !WRITE(20,*),'IFRVISI2=',IFRVISI2,NVISIPERIOD2,TSTEP,JSTEP
    LRESET_VISI2=.FALSE.
    IF(MOD(NSTEP,IFRVISI2)==0) LRESET_VISI2=.TRUE.
  ENDIF

  !IF WE WANT TO COMPUTE PRECIPS TYPE OVER A DEFINED PERIOD (NDPRECPERIOD)
  IF (LDPRECIPS.AND.NDPRECPERIOD>0) THEN
     !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT 
     ! PRECIPS TYPE
     IFRPRECIP=NINT(REAL(NDPRECPERIOD,JPRB)/TSTEP)
     LRESET_PRECIP=.FALSE.
    IF(MOD(NSTEP,IFRPRECIP)==0) LRESET_PRECIP=.TRUE.
  ENDIF

  !IF WE WANT TO COMPUTE PRECIPS TYPE OVER A DEFINED PERIOD (NDPRECPERIOD2)
  IF (LDPRECIPS2.AND.NDPRECPERIOD2>0) THEN
     !IF WE SAVE HISTORY OR FULLPOS FILE THIS TIME STEP, WE REINIT 
     ! PRECIPS TYPE
     IFRPRECIP2=NINT(REAL(NDPRECPERIOD2,JPRB)/TSTEP)
     LRESET_PRECIP2=.FALSE.
    IF(MOD(NSTEP,IFRPRECIP2)==0) LRESET_PRECIP2=.TRUE.
  ENDIF


ELSE

  LRESET=.TRUE.
  LRESET_GST=.TRUE.
  LRESET_GST2=.TRUE.
  LRESET_VISI=.TRUE.
  LRESET_VISI2=.TRUE.
  LRESET_PRECIP=.TRUE.
  LRESET_PRECIP2=.TRUE.

ENDIF

END ASSOCIATE
END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPG_DRV_RESET_XFU', 1, ZHOOK_HANDLE)

END SUBROUTINE CPG_DRV_RESET_XFU

