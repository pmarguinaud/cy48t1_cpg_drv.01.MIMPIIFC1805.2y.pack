SUBROUTINE CPG_DRV_SETTLOFF (YDGEOMETRY, YDMODEL, KWSETTLOFF)

USE PARKIND1, ONLY : JPIM, JPRB

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE TYPE_MODEL         , ONLY : MODEL
USE YOMCT3             , ONLY : NSTEP
USE YOMLUN             , ONLY : NULOUT
USE MPL_MODULE         , ONLY : MPL_ALLREDUCE
USE YOMMP0             , ONLY : MYPROC, NPROC
USE YOMCT0             , ONLY : LRPLANE


IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL)       ,INTENT(IN)    :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)    :: KWSETTLOFF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)

INTEGER(KIND=JPIM) :: ING
INTEGER(KIND=JPIM) :: IOFF
INTEGER(KIND=JPIM) :: ISETTLOFF(YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    :: ZPD_STAT

#include "abor1.intfb.h"

INTEGER (KIND=JPIM) :: IBL
INTEGER (KIND=JPIM) :: JLEV


IF (YDMODEL%YRML_DYN%YRDYN%LSETTLSVF.AND.NSTEP>YDMODEL%YRML_GCONF%YRRIP%NSTART.AND.YDMODEL%YRML_DYN%YRDYN%LSETFSTAT) THEN

  ISETTLOFF=0
  DO JLEV=1,YDMODEL%YRML_DYN%YRDYN%NFLEVSF
    DO IBL=1,YDGEOMETRY%YRDIM%NGPBLKS
      ISETTLOFF(JLEV)=ISETTLOFF(JLEV)+SUM(KWSETTLOFF(:,JLEV,IBL))
    ENDDO
  ENDDO
 
  IF (NPROC>1) THEN
    CALL MPL_ALLREDUCE( ISETTLOFF, 'SUM', LDREPROD=.FALSE., CDSTRING='CPG_DRV:')
  ENDIF

  IF (MYPROC == 1) THEN
    DO JLEV=1,YDMODEL%YRML_DYN%YRDYN%NFLEVSF
      WRITE(NULOUT,*) '% of pts extrapolation is off at lev:',JLEV,'is:',100.*ISETTLOFF(JLEV)/YDGEOMETRY%YRGEM%NGPTOTG
    ENDDO
  ENDIF
ENDIF

! global characteristics for LMIXETTLS scheme
! temporarily ISETTLOFF is used
IF (YDMODEL%YRML_DYN%YRDYNA%LMIXETTLS.AND.NSTEP>YDMODEL%YRML_GCONF%YRRIP%NSTART) THEN
  IF (LRPLANE) THEN

    ING = YDGEOMETRY%YRDIM%NDGUXG * YDGEOMETRY%YRDIM%NDLON
    ! accumulate characteristics for all NPROMA packets
    ISETTLOFF=0
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO IBL=1,YDGEOMETRY%YRDIM%NGPBLKS
        ISETTLOFF(JLEV)=ISETTLOFF(JLEV)+SUM(KWSETTLOFF(:,JLEV,IBL))
      ENDDO
    ENDDO
 
    ! global sum over processors
    IF (NPROC>1) THEN
      CALL MPL_ALLREDUCE( ISETTLOFF, 'SUM', LDREPROD=.FALSE., CDSTRING='CPG_DRV:')
    ENDIF

    ! compute on all processors
    IF (YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER == 0) THEN
      DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
        IOFF    = JLEV
        ZPD_STAT= REAL(ISETTLOFF(IOFF),JPRB)/REAL(ING,JPRB) * 100.0_JPRB
      ENDDO
    ENDIF

    ! write only on proc 1
    IF (MYPROC == 1) THEN
      DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
        IOFF    = JLEV
        ZPD_STAT= REAL(ISETTLOFF(IOFF),JPRB)/REAL(ING,JPRB) * 100.0_JPRB
        IF (YDMODEL%YRML_DYN%YRDYNA%LMIXETTLS_PRINT.AND.(YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER==0)) THEN
          WRITE(NULOUT,'(A,2(I5,";"),F15.8)') 'CPG_DRV STATISTICS: ', NSTEP, JLEV, ZPD_STAT
        ENDIF
      ENDDO
    ENDIF

  ELSE
    CALL ABOR1('CPG_DRV: GLOBAL GEOMETRY NOT YET CODED FOR MIXED TTL SCHEME.')
  ENDIF

ENDIF



END SUBROUTINE
