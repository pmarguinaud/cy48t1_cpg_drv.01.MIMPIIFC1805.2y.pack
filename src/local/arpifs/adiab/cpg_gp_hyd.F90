#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_HYD(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDMODEL,YDGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & KST,KEND,KSTGLO,LDUVH,LDGW,LDLDIAB,LDMPA,&
 & KIBL,POROG,POROGL,POROGM,PRE0L,PRE0M,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,PGMV,PRE0,PRE9,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & PRE0F,PXYB0,PUVH0,&
 & PHI0,PHIF0,PHI0FL,PHI0FM,PRCP0,PCTY0,PGWFT0,PKENE0,&
 & PRT0L,PRT0M,&
 & PRE9F,PXYB9,PHI9,PHIF9,PRCP9,PGWFT9,&
 & PATND)

!**** *CPG_GP_HYD* - Grid point calculations at instants t and t-dt for hydrostatic model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for hydrostatic model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_HYD(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDUVH     : compute half level winds.
!        LDGW      : compute [gw].
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.

!     INPUT/OUTPUT:
!     -------------
!        PGFL      : unified_treatment grid-point fields at t
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad RT" at full levels.
!        PRT0M     : meridian component of "grad RT" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (Apr 2018): introduce key L_RDRY_VD (ensure consistent definition of "dver" everywhere).
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
!   P. Smolikova (Sep 2020): Remove obsolete calculations.
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE CPG_DYN_TYPE_MOD,ONLY : CPG_DYN_TYPE
USE YOMGMV   , ONLY : TGMV
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE SC2PRG_MOD,ONLY : SC2PRG
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LSFORC
USE YOMCST   , ONLY : RD, RV
USE YOMDYNA  , ONLY : LGRADSP, LRUBC, L_RDRY_VD
USE YOMCVER  , ONLY : LVERTFE
USE YOMCT3   , ONLY : NSTEP
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND, YYTHW0, YYTHW9,&
 & YYTCTY0, YYTXYBDER0, YYTRCP0, YYTRCP9, YYTXYB0, YYTXYB9

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO
LOGICAL           ,INTENT(IN)    :: LDUVH
LOGICAL           ,INTENT(IN)    :: LDGW
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGL(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE0L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE0M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRE0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRE9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXYB0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUVH0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHIF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0FL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0FM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRCP0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKENE0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRT0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRT0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXYB9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB9%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHIF9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRCP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP9%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

!    ==== MISCELLANEOUS ======================
REAL(KIND=JPRB) :: ZXYBDER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYBDER0%NDIM)
REAL(KIND=JPRB) :: ZRPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR0T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZPSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGPHL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGPHM(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUVH9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW9%NDIM)
REAL(KIND=JPRB) :: ZDPHYCTY(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHT0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHT9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHXT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHXT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZDUNI(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)     ! useless input dummy arg

REAL(KIND=JPRB), POINTER :: ZT0DIV(:,:), ZT0EDOT(:,:), ZT0SGRTL(:,:), ZT0SGRTM(:,:)
REAL(KIND=JPRB), POINTER :: ZT0T(:,:), ZT0TL(:,:), ZT0TM(:,:), ZT0U(:,:)
REAL(KIND=JPRB), POINTER :: ZT0V(:,:), ZT9DIV(:,:), ZT9SGRTL(:,:), ZT9SGRTM(:,:)
REAL(KIND=JPRB), POINTER :: ZT9T(:,:), ZT9U(:,:), ZT9V(:,:)
REAL(KIND=JPRB), POINTER :: ZP1FORC(:,:), ZPGFLYPHYCTY(:,:), ZPQL(:,:), ZPQM(:,:)
REAL(KIND=JPRB), POINTER :: ZPRSPECL(:,:), ZPRSPECM(:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "gpgrxyb.intfb.h"
#include "gprcp.intfb.h"
#include "gprt.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gpgrp.intfb.h"
#include "gpxx.intfb.h"
#include "gpgw.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_GP_HYD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL),YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), &
 & YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL)
ASSOCIATE(NPROMA=>YDDIM%NPROMA,NFLEVG=>YDDIMV%NFLEVG, &
 & NDIM=>YGFL%NDIM, YQ=>YGFL%YQ,YRSPEC=>YGFL%YRSPEC, YFORC=>YGFL%YFORC, &
 & NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
 & NDIMGMV=>YDGMV%NDIMGMV,YT0=>YDGMV%YT0,YT9=>YDGMV%YT9,&
 & LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH)

CALL SC2PRG(1,YFORC(:)%MP,PGFL ,ZP1FORC)
CALL SC2PRG(YGFL%YPHYCTY%MP,PGFL,ZPGFLYPHYCTY)
CALL SC2PRG(YQ%MPL    ,PGFL    ,ZPQL)
CALL SC2PRG(YQ%MPM    ,PGFL    ,ZPQM)
CALL SC2PRG(YRSPEC%MPL,PGFL    ,ZPRSPECL)
CALL SC2PRG(YRSPEC%MPM,PGFL    ,ZPRSPECM)
CALL SC2PRG(YT0%MDIV  ,PGMV    ,ZT0DIV)
CALL SC2PRG(YT0%MEDOT ,PGMV    ,ZT0EDOT)
CALL SC2PRG(YT0%MSGRTL,PGMV    ,ZT0SGRTL)
CALL SC2PRG(YT0%MSGRTM,PGMV    ,ZT0SGRTM)
CALL SC2PRG(YT0%MT    ,PGMV    ,ZT0T)
CALL SC2PRG(YT0%MTL   ,PGMV    ,ZT0TL)
CALL SC2PRG(YT0%MTM   ,PGMV    ,ZT0TM)
CALL SC2PRG(YT0%MU    ,PGMV    ,ZT0U)
CALL SC2PRG(YT0%MV    ,PGMV    ,ZT0V)
CALL SC2PRG(YT9%MDIV  ,PGMV    ,ZT9DIV)
CALL SC2PRG(YT9%MSGRTL,PGMV    ,ZT9SGRTL)
CALL SC2PRG(YT9%MSGRTM,PGMV    ,ZT9SGRTM)
CALL SC2PRG(YT9%MT    ,PGMV    ,ZT9T)
CALL SC2PRG(YT9%MU    ,PGMV    ,ZT9U)
CALL SC2PRG(YT9%MV    ,PGMV    ,ZT9V)

!     ------------------------------------------------------------------

!*       0.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

! Fill input useless argument ZDUNI with absurd values.
ZDUNI(KST:KEND,1:NFLEVG)=-999999._JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,PRE0,PRESF=PRE0F, &
          & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
          & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
          & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZRPRE0F(JROF,JLEV)=1.0_JPRB/PRE0F(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB(NPROMA,KST,KEND,NFLEVG,.FALSE.,YDVAB,PRE0L,PRE0M,PXYB0,ZXYBDER0)

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,PCP=YDCPG_DYN0%RCP%CP,&
 & PR=YDCPG_DYN0%RCP%R,PKAP=YDCPG_DYN0%RCP%KAP)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,ZT0T,ZT0TL,ZT0TM,&
   & ZPQL,ZPQM,&
   & ZRT0,PRT0L,PRT0M,PRL=ZPRSPECL,PRM=ZPRSPECM)
ELSE
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,ZT0T,ZT0TL,ZT0TM,&
   & ZPQL,ZPQM,&
   & ZRT0,PRT0L,PRT0M)
ENDIF

! ZRDT0 is the (RT) at "t" used in definition of "dver".
IF (L_RDRY_VD) THEN
  ZRDT0(KST:KEND,1:NFLEVG)=RD*ZT0T(KST:KEND,1:NFLEVG)
ELSE
  ZRDT0(KST:KEND,1:NFLEVG)=ZRT0(KST:KEND,1:NFLEVG)
ENDIF

!*     3.1.7c COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            "omega/prehyd" and "W".

!* Mass tendencies from physics as RHS of the continuity equation.
! Specific mass tendencies (unit: s-1) are computed
! in the last physics call (previous time step in IFS).
! They are stored in GFL YPHYCTY.
IF (YGFL%YPHYCTY%LACTIVE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZDPHYCTY(JROF,JLEV)=ZPGFLYPHYCTY(JROF,JLEV)
    ENDDO
  ENDDO
ELSE
  ZDPHYCTY(:,:)=0.0_JPRB
ENDIF

CALL GPCTY(YDVFE,NPROMA,KST,KEND,NFLEVG,LRUBC,YDVAB,YDVETA,&
 & ZT0U,ZT0V,ZT0DIV,ZT0EDOT,&
 & PXYB0,PRE0L,PRE0M,ZRPRE0F,PCTY0,PDPHYCTY=ZDPHYCTY)

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,NPROMA,NFLEVG,KEND,NSTEP,KSTGLO,PRE0,PCTY0(1,1,YYTCTY0%M_DIVDP))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,KST,KEND, &
   & ZP1FORC,PRE0F,ZT0T,YDCPG_DYN0%RCP%R,PCTY0(1,0,YYTCTY0%M_EVEL),PCTY0(1,1,YYTCTY0%M_VVEL))
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

PHI0(KST:KEND,NFLEVG)=POROG(KST:KEND)
CALL GPGEO(NPROMA,KST,KEND,NFLEVG,PHI0,PHIF0,ZT0T,&
 & YDCPG_DYN0%RCP%R,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY,NPROMA,KST,KEND,NFLEVG,&
 & ZRT0,PRT0L,PRT0M,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
 & POROGL,POROGM,&
 & PHI0FL,PHI0FM,ZGPHL,ZGPHM)

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    PKENE0(JROF,JLEV)=0.5_JPRB*(ZT0U(JROF,JLEV)**2+ZT0V(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

IF (LDUVH) THEN
  CALL GPHLWI(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,PWW0=YDCPG_DYN0%UVH%WWI)
  CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,ZT0U,ZT0V,&
            & PWWI=YDCPG_DYN0%UVH%WWI,PUH=YDCPG_DYN0%UVH%UH,PVH=YDCPG_DYN0%UVH%VH)
ENDIF
IF (LDGW) THEN
  LLDER=.FALSE.
  CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,ZT0U,ZT0V,ZUS0,ZVS0)
ENDIF

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

CALL GPGRP(YDGEOMETRY,KST,KEND,&
 & ZRT0,PRT0L,PRT0M,PRE0L,PRE0M,PXYB0,ZXYBDER0,&
 & ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
 & ZPSGRTL,ZPSGRTM)

! store for next time-step
IF( LGRADSP ) THEN

  ! adjust field with filter result
  ZSGRTL(KST:KEND,1:NFLEVG)=ZPSGRTL(KST:KEND,1:NFLEVG)&
   & - (ZT9SGRTL(KST:KEND,1:NFLEVG) - ZT0SGRTL(KST:KEND,1:NFLEVG))
  ZSGRTM(KST:KEND,1:NFLEVG)=ZPSGRTM(KST:KEND,1:NFLEVG)&
   & - (ZT9SGRTM(KST:KEND,1:NFLEVG) - ZT0SGRTM(KST:KEND,1:NFLEVG))

  ! store for next time-step unfiltered fields
  ZT0SGRTL(KST:KEND,1:NFLEVG)=ZPSGRTL(KST:KEND,1:NFLEVG)
  ZT0SGRTM(KST:KEND,1:NFLEVG)=ZPSGRTM(KST:KEND,1:NFLEVG)

  ! set new values for current timestep
  ZPSGRTL(KST:KEND,1:NFLEVG)=ZSGRTL(KST:KEND,1:NFLEVG)
  ZPSGRTM(KST:KEND,1:NFLEVG)=ZSGRTM(KST:KEND,1:NFLEVG)

ENDIF

!*     3.1.13 COMPUTES TERM "NHX".

IF (LDGW) THEN
  CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
   & YDCPG_DYN0%XYB%LNPR,ZRT0,&
   & ZT0U,ZT0V,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & ZNHXT0)

!*     3.1.14 COMPUTES dver.

  IF (L_RDRY_VD) THEN
    ! dver_hyd=-(R/Rd)*(D+X+(cv/cp)(omega/prehyd))
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        ZDVER0(JROF,JLEV)=-(YDCPG_DYN0%RCP%R(JROF,JLEV)/RD)*(ZT0DIV(JROF,JLEV)&
         & +ZNHXT0(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL))
      ENDDO
    ENDDO
  ELSE
    ! dver_hyd=-(D+X+(cv/cp)(omega/prehyd))
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        ZDVER0(JROF,JLEV)=-(ZT0DIV(JROF,JLEV)&
         & +ZNHXT0(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL))
      ENDDO
    ENDDO
  ENDIF

!*     3.1.16 COMPUTES term [gw].

! Caution: must use consistent definitions of RT in ZRDT0 and ZDVER0.
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
   & POROGL,POROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZUS0,ZVS0,&
   & ZRDT0,ZDVER0,ZGWHT0,PGWFT0)
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY,YDGMV,YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_DYN%YRDYN,&
 & KST,KEND,YDGSGEOM%RCORI,&
 & YDGSGEOM%GNORDL,YDGSGEOM%GNORDM,&
 & ZPSGRTL,ZPSGRTM,&
 & PGMV,PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
 & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC))

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    PATND(JROF,JLEV,YYTTND%M_TNDT)=&
     & YDCPG_DYN0%RCP%KAP(JROF,JLEV)*ZT0T(JROF,JLEV)*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,PRE9,PXYB=PXYB9,PRESF=PRE9F)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=YDCPG_DYN9%RCP%CP,&
   & PR=YDCPG_DYN9%RCP%R)

!*     3.2.4 COMPUTE "RT".

  ! ZR0T9 is used in calculation of NHX(t-dt).
  ZR0T9(KST:KEND,1:NFLEVG)=ZT9T(KST:KEND,1:NFLEVG)*YDCPG_DYN0%RCP%R(KST:KEND,1:NFLEVG)

  ! ZRDT9 is the (RT) at "t-dt" used in the definition of "dver":
  IF (L_RDRY_VD) THEN
    ZRDT9(KST:KEND,1:NFLEVG)=RD*ZT9T(KST:KEND,1:NFLEVG)
  ELSE
    ! use of R_moist at time t to be consistent with ZR0T9 and ZDVER9.
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        ZRDT9(JROF,JLEV)=YDCPG_DYN0%RCP%R(JROF,JLEV)*ZT9T(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF(LDLDIAB.OR.LSIMPH) THEN
    PHI9(KST:KEND,NFLEVG)=POROG(KST:KEND)
    CALL GPGEO(NPROMA,KST,KEND,NFLEVG,PHI9,PHIF9,ZT9T,&
     & YDCPG_DYN9%RCP%R,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,&
     & YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  IF (LDGW) THEN
    ! * same remark as for time t half-level winds.
    ZUVH9(KST:KEND,1:NFLEVG,YYTHW9%M_WWI)=YDCPG_DYN0%UVH%WWI(KST:KEND,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,ZT9U,ZT9V,ZUVH9)

    LLDER=.FALSE.
    CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,ZT9U,ZT9V,ZUS9,ZVS9)

!*     3.2.13 COMPUTES TERM "NHX".

  ! caution: must use "R" at time t.
    CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
     & YDCPG_DYN9%XYB%LNPR,ZR0T9,&
     & ZT9U,ZT9V,ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),&
     & ZNHXT9)

!*     3.2.14 COMPUTES dver.

    IF (L_RDRY_VD) THEN
      ! dver_hyd=-(R/Rd)*(D+X+(cv/cp)(omega/prehyd))
      !  Caution: must use R, Kap and (omega/prehyd) at time t.
      DO JLEV=1,NFLEVG
        DO JROF=KST,KEND
          ZDVER9(JROF,JLEV)=-(YDCPG_DYN0%RCP%R(JROF,JLEV)/RD)*(ZT9DIV(JROF,JLEV)&
           & +ZNHXT9(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL))
        ENDDO
      ENDDO
    ELSE
      ! dver_hyd=-(D+X+(cv/cp)(omega/prehyd))
      !  Caution: must use R, Kap and (omega/prehyd) at time t.
      DO JLEV=1,NFLEVG
        DO JROF=KST,KEND
          ZDVER9(JROF,JLEV)=-(ZT9DIV(JROF,JLEV)&
           & +ZNHXT9(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL))
        ENDDO
      ENDDO
    ENDIF
  ENDIF

!*     3.2.16 COMPUTES term [gw].

  ! Caution: must use consistent definitions of RT in ZRDT9 and ZDVER9.
  LLGDWI=.FALSE.
  IF (LDGW) THEN
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
     & POROGL,POROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZUS9,ZVS9,&
     & ZRDT9,ZDVER9,ZGWHT9,PGWFT9)
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_HYD',1,ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_HYD
