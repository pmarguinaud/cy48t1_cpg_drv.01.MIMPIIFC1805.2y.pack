SUBROUTINE CPG_GP_HYD_T9 (YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDVARS, YDCPG_DYN0, YDMODEL, LDGW, LD_RDRY_VD, P_GPHL_T0, P_GPHM_T0, &
                        & YDCPG_DYN9, LDLDIAB, LDMPA)

USE PARKIND1, ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK

USE GEOMETRY_MOD,       ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD,  ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,       ONLY : CPG_DYN_TYPE
USE TYPE_MODEL,         ONLY : MODEL
USE CPG_TYPE_MOD,       ONLY : CPG_DYN_TYPE

IMPLICIT NONE

TYPE(GEOMETRY),         INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),    INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),    INTENT(IN)    :: YDCPG_OPTS
TYPE(FIELD_VARIABLES),  INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE),     INTENT(INOUT) :: YDCPG_DYN0
TYPE(MODEL),            INTENT(INOUT) :: YDMODEL
LOGICAL,                INTENT(IN)    :: LDGW
LOGICAL,                INTENT(IN)    :: LD_RDRY_VD
REAL(KIND=JPRB),        INTENT(INOUT) :: P_GPHL_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),        INTENT(INOUT) :: P_GPHM_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
TYPE(CPG_DYN_TYPE),     INTENT(INOUT) :: YDCPG_DYN9
LOGICAL,                INTENT(IN)    :: LDLDIAB
LOGICAL,                INTENT(IN)    :: LDMPA

#include "gpgeo_expl.intfb.h"
#include "gpgw.intfb.h"
#include "gphluv_expl.intfb.h"
#include "gphpre_expl.intfb.h"
#include "gprcp_expl.intfb.h"
#include "gpuvs.intfb.h"
#include "gpxx.intfb.h"

REAL (KIND=JPRB) :: Z_DVER_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHT_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXT_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_R0T_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RDT_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_US_T9 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_UVH_UH_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_UVH_VH_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_UVH_WWI_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_VS_T9 (YDGEOMETRY%YRDIM%NPROMA)

LOGICAL :: LLDER
LOGICAL :: LLGDWI

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('CPG_GP_HYD_T9', 0, ZHOOK_HANDLE)

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

CALL GPHPRE_EXPL(YDCPG_OPTS%LAPRXPK, YDCPG_OPTS%LVERTFE, YDCPG_OPTS%NDLNPR, YDCPG_OPTS%RHYDR0, YDCPG_OPTS%TOPPRES, YDMODEL%YRCST, YDGEOMETRY%YRDIM%NPROMA, YDGEOMETRY%YRDIMV%NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDGEOMETRY%YRVAB, YDCPG_DYN9%PRE, PRESF=YDCPG_DYN9%PREF,   &
& PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, PALPH=YDCPG_DYN9%XYB%ALPH, &
& PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE, PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

CALL GPRCP_EXPL (YDMODEL%YRCST, YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDGEOMETRY%YRDIMV%NFLEVG, YDVARS=YDVARS, KGFLTYP=9, &
               & PCP=YDCPG_DYN9%RCP%CP, PR=YDCPG_DYN9%RCP%R)

!*     3.2.4 COMPUTE "RT".

! ZR0T9 is used in calculation of NHX(t-dt).
Z_R0T_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)=YDVARS%T%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)*YDCPG_DYN0%RCP%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)

! ZRDT9 is the (RT) at "t-dt" used in the definition of "dver":
IF (LD_RDRY_VD) THEN
  Z_RDT_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)=YDMODEL%YRCST%RD*YDVARS%T%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)
ELSE
  ! use of R_moist at time t to be consistent with ZR0T9 and ZDVER9.
  DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_RDT_T9(JROF,JLEV)=YDCPG_DYN0%RCP%R(JROF,JLEV)*YDVARS%T%T9(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.
IF(LDLDIAB.OR.YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH) THEN
  YDCPG_DYN9%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,YDGEOMETRY%YRDIMV%NFLEVG)=YDVARS%GEOMETRY%OROG%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
  CALL GPGEO_EXPL(YDCPG_OPTS%LVERTFE,YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDGEOMETRY%YRDIMV%NFLEVG, YDCPG_DYN9%PHI, YDCPG_DYN9%PHIF, YDVARS%T%T9, &
  & YDCPG_DYN9%RCP%R, YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%ALPH, YDGEOMETRY%YRVERT_GEOM)
ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

IF (LDGW) THEN
  ! * same remark as for time t half-level winds.
  Z_UVH_WWI_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)=YDCPG_DYN0%UVH%WWI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDGEOMETRY%YRDIMV%NFLEVG)
  CALL GPHLUV_EXPL(YDGEOMETRY%YRDIMV, YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T9, YDVARS%V%T9, &
  & PWWI=Z_UVH_WWI_T9, PUH=Z_UVH_UH_T9, PVH=Z_UVH_VH_T9)

  LLDER=.FALSE.
  CALL GPUVS(YDGEOMETRY%YRDIMV%NFLEVG, YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, YDVARS%U%T9, YDVARS%V%T9, &
  & Z_US_T9, Z_VS_T9)

!*     3.2.13 COMPUTES TERM "NHX".

! caution: must use "R" at time t.
  CALL GPXX(YDCPG_OPTS%LVERTFE, YDGEOMETRY, YDGEOMETRY%YRDIMV%NFLEVG, YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, P_GPHL_T0, P_GPHM_T0, YDCPG_DYN0%PHIFL, &
  & YDCPG_DYN0%PHIFM, YDCPG_DYN9%XYB%LNPR, Z_R0T_T9, YDVARS%U%T9, YDVARS%V%T9, Z_UVH_UH_T9,                         &
  & Z_UVH_VH_T9, Z_NHXT_T9)

!*     3.2.14 COMPUTES dver.

  IF (LD_RDRY_VD) THEN
    ! dver_hyd=-(R/Rd)*(D+X+(cv/cp)(omega/prehyd))
    !  Caution: must use R, Kap and (omega/prehyd) at time t.
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        Z_DVER_T9(JROF,JLEV)=-(YDCPG_DYN0%RCP%R(JROF,JLEV)/YDMODEL%YRCST%RD)*(YDVARS%DIV%T9(JROF,JLEV)&
         & +Z_NHXT_T9(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*YDCPG_DYN0%CTY%VVEL(JROF,JLEV))
      ENDDO
    ENDDO
  ELSE
    ! dver_hyd=-(D+X+(cv/cp)(omega/prehyd))
    !  Caution: must use R, Kap and (omega/prehyd) at time t.
    DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        Z_DVER_T9(JROF,JLEV)=-(YDVARS%DIV%T9(JROF,JLEV)&
         & +Z_NHXT_T9(JROF,JLEV)+(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))*YDCPG_DYN0%CTY%VVEL(JROF,JLEV))
      ENDDO
    ENDDO
  ENDIF
ENDIF

!*     3.2.16 COMPUTES term [gw].

! Caution: must use consistent definitions of RT in ZRDT9 and ZDVER9.
LLGDWI=.FALSE.
IF (LDGW) THEN
  CALL GPGW(YDCPG_OPTS%LNHDYN, YDCPG_OPTS%LVERTFE, YDCPG_OPTS%LVFE_GW, YDGEOMETRY, YDGEOMETRY%YRDIMV%NFLEVG, YDGEOMETRY%YRDIM%NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LDMPA, LLGDWI, YDCPG_DYN0%OROGL, &
  & YDCPG_DYN0%OROGM, YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%ALPH, Z_US_T9, Z_VS_T9, Z_RDT_T9,                  &
  & Z_DVER_T9, Z_GWHT_T9, YDCPG_DYN9%GWFT)
ENDIF

Z_GWHT_T9(:,:)=0.0_JPRB

IF (LHOOK) CALL DR_HOOK('CPG_GP_HYD_T9', 1, ZHOOK_HANDLE)

END SUBROUTINE
