#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHEE(YDGEOMETRY,YDCPG_DIM,YDTMP,YDVARS,YDCPG_DYN0,YDCPG_DYN9,YDMODEL,YDGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & LDGPXX,LDLDIAB,LDMPA,&
 & POROG,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & PATND)

!**** *CPG_GP_NHEE* - Grid point calculations at instants t and t-dt for NHEE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHEE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHEE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.

!     INPUT/OUTPUT:
!     -------------
!        PGFL      : unified_treatment grid-point fields at t
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad RT" at full levels.
!        PRT0M     : meridian component of "grad RT" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D (gw)_surf / Dt]_adiab.
!        PRDPHI    : pre/(R T prehyd [Delta log(prehyd)]) at t.
!                    "R" is the version of R (may be Rdry or Rmoist) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PNHYT0    : term 'NHY' at t.
!        PNHYT9    : term 'NHY' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (Apr 2018): introduce key L_RDRY_VD (ensure consistent definition of "dver" everywhere).
!      K. Yessad (July 2018): alternate treatment of Laplacian term in gw and dver equations.
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,ONLY : CPG_DYN_TYPE, CPG_GP_TMP_TYPE
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE
USE YOMGMV   , ONLY : TGMV
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE SC2PRG_MOD,ONLY : SC2PRG
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC
USE YOMCST   , ONLY : RD, RV
USE YOMDYNA  , ONLY : NVDVAR, LGWADV, LRDBBC, LGRADSP, LRUBC, L_RDRY_VD, LNHEE_SVDLAPL_FIRST, &
                    & LNHEE_REFINE_GRP, LNHEE_REFINE_PREH_BBC, LVEREGINT
USE YOMCT3   , ONLY : NSTEP
USE YOMCVER  , ONLY : LVERTFE,LVFE_LAPL_BC,LVFE_GW
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND, YYTHW0, YYTHW9,&
 & YYTCTY0, YYTXYBDER0, YYTRCP0, YYTRCP9, YYTXYB0, YYTXYB9

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_GP_TMP_TYPE),INTENT(INOUT) :: YDTMP
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV



LOGICAL           ,INTENT(IN)    :: LDGPXX
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA

REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

!    ==== MISCELLANEOUS ======================

REAL(KIND=JPRB) :: ZRCV

REAL(KIND=JPRB), POINTER :: ZP1FORC(:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "gpgrxyb.intfb.h"
#include "gprcp.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhpreh.intfb.h"
#include "gnhgrpre.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gpflwi.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhee_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gpyy.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gnhd3.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnh_tndlagadiab_spd.intfb.h"
#include "gnh_tndlagadiab_gw.intfb.h"
#include "gnh_tndlagadiab_svd.intfb.h"
#include "gnhee_tndlagadiab_gw.intfb.h"
#include "gnhee_tndlagadiab_svd.intfb.h"
#include "gnhee_svdincr13.intfb.h"
#include "gnhee_refine_grp.intfb.h"
#include "gnhee_refine_preh.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(YDCPG_DIM%KBL),YDGSGEOM=>YDGEOMETRY%YRGSGEOM(YDCPG_DIM%KBL), &
 & YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE, &
 & NPROMA=>YDGEOMETRY%YRDIM%NPROMA,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)
ASSOCIATE(&
 & YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, &
 & YFORC=>YDMODEL%YRML_GCONF%YGFL%YFORC, &
 & NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
 & LBOUND_D3=>YDMODEL%YRML_DYN%YRDYN%LBOUND_D3,RMAX_D3=>YDMODEL%YRML_DYN%YRDYN%RMAX_D3, &
 & LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH)


CALL SC2PRG(1,YFORC(:)%MP,PGFL ,ZP1FORC)

YDTMP%T0%ZERO(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=0.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVAB,YDCPG_DYN0%PRE,PRESF=YDCPG_DYN0%PREF, &
          & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
          & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
          & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDTMP%T0%RPREF(JROF,JLEV)=1.0_JPRB/YDCPG_DYN0%PREF(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,.FALSE.,YDVAB,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,PXYBDER=YDTMP%T0%XYBDER%ZVIEW,&
           & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
           & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
           & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,PGFL=PGFL,PCP=YDCPG_DYN0%RCP%CP,&
 & PR=YDCPG_DYN0%RCP%R,PKAP=YDCPG_DYN0%RCP%KAP)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,PRL=YDVARS%RSPEC%DL,PRM=YDVARS%RSPEC%DM)
ELSE
  CALL GPRT(LSPRT,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM)
ENDIF

! ZRDT0 contains (RT) at "t" used in the definition of "dver":
IF (L_RDRY_VD) THEN
  YDTMP%T0%RDT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=RD*YDVARS%T%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ! ky: remark for LSPRT=T:
  !  (ZRDT0L,ZRDT0M) contain grad(Rd*Tv)=grad(RT), and grad(Rd*T) is not available.
  !  That affects only the .NOT.LGWADV case which requires the call to GNHGRGW.
  YDTMP%T0%RDTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=RD*YDVARS%T%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%RDTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=RD*YDVARS%T%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ELSE
  YDTMP%T0%RDT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%RT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%RDTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%RTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%RDTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%RTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHEE MODEL.

CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T0,YDCPG_DYN0%PREF,&
 & PNHPREF=YDCPG_DYN0%NHPREF,PNHPPI=YDTMP%T0%NHPPI,PRNHPPI=YDTMP%T0%RNHPPI,PDEP=YDTMP%T0%PDEP)
DO JLEV=1,NFLEVG
  YDTMP%T0%RRED(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)=YDCPG_DYN0%RCP%R(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)/YDTMP%T0%NHPPI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)
  YDTMP%T0%RTR(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)=YDTMP%T0%RRED(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)*YDVARS%T%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)
ENDDO
CALL GNHPREH(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T0,YDCPG_DYN0%PRE,&
 & YDCPG_DYN0%XYB%DELP,YDCPG_DYN0%XYB%LNPR,YDTMP%T0%NHPPI,YDCPG_DYN0%NHPREF,YDCPG_DYN0%NHPREH,YDTMP%T0%DELNHPRE)
CALL GNHGRPRE(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,&
 & YDCPG_DYN0%NHPREF,YDVARS%SPD%DL,YDVARS%SPD%DM,&
 & YDTMP%T0%NHPREL,YDTMP%T0%NHPREM,YDTMP%T0%LNNHPREFL,YDTMP%T0%LNNHPREFM,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM)

!*     3.1.7 COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            "omega/prehyd" and "W".

CALL GPCTY(YDVFE,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,LRUBC,YDVAB,YDVETA,&
 & YDVARS%U%T0,YDVARS%V%T0,YDVARS%DIV%T0,YDVARS%EDOT%T0,&
 & PSPL=YDCPG_DYN0%PREL,PSPM=YDCPG_DYN0%PREM,PRPREF=YDTMP%T0%RPREF,&
 & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
 & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
 & PRPP=YDCPG_DYN0%XYB%RPP, PEVEL=YDCPG_DYN0%CTY%EVEL, PVVEL=YDCPG_DYN0%CTY%VVEL, &
 & PPSDIV=YDCPG_DYN0%CTY%PSDIV, PPSDVBC=YDCPG_DYN0%CTY%PSDVBC, PDIVDP=YDCPG_DYN0%CTY%DIVDP)

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,NPROMA,NFLEVG,YDCPG_DIM%KFDIA,NSTEP,YDCPG_DIM%KSTGLO,YDCPG_DYN0%PRE,YDCPG_DYN0%CTY%DIVDP(:,1:))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA, &
   & ZP1FORC,YDCPG_DYN0%PREF,YDVARS%T%T0,YDCPG_DYN0%RCP%R,YDCPG_DYN0%CTY%EVEL,YDCPG_DYN0%CTY%VVEL(:,1:))
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

YDCPG_DYN0%PHI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)=POROG(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
CALL GPGEO(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,YDCPG_DYN0%PHI,YDCPG_DYN0%PHIF,YDVARS%T%T0,&
 & YDTMP%T0%RRED,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,&
 & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
 & PHIFL=YDCPG_DYN0%PHIFL,PHIFM=YDCPG_DYN0%PHIFM,PHIHL=YDTMP%T0%GPHL,PHIHM=YDTMP%T0%GPHM,&
 & LDNHEE=.TRUE.,PRNHPPI=YDTMP%T0%RNHPPI,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM,&
 & PLNPRL_DER=YDTMP%T0%XYBDER%LNPRL, PLNPRM_DER=YDTMP%T0%XYBDER%LNPRM, &
 & PALPHL_DER=YDTMP%T0%XYBDER%ALPHL, PALPHM_DER=YDTMP%T0%XYBDER%ALPHM)

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDCPG_DYN0%KENE(JROF,JLEV)=0.5_JPRB*(YDVARS%U%T0(JROF,JLEV)**2+YDVARS%V%T0(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%XYB%LNPR,&
 & YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%UVH%WWI (1:,1:),LDVERINT=LVEREGINT)
CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%U%T0,YDVARS%V%T0,&
          & PWWI=YDCPG_DYN0%UVH%WWI,PUH=YDCPG_DYN0%UVH%UH,PVH=YDCPG_DYN0%UVH%VH)

LLDER=.TRUE.
CALL GPUVS(NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,&
 & YDVARS%U%T0,YDVARS%V%T0,YDTMP%T0%US,YDTMP%T0%VS,&
 & PDIVF=YDVARS%DIV%T0,PVORF=YDVARS%VOR%T0,&
 & PUFL=YDVARS%U%DL,PVFL=YDVARS%V%DL,&
 & PUS_L=YDTMP%T0%US_L,PVS_L=YDTMP%T0%VS_L,PUS_M=YDTMP%T0%US_M,PVS_M=YDTMP%T0%VS_M)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

IF (LNHEE_REFINE_GRP) THEN
  CALL GPFLWI(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA, &
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PRE,&
   & YDCPG_DYN0%PREF,YDCPG_DYN0%XYB%DELP,YDTMP%T0%WH2F,LDVERINT=LVEREGINT)

  CALL GNHEE_REFINE_GRP(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,&
   & YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%XYBDER%ZVIEW,&
   & YDTMP%T0%RNHPPI,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM,YDTMP%T0%GPHL,YDTMP%T0%GPHM,       &
   & YDTMP%T0%PDEP,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,YDTMP%T0%WH2F,&
   & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM)
ELSE
  CALL GNHEE_GRP(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,&
   & YDCPG_DYN0%XYB%RDELP,YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%XYBDER%ZVIEW,&
   & YDTMP%T0%DELNHPRE,YDTMP%T0%LNNHPREFL,YDTMP%T0%LNNHPREFM,YDTMP%T0%RNHPPI,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM,&
   & YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
   & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM)
ENDIF

! store for next time-step
IF( LGRADSP ) THEN
  ! adjust field with filter result
  YDTMP%T0%SGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTL%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG) - YDVARS%SGRTL%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG))
  YDTMP%T0%SGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTM%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG) - YDVARS%SGRTM%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG))

  ! store for next time-step unfiltered fields
  YDVARS%SGRTL%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDVARS%SGRTM%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

  ! set new values for current timestep
  YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%SGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%SGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

ENDIF

YDTMP%T0%SGRTSL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
YDTMP%T0%SGRTSM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)

!*     3.1.13a COMPUTES TERM "NHX"

IF (LDGPXX) THEN
  CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
   & YDCPG_DYN0%XYB%LNPR,YDTMP%T0%RT,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & YDTMP%T0%NHXS,PNHPPI=YDTMP%T0%NHPPI)
   ! PNHXT0 always stores NHX
  IF (NVDVAR == 3 .OR. NVDVAR == 4) THEN
    ! NHX = NHX_S
    YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    ! NHX = NHX_S - (d13 - dver)
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=YDTMP%T0%RDT,PNHPPI=YDTMP%T0%NHPPI,&
     & PSVDINCR13=YDTMP%T0%SVDINCR13)
    YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDTMP%T0%SVDINCR13(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ENDIF
ELSE
  ! This case only occurs for a subset of NVDVAR= 4 or 5 cases
  IF (NVDVAR == 4) THEN
    YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ! NHX_S
    YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG) =YDVARS%NHX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ! NHX_S = NHX + (d13 - dver)
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=YDTMP%T0%RDT,PNHPPI=YDTMP%T0%NHPPI,&
     & PSVDINCR13=YDTMP%T0%SVDINCR13)
    YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)+YDTMP%T0%SVDINCR13(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ENDIF
ENDIF

!*     3.1.13b COMPUTES TERM "NHY"
IF (NVDVAR == 5 .AND. LGWADV) THEN
  CALL GPYY(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & YDCPG_DYN0%NHY)
ELSE
  YDCPG_DYN0%NHY(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG) = 0.0_JPRB
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

IF (NVDVAR == 3) THEN
  YDTMP%T0%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%DVERL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%DVERM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ELSEIF (NVDVAR == 4) THEN
  YDTMP%T0%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%DVERL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDVARS%NHX%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%DVERM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDVARS%NHX%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ELSEIF (NVDVAR == 5) THEN
  ! d13 in ZDVERW0
  YDTMP%T0%DVERW(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ! dver in ZDVER0: dver = d13 - (d13 - dver)
  CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
   & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=YDTMP%T0%RDT,PNHPPI=YDTMP%T0%NHPPI,&
   & PSVDINCR13=YDTMP%T0%SVDINCR13)
  YDTMP%T0%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%DVERW(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDTMP%T0%SVDINCR13(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ! grad(dver) is not used; ZDVER0[L,M] are used to store grad(d13)
  YDTMP%T0%DVERL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDVARS%NHX%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%DVERM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDVARS%NHX%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ENDIF

!*     3.1.16 COMPUTES term [gw] or [gW].

! Caution: must use consistent definitions of RT in ZRDT0 and ZDVER0.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL2TL)
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLGWF0,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%US,YDTMP%T0%VS,&
     & YDTMP%T0%RDT,YDTMP%T0%DVER,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=YDTMP%T0%RNHPPI,PGDW=YDTMP%T0%GDW)

    YDCPG_DYN0%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%GWFT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSE
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLGWF0,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%US,YDTMP%T0%VS,&
     & YDTMP%T0%RDT,YDTMP%T0%DVER,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=YDTMP%T0%RNHPPI)

    IF (LGWADV) YDCPG_DYN0%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains (gw)_surf
    YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
  ENDIF
ELSEIF (NVDVAR==5) THEN
  ! Case LVFE_GW is ignored and not coded.
  ! -- Modified W from d13; W_surf=0, this is why ZZERO enters GPGW instead of
  ! (POROGL,POROGM,ZUS0,ZVS0)
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLGWF0,LLGDWI,&
   & YDTMP%T0%ZERO,YDTMP%T0%ZERO,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%ZERO,YDTMP%T0%ZERO,&
   & YDTMP%T0%RDT,YDTMP%T0%DVERW,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=YDTMP%T0%RNHPPI)
  IF (LGWADV) THEN
    YDCPG_DYN0%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains 0
    YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=0.0_JPRB
  ENDIF
ENDIF

!*     3.1.17 COMPUTES grad(gw) or  grad(gW).

! Caution: must use consistent definitions of RT in ZRDT0,ZRDT0L,ZRDT0M, and
! ZDVER0,ZDVER0L,ZDVER0M.
LLDER=.TRUE.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,&
   & YDTMP%T0%RDT,YDTMP%T0%RDTL,YDTMP%T0%RDTM,&
   & YDTMP%T0%DVER,YDTMP%T0%DVERL,YDTMP%T0%DVERM,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
   & PUS=YDTMP%T0%US,PVS=YDTMP%T0%VS,PUS_L=YDTMP%T0%US_L,PVS_L=YDTMP%T0%VS_L,PUS_M=YDTMP%T0%US_M,PVS_M=YDTMP%T0%VS_M,&
   & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,POROGLM=YDTMP%T0%OROGLM,POROGLL=YDTMP%T0%OROGLL,POROGMM=YDTMP%T0%OROGMM,&
   & PGWFL=YDCPG_DYN0%GWFL,PGWFM=YDCPG_DYN0%GWFM,PGWHL=YDTMP%T0%GWHL,PGWHM=YDTMP%T0%GWHM,&
   & LDNHEE=.TRUE.,PRNHPPI=YDTMP%T0%RNHPPI,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM,&
   & PLNPRL_DER=YDTMP%T0%XYBDER%LNPRL, PLNPRM_DER=YDTMP%T0%XYBDER%LNPRM, &
   & PALPHL_DER=YDTMP%T0%XYBDER%ALPHL, PALPHM_DER=YDTMP%T0%XYBDER%ALPHM)
ELSEIF (NVDVAR==5) THEN
  ! -- grad(gW) from d13 and grad(d13); grad(W)_surf=0, this is why ZZERO enters
  ! GNHGRGW instead of (POROG.. and Z[U,V]S0.. arrays)
  CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,&
   & YDTMP%T0%RDT,YDTMP%T0%RDTL,YDTMP%T0%RDTM,&
   & YDTMP%T0%DVERW,YDTMP%T0%DVERL,YDTMP%T0%DVERM,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
   & PUS=YDTMP%T0%ZERO,PVS=YDTMP%T0%ZERO,PUS_L=YDTMP%T0%ZERO,PVS_L=YDTMP%T0%ZERO,PUS_M=YDTMP%T0%ZERO,PVS_M=YDTMP%T0%ZERO,&
   & POROGL=YDTMP%T0%ZERO,POROGM=YDTMP%T0%ZERO,POROGLM=YDTMP%T0%ZERO,POROGLL=YDTMP%T0%ZERO,POROGMM=YDTMP%T0%ZERO,&
   & PGWFL=YDCPG_DYN0%GWFL,PGWFM=YDCPG_DYN0%GWFM,PGWHL=YDTMP%T0%GWHL,PGWHM=YDTMP%T0%GWHM,&
   & LDNHEE=.TRUE.,PRNHPPI=YDTMP%T0%RNHPPI,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM,&
   & PLNPRL_DER=YDTMP%T0%XYBDER%LNPRL, PLNPRM_DER=YDTMP%T0%XYBDER%LNPRM, &
   & PALPHL_DER=YDTMP%T0%XYBDER%ALPHL, PALPHM_DER=YDTMP%T0%XYBDER%ALPHM)
ENDIF

!*     3.1.18 COMPUTES D3.

CALL GNHD3(NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
 & YDVARS%DIV%T0,YDTMP%T0%NHXS,YDTMP%T0%DVER,YDCPG_DYN0%RCP%R,RMAX_D3,LBOUND_D3,YDTMP%T0%Z3DIVG)

!*     3.1.19 COMPUTES pre/(Rd T prehyd [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  IF (L_RDRY_VD) THEN
    ! ky: caution, PRDPHI is currently computed with R instead of Rd (by error),
    !  but Rd would be the right value.
    !  The right code should be:
    !  PRDPHI(.,.)=1.0_JPRB/(PXYB0(.,.,YYTXYB0%M_LNPR)*RD*PGMV(.,.,YT0%MT)*ZRNHPPI0(.,.))
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*YDTMP%T0%RTR(JROF,JLEV))
      ENDDO
    ENDDO
  ELSE
    ! ky: PRDPHI uses the right (moist) version of "R" in this case.
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*YDTMP%T0%RTR(JROF,JLEV))
      ENDDO
    ENDDO
  ENDIF
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY,YDGMV,YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_DYN%YRDYN,&
 & YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDGSGEOM%RCORI,&
 & YDGSGEOM%GNORDL,YDGSGEOM%GNORDM,&
 & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM,&
 & YDVARS%U%T0,YDVARS%V%T0,PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
 & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC))

!*     3.1.20b COMPUTES [D V_surf/Dt].

IF (.NOT.LGWADV.OR.LNHEE_SVDLAPL_FIRST.OR.LNHEE_REFINE_PREH_BBC) THEN
  CALL GNH_TNDLAGADIAB_UVS(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LRPLANE,&
   & YDGSGEOM%RCORI,YDCSGEOM%RATATX,YDCSGEOM%RATATH,&
   & YDTMP%T0%US,YDTMP%T0%VS,YDTMP%T0%SGRTSL,YDTMP%T0%SGRTSM,&
   & YDTMP%T0%TNDUS,YDTMP%T0%TNDVS)
ENDIF

!*     3.1.20c REFINE half-level pre, pres. grad. term, [D V/Dt] at bottom

IF (LNHEE_REFINE_PREH_BBC) THEN

  ! remark (K.Y.) for option LGRADSP: filter is applied to main part of the
  ! pressure gradient term computed in the predictor step,                
  ! but no filter is applied to the correction done in GNHEE_REFINE_PREH.
  ! But I do not guarantee that LGRADSP=T and LNHEE_REFINE_PREH_BBC=T are
  ! consistent.

  CALL GNHEE_REFINE_PREH(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
    & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
    & YDTMP%T0%US,YDTMP%T0%VS,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,YDTMP%T0%PDEP,YDTMP%T0%WH2F,&
    & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM,YDTMP%T0%SGRTSL,YDTMP%T0%SGRTSM,&
    & PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
    & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC),&
    & YDTMP%T0%TNDUS,YDTMP%T0%TNDVS,YDCPG_DYN0%NHPREH)

ENDIF

!*     3.1.21 COMPUTES [D(gw)/Dt].

IF (LNHEE_SVDLAPL_FIRST) THEN
  ! * Laplacian term computed first, then rhs of (gw) eqn computed.
  !   GNHEE_TNDLAGADIAB_GW is called for LGWADV=T only.
  IF (LGWADV) THEN
    ! warning: formerly LVFE_LAPL.OR.LVFE_GW
    IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHEE 3.1.21: option not yet coded!')

    CALL GNHEE_TNDLAGADIAB_GW(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
     & YDTMP%T0%US,YDTMP%T0%VS,YDTMP%T0%TNDUS,YDTMP%T0%TNDVS,&
     & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,YDTMP%T0%PDEP,&
     & PATND(1,1,YYTTND%M_TNDGW))
  ENDIF
ELSE
  ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
  !   GNH_TNDLAGADIAB_GW may be called for both LGWADV=T and LGWADV=F.
  !   PDBBC is required only if LGWADV=F.
  !   Dataflow may be tricky, and not always convenient to test future variable changes.

  IF (LGWADV.AND.LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & PDELP=YDCPG_DYN0%XYB%DELP,PDELNHPRE=YDTMP%T0%DELNHPRE,&
     & PTNDGWF_LAP=YDTMP%T0%TNDGWF_LAP,PTNDGWF_OTH=YDTMP%T0%TNDGWF_OTH)
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        PATND(JROF,JLEV,YYTTND%M_TNDGW)=YDTMP%T0%TNDGWF_LAP(JROF,JLEV)+YDTMP%T0%TNDGWF_OTH(JROF,JLEV)
      ENDDO
    ENDDO
  ELSEIF (LGWADV) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & PDEP=YDTMP%T0%PDEP,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PTNDGWH_LAP=YDTMP%T0%TNDGWH_LAP,PTNDGWH_OTH=YDTMP%T0%TNDGWH_OTH)
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        PATND(JROF,JLEV,YYTTND%M_TNDGW)=YDTMP%T0%TNDGWH_LAP(JROF,JLEV-1)+YDTMP%T0%TNDGWH_OTH(JROF,JLEV-1)
      ENDDO
    ENDDO
  ELSE IF (LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
     & POROGLM=YDTMP%T0%OROGLM,POROGLL=YDTMP%T0%OROGLL,POROGMM=YDTMP%T0%OROGMM,&
     & PUS=YDTMP%T0%US,PVS=YDTMP%T0%VS,PTNDUS=YDTMP%T0%TNDUS,PTNDVS=YDTMP%T0%TNDVS,&
     & PDEP=YDTMP%T0%PDEP,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PDELP=YDCPG_DYN0%XYB%DELP,PDELNHPRE=YDTMP%T0%DELNHPRE,&
     & PTNDGWH_LAP=YDTMP%T0%TNDGWH_LAP,PTNDGWF_OTH=YDTMP%T0%TNDGWF_OTH,PDBBC=YDCPG_DYN0%DBBC)
  ELSE
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDTMP%T0%PDEPS(JROF)=YDCPG_DYN0%NHPREH(JROF,NFLEVG)-YDCPG_DYN0%PRE(JROF,NFLEVG)
    ENDDO

    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
     & POROGLM=YDTMP%T0%OROGLM,POROGLL=YDTMP%T0%OROGLL,POROGMM=YDTMP%T0%OROGMM,&
     & PUS=YDTMP%T0%US,PVS=YDTMP%T0%VS,PTNDUS=YDTMP%T0%TNDUS,PTNDVS=YDTMP%T0%TNDVS,&
     & PDEP=YDTMP%T0%PDEP,PDEPS=YDTMP%T0%PDEPS,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PTNDGWH_LAP=YDTMP%T0%TNDGWH_LAP,PTNDGWH_OTH=YDTMP%T0%TNDGWH_OTH,PDBBC=YDCPG_DYN0%DBBC)
  ENDIF
ENDIF

!*     3.1.22 COMPUTES [D(pressure departure variable)/Dt].

CALL GNH_TNDLAGADIAB_SPD(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
 & YDCPG_DYN0%RCP%KAP,YDTMP%T0%Z3DIVG,YDCPG_DYN0%CTY%VVEL(:,1:),&
 & PATND(1,1,YYTTND%M_TNDPD) )

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt].

IF (.NOT.LGWADV) THEN
  ! Remark: PATND(.,.,YYTTND%M_TNDVD) is useless (and not computed) if LGWADV.
  IF (LNHEE_SVDLAPL_FIRST) THEN
    ! * Laplacian term directly computed, does not require the rhs of (gw) eqn.
    !   GNHEE_TNDLAGADIAB_SVD is called for LGWADV=F only.
    IF (LVERTFE) CALL ABOR1(' CPG_GP_NHEE 3.1.23: not yet coded case')

    CALL GNHEE_TNDLAGADIAB_SVD(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
     & YDCPG_DYN0%CTY%EVEL,YDCPG_DYN0%XYB%RDELP,&
     & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,YDTMP%T0%PDEP,&
     & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),YDTMP%T0%TNDUS,YDTMP%T0%TNDVS,&
     & YDCPG_DYN0%RDPHI,YDTMP%T0%GWHL,YDTMP%T0%GWHM,YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,YDTMP%T0%DVER,YDTMP%T0%DVERW,YDVARS%DIV%T0,YDTMP%T0%Z3DIVG,&
     & PATND(1,1,YYTTND%M_TNDVD),YDCPG_DYN0%DBBC)
  ELSE
    ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
    !   GNH_TNDLAGADIAB_GW must have been called above to provide ZTNDGW.. arrays.
    !   Dataflow may be tricky, and not always convenient to test future variable changes.
    CALL GNH_TNDLAGADIAB_SVD(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & YDCPG_DYN0%RDPHI,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,YDTMP%T0%GWHL,YDTMP%T0%GWHM,YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,YDTMP%T0%DVER,YDVARS%DIV%T0,YDTMP%T0%Z3DIVG,&
     & YDTMP%T0%TNDGWH_LAP,YDTMP%T0%TNDGWH_OTH,YDTMP%T0%TNDGWF_OTH,&
     & YDTMP%T0%PDEP,YDCPG_DYN0%PRE,YDCPG_DYN0%PREF,YDCPG_DYN0%XYB%DELP,YDCPG_DYN0%XYB%LNPR,&
     & PATND(1,1,YYTTND%M_TNDVD) )
  ENDIF
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    ZRCV=1.0_JPRB/(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))-1.0_JPRB
    PATND(JROF,JLEV,YYTTND%M_TNDT)=-ZRCV*YDVARS%T%T0(JROF,JLEV)*YDTMP%T0%Z3DIVG(JROF,JLEV)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVAB,YDCPG_DYN9%PRE,PRESF=YDCPG_DYN9%PREF,&
            & PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, &
            & PALPH=YDCPG_DYN9%XYB%ALPH, PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE,  &
            & PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=YDCPG_DYN9%RCP%CP,&
   & PR=YDCPG_DYN9%RCP%R)

!*     3.2.4 COMPUTE "RT".

  ! ZR0T9 is used in calculation of NHX(t-dt).
  YDTMP%T9%R0T(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%T%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)*YDCPG_DYN0%RCP%R(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

  ! ZRDT9 is the (RT) at "t-dt" used in the definition of "dver":
  IF (L_RDRY_VD) THEN
    YDTMP%T9%RDT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=RD*YDVARS%T%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSE
    ! use of R_moist at time t to be consistent with ZR0T9 and ZDVER9.
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        YDTMP%T9%RDT(JROF,JLEV)=YDCPG_DYN0%RCP%R(JROF,JLEV)*YDVARS%T%T9(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF

!*     3.2.6 COMPUTE QUANTITIES FOR NHEE MODEL.

  CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T9,YDCPG_DYN9%PREF,&
   & PNHPREF=YDCPG_DYN9%NHPREF,PNHPPI=YDTMP%T9%NHPPI,PRNHPPI=YDTMP%T9%RNHPPI,PDEP=YDTMP%T9%PDEP)
  DO JLEV=1,NFLEVG
    YDTMP%T9%RRED(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)=YDCPG_DYN9%RCP%R(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)/YDTMP%T9%NHPPI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JLEV)
  ENDDO
  CALL GNHPREH(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T9,YDCPG_DYN9%PRE,&
   & YDCPG_DYN9%XYB%DELP,YDCPG_DYN9%XYB%LNPR,YDTMP%T9%NHPPI,YDCPG_DYN9%NHPREF,YDCPG_DYN9%NHPREH,YDTMP%T9%DELNHPRE)

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF (LDLDIAB.OR.LSIMPH) THEN
    YDCPG_DYN9%PHI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)=POROG(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
    CALL GPGEO(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,YDCPG_DYN9%PHI,YDCPG_DYN9%PHIF,YDVARS%T%T9,&
     & YDTMP%T9%RRED,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,&
     & YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  ! Not needed if Z-term and X-term are calculated FE
  IF (LDGPXX .AND. .NOT.LVERTFE) THEN
    ! * same remark as for time t half-level winds.
    YDTMP%T9%UVH%WWI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%UVH%WWI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%U%T9,YDVARS%V%T9, &
              & PWWI=YDTMP%T9%UVH%WWI,PUH=YDTMP%T9%UVH%UH,PVH=YDTMP%T9%UVH%VH)
  ENDIF

  LLDER=.FALSE.
  CALL GPUVS(NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,YDVARS%U%T9,YDVARS%V%T9,YDTMP%T9%US,YDTMP%T9%VS)

!*     3.2.13a COMPUTES TERM "NHX".

  ! caution: must use "R" and "pre/prehyd" at time t.
  IF (LDGPXX) THEN
    CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
     & YDCPG_DYN9%XYB%LNPR,YDTMP%T9%R0T,&
     & YDVARS%U%T9,YDVARS%V%T9,YDTMP%T9%UVH%UH,YDTMP%T9%UVH%VH,&
     & YDTMP%T0%NHXS,PNHPPI=YDTMP%T0%NHPPI)
    IF (NVDVAR == 3 .OR. NVDVAR == 4) THEN
      ! NHX = NHX_S
      YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ELSEIF (NVDVAR == 5) THEN
      ! NHX = NHX_S - (d13 - dver)
      CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
       & YDTMP%T9%UVH%UH,YDTMP%T9%UVH%VH,&
       & PLNPR=YDCPG_DYN9%XYB%LNPR,PRT=YDTMP%T9%RDT,PNHPPI=YDTMP%T9%NHPPI,&
       & PSVDINCR13=YDTMP%T0%SVDINCR13)
      YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%NHXS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDTMP%T0%SVDINCR13(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ENDIF
  ELSE
    ! This case only occurs for a subset of NVDVAR= 4 or 5 cases
    ! PNHXT9 and PGMV.,.,YT9%MNHX) always stores NHX; calculation of NHX_S is
    ! useless.
    YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ENDIF

  !*     3.1.13b COMPUTES TERM "NHY"

  IF (NVDVAR == 5 .AND. LGWADV) THEN
    CALL GPYY(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDTMP%T9%UVH%UH,YDTMP%T9%UVH%VH,YDCPG_DYN9%NHY)
  ELSE
    YDCPG_DYN9%NHY(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG) = 0.0_JPRB
  ENDIF


!*     3.2.14 COMPUTES dver.

  IF (NVDVAR == 3) THEN
    YDTMP%T9%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 4) THEN
    YDTMP%T9%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    ! d13 in ZDVERW9
    YDTMP%T9%DVERW(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SVD%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ! dver in ZDVER9
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDTMP%T9%UVH%UH,YDTMP%T9%UVH%VH,&
     & PLNPR=YDCPG_DYN9%XYB%LNPR,PRT=YDTMP%T9%RDT,PNHPPI=YDTMP%T9%NHPPI,&
     & PSVDINCR13=YDTMP%T0%SVDINCR13)
    YDTMP%T9%DVER(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T9%DVERW(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)-YDTMP%T0%SVDINCR13(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ENDIF

!*     3.2.16 COMPUTES term [gw] or [gW].

  ! Caution: must use consistent definitions of RT in ZRDT9 and ZDVER9.
  IF (NVDVAR==3 .OR. NVDVAR==4) THEN
    LLGDWI=.FALSE.
    IF (LGWADV.AND.LVFE_GW) THEN
      ! PGDW is required (at least for SL3TL)
      CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LDMPA,LLGDWI,&
       & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,YDTMP%T9%US,YDTMP%T9%VS,&
       & YDTMP%T9%RDT,YDTMP%T9%DVER,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=YDTMP%T9%RNHPPI,PGDW=YDTMP%T9%GDW)

      YDCPG_DYN9%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN9%GWFT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    ELSE
      CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LDMPA,LLGDWI,&
       & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,YDTMP%T9%US,YDTMP%T9%VS,&
       & YDTMP%T9%RDT,YDTMP%T9%DVER,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=YDTMP%T9%RNHPPI)

      IF (LGWADV) YDCPG_DYN9%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T9%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)
    ENDIF

    IF (LRDBBC) THEN
      ! PGWS contains (gw)_surf
      YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T9%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
    ENDIF
  ELSEIF (NVDVAR==5) THEN
    ! Case LVFE_GW is ignored and not coded.
    ! -- Modified W
    LLGDWI=.FALSE.
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LDMPA,LLGDWI,&
     & YDTMP%T0%ZERO,YDTMP%T0%ZERO,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,YDTMP%T0%ZERO,YDTMP%T0%ZERO,&
     & YDTMP%T9%RDT,YDTMP%T9%DVERW,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=YDTMP%T9%RNHPPI)

    IF (LGWADV) YDCPG_DYN9%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T9%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)

    IF (LRDBBC) THEN
      ! PGWS contains 0
      YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=0.0_JPRB
    ENDIF
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE',1,ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHEE
