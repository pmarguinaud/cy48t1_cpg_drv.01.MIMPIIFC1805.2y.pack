#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHEE(YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDVARS, YDCPG_DYN0, YDCPG_DYN9, YDMODEL, &
& LDGPXX, LDLDIAB, LDMPA, P_OROGLL_T0, P_OROGMM_T0, P_OROGLM_T0, P_GDW_T0, P_GWHT_T0, YDCPG_TND, &
& PP1FORC)

!**** *CPG_GP_NHEE* - Grid point calculations at instants t and t-dt for NHEE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHEE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHEE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.

!     INPUT/OUTPUT:
!     -------------
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad RT" at full levels.
!        PRT0M     : meridian component of "grad RT" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D (gw)_surf / Dt]_adiab.
!        PRDPHI    : pre/(R T prehyd [Delta log(prehyd)]) at t.
!                    "R" is the version of R (may be Rdry or Rmoist) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PNHYT0    : term 'NHY' at t.
!        PNHYT9    : term 'NHY' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (Apr 2018): introduce key L_RDRY_VD (ensure consistent definition of "dver" everywhere).
!      K. Yessad (July 2018): alternate treatment of Laplacian term in gw and dver equations.
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD,        ONLY : GEOMETRY
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,        ONLY : CPG_DYN_TYPE, CPG_TND_TYPE
USE CPG_OPTS_TYPE_MOD,   ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE TYPE_MODEL,          ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC, LRPLANE, LNHDYN
USE YOMDYNA  , ONLY : NPDVAR, NVDVAR, LGWADV, LRDBBC, LGRADSP, LRUBC, L_RDRY_VD, LNHEE_SVDLAPL_FIRST, &
                    & LNHEE_REFINE_GRP, LNHEE_REFINE_PREH_BBC, LVEREGINT, LAPRXPK, NDLNPR, RHYDR0
USE YOMCT3   , ONLY : NSTEP
USE YOMCVER  , ONLY : LVERTFE,LVFE_LAPL_BC,LVFE_GW,LVFE_ECMWF
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMVERT  , ONLY : TOPPRES
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)         ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE)    ,INTENT(IN)   :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE)    ,INTENT(IN)   :: YDCPG_OPTS
TYPE(FIELD_VARIABLES)  ,INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE)     ,INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE)     ,INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)            ,INTENT(INOUT) :: YDMODEL
LOGICAL                ,INTENT(IN)    :: LDGPXX
LOGICAL                ,INTENT(IN)    :: LDLDIAB
LOGICAL                ,INTENT(IN)    :: LDMPA
REAL(KIND=JPRB)        ,INTENT(INOUT) :: P_GDW_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)        ,INTENT(INOUT) :: P_GWHT_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)        ,INTENT(INOUT) :: P_OROGLL_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)        ,INTENT(INOUT) :: P_OROGLM_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)        ,INTENT(INOUT) :: P_OROGMM_T0 (YDGEOMETRY%YRDIM%NPROMA)
TYPE(CPG_TND_TYPE)     ,INTENT(INOUT) :: YDCPG_TND
REAL(KIND=JPRB)        ,INTENT(INOUT), OPTIONAL :: PP1FORC (:,:)

!     ------------------------------------------------------------------

#include "gphpre_expl.intfb.h"
#include "gpgrxyb_expl.intfb.h"
#include "gprcp_expl.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhpreh.intfb.h"
#include "gnhgrpre.intfb.h"
#include "gpcty_expl.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo_expl.intfb.h"
#include "gpgrgeo_expl.intfb.h"
#include "gpflwi.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv_expl.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhee_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gpyy.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gnhd3.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnh_tndlagadiab_spd.intfb.h"
#include "gnh_tndlagadiab_gw.intfb.h"
#include "gnh_tndlagadiab_svd.intfb.h"
#include "gnhee_tndlagadiab_gw.intfb.h"
#include "gnhee_tndlagadiab_svd.intfb.h"
#include "gnhee_svdincr13.intfb.h"
#include "gnhee_refine_grp_expl.intfb.h"
#include "gnhee_refine_preh.intfb.h"
#include "cpg_gp_nhee_t9.intfb.h"

!     ------------------------------------------------------------------

REAL (KIND=JPRB) :: Z_DELNHPRE_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_DVERL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_DVERM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_DVERW_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_DVER_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)


REAL (KIND=JPRB) :: Z_GPHL_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GPHM_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHL_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHM_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_LNNHPREFL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_LNNHPREFM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHPPI_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_NHPREL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHPREM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXS_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_PDEPS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_PDEP_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_PSGRTL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_PSGRTM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_RDTL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RDTM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RDT_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_RNHPPI_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_RPREF_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RRED_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL (KIND=JPRB) :: Z_RTR_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RT_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTSL_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_SGRTSM_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_SVDINCR13_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_TNDGWF_LAP_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_TNDGWF_OTH_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_TNDGWH_LAP_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_TNDGWH_OTH_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_TNDUS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_TNDVS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_L_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_M_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_T0 (YDGEOMETRY%YRDIM%NPROMA)




REAL (KIND=JPRB) :: Z_VS_L_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_VS_M_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_VS_T0 (YDGEOMETRY%YRDIM%NPROMA)

REAL (KIND=JPRB) :: Z_WH2F_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,2)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHPLL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHPLM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFAPL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFA_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFD_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_LNPRL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_LNPRM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_Z3DIVG_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_ZERO_T0 (YDGEOMETRY%YRDIM%NPROMA)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

REAL(KIND=JPRB) :: ZRCV

REAL(KIND=JPRB) :: ZHOOK_HANDLE


IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE', 0, ZHOOK_HANDLE)

ASSOCIATE(    YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, NPROMA=>YDGEOMETRY%YRDIM%NPROMA, &
& NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, YDCST => YDMODEL%YRCST)
ASSOCIATE(  YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER,                           &
& LBOUND_D3=>YDMODEL%YRML_DYN%YRDYN%LBOUND_D3, RMAX_D3=>YDMODEL%YRML_DYN%YRDYN%RMAX_D3, LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH,&
& RD => YDCST%RD, RV => YDCST%RV)


Z_ZERO_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE_EXPL(LAPRXPK, LVERTFE, NDLNPR, RHYDR0, TOPPRES, YDMODEL%YRCST, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVAB, YDCPG_DYN0%PRE, PRESF=YDCPG_DYN0%PREF,   &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH, &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_RPREF_T0(JROF,JLEV)=1.0_JPRB/YDCPG_DYN0%PREF(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB_EXPL(LVERTFE, NDLNPR, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, .FALSE., YDVAB, YDCPG_DYN0%PREL, YDCPG_DYN0%PREM,         &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH,                 &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP, PCOEFD_DER=Z_XYBDER_COEFD_T0,                  &
& PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0, PCOEFA_DER=Z_XYBDER_COEFA_T0, PCOEFAPL_DER=Z_XYBDER_COEFAPL_T0,   &
& PALPHPLL_DER=Z_XYBDER_ALPHPLL_T0, PALPHPLM_DER=Z_XYBDER_ALPHPLM_T0, PALPHL_DER=Z_XYBDER_ALPHL_T0, PALPHM_DER=Z_XYBDER_ALPHM_T0&
&   )

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP_EXPL (YDMODEL%YRCST, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDVARS=YDVARS, PCP=YDCPG_DYN0%RCP%CP, &
               & PR=YDCPG_DYN0%RCP%R, PKAP=YDCPG_DYN0%RCP%KAP)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, RD, RV, YDCPG_DYN0%RCP%R, YDVARS%T%T0,         &
  & YDVARS%T%DL, YDVARS%T%DM, YDVARS%Q%DL, YDVARS%Q%DM, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM, PRL=YDVARS%RSPEC%DL, &
  & PRM=YDVARS%RSPEC%DM)
ELSE
  CALL GPRT(LSPRT, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, RD, RV, YDCPG_DYN0%RCP%R, YDVARS%T%T0, &
  & YDVARS%T%DL, YDVARS%T%DM, YDVARS%Q%DL, YDVARS%Q%DM, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM)
ENDIF

! ZRDT0 contains (RT) at "t" used in the definition of "dver":
IF (L_RDRY_VD) THEN
  Z_RDT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=RD*YDVARS%T%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ! ky: remark for LSPRT=T:
  !  (ZRDT0L,ZRDT0M) contain grad(Rd*Tv)=grad(RT), and grad(Rd*T) is not available.
  !  That affects only the .NOT.LGWADV case which requires the call to GNHGRGW.
  Z_RDTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=RD*YDVARS%T%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_RDTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=RD*YDVARS%T%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ELSE
  Z_RDT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_RT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_RDTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN0%RTL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_RDTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN0%RTM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHEE MODEL.

CALL GNHPRE(NPDVAR, YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%SPD%T0, YDCPG_DYN0%PREF, &
& PNHPREF=YDCPG_DYN0%NHPREF, PNHPPI=Z_NHPPI_T0, PRNHPPI=Z_RNHPPI_T0, PDEP=Z_PDEP_T0)
DO JLEV=1,NFLEVG
  Z_RRED_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=YDCPG_DYN0%RCP%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)/Z_NHPPI_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
  Z_RTR_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)=Z_RRED_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)*YDVARS%T%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
ENDDO
CALL GNHPREH(LVERTFE, LVFE_ECMWF, NPDVAR, YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%SPD%T0, YDCPG_DYN0%PRE, &
& YDCPG_DYN0%XYB%DELP, YDCPG_DYN0%XYB%LNPR, Z_NHPPI_T0, YDCPG_DYN0%NHPREF, YDCPG_DYN0%NHPREH,               &
& Z_DELNHPRE_T0  )
CALL GNHGRPRE(NPDVAR, YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%PREL, &
& YDCPG_DYN0%PREM, YDCPG_DYN0%NHPREF, YDVARS%SPD%DL, YDVARS%SPD%DM, Z_NHPREL_T0, Z_NHPREM_T0,                       &
& Z_LNNHPREFL_T0, Z_LNNHPREFM_T0, YDCPG_DYN0%QCHAL, YDCPG_DYN0%QCHAM)

!*     3.1.7 COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            "omega/prehyd" and "W".

CALL GPCTY_EXPL(LVERTFE, YDVFE, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, LRUBC, YDVAB, YDVETA, YDVARS%U%T0,       &
& YDVARS%V%T0, YDVARS%DIV%T0, YDVARS%EDOT%T0, PSPL=YDCPG_DYN0%PREL, PSPM=YDCPG_DYN0%PREM, PRPREF=Z_RPREF_T0,        &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH,     &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP, PEVEL=YDCPG_DYN0%CTY%EVEL,         &
& PVVEL=YDCPG_DYN0%CTY%VVEL, PPSDIV=YDCPG_DYN0%CTY%PSDIV, PPSDVBC=YDCPG_DYN0%CTY%PSDVBC, PDIVDP=YDCPG_DYN0%CTY%DIVDP&
&     )

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY, YDMODEL%YRML_GCONF%YRRIP, NPROMA, NFLEVG, YDCPG_BNDS%KFDIA, NSTEP, YDCPG_BNDS%KSTGLO, &
  & YDCPG_DYN0%PRE, YDCPG_DYN0%CTY%DIVDP(:, 1:))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDMODEL%YRCST, YDGEOMETRY, YDMODEL%YRML_GCONF, YDMODEL%YRML_PHY_MF%YRPHY2, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, &
  & PP1FORC, YDCPG_DYN0%PREF, YDVARS%T%T0, YDCPG_DYN0%RCP%R, YDCPG_DYN0%CTY%EVEL, YDCPG_DYN0%CTY%VVEL(:, 1:)      &
  &                 )
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

YDCPG_DYN0%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)=YDVARS%GEOMETRY%OROG%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
CALL GPGEO_EXPL(LVERTFE, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDCPG_DYN0%PHI, YDCPG_DYN0%PHIF, YDVARS%T%T0, &
& Z_RRED_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO_EXPL(LVERTFE, YDGEOMETRY, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, Z_RT_T0, YDCPG_DYN0%RTL,              &
& YDCPG_DYN0%RTM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, POROGL=YDCPG_DYN0%OROGL, POROGM=YDCPG_DYN0%OROGM,           &
& PHIFL=YDCPG_DYN0%PHIFL, PHIFM=YDCPG_DYN0%PHIFM, PHIHL=Z_GPHL_T0, PHIHM=Z_GPHM_T0, LDNHEE=.TRUE., PRNHPPI=Z_RNHPPI_T0, &
& PQCHAL=YDCPG_DYN0%QCHAL, PQCHAM=YDCPG_DYN0%QCHAM, PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0,         &
& PALPHL_DER=Z_XYBDER_ALPHL_T0, PALPHM_DER=Z_XYBDER_ALPHM_T0           )

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    YDCPG_DYN0%KENE(JROF,JLEV)=0.5_JPRB*(YDVARS%U%T0(JROF,JLEV)**2+YDVARS%V%T0(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, &
& YDCPG_DYN0%UVH%WWI (1:, 1:), LDVERINT=LVEREGINT)
CALL GPHLUV_EXPL(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T0, YDVARS%V%T0, &
& PWWI=YDCPG_DYN0%UVH%WWI, PUH=YDCPG_DYN0%UVH%UH, PVH=YDCPG_DYN0%UVH%VH)

LLDER=.TRUE.
CALL GPUVS(NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, YDVARS%U%T0, YDVARS%V%T0,                    &
& Z_US_T0, Z_VS_T0, PDIVF=YDVARS%DIV%T0, PVORF=YDVARS%VOR%T0, PUFL=YDVARS%U%DL, PVFL=YDVARS%V%DL, PUS_L=Z_US_L_T0, &
& PVS_L=Z_VS_L_T0, PUS_M=Z_US_M_T0, PVS_M=Z_VS_M_T0)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

IF (LNHEE_REFINE_GRP) THEN
  CALL GPFLWI(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, &
  & YDCPG_DYN0%PRE, YDCPG_DYN0%PREF, YDCPG_DYN0%XYB%DELP, Z_WH2F_T0, LDVERINT=LVEREGINT)

  CALL GNHEE_REFINE_GRP_EXPL(LVERTFE, YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM, &
  & YDCPG_DYN0%PREL, YDCPG_DYN0%PREM, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%XYB%ALPH, PRNHPPI=Z_RNHPPI_T0,                  &
  & PQCHAL=YDCPG_DYN0%QCHAL, PQCHAM=YDCPG_DYN0%QCHAM, PHIHL=Z_GPHL_T0, PHIHM=Z_GPHM_T0, PDEP=Z_PDEP_T0,               &
  & PREF=YDCPG_DYN0%PREF, PREH=YDCPG_DYN0%PRE, PWWH2F=Z_WH2F_T0, PSGRTL=Z_PSGRTL_T0, PSGRTM=Z_PSGRTM_T0,              &
  & PALPHPLL_DER=Z_XYBDER_ALPHPLL_T0, PALPHPLM_DER=Z_XYBDER_ALPHPLM_T0)
ELSE
  CALL GNHEE_GRP(LVERTFE, YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM,       &
  & YDCPG_DYN0%PREL, YDCPG_DYN0%PREM, YDCPG_DYN0%XYB%RDELP, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%XYB%ALPH,           &
  & PDELNHPRE=Z_DELNHPRE_T0, PLNNHPREFL=Z_LNNHPREFL_T0, PLNNHPREFM=Z_LNNHPREFM_T0, PRNHPPI=Z_RNHPPI_T0,         &
  & PQCHAL=YDCPG_DYN0%QCHAL, PQCHAM=YDCPG_DYN0%QCHAM, PHIHL=Z_GPHL_T0, PHIHM=Z_GPHM_T0, PHIFL=YDCPG_DYN0%PHIFL, &
  & PHIFM=YDCPG_DYN0%PHIFM, PSGRTL=Z_PSGRTL_T0, PSGRTM=Z_PSGRTM_T0, PALPHPLL_DER=Z_XYBDER_ALPHPLL_T0,           &
  & PALPHPLM_DER=Z_XYBDER_ALPHPLM_T0)
ENDIF

! store for next time-step
IF( LGRADSP ) THEN
  ! adjust field with filter result
  Z_SGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTL%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) - YDVARS%SGRTL%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG))
  Z_SGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTM%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) - YDVARS%SGRTM%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG))

  ! store for next time-step unfiltered fields
  YDVARS%SGRTL%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  YDVARS%SGRTM%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

  ! set new values for current timestep
  Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_SGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_SGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

ENDIF

Z_SGRTSL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)
Z_SGRTSM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)

!*     3.1.13a COMPUTES TERM "NHX"

IF (LDGPXX) THEN
  CALL GPXX(LVERTFE, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_GPHL_T0, Z_GPHM_T0, YDCPG_DYN0%PHIFL, &
  & YDCPG_DYN0%PHIFM, YDCPG_DYN0%XYB%LNPR, Z_RT_T0, YDVARS%U%T0, YDVARS%V%T0, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, &
  & Z_NHXS_T0, PNHPPI=Z_NHPPI_T0)
   ! PNHXT0 always stores NHX
  IF (NVDVAR == 3 .OR. NVDVAR == 4) THEN
    ! NHX = NHX_S
    YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_NHXS_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    ! NHX = NHX_S - (d13 - dver)
    CALL GNHEE_SVDINCR13(LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, PLNPR=YDCPG_DYN0%XYB%LNPR, PRT=Z_RDT_T0,     &
    & PNHPPI=Z_NHPPI_T0, PSVDINCR13=Z_SVDINCR13_T0)
    YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_NHXS_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-Z_SVDINCR13_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ENDIF
ELSE
  ! This case only occurs for a subset of NVDVAR= 4 or 5 cases
  IF (NVDVAR == 4) THEN
    YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    ! NHX_S
    Z_NHXS_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) =YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    ! NHX_S = NHX + (d13 - dver)
    CALL GNHEE_SVDINCR13(LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, PLNPR=YDCPG_DYN0%XYB%LNPR, PRT=Z_RDT_T0,     &
    & PNHPPI=Z_NHPPI_T0, PSVDINCR13=Z_SVDINCR13_T0)
    Z_NHXS_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)+Z_SVDINCR13_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ENDIF
ENDIF

!*     3.1.13b COMPUTES TERM "NHY"
IF (NVDVAR == 5 .AND. LGWADV) THEN
  CALL GPYY(LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM, &
  & YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, YDCPG_DYN0%NHY)
ELSE
  YDCPG_DYN0%NHY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0:NFLEVG) = 0.0_JPRB
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

IF (NVDVAR == 3) THEN
  Z_DVER_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_DVERL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_DVERM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ELSEIF (NVDVAR == 4) THEN
  Z_DVER_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_DVERL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDVARS%NHX%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_DVERM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDVARS%NHX%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ELSEIF (NVDVAR == 5) THEN
  ! d13 in ZDVERW0
  Z_DVERW_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ! dver in ZDVER0: dver = d13 - (d13 - dver)
  CALL GNHEE_SVDINCR13(LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, &
  & YDCPG_DYN0%OROGM, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, PLNPR=YDCPG_DYN0%XYB%LNPR, PRT=Z_RDT_T0,     &
  & PNHPPI=Z_NHPPI_T0, PSVDINCR13=Z_SVDINCR13_T0)
  Z_DVER_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_DVERW_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-Z_SVDINCR13_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ! grad(dver) is not used; ZDVER0[L,M] are used to store grad(d13)
  Z_DVERL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDVARS%NHX%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_DVERM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SVD%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)-YDVARS%NHX%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ENDIF

!*     3.1.16 COMPUTES term [gw] or [gW].

! Caution: must use consistent definitions of RT in ZRDT0 and ZDVER0.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL2TL)
    CALL GPGW(LNHDYN, LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLGWF0, LLGDWI, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, Z_US_T0, Z_VS_T0, Z_RDT_T0,                   &
    & Z_DVER_T0, P_GWHT_T0, YDCPG_DYN0%GWFT, PRNHPPI=Z_RNHPPI_T0, PGDW=P_GDW_T0)

    YDCPG_DYN0%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN0%GWFT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ELSE
    CALL GPGW(LNHDYN, LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLGWF0, LLGDWI, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, Z_US_T0, Z_VS_T0, Z_RDT_T0,                   &
    & Z_DVER_T0, P_GWHT_T0, YDCPG_DYN0%GWFT, PRNHPPI=Z_RNHPPI_T0)

    IF (LGWADV) YDCPG_DYN0%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=P_GWHT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains (gw)_surf
    YDCPG_DYN0%GWS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=P_GWHT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)
  ENDIF
ELSEIF (NVDVAR==5) THEN
  ! Case LVFE_GW is ignored and not coded.
  ! -- Modified W from d13; W_surf=0, this is why ZZERO enters GPGW instead of
  ! (POROGL,POROGM,ZUS0,ZVS0)
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  CALL GPGW(LNHDYN, LVERTFE, LVFE_GW, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLGWF0, LLGDWI,         &
  & Z_ZERO_T0, Z_ZERO_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, Z_ZERO_T0, Z_ZERO_T0, Z_RDT_T0, &
  & Z_DVERW_T0, P_GWHT_T0, YDCPG_DYN0%GWFT, PRNHPPI=Z_RNHPPI_T0)
  IF (LGWADV) THEN
    YDCPG_DYN0%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=P_GWHT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains 0
    YDCPG_DYN0%GWS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB
  ENDIF
ENDIF

!*     3.1.17 COMPUTES grad(gw) or  grad(gW).

! Caution: must use consistent definitions of RT in ZRDT0,ZRDT0L,ZRDT0M, and
! ZDVER0,ZDVER0L,ZDVER0M.
LLDER=.TRUE.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  CALL GNHGRGW(LVERTFE, YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, Z_RDT_T0,                            &
  & Z_RDTL_T0, Z_RDTM_T0, Z_DVER_T0, Z_DVERL_T0, Z_DVERM_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH,                     &
  & PUS=Z_US_T0, PVS=Z_VS_T0, PUS_L=Z_US_L_T0, PVS_L=Z_VS_L_T0, PUS_M=Z_US_M_T0, PVS_M=Z_VS_M_T0, POROGL=YDCPG_DYN0%OROGL, &
  & POROGM=YDCPG_DYN0%OROGM, POROGLM=P_OROGLM_T0, POROGLL=P_OROGLL_T0, POROGMM=P_OROGMM_T0, PGWFL=YDCPG_DYN0%GWFL,         &
  & PGWFM=YDCPG_DYN0%GWFM, PGWHL=Z_GWHL_T0, PGWHM=Z_GWHM_T0, LDNHEE=.TRUE., PRNHPPI=Z_RNHPPI_T0, PQCHAL=YDCPG_DYN0%QCHAL,  &
  & PQCHAM=YDCPG_DYN0%QCHAM, PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0, PALPHL_DER=Z_XYBDER_ALPHL_T0,     &
  & PALPHM_DER=Z_XYBDER_ALPHM_T0                          )
ELSEIF (NVDVAR==5) THEN
  ! -- grad(gW) from d13 and grad(d13); grad(W)_surf=0, this is why ZZERO enters
  ! GNHGRGW instead of (POROG.. and Z[U,V]S0.. arrays)
  CALL GNHGRGW(LVERTFE, YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, Z_RDT_T0,                           &
  & Z_RDTL_T0, Z_RDTM_T0, Z_DVERW_T0, Z_DVERL_T0, Z_DVERM_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH,                   &
  & PUS=Z_ZERO_T0, PVS=Z_ZERO_T0, PUS_L=Z_ZERO_T0, PVS_L=Z_ZERO_T0, PUS_M=Z_ZERO_T0, PVS_M=Z_ZERO_T0,                     &
  & POROGL=Z_ZERO_T0, POROGM=Z_ZERO_T0, POROGLM=Z_ZERO_T0, POROGLL=Z_ZERO_T0, POROGMM=Z_ZERO_T0, PGWFL=YDCPG_DYN0%GWFL,   &
  & PGWFM=YDCPG_DYN0%GWFM, PGWHL=Z_GWHL_T0, PGWHM=Z_GWHM_T0, LDNHEE=.TRUE., PRNHPPI=Z_RNHPPI_T0, PQCHAL=YDCPG_DYN0%QCHAL, &
  & PQCHAM=YDCPG_DYN0%QCHAM, PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0, PALPHL_DER=Z_XYBDER_ALPHL_T0,    &
  & PALPHM_DER=Z_XYBDER_ALPHM_T0                          )
ENDIF

!*     3.1.18 COMPUTES D3.

CALL GNHD3(L_RDRY_VD, YDMODEL%YRCST, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%DIV%T0, Z_NHXS_T0, Z_DVER_T0, YDCPG_DYN0%RCP%R, &
& RMAX_D3, LBOUND_D3, Z_Z3DIVG_T0)

!*     3.1.19 COMPUTES pre/(Rd T prehyd [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  IF (L_RDRY_VD) THEN
    ! ky: caution, PRDPHI is currently computed with R instead of Rd (by error),
    !  but Rd would be the right value.
    !  The right code should be:
    !  PRDPHI(.,.)=1.0_JPRB/(PXYB0(.,.,YYTXYB0%M_LNPR)*RD*PGMV(.,.,YT0%MT)*ZRNHPPI0(.,.))
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*Z_RTR_T0(JROF,JLEV))
      ENDDO
    ENDDO
  ELSE
    ! ky: PRDPHI uses the right (moist) version of "R" in this case.
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*Z_RTR_T0(JROF,JLEV))
      ENDDO
    ENDDO
  ENDIF
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(LRPLANE, YDGEOMETRY, YDMODEL%YRML_PHY_EC%YREPHY, YDMODEL%YRML_DYN%YRDYN, YDCPG_BNDS%KIDIA, &
& YDCPG_BNDS%KFDIA, YDVARS%GEOMETRY%RCORI%T0, YDVARS%GEOMETRY%GNORDL%T0, YDVARS%GEOMETRY%GNORDM%T0,             &
& Z_PSGRTL_T0, Z_PSGRTM_T0, YDVARS%U%T0, YDVARS%V%T0, YDCPG_TND%TNDU, YDCPG_TND%TNDV,   &
& YDCPG_TND%TNDU_NOC, YDCPG_TND%TNDV_NOC)

!*     3.1.20b COMPUTES [D V_surf/Dt].

IF (.NOT.LGWADV.OR.LNHEE_SVDLAPL_FIRST.OR.LNHEE_REFINE_PREH_BBC) THEN
  CALL GNH_TNDLAGADIAB_UVS(NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LRPLANE, YDVARS%GEOMETRY%RCORI%T0, &
  & YDVARS%GEOMETRY%RATATX%T0, YDVARS%GEOMETRY%RATATH%T0, Z_US_T0, Z_VS_T0, Z_SGRTSL_T0, Z_SGRTSM_T0,     &
  & Z_TNDUS_T0, Z_TNDVS_T0    )
ENDIF

!*     3.1.20c REFINE half-level pre, pres. grad. term, [D V/Dt] at bottom

IF (LNHEE_REFINE_PREH_BBC) THEN

  ! remark (K.Y.) for option LGRADSP: filter is applied to main part of the
  ! pressure gradient term computed in the predictor step,                
  ! but no filter is applied to the correction done in GNHEE_REFINE_PREH.
  ! But I do not guarantee that LGRADSP=T and LNHEE_REFINE_PREH_BBC=T are
  ! consistent.

  CALL GNHEE_REFINE_PREH(LVERTFE, LVFE_GW, YDMODEL%YRCST, YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM, &
  & P_OROGLM_T0, P_OROGLL_T0, P_OROGMM_T0, Z_US_T0, Z_VS_T0, YDCPG_DYN0%PREF, YDCPG_DYN0%PRE,                &
  & Z_PDEP_T0, Z_WH2F_T0, Z_PSGRTL_T0, Z_PSGRTM_T0, Z_SGRTSL_T0, Z_SGRTSM_T0, YDCPG_TND%TNDU,    &
  & YDCPG_TND%TNDV, YDCPG_TND%TNDU_NOC, YDCPG_TND%TNDV_NOC,              &
  & Z_TNDUS_T0, Z_TNDVS_T0, YDCPG_DYN0%NHPREH)

ENDIF

!*     3.1.21 COMPUTES [D(gw)/Dt].

IF (LNHEE_SVDLAPL_FIRST) THEN
  ! * Laplacian term computed first, then rhs of (gw) eqn computed.
  !   GNHEE_TNDLAGADIAB_GW is called for LGWADV=T only.
  IF (LGWADV) THEN
    ! warning: formerly LVFE_LAPL.OR.LVFE_GW
    IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHEE 3.1.21: option not yet coded!')

    CALL GNHEE_TNDLAGADIAB_GW(LVFE_GW, LVFE_LAPL_BC, NVDVAR, YDMODEL%YRCST, YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM, &
    & P_OROGLM_T0, P_OROGLL_T0, P_OROGMM_T0, Z_US_T0, Z_VS_T0, Z_TNDUS_T0, Z_TNDVS_T0, YDCPG_DYN0%XYB%LNPR,       &
    & YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%PREF, YDCPG_DYN0%PRE, Z_PDEP_T0, YDCPG_TND%TNDGW                &
    &                     )
  ENDIF
ELSE
  ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
  !   GNH_TNDLAGADIAB_GW may be called for both LGWADV=T and LGWADV=F.
  !   PDBBC is required only if LGWADV=F.
  !   Dataflow may be tricky, and not always convenient to test future variable changes.

  IF (LGWADV.AND.LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(LGWADV, LVERTFE, LVFE_ECMWF, LVFE_GW, LVFE_LAPL_BC, YDMODEL%YRCST, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, PDELP=YDCPG_DYN0%XYB%DELP, &
    & PDELNHPRE=Z_DELNHPRE_T0, PTNDGWF_LAP=Z_TNDGWF_LAP_T0, PTNDGWF_OTH=Z_TNDGWF_OTH_T0)
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_TND%TNDGW(JROF,JLEV)=Z_TNDGWF_LAP_T0(JROF,JLEV)+Z_TNDGWF_OTH_T0(JROF,JLEV)
      ENDDO
    ENDDO
  ELSEIF (LGWADV) THEN
    CALL GNH_TNDLAGADIAB_GW(LGWADV, LVERTFE, LVFE_ECMWF, LVFE_GW, LVFE_LAPL_BC, YDMODEL%YRCST, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, PDEP=Z_PDEP_T0, &
    & PREF=YDCPG_DYN0%PREF, PREH=YDCPG_DYN0%PRE, PTNDGWH_LAP=Z_TNDGWH_LAP_T0, PTNDGWH_OTH=Z_TNDGWH_OTH_T0   &
    &        )
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_TND%TNDGW(JROF,JLEV)=Z_TNDGWH_LAP_T0(JROF,JLEV-1)+Z_TNDGWH_OTH_T0(JROF,JLEV-1)
      ENDDO
    ENDDO
  ELSE IF (LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(LGWADV, LVERTFE, LVFE_ECMWF, LVFE_GW, LVFE_LAPL_BC, YDMODEL%YRCST, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, POROGL=YDCPG_DYN0%OROGL, &
    & POROGM=YDCPG_DYN0%OROGM, POROGLM=P_OROGLM_T0, POROGLL=P_OROGLL_T0, POROGMM=P_OROGMM_T0, PUS=Z_US_T0,           &
    & PVS=Z_VS_T0, PTNDUS=Z_TNDUS_T0, PTNDVS=Z_TNDVS_T0, PDEP=Z_PDEP_T0, PREF=YDCPG_DYN0%PREF, PREH=YDCPG_DYN0%PRE,  &
    & PDELP=YDCPG_DYN0%XYB%DELP, PDELNHPRE=Z_DELNHPRE_T0, PTNDGWH_LAP=Z_TNDGWH_LAP_T0, PTNDGWF_OTH=Z_TNDGWF_OTH_T0,  &
    & PDBBC=YDCPG_DYN0%DBBC)
  ELSE
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_PDEPS_T0(JROF)=YDCPG_DYN0%NHPREH(JROF,NFLEVG)-YDCPG_DYN0%PRE(JROF,NFLEVG)
    ENDDO

    CALL GNH_TNDLAGADIAB_GW(LGWADV, LVERTFE, LVFE_ECMWF, LVFE_GW, LVFE_LAPL_BC, YDMODEL%YRCST, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, POROGL=YDCPG_DYN0%OROGL, &
    & POROGM=YDCPG_DYN0%OROGM, POROGLM=P_OROGLM_T0, POROGLL=P_OROGLL_T0, POROGMM=P_OROGMM_T0, PUS=Z_US_T0,           &
    & PVS=Z_VS_T0, PTNDUS=Z_TNDUS_T0, PTNDVS=Z_TNDVS_T0, PDEP=Z_PDEP_T0, PDEPS=Z_PDEPS_T0, PREF=YDCPG_DYN0%PREF,     &
    & PREH=YDCPG_DYN0%PRE, PTNDGWH_LAP=Z_TNDGWH_LAP_T0, PTNDGWH_OTH=Z_TNDGWH_OTH_T0, PDBBC=YDCPG_DYN0%DBBC           &
    &              )
  ENDIF
ENDIF

!*     3.1.22 COMPUTES [D(pressure departure variable)/Dt].

CALL GNH_TNDLAGADIAB_SPD(NPDVAR, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%RCP%KAP, &
& Z_Z3DIVG_T0, YDCPG_DYN0%CTY%VVEL(:, 1:), YDCPG_TND%TNDPD )

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt].

IF (.NOT.LGWADV) THEN
  ! Remark: PATND(.,.,YYTTND%M_TNDVD) is useless (and not computed) if LGWADV.
  IF (LNHEE_SVDLAPL_FIRST) THEN
    ! * Laplacian term directly computed, does not require the rhs of (gw) eqn.
    !   GNHEE_TNDLAGADIAB_SVD is called for LGWADV=F only.
    IF (LVERTFE) CALL ABOR1(' CPG_GP_NHEE 3.1.23: not yet coded case')

    CALL GNHEE_TNDLAGADIAB_SVD(LVERTFE, NVDVAR, YDMODEL%YRCST, YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM,  &
    & P_OROGLM_T0, P_OROGLL_T0, P_OROGMM_T0, YDCPG_DYN0%CTY%EVEL, YDCPG_DYN0%XYB%RDELP, YDCPG_DYN0%XYB%LNPR,        &
    & YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%PREF, YDCPG_DYN0%PRE, Z_PDEP_T0, YDVARS%U%T0, YDVARS%V%T0, YDCPG_DYN0%UVH%UH, &
    & YDCPG_DYN0%UVH%VH, YDCPG_TND%TNDU, YDCPG_TND%TNDV, Z_TNDUS_T0,                        &
    & Z_TNDVS_T0, YDCPG_DYN0%RDPHI, Z_GWHL_T0, Z_GWHM_T0, YDCPG_DYN0%GWFL, YDCPG_DYN0%GWFM,                         &
    & Z_DVER_T0, Z_DVERW_T0, YDVARS%DIV%T0, Z_Z3DIVG_T0, YDCPG_TND%TNDVD, YDCPG_DYN0%DBBC               &
    &                            )
  ELSE
    ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
    !   GNH_TNDLAGADIAB_GW must have been called above to provide ZTNDGW.. arrays.
    !   Dataflow may be tricky, and not always convenient to test future variable changes.
    CALL GNH_TNDLAGADIAB_SVD(LVFE_GW, LVFE_LAPL_BC, NVDVAR, YDMODEL%YRCST, YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T0, &
    & YDVARS%V%T0, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, YDCPG_DYN0%RDPHI, P_GWHT_T0, YDCPG_DYN0%GWFT,    &
    & Z_GWHL_T0, Z_GWHM_T0, YDCPG_DYN0%GWFL, YDCPG_DYN0%GWFM, Z_DVER_T0, YDVARS%DIV%T0, Z_Z3DIVG_T0,      &
    & Z_TNDGWH_LAP_T0, Z_TNDGWH_OTH_T0, Z_TNDGWF_OTH_T0, Z_PDEP_T0, YDCPG_DYN0%PRE, YDCPG_DYN0%PREF,      &
    & YDCPG_DYN0%XYB%DELP, YDCPG_DYN0%XYB%LNPR, YDCPG_TND%TNDVD )
  ENDIF
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZRCV=1.0_JPRB/(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))-1.0_JPRB
    YDCPG_TND%TNDT(JROF,JLEV)=-ZRCV*YDVARS%T%T0(JROF,JLEV)*Z_Z3DIVG_T0(JROF,JLEV)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

CALL CPG_GP_NHEE_T9 (YDGEOMETRY, YDCPG_BNDS, YDVARS, YDCPG_DYN0, YDMODEL, LDGPXX, LGWADV, LRDBBC, LVERTFE, &
                   & LVFE_GW, L_RDRY_VD, NVDVAR, Z_GPHL_T0, Z_GPHM_T0, Z_NHPPI_T0, Z_NHXS_T0, Z_SVDINCR13_T0, Z_ZERO_T0,  &
                   & YDCPG_DYN9, LDLDIAB, LDMPA)

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE', 1, ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHEE
