#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHEE(YDGEOMETRY,YDTMP,YDVARS,YDCPG_DYN0,YDCPG_DYN9,YDMODEL,YDGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & KST,KEND,KSTGLO,LDGPXX,LDLDIAB,LDMPA,&
 & KIBL,POROG,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & PATND)

!**** *CPG_GP_NHEE* - Grid point calculations at instants t and t-dt for NHEE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHEE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHEE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.

!     INPUT/OUTPUT:
!     -------------
!        PGFL      : unified_treatment grid-point fields at t
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad RT" at full levels.
!        PRT0M     : meridian component of "grad RT" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D (gw)_surf / Dt]_adiab.
!        PRDPHI    : pre/(R T prehyd [Delta log(prehyd)]) at t.
!                    "R" is the version of R (may be Rdry or Rmoist) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PNHYT0    : term 'NHY' at t.
!        PNHYT9    : term 'NHY' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (Apr 2018): introduce key L_RDRY_VD (ensure consistent definition of "dver" everywhere).
!      K. Yessad (July 2018): alternate treatment of Laplacian term in gw and dver equations.
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,ONLY : CPG_DYN_TYPE, CPG_GP_TMP_TYPE
USE YOMGMV   , ONLY : TGMV
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE SC2PRG_MOD,ONLY : SC2PRG
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC
USE YOMCST   , ONLY : RD, RV
USE YOMDYNA  , ONLY : NVDVAR, LGWADV, LRDBBC, LGRADSP, LRUBC, L_RDRY_VD, LNHEE_SVDLAPL_FIRST, &
                    & LNHEE_REFINE_GRP, LNHEE_REFINE_PREH_BBC, LVEREGINT
USE YOMCT3   , ONLY : NSTEP
USE YOMCVER  , ONLY : LVERTFE,LVFE_LAPL_BC,LVFE_GW
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND, YYTHW0, YYTHW9,&
 & YYTCTY0, YYTXYBDER0, YYTRCP0, YYTRCP9, YYTXYB0, YYTXYB9

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_GP_TMP_TYPE),INTENT(INOUT) :: YDTMP
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO
LOGICAL           ,INTENT(IN)    :: LDGPXX
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

!    ==== MISCELLANEOUS ======================
REAL(KIND=JPRB) :: ZXYBDER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYBDER0%NDIM)
REAL(KIND=JPRB) :: ZPDEP0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPDEPS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZPDEP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDELNHPRE0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDELNHPRE9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZLNNHPREF0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZLNNHPREF0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHPRE0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHPRE0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRNHPPI0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRNHPPI9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRRED0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRRED9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR0T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRDT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRTR0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHPPI0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHPPI9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZZ3DIVG(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVERW0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVERW9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS0_L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0_L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS0_M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0_M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZSGRTSL(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZSGRTSM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZPSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTNDUS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTNDVS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTNDGWF_LAP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTNDGWF_OTH(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTNDGWH_LAP(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTNDGWH_OTH(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGPHL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGPHM(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHM(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUVH9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW9%NDIM)
REAL(KIND=JPRB) :: ZNHXS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZZERO(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZWH2F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,2)
REAL(KIND=JPRB) :: ZSVDINCR13(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZRCV

REAL(KIND=JPRB), POINTER :: ZP1FORC(:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "gpgrxyb.intfb.h"
#include "gprcp.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhpreh.intfb.h"
#include "gnhgrpre.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gpflwi.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhee_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gpyy.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gnhd3.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnh_tndlagadiab_spd.intfb.h"
#include "gnh_tndlagadiab_gw.intfb.h"
#include "gnh_tndlagadiab_svd.intfb.h"
#include "gnhee_tndlagadiab_gw.intfb.h"
#include "gnhee_tndlagadiab_svd.intfb.h"
#include "gnhee_svdincr13.intfb.h"
#include "gnhee_refine_grp.intfb.h"
#include "gnhee_refine_preh.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL),YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), &
 & YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE, &
 & NPROMA=>YDGEOMETRY%YRDIM%NPROMA,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)
ASSOCIATE(&
 & YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, &
 & YFORC=>YDMODEL%YRML_GCONF%YGFL%YFORC, &
 & NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
 & LBOUND_D3=>YDMODEL%YRML_DYN%YRDYN%LBOUND_D3,RMAX_D3=>YDMODEL%YRML_DYN%YRDYN%RMAX_D3, &
 & LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH)


CALL SC2PRG(1,YFORC(:)%MP,PGFL ,ZP1FORC)

ZZERO(KST:KEND)=0.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,YDCPG_DYN0%PRE,PRESF=YDCPG_DYN0%PREF, &
          & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
          & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
          & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZRPRE0F(JROF,JLEV)=1.0_JPRB/YDCPG_DYN0%PREF(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB(NPROMA,KST,KEND,NFLEVG,.FALSE.,YDVAB,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,PXYBDER=ZXYBDER0,&
           & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
           & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
           & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,PCP=YDCPG_DYN0%RCP%CP,&
 & PR=YDCPG_DYN0%RCP%R,PKAP=YDCPG_DYN0%RCP%KAP)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & ZRT0,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,PRL=YDVARS%RSPEC%DL,PRM=YDVARS%RSPEC%DM)
ELSE
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & ZRT0,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM)
ENDIF

! ZRDT0 contains (RT) at "t" used in the definition of "dver":
IF (L_RDRY_VD) THEN
  ZRDT0(KST:KEND,1:NFLEVG)=RD*YDVARS%T%T0(KST:KEND,1:NFLEVG)
  ! ky: remark for LSPRT=T:
  !  (ZRDT0L,ZRDT0M) contain grad(Rd*Tv)=grad(RT), and grad(Rd*T) is not available.
  !  That affects only the .NOT.LGWADV case which requires the call to GNHGRGW.
  ZRDT0L(KST:KEND,1:NFLEVG)=RD*YDVARS%T%DL(KST:KEND,1:NFLEVG)
  ZRDT0M(KST:KEND,1:NFLEVG)=RD*YDVARS%T%DM(KST:KEND,1:NFLEVG)
ELSE
  ZRDT0(KST:KEND,1:NFLEVG)=ZRT0(KST:KEND,1:NFLEVG)
  ZRDT0L(KST:KEND,1:NFLEVG)=YDCPG_DYN0%RTL(KST:KEND,1:NFLEVG)
  ZRDT0M(KST:KEND,1:NFLEVG)=YDCPG_DYN0%RTM(KST:KEND,1:NFLEVG)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHEE MODEL.

CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,YDVARS%SPD%T0,YDCPG_DYN0%PREF,&
 & PNHPREF=YDCPG_DYN0%NHPREF,PNHPPI=ZNHPPI0,PRNHPPI=ZRNHPPI0,PDEP=ZPDEP0)
DO JLEV=1,NFLEVG
  ZRRED0(KST:KEND,JLEV)=YDCPG_DYN0%RCP%R(KST:KEND,JLEV)/ZNHPPI0(KST:KEND,JLEV)
  ZRTR0(KST:KEND,JLEV)=ZRRED0(KST:KEND,JLEV)*YDVARS%T%T0(KST:KEND,JLEV)
ENDDO
CALL GNHPREH(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,YDVARS%SPD%T0,YDCPG_DYN0%PRE,&
 & YDCPG_DYN0%XYB%DELP,YDCPG_DYN0%XYB%LNPR,ZNHPPI0,YDCPG_DYN0%NHPREF,YDCPG_DYN0%NHPREH,ZDELNHPRE0)
CALL GNHGRPRE(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,&
 & YDCPG_DYN0%NHPREF,YDVARS%SPD%DL,YDVARS%SPD%DM,&
 & ZNHPRE0L,ZNHPRE0M,ZLNNHPREF0L,ZLNNHPREF0M,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM)

!*     3.1.7 COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            "omega/prehyd" and "W".

CALL GPCTY(YDVFE,NPROMA,KST,KEND,NFLEVG,LRUBC,YDVAB,YDVETA,&
 & YDVARS%U%T0,YDVARS%V%T0,YDVARS%DIV%T0,YDVARS%EDOT%T0,&
 & PSPL=YDCPG_DYN0%PREL,PSPM=YDCPG_DYN0%PREM,PRPREF=ZRPRE0F,&
 & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
 & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
 & PRPP=YDCPG_DYN0%XYB%RPP, PEVEL=YDCPG_DYN0%CTY%EVEL, PVVEL=YDCPG_DYN0%CTY%VVEL, &
 & PPSDIV=YDCPG_DYN0%CTY%PSDIV, PPSDVBC=YDCPG_DYN0%CTY%PSDVBC, PDIVDP=YDCPG_DYN0%CTY%DIVDP)

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,NPROMA,NFLEVG,KEND,NSTEP,KSTGLO,YDCPG_DYN0%PRE,YDCPG_DYN0%CTY%DIVDP(:,1:))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,KST,KEND, &
   & ZP1FORC,YDCPG_DYN0%PREF,YDVARS%T%T0,YDCPG_DYN0%RCP%R,YDCPG_DYN0%CTY%EVEL,YDCPG_DYN0%CTY%VVEL(:,1:))
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

YDCPG_DYN0%PHI(KST:KEND,NFLEVG)=POROG(KST:KEND)
CALL GPGEO(NPROMA,KST,KEND,NFLEVG,YDCPG_DYN0%PHI,YDCPG_DYN0%PHIF,YDVARS%T%T0,&
 & ZRRED0,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY,NPROMA,KST,KEND,NFLEVG,&
 & ZRT0,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
 & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
 & YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,ZGPHL,ZGPHM,&
 & LDNHEE=.TRUE.,PRNHPPI=ZRNHPPI0,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM)

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    YDCPG_DYN0%KENE(JROF,JLEV)=0.5_JPRB*(YDVARS%U%T0(JROF,JLEV)**2+YDVARS%V%T0(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,YDCPG_DYN0%XYB%LNPR,&
 & YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%UVH%WWI (1:,1:),LDVERINT=LVEREGINT)
CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,YDVARS%U%T0,YDVARS%V%T0,&
          & PWWI=YDCPG_DYN0%UVH%WWI,PUH=YDCPG_DYN0%UVH%UH,PVH=YDCPG_DYN0%UVH%VH)

LLDER=.TRUE.
CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,&
 & YDVARS%U%T0,YDVARS%V%T0,ZUS0,ZVS0,&
 & PDIVF=YDVARS%DIV%T0,PVORF=YDVARS%VOR%T0,&
 & PUFL=YDVARS%U%DL,PVFL=YDVARS%V%DL,&
 & PUS_L=ZUS0_L,PVS_L=ZVS0_L,PUS_M=ZUS0_M,PVS_M=ZVS0_M)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

IF (LNHEE_REFINE_GRP) THEN
  CALL GPFLWI(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND, &
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PRE,&
   & YDCPG_DYN0%PREF,YDCPG_DYN0%XYB%DELP,ZWH2F,LDVERINT=LVEREGINT)

  CALL GNHEE_REFINE_GRP(YDGEOMETRY,KST,KEND,&
   & ZRT0,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
   & ZRNHPPI0,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM,ZGPHL,ZGPHM,       &
   & ZPDEP0,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,ZWH2F,&
   & ZPSGRTL,ZPSGRTM)
ELSE
  CALL GNHEE_GRP(YDGEOMETRY,KST,KEND,&
   & ZRT0,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,YDCPG_DYN0%XYB%RDELP,YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
   & ZDELNHPRE0,ZLNNHPREF0L,ZLNNHPREF0M,ZRNHPPI0,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM,&
   & ZGPHL,ZGPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
   & ZPSGRTL,ZPSGRTM)
ENDIF

! store for next time-step
IF( LGRADSP ) THEN
  ! adjust field with filter result
  ZSGRTL(KST:KEND,1:NFLEVG)=ZPSGRTL(KST:KEND,1:NFLEVG)&
   & - (YDVARS%SGRTL%T9(KST:KEND,1:NFLEVG) - YDVARS%SGRTL%T0(KST:KEND,1:NFLEVG))
  ZSGRTM(KST:KEND,1:NFLEVG)=ZPSGRTM(KST:KEND,1:NFLEVG)&
   & - (YDVARS%SGRTM%T9(KST:KEND,1:NFLEVG) - YDVARS%SGRTM%T0(KST:KEND,1:NFLEVG))

  ! store for next time-step unfiltered fields
  YDVARS%SGRTL%T0(KST:KEND,1:NFLEVG)=ZPSGRTL(KST:KEND,1:NFLEVG)
  YDVARS%SGRTM%T0(KST:KEND,1:NFLEVG)=ZPSGRTM(KST:KEND,1:NFLEVG)

  ! set new values for current timestep
  ZPSGRTL(KST:KEND,1:NFLEVG)=ZSGRTL(KST:KEND,1:NFLEVG)
  ZPSGRTM(KST:KEND,1:NFLEVG)=ZSGRTM(KST:KEND,1:NFLEVG)

ENDIF

ZSGRTSL(KST:KEND)=ZPSGRTL(KST:KEND,NFLEVG)
ZSGRTSM(KST:KEND)=ZPSGRTM(KST:KEND,NFLEVG)

!*     3.1.13a COMPUTES TERM "NHX"

IF (LDGPXX) THEN
  CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
   & YDCPG_DYN0%XYB%LNPR,ZRT0,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & ZNHXS,PNHPPI=ZNHPPI0)
   ! PNHXT0 always stores NHX
  IF (NVDVAR == 3 .OR. NVDVAR == 4) THEN
    ! NHX = NHX_S
    YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)=ZNHXS(KST:KEND,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    ! NHX = NHX_S - (d13 - dver)
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=ZRDT0,PNHPPI=ZNHPPI0,&
     & PSVDINCR13=ZSVDINCR13)
    YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)=ZNHXS(KST:KEND,1:NFLEVG)-ZSVDINCR13(KST:KEND,1:NFLEVG)
  ENDIF
ELSE
  ! This case only occurs for a subset of NVDVAR= 4 or 5 cases
  IF (NVDVAR == 4) THEN
    YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)=YDVARS%NHX%T0(KST:KEND,1:NFLEVG)
    ! NHX_S
    ZNHXS(KST:KEND,1:NFLEVG) =YDVARS%NHX%T0(KST:KEND,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)=YDVARS%NHX%T0(KST:KEND,1:NFLEVG)
    ! NHX_S = NHX + (d13 - dver)
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=ZRDT0,PNHPPI=ZNHPPI0,&
     & PSVDINCR13=ZSVDINCR13)
    ZNHXS(KST:KEND,1:NFLEVG)=YDVARS%NHX%T0(KST:KEND,1:NFLEVG)+ZSVDINCR13(KST:KEND,1:NFLEVG)
  ENDIF
ENDIF

!*     3.1.13b COMPUTES TERM "NHY"
IF (NVDVAR == 5 .AND. LGWADV) THEN
  CALL GPYY(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & YDCPG_DYN0%NHY)
ELSE
  YDCPG_DYN0%NHY(KST:KEND,0:NFLEVG) = 0.0_JPRB
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

IF (NVDVAR == 3) THEN
  ZDVER0(KST:KEND,1:NFLEVG)=YDVARS%SVD%T0(KST:KEND,1:NFLEVG)
  ZDVER0L(KST:KEND,1:NFLEVG)=YDVARS%SVD%DL(KST:KEND,1:NFLEVG)
  ZDVER0M(KST:KEND,1:NFLEVG)=YDVARS%SVD%DM(KST:KEND,1:NFLEVG)
ELSEIF (NVDVAR == 4) THEN
  ZDVER0(KST:KEND,1:NFLEVG)=YDVARS%SVD%T0(KST:KEND,1:NFLEVG)-YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)
  ZDVER0L(KST:KEND,1:NFLEVG)=YDVARS%SVD%DL(KST:KEND,1:NFLEVG)-YDVARS%NHX%DL(KST:KEND,1:NFLEVG)
  ZDVER0M(KST:KEND,1:NFLEVG)=YDVARS%SVD%DM(KST:KEND,1:NFLEVG)-YDVARS%NHX%DM(KST:KEND,1:NFLEVG)
ELSEIF (NVDVAR == 5) THEN
  ! d13 in ZDVERW0
  ZDVERW0(KST:KEND,1:NFLEVG)=YDVARS%SVD%T0(KST:KEND,1:NFLEVG)-YDCPG_DYN0%NHX(KST:KEND,1:NFLEVG)
  ! dver in ZDVER0: dver = d13 - (d13 - dver)
  CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
   & YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & PLNPR=YDCPG_DYN0%XYB%LNPR,PRT=ZRDT0,PNHPPI=ZNHPPI0,&
   & PSVDINCR13=ZSVDINCR13)
  ZDVER0(KST:KEND,1:NFLEVG)=ZDVERW0(KST:KEND,1:NFLEVG)-ZSVDINCR13(KST:KEND,1:NFLEVG)
  ! grad(dver) is not used; ZDVER0[L,M] are used to store grad(d13)
  ZDVER0L(KST:KEND,1:NFLEVG)=YDVARS%SVD%DL(KST:KEND,1:NFLEVG)-YDVARS%NHX%DL(KST:KEND,1:NFLEVG)
  ZDVER0M(KST:KEND,1:NFLEVG)=YDVARS%SVD%DM(KST:KEND,1:NFLEVG)-YDVARS%NHX%DM(KST:KEND,1:NFLEVG)
ENDIF

!*     3.1.16 COMPUTES term [gw] or [gW].

! Caution: must use consistent definitions of RT in ZRDT0 and ZDVER0.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL2TL)
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZUS0,ZVS0,&
     & ZRDT0,ZDVER0,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=ZRNHPPI0,PGDW=YDTMP%T0%GDW)

    YDCPG_DYN0%GWT(KST:KEND,1:NFLEVG)=YDCPG_DYN0%GWFT(KST:KEND,1:NFLEVG)
  ELSE
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZUS0,ZVS0,&
     & ZRDT0,ZDVER0,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=ZRNHPPI0)

    IF (LGWADV) YDCPG_DYN0%GWT(KST:KEND,1:NFLEVG)=YDTMP%T0%GWHT(KST:KEND,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains (gw)_surf
    YDCPG_DYN0%GWS(KST:KEND)=YDTMP%T0%GWHT(KST:KEND,NFLEVG)
  ENDIF
ELSEIF (NVDVAR==5) THEN
  ! Case LVFE_GW is ignored and not coded.
  ! -- Modified W from d13; W_surf=0, this is why ZZERO enters GPGW instead of
  ! (POROGL,POROGM,ZUS0,ZVS0)
  LLGWF0=.TRUE.
  LLGDWI=.FALSE.
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
   & ZZERO,ZZERO,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZZERO,ZZERO,&
   & ZRDT0,ZDVERW0,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PRNHPPI=ZRNHPPI0)
  IF (LGWADV) THEN
    YDCPG_DYN0%GWT(KST:KEND,1:NFLEVG)=YDTMP%T0%GWHT(KST:KEND,0:NFLEVG-1)
  ENDIF

  IF (LTWOTL.AND.LRDBBC) THEN
    ! PGWS contains 0
    YDCPG_DYN0%GWS(KST:KEND)=0.0_JPRB
  ENDIF
ENDIF

!*     3.1.17 COMPUTES grad(gw) or  grad(gW).

! Caution: must use consistent definitions of RT in ZRDT0,ZRDT0L,ZRDT0M, and
! ZDVER0,ZDVER0L,ZDVER0M.
LLDER=.TRUE.
IF (NVDVAR==3 .OR. NVDVAR==4) THEN
  CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,LLDER,&
   & ZRDT0,ZRDT0L,ZRDT0M,&
   & ZDVER0,ZDVER0L,ZDVER0M,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
   & ZUS0,ZVS0,ZUS0_L,ZVS0_L,ZUS0_M,ZVS0_M,&
   & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
   & YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,ZGWHL,ZGWHM,&
   & LDNHEE=.TRUE.,PRNHPPI=ZRNHPPI0,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM)
ELSEIF (NVDVAR==5) THEN
  ! -- grad(gW) from d13 and grad(d13); grad(W)_surf=0, this is why ZZERO enters
  ! GNHGRGW instead of (POROG.. and Z[U,V]S0.. arrays)
  CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,LLDER,&
   & ZRDT0,ZRDT0L,ZRDT0M,&
   & ZDVERW0,ZDVER0L,ZDVER0M,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
   & ZZERO,ZZERO,ZZERO,ZZERO,ZZERO,ZZERO,&
   & ZZERO,ZZERO,ZZERO,ZZERO,ZZERO,&
   & YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,ZGWHL,ZGWHM,&
   & LDNHEE=.TRUE.,PRNHPPI=ZRNHPPI0,PQCHAL=YDCPG_DYN0%QCHAL,PQCHAM=YDCPG_DYN0%QCHAM)
ENDIF

!*     3.1.18 COMPUTES D3.

CALL GNHD3(NFLEVG,NPROMA,KST,KEND,&
 & YDVARS%DIV%T0,ZNHXS,ZDVER0,YDCPG_DYN0%RCP%R,RMAX_D3,LBOUND_D3,ZZ3DIVG)

!*     3.1.19 COMPUTES pre/(Rd T prehyd [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  IF (L_RDRY_VD) THEN
    ! ky: caution, PRDPHI is currently computed with R instead of Rd (by error),
    !  but Rd would be the right value.
    !  The right code should be:
    !  PRDPHI(.,.)=1.0_JPRB/(PXYB0(.,.,YYTXYB0%M_LNPR)*RD*PGMV(.,.,YT0%MT)*ZRNHPPI0(.,.))
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*ZRTR0(JROF,JLEV))
      ENDDO
    ENDDO
  ELSE
    ! ky: PRDPHI uses the right (moist) version of "R" in this case.
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*ZRTR0(JROF,JLEV))
      ENDDO
    ENDDO
  ENDIF
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY,YDGMV,YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_DYN%YRDYN,&
 & KST,KEND,YDGSGEOM%RCORI,&
 & YDGSGEOM%GNORDL,YDGSGEOM%GNORDM,&
 & ZPSGRTL,ZPSGRTM,&
 & YDVARS%U%T0,YDVARS%V%T0,PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
 & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC))

!*     3.1.20b COMPUTES [D V_surf/Dt].

IF (.NOT.LGWADV.OR.LNHEE_SVDLAPL_FIRST.OR.LNHEE_REFINE_PREH_BBC) THEN
  CALL GNH_TNDLAGADIAB_UVS(NPROMA,KST,KEND,LRPLANE,&
   & YDGSGEOM%RCORI,YDCSGEOM%RATATX,YDCSGEOM%RATATH,&
   & ZUS0,ZVS0,ZSGRTSL,ZSGRTSM,&
   & ZTNDUS,ZTNDVS)
ENDIF

!*     3.1.20c REFINE half-level pre, pres. grad. term, [D V/Dt] at bottom

IF (LNHEE_REFINE_PREH_BBC) THEN

  ! remark (K.Y.) for option LGRADSP: filter is applied to main part of the
  ! pressure gradient term computed in the predictor step,                
  ! but no filter is applied to the correction done in GNHEE_REFINE_PREH.
  ! But I do not guarantee that LGRADSP=T and LNHEE_REFINE_PREH_BBC=T are
  ! consistent.

  CALL GNHEE_REFINE_PREH(YDGEOMETRY,KST,KEND,&
    & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
    & ZUS0,ZVS0,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,ZPDEP0,ZWH2F,&
    & ZPSGRTL,ZPSGRTM,ZSGRTSL,ZSGRTSM,&
    & PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
    & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC),&
    & ZTNDUS,ZTNDVS,YDCPG_DYN0%NHPREH)

ENDIF

!*     3.1.21 COMPUTES [D(gw)/Dt].

IF (LNHEE_SVDLAPL_FIRST) THEN
  ! * Laplacian term computed first, then rhs of (gw) eqn computed.
  !   GNHEE_TNDLAGADIAB_GW is called for LGWADV=T only.
  IF (LGWADV) THEN
    ! warning: formerly LVFE_LAPL.OR.LVFE_GW
    IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHEE 3.1.21: option not yet coded!')

    CALL GNHEE_TNDLAGADIAB_GW(YDGEOMETRY,KST,KEND,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
     & ZUS0,ZVS0,ZTNDUS,ZTNDVS,&
     & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,ZPDEP0,&
     & PATND(1,1,YYTTND%M_TNDGW))
  ENDIF
ELSE
  ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
  !   GNH_TNDLAGADIAB_GW may be called for both LGWADV=T and LGWADV=F.
  !   PDBBC is required only if LGWADV=F.
  !   Dataflow may be tricky, and not always convenient to test future variable changes.

  IF (LGWADV.AND.LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & PDELP=YDCPG_DYN0%XYB%DELP,PDELNHPRE=ZDELNHPRE0,&
     & PTNDGWF_LAP=ZTNDGWF_LAP,PTNDGWF_OTH=ZTNDGWF_OTH)
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        PATND(JROF,JLEV,YYTTND%M_TNDGW)=ZTNDGWF_LAP(JROF,JLEV)+ZTNDGWF_OTH(JROF,JLEV)
      ENDDO
    ENDDO
  ELSEIF (LGWADV) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & PDEP=ZPDEP0,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PTNDGWH_LAP=ZTNDGWH_LAP,PTNDGWH_OTH=ZTNDGWH_OTH)
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        PATND(JROF,JLEV,YYTTND%M_TNDGW)=ZTNDGWH_LAP(JROF,JLEV-1)+ZTNDGWH_OTH(JROF,JLEV-1)
      ENDDO
    ENDDO
  ELSE IF (LVFE_GW) THEN
    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
     & POROGLM=YDTMP%T0%OROGLM,POROGLL=YDTMP%T0%OROGLL,POROGMM=YDTMP%T0%OROGMM,&
     & PUS=ZUS0,PVS=ZVS0,PTNDUS=ZTNDUS,PTNDVS=ZTNDVS,&
     & PDEP=ZPDEP0,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PDELP=YDCPG_DYN0%XYB%DELP,PDELNHPRE=ZDELNHPRE0,&
     & PTNDGWH_LAP=ZTNDGWH_LAP,PTNDGWF_OTH=ZTNDGWF_OTH,PDBBC=YDCPG_DYN0%DBBC)
  ELSE
    DO JROF=KST,KEND
      ZPDEPS0(JROF)=YDCPG_DYN0%NHPREH(JROF,NFLEVG)-YDCPG_DYN0%PRE(JROF,NFLEVG)
    ENDDO

    CALL GNH_TNDLAGADIAB_GW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
     & POROGLM=YDTMP%T0%OROGLM,POROGLL=YDTMP%T0%OROGLL,POROGMM=YDTMP%T0%OROGMM,&
     & PUS=ZUS0,PVS=ZVS0,PTNDUS=ZTNDUS,PTNDVS=ZTNDVS,&
     & PDEP=ZPDEP0,PDEPS=ZPDEPS0,PREF=YDCPG_DYN0%PREF,PREH=YDCPG_DYN0%PRE,&
     & PTNDGWH_LAP=ZTNDGWH_LAP,PTNDGWH_OTH=ZTNDGWH_OTH,PDBBC=YDCPG_DYN0%DBBC)
  ENDIF
ENDIF

!*     3.1.22 COMPUTES [D(pressure departure variable)/Dt].

CALL GNH_TNDLAGADIAB_SPD(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
 & YDCPG_DYN0%RCP%KAP,ZZ3DIVG,YDCPG_DYN0%CTY%VVEL(:,1:),&
 & PATND(1,1,YYTTND%M_TNDPD) )

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt].

IF (.NOT.LGWADV) THEN
  ! Remark: PATND(.,.,YYTTND%M_TNDVD) is useless (and not computed) if LGWADV.
  IF (LNHEE_SVDLAPL_FIRST) THEN
    ! * Laplacian term directly computed, does not require the rhs of (gw) eqn.
    !   GNHEE_TNDLAGADIAB_SVD is called for LGWADV=F only.
    IF (LVERTFE) CALL ABOR1(' CPG_GP_NHEE 3.1.23: not yet coded case')

    CALL GNHEE_TNDLAGADIAB_SVD(YDGEOMETRY,KST,KEND,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
     & YDCPG_DYN0%CTY%EVEL,YDCPG_DYN0%XYB%RDELP,&
     & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,ZPDEP0,&
     & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),ZTNDUS,ZTNDVS,&
     & YDCPG_DYN0%RDPHI,ZGWHL,ZGWHM,YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,ZDVER0,ZDVERW0,YDVARS%DIV%T0,ZZ3DIVG,&
     & PATND(1,1,YYTTND%M_TNDVD),YDCPG_DYN0%DBBC)
  ELSE
    ! * rhs of (gw) eqn computed first, then Laplacian term in "dver" eqn computed.
    !   GNH_TNDLAGADIAB_GW must have been called above to provide ZTNDGW.. arrays.
    !   Dataflow may be tricky, and not always convenient to test future variable changes.
    CALL GNH_TNDLAGADIAB_SVD(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
     & YDCPG_DYN0%RDPHI,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,ZGWHL,ZGWHM,YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,ZDVER0,YDVARS%DIV%T0,ZZ3DIVG,&
     & ZTNDGWH_LAP,ZTNDGWH_OTH,ZTNDGWF_OTH,&
     & ZPDEP0,YDCPG_DYN0%PRE,YDCPG_DYN0%PREF,YDCPG_DYN0%XYB%DELP,YDCPG_DYN0%XYB%LNPR,&
     & PATND(1,1,YYTTND%M_TNDVD) )
  ENDIF
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    ZRCV=1.0_JPRB/(1.0_JPRB-YDCPG_DYN0%RCP%KAP(JROF,JLEV))-1.0_JPRB
    PATND(JROF,JLEV,YYTTND%M_TNDT)=-ZRCV*YDVARS%T%T0(JROF,JLEV)*ZZ3DIVG(JROF,JLEV)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,YDCPG_DYN9%PRE,PRESF=YDCPG_DYN9%PREF,&
            & PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, &
            & PALPH=YDCPG_DYN9%XYB%ALPH, PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE,  &
            & PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=YDCPG_DYN9%RCP%CP,&
   & PR=YDCPG_DYN9%RCP%R)

!*     3.2.4 COMPUTE "RT".

  ! ZR0T9 is used in calculation of NHX(t-dt).
  ZR0T9(KST:KEND,1:NFLEVG)=YDVARS%T%T9(KST:KEND,1:NFLEVG)*YDCPG_DYN0%RCP%R(KST:KEND,1:NFLEVG)

  ! ZRDT9 is the (RT) at "t-dt" used in the definition of "dver":
  IF (L_RDRY_VD) THEN
    ZRDT9(KST:KEND,1:NFLEVG)=RD*YDVARS%T%T9(KST:KEND,1:NFLEVG)
  ELSE
    ! use of R_moist at time t to be consistent with ZR0T9 and ZDVER9.
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        ZRDT9(JROF,JLEV)=YDCPG_DYN0%RCP%R(JROF,JLEV)*YDVARS%T%T9(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF

!*     3.2.6 COMPUTE QUANTITIES FOR NHEE MODEL.

  CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,YDVARS%SPD%T9,YDCPG_DYN9%PREF,&
   & PNHPREF=YDCPG_DYN9%NHPREF,PNHPPI=ZNHPPI9,PRNHPPI=ZRNHPPI9,PDEP=ZPDEP9)
  DO JLEV=1,NFLEVG
    ZRRED9(KST:KEND,JLEV)=YDCPG_DYN9%RCP%R(KST:KEND,JLEV)/ZNHPPI9(KST:KEND,JLEV)
  ENDDO
  CALL GNHPREH(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,YDVARS%SPD%T9,YDCPG_DYN9%PRE,&
   & YDCPG_DYN9%XYB%DELP,YDCPG_DYN9%XYB%LNPR,ZNHPPI9,YDCPG_DYN9%NHPREF,YDCPG_DYN9%NHPREH,ZDELNHPRE9)

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF (LDLDIAB.OR.LSIMPH) THEN
    YDCPG_DYN9%PHI(KST:KEND,NFLEVG)=POROG(KST:KEND)
    CALL GPGEO(NPROMA,KST,KEND,NFLEVG,YDCPG_DYN9%PHI,YDCPG_DYN9%PHIF,YDVARS%T%T9,&
     & ZRRED9,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,&
     & YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  ! Not needed if Z-term and X-term are calculated FE
  IF (LDGPXX .AND. .NOT.LVERTFE) THEN
    ! * same remark as for time t half-level winds.
    ZUVH9(KST:KEND,1:NFLEVG,YYTHW9%M_WWI)=YDCPG_DYN0%UVH%WWI(KST:KEND,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,YDVARS%U%T9,YDVARS%V%T9,ZUVH9)
  ENDIF

  LLDER=.FALSE.
  CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,YDVARS%U%T9,YDVARS%V%T9,ZUS9,ZVS9)

!*     3.2.13a COMPUTES TERM "NHX".

  ! caution: must use "R" and "pre/prehyd" at time t.
  IF (LDGPXX) THEN
    CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
     & YDCPG_DYN9%XYB%LNPR,ZR0T9,&
     & YDVARS%U%T9,YDVARS%V%T9,ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),&
     & ZNHXS,PNHPPI=ZNHPPI0)
    IF (NVDVAR == 3 .OR. NVDVAR == 4) THEN
      ! NHX = NHX_S
      YDCPG_DYN9%NHX(KST:KEND,1:NFLEVG)=ZNHXS(KST:KEND,1:NFLEVG)
    ELSEIF (NVDVAR == 5) THEN
      ! NHX = NHX_S - (d13 - dver)
      CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
       & ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),&
       & PLNPR=YDCPG_DYN9%XYB%LNPR,PRT=ZRDT9,PNHPPI=ZNHPPI9,&
       & PSVDINCR13=ZSVDINCR13)
      YDCPG_DYN9%NHX(KST:KEND,1:NFLEVG)=ZNHXS(KST:KEND,1:NFLEVG)-ZSVDINCR13(KST:KEND,1:NFLEVG)
    ENDIF
  ELSE
    ! This case only occurs for a subset of NVDVAR= 4 or 5 cases
    ! PNHXT9 and PGMV.,.,YT9%MNHX) always stores NHX; calculation of NHX_S is
    ! useless.
    YDCPG_DYN9%NHX(KST:KEND,1:NFLEVG)=YDVARS%NHX%T9(KST:KEND,1:NFLEVG)
  ENDIF

  !*     3.1.13b COMPUTES TERM "NHY"

  IF (NVDVAR == 5 .AND. LGWADV) THEN
    CALL GPYY(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),YDCPG_DYN9%NHY)
  ELSE
    YDCPG_DYN9%NHY(KST:KEND,0:NFLEVG) = 0.0_JPRB
  ENDIF


!*     3.2.14 COMPUTES dver.

  IF (NVDVAR == 3) THEN
    ZDVER9(KST:KEND,1:NFLEVG)=YDVARS%SVD%T9(KST:KEND,1:NFLEVG)
  ELSEIF (NVDVAR == 4) THEN
    ZDVER9(KST:KEND,1:NFLEVG)=YDVARS%SVD%T9(KST:KEND,1:NFLEVG)-YDCPG_DYN9%NHX(KST:KEND,1:NFLEVG)
  ELSEIF (NVDVAR == 5) THEN
    ! d13 in ZDVERW9
    ZDVERW9(KST:KEND,1:NFLEVG)=YDVARS%SVD%T9(KST:KEND,1:NFLEVG)-YDCPG_DYN9%NHX(KST:KEND,1:NFLEVG)
    ! dver in ZDVER9
    CALL GNHEE_SVDINCR13(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,&
     & ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),&
     & PLNPR=YDCPG_DYN9%XYB%LNPR,PRT=ZRDT9,PNHPPI=ZNHPPI9,&
     & PSVDINCR13=ZSVDINCR13)
    ZDVER9(KST:KEND,1:NFLEVG)=ZDVERW9(KST:KEND,1:NFLEVG)-ZSVDINCR13(KST:KEND,1:NFLEVG)
  ENDIF

!*     3.2.16 COMPUTES term [gw] or [gW].

  ! Caution: must use consistent definitions of RT in ZRDT9 and ZDVER9.
  IF (NVDVAR==3 .OR. NVDVAR==4) THEN
    LLGDWI=.FALSE.
    IF (LGWADV.AND.LVFE_GW) THEN
      ! PGDW is required (at least for SL3TL)
      CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
       & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZUS9,ZVS9,&
       & ZRDT9,ZDVER9,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=ZRNHPPI9,PGDW=YDTMP%T9%GDW)

      YDCPG_DYN9%GWT(KST:KEND,1:NFLEVG)=YDCPG_DYN9%GWFT(KST:KEND,1:NFLEVG)
    ELSE
      CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
       & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZUS9,ZVS9,&
       & ZRDT9,ZDVER9,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=ZRNHPPI9)

      IF (LGWADV) YDCPG_DYN9%GWT(KST:KEND,1:NFLEVG)=YDTMP%T9%GWHT(KST:KEND,0:NFLEVG-1)
    ENDIF

    IF (LRDBBC) THEN
      ! PGWS contains (gw)_surf
      YDCPG_DYN0%GWS(KST:KEND)=YDTMP%T9%GWHT(KST:KEND,NFLEVG)
    ENDIF
  ELSEIF (NVDVAR==5) THEN
    ! Case LVFE_GW is ignored and not coded.
    ! -- Modified W
    LLGDWI=.FALSE.
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
     & ZZERO,ZZERO,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZZERO,ZZERO,&
     & ZRDT9,ZDVERW9,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PRNHPPI=ZRNHPPI9)

    IF (LGWADV) YDCPG_DYN9%GWT(KST:KEND,1:NFLEVG)=YDTMP%T9%GWHT(KST:KEND,0:NFLEVG-1)

    IF (LRDBBC) THEN
      ! PGWS contains 0
      YDCPG_DYN0%GWS(KST:KEND)=0.0_JPRB
    ENDIF
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHEE',1,ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHEE
