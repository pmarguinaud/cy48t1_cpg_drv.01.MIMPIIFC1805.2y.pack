#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHQE(YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, YDVARS, YDCPG_DYN0, YDCPG_DYN9, YDMODEL, &
& LDGPXX, LDLDIAB, LDMPA, P_OROGLL_T0, P_OROGMM_T0, P_OROGLM_T0, P_GDW_T0, P_GWHT_T0, &
& PATND, PP1FORC)

!**** *CPG_GP_NHQE* - Grid point calculations at instants t and t-dt for NHQE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHQE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHQE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.
!        PRE9L     : zonal component of "grad prehyds" at t-dt.
!        PRE9M     : meridian component of "grad prehyds" at t-dt.

!     INPUT/OUTPUT:
!     -------------
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad R Tt" at full levels.
!        PRT0M     : meridian component of "grad R Tt" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D(gw)_surf/Dt]_adiab.
!        PRDPHI    : 1/(R Tt [Delta log(prehyd)]) at t.
!                    "R" is the version of R (Rmoist in NHQE model) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (March 2018): rewrite Laplacian term in NHQE model.
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,ONLY : CPG_DYN_TYPE
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC
USE YOMDYNA  , ONLY : LGWADV, LRDBBC, LGRADSP, LRUBC
USE YOMCVER  , ONLY : LVERTFE, LVFE_GW,LVFE_LAPL_BC
USE YOMCT3   , ONLY : NSTEP
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),INTENT(IN)   :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),INTENT(IN)   :: YDCPG_OPTS
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
LOGICAL           ,INTENT(IN)    :: LDGPXX
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA

REAL(KIND=JPRB)   ,INTENT(INOUT) :: P_GDW_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: P_GWHT_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: P_OROGLL_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: P_OROGLM_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: P_OROGMM_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT), OPTIONAL :: PP1FORC (:,:)

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "gphpre.intfb.h"
#include "gpgrxyb_expl.intfb.h"
#include "gprcp_ydvars.intfb.h"
#include "gprcph.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhqe_preh.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhqe_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gnhqe_xxd.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnhqe_tndlagadiab_gw.intfb.h"
#include "gnhqe_tndlagadiab_svd.intfb.h"

!     ------------------------------------------------------------------

REAL (KIND=JPRB) :: Z_DVERL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_DVERM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_DVER_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_DVER_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_EIQCHAF_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_EQCHAF_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_EQCHAH_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GDW_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GPHL_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GPHM_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHL_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHM_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_GWHT_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_KAPH_T0 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXD_T_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXD_T_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXS_T_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_NHXS_T_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_ONE_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_PSGRTL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_PSGRTM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_R0T_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_R9T_T9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RPREF_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_RT_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_SGRTSL_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_SGRTSM_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_TNDUS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_TNDVS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_L_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_M_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_US_T9 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_UVH_UH_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_UVH_VH_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_UVH_WWI_T9 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_VS_L_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_VS_M_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_VS_T0 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_VS_T9 (YDGEOMETRY%YRDIM%NPROMA)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHPLL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_ALPHPLM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFAPL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFA_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_COEFD_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_LNPRL_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL (KIND=JPRB) :: Z_XYBDER_LNPRM_T0 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE', 0, ZHOOK_HANDLE)

ASSOCIATE(    YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, NPROMA=>YDGEOMETRY%YRDIM%NPROMA, &
& NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, YDCST => YDMODEL%YRCST)
ASSOCIATE(  YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
& LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH, RD => YDCST%RD, RV => YDCST%RV)

!     ------------------------------------------------------------------

!*       0.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

Z_ONE_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=1.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVAB, YDCPG_DYN0%PRE, PRESF=YDCPG_DYN0%PREF,   &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH, &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_RPREF_T0(JROF,JLEV)=1.0_JPRB/YDCPG_DYN0%PREF(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

CALL GPGRXYB_EXPL(NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, .FALSE., YDVAB, YDCPG_DYN0%PREL, YDCPG_DYN0%PREM,         &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH,                 &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP, PCOEFD_DER=Z_XYBDER_COEFD_T0,                  &
& PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0, PCOEFA_DER=Z_XYBDER_COEFA_T0, PCOEFAPL_DER=Z_XYBDER_COEFAPL_T0,   &
& PALPHPLL_DER=Z_XYBDER_ALPHPLL_T0, PALPHPLM_DER=Z_XYBDER_ALPHPLM_T0, PALPHL_DER=Z_XYBDER_ALPHL_T0, PALPHM_DER=Z_XYBDER_ALPHM_T0&
&   )

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP_YDVARS (NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDVARS=YDVARS, PCP=YDCPG_DYN0%RCP%CP, &
                 & PR=YDCPG_DYN0%RCP%R, PKAP=YDCPG_DYN0%RCP%KAP)
CALL GPRCPH(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%RCP%KAP, Z_KAPH_T0)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, RD, RV, YDCPG_DYN0%RCP%R, YDVARS%T%T0,         &
  & YDVARS%T%DL, YDVARS%T%DM, YDVARS%Q%DL, YDVARS%Q%DM, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM, PRL=YDVARS%RSPEC%DL, &
  & PRM=YDVARS%RSPEC%DM)
ELSE
  CALL GPRT(LSPRT, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, RD, RV, YDCPG_DYN0%RCP%R, YDVARS%T%T0, &
  & YDVARS%T%DL, YDVARS%T%DM, YDVARS%Q%DL, YDVARS%Q%DM, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHQE MODEL.

CALL GNHPRE(YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%SPD%T0, YDCPG_DYN0%PREF, &
& PKAP=YDCPG_DYN0%RCP%KAP, PNHPREF=YDCPG_DYN0%NHPREF, PEQCHAF=Z_EQCHAF_T0, PEIQCHAF=Z_EIQCHAF_T0            &
&                     )
CALL GNHQE_PREH(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%PRE, YDVARS%SPD%T0, YDCPG_DYN0%NHPREH, &
& PREF=YDCPG_DYN0%PREF, PEQCHAF=Z_EQCHAF_T0, PKAPH=Z_KAPH_T0, PEQCHAH=Z_EQCHAH_T0)

YDCPG_DYN0%QCHAL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SPD%DL(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
YDCPG_DYN0%QCHAM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%SPD%DM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

!*     3.1.7c COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            and "omega/prehyd".

CALL GPCTY(YDVFE, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, LRUBC, YDVAB, YDVETA, YDVARS%U%T0,            &
& YDVARS%V%T0, YDVARS%DIV%T0, YDVARS%EDOT%T0, PSPL=YDCPG_DYN0%PREL, PSPM=YDCPG_DYN0%PREM, PRPREF=Z_RPREF_T0,        &
& PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, PALPH=YDCPG_DYN0%XYB%ALPH,     &
& PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE, PRPP=YDCPG_DYN0%XYB%RPP, PEVEL=YDCPG_DYN0%CTY%EVEL,         &
& PVVEL=YDCPG_DYN0%CTY%VVEL, PPSDIV=YDCPG_DYN0%CTY%PSDIV, PPSDVBC=YDCPG_DYN0%CTY%PSDVBC, PDIVDP=YDCPG_DYN0%CTY%DIVDP&
&     )

!*     3.1.7d UPDATE_GWDIAG, GPCTY_FORC, etc.

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY, YDMODEL%YRML_GCONF%YRRIP, NPROMA, NFLEVG, YDCPG_BNDS%KFDIA, NSTEP, YDCPG_BNDS%KSTGLO, &
  & YDCPG_DYN0%PRE, YDCPG_DYN0%CTY%DIVDP(:, 1:))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY, YDMODEL%YRML_GCONF, YDMODEL%YRML_PHY_MF%YRPHY2, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, &
  & PP1FORC, YDCPG_DYN0%PREF, YDVARS%T%T0, YDCPG_DYN0%RCP%R, YDCPG_DYN0%CTY%EVEL, YDCPG_DYN0%CTY%VVEL(:, 1:)      &
  &     )
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

YDCPG_DYN0%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)=YDVARS%GEOMETRY%OROG%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
CALL GPGEO(NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDCPG_DYN0%PHI, YDCPG_DYN0%PHIF, &
& Z_RT_T0, Z_ONE_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, Z_RT_T0, YDCPG_DYN0%RTL,             &
& YDCPG_DYN0%RTM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, POROGL=YDCPG_DYN0%OROGL, POROGM=YDCPG_DYN0%OROGM,     &
& PHIFL=YDCPG_DYN0%PHIFL, PHIFM=YDCPG_DYN0%PHIFM, PHIHL=Z_GPHL_T0, PHIHM=Z_GPHM_T0, PLNPRL_DER=Z_XYBDER_LNPRL_T0, &
& PLNPRM_DER=Z_XYBDER_LNPRM_T0, PALPHL_DER=Z_XYBDER_ALPHL_T0, PALPHM_DER=Z_XYBDER_ALPHM_T0                        &
&                                                     )

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    YDCPG_DYN0%KENE(JROF,JLEV)=0.5_JPRB*(YDVARS%U%T0(JROF,JLEV)**2+YDVARS%V%T0(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, &
& YDCPG_DYN0%UVH%WWI (1:, 1:))
CALL GPHLUV(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T0, YDVARS%V%T0, &
& PWWI=YDCPG_DYN0%UVH%WWI, PUH=YDCPG_DYN0%UVH%UH, PVH=YDCPG_DYN0%UVH%VH)


LLDER=.TRUE.
CALL GPUVS(NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, YDVARS%U%T0, YDVARS%V%T0,                    &
& Z_US_T0, Z_VS_T0, PDIVF=YDVARS%DIV%T0, PVORF=YDVARS%VOR%T0, PUFL=YDVARS%U%DL, PVFL=YDVARS%V%DL, PUS_L=Z_US_L_T0, &
& PVS_L=Z_VS_L_T0, PUS_M=Z_US_M_T0, PVS_M=Z_VS_M_T0)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

CALL GNHQE_GRP(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_RT_T0, YDCPG_DYN0%RTL, YDCPG_DYN0%RTM,                             &
& YDCPG_DYN0%PREL, YDCPG_DYN0%PREM, YDCPG_DYN0%XYB%RDELP, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%XYB%ALPH, PREHYDH=YDCPG_DYN0%PRE,         &
& PQCHAF=YDVARS%SPD%T0, PQCHAL=YDCPG_DYN0%QCHAL, PQCHAM=YDCPG_DYN0%QCHAM, PEQCHAF=Z_EQCHAF_T0, PEQCHAH=Z_EQCHAH_T0,                 &
& PKAPF=YDCPG_DYN0%RCP%KAP, PKAPH=Z_KAPH_T0, PHIHL=Z_GPHL_T0, PHIHM=Z_GPHM_T0, PHIFL=YDCPG_DYN0%PHIFL,                              &
& PHIFM=YDCPG_DYN0%PHIFM, PSGRTL=Z_PSGRTL_T0, PSGRTM=Z_PSGRTM_T0, PALPHPLL_DER=Z_XYBDER_ALPHPLL_T0, PALPHPLM_DER=Z_XYBDER_ALPHPLM_T0&
&   )

! store for next time-step
IF( LGRADSP ) THEN

  ! adjust field with filter result
  Z_SGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTL%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) - YDVARS%SGRTL%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG))
  Z_SGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTM%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG) - YDVARS%SGRTM%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG))

  ! store for next time-step unfiltered fields
  YDVARS%SGRTL%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  YDVARS%SGRTM%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

  ! set new values for current timestep
  Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_SGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_SGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

ENDIF

Z_SGRTSL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=Z_PSGRTL_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)
Z_SGRTSM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=Z_PSGRTM_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)

!*     3.1.13 COMPUTES TERM "NHX".

IF (LDGPXX) THEN
  ! * compute NHX_s = "wind shear" part of NHX
  CALL GPXX(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_GPHL_T0, Z_GPHM_T0, YDCPG_DYN0%PHIFL, &
  & YDCPG_DYN0%PHIFM, YDCPG_DYN0%XYB%LNPR, Z_RT_T0, YDVARS%U%T0, YDVARS%V%T0, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, &
  & Z_NHXS_T_T0)
  ! * compute NHX_d = "divergence" part of NHX
  CALL GNHQE_XXD(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T0, YDVARS%V%T0, &
  & YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%RDELP, YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%PREL,  &
  & YDCPG_DYN0%PREM, YDCPG_DYN0%RCP%KAP, Z_NHXD_T_T0)
  ! * compute NHX
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      YDCPG_DYN0%NHX(JROF,JLEV)=Z_NHXS_T_T0(JROF,JLEV)+Z_NHXD_T_T0(JROF,JLEV)
    ENDDO
  ENDDO
ELSE
  YDCPG_DYN0%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ! ??? But we also need ZNHXS_T0 for RHS of dver equation, and there are two possibilities
  !     which a priori do not give the same results:
  ! ??? first method: compute ZNHXD_T0 via GNHQE_XXD, , then ZNHXS_T0=PNHXT0-ZNHXD_T0
  !     this is the most consistent with what is done in NHEE model
  ! * compute NHX_d
  CALL GNHQE_XXD(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T0, YDVARS%V%T0, &
  & YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%RDELP, YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%XYB%RTGR, YDCPG_DYN0%PREL,  &
  & YDCPG_DYN0%PREM, YDCPG_DYN0%RCP%KAP, Z_NHXD_T_T0)
  ! * compute NHX_s
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_NHXS_T_T0(JROF,JLEV)=YDCPG_DYN0%NHX(JROF,JLEV)-Z_NHXD_T_T0(JROF,JLEV)
    ENDDO
  ENDDO
  ! ??? second method: compute ZNHXS_T0 via GPXX, ZNHXD_T0 is useless
  !     CALL GPXX as above
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    Z_DVER_T0(JROF,JLEV)=YDVARS%SVD%T0(JROF,JLEV)-YDCPG_DYN0%NHX(JROF,JLEV)
    Z_DVERL_T0(JROF,JLEV)=YDVARS%SVD%DL(JROF,JLEV)-YDVARS%NHX%DL(JROF,JLEV)
    Z_DVERM_T0(JROF,JLEV)=YDVARS%SVD%DM(JROF,JLEV)-YDVARS%NHX%DM(JROF,JLEV)
  ENDDO
ENDDO

!*     3.1.16 COMPUTES term [gw].

LLGWF0=.TRUE.
LLGDWI=.FALSE.
IF (LGWADV.AND.LVFE_GW) THEN
  ! PGDW is required (at least for SL2TL)
  CALL GPGW(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLGWF0, LLGDWI, YDCPG_DYN0%OROGL, &
  & YDCPG_DYN0%OROGM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, Z_US_T0, Z_VS_T0, Z_RT_T0, Z_DVER_T0,         &
  & P_GWHT_T0, YDCPG_DYN0%GWFT, PGDW=P_GDW_T0)

  YDCPG_DYN0%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN0%GWFT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
ELSE
  CALL GPGW(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLGWF0, LLGDWI, YDCPG_DYN0%OROGL, &
  & YDCPG_DYN0%OROGM, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, Z_US_T0, Z_VS_T0, Z_RT_T0, Z_DVER_T0,         &
  & P_GWHT_T0, YDCPG_DYN0%GWFT)

  IF (LGWADV) YDCPG_DYN0%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=P_GWHT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0:NFLEVG-1)
ENDIF

IF (LTWOTL.AND.LRDBBC) THEN
  ! PGWS contains (gw)_surf
  YDCPG_DYN0%GWS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=P_GWHT_T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)
ENDIF

!*     3.1.17 COMPUTES grad(gw).

LLDER=.TRUE.
CALL GNHGRGW(YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, Z_RT_T0, YDCPG_DYN0%RTL,           &
& YDCPG_DYN0%RTM, Z_DVER_T0, Z_DVERL_T0, Z_DVERM_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, PUS=Z_US_T0,            &
& PVS=Z_VS_T0, PUS_L=Z_US_L_T0, PVS_L=Z_VS_L_T0, PUS_M=Z_US_M_T0, PVS_M=Z_VS_M_T0, POROGL=YDCPG_DYN0%OROGL,            &
& POROGM=YDCPG_DYN0%OROGM, POROGLM=P_OROGLM_T0, POROGLL=P_OROGLL_T0, POROGMM=P_OROGMM_T0, PGWFL=YDCPG_DYN0%GWFL,       &
& PGWFM=YDCPG_DYN0%GWFM, PGWHL=Z_GWHL_T0, PGWHM=Z_GWHM_T0, PLNPRL_DER=Z_XYBDER_LNPRL_T0, PLNPRM_DER=Z_XYBDER_LNPRM_T0, &
& PALPHL_DER=Z_XYBDER_ALPHL_T0, PALPHM_DER=Z_XYBDER_ALPHM_T0)

!*     3.1.19 COMPUTES 1/(R Tt [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*Z_RT_T0(JROF,JLEV))
    ENDDO
  ENDDO
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY, YDMODEL%YRML_PHY_EC%YREPHY, YDMODEL%YRML_DYN%YRDYN, YDCPG_BNDS%KIDIA, &
& YDCPG_BNDS%KFDIA, YDVARS%GEOMETRY%RCORI%T0, YDVARS%GEOMETRY%GNORDL%T0, YDVARS%GEOMETRY%GNORDM%T0,             &
& Z_PSGRTL_T0, Z_PSGRTM_T0, YDVARS%U%T0, YDVARS%V%T0, PATND(1, 1, YYTTND%M_TNDU), PATND(1, 1, YYTTND%M_TNDV),   &
& PATND(1, 1, YYTTND%M_TNDU_NOC), PATND(1, 1, YYTTND%M_TNDV_NOC))

!*     3.1.20b COMPUTES [D V_surf/Dt].

CALL GNH_TNDLAGADIAB_UVS(NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LRPLANE, YDVARS%GEOMETRY%RCORI%T0, &
& YDVARS%GEOMETRY%RATATX%T0, YDVARS%GEOMETRY%RATATH%T0, Z_US_T0, Z_VS_T0, Z_SGRTSL_T0, Z_SGRTSM_T0,     &
& Z_TNDUS_T0, Z_TNDVS_T0    )

!*     3.1.21 COMPUTES [D(gw)/Dt] if LGWADV=T.

! Remark: PATND(.,.,YYTTND%M_TNDGW) is computed only if LGWADV=T.
IF (LGWADV) THEN
  ! warning: formerly LVFE_LAPL.OR.LVFE_GW
  IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHQE 3.1.21: option not yet coded!')

  CALL GNHQE_TNDLAGADIAB_GW(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM,       &
  & P_OROGLM_T0, P_OROGLL_T0, P_OROGMM_T0, Z_US_T0, Z_VS_T0, Z_TNDUS_T0, Z_TNDVS_T0, YDCPG_DYN0%XYB%LNPR,             &
  & YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%PREF, YDCPG_DYN0%PRE, Z_EQCHAF_T0, YDCPG_DYN0%RCP%KAP, PATND(1, 1, YYTTND%M_TNDGW)&
  &   )
ENDIF

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt] if LGWADV=F.

! Remark: PATND(.,.,YYTTND%M_TNDVD) is computed only if LGWADV=F.
IF (.NOT.LGWADV) THEN
  IF (LVERTFE) CALL ABOR1(' CPG_GP_NHQE 3.1.23: not yet coded case')

  CALL GNHQE_TNDLAGADIAB_SVD(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN0%OROGL, YDCPG_DYN0%OROGM, &
  & P_OROGLM_T0, P_OROGLL_T0, P_OROGMM_T0, YDCPG_DYN0%XYB%LNPR, YDCPG_DYN0%XYB%ALPH, YDCPG_DYN0%PREF,            &
  & YDCPG_DYN0%PRE, YDVARS%U%T0, YDVARS%V%T0, YDCPG_DYN0%UVH%UH, YDCPG_DYN0%UVH%VH, Z_TNDUS_T0,                  &
  & Z_TNDVS_T0, Z_EQCHAF_T0, YDCPG_DYN0%RCP%KAP, YDCPG_DYN0%RDPHI, Z_GWHL_T0, Z_GWHM_T0, Z_DVER_T0,              &
  & Z_NHXS_T_T0, PATND(1, 1, YYTTND%M_TNDVD), YDCPG_DYN0%DBBC)
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    PATND(JROF,JLEV,YYTTND%M_TNDT)=YDCPG_DYN0%RCP%KAP(JROF,JLEV)*YDVARS%T%T0(JROF,JLEV)*YDCPG_DYN0%CTY%VVEL(JROF,JLEV)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVAB, YDCPG_DYN9%PRE, PRESF=YDCPG_DYN9%PREF,   &
  & PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, PALPH=YDCPG_DYN9%XYB%ALPH, &
  & PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE, PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP_YDVARS (NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDVARS=YDVARS, KGFLTYP=9, &
                   & PCP=YDCPG_DYN9%RCP%CP, PR=YDCPG_DYN9%RCP%R, PKAP=YDCPG_DYN9%RCP%KAP)

!*     3.2.4 COMPUTE "RT".

  ! ZR9T9 is R(t-dt)*Tt(t-dt); ZR0T9 is R(t)*Tt(t-dt) (for GPXX)
  Z_R9T_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%T%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)*YDCPG_DYN9%RCP%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  Z_R0T_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%T%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)*YDCPG_DYN0%RCP%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)

!*     3.2.6 COMPUTE QUANTITIES FOR NHQE MODEL.

  CALL GNHPRE(YDGEOMETRY, NPROMA, NFLEVG, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%SPD%T9, YDCPG_DYN9%PREF, &
  & PKAP=YDCPG_DYN9%RCP%KAP, PNHPREF=YDCPG_DYN9%NHPREF)
  CALL GNHQE_PREH(YDGEOMETRY, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_DYN9%PRE, YDVARS%SPD%T9, YDCPG_DYN9%NHPREH&
  &     )

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF((LDLDIAB.OR.LSIMPH)) THEN
    YDCPG_DYN9%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)=YDVARS%GEOMETRY%OROG%T0(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
    CALL GPGEO(NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, NFLEVG, YDCPG_DYN9%PHI, YDCPG_DYN9%PHIF, &
    & Z_R9T_T9, Z_ONE_T0, YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%ALPH, YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  ! Not needed if X-term and Z_term calculated FE.
  IF (LDGPXX .AND. .NOT.LVERTFE) THEN
    ! * same remark as for time t half-level winds.
    Z_UVH_WWI_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN0%UVH%WWI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T9, YDVARS%V%T9, &
    & PWWI=Z_UVH_WWI_T9, PUH=Z_UVH_UH_T9, PVH=Z_UVH_VH_T9)
  ENDIF

  LLDER=.FALSE.
  CALL GPUVS(NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LLDER, YDVARS%U%T9, YDVARS%V%T9, &
  & Z_US_T9, Z_VS_T9)

!*     3.2.13 COMPUTES TERM "NHX".

  IF (LDGPXX) THEN
    ! * compute NHX_s = "wind shear" part of NHX
    CALL GPXX(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, Z_GPHL_T0, Z_GPHM_T0, YDCPG_DYN0%PHIFL, &
    & YDCPG_DYN0%PHIFM, YDCPG_DYN9%XYB%LNPR, Z_R0T_T9, YDVARS%U%T9, YDVARS%V%T9, Z_UVH_UH_T9,                         &
    & Z_UVH_VH_T9, Z_NHXS_T_T9)
    ! * compute NHX_d = "divergence" part of NHX
    CALL GNHQE_XXD(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDVARS%U%T9, YDVARS%V%T9, &
    & YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%RDELP, YDCPG_DYN9%XYB%ALPH, YDCPG_DYN9%XYB%RTGR, YDCPG_DYN9%PREL,  &
    & YDCPG_DYN9%PREM, YDCPG_DYN9%RCP%KAP, Z_NHXD_T_T9)
    ! * compute NHX
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        YDCPG_DYN9%NHX(JROF,JLEV)=Z_NHXS_T_T9(JROF,JLEV)+Z_NHXD_T_T9(JROF,JLEV)
      ENDDO
    ENDDO
  ELSE
    ! ??? we do not need separate contributions ZNHXS_T9 and ZNHXD_T9 in this case.
    YDCPG_DYN9%NHX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDVARS%NHX%T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ENDIF

!*     3.2.14 COMPUTES dver.

  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      Z_DVER_T9(JROF,JLEV)=YDVARS%SVD%T9(JROF,JLEV)-YDCPG_DYN9%NHX(JROF,JLEV)
    ENDDO
  ENDDO

!*     3.2.16 COMPUTES term [gw].

  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL3TL)
    CALL GPGW(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LDMPA, LLGDWI, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%ALPH, Z_US_T9, Z_VS_T9, Z_R9T_T9,                  &
    & Z_DVER_T9, Z_GWHT_T9, YDCPG_DYN9%GWFT, PGDW=Z_GDW_T9)

    YDCPG_DYN9%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=YDCPG_DYN9%GWFT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)
  ELSE
    CALL GPGW(YDGEOMETRY, NFLEVG, NPROMA, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, LDMPA, LLGDWI, YDCPG_DYN0%OROGL, &
    & YDCPG_DYN0%OROGM, YDCPG_DYN9%XYB%LNPR, YDCPG_DYN9%XYB%ALPH, Z_US_T9, Z_VS_T9, Z_R9T_T9,                  &
    & Z_DVER_T9, Z_GWHT_T9, YDCPG_DYN9%GWFT)

    IF (LGWADV) YDCPG_DYN9%GWT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:NFLEVG)=Z_GWHT_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LRDBBC) THEN
    ! PGWS contains (gw)_surf
    YDCPG_DYN0%GWS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=Z_GWHT_T9(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,NFLEVG)
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE', 1, ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHQE
