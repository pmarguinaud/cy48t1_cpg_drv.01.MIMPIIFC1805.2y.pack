#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHQE(YDGEOMETRY,YDCPG_DIM,YDTMP,YDVARS,YDCPG_DYN0,YDCPG_DYN9,YDMODEL,YDGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & LDGPXX,LDLDIAB,LDMPA,&
 & POROG,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & PATND)

!**** *CPG_GP_NHQE* - Grid point calculations at instants t and t-dt for NHQE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHQE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHQE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.
!        PRE9L     : zonal component of "grad prehyds" at t-dt.
!        PRE9M     : meridian component of "grad prehyds" at t-dt.

!     INPUT/OUTPUT:
!     -------------
!        PGFL      : unified_treatment grid-point fields at t
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad R Tt" at full levels.
!        PRT0M     : meridian component of "grad R Tt" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D(gw)_surf/Dt]_adiab.
!        PRDPHI    : 1/(R Tt [Delta log(prehyd)]) at t.
!                    "R" is the version of R (Rmoist in NHQE model) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (March 2018): rewrite Laplacian term in NHQE model.
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELD_VARIABLES_MOD,ONLY : FIELD_VARIABLES
USE CPG_TYPE_MOD,ONLY : CPG_DYN_TYPE, CPG_GP_TMP_TYPE
USE CPG_DIM_TYPE_MOD, ONLY : CPG_DIM_TYPE
USE YOMGMV   , ONLY : TGMV
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC
USE YOMCST   , ONLY : RD, RV
USE YOMDYNA  , ONLY : LGWADV, LRDBBC, LGRADSP, LRUBC
USE YOMCVER  , ONLY : LVERTFE, LVFE_GW,LVFE_LAPL_BC
USE YOMCT3   , ONLY : NSTEP
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND, YYTHW0, YYTHW9,&
 & YYTCTY0, YYTXYBDER0, YYTRCP0, YYTRCP9, YYTXYB0, YYTXYB9

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_GP_TMP_TYPE),INTENT(INOUT) :: YDTMP
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV



LOGICAL           ,INTENT(IN)    :: LDGPXX
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA

REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

!    ==== MISCELLANEOUS ======================

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "gphpre.intfb.h"
#include "gpgrxyb.intfb.h"
#include "gprcp.intfb.h"
#include "gprcph.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhqe_preh.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhqe_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gnhqe_xxd.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnhqe_tndlagadiab_gw.intfb.h"
#include "gnhqe_tndlagadiab_svd.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(YDCPG_DIM%KBL),YDGSGEOM=>YDGEOMETRY%YRGSGEOM(YDCPG_DIM%KBL), &
 & YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE, &
 & NPROMA=>YDGEOMETRY%YRDIM%NPROMA,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)
ASSOCIATE(&
 & YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, &
 & YFORC=>YDMODEL%YRML_GCONF%YGFL%YFORC, &
 & NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
 & LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH)

!     ------------------------------------------------------------------

!*       0.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

YDTMP%T0%ONE(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=1.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVAB,YDCPG_DYN0%PRE,PRESF=YDCPG_DYN0%PREF, &
          & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
          & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
          & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDTMP%T0%RPREF(JROF,JLEV)=1.0_JPRB/YDCPG_DYN0%PREF(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,.FALSE.,YDVAB,YDCPG_DYN0%PREL,&
           & YDCPG_DYN0%PREM,PXYBDER=YDTMP%T0%XYBDER%ZVIEW,&
           & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
           & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
           & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,PGFL=PGFL,PCP=YDCPG_DYN0%RCP%CP,&
 & PR=YDCPG_DYN0%RCP%R,PKAP=YDCPG_DYN0%RCP%KAP)
CALL GPRCPH(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%RCP%KAP,YDTMP%T0%KAPH)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,PRL=YDVARS%RSPEC%DL,PRM=YDVARS%RSPEC%DM)
ELSE
  CALL GPRT(LSPRT,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,YDVARS%T%T0,YDVARS%T%DL,YDVARS%T%DM,&
   & YDVARS%Q%DL,YDVARS%Q%DM,&
   & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHQE MODEL.

CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T0,YDCPG_DYN0%PREF,&
 & PKAP=YDCPG_DYN0%RCP%KAP,&
 & PNHPREF=YDCPG_DYN0%NHPREF,PEQCHAF=YDTMP%T0%EQCHAF,PEIQCHAF=YDTMP%T0%EIQCHAF)
CALL GNHQE_PREH(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%PRE,YDVARS%SPD%T0,YDCPG_DYN0%NHPREH,&
 & PREF=YDCPG_DYN0%PREF,PEQCHAF=YDTMP%T0%EQCHAF,PKAPH=YDTMP%T0%KAPH,&
 & PEQCHAH=YDTMP%T0%EQCHAH)

YDCPG_DYN0%QCHAL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SPD%DL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
YDCPG_DYN0%QCHAM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%SPD%DM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

!*     3.1.7c COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            and "omega/prehyd".

CALL GPCTY(YDVFE,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,LRUBC,YDVAB,YDVETA,&
 & YDVARS%U%T0,YDVARS%V%T0,YDVARS%DIV%T0,YDVARS%EDOT%T0,&
 & PSPL=YDCPG_DYN0%PREL,PSPM=YDCPG_DYN0%PREM,PRPREF=YDTMP%T0%RPREF,&
 & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
 & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
 & PRPP=YDCPG_DYN0%XYB%RPP, PEVEL=YDCPG_DYN0%CTY%EVEL, PVVEL=YDCPG_DYN0%CTY%VVEL, &
 & PPSDIV=YDCPG_DYN0%CTY%PSDIV, PPSDVBC=YDCPG_DYN0%CTY%PSDVBC, PDIVDP=YDCPG_DYN0%CTY%DIVDP)

!*     3.1.7d UPDATE_GWDIAG, GPCTY_FORC, etc.

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,NPROMA,NFLEVG,YDCPG_DIM%KFDIA,NSTEP,YDCPG_DIM%KSTGLO,YDCPG_DYN0%PRE,YDCPG_DYN0%CTY%DIVDP(:,1:))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA, &
   & PGFL(:,:,YFORC(1)%MP),YDCPG_DYN0%PREF,YDVARS%T%T0,YDCPG_DYN0%RCP%R,YDCPG_DYN0%CTY%EVEL,YDCPG_DYN0%CTY%VVEL(:,1:))
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

YDCPG_DYN0%PHI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)=POROG(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
CALL GPGEO(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,YDCPG_DYN0%PHI,YDCPG_DYN0%PHIF,YDTMP%T0%RT,&
 & YDTMP%T0%ONE,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,&
 & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & POROGL=YDCPG_DYN0%OROGL,POROGM=YDCPG_DYN0%OROGM,&
 & PHIFL=YDCPG_DYN0%PHIFL,PHIFM=YDCPG_DYN0%PHIFM,PHIHL=YDTMP%T0%GPHL,PHIHM=YDTMP%T0%GPHM, &
 & PLNPRL_DER=YDTMP%T0%XYBDER%LNPRL, PLNPRM_DER=YDTMP%T0%XYBDER%LNPRM, &
 & PALPHL_DER=YDTMP%T0%XYBDER%ALPHL, PALPHM_DER=YDTMP%T0%XYBDER%ALPHM)

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDCPG_DYN0%KENE(JROF,JLEV)=0.5_JPRB*(YDVARS%U%T0(JROF,JLEV)**2+YDVARS%V%T0(JROF,JLEV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%UVH%WWI (1:,1:))
CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%U%T0,YDVARS%V%T0,&
          & PWWI=YDCPG_DYN0%UVH%WWI,PUH=YDCPG_DYN0%UVH%UH,PVH=YDCPG_DYN0%UVH%VH)


LLDER=.TRUE.
CALL GPUVS(NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,&
 & YDVARS%U%T0,YDVARS%V%T0,YDTMP%T0%US,YDTMP%T0%VS,&
 & PDIVF=YDVARS%DIV%T0,PVORF=YDVARS%VOR%T0,&
 & PUFL=YDVARS%U%DL,PVFL=YDVARS%V%DL,&
 & PUS_L=YDTMP%T0%US_L,PVS_L=YDTMP%T0%VS_L,PUS_M=YDTMP%T0%US_M,PVS_M=YDTMP%T0%VS_M)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

CALL GNHQE_GRP(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
 & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,YDCPG_DYN0%XYB%RDELP,&
 & YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%XYBDER%ZVIEW,YDCPG_DYN0%PRE,&
 & YDVARS%SPD%T0,YDCPG_DYN0%QCHAL,YDCPG_DYN0%QCHAM,YDTMP%T0%EQCHAF,YDTMP%T0%EQCHAH,YDCPG_DYN0%RCP%KAP,YDTMP%T0%KAPH,&
 & YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
 & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM)

! store for next time-step
IF( LGRADSP ) THEN

  ! adjust field with filter result
  YDTMP%T0%SGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTL%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG) - YDVARS%SGRTL%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG))
  YDTMP%T0%SGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)&
   & - (YDVARS%SGRTM%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG) - YDVARS%SGRTM%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG))

  ! store for next time-step unfiltered fields
  YDVARS%SGRTL%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDVARS%SGRTM%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

  ! set new values for current timestep
  YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%SGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%SGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

ENDIF

YDTMP%T0%SGRTSL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%PSGRTL(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
YDTMP%T0%SGRTSM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%PSGRTM(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)

!*     3.1.13 COMPUTES TERM "NHX".

IF (LDGPXX) THEN
  ! * compute NHX_s = "wind shear" part of NHX
  CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
   & YDCPG_DYN0%XYB%LNPR,YDTMP%T0%RT,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & YDTMP%T0%NHXS_T)
  ! * compute NHX_d = "divergence" part of NHX
  CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%RDELP,YDCPG_DYN0%XYB%ALPH,&
   & YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,YDCPG_DYN0%RCP%KAP,&
   & YDTMP%T0%NHXD_T)
  ! * compute NHX
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDCPG_DYN0%NHX(JROF,JLEV)=YDTMP%T0%NHXS_T(JROF,JLEV)+YDTMP%T0%NHXD_T(JROF,JLEV)
    ENDDO
  ENDDO
ELSE
  YDCPG_DYN0%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ! ??? But we also need ZNHXS_T0 for RHS of dver equation, and there are two possibilities
  !     which a priori do not give the same results:
  ! ??? first method: compute ZNHXD_T0 via GNHQE_XXD, , then ZNHXS_T0=PNHXT0-ZNHXD_T0
  !     this is the most consistent with what is done in NHEE model
  ! * compute NHX_d
  CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%RDELP,YDCPG_DYN0%XYB%ALPH,&
   & YDCPG_DYN0%XYB%RTGR,YDCPG_DYN0%PREL,YDCPG_DYN0%PREM,YDCPG_DYN0%RCP%KAP,YDTMP%T0%NHXD_T)
  ! * compute NHX_s
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDTMP%T0%NHXS_T(JROF,JLEV)=YDCPG_DYN0%NHX(JROF,JLEV)-YDTMP%T0%NHXD_T(JROF,JLEV)
    ENDDO
  ENDDO
  ! ??? second method: compute ZNHXS_T0 via GPXX, ZNHXD_T0 is useless
  !     CALL GPXX as above
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDTMP%T0%DVER(JROF,JLEV)=YDVARS%SVD%T0(JROF,JLEV)-YDCPG_DYN0%NHX(JROF,JLEV)
    YDTMP%T0%DVERL(JROF,JLEV)=YDVARS%SVD%DL(JROF,JLEV)-YDVARS%NHX%DL(JROF,JLEV)
    YDTMP%T0%DVERM(JROF,JLEV)=YDVARS%SVD%DM(JROF,JLEV)-YDVARS%NHX%DM(JROF,JLEV)
  ENDDO
ENDDO

!*     3.1.16 COMPUTES term [gw].

LLGWF0=.TRUE.
LLGDWI=.FALSE.
IF (LGWADV.AND.LVFE_GW) THEN
  ! PGDW is required (at least for SL2TL)
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLGWF0,LLGDWI,&
   & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%US,YDTMP%T0%VS,&
   & YDTMP%T0%RT,YDTMP%T0%DVER,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT,PGDW=YDTMP%T0%GDW)

  YDCPG_DYN0%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%GWFT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
ELSE
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLGWF0,LLGDWI,&
   & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%US,YDTMP%T0%VS,&
   & YDTMP%T0%RT,YDTMP%T0%DVER,YDTMP%T0%GWHT,YDCPG_DYN0%GWFT)

  IF (LGWADV) YDCPG_DYN0%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T0%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)
ENDIF

IF (LTWOTL.AND.LRDBBC) THEN
  ! PGWS contains (gw)_surf
  YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T0%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
ENDIF

!*     3.1.17 COMPUTES grad(gw).

LLDER=.TRUE.
CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,&
 & YDTMP%T0%RT,YDCPG_DYN0%RTL,YDCPG_DYN0%RTM,&
 & YDTMP%T0%DVER,YDTMP%T0%DVERL,YDTMP%T0%DVERM,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDTMP%T0%XYBDER%ZVIEW,&
 & YDTMP%T0%US,YDTMP%T0%VS,YDTMP%T0%US_L,YDTMP%T0%VS_L,YDTMP%T0%US_M,YDTMP%T0%VS_M,&
 & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
 & YDCPG_DYN0%GWFL,YDCPG_DYN0%GWFM,YDTMP%T0%GWHL,YDTMP%T0%GWHM)

!*     3.1.19 COMPUTES 1/(R Tt [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDCPG_DYN0%RDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*YDTMP%T0%RT(JROF,JLEV))
    ENDDO
  ENDDO
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY,YDGMV,YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_DYN%YRDYN,&
 & YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDGSGEOM%RCORI,&
 & YDGSGEOM%GNORDL,YDGSGEOM%GNORDM,&
 & YDTMP%T0%PSGRTL,YDTMP%T0%PSGRTM,&
 & YDVARS%U%T0,YDVARS%V%T0,PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
 & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC))

!*     3.1.20b COMPUTES [D V_surf/Dt].

CALL GNH_TNDLAGADIAB_UVS(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LRPLANE,&
 & YDGSGEOM%RCORI,YDCSGEOM%RATATX,YDCSGEOM%RATATH,&
 & YDTMP%T0%US,YDTMP%T0%VS,YDTMP%T0%SGRTSL,YDTMP%T0%SGRTSM,&
 & YDTMP%T0%TNDUS,YDTMP%T0%TNDVS)

!*     3.1.21 COMPUTES [D(gw)/Dt] if LGWADV=T.

! Remark: PATND(.,.,YYTTND%M_TNDGW) is computed only if LGWADV=T.
IF (LGWADV) THEN
  ! warning: formerly LVFE_LAPL.OR.LVFE_GW
  IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHQE 3.1.21: option not yet coded!')

  CALL GNHQE_TNDLAGADIAB_GW(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
   & YDTMP%T0%US,YDTMP%T0%VS,YDTMP%T0%TNDUS,YDTMP%T0%TNDVS,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,&
   & YDTMP%T0%EQCHAF,YDCPG_DYN0%RCP%KAP,&
   & PATND(1,1,YYTTND%M_TNDGW))
ENDIF

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt] if LGWADV=F.

! Remark: PATND(.,.,YYTTND%M_TNDVD) is computed only if LGWADV=F.
IF (.NOT.LGWADV) THEN
  IF (LVERTFE) CALL ABOR1(' CPG_GP_NHQE 3.1.23: not yet coded case')

  CALL GNHQE_TNDLAGADIAB_SVD(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
   & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDTMP%T0%OROGLM,YDTMP%T0%OROGLL,YDTMP%T0%OROGMM,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,YDCPG_DYN0%PREF,YDCPG_DYN0%PRE,&
   & YDVARS%U%T0,YDVARS%V%T0,YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,YDTMP%T0%TNDUS,YDTMP%T0%TNDVS,&
   & YDTMP%T0%EQCHAF,YDCPG_DYN0%RCP%KAP,&
   & YDCPG_DYN0%RDPHI,YDTMP%T0%GWHL,YDTMP%T0%GWHM,YDTMP%T0%DVER,YDTMP%T0%NHXS_T,&
   & PATND(1,1,YYTTND%M_TNDVD),YDCPG_DYN0%DBBC)
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    PATND(JROF,JLEV,YYTTND%M_TNDT)=YDCPG_DYN0%RCP%KAP(JROF,JLEV)*YDVARS%T%T0(JROF,JLEV)*YDCPG_DYN0%CTY%VVEL(JROF,JLEV)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVAB,YDCPG_DYN9%PRE,PRESF=YDCPG_DYN9%PREF,&
            & PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, &
            & PALPH=YDCPG_DYN9%XYB%ALPH, PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE,  &
            & PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=YDCPG_DYN9%RCP%CP,&
   & PR=YDCPG_DYN9%RCP%R,PKAP=YDCPG_DYN9%RCP%KAP)

!*     3.2.4 COMPUTE "RT".

  ! ZR9T9 is R(t-dt)*Tt(t-dt); ZR0T9 is R(t)*Tt(t-dt) (for GPXX)
  YDTMP%T9%R9T(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%T%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)*YDCPG_DYN9%RCP%R(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  YDTMP%T9%R0T(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%T%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)*YDCPG_DYN0%RCP%R(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)

!*     3.2.6 COMPUTE QUANTITIES FOR NHQE MODEL.

  CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%SPD%T9,YDCPG_DYN9%PREF,&
   & PKAP=YDCPG_DYN9%RCP%KAP,&
   & PNHPREF=YDCPG_DYN9%NHPREF)
  CALL GNHQE_PREH(YDGEOMETRY,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DYN9%PRE,YDVARS%SPD%T9,YDCPG_DYN9%NHPREH)

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF((LDLDIAB.OR.LSIMPH)) THEN
    YDCPG_DYN9%PHI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)=POROG(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
    CALL GPGEO(NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,NFLEVG,YDCPG_DYN9%PHI,YDCPG_DYN9%PHIF,YDTMP%T9%R9T,&
     & YDTMP%T0%ONE,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,&
     & YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  ! Not needed if X-term and Z_term calculated FE.
  IF (LDGPXX .AND. .NOT.LVERTFE) THEN
    ! * same remark as for time t half-level winds.
    YDTMP%T9%UVH%WWI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN0%UVH%WWI(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDVARS%U%T9,YDVARS%V%T9, &
              & PWWI=YDTMP%T9%UVH%WWI,PUH=YDTMP%T9%UVH%UH,PVH=YDTMP%T9%UVH%VH)
  ENDIF

  LLDER=.FALSE.
  CALL GPUVS(NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LLDER,YDVARS%U%T9,YDVARS%V%T9,YDTMP%T9%US,YDTMP%T9%VS)

!*     3.2.13 COMPUTES TERM "NHX".

  IF (LDGPXX) THEN
    ! * compute NHX_s = "wind shear" part of NHX
    CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDTMP%T0%GPHL,YDTMP%T0%GPHM,YDCPG_DYN0%PHIFL,YDCPG_DYN0%PHIFM,&
     & YDCPG_DYN9%XYB%LNPR,YDTMP%T9%R0T,&
     & YDVARS%U%T9,YDVARS%V%T9,YDTMP%T9%UVH%UH,YDTMP%T9%UVH%VH,&
     & YDTMP%T9%NHXS_T)
    ! * compute NHX_d = "divergence" part of NHX
    CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,&
     & YDVARS%U%T9,YDVARS%V%T9,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN9%XYB%RDELP,YDCPG_DYN9%XYB%ALPH,&
     & YDCPG_DYN9%XYB%RTGR,YDCPG_DYN9%PREL,YDCPG_DYN9%PREM,YDCPG_DYN9%RCP%KAP,YDTMP%T9%NHXD_T)
    ! * compute NHX
    DO JLEV=1,NFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        YDCPG_DYN9%NHX(JROF,JLEV)=YDTMP%T9%NHXS_T(JROF,JLEV)+YDTMP%T9%NHXD_T(JROF,JLEV)
      ENDDO
    ENDDO
  ELSE
    ! ??? we do not need separate contributions ZNHXS_T9 and ZNHXD_T9 in this case.
    YDCPG_DYN9%NHX(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDVARS%NHX%T9(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ENDIF

!*     3.2.14 COMPUTES dver.

  DO JLEV=1,NFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDTMP%T9%DVER(JROF,JLEV)=YDVARS%SVD%T9(JROF,JLEV)-YDCPG_DYN9%NHX(JROF,JLEV)
    ENDDO
  ENDDO

!*     3.2.16 COMPUTES term [gw].

  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL3TL)
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LDMPA,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,YDTMP%T9%US,YDTMP%T9%VS,&
     & YDTMP%T9%R9T,YDTMP%T9%DVER,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT,PGDW=YDTMP%T9%GDW)

    YDCPG_DYN9%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDCPG_DYN9%GWFT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)
  ELSE
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,LDMPA,LLGDWI,&
     & YDCPG_DYN0%OROGL,YDCPG_DYN0%OROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,YDTMP%T9%US,YDTMP%T9%VS,&
     & YDTMP%T9%R9T,YDTMP%T9%DVER,YDTMP%T9%GWHT,YDCPG_DYN9%GWFT)

    IF (LGWADV) YDCPG_DYN9%GWT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:NFLEVG)=YDTMP%T9%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,0:NFLEVG-1)
  ENDIF

  IF (LRDBBC) THEN
    ! PGWS contains (gw)_surf
    YDCPG_DYN0%GWS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDTMP%T9%GWHT(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,NFLEVG)
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE',1,ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHQE
