#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE CPG_GP_NHQE(YDGEOMETRY,YDCPG_DYN0,YDCPG_DYN9,YDMODEL,YDGMV,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & KST,KEND,KSTGLO,LDGPXX,LDLDIAB,LDMPA,&
 & KIBL,POROG,POROGL,POROGM,POROGLL,POROGMM,POROGLM,PRE0L,PRE0M,PRE9L,PRE9M,&
 !---------------------------------------------------------------------
 ! - INPUT/OUTPUT .
 & PGFL,PGMV,PRE0,PRE9,&
 !---------------------------------------------------------------------
 ! - OUTPUT .
 & PRE0F,PNHPRE0F,PNHPRE0H,PXYB0,PUVH0,&
 & PHI0,PHIF0,PHI0FL,PHI0FM,PRCP0,PCTY0,PGWFT0,PGWHT0,PGWT0,PGWS,PGWFL,PGWFM,PGDW0,PKENE0,&
 & PRT0L,PRT0M,&
 & PRE9F,PNHPRE9F,PNHPRE9H,PXYB9,PHI9,PHIF9,PRCP9,PGWFT9,PGWHT9,PGWT9,PGDW9,&
 & PATND,PDBBC,PRDPHI,&
 & PNHXT0,PNHXT9,PQCHA0L,PQCHA0M)

!**** *CPG_GP_NHQE* - Grid point calculations at instants t and t-dt for NHQE model.

!     Purpose.
!     --------
!      Grid-point calculations at instants t and t-dt for NHQE model.
!      Computes some intermediate quantities.
!      Computes some Lagrangian equation RHS.

!**   Interface.
!     ----------
!        *CALL* *CPG_GP_NHQE(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDGPXX    : Term 'NHX' recomputed via GPXX.
!        LDLDIAB   : .T. if complete physics is activated and predictor step.
!        LDMPA     : AROME upper-air physics.
!        KIBL      : index into YRGSGEOM/YRCSGEOM types in YDGEOMETRY
!        POROG     : surface orography.
!        POROGL    : zonal component of "grad(surf orography)"
!        POROGM    : meridian component of "grad(surf orography)"
!        POROGLL   : second order zonal der. of the orography.
!        POROGMM   : second order merid der. of the orography.
!        POROGLM   : second order crossed der. of the orography.
!        PRE0L     : zonal component of "grad prehyds" at t.
!        PRE0M     : meridian component of "grad prehyds" at t.
!        PRE9L     : zonal component of "grad prehyds" at t-dt.
!        PRE9M     : meridian component of "grad prehyds" at t-dt.

!     INPUT/OUTPUT:
!     -------------
!        PGFL      : unified_treatment grid-point fields at t
!        PGMV      : upper air GMV variables at time t and t-dt.
!        PRE0      : hydrostatic pressure "prehyd" at half levels at time t.
!        PRE9      : hydrostatic pressure "prehyd" at half levels at t-dt.

!     OUTPUT:
!     -------
!        PRE0F     : hydrostatic pressure "prehyd" at full levels at time t.
!        PNHPRE0F  : "pre" at full levels (time t).
!        PNHPRE0H  : "pre" at half levels (time t).
!        PXYB0     : contains pressure depth, "delta", "alpha" at t.
!        PUVH0     : horizontal wind at time t at half levels.
!        PHI0      : geopotential height "gz" at t at half levels.
!        PHIF0     : geopotential height "gz" at t at full levels.
!        PHI0FL    : zonal component of "grad (gz)" at t at full levels.
!        PHI0FM    : meridian component of "grad (gz)" at t at full levels.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PGWFT0    : [gw] at full levels at t.
!        PGWHT0    : [gw] at half levels at t.
!        PGWT0     : [gw] at t (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGWS      : [gw]_surf at t (LRDBBC=T only).
!        PGWFL     : zonal comp grad(gw) at full level at t.
!        PGWFM     : meridian comp grad(gw) at full level at t.
!        PGDW0     : 'g dw' at full levels at t.
!        PKENE0    : kinetic energy at t.
!        PRT0L     : zonal component of "grad R Tt" at full levels.
!        PRT0M     : meridian component of "grad R Tt" at full levels.
!        PRE9F     : hydrostatic pressure "prehyd" at full levels at time t-dt.
!        PNHPRE9F  : "pre" at full levels (time t-dt).
!        PNHPRE9H  : "pre" at half levels (time t-dt).
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height "gz" at t-dt at half levels.
!        PHIF9     : geopotential height "gz" at t-dt at full levels.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PGWFT9    : [gw] at full levels at t-dt.
!        PGWHT9    : [gw] at half levels at t-dt.
!        PGWT9     : [gw] at t-dt (LGWADV=T only, may be at "full 1 to L" or "half 0 to L-1" levels).
!        PGDW9     : 'g dw' at full levels at t-dt.
!        PATND     : adiabatic Lagrangian tendencies.
!        PDBBC     : [D(gw)_surf/Dt]_adiab.
!        PRDPHI    : 1/(R Tt [Delta log(prehyd)]) at t.
!                    "R" is the version of R (Rmoist in NHQE model) used in the definition of vertical divergence "dver".
!        PNHXT0    : term 'NHX' at t.
!        PNHXT9    : term 'NHX' at t-dt.
!        PQCHA0L   : zonal comp grad(log(pre/prehyd)).
!        PQCHA0M   : merid comp grad(log(pre/prehyd)).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ARPEGE documentation

!     Author.
!     -------
!        K. YESSAD, after part 3 of CPG_GP.
!        Original : March 2017

! Modifications
! -------------
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      K. Yessad (March 2018): rewrite Laplacian term in NHQE model.
! End Modifications
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE CPG_DYN_TYPE_MOD,ONLY : CPG_DYN_TYPE
USE YOMGMV   , ONLY : TGMV
USE TYPE_MODEL    , ONLY : MODEL

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LTWOTL, LSPRT, LRPLANE, LSFORC
USE YOMCST   , ONLY : RD, RV
USE YOMDYNA  , ONLY : LGWADV, LRDBBC, LGRADSP, LRUBC
USE YOMCVER  , ONLY : LVERTFE, LVFE_GW,LVFE_LAPL_BC
USE YOMCT3   , ONLY : NSTEP
USE YOMLSFORC, ONLY : LSW_FRC, LSOMEGA_FRC
USE YOMGWDIAG, ONLY : UPDATE_GWDIAG,LGWDIAGS_ON
USE INTDYN_MOD,ONLY : YYTTND, YYTHW0, YYTHW9,&
 & YYTCTY0, YYTXYBDER0, YYTRCP0, YYTRCP9, YYTXYB0, YYTXYB9

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN9
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO
LOGICAL           ,INTENT(IN)    :: LDGPXX
LOGICAL           ,INTENT(IN)    :: LDLDIAB
LOGICAL           ,INTENT(IN)    :: LDMPA
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGL(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGLL(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGMM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROGLM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE0L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE0M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE9L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRE9M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRE0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRE9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHPRE0H(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXYB0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUVH0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHIF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0FL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI0FM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRCP0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWHT0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGDW0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKENE0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRT0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRT0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHPRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHPRE9H(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXYB9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB9%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHI9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHIF9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRCP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP9%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWFT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWHT9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGDW9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATND(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTTND%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDBBC(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRDPHI(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHXT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNHXT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQCHA0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQCHA0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JROF
LOGICAL :: LLDER,LLGWF0,LLGDWI

!    ==== MISCELLANEOUS ======================
REAL(KIND=JPRB) :: ZKAP0H(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZEQCHA0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZEQCHA0H(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZEIQCHA0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXYBDER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYBDER0%NDIM)
REAL(KIND=JPRB) :: ZRPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR0T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZR9T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0L(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER0M(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDVER9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS0_L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0_L(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS0_M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS0_M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZVS9(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZSGRTSL(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZSGRTSM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZPSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSGRTM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTNDUS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTNDVS(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZGPHL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGPHM(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGWHM(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUVH9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTHW9%NDIM)
REAL(KIND=JPRB) :: ZNHXS_T0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHXD_T0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHXS_T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNHXD_T9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZONE(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)      ! contains 1

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "gphpre.intfb.h"
#include "gpgrxyb.intfb.h"
#include "gprcp.intfb.h"
#include "gprcph.intfb.h"
#include "gprt.intfb.h"
#include "gnhpre.intfb.h"
#include "gnhqe_preh.intfb.h"
#include "gpcty.intfb.h"
#include "gpcty_forc.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgrgeo.intfb.h"
#include "gphlwi.intfb.h"
#include "gphluv.intfb.h"
#include "gpuvs.intfb.h"
#include "gnhqe_grp.intfb.h"
#include "gpxx.intfb.h"
#include "gnhqe_xxd.intfb.h"
#include "gpgw.intfb.h"
#include "gnhgrgw.intfb.h"
#include "gp_tndlagadiab_uv.intfb.h"
#include "gnh_tndlagadiab_uvs.intfb.h"
#include "gnhqe_tndlagadiab_gw.intfb.h"
#include "gnhqe_tndlagadiab_svd.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL),YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), &
 & YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE, &
 & NPROMA=>YDGEOMETRY%YRDIM%NPROMA,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)
ASSOCIATE(NDIM=>YDMODEL%YRML_GCONF%YGFL%NDIM, &
 & YQ=>YDMODEL%YRML_GCONF%YGFL%YQ,YRSPEC=>YDMODEL%YRML_GCONF%YGFL%YRSPEC, &
 & YFORC=>YDMODEL%YRML_GCONF%YGFL%YFORC, &
 & NCURRENT_ITER=>YDMODEL%YRML_DYN%YRDYN%NCURRENT_ITER, &
 & NDIMGMV=>YDGMV%NDIMGMV,YT0=>YDGMV%YT0,YT9=>YDGMV%YT9,&
 & LSIMPH=>YDMODEL%YRML_PHY_MF%YRSIMPHL%LSIMPH)

!     ------------------------------------------------------------------

!*       0.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

ZONE(KST:KEND,1:NFLEVG)=1.0_JPRB

!     ------------------------------------------------------------------

!*       3.    INITIALISE AUXILIARY VARIABLES AND TENDENCIES.
!              ----------------------------------------------

!---------------------------------------------------
!      3.1         TIME t0 CALCULATIONS
!---------------------------------------------------

!*     3.1.1 COMPUTE PRE0, PXYB0, PRE0F.

CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,PRE0,PRESF=PRE0F, &
          & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
          & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
          & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.2 COMPUTE ZRPRE0F AND ZXYBDER0.

IF(LVERTFE) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZRPRE0F(JROF,JLEV)=1.0_JPRB/PRE0F(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF
CALL GPGRXYB(NPROMA,KST,KEND,NFLEVG,.FALSE.,YDVAB,PRE0L,PRE0M,PXYBDER=ZXYBDER0,&
           & PDELP=YDCPG_DYN0%XYB%DELP, PLNPR=YDCPG_DYN0%XYB%LNPR, PRDELP=YDCPG_DYN0%XYB%RDELP, &
           & PALPH=YDCPG_DYN0%XYB%ALPH, PRTGR=YDCPG_DYN0%XYB%RTGR, PRPRE=YDCPG_DYN0%XYB%RPRE,  &
           & PRPP=YDCPG_DYN0%XYB%RPP)

!*     3.1.3 COMPUTE "R", "Cp" AND "kap=R/Cp".

CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,PCP=YDCPG_DYN0%RCP%CP,&
 & PR=YDCPG_DYN0%RCP%R,PKAP=YDCPG_DYN0%RCP%KAP)
CALL GPRCPH(YDGEOMETRY,KST,KEND,YDCPG_DYN0%RCP%KAP,ZKAP0H)

!*     3.1.4 COMPUTES "RT" AND ITS GRADIENT.

IF(YRSPEC%LGP) THEN
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,PGMV(1,1,YT0%MT),PGMV(1,1,YT0%MTL),PGMV(1,1,YT0%MTM),&
   & PGFL(1,1,YQ%MPL),PGFL(1,1,YQ%MPM),&
   & ZRT0,PRT0L,PRT0M,PRL=PGFL(1,1,YRSPEC%MPL),PRM=PGFL(1,1,YRSPEC%MPM))
ELSE
  CALL GPRT(LSPRT,NPROMA,KST,KEND,NFLEVG,RD,RV,&
   & YDCPG_DYN0%RCP%R,PGMV(1,1,YT0%MT),PGMV(1,1,YT0%MTL),PGMV(1,1,YT0%MTM),&
   & PGFL(1,1,YQ%MPL),PGFL(1,1,YQ%MPM),&
   & ZRT0,PRT0L,PRT0M)
ENDIF

!*     3.1.6 COMPUTE QUANTITIES FOR NHQE MODEL.

CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,PGMV(1,1,YT0%MSPD),PRE0F,&
 & PKAP=YDCPG_DYN0%RCP%KAP,&
 & PNHPREF=PNHPRE0F,PEQCHAF=ZEQCHA0F,PEIQCHAF=ZEIQCHA0F)
CALL GNHQE_PREH(YDGEOMETRY,KST,KEND,PRE0,PGMV(1,1,YT0%MSPD),PNHPRE0H,&
 & PREF=PRE0F,PEQCHAF=ZEQCHA0F,PKAPH=ZKAP0H,&
 & PEQCHAH=ZEQCHA0H)

PQCHA0L(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT0%MSPDL)
PQCHA0M(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT0%MSPDM)

!*     3.1.7c COMPUTATION OF SOME VERTICAL VELOCITIES.
!*           Solve continuity equation
!            Computation of the vertical velocities "etapt (d prehyd / d eta)"
!            and "omega/prehyd".

CALL GPCTY(YDVFE,NPROMA,KST,KEND,NFLEVG,LRUBC,YDVAB,YDVETA,&
 & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),PGMV(1,1,YT0%MDIV),PGMV(1,1,YT0%MEDOT),&
 & PXYB0,PRE0L,PRE0M,ZRPRE0F,PCTY0)

!*     3.1.7d UPDATE_GWDIAG, GPCTY_FORC, etc.

! Diagnostics of gravity wave noise reflected in surface pressure tendency
IF(LGWDIAGS_ON) THEN
  CALL UPDATE_GWDIAG(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,NPROMA,NFLEVG,KEND,NSTEP,KSTGLO,PRE0,PCTY0(1,1,YYTCTY0%M_DIVDP))
ENDIF

! Overwrite PCTY0 for "EVEL" and "VVEL" with large scale forced values in case
! of 1D model (SCUM)
! (in SCUM, these are 0 in GPCTY)
IF ( LSFORC .AND. (LSW_FRC.OR.LSOMEGA_FRC) ) THEN
  CALL GPCTY_FORC(YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF%YRPHY2,KST,KEND, &
   & PGFL(1,1,YFORC(1)%MP),PRE0F,PGMV(1,1,YT0%MT),YDCPG_DYN0%RCP%R,PCTY0(1,0,YYTCTY0%M_EVEL),PCTY0(1,1,YYTCTY0%M_VVEL))
ENDIF

!*     3.1.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

! * "gz" at full levels and half levels.

PHI0(KST:KEND,NFLEVG)=POROG(KST:KEND)
CALL GPGEO(NPROMA,KST,KEND,NFLEVG,PHI0,PHIF0,ZRT0,&
 & ZONE,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,&
 & YDGEOMETRY%YRVERT_GEOM)

! * "grad gz" at full levels and half levels.

CALL GPGRGEO(YDGEOMETRY,NPROMA,KST,KEND,NFLEVG,&
 & ZRT0,PRT0L,PRT0M,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
 & POROGL,POROGM,&
 & PHI0FL,PHI0FM,ZGPHL,ZGPHM)

!*     3.1.9 COMPUTES KINETIC ENERGY.

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    PKENE0(JROF,JLEV)=0.5_JPRB*(PGMV(JROF,JLEV,YT0%MU)**2+PGMV(JROF,JLEV,YT0%MV)**2)
  ENDDO
ENDDO

!*     3.1.10 COMPUTES HALF-LEVEL WINDS.

CALL GPHLWI(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,PWW0=YDCPG_DYN0%UVH%WWI)
CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),&
          & PWWI=YDCPG_DYN0%UVH%WWI,PUH=YDCPG_DYN0%UVH%UH,PVH=YDCPG_DYN0%UVH%VH)


LLDER=.TRUE.
CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,&
 & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),ZUS0,ZVS0,&
 & PDIVF=PGMV(1,1,YT0%MDIV),PVORF=PGMV(1,1,YT0%MVOR),&
 & PUFL=PGMV(1,1,YT0%MUL),PVFL=PGMV(1,1,YT0%MVL),&
 & PUS_L=ZUS0_L,PVS_L=ZVS0_L,PUS_M=ZUS0_M,PVS_M=ZVS0_M)

!*     3.1.12 COMPUTES THE PRESSURE FORCE FOR THE RHS OF MOMENTUM EQN.

CALL GNHQE_GRP(YDGEOMETRY,KST,KEND,&
 & ZRT0,PRT0L,PRT0M,PRE0L,PRE0M,PXYB0,ZXYBDER0,PRE0,&
 & PGMV(1,1,YT0%MSPD),PQCHA0L,PQCHA0M,ZEQCHA0F,ZEQCHA0H,YDCPG_DYN0%RCP%KAP,ZKAP0H,&
 & ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
 & ZPSGRTL,ZPSGRTM)

! store for next time-step
IF( LGRADSP ) THEN

  ! adjust field with filter result
  ZSGRTL(KST:KEND,1:NFLEVG)=ZPSGRTL(KST:KEND,1:NFLEVG)&
   & - (PGMV(KST:KEND,1:NFLEVG,YT9%MSGRTL) - PGMV(KST:KEND,1:NFLEVG,YT0%MSGRTL))
  ZSGRTM(KST:KEND,1:NFLEVG)=ZPSGRTM(KST:KEND,1:NFLEVG)&
   & - (PGMV(KST:KEND,1:NFLEVG,YT9%MSGRTM) - PGMV(KST:KEND,1:NFLEVG,YT0%MSGRTM))

  ! store for next time-step unfiltered fields
  PGMV(KST:KEND,1:NFLEVG,YT0%MSGRTL)=ZPSGRTL(KST:KEND,1:NFLEVG)
  PGMV(KST:KEND,1:NFLEVG,YT0%MSGRTM)=ZPSGRTM(KST:KEND,1:NFLEVG)

  ! set new values for current timestep
  ZPSGRTL(KST:KEND,1:NFLEVG)=ZSGRTL(KST:KEND,1:NFLEVG)
  ZPSGRTM(KST:KEND,1:NFLEVG)=ZSGRTM(KST:KEND,1:NFLEVG)

ENDIF

ZSGRTSL(KST:KEND)=ZPSGRTL(KST:KEND,NFLEVG)
ZSGRTSM(KST:KEND)=ZPSGRTM(KST:KEND,NFLEVG)

!*     3.1.13 COMPUTES TERM "NHX".

IF (LDGPXX) THEN
  ! * compute NHX_s = "wind shear" part of NHX
  CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
   & YDCPG_DYN0%XYB%LNPR,ZRT0,&
   & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,&
   & ZNHXS_T0)
  ! * compute NHX_d = "divergence" part of NHX
  CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
   & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),PXYB0,PRE0L,PRE0M,YDCPG_DYN0%RCP%KAP,&
   & ZNHXD_T0)
  ! * compute NHX
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      PNHXT0(JROF,JLEV)=ZNHXS_T0(JROF,JLEV)+ZNHXD_T0(JROF,JLEV)
    ENDDO
  ENDDO
ELSE
  PNHXT0(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT0%MNHX)
  ! ??? But we also need ZNHXS_T0 for RHS of dver equation, and there are two possibilities
  !     which a priori do not give the same results:
  ! ??? first method: compute ZNHXD_T0 via GNHQE_XXD, , then ZNHXS_T0=PNHXT0-ZNHXD_T0
  !     this is the most consistent with what is done in NHEE model
  ! * compute NHX_d
  CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
   & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),PXYB0,PRE0L,PRE0M,YDCPG_DYN0%RCP%KAP,&
   & ZNHXD_T0)
  ! * compute NHX_s
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZNHXS_T0(JROF,JLEV)=PNHXT0(JROF,JLEV)-ZNHXD_T0(JROF,JLEV)
    ENDDO
  ENDDO
  ! ??? second method: compute ZNHXS_T0 via GPXX, ZNHXD_T0 is useless
  !     CALL GPXX as above
ENDIF

!*     3.1.14 COMPUTES dver AND grad(dver).

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    ZDVER0(JROF,JLEV)=PGMV(JROF,JLEV,YT0%MSVD)-PNHXT0(JROF,JLEV)
    ZDVER0L(JROF,JLEV)=PGMV(JROF,JLEV,YT0%MSVDL)-PGMV(JROF,JLEV,YT0%MNHXL)
    ZDVER0M(JROF,JLEV)=PGMV(JROF,JLEV,YT0%MSVDM)-PGMV(JROF,JLEV,YT0%MNHXM)
  ENDDO
ENDDO

!*     3.1.16 COMPUTES term [gw].

LLGWF0=.TRUE.
LLGDWI=.FALSE.
IF (LGWADV.AND.LVFE_GW) THEN
  ! PGDW is required (at least for SL2TL)
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
   & POROGL,POROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZUS0,ZVS0,&
   & ZRT0,ZDVER0,PGWHT0,PGWFT0,PGDW=PGDW0)

  PGWT0(KST:KEND,1:NFLEVG)=PGWFT0(KST:KEND,1:NFLEVG)
ELSE
  CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LLGWF0,LLGDWI,&
   & POROGL,POROGM,YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZUS0,ZVS0,&
   & ZRT0,ZDVER0,PGWHT0,PGWFT0)

  IF (LGWADV) PGWT0(KST:KEND,1:NFLEVG)=PGWHT0(KST:KEND,0:NFLEVG-1)
ENDIF

IF (LTWOTL.AND.LRDBBC) THEN
  ! PGWS contains (gw)_surf
  PGWS(KST:KEND)=PGWHT0(KST:KEND,NFLEVG)
ENDIF

!*     3.1.17 COMPUTES grad(gw).

LLDER=.TRUE.
CALL GNHGRGW(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,LLDER,&
 & ZRT0,PRT0L,PRT0M,&
 & ZDVER0,ZDVER0L,ZDVER0M,&
 & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,ZXYBDER0,&
 & ZUS0,ZVS0,ZUS0_L,ZVS0_L,ZUS0_M,ZVS0_M,&
 & POROGL,POROGM,POROGLM,POROGLL,POROGMM,&
 & PGWFL,PGWFM,ZGWHL,ZGWHM)

!*     3.1.19 COMPUTES 1/(R Tt [Delta log(prehyd)]) at t.

IF (.NOT.LGWADV) THEN
  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      PRDPHI(JROF,JLEV)=1.0_JPRB/(YDCPG_DYN0%XYB%LNPR(JROF,JLEV)*ZRT0(JROF,JLEV))
    ENDDO
  ENDDO
ENDIF

!*     3.1.20a COMPUTES [D V/Dt].

! Pressure gradient term + explicit Coriolis + Rayleigh friction
CALL GP_TNDLAGADIAB_UV(YDGEOMETRY,YDGMV,YDMODEL%YRML_PHY_EC%YREPHY,YDMODEL%YRML_DYN%YRDYN,&
 & KST,KEND,YDGSGEOM%RCORI,&
 & YDGSGEOM%GNORDL,YDGSGEOM%GNORDM,&
 & ZPSGRTL,ZPSGRTM,&
 & PGMV,PATND(1,1,YYTTND%M_TNDU),PATND(1,1,YYTTND%M_TNDV),&
 & PATND(1,1,YYTTND%M_TNDU_NOC),PATND(1,1,YYTTND%M_TNDV_NOC))

!*     3.1.20b COMPUTES [D V_surf/Dt].

CALL GNH_TNDLAGADIAB_UVS(NPROMA,KST,KEND,LRPLANE,&
 & YDGSGEOM%RCORI,YDCSGEOM%RATATX,YDCSGEOM%RATATH,&
 & ZUS0,ZVS0,ZSGRTSL,ZSGRTSM,&
 & ZTNDUS,ZTNDVS)

!*     3.1.21 COMPUTES [D(gw)/Dt] if LGWADV=T.

! Remark: PATND(.,.,YYTTND%M_TNDGW) is computed only if LGWADV=T.
IF (LGWADV) THEN
  ! warning: formerly LVFE_LAPL.OR.LVFE_GW
  IF (LVFE_LAPL_BC.OR.LVFE_GW) CALL ABOR1(' CPG_GP_NHQE 3.1.21: option not yet coded!')

  CALL GNHQE_TNDLAGADIAB_GW(YDGEOMETRY,KST,KEND,&
   & POROGL,POROGM,POROGLM,POROGLL,POROGMM,&
   & ZUS0,ZVS0,ZTNDUS,ZTNDVS,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,PRE0F,PRE0,&
   & ZEQCHA0F,YDCPG_DYN0%RCP%KAP,&
   & PATND(1,1,YYTTND%M_TNDGW))
ENDIF

!*     3.1.23 COMPUTES [D(vertical divergence variable)/Dt] if LGWADV=F.

! Remark: PATND(.,.,YYTTND%M_TNDVD) is computed only if LGWADV=F.
IF (.NOT.LGWADV) THEN
  IF (LVERTFE) CALL ABOR1(' CPG_GP_NHQE 3.1.23: not yet coded case')

  CALL GNHQE_TNDLAGADIAB_SVD(YDGEOMETRY,KST,KEND,&
   & POROGL,POROGM,POROGLM,POROGLL,POROGMM,&
   & YDCPG_DYN0%XYB%LNPR,YDCPG_DYN0%XYB%ALPH,PRE0F,PRE0,&
   & PGMV(1,1,YT0%MU),PGMV(1,1,YT0%MV),YDCPG_DYN0%UVH%UH,YDCPG_DYN0%UVH%VH,ZTNDUS,ZTNDVS,&
   & ZEQCHA0F,YDCPG_DYN0%RCP%KAP,&
   & PRDPHI,ZGWHL,ZGWHM,ZDVER0,ZNHXS_T0,&
   & PATND(1,1,YYTTND%M_TNDVD),PDBBC)
ENDIF

!*     3.1.24 COMPUTES [DT/Dt].

DO JLEV=1,NFLEVG
  DO JROF=KST,KEND
    PATND(JROF,JLEV,YYTTND%M_TNDT)=YDCPG_DYN0%RCP%KAP(JROF,JLEV)*PGMV(JROF,JLEV,YT0%MT)*PCTY0(JROF,JLEV,YYTCTY0%M_VVEL)
  ENDDO
ENDDO

!---------------------------------------------------
!      3.2         TIME t9 CALCULATIONS
!---------------------------------------------------

IF (.NOT.LTWOTL .AND. NCURRENT_ITER == 0) THEN

!*     3.2.1 COMPUTE PRE9, PXYB9, PRE9F.

  CALL GPHPRE(NPROMA,NFLEVG,KST,KEND,YDVAB,PRE9,PRESF=PRE9F,&
            & PDELP=YDCPG_DYN9%XYB%DELP, PLNPR=YDCPG_DYN9%XYB%LNPR, PRDELP=YDCPG_DYN9%XYB%RDELP, &
            & PALPH=YDCPG_DYN9%XYB%ALPH, PRTGR=YDCPG_DYN9%XYB%RTGR, PRPRE=YDCPG_DYN9%XYB%RPRE,  &
            & PRPP=YDCPG_DYN9%XYB%RPP)

!*     3.2.3 COMPUTE "R", "Cp" AND "Kap=R/Cp".

  CALL GPRCP(NPROMA,KST,KEND,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=YDCPG_DYN9%RCP%CP,&
   & PR=YDCPG_DYN9%RCP%R,PKAP=YDCPG_DYN9%RCP%KAP)

!*     3.2.4 COMPUTE "RT".

  ! ZR9T9 is R(t-dt)*Tt(t-dt); ZR0T9 is R(t)*Tt(t-dt) (for GPXX)
  ZR9T9(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT9%MT)*YDCPG_DYN9%RCP%R(KST:KEND,1:NFLEVG)
  ZR0T9(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT9%MT)*YDCPG_DYN0%RCP%R(KST:KEND,1:NFLEVG)

!*     3.2.6 COMPUTE QUANTITIES FOR NHQE MODEL.

  CALL GNHPRE(YDGEOMETRY,NPROMA,NFLEVG,KST,KEND,PGMV(1,1,YT9%MSPD),PRE9F,&
   & PKAP=YDCPG_DYN9%RCP%KAP,&
   & PNHPREF=PNHPRE9F)
  CALL GNHQE_PREH(YDGEOMETRY,KST,KEND,PRE9,PGMV(1,1,YT9%MSPD),PNHPRE9H)

!*     3.2.8 COMPUTES THE GEOPOTENTIAL HEIGHT "gz" AND ITS GRADIENT.

  ! * "gz" at full levels and half levels.
  IF((LDLDIAB.OR.LSIMPH)) THEN
    PHI9(KST:KEND,NFLEVG)=POROG(KST:KEND)
    CALL GPGEO(NPROMA,KST,KEND,NFLEVG,PHI9,PHIF9,ZR9T9,&
     & ZONE,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,&
     & YDGEOMETRY%YRVERT_GEOM)
  ENDIF

!*     3.2.10 COMPUTES HALF-LEVEL WINDS.

  ! Not needed if X-term and Z_term calculated FE.
  IF (LDGPXX .AND. .NOT.LVERTFE) THEN
    ! * same remark as for time t half-level winds.
    ZUVH9(KST:KEND,1:NFLEVG,YYTHW9%M_WWI)=YDCPG_DYN0%UVH%WWI(KST:KEND,1:NFLEVG)
    CALL GPHLUV(YDGEOMETRY%YRDIMV,NPROMA,KST,KEND,PGMV(1,1,YT9%MU),PGMV(1,1,YT9%MV),ZUVH9)
  ENDIF

  LLDER=.FALSE.
  CALL GPUVS(NFLEVG,NPROMA,KST,KEND,LLDER,PGMV(1,1,YT9%MU),PGMV(1,1,YT9%MV),ZUS9,ZVS9)

!*     3.2.13 COMPUTES TERM "NHX".

  IF (LDGPXX) THEN
    ! * compute NHX_s = "wind shear" part of NHX
    CALL GPXX(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,ZGPHL,ZGPHM,PHI0FL,PHI0FM,&
     & YDCPG_DYN9%XYB%LNPR,ZR0T9,&
     & PGMV(1,1,YT9%MU),PGMV(1,1,YT9%MV),ZUVH9(1,0,YYTHW9%M_UH),ZUVH9(1,0,YYTHW9%M_VH),&
     & ZNHXS_T9)
    ! * compute NHX_d = "divergence" part of NHX
    CALL GNHQE_XXD(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,&
     & PGMV(1,1,YT9%MU),PGMV(1,1,YT9%MV),PXYB9,PRE9L,PRE9M,YDCPG_DYN9%RCP%KAP,&
     & ZNHXD_T9)
    ! * compute NHX
    DO JLEV=1,NFLEVG
      DO JROF=KST,KEND
        PNHXT9(JROF,JLEV)=ZNHXS_T9(JROF,JLEV)+ZNHXD_T9(JROF,JLEV)
      ENDDO
    ENDDO
  ELSE
    ! ??? we do not need separate contributions ZNHXS_T9 and ZNHXD_T9 in this case.
    PNHXT9(KST:KEND,1:NFLEVG)=PGMV(KST:KEND,1:NFLEVG,YT9%MNHX)
  ENDIF

!*     3.2.14 COMPUTES dver.

  DO JLEV=1,NFLEVG
    DO JROF=KST,KEND
      ZDVER9(JROF,JLEV)=PGMV(JROF,JLEV,YT9%MSVD)-PNHXT9(JROF,JLEV)
    ENDDO
  ENDDO

!*     3.2.16 COMPUTES term [gw].

  LLGDWI=.FALSE.
  IF (LGWADV.AND.LVFE_GW) THEN
    ! PGDW is required (at least for SL3TL)
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
     & POROGL,POROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZUS9,ZVS9,&
     & ZR9T9,ZDVER9,PGWHT9,PGWFT9,PGDW=PGDW9)

    PGWT9(KST:KEND,1:NFLEVG)=PGWFT9(KST:KEND,1:NFLEVG)
  ELSE
    CALL GPGW(YDGEOMETRY,NFLEVG,NPROMA,KST,KEND,LDMPA,LLGDWI,&
     & POROGL,POROGM,YDCPG_DYN9%XYB%LNPR,YDCPG_DYN9%XYB%ALPH,ZUS9,ZVS9,&
     & ZR9T9,ZDVER9,PGWHT9,PGWFT9)

    IF (LGWADV) PGWT9(KST:KEND,1:NFLEVG)=PGWHT9(KST:KEND,0:NFLEVG-1)
  ENDIF

  IF (LRDBBC) THEN
    ! PGWS contains (gw)_surf
    PGWS(KST:KEND)=PGWHT9(KST:KEND,NFLEVG)
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPG_GP_NHQE',1,ZHOOK_HANDLE)
END SUBROUTINE CPG_GP_NHQE
