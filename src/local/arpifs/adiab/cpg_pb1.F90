SUBROUTINE CPG_PB1 (YDCPG_DIM, YDCPG_OPTS, YDMODEL, YDSL, PB1, PSLBUF1AU)

!*       5.2   Update PB1, first fields with prefetch (Intel only)

USE PARKIND1, ONLY : JPIM, JPRB

USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE, CPG_OPTS_TYPE
USE TYPE_MODEL         , ONLY : MODEL
USE EINT_MOD           , ONLY : SL_STRUCT

IMPLICIT NONE

TYPE(CPG_DIM_TYPE),INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_OPTS_TYPE),INTENT(IN)   :: YDCPG_OPTS
TYPE(MODEL)       ,INTENT(IN)    :: YDMODEL
TYPE(SL_STRUCT),   INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB1(YDSL%NASLB1,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSLBUF1AU(YDCPG_OPTS%KLON,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)

#ifdef __INTEL_COMPILER
INTEGER(KIND=JPIM), PARAMETER :: JPREFETCH = 8
#else
INTEGER(KIND=JPIM), PARAMETER :: JPREFETCH = 0
#endif


INTEGER (KIND=JPIM) :: JFLD
INTEGER (KIND=JPIM) :: JROF


#ifdef __INTEL_COMPILER
  ! optim: prefetch does not help on nslcore, prefer to prefetch 1 level
  JFLD = MIN(YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1,JPREFETCH)
  DO JROF=YDCPG_DIM%KSTGLO,YDCPG_DIM%KSTGLO+YDCPG_DIM%KFDIA-1,4
    CALL MM_PREFETCH(PB1(YDSL%NSLCORE(JROF),JFLD),3)
  ENDDO
#endif

  DO JFLD=1,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1-JPREFETCH
    !DIR$ NEXTSCALAR
    !DIR$ IVDEP
    !DIR$ NOINTERCHANGE
    !DIR$ LOOP COUNT AVG(16)
    DO JROF=YDCPG_DIM%KSTGLO,YDCPG_DIM%KSTGLO+YDCPG_DIM%KFDIA-1
#ifdef __INTEL_COMPILER
      ! Prefetch next field from PB1 to L2, same point
      CALL MM_PREFETCH(PB1(YDSL%NSLCORE(JROF),JFLD+JPREFETCH),3)
#endif
      PB1(YDSL%NSLCORE(JROF),JFLD)=PSLBUF1AU(JROF-YDCPG_DIM%KSTGLO+1,JFLD)
    ENDDO
  ENDDO

  DO JFLD=MAX(YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1-JPREFETCH+1,1),YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1
    !DIR$ IVDEP
    !DIR$ LOOP COUNT AVG(16)
    DO JROF=YDCPG_DIM%KSTGLO,YDCPG_DIM%KSTGLO+YDCPG_DIM%KFDIA-1
      PB1(YDSL%NSLCORE(JROF),JFLD)=PSLBUF1AU(JROF-YDCPG_DIM%KSTGLO+1,JFLD)
    ENDDO
  ENDDO



END SUBROUTINE
