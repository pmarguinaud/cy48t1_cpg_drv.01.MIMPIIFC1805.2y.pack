!---------------------------------------------------------------
SUBROUTINE CPTENDSM( YDPHY,KPROMA, KSTART, KPROF, KFLEV,&
 !---------------------------------------------------------------      
 !  INPUT 2D 
 & PDIFCQ , PDIFCS , PDIFTQ , PDIFTS ,&
 & PFCCQL , PFCCQN , PFCSQL , PFCSQN ,&
 & PFPLCL , PFPLCN , PFPLSL , PFPLSN ,&
 & PFRSO  , PFRTH  ,&
 & PSTRCU , PSTRCV , PSTRDU , PSTRDV ,&
 & PSTRTU , PSTRTV , &
 & PSTRMU , PSTRMV , PFRMH  ,&
 & PRDELP , PAPHIF , &
 & PU     , PV     , PT     , PQ     ,          &
 !  INPUT 1D 
 & PQS    , PTS    , POROG  ,      &
 !  OUTPUT 2D 
 & PTENDU , PTENDV , PTENDH ,&
 & PTENDQ ,&
 & P_ZFHPCL , P_ZFHPCN , P_ZFHPSL , P_ZFHPSN ,&
 & P_ZFHSCL , P_ZFHSCN , P_ZFHSSL , P_ZFHSSN )  

!**** *CPTENDSM* - COMPUTATION OF PHYSICAL TENDENCIES FOR THE SIMPLIFIED
!                  PHYSICS

!    Subject.
!    --------
!     - COMPUTATION OF THE FLUXES OF ENTHALPY AND SENSIBLE HEAT FOR
!       PRECIPITATION AND CONDENSATION
!     - COMPUTATION OF PHYSICAL TENDENCIES FOR U, V, ENTHALPY H, 
!       SPECIFIC HUMIDITY Q AND FOR TEMPERATURE

!     SEE THE DOCUMENTATION - INTERFACE PHYSICO-DYNAMIQUE

! --------------------------------------------------------------------

! -   ARGUMENTS FOR INPUT
!     -------------------

! - DIMENSIONAL PARAMETERS

!     KSTART : START OF HORIZONTAL LOOP.
!     KPROF  : END OF HORIZONTAL LOOP.
!     KPROMA : HORIZONTAL DIMENSION.
!     KFLEV  : VERTICAL DIMENSION (FULL LEVELS).

! - 2D (0:KFLEV).
!   -------------
!     PDIFCQ     : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
!     PDIFCS     : CONVECTIVE FLUX OF ENTHALPY (NOT RAIN/SNOW).
!     PDIFTQ     : TURBULENT FLUX (INC. Q NEGATIVE) OF SPECIFIC HUMIDITY.
!     PDIFTS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY).
!     PFCCQL     : CONVECTIVE CONDENSATION FLUX FOR LIQUID WATER.
!     PFCCQN     : CONVECTIVE CONDENSATION FLUX FOR ICE. 
!     PFCSQL     : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
!     PFCSQN     : STRATIFORM CONDENSATION FLUX FOR ICE.
!     PFPLCL     : CONVECTIVE PRECIPITATION AS RAIN.
!     PFPLCN     : CONVECTIVE PRECIPITATION AS SNOW.
!     PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
!     PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.
!     PFRSO      : SHORTWAVE RADIATIVE FLUX.
!     PFRTH      : LONGWAVE RADIATIVE FLUX.
!     PSTRCU     : CONVECTIVE FLUX OF MOMENTUM "U".
!     PSTRCV     : CONVECTIVE FLUX OF MOMENTUM "V".
!     PSTRDU     : GRAVITY WAVE DRAG FLUX "U".
!     PSTRDV     : GRAVITY WAVE DRAG FLUX "V".
!     PSTRTU     : TURBULENT FLUX OF MOMENTUM "U".
!     PSTRTV     : TURBULENT FLUX OF MOMENTUM "V".
!     PSTRMU     : MESOSPHERIC FLUX FOR "U"-MOMENTUM.
!     PSTRMV     : MESOSPHERIC FLUX FOR "V"-MOMENTUM.
!     PFRMH      : MESOSPHERIC ENTHALPY FLUX.

! - 2D (1:KFLEV).
!   --------------
!     PRDELP     : INVERSE OF LAYER THICKNESS IN PRESSURE UNITS.
!     PAPHIF     : GEOPOTENTIAL ON FULL LEVELS.
!     PU         : X COMPONENT OF WIND.
!     PV         : Y COMPONENT OF WIND.
!     PT         : TEMPERATURE.
!     PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.

! --- INPUT 1D.
!     ----------
!     PTS        : SURFACE TEMPERATURE. 
!     PQS        : SURFACE SPECIFIC HUMIDITY. 
!     POROG      : SURFACE GEOPOTENTIAL.

! ---------------------------------------------------------------------

! -   ARGUMENTS FOR OUTPUT.
!     ----------------------

! --- 2D (1,KFLEV).
!     -------------
!     PTENDU     : TENDENCY OF U COMPONENT OF WIND.
!     PTENDV     : TENDENCY OF V COMPONENT OF WIND.
!     PTENDH     : TENDENCY OF ENTHALPY. 
!     PTENDQ     : TENDENCY OF HUMIDITY.

!---------------------------------------------------------------------

! -  IMPLICIT ARGUMENTS.
!    -------------------

!     COMMON YOMCST : RG,RCPD,RCPV,RCW,RCS,RLVZER,RLSZER
!     COMMON YOMPHY : NDPSFI,LCONDWT

!---------------------------------------------------------------------  

!     Methode.
!     --------

!     Author.
!     -------
!        see CPFHPFS and CPTEND

!     Modifications.
!     --------------
!      01-03, mesospheric drag - F. Bouyssel
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!------------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RCPD     ,RCPV     ,RCW      ,&
 & RCS      ,RLVZER   ,RLSZER  
USE YOMPHY   , ONLY : TPHY

!------------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTART 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCQ(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCS(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTQ(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTS(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCCQL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCCQN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRSO(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRTH(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRCU(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRCV(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRDU(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRDV(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRTU(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRTV(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRMU(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTRMV(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRMH(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDU(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDV(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDH(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDQ(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHPCL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHPCN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHPSL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHPSN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHSCL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHSCN(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHSSL(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: P_ZFHSSN(KPROMA,0:KFLEV) 

!------------------------------------------------------------------------

REAL(KIND=JPRB) :: ZGSDP(KPROMA,KFLEV),ZCP(KPROMA,KFLEV)

REAL(KIND=JPRB) :: ZFHP  (KPROMA,0:KFLEV),ZFP   (KPROMA,0:KFLEV)
REAL(KIND=JPRB) :: ZFEPFP(KPROMA,0:KFLEV)

REAL(KIND=JPRB) :: ZCPS (KPROMA)

INTEGER(KIND=JPIM) :: JLEV, JLON, JROF

REAL(KIND=JPRB) :: ZCPI, ZDPSFI, ZFPDP, ZTI
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPTENDSM',0,ZHOOK_HANDLE)
ASSOCIATE(NDPSFI=>YDPHY%NDPSFI)
!------------------------------------------------------------------------

!*
!     ---------------------------------------------------
!     I - AUXILIARY CONSTANTS

ZDPSFI = REAL( NDPSFI ,JPRB)

!*
!     -----------------------------------------------------
!     II - COMPUTATION OF Cp.

! - TEMPORARY 2D

!     ZCP  : VALUE OF ATMOSPHERIC Cp

! - TEMPORARY 1D

!     ZCPS  : SPECIFIC HEAT AT THE SURFACE

DO JLON=KSTART,KPROF

  ZCPS(JLON)=RCPD*(1.0_JPRB-PQS(JLON))+RCPV*PQS(JLON)
  DO JLEV=1,KFLEV

    ZCP(JLON,JLEV)=RCPD*(1.0_JPRB-PQ(JLON,JLEV))+RCPV*PQ(JLON,JLEV)
  ENDDO
ENDDO

!*
!     ----------------------------------------------------------
!     III - COMPUTATION OF THE FLUXES OF ENTHALPY AND SENSIBLE HEAT FOR
!           PRECIPITATION AND CONDENSATION

! - TEMPORARY 2D (0:KFLEV).

!     ZFHSCL : CONVECTIVE SENSIBLE HEAT FLUX FOR LIQUID WATER.
!     ZFHSCN : CONVECTIVE SENSIBLE HEAT FLUX FOR SNOW.
!     ZFHSSL : STRATIFORM SENSIBLE HEAT FLUX FOR LIQUID WATER.
!     ZFHSSN : STRATIFORM SENSIBLE HEAT FLUX FOR SNOW.
!     ZFHPCL : FLUX OF ENTHALPY FOR LIQUID CONVECTIVE CONDENSATION.
!     ZFHPCN : FLUX OF ENTHALPY FOR SNOW CONVECTIVE CONDENSATION.
!     ZFHPSL : FLUX OF ENTHALPY FOR LIQUID STRATIFORM CONDENSATION.
!     ZFHPSN : FLUX OF ENTHALPY FOR SNOW STRATIFORM CONDENSATION.
!     ZFHP   : TOTAL FLUX OF ENTHALPY + SENSIBLE HEAT.
!     ZFP    : TOTAL FLUX OF PRECIPITATIONS.
!     ZFEPFP : PSEUDO-FLUX OF ENTHALPY FOR ENERGY OF PRECIPITATIONS.

! --- COMPUTATION AT THE TOP.
!     -----------------------

DO JLON=KSTART,KPROF
  P_ZFHSCL(JLON,0)=PFPLCL(JLON,0)*PT(JLON,1)*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSCN(JLON,0)=PFPLCN(JLON,0)*PT(JLON,1)*(RCS-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSSL(JLON,0)=PFPLSL(JLON,0)*PT(JLON,1)*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSSN(JLON,0)=PFPLSN(JLON,0)*PT(JLON,1)*(RCS-RCPD*(1.0_JPRB-ZDPSFI))

  P_ZFHPCL(JLON,0)= - RLVZER * PFCCQL(JLON,0)
  P_ZFHPCN(JLON,0)= - RLSZER * PFCCQN(JLON,0)
  P_ZFHPSL(JLON,0)= - RLVZER * PFCSQL(JLON,0)
  P_ZFHPSN(JLON,0)= - RLSZER * PFCSQN(JLON,0)

  ZFP(JLON,0)= PFPLCL(JLON,0)+PFPLCN(JLON,0)+ PFPLSL(JLON,0)+PFPLSN(JLON,0)

  ZFEPFP(JLON,0)= - ZDPSFI * ZCP(JLON,1) * PT(JLON,1) *   ZFP(JLON,0)

  ZFHP(JLON,0) = P_ZFHSCL(JLON,0) + P_ZFHSCN(JLON,0)&
   & + P_ZFHSSL(JLON,0) + P_ZFHSSN(JLON,0)&
   & + P_ZFHPCL(JLON,0) + P_ZFHPCN(JLON,0)&
   & + P_ZFHPSL(JLON,0) + P_ZFHPSN(JLON,0)&
   & + ZFEPFP(JLON,0)  
ENDDO

! --- CALCULATION THE FLUXES AT THE LEVELS
!     -------------------------------------

DO JLEV = 1 , KFLEV-1
  DO JLON = KSTART , KPROF
    ZTI = 0.5_JPRB * ( PT(JLON,JLEV) + PT(JLON,JLEV+1) )
    ZCPI= 0.5_JPRB * ( ZCP(JLON,JLEV) + ZCP(JLON,JLEV+1) )

    P_ZFHSCL(JLON,JLEV)=PFPLCL(JLON,JLEV)*ZTI*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
    P_ZFHSCN(JLON,JLEV)=PFPLCN(JLON,JLEV)*ZTI*(RCS-RCPD*(1.0_JPRB-ZDPSFI))
    P_ZFHSSL(JLON,JLEV)=PFPLSL(JLON,JLEV)*ZTI*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
    P_ZFHSSN(JLON,JLEV)=PFPLSN(JLON,JLEV)*ZTI*(RCS-RCPD*(1.0_JPRB-ZDPSFI))

    P_ZFHPCL(JLON,JLEV)= - RLVZER * PFCCQL(JLON,JLEV)
    P_ZFHPCN(JLON,JLEV)= - RLSZER * PFCCQN(JLON,JLEV)
    P_ZFHPSL(JLON,JLEV)= - RLVZER * PFCSQL(JLON,JLEV)
    P_ZFHPSN(JLON,JLEV)= - RLSZER * PFCSQN(JLON,JLEV)

    ZFP(JLON,JLEV)= PFPLCL(JLON,JLEV)+PFPLCN(JLON,JLEV)&
     & + PFPLSL(JLON,JLEV)+PFPLSN(JLON,JLEV)  

    ZFEPFP(JLON,JLEV)= - ZDPSFI * ZCPI * ZTI * ZFP(JLON,JLEV)

    ZFHP(JLON,JLEV) = P_ZFHSCL(JLON,JLEV) + P_ZFHSCN(JLON,JLEV)&
     & + P_ZFHSSL(JLON,JLEV) + P_ZFHSSN(JLON,JLEV)&
     & + P_ZFHPCL(JLON,JLEV) + P_ZFHPCN(JLON,JLEV)&
     & + P_ZFHPSL(JLON,JLEV) + P_ZFHPSN(JLON,JLEV)&
     & + ZFEPFP(JLON,JLEV)  
  ENDDO
ENDDO

! --- SURFACE CALCULATIONS.
!     ----------------------

DO JLON=KSTART,KPROF
  P_ZFHSCL(JLON,KFLEV)=PFPLCL(JLON,KFLEV)*PTS(JLON)*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSCN(JLON,KFLEV)=PFPLCN(JLON,KFLEV)*PTS(JLON)*(RCS-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSSL(JLON,KFLEV)=PFPLSL(JLON,KFLEV)*PTS(JLON)*(RCW-RCPD*(1.0_JPRB-ZDPSFI))
  P_ZFHSSN(JLON,KFLEV)=PFPLSN(JLON,KFLEV)*PTS(JLON)*(RCS-RCPD*(1.0_JPRB-ZDPSFI))

  P_ZFHPCL(JLON,KFLEV)= - RLVZER * PFCCQL(JLON,KFLEV)
  P_ZFHPCN(JLON,KFLEV)= - RLSZER * PFCCQN(JLON,KFLEV)
  P_ZFHPSL(JLON,KFLEV)= - RLVZER * PFCSQL(JLON,KFLEV)
  P_ZFHPSN(JLON,KFLEV)= - RLSZER * PFCSQN(JLON,KFLEV)

  ZFP(JLON,KFLEV)= PFPLCL(JLON,KFLEV)+PFPLCN(JLON,KFLEV)&
   & + PFPLSL(JLON,KFLEV)+PFPLSN(JLON,KFLEV)  

  ZFEPFP(JLON,KFLEV)= - ZDPSFI * ZCPS(JLON) * PTS(JLON) * ZFP(JLON,KFLEV)

  ZFHP(JLON,KFLEV) = P_ZFHSCL(JLON,KFLEV) + P_ZFHSCN(JLON,KFLEV)&
   & + P_ZFHSSL(JLON,KFLEV) + P_ZFHSSN(JLON,KFLEV)&
   & + P_ZFHPCL(JLON,KFLEV) + P_ZFHPCN(JLON,KFLEV)&
   & + P_ZFHPSL(JLON,KFLEV) + P_ZFHPSN(JLON,KFLEV)&
   & + ZFEPFP(JLON,KFLEV)  
ENDDO

!*
!     ---------------------------------------------------------------
!     IV - CALCULATION OF THE EVOLUTION Q AND dU/dT AND dH/dt
!          (WITH H = CpT + Phi + Ec )

DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF

    ZGSDP(JROF,JLEV)=RG*PRDELP(JROF,JLEV)

    PTENDU(JROF,JLEV) = ZGSDP(JROF,JLEV)&
     & *((PSTRCU(JROF,JLEV-1) - PSTRCU(JROF,JLEV))&
     & + (PSTRDU(JROF,JLEV-1) - PSTRDU(JROF,JLEV))&
     & + (PSTRMU(JROF,JLEV-1) - PSTRMU(JROF,JLEV))&
     & + (PSTRTU(JROF,JLEV-1) - PSTRTU(JROF,JLEV)))  

    PTENDV(JROF,JLEV) = ZGSDP(JROF,JLEV)&
     & *((PSTRCV(JROF,JLEV-1) - PSTRCV(JROF,JLEV))&
     & + (PSTRDV(JROF,JLEV-1) - PSTRDV(JROF,JLEV))&
     & + (PSTRMV(JROF,JLEV-1) - PSTRMV(JROF,JLEV))&
     & + (PSTRTV(JROF,JLEV-1) - PSTRTV(JROF,JLEV)))  

    PTENDQ(JROF,JLEV) = ZGSDP(JROF,JLEV)&
     & *((PDIFTQ(JROF,JLEV-1) - PDIFTQ(JROF,JLEV))&
     & + (PDIFCQ(JROF,JLEV-1) - PDIFCQ(JROF,JLEV))&
     & + (PFCCQL(JROF,JLEV-1) - PFCCQL(JROF,JLEV))&
     & + (PFCCQN(JROF,JLEV-1) - PFCCQN(JROF,JLEV))&
     & + (PFCSQL(JROF,JLEV-1) - PFCSQL(JROF,JLEV))&
     & + (PFCSQN(JROF,JLEV-1) - PFCSQN(JROF,JLEV)))  

    PTENDH(JROF,JLEV) = ZGSDP(JROF,JLEV)&
     & *((PFRSO (JROF,JLEV-1) - PFRSO (JROF,JLEV))&
     & + (PFRTH (JROF,JLEV-1) - PFRTH (JROF,JLEV))&
     & + (PFRMH (JROF,JLEV-1) - PFRMH (JROF,JLEV))&
     & + (PDIFTS(JROF,JLEV-1) - PDIFTS(JROF,JLEV))&
     & + (PDIFCS(JROF,JLEV-1) - PDIFCS(JROF,JLEV))&
     & + (ZFHP  (JROF,JLEV-1) - ZFHP  (JROF,JLEV)))  

  ENDDO
ENDDO

!*
!------------------------------------------------------------------------
!     V -  CAS "delta m=1"
!     ------------------------------------------------------------------
IF ( NDPSFI == 1 ) THEN
                                                                                
  ! * Traitement de l'humidite specifique:
  DO JLEV = 1, KFLEV
    DO JROF = KSTART, KPROF
      PTENDQ(JROF,JLEV) = PTENDQ(JROF,JLEV) -&
       & ZGSDP(JROF,JLEV) * PQ(JROF,JLEV) *&
       & (ZFP   (JROF,JLEV-1) - ZFP   (JROF,JLEV) )
      ZGSDP(JROF,JLEV)= - 0.5_JPRB * ZGSDP(JROF,JLEV)
    ENDDO
  ENDDO
                                                                                
  ! * Terme supplementaire au sommet lie a "dq/d prehyd"
  !   q_top est suppose etre egal a q(l=1)
  DO JROF = KSTART, KPROF
    ZFPDP = ZGSDP(JROF,1) * ZFP(JROF,1)
    PTENDQ(JROF,1)=PTENDQ(JROF,1)-ZFPDP*(PQ(JROF,2)-PQ(JROF,1))
  ENDDO
 
  ! * Terme supplementaire sur les couches 2 a kflev-1 lie a "dq/d prehyd"
  DO JLEV = 2, KFLEV-1
    DO JROF = KSTART, KPROF
      PTENDQ(JROF,JLEV) = PTENDQ(JROF,JLEV) - ZGSDP(JROF,JLEV) * (&
       & ZFP(JROF,JLEV-1) * (PQ(JROF,JLEV)-PQ(JROF,JLEV-1)) +&
       & ZFP(JROF,JLEV)   * (PQ(JROF,JLEV+1)-PQ(JROF,JLEV)) )
    ENDDO
  ENDDO
 
  ! * Terme supplementaire a la surface lie a "dq/d prehyd"
  DO JROF = KSTART, KPROF
    PTENDQ(JROF,KFLEV) = PTENDQ(JROF,KFLEV) - ZGSDP(JROF,KFLEV) * (&
     & ZFP(JROF,KFLEV-1) * (PQ(JROF,KFLEV)-PQ(JROF,KFLEV-1)) +&
     & 2.0_JPRB * ZFP(JROF,KFLEV) * (PQS(JROF)-PQ(JROF,KFLEV)) )
  ENDDO

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPTENDSM',1,ZHOOK_HANDLE)
END SUBROUTINE CPTENDSM
