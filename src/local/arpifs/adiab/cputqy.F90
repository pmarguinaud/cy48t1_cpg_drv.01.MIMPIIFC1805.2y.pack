SUBROUTINE CPUTQY(YDDIMV,YDGMV, YGFL,YDPTRSLB1,YDPHY,KPROMA, KST, KPROF, KFLEV, PDT, KPGFL, &
 !  Pointeurs
 & KSLB1T9,KSLB1U9,KSLB1V9,KSLB1D9, KSLB1GFL9,&
 !  Variables 2D Input
 & PTENDH, PTENDT, PTENDU, PTENDV, PTENDU_ZDEC, PTENDV_ZDEC, PTENDD, PTENDGFL,&
 & PCP, PDELP, PTT, PUT, PVT,&
 !  Variables 2D In/Out
 & PB1, PGMVT1, PGFLT1,&
 !  Variables 2D Out
 & PFDIS)

!     -----------------------------------------------------------------
!**** *CPUTQY*  EVOLUTION DE T, VENT , Q , QI , QL, QR, QS
!               ET  TKE.
!     -----------------------------------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDQI ( IDEM ) : TENDANCE DE L'EAU GLACE
!       PTENDQL ( IDEM ) : TENDANCE DE L'EAU LIQUIDE
!       PTENDQR ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE LIQUIDE
!       PTENDQS ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE SOLIDE
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PTENDU_ZDEC, PTENDV_ZDEC (KPROMA,KFLEV) : TENDANCE DU VENT POUR CALCULER ZDEC
!       PTENDTKE : TENDANCE DE L'ENERGIE KINETIQUE TURB.
!       PTENDT   : TENDANCE DE LA TEMPERATURE SI ON N'UTILISE PAS
!                  LES FLUXES, MAIS LES TENDANCES (HIRLAM OPTION)
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PQIT1,PQLT1 (IDEM) : EAU SOL. ET LIQ. A FAIRE EVOLUER
!       PQST1,PQRT1 (IDEM) : EAU SOL. ET LIQ. PRECIPITANTE A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC
!       PTKET1               : ENERGIE KINETIQUE TURB. A FAIRE EVOLUER

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      07/10/04 Modification par F. Bouyssel (Lopez Ql/Qi/Qr prog. scheme)
!      25/01/06 Modification par F. Bouyssel (PTENDQS, PQST1)
!      01/06/06 Modification par B. Sass (PTENDTKE,PTKET1,PTENDT) 
!      30/10/06 Modification par F. Bouyssel (Bug correction found by M.Bellus)
!      02/10/06 Modification par M. Bellus (PTENDPTKE,LPTKE,L3MT,LSTRAPRO);
!               + corrected bug in ZTDCP comp. (missing snow, rain parts)
!      01/02/07 Modification par M.Janousek (ALARO pTKE is not *Dt, adding
!               pTKE tendency to T and enthalpy diss.flux)
!      26/04/10 Modification par Y.Bouteloup (Only one call to cputqy in mf_phys)
!      2013-11, D. Degrauwe: Generality in terms of GFL; added tendency 
!                            of D (NH).
!-----------------------------------------------------------------------

USE YOMDIMV  , ONLY : TDIMV
USE YOMGMV   , ONLY : TGMV
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LSLAG, LTWOTL, LNHDYN
USE YOMCST   , ONLY : RG, RCPD
USE YOMPHY   , ONLY : TPHY
USE YOM_YGFL , ONLY : TYPE_GFLD
USE PTRSLB1  , ONLY : TPTRSLB1
 
!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TDIMV)       ,INTENT(IN)    :: YDDIMV
TYPE(TGMV)        ,INTENT(IN)    :: YDGMV
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPTRSLB1)    ,INTENT(IN)    :: YDPTRSLB1
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPGFL(YGFL%NUMFLDS)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1T9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1U9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1V9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1D9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1GFL9
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU_ZDEC(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV_ZDEC(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDT(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDD(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDGFL(KPROMA,KFLEV,YGFL%NUMFLDS)

REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVT(KPROMA,KFLEV) 

REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIS(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PB1(KPROMA,YDPTRSLB1%NFLDSLB1) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PGMVT1(KPROMA,KFLEV,YDGMV%YT1%NDIM) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(KPROMA,KFLEV,YGFL%NDIM1)
!-----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF, IPGFL, JGFL

!  Local pointers LSLAG choice
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZTT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZUT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZVT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZDT1

!  Local pointers LTWOTL choice

REAL(KIND=JPRB) :: ZCPF, ZFLAG, ZRDT, ZRRG, ZH
REAL(KIND=JPRB) :: ZDTT1(KPROMA,KFLEV),ZTDCP(KPROMA,KFLEV),ZDEC(KPROMA,KFLEV),ZRRGDELP(KPROMA,KFLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

LOGICAL :: LLPREC, LLTKE

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPUTQY',0,ZHOOK_HANDLE)
ASSOCIATE(NDIM1=>YGFL%NDIM1, NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, &
 & YI=>YGFL%YI, YL=>YGFL%YL, YQ=>YGFL%YQ, YR=>YGFL%YR, YS=>YGFL%YS, &
 & YG=>YGFL%YG,YTKE=>YGFL%YTKE, &
 & NFLSA=>YDDIMV%NFLSA, &
 & YT1=>YDGMV%YT1, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1, LCONDWT=>YDPHY%LCONDWT, &
 & L3MT=>YDPHY%L3MT, LECT=>YDPHY%LECT, &
 & LSTRAPRO=>YDPHY%LSTRAPRO, LPTKE=>YDPHY%LPTKE, LPROCLD=>YDPHY%LPROCLD,&
 & LGRAPRO=>YDPHY%LGRAPRO)
!-----------------------------------------------------------------------

!  ------------------------------------------------------------
!   1. CALCUL DE L'EVOLUTION DU VENT , DE Q ET DE LA TEMPERATURE
!  ------------------------------------------------------------

LLPREC=LPROCLD.OR.L3MT.OR.LSTRAPRO
LLTKE=LECT



IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF
ZRRG   = 1.0_JPRB/RG
DO JROF=KST,KPROF
  PFDIS(JROF,0)=0.0_JPRB
ENDDO

! -------------------------------------------------------------------
! Define local pointers LSLAG choice
!  GMV variables

ZDT1 => NULL ()

IF (LSLAG) THEN
  ZTT1 => PB1(:,KSLB1T9+1-NFLSA:)
  ZUT1 => PB1(:,KSLB1U9+1-NFLSA:)
  ZVT1 => PB1(:,KSLB1V9+1-NFLSA:)
  IF (LNHDYN) THEN
    ZDT1 => PB1(:,KSLB1D9+1-NFLSA:)
  ENDIF
ELSE
  ZTT1 => PGMVT1(:,:,YT1%MT)
  ZUT1 => PGMVT1(:,:,YT1%MU)
  ZVT1 => PGMVT1(:,:,YT1%MV)
  IF (LNHDYN) THEN
    ZDT1 => PGMVT1(:,:,YT1%MSVD)
  ENDIF
ENDIF  

! -------------------------------------------------------------------

!     Calculation of ZTDCP,ZDEC,ZRRGDELP,ZDTT1:
ZTDCP(KST:KPROF,:)=0.
DO JGFL=1,NDIM1
  IF (YCOMP(JGFL)%LWATER .AND. YCOMP(JGFL)%LT1) THEN
    ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YCOMP(JGFL)%RCP-RCPD)*PTENDGFL(KST:KPROF,:,YCOMP(JGFL)%MP1)
  ENDIF
ENDDO

! ZDEC,ZRRGDELP,ZDTT1:

DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    ZCPF = 1.0_JPRB / ( PCP(JROF,JLEV) + PDT * ZTDCP(JROF,JLEV) )
    ZH = PCP(JROF,JLEV) * PTT(JROF,JLEV)
    ZDEC(JROF,JLEV) = ( PUT(JROF,JLEV)*PTENDU_ZDEC(JROF,JLEV) &
     & + PVT(JROF,JLEV)*PTENDV_ZDEC(JROF,JLEV) + 0.5_JPRB * PDT * &
     & ( (PTENDU_ZDEC(JROF,JLEV))**2 + (PTENDV_ZDEC(JROF,JLEV))**2 ) )
    ZRRGDELP(JROF,JLEV)=ZFLAG*ZRRG*PDELP(JROF,JLEV)
    ZDTT1(JROF,JLEV) = ZFLAG * ( ZCPF * ( ZH - PDT*ZDEC(JROF,JLEV)&
     & + PDT * PTENDH(JROF,JLEV) ) - PTT(JROF,JLEV) )
    IF (LPTKE) THEN
      ZDTT1(JROF,JLEV) = ZDTT1(JROF,JLEV)&
       & - ZFLAG*ZCPF*PDT*PTENDGFL(JROF,JLEV,YTKE%MP1)
    ENDIF
  ENDDO
ENDDO

!      Calculation of PFDIS:
PFDIS(KST:KPROF,0)=0.0_JPRB
DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)&
     & + ZRRGDELP(JROF,JLEV)*ZDEC(JROF,JLEV)
  ENDDO
  IF (LPTKE) THEN
    DO JROF=KST,KPROF
      PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV)&
       & + ZRRGDELP(JROF,JLEV)*PTENDGFL(JROF,JLEV,YTKE%MP1)
    ENDDO
  ENDIF
ENDDO

!    Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:
DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    ZUT1(JROF,JLEV)=ZUT1(JROF,JLEV) + PDT*PTENDU(JROF,JLEV)
    ZVT1(JROF,JLEV)=ZVT1(JROF,JLEV) + PDT*PTENDV(JROF,JLEV)
    ZTT1(JROF,JLEV)=ZTT1(JROF,JLEV) + ZDTT1(JROF,JLEV)
  ENDDO
ENDDO

IF(LNHDYN) THEN
  DO JLEV=1,KFLEV
    DO JROF=KST,KPROF
      ZDT1(JROF,JLEV)=ZDT1(JROF,JLEV) + PDT*PTENDD(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!    Incrementation of PB1 or PGFLT1 for GFL variables:
IF (LSLAG) THEN
       ! Increment PGFLT1 for non-advected GFL, PB1 for advected GFL.
  DO JGFL=1,NUMFLDS
    IF (YCOMP(JGFL)%LT1) THEN
      IF (YCOMP(JGFL)%LADV) THEN
        IPGFL=KPGFL(YCOMP(JGFL)%MP1)
        DO JLEV=1,KFLEV
          DO JROF=KST,KPROF
            PB1(JROF,KSLB1GFL9+IPGFL+JLEV-NFLSA)=&
             & PB1(JROF,KSLB1GFL9+IPGFL+JLEV-NFLSA)&
             & + PDT*PTENDGFL(JROF,JLEV,YCOMP(JGFL)%MP1)
          ENDDO
        ENDDO
      ELSE
        DO JLEV=1,KFLEV
          DO JROF=KST,KPROF
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=&
             & PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)&
             & + PDT*PTENDGFL(JROF,JLEV,YCOMP(JGFL)%MP1)
          ENDDO
        ENDDO
      ENDIF
    ENDIF ! End of non-zero diabatic tendency for this GFL
  ENDDO
ELSE
  ! Increment PGFLT1.
  DO JGFL=1,NUMFLDS
    IF (YCOMP(JGFL)%LT1) THEN
      DO JLEV=1,KFLEV
        DO JROF=KST,KPROF
          PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=&
           & PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)&
           & + PDT*PTENDGFL(JROF,JLEV,YCOMP(JGFL)%MP1)
        ENDDO
      ENDDO
    ENDIF  ! End of non-zero diabatic tendency for this GFL
  ENDDO
ENDIF


!-----------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPUTQY',1,ZHOOK_HANDLE)
END SUBROUTINE CPUTQY
