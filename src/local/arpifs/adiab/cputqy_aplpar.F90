SUBROUTINE CPUTQY_APLPAR(YDCPG_DIM, YDMF_PHYS_NEXT_STATE, YDVARS,YDDIMV,YDGMV, &
 & YGFL,YDPTRSLB1,YDPHY,KPROMA, KST, KPROF, KFLEV, PDT, KPGFL, &
 !  Pointeurs
 & KSLB1GFL9,&
 !  Variables 2D Input
 & PTENDH, PTENDT, PTENDU, PTENDV, PTENDU_ZDEC, PTENDV_ZDEC, PTENDD, PTENDGFL,&
 & PTENDEFB1, PTENDEFB2, PTENDEFB3, PTENDG, PTENDICONV, PTENDI, PTENDLCONV, &
 & PTENDL, PTENDQ, PTENDRCONV, PTENDR, PTENDSCONV, PTENDS, PTENDTKE, &
 & PCP, PDELP, PTT, PUT, PVT,&
 !  Variables 2D In/Out
 & PB1, PGMVT1, PGFLT1,&
 !  Variables 2D Out
 & PFDIS)

!     -----------------------------------------------------------------
!**** *CPUTQY_APLPAR*  EVOLUTION DE T, VENT , Q , QI , QL, QR, QS
!               ET  TKE.
!     -----------------------------------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDQI ( IDEM ) : TENDANCE DE L'EAU GLACE
!       PTENDQL ( IDEM ) : TENDANCE DE L'EAU LIQUIDE
!       PTENDQR ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE LIQUIDE
!       PTENDQS ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE SOLIDE
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PTENDU_ZDEC, PTENDV_ZDEC (KPROMA,KFLEV) : TENDANCE DU VENT POUR CALCULER ZDEC
!       PTENDTKE : TENDANCE DE L'ENERGIE KINETIQUE TURB.
!       PTENDT   : TENDANCE DE LA TEMPERATURE SI ON N'UTILISE PAS
!                  LES FLUXES, MAIS LES TENDANCES (HIRLAM OPTION)
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PQIT1,PQLT1 (IDEM) : EAU SOL. ET LIQ. A FAIRE EVOLUER
!       PQST1,PQRT1 (IDEM) : EAU SOL. ET LIQ. PRECIPITANTE A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC
!       PTKET1               : ENERGIE KINETIQUE TURB. A FAIRE EVOLUER

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      07/10/04 Modification par F. Bouyssel (Lopez Ql/Qi/Qr prog. scheme)
!      25/01/06 Modification par F. Bouyssel (PTENDQS, PQST1)
!      01/06/06 Modification par B. Sass (PTENDTKE,PTKET1,PTENDT) 
!      30/10/06 Modification par F. Bouyssel (Bug correction found by M.Bellus)
!      02/10/06 Modification par M. Bellus (PTENDPTKE,LPTKE,L3MT,LSTRAPRO);
!               + corrected bug in ZTDCP comp. (missing snow, rain parts)
!      01/02/07 Modification par M.Janousek (ALARO pTKE is not *Dt, adding
!               pTKE tendency to T and enthalpy diss.flux)
!      26/04/10 Modification par Y.Bouteloup (Only one call to cputqy in mf_phys)
!      2013-11, D. Degrauwe: Generality in terms of GFL; added tendency 
!                            of D (NH).
!-----------------------------------------------------------------------

USE YOMDIMV  , ONLY : TDIMV
USE YOMGMV   , ONLY : TGMV
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LSLAG, LTWOTL, LNHDYN
USE YOMCST   , ONLY : RG, RCPD
USE YOMPHY   , ONLY : TPHY
USE YOM_YGFL , ONLY : TYPE_GFLD
USE PTRSLB1  , ONLY : TPTRSLB1
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_NEXT_STATE_TYPE
USE CPG_DIM_TYPE_MOD, ONLY : CPG_DIM_TYPE
 
!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(CPG_DIM_TYPE),INTENT(IN)    :: YDCPG_DIM
TYPE (MF_PHYS_NEXT_STATE_TYPE), INTENT(INOUT) :: YDMF_PHYS_NEXT_STATE
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(TDIMV)       ,INTENT(IN)    :: YDDIMV
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPTRSLB1)    ,INTENT(IN)    :: YDPTRSLB1
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPGFL(YGFL%NUMFLDS)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1GFL9
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU_ZDEC(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV_ZDEC(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDT(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDD(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDGFL(KPROMA,KFLEV,YGFL%NUMFLDS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB1(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB2(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB3(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDG(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDICONV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDI(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDLCONV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDL(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDQ(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDRCONV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDR(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDSCONV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDS(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDTKE(KPROMA,KFLEV)

REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVT(KPROMA,KFLEV) 

REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIS(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PB1(KPROMA,YDPTRSLB1%NFLDSLB1) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PGMVT1(KPROMA,KFLEV,YDGMV%YT1%NDIM) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(KPROMA,KFLEV,YGFL%NDIM1)

!-----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF, IPGFL, JGFL

!  Local pointers LTWOTL choice

REAL(KIND=JPRB) :: ZCPF, ZFLAG, ZRDT, ZRRG, ZH
REAL(KIND=JPRB) :: ZDTT1(KPROMA,KFLEV),ZTDCP(KPROMA,KFLEV),ZDEC(KPROMA,KFLEV),ZRRGDELP(KPROMA,KFLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

LOGICAL :: LLPREC, LLTKE

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR',0,ZHOOK_HANDLE)
ASSOCIATE(NDIM1=>YGFL%NDIM1, NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, &
 & YI=>YGFL%YI, YL=>YGFL%YL, YQ=>YGFL%YQ, YR=>YGFL%YR, YS=>YGFL%YS, &
 & YG=>YGFL%YG,YTKE=>YGFL%YTKE, &
 & NFLSA=>YDDIMV%NFLSA, &
 & YT1=>YDGMV%YT1, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1, LCONDWT=>YDPHY%LCONDWT, &
 & L3MT=>YDPHY%L3MT, LECT=>YDPHY%LECT, &
 & LSTRAPRO=>YDPHY%LSTRAPRO, LPTKE=>YDPHY%LPTKE, LPROCLD=>YDPHY%LPROCLD,&
 & LGRAPRO=>YDPHY%LGRAPRO)
!-----------------------------------------------------------------------

!  ------------------------------------------------------------
!   1. CALCUL DE L'EVOLUTION DU VENT , DE Q ET DE LA TEMPERATURE
!  ------------------------------------------------------------

LLPREC=LPROCLD.OR.L3MT.OR.LSTRAPRO
LLTKE=LECT



IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF
ZRRG   = 1.0_JPRB/RG
DO JROF=KST,KPROF
  PFDIS(JROF,0)=0.0_JPRB
ENDDO

! -------------------------------------------------------------------

!     Calculation of ZTDCP,ZDEC,ZRRGDELP,ZDTT1:
ZTDCP(KST:KPROF,:)=0.

IF (YDVARS%L%LT1)     ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%L%RCP-RCPD)    *PTENDL(KST:KPROF,:)
IF (YDVARS%I%LT1)     ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%I%RCP-RCPD)    *PTENDI(KST:KPROF,:)
IF (YDVARS%S%LT1)     ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%S%RCP-RCPD)    *PTENDS(KST:KPROF,:)
IF (YDVARS%R%LT1)     ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%R%RCP-RCPD)    *PTENDR(KST:KPROF,:)
IF (YDVARS%LCONV%LT1) ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%LCONV%RCP-RCPD)*PTENDLCONV(KST:KPROF,:)
IF (YDVARS%ICONV%LT1) ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%ICONV%RCP-RCPD)*PTENDICONV(KST:KPROF,:)
IF (YDVARS%RCONV%LT1) ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%RCONV%RCP-RCPD)*PTENDRCONV(KST:KPROF,:)
IF (YDVARS%SCONV%LT1) ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%SCONV%RCP-RCPD)*PTENDSCONV(KST:KPROF,:)
IF (YDVARS%Q%LT1)     ZTDCP(KST:KPROF,:)=ZTDCP(KST:KPROF,:)+(YDVARS%Q%RCP-RCPD)    *PTENDQ(KST:KPROF,:)

! GMV                                      
! PTENDD      : YD                         PTENDD
! PTENDT      : YT                         PTENDT
! PTENDU      : YU                         PTENDU
! PTENDV      : YV                         PTENDV
                    
! GFL (sorted)
! PTENDQL     : YL                         PTENDL
! PTENDQI     : YI                         PTENDI
! PTENDQS     : YS                         PTENDS
! PTENDQR     : YR                         PTENDR
!               YG                         PTENDG
! PTENDTKE    : YTKE                       PTENDTKE
!               YEFB1                      PTENDEFB1
!               YEFB2                      PTENDEFB2
!               YEFB3                      PTENDEFB3
! PTENDQLCONV : YLCONV                     PTENDLCONV
! PTENDQICONV : YICONV                     PTENDICONV
! PTENDQRCONV : YRCONV                     PTENDRCONV
! PTENDQSCONV : YSCONV                     PTENDSCONV
! PTENDQ      : YQ                         PTENDQ

!sugfl2.F90:     YL : LIQUID_WATER               1
!sugfl2.F90:     YI : SOLID_WATER                2
!sugfl2.F90:     YS : SNOW                       3
!sugfl2.F90:     YR : RAIN                       4
!sugfl2.F90:     YTKE : TKE                        5
!sugfl2.F90:     YLCONV : QL_CONV                    8
!sugfl2.F90:     YICONV : QI_CONV                    9
!sugfl2.F90:     YRCONV : QR_CONV                   10
!sugfl2.F90:     YSCONV : QS_CONV                   11
!sugfl2.F90:     YQ : HUMI.SPECIFI              18

! LACE                                  LWATER LT1
! CPUTQY : LIQUID_WATER               1 T T
! CPUTQY : SOLID_WATER                2 T T
! CPUTQY : SNOW                       3 T T
! CPUTQY : RAIN                       4 T T
! CPUTQY : TKE                        5 F T
! CPUTQY : CLOUD_FRACTI               6 F T
! CPUTQY : CV_PREC_FLUX               7 F T
! CPUTQY : ST_PREC_FLUX               8 F T
! CPUTQY : QL_CONV                    9 T T
! CPUTQY : QI_CONV                   10 T T
! CPUTQY : QR_CONV                   11 T T
! CPUTQY : QS_CONV                   12 T T
! CPUTQY : RAD_LIQUID_WATER          13 F T
! CPUTQY : RAD_SOLID_WATER           14 F T
! CPUTQY : UD_OMEGA                  15 F T
! CPUTQY : UD_MESH_FRAC              16 F T
! CPUTQY : DD_OMEGA                  17 F T
! CPUTQY : DD_MESH_FRAC              18 F T
! CPUTQY : PSHI_CONV_CLOUD           19 F T
! CPUTQY : HUMI.SPECIFI              20 T T



DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    ZCPF = 1.0_JPRB / ( PCP(JROF,JLEV) + PDT * ZTDCP(JROF,JLEV) )
    ZH = PCP(JROF,JLEV) * PTT(JROF,JLEV)
    ZDEC(JROF,JLEV) = ( PUT(JROF,JLEV)*PTENDU_ZDEC(JROF,JLEV) &
     & + PVT(JROF,JLEV)*PTENDV_ZDEC(JROF,JLEV) + 0.5_JPRB * PDT * &
     & ( (PTENDU_ZDEC(JROF,JLEV))**2 + (PTENDV_ZDEC(JROF,JLEV))**2 ) )
    ZRRGDELP(JROF,JLEV)=ZFLAG*ZRRG*PDELP(JROF,JLEV)
    ZDTT1(JROF,JLEV) = ZFLAG * ( ZCPF * ( ZH - PDT*ZDEC(JROF,JLEV)&
     & + PDT * PTENDH(JROF,JLEV) ) - PTT(JROF,JLEV) )
    IF (LPTKE) THEN
      ZDTT1(JROF,JLEV) = ZDTT1(JROF,JLEV)&
       & - ZFLAG*ZCPF*PDT*PTENDTKE(JROF,JLEV)
    ENDIF
  ENDDO
ENDDO

!      Calculation of PFDIS:
PFDIS(KST:KPROF,0)=0.0_JPRB
DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)&
     & + ZRRGDELP(JROF,JLEV)*ZDEC(JROF,JLEV)
  ENDDO
  IF (LPTKE) THEN
    DO JROF=KST,KPROF
      PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV)&
       & + ZRRGDELP(JROF,JLEV)*PTENDTKE(JROF,JLEV)
    ENDDO
  ENDIF
ENDDO

!    Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:
DO JLEV=1,KFLEV
  DO JROF=KST,KPROF
    YDMF_PHYS_NEXT_STATE%U (JROF, JLEV) = &
  & YDMF_PHYS_NEXT_STATE%U (JROF, JLEV) + PDT*PTENDU(JROF,JLEV)
    YDMF_PHYS_NEXT_STATE%V (JROF, JLEV) = &
  & YDMF_PHYS_NEXT_STATE%V (JROF, JLEV) + PDT*PTENDV(JROF,JLEV)
    YDMF_PHYS_NEXT_STATE%T (JROF, JLEV) = &
  & YDMF_PHYS_NEXT_STATE%T (JROF, JLEV) + ZDTT1(JROF,JLEV)
  ENDDO
ENDDO

IF(LNHDYN) THEN
  DO JLEV=1,KFLEV
    DO JROF=KST,KPROF
      YDMF_PHYS_NEXT_STATE%SVD (JROF, JLEV) = &
    & YDMF_PHYS_NEXT_STATE%SVD (JROF, JLEV) + PDT*PTENDD(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF


! Incrementation of PB1 or PGFLT1 for GFL variables:
! Increment PGFLT1 for non-advected GFL, PB1 for advected GFL.

DO JGFL=1,NUMFLDS
  IF (YCOMP(JGFL)%LT1) THEN
    IF (LSLAG .AND. YCOMP(JGFL)%LADV) THEN
      IPGFL=KPGFL(YCOMP(JGFL)%MP1)
      DO JLEV=1,KFLEV
        DO JROF=KST,KPROF
          PB1(JROF,KSLB1GFL9+IPGFL+JLEV-NFLSA)=&
           & PB1(JROF,KSLB1GFL9+IPGFL+JLEV-NFLSA)&
           & + PDT*PTENDGFL(JROF,JLEV,YCOMP(JGFL)%MP1)
        ENDDO
      ENDDO
    ELSE
      DO JLEV=1,KFLEV
        DO JROF=KST,KPROF
          PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=&
           & PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)&
           & + PDT*PTENDGFL(JROF,JLEV,YCOMP(JGFL)%MP1)
        ENDDO
      ENDDO
    ENDIF
  ENDIF ! End of non-zero diabatic tendency for this GFL
ENDDO

!-----------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR',1,ZHOOK_HANDLE)
END SUBROUTINE CPUTQY_APLPAR
