SUBROUTINE CPUTQY_APLPAR_EXPL(YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS_NEXT_STATE, YDMF_PHYS_BASE_STATE,     &
& YDVARS, YDPHY, PDT, PTENDH, PTENDU, PTENDV, PTENDU_ZDEC, PTENDV_ZDEC, PTENDD, PTENDEFB1, PTENDEFB2, &
& PTENDEFB3, PTENDG, PTENDICONV, PTENDI, PTENDLCONV, PTENDL, PTENDQ, PTENDRCONV, PTENDR, PTENDSCONV,  &
& PTENDS, PTENDTKE, PFDIS)

!     -----------------------------------------------------------------
!**** *CPUTQY_APLPAR_EXPL*  EVOLUTION DE T, VENT , Q , QI , QL, QR, QS
!               ET  TKE.
!     -----------------------------------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDQI ( IDEM ) : TENDANCE DE L'EAU GLACE
!       PTENDQL ( IDEM ) : TENDANCE DE L'EAU LIQUIDE
!       PTENDQR ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE LIQUIDE
!       PTENDQS ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE SOLIDE
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PTENDU_ZDEC, PTENDV_ZDEC (KPROMA,KFLEV) : TENDANCE DU VENT POUR CALCULER ZDEC
!       PTENDTKE : TENDANCE DE L'ENERGIE KINETIQUE TURB.
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PQIT1,PQLT1 (IDEM) : EAU SOL. ET LIQ. A FAIRE EVOLUER
!       PQST1,PQRT1 (IDEM) : EAU SOL. ET LIQ. PRECIPITANTE A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC
!       PTKET1               : ENERGIE KINETIQUE TURB. A FAIRE EVOLUER

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      07/10/04 Modification par F. Bouyssel (Lopez Ql/Qi/Qr prog. scheme)
!      25/01/06 Modification par F. Bouyssel (PTENDQS, PQST1)
!      01/06/06 Modification par B. Sass (PTENDTKE,PTKET1,PTENDT) 
!      30/10/06 Modification par F. Bouyssel (Bug correction found by M.Bellus)
!      02/10/06 Modification par M. Bellus (PTENDPTKE,LPTKE,L3MT,LSTRAPRO);
!               + corrected bug in ZTDCP comp. (missing snow, rain parts)
!      01/02/07 Modification par M.Janousek (ALARO pTKE is not *Dt, adding
!               pTKE tendency to T and enthalpy diss.flux)
!      26/04/10 Modification par Y.Bouteloup (Only one call to cputqy in mf_phys)
!      2013-11, D. Degrauwe: Generality in terms of GFL; added tendency 
!                            of D (NH).
!-----------------------------------------------------------------------

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LNHDYN
USE YOMCST   , ONLY : RG, RCPD
USE YOMPHY   , ONLY : TPHY
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_NEXT_STATE_TYPE
USE CPG_OPTS_TYPE_MOD, ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
 
!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(CPG_BNDS_TYPE),INTENT(IN)   :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),INTENT(IN)   :: YDCPG_OPTS
TYPE (MF_PHYS_NEXT_STATE_TYPE), INTENT(INOUT) :: YDMF_PHYS_NEXT_STATE
TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDU_ZDEC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDV_ZDEC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB1(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB2(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDEFB3(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDG(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDICONV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDLCONV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDRCONV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDSCONV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDTKE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIS(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 

#include "cputqy0.intfb.h"

!-----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF, IPGFL, JGFL

REAL(KIND=JPRB) :: ZCPF, ZFLAG, ZRDT, ZRRG, ZH
REAL(KIND=JPRB) :: ZDTT1(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG),ZTDCP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG),ZDEC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG),ZRRGDELP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

LOGICAL :: LLPREC, LLTKE

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR_EXPL', 0, ZHOOK_HANDLE)

ASSOCIATE(L3MT=>YDPHY%L3MT, LECT=>YDPHY%LECT, &
 & LSTRAPRO=>YDPHY%LSTRAPRO, LPTKE=>YDPHY%LPTKE, LPROCLD=>YDPHY%LPROCLD)

!-----------------------------------------------------------------------

!  ------------------------------------------------------------
!   1. CALCUL DE L'EVOLUTION DU VENT , DE Q ET DE LA TEMPERATURE
!  ------------------------------------------------------------

LLPREC=LPROCLD.OR.L3MT.OR.LSTRAPRO
LLTKE=LECT

IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF
ZRRG   = 1.0_JPRB/RG
DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
  PFDIS(JROF,0)=0.0_JPRB
ENDDO

! -------------------------------------------------------------------

!     Calculation of ZTDCP,ZDEC,ZRRGDELP,ZDTT1:
ZTDCP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,:)=0.

CALL ACCTND (YDVARS%L%LT1, YDVARS%L%RCP, ZTDCP, PTENDL      )
CALL ACCTND (YDVARS%I%LT1, YDVARS%I%RCP, ZTDCP, PTENDI      )
CALL ACCTND (YDVARS%S%LT1, YDVARS%S%RCP, ZTDCP, PTENDS      )
CALL ACCTND (YDVARS%R%LT1, YDVARS%R%RCP, ZTDCP, PTENDR      )
CALL ACCTND (YDVARS%LCONV%LT1, YDVARS%LCONV%RCP, ZTDCP, PTENDLCONV  )
CALL ACCTND (YDVARS%ICONV%LT1, YDVARS%ICONV%RCP, ZTDCP, PTENDICONV  )
CALL ACCTND (YDVARS%RCONV%LT1, YDVARS%RCONV%RCP, ZTDCP, PTENDRCONV  )
CALL ACCTND (YDVARS%SCONV%LT1, YDVARS%SCONV%RCP, ZTDCP, PTENDSCONV  )
CALL ACCTND (YDVARS%Q%LT1, YDVARS%Q%RCP, ZTDCP, PTENDQ      )

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZCPF = 1.0_JPRB / ( YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) + PDT * ZTDCP(JROF,JLEV) )
    ZH = YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) * YDMF_PHYS_BASE_STATE%T(JROF,JLEV)
    ZDEC(JROF,JLEV) = ( YDMF_PHYS_BASE_STATE%U(JROF,JLEV)*PTENDU_ZDEC(JROF,JLEV) &
     & + YDMF_PHYS_BASE_STATE%V(JROF,JLEV)*PTENDV_ZDEC(JROF,JLEV) + 0.5_JPRB * PDT * &
     & ( (PTENDU_ZDEC(JROF,JLEV))**2 + (PTENDV_ZDEC(JROF,JLEV))**2 ) )
    ZRRGDELP(JROF,JLEV)=ZFLAG*ZRRG*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JROF,JLEV)
    ZDTT1(JROF,JLEV) = ZFLAG * ( ZCPF * ( ZH - PDT*ZDEC(JROF,JLEV)&
     & + PDT * PTENDH(JROF,JLEV) ) - YDMF_PHYS_BASE_STATE%T(JROF,JLEV) )
    IF (LPTKE) THEN
      ZDTT1(JROF,JLEV) = ZDTT1(JROF,JLEV)&
       & - ZFLAG*ZCPF*PDT*PTENDTKE(JROF,JLEV)
    ENDIF
  ENDDO
ENDDO

!      Calculation of PFDIS:
PFDIS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,0)=0.0_JPRB
DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)&
     & + ZRRGDELP(JROF,JLEV)*ZDEC(JROF,JLEV)
  ENDDO
  IF (LPTKE) THEN
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV)&
       & + ZRRGDELP(JROF,JLEV)*PTENDTKE(JROF,JLEV)
    ENDDO
  ENDIF
ENDDO

!    Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:

CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%U, PDT, YDMF_PHYS_NEXT_STATE%U, PTENDU)
CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%V, PDT, YDMF_PHYS_NEXT_STATE%V, PTENDV)
CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%T, 1._JPRB, YDMF_PHYS_NEXT_STATE%T, &
& ZDTT1)

IF (LNHDYN) THEN
  CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%SVD, PDT, YDMF_PHYS_NEXT_STATE%SVD, &
  & PTENDD)
ENDIF

IF (YDVARS%L%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%L, PDT, YDMF_PHYS_NEXT_STATE%L, &
                      & PTENDL)
IF (YDVARS%I%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%I, PDT, YDMF_PHYS_NEXT_STATE%I, &
                      & PTENDI)
IF (YDVARS%S%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%S, PDT, YDMF_PHYS_NEXT_STATE%S, &
                      & PTENDS)
IF (YDVARS%R%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%R, PDT, YDMF_PHYS_NEXT_STATE%R, &
                      & PTENDR)
IF (YDVARS%G%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%G, PDT, YDMF_PHYS_NEXT_STATE%G, &
                      & PTENDG)
IF (YDVARS%TKE%LT1)   CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%TKE, PDT, YDMF_PHYS_NEXT_STATE%TKE, &
                      & PTENDTKE)
IF (YDVARS%EFB1%LT1)  CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB1, PDT, YDMF_PHYS_NEXT_STATE%EFB1, &
                      & PTENDEFB1)
IF (YDVARS%EFB2%LT1)  CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB2, PDT, YDMF_PHYS_NEXT_STATE%EFB2, &
                      & PTENDEFB2)
IF (YDVARS%EFB3%LT1)  CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB3, PDT, YDMF_PHYS_NEXT_STATE%EFB3, &
                      & PTENDEFB3)
IF (YDVARS%LCONV%LT1) CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%LCONV, PDT, YDMF_PHYS_NEXT_STATE%LCONV, &
                      & PTENDLCONV)
IF (YDVARS%ICONV%LT1) CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%ICONV, PDT, YDMF_PHYS_NEXT_STATE%ICONV, &
                      & PTENDICONV)
IF (YDVARS%RCONV%LT1) CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%RCONV, PDT, YDMF_PHYS_NEXT_STATE%RCONV, &
                      & PTENDRCONV)
IF (YDVARS%SCONV%LT1) CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%SCONV, PDT, YDMF_PHYS_NEXT_STATE%SCONV, &
                      & PTENDSCONV)
IF (YDVARS%Q%LT1)     CALL CPUTQY0 (YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%Q, PDT, YDMF_PHYS_NEXT_STATE%Q, &
                      & PTENDQ)

!-----------------------------------------------------------------------

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR_EXPL', 1, ZHOOK_HANDLE)

CONTAINS

SUBROUTINE ACCTND (LDT1, PRCP, PACC, PTND)

LOGICAL,          INTENT (IN)    :: LDT1
REAL (KIND=JPRB), INTENT (IN)    :: PRCP
REAL (KIND=JPRB), INTENT (INOUT) :: PACC (YDCPG_OPTS%KLON,1:YDCPG_OPTS%KFLEVG)
REAL (KIND=JPRB), INTENT (IN)    :: PTND (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

IF (LDT1) THEN
  DO JLEV = 1, YDCPG_OPTS%KFLEVG
    DO JROF = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      PACC (JROF,JLEV) = PACC (JROF,JLEV) + (PRCP-RCPD) * PTND (JROF, JLEV)
    ENDDO
  ENDDO
ENDIF

END SUBROUTINE

END SUBROUTINE CPUTQY_APLPAR_EXPL
