SUBROUTINE CPUTQY_APLPAR_EXPL(YDCST, YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS_NEXT_STATE, YDMF_PHYS_BASE_STATE, &
& YDVARS, YDPHY, PDT, PTENDH, PTENDU, PTENDV, PTENDU_ZDEC, PTENDV_ZDEC, PTENDD, PTENDEFB1, PTENDEFB2,    &
& PTENDEFB3, PTENDG, PTENDICONV, PTENDI, PTENDLCONV, PTENDL, PTENDQ, PTENDRCONV, PTENDR, PTENDSCONV,     &
& PTENDS, PTENDTKE, PFDIS)

!     -----------------------------------------------------------------
!**** *CPUTQY_APLPAR_EXPL*  EVOLUTION DE T, VENT , Q , QI , QL, QR, QS
!               ET  TKE.
!     -----------------------------------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDQI ( IDEM ) : TENDANCE DE L'EAU GLACE
!       PTENDQL ( IDEM ) : TENDANCE DE L'EAU LIQUIDE
!       PTENDQR ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE LIQUIDE
!       PTENDQS ( IDEM ) : TENDANCE DE L'EAU PRECIPITANTE SOLIDE
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PTENDU_ZDEC, PTENDV_ZDEC (KPROMA,KFLEV) : TENDANCE DU VENT POUR CALCULER ZDEC
!       PTENDTKE : TENDANCE DE L'ENERGIE KINETIQUE TURB.
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PQIT1,PQLT1 (IDEM) : EAU SOL. ET LIQ. A FAIRE EVOLUER
!       PQST1,PQRT1 (IDEM) : EAU SOL. ET LIQ. PRECIPITANTE A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC
!       PTKET1               : ENERGIE KINETIQUE TURB. A FAIRE EVOLUER

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      07/10/04 Modification par F. Bouyssel (Lopez Ql/Qi/Qr prog. scheme)
!      25/01/06 Modification par F. Bouyssel (PTENDQS, PQST1)
!      01/06/06 Modification par B. Sass (PTENDTKE,PTKET1,PTENDT) 
!      30/10/06 Modification par F. Bouyssel (Bug correction found by M.Bellus)
!      02/10/06 Modification par M. Bellus (PTENDPTKE,LPTKE,L3MT,LSTRAPRO);
!               + corrected bug in ZTDCP comp. (missing snow, rain parts)
!      01/02/07 Modification par M.Janousek (ALARO pTKE is not *Dt, adding
!               pTKE tendency to T and enthalpy diss.flux)
!      26/04/10 Modification par Y.Bouteloup (Only one call to cputqy in mf_phys)
!      2013-11, D. Degrauwe: Generality in terms of GFL; added tendency 
!                            of D (NH).
!-----------------------------------------------------------------------

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCST   , ONLY : TCST
USE YOMPHY   , ONLY : TPHY
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_NEXT_STATE_TYPE
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
 
IMPLICIT NONE

TYPE(TCST),                    INTENT(IN)    :: YDCST
TYPE(CPG_BNDS_TYPE),           INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),           INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_NEXT_STATE_TYPE), INTENT(INOUT) :: YDMF_PHYS_NEXT_STATE
TYPE(MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(FIELD_VARIABLES),         INTENT(INOUT) :: YDVARS
TYPE(TPHY),                    INTENT(IN)    :: YDPHY
REAL(KIND=JPRB),               INTENT(IN)    :: PDT 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDH      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDU      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDV      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDU_ZDEC (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDV_ZDEC (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDD      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDEFB1   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDEFB2   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDEFB3   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDG      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDICONV  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDI      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDLCONV  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDL      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDQ      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDRCONV  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDR      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDSCONV  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDS      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(IN)    :: PTENDTKE    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),               INTENT(OUT)   :: PFDIS       (YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 

#include "cputqy0.intfb.h"
#include "acctnd0.intfb.h"

INTEGER(KIND=JPIM) :: JLEV, JROF, IPGFL, JGFL

REAL(KIND=JPRB) :: ZCPF, ZFLAG, ZRDT, ZRRG, ZH
REAL(KIND=JPRB) :: ZDTT1   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTDCP   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDEC    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZRRGDELP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

LOGICAL :: LLPREC, LLTKE

IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR_EXPL', 0, ZHOOK_HANDLE)

ASSOCIATE (L3MT=>YDPHY%L3MT, LECT=>YDPHY%LECT, LSTRAPRO=>YDPHY%LSTRAPRO, LPTKE=>YDPHY%LPTKE, LPROCLD=>YDPHY%LPROCLD)

LLPREC=LPROCLD.OR.L3MT.OR.LSTRAPRO
LLTKE=LECT

IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF

ZRRG  = 1.0_JPRB/YDCST%RG
ZTDCP = 0._JPRB

CALL ACCTND0 (YDCST, YDVARS%L%LT1,     YDCPG_OPTS, YDCPG_BNDS, YDVARS%L%RCP,     ZTDCP, PTENDL      )
CALL ACCTND0 (YDCST, YDVARS%I%LT1,     YDCPG_OPTS, YDCPG_BNDS, YDVARS%I%RCP,     ZTDCP, PTENDI      )
CALL ACCTND0 (YDCST, YDVARS%S%LT1,     YDCPG_OPTS, YDCPG_BNDS, YDVARS%S%RCP,     ZTDCP, PTENDS      )
CALL ACCTND0 (YDCST, YDVARS%R%LT1,     YDCPG_OPTS, YDCPG_BNDS, YDVARS%R%RCP,     ZTDCP, PTENDR      )
CALL ACCTND0 (YDCST, YDVARS%LCONV%LT1, YDCPG_OPTS, YDCPG_BNDS, YDVARS%LCONV%RCP, ZTDCP, PTENDLCONV  )
CALL ACCTND0 (YDCST, YDVARS%ICONV%LT1, YDCPG_OPTS, YDCPG_BNDS, YDVARS%ICONV%RCP, ZTDCP, PTENDICONV  )
CALL ACCTND0 (YDCST, YDVARS%RCONV%LT1, YDCPG_OPTS, YDCPG_BNDS, YDVARS%RCONV%RCP, ZTDCP, PTENDRCONV  )
CALL ACCTND0 (YDCST, YDVARS%SCONV%LT1, YDCPG_OPTS, YDCPG_BNDS, YDVARS%SCONV%RCP, ZTDCP, PTENDSCONV  )
CALL ACCTND0 (YDCST, YDVARS%Q%LT1,     YDCPG_OPTS, YDCPG_BNDS, YDVARS%Q%RCP,     ZTDCP, PTENDQ      )

!=PARALLEL

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZCPF = 1.0_JPRB / ( YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) + PDT * ZTDCP(JROF,JLEV) )
    ZH = YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) * YDMF_PHYS_BASE_STATE%T(JROF,JLEV)
    ZDEC(JROF,JLEV) = ( YDMF_PHYS_BASE_STATE%U(JROF,JLEV)*PTENDU_ZDEC(JROF,JLEV) &
     & + YDMF_PHYS_BASE_STATE%V(JROF,JLEV)*PTENDV_ZDEC(JROF,JLEV) + 0.5_JPRB * PDT * &
     & ( (PTENDU_ZDEC(JROF,JLEV))**2 + (PTENDV_ZDEC(JROF,JLEV))**2 ) )
    ZRRGDELP(JROF,JLEV)=ZFLAG*ZRRG*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JROF,JLEV)
    ZDTT1(JROF,JLEV) = ZFLAG * ( ZCPF * ( ZH - PDT*ZDEC(JROF,JLEV)&
     & + PDT * PTENDH(JROF,JLEV) ) - YDMF_PHYS_BASE_STATE%T(JROF,JLEV) )
    IF (LPTKE) THEN
      ZDTT1(JROF,JLEV) = ZDTT1(JROF,JLEV)&
       & - ZFLAG*ZCPF*PDT*PTENDTKE(JROF,JLEV)
    ENDIF
  ENDDO
ENDDO

!=END PARALLEL

! Calculation of PFDIS:

!=PARALLEL

PFDIS(:,0)=0.0_JPRB

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)&
     & + ZRRGDELP(JROF,JLEV)*ZDEC(JROF,JLEV)
  ENDDO
  IF (LPTKE) THEN
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV)&
       & + ZRRGDELP(JROF,JLEV)*PTENDTKE(JROF,JLEV)
    ENDDO
  ENDIF
ENDDO

!=END PARALLEL

! Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:

CALL CPUTQY0 (.TRUE.,            YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%U,     PDT,     YDMF_PHYS_NEXT_STATE%U,     PTENDU)
CALL CPUTQY0 (.TRUE.,            YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%V,     PDT,     YDMF_PHYS_NEXT_STATE%V,     PTENDV)
CALL CPUTQY0 (.TRUE.,            YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%T,     1._JPRB, YDMF_PHYS_NEXT_STATE%T,     ZDTT1)
CALL CPUTQY0 (YDCPG_OPTS%LNHDYN, YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%SVD,   PDT,     YDMF_PHYS_NEXT_STATE%SVD,   PTENDD)
CALL CPUTQY0 (YDVARS%L%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%L,     PDT,     YDMF_PHYS_NEXT_STATE%L,     PTENDL)
CALL CPUTQY0 (YDVARS%I%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%I,     PDT,     YDMF_PHYS_NEXT_STATE%I,     PTENDI)
CALL CPUTQY0 (YDVARS%S%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%S,     PDT,     YDMF_PHYS_NEXT_STATE%S,     PTENDS)
CALL CPUTQY0 (YDVARS%R%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%R,     PDT,     YDMF_PHYS_NEXT_STATE%R,     PTENDR)
CALL CPUTQY0 (YDVARS%G%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%G,     PDT,     YDMF_PHYS_NEXT_STATE%G,     PTENDG)
CALL CPUTQY0 (YDVARS%TKE%LT1,    YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%TKE,   PDT,     YDMF_PHYS_NEXT_STATE%TKE,   PTENDTKE)
CALL CPUTQY0 (YDVARS%EFB1%LT1,   YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB1,  PDT,     YDMF_PHYS_NEXT_STATE%EFB1,  PTENDEFB1)
CALL CPUTQY0 (YDVARS%EFB2%LT1,   YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB2,  PDT,     YDMF_PHYS_NEXT_STATE%EFB2,  PTENDEFB2)
CALL CPUTQY0 (YDVARS%EFB3%LT1,   YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%EFB3,  PDT,     YDMF_PHYS_NEXT_STATE%EFB3,  PTENDEFB3)
CALL CPUTQY0 (YDVARS%LCONV%LT1,  YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%LCONV, PDT,     YDMF_PHYS_NEXT_STATE%LCONV, PTENDLCONV)
CALL CPUTQY0 (YDVARS%ICONV%LT1,  YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%ICONV, PDT,     YDMF_PHYS_NEXT_STATE%ICONV, PTENDICONV)
CALL CPUTQY0 (YDVARS%RCONV%LT1,  YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%RCONV, PDT,     YDMF_PHYS_NEXT_STATE%RCONV, PTENDRCONV)
CALL CPUTQY0 (YDVARS%SCONV%LT1,  YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%SCONV, PDT,     YDMF_PHYS_NEXT_STATE%SCONV, PTENDSCONV)
CALL CPUTQY0 (YDVARS%Q%LT1,      YDCPG_BNDS, YDCPG_OPTS, YDCPG_OPTS%YRVARS_DIMS%Q,     PDT,     YDMF_PHYS_NEXT_STATE%Q,     PTENDQ)


END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('CPUTQY_APLPAR_EXPL', 1, ZHOOK_HANDLE)

END SUBROUTINE CPUTQY_APLPAR_EXPL
