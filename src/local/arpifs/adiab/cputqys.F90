SUBROUTINE CPUTQYS(YDDIMV,YDGMV, YGFL,YDPTRSLB1,&
 & YDSTOPH, YDPHY2, &
 & KPROMA, KSTART, KPROF, KFLEV, PDT, KPGFL,&
 !  Pointeurs
 & KSLB1U9,KSLB1V9,KSLB1T9,KSLB1GFL9,&
 !  Variables 2D Input
 & PTENDH, PTENDQ, PTENDU, PTENDV,&
 & PFORCEU, PFORCEV, PFORCET, PFORCEQ,&
 & PCP, PDELP, PTT, PUT, PVT,&
 !  Variables 2D In/Out
 & PB1, PGMVT1, PGFLT1,&
 !  Variables 2D Out
 & PFDIS)

!     ------------------------------------
!**** *CPUTQYS*  EVOLUTION DE T, VENT , Q 
!     ------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      26/04/10 Y.Bouteloup (Only one call to cputqys in mf_phys)
!  ----------------------------------------------------------------------

USE YOMDIMV  , ONLY : TDIMV
USE YOMGMV   , ONLY : TGMV
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LSLAG, LTWOTL
USE YOMCT3   , ONLY : NSTEP
USE YOMCST   , ONLY : RG, RCPD, RCPV, RHOUR
USE YOM_YGFL , ONLY : TYPE_GFLD
USE PTRSLB1  , ONLY : TPTRSLB1
USE YOMPHY2  , ONLY : TPHY2
USE STOPH_MIX, ONLY : TSTOPH
!  ----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TDIMV)       ,INTENT(IN)    :: YDDIMV
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TPTRSLB1)    ,INTENT(INOUT) :: YDPTRSLB1
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
TYPE(TSTOPH)      ,INTENT(IN)    :: YDSTOPH
TYPE(TPHY2)       ,INTENT(IN)    :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTART 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPGFL(YGFL%NUMFLDS)
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1U9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1V9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1T9
INTEGER(KIND=JPIM),INTENT(IN)    :: KSLB1GFL9
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDQ(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDU(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEU(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCET(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEQ(KPROMA,KFLEV)

REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUT(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVT(KPROMA,KFLEV) 

REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIS(KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PB1(KPROMA,YDPTRSLB1%NFLDSLB1) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PGMVT1(KPROMA,KFLEV,YDGMV%YT1%NDIM) 
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT) :: PGFLT1(KPROMA,KFLEV,YGFL%NDIM1)
!  ----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF, IPGFL

!  Local pointers LSLAG choice
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZTT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZUT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZVT1
REAL(KIND=JPRB),DIMENSION(:,:),    POINTER :: ZQT1
!  Local pointers LTWOTL choice

REAL(KIND=JPRB) :: ZCPF, ZDEC, ZFLAG, ZH, ZRDT, ZRRG, ZTDCP
REAL(KIND=JPRB) :: ZDTT1(KPROMA,KFLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!  ----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPUTQYS',0,ZHOOK_HANDLE)
ASSOCIATE(NDIM1=>YGFL%NDIM1, NUMFLDS=>YGFL%NUMFLDS, YQ=>YGFL%YQ, &
 & NFLSA=>YDDIMV%NFLSA, &
 & YT1=>YDGMV%YT1, &
 & TSPHY=>YDPHY2%TSPHY, &
 & LFORCENL=>YDSTOPH%LFORCENL, &
 & NFORCESTART=>YDSTOPH%NFORCESTART, &
 & NFORCEEND=>YDSTOPH%NFORCEEND, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1)
!  ----------------------------------------------------------------------


! -------------------------------------------------------------------
! Define local pointers LSLAG choice
!  GMV variables
IF (LSLAG) THEN
  ZTT1 => PB1(:,KSLB1T9+1-NFLSA:)
  ZUT1 => PB1(:,KSLB1U9+1-NFLSA:)
  ZVT1 => PB1(:,KSLB1V9+1-NFLSA:)
ELSE
  ZTT1 => PGMVT1(:,:,YT1%MT)
  ZUT1 => PGMVT1(:,:,YT1%MU)
  ZVT1 => PGMVT1(:,:,YT1%MV)
ENDIF  
! GFL variables (only Q presently)
IF (LSLAG) THEN
       !  PGFLT1 for non-advected GFL, PB1 for advected GFL.
  IPGFL=KPGFL(YQ%MP1)     
  IF (YQ%LADV) THEN
    ZQT1 => PB1(:,KSLB1GFL9+IPGFL+1-NFLSA:)
  ELSE 
    ZQT1 => PGFLT1(:,:,YQ%MP1) 
  ENDIF
ELSE
  ZQT1 => PGFLT1(:,:,YQ%MP1) 
ENDIF        
! -------------------------------------------------------------------

!  ------------------------------------------------------------
!   1. CALCUL DE L'EVOLUTION DU VENT , DE Q ET DE LA TEMPERATURE
!  ------------------------------------------------------------

IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF
ZRRG   = 1.0_JPRB/RG
DO JROF=KSTART,KPROF
  PFDIS(JROF,0)=0.0_JPRB
ENDDO

! ZDEC,ZRRGDELP,ZDTT1:

DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF
  ZTDCP = (RCPV - RCPD) * PTENDQ(JROF,JLEV)
  ZCPF  = 1.0_JPRB / ( PCP(JROF,JLEV) + PDT * ZTDCP )
  ZDEC = PDT * ( PUT(JROF,JLEV)*PTENDU(JROF,JLEV) +&
   & PVT(JROF,JLEV)*PTENDV(JROF,JLEV) + 0.5_JPRB * PDT * (&
   & (PTENDU(JROF,JLEV))**2 + (PTENDV(JROF,JLEV))**2 ))  
  ZH  = PCP(JROF,JLEV) * PTT(JROF,JLEV)

  ZDTT1(JROF,JLEV) = ( ZCPF * ( ZH - ZDEC&
   & + PDT * PTENDH(JROF,JLEV) ) - PTT(JROF,JLEV) ) * ZFLAG  
  PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)+ ZRDT*ZRRG*PDELP(JROF,JLEV)*ZDEC
  
  ENDDO
ENDDO  

IF (LFORCENL.AND.(NSTEP*(TSPHY/RHOUR)>=NFORCESTART).AND.&
               & (NSTEP*(TSPHY/RHOUR)<=NFORCEEND)) THEN
  DO JLEV = 1, KFLEV
    DO JROF = KSTART, KPROF
      PTENDU(JROF,JLEV)=PTENDU(JROF,JLEV)+&
    &  PFORCEU(JROF,JLEV)
      PTENDV(JROF,JLEV)=PTENDV(JROF,JLEV)+&
    &  PFORCEV(JROF,JLEV)
      ZDTT1(JROF,JLEV)=ZDTT1(JROF,JLEV)+&
    &  PDT*PFORCET(JROF,JLEV)
      PTENDQ(JROF,JLEV)=PTENDQ(JROF,JLEV)+&
    &  PFORCEQ(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!    Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:

DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF
    ZUT1(JROF,JLEV)=ZUT1(JROF,JLEV) + PDT*PTENDU(JROF,JLEV)
    ZVT1(JROF,JLEV)=ZVT1(JROF,JLEV) + PDT*PTENDV(JROF,JLEV)
    ZTT1(JROF,JLEV)=ZTT1(JROF,JLEV) + ZDTT1(JROF,JLEV)
    ZQT1(JROF,JLEV)=ZQT1(JROF,JLEV) + PDT*PTENDQ(JROF,JLEV)
  ENDDO
ENDDO

!  ----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPUTQYS',1,ZHOOK_HANDLE)
END SUBROUTINE CPUTQYS
