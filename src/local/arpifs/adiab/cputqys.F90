SUBROUTINE CPUTQYS(YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, &
 & YDSTOPH, YDPHY2, &
 & KPROMA, KSTART, KPROF, KFLEV, PDT, &
 !  Variables 2D Input
 & PTENDH, PTENDQ, PTENDU, PTENDV,&
 & PFORCEU, PFORCEV, PFORCET, PFORCEQ,&
 !  Variables 2D Out
 & PFDIS)

!     ------------------------------------
!**** *CPUTQYS*  EVOLUTION DE T, VENT , Q 
!     ------------------------------------

!     ARGUMENTS D'ENTREE
!     ------------------
!       KPROMA : DIMENSION HORIZONTALE
!       KSTART : DEBUT DE LA BOUCLE HORIZONTALE
!       KPROF : BORNE HORIZONTALE DES CALCULS
!       KFLEV : DIMENSION ET BORNE VERTICALE
!       PDT : PAS DE TEMPS EFFECTIF

!       PTENDH (KPROMA,KFLEV) : TENDANCE DE L'ENTHALPIE
!       PTENDQ ( IDEM ) : TENDANCE DE L'EAU VAPEUR
!       PTENDU, PTENDV (KPROMA,KFLEV) : TENDANCE DU VENT
!       PCP9 ( IDEM ) : CHALEUR SPECIFIQUE A T=9
!       PDELP9 ( IDEM ) : EPAISSEUR DE LA COUCHE A T=9
!       PTT9, PUT9, PVT9 (IDEM) : ETAT THERMODYNAMIQUE A T=9

!     ARGUMENTS IMPLICITES
!     --------------------
!       CONSTANTS UNIVERSELLES  = COMMON /YOMCST/:  RG,RCPD,RCPV,RCS,RCW

!     SORTIES
!     -------
!       PTT1, PQT1 (KPROMA,KFLEV): VARIABLES A FAIRE EVOLUER
!       PUT1, PVT1 (IDEM)    : VENT A FAIRE EVOLUER
!       PFDIS(KPROMA,0:KFLEV): FLUX ENTHALPIE DU A LA DISSIPATION EC

! --- AUTHOR.
!     -------
!      E.Bazile .

! --- MODIFICATIONS.
!     --------------
!      26/04/10 Y.Bouteloup (Only one call to cputqys in mf_phys)
!  ----------------------------------------------------------------------

USE YOMDIMV  , ONLY : TDIMV
USE YOMGMV   , ONLY : TGMV
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LSLAG, LTWOTL
USE YOMCT3   , ONLY : NSTEP
USE YOMCST   , ONLY : RG, RCPD, RCPV, RHOUR
USE YOM_YGFL , ONLY : TYPE_GFLD
USE PTRSLB1  , ONLY : TPTRSLB1
USE YOMPHY2  , ONLY : TPHY2
USE STOPH_MIX, ONLY : TSTOPH

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_NEXT_STATE_TYPE
!  ----------------------------------------------------------------------

IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT (IN)     :: YDMF_PHYS_BASE_STATE
TYPE (MF_PHYS_NEXT_STATE_TYPE), INTENT (INOUT)  :: YDMF_PHYS_NEXT_STATE
TYPE(TSTOPH)      ,INTENT(IN)    :: YDSTOPH
TYPE(TPHY2)       ,INTENT(IN)    :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTART 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH(KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDQ(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDU(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEU(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEV(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCET(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFORCEQ(KPROMA,KFLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIS(KPROMA,0:KFLEV) 
!  ----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JLEV, JROF, JPGFL, JGFL

REAL(KIND=JPRB) :: ZCPF, ZDEC, ZFLAG, ZH, ZRDT, ZRRG, ZTDCP
REAL(KIND=JPRB) :: ZDTT1(KPROMA,KFLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!  ----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CPUTQYS',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & TSPHY=>YDPHY2%TSPHY, &
 & LFORCENL=>YDSTOPH%LFORCENL, &
 & NFORCESTART=>YDSTOPH%NFORCESTART, &
 & NFORCEEND=>YDSTOPH%NFORCEEND)
!  ----------------------------------------------------------------------



!  ------------------------------------------------------------
!   1. CALCUL DE L'EVOLUTION DU VENT , DE Q ET DE LA TEMPERATURE
!  ------------------------------------------------------------

IF (PDT <= 0.0_JPRB) THEN
  ZFLAG=0.0_JPRB
  ZRDT= 0.0_JPRB
ELSE
  ZFLAG=1.0_JPRB
  ZRDT= 1.0_JPRB/PDT
ENDIF
ZRRG   = 1.0_JPRB/RG
DO JROF=KSTART,KPROF
  PFDIS(JROF,0)=0.0_JPRB
ENDDO

! ZDEC,ZRRGDELP,ZDTT1:

DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF
  ZTDCP = (RCPV - RCPD) * PTENDQ(JROF,JLEV)
  ZCPF  = 1.0_JPRB / ( YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) + PDT * ZTDCP )
  ZDEC = PDT * ( YDMF_PHYS_BASE_STATE%U(JROF,JLEV)*PTENDU(JROF,JLEV) +&
   & YDMF_PHYS_BASE_STATE%V(JROF,JLEV)*PTENDV(JROF,JLEV) + 0.5_JPRB * PDT * (&
   & (PTENDU(JROF,JLEV))**2 + (PTENDV(JROF,JLEV))**2 ))  
  ZH  = YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JROF,JLEV) * YDMF_PHYS_BASE_STATE%T(JROF,JLEV)

  ZDTT1(JROF,JLEV) = ( ZCPF * ( ZH - ZDEC&
   & + PDT * PTENDH(JROF,JLEV) ) - YDMF_PHYS_BASE_STATE%T(JROF,JLEV) ) * ZFLAG  
  PFDIS(JROF,JLEV)= PFDIS(JROF,JLEV-1)+ ZRDT*ZRRG*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JROF,JLEV)*ZDEC
  
  ENDDO
ENDDO  

IF (LFORCENL.AND.(NSTEP*(TSPHY/RHOUR)>=NFORCESTART).AND.&
               & (NSTEP*(TSPHY/RHOUR)<=NFORCEEND)) THEN
  DO JLEV = 1, KFLEV
    DO JROF = KSTART, KPROF
      PTENDU(JROF,JLEV)=PTENDU(JROF,JLEV)+&
    &  PFORCEU(JROF,JLEV)
      PTENDV(JROF,JLEV)=PTENDV(JROF,JLEV)+&
    &  PFORCEV(JROF,JLEV)
      ZDTT1(JROF,JLEV)=ZDTT1(JROF,JLEV)+&
    &  PDT*PFORCET(JROF,JLEV)
      PTENDQ(JROF,JLEV)=PTENDQ(JROF,JLEV)+&
    &  PFORCEQ(JROF,JLEV)
    ENDDO
  ENDDO
ENDIF

!    Incrementation of PB1 (SL) or P[X]T1 (EUL) for GMV variables:

DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF
    YDMF_PHYS_NEXT_STATE%U(JROF,JLEV)=YDMF_PHYS_NEXT_STATE%U(JROF,JLEV) + PDT*PTENDU(JROF,JLEV)
    YDMF_PHYS_NEXT_STATE%V(JROF,JLEV)=YDMF_PHYS_NEXT_STATE%V(JROF,JLEV) + PDT*PTENDV(JROF,JLEV)
    YDMF_PHYS_NEXT_STATE%T(JROF,JLEV)=YDMF_PHYS_NEXT_STATE%T(JROF,JLEV) + ZDTT1(JROF,JLEV)
    YDMF_PHYS_NEXT_STATE%Q(JROF,JLEV)=YDMF_PHYS_NEXT_STATE%Q(JROF,JLEV) + PDT*PTENDQ(JROF,JLEV)
  ENDDO
ENDDO

!  ----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CPUTQYS',1,ZHOOK_HANDLE)
END SUBROUTINE CPUTQYS
