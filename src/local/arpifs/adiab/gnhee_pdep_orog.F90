!OCL  NOEVAL
SUBROUTINE GNHEE_PDEP_OROG(&
 ! --- INPUT -----------------------------------------------------------------
 & LDVERTFE,LDVFE_GW,YDGEOMETRY,KSTART,KPROF,PATND13,PREF,&
 ! --- OUTPUT ----------------------------------------------------------------
 & PART_RESCALED_PDEPF)

!**** *GNHEE_PDEP_OROG* - Computation of the equivalent (p-Pi)/Pi generated by surface orography in NHEE model.

!     Purpose.
!     --------

!     In the NHEE model, surface orography generates an equivalent partial anhydrostatic contribution in pressure.
!     Surface orography acts via a term [S V grad(Phi_s)].
!     S is a vertical dependent coefficient.
!     [S grad(Phi_s)] is a 'as good as possible' approximation of grad(Phi).
!     [(1/g**2) D(S V grad(Phi_s))/Dt] generates [(p-Pi)/Pi]: rescaled partial pressure departure generated by orography.
!     Top value of this version of [(p-Pi)/Pi] is assumed to be 0.

!**   Interface.
!     ----------
!        *CALL* *GNHEE_PDEP_OROG(...)

!        Explicit arguments :
!        --------------------
!         * INPUT:
!           YDGEOMETRY          : structure containing geometry.
!           KSTART              : start of work.
!           KPROF               : working length.
!           PATND13             : [(1/g**2) D(S V grad(Phi_s))/Dt] at full levels.
!           PREF                : "prehyd" at full levels.

!         * OUTPUT:
!           PART_RESCALED_PDEPF : rescaled partial pressure departure generated by orography, at full levels.

!        Implicit arguments :   None.
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.    None.
!     ----------

!     Reference.
!     ----------
!        See documentations Arpege about the NH model.

!     Author.
!     -------
!        K. YESSAD.
!        Original : August 2018

!     Modifications.
!     --------------
!       F. VOITUS (Feb 2020) Simplification
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK

!     ------------------------------------------------------------------

IMPLICIT NONE

LOGICAL            ,INTENT(IN)   :: LDVERTFE
LOGICAL            ,INTENT(IN)   :: LDVFE_GW
TYPE(GEOMETRY)     ,INTENT(IN)   :: YDGEOMETRY
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KSTART 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KPROF 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PATND13(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)   :: PREF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PART_RESCALED_PDEPF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JROF,JLEV

REAL(KIND=JPRB) :: ZHOOK_HANDLE

! -----------------------------------------------------------------------------

#include "abor1.intfb.h"

! -----------------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('GNHEE_PDEP_OROG',0,ZHOOK_HANDLE)
ASSOCIATE(NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)

!     ------------------------------------------------------------------
!     ------------------------------------------------------------------

! * Compute the equivalent '(p-Pi)/Pi' generated by term 'S V grad(Phi_s)',
!   first at half levels, then at full levels, assuming that top value is 0.
IF (LDVERTFE.AND.LDVFE_GW) THEN
  ! Only LVFE_GW=F is available there
  CALL ABOR1(' GNHEE_PDEP_OROG 1: option not yet coded!')
ELSE
  ! This piece of code is valid for LVFE_GW=F only
  PART_RESCALED_PDEPF(KSTART:KPROF,1)=0.0_JPRB
  DO JLEV=2,NFLEVG
    DO JROF=KSTART,KPROF
      PART_RESCALED_PDEPF(JROF,JLEV)=(PREF(JROF,JLEV-1)/PREF(JROF,JLEV))*PART_RESCALED_PDEPF(JROF,JLEV-1) &
       & + (1.0_JPRB-(PREF(JROF,JLEV-1)/PREF(JROF,JLEV)))*PATND13(JROF,JLEV-1)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GNHEE_PDEP_OROG',1,ZHOOK_HANDLE)
END SUBROUTINE GNHEE_PDEP_OROG
