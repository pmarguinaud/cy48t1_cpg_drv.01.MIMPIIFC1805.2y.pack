SUBROUTINE GPHPREAD(KPROMA,KFLEV,KSTART,KPROF,YDVAB,PRESH,PRESH5,LDSI,PXYB,PXYB5,PRESF)

!**** *GPHPREAD* - Computes half and full level pressure (AD code)
!                  Modern version of former GPPREHAD+GPXYBAD+GPPREFAD

!     Purpose.
!     --------
!           Computes pressures at half and full model levels.

!**   Interface.
!     ----------
!        *CALL* *GPHPREAD(...)

!        Explicit arguments :
!        --------------------

!          KPROMA    : horizontal dimensioning                                (in)
!          KFLEV     : vertical dimensioning                                  (in)
!          KSTART    : start of work                                          (in)
!          KPROF     : depth of work                                          (in)
!          YDVAB     : contains information about hybrid vertical coordinate  (in)
!          PRESH     : half level pressure                                    (inout)
!          PRESH5    : half level pressure (trajectory)                       (in)
!          LDSI      : if routine is called in the SI NH scheme               (opt in)
!          PXYB      : contains pressure depth, "delta", "alpha"              (opt out)
!          PXYB5     : contains pressure depth, "delta", "alpha" (trajectory) (opt in)
!          PRESF     : full level pressure                                    (opt out)

!        Implicit arguments :  NONE.
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.  None.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      K. YESSAD (Sep 2011) after GPPREHAD, GPXYBAD and GPPREFAD.

!     Modifications.
!     --------------
!   K. Yessad (Dec 2016): Prune obsolete options.
!   H Petithomme (Dec 2020): optimisation with pointer
!     ------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM, JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK
USE YOMDYNA   , ONLY : YRDYNA
USE YOMCVER   , ONLY : LVERTFE
USE YOMCST    , ONLY : RD, RCVD
USE YOMVERT   , ONLY : TVAB, TOPPRES
USE INTDYN_MOD, ONLY : YYTXYB

!     ------------------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JPIM)         ,INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM)         ,INTENT(IN)    :: KFLEV 
INTEGER(KIND=JPIM)         ,INTENT(IN)    :: KSTART 
INTEGER(KIND=JPIM)         ,INTENT(IN)    :: KPROF 
TYPE(TVAB)                 ,INTENT(IN)    :: YDVAB
REAL(KIND=JPRB)            ,INTENT(INOUT) :: PRESH(KPROMA,0:KFLEV)
REAL(KIND=JPRB)            ,INTENT(IN)    :: PRESH5(KPROMA,0:KFLEV)
LOGICAL,OPTIONAL           ,INTENT(IN)    :: LDSI
REAL(KIND=JPRB),OPTIONAL,TARGET,INTENT(INOUT) :: PXYB(KPROMA,KFLEV,YYTXYB%NDIM)
REAL(KIND=JPRB),OPTIONAL   ,INTENT(IN)    :: PXYB5(KPROMA,KFLEV,YYTXYB%NDIM)
REAL(KIND=JPRB),OPTIONAL   ,INTENT(INOUT) :: PRESF(KPROMA,KFLEV)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IFIRST, JLEV, JLON
REAL(KIND=JPRB) :: ZPRESF,ZPRESFD,ZPRESF5,ZPRESF5D,ZMUL
REAL(KIND=JPRB),CONTIGUOUS,POINTER :: ZXYB(:,:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('GPHPREAD',0,ZHOOK_HANDLE)
!     ------------------------------------------------------------------

!*       1.    PRELIMINARY CALCULATIONS
!              ------------------------

IF ((PRESENT(PXYB).AND.PRESENT(PXYB5)).OR.(PRESENT(PRESF).AND.PRESENT(PXYB5))) THEN

  ! This is introduced to get rid of the implicit
  ! assumption that the top level input for pressure is 0 hPa.
  ! The first block if is for economy (no do loop start up) and the second for safety.
  IF(PRESH5(KSTART,0) <= TOPPRES)THEN
    IFIRST=2
  ELSE
    IFIRST=1
    DO JLON=KSTART,KPROF
      IF(PRESH5(JLON,0) <= TOPPRES)THEN
        IFIRST=2
        EXIT
      ENDIF
    ENDDO
  ENDIF

ENDIF

IF (PRESENT(PXYB)) THEN
  ZXYB => PXYB(:,:,:)
ELSE
  ALLOCATE(ZXYB(KPROMA,KFLEV,YYTXYB%NDIM))
  ZXYB(:,:,:)=0.0_JPRB
ENDIF

!     ------------------------------------------------------------------

!*       4.    COMPUTES FULL LEVEL PRESSURES
!              -----------------------------

IF (PRESENT(PRESF).AND.PRESENT(PXYB5)) THEN

  IF (LVERTFE) THEN
    DO JLEV=1,KFLEV
      PRESH(KSTART:KPROF,KFLEV)=PRESH(KSTART:KPROF,KFLEV)&
       & +YDVAB%VBF(JLEV)*PRESF(KSTART:KPROF,JLEV)
      PRESF(KSTART:KPROF,JLEV)=0.0_JPRB
    ENDDO
  ELSE
    IF (YRDYNA%NDLNPR == 0) THEN
      IF (YRDYNA%LAPRXPK) THEN
        DO JLEV=KFLEV,1,-1
          DO JLON=KSTART,KPROF
            PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)+0.5_JPRB*PRESF(JLON,JLEV)
            PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+0.5_JPRB*PRESF(JLON,JLEV)
            PRESF(JLON,JLEV)=0.0_JPRB
          ENDDO
        ENDDO
      ELSE
        DO JLEV=KFLEV,1,-1
          DO JLON=KSTART,KPROF
            PRESH(JLON,JLEV)=PRESH(JLON,JLEV)&
             & +EXP(-PXYB5(JLON,JLEV,YYTXYB%M_ALPH))*PRESF(JLON,JLEV)
            ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=ZXYB(JLON,JLEV,YYTXYB%M_ALPH)&
             & -EXP(-PXYB5(JLON,JLEV,YYTXYB%M_ALPH))*PRESH5(JLON,JLEV)*PRESF(JLON,JLEV)
            PRESF(JLON,JLEV)=0.0_JPRB
          ENDDO
        ENDDO
      ENDIF
    ELSEIF (YRDYNA%NDLNPR == 1) THEN
      ZMUL=1.0_JPRB/(2.0_JPRB+RCVD/RD)
      DO JLEV=1,IFIRST-1
        DO JLON=KSTART,KPROF
          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZMUL*PRESF(JLON,JLEV)
          PRESF(JLON,JLEV)=0.0_JPRB
        ENDDO
      ENDDO
      DO JLEV=KFLEV,IFIRST,-1
        DO JLON=KSTART,KPROF
          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)&
           & +(1.0_JPRB-PXYB5(JLON,JLEV,YYTXYB%M_ALPH))*PRESF(JLON,JLEV)
          ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=ZXYB(JLON,JLEV,YYTXYB%M_ALPH)-PRESH5(JLON,JLEV)*PRESF(JLON,JLEV)
          PRESF(JLON,JLEV)=0.0_JPRB
        ENDDO
      ENDDO
    ENDIF ! NDLNPR
  ENDIF ! LVERTFE

ENDIF

!     ------------------------------------------------------------------

!*       3.    COMPUTES PXYB
!              -------------

IF ((PRESENT(PXYB).AND.PRESENT(PXYB5)).OR.(PRESENT(PRESF).AND.PRESENT(PXYB5))) THEN

  IF(LVERTFE) THEN

    DO JLEV=1,KFLEV
      !DIR$ IVDEP
      !CDIR NODEP
      DO JLON=KSTART,KPROF

        ! Trajectory:
        ZPRESF5=YDVAB%VAF(JLEV)+YDVAB%VBF(JLEV)*PRESH5(JLON,KFLEV)
        ZPRESF5D=1.0_JPRB/ZPRESF5

        ! Adjoint:
        ZPRESF=0.0_JPRB
        ZPRESFD=0.0_JPRB

        ZPRESFD=ZPRESFD+(PRESH5(JLON,JLEV)-ZPRESF5)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
        PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZPRESF5D*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
        ZPRESF=ZPRESF-ZPRESF5D*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
        ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=0.0_JPRB

        ZPRESFD=ZPRESFD+YDVAB%VBF(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
        ZXYB(JLON,JLEV,YYTXYB%M_RTGR)=0.0_JPRB

        ZPRESF=ZPRESF-PXYB5(JLON,JLEV,YYTXYB%M_DELP)*ZXYB(JLON,JLEV,YYTXYB%M_LNPR)*ZPRESF5D**2
        ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)+ZXYB(JLON,JLEV,YYTXYB%M_LNPR)*ZPRESF5D
        ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=0.0_JPRB

        ZPRESF=ZPRESF-(ZPRESF5D*ZPRESF5D)*ZPRESFD
    
        PRESH(JLON,KFLEV)=PRESH(JLON,KFLEV)+YDVAB%VBF(JLEV)*ZPRESF
        ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)&
         & -ZXYB(JLON,JLEV,YYTXYB%M_RDELP)/PXYB5(JLON,JLEV,YYTXYB%M_DELP)**2  
        ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=0.0_JPRB
    
        PRESH(JLON,KFLEV)=PRESH(JLON,KFLEV)+YDVAB%VDELB(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_DELP)
        ZXYB(JLON,JLEV,YYTXYB%M_DELP)=0.0_JPRB

      ENDDO
    ENDDO

  ELSE

    IF(YRDYNA%NDLNPR == 0)THEN

      DO JLEV=1,IFIRST-1
        !DIR$ IVDEP
        !CDIR NODEP
        DO JLON=KSTART,KPROF

          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+YDVAB%VDELB(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RTGR)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)-(PXYB5(JLON,JLEV,YYTXYB%M_RPRE)**2/TOPPRES)&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          ZXYB(JLON,JLEV,YYTXYB%M_RPP)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)-PXYB5(JLON,JLEV,YYTXYB%M_RPRE)&
           & *PXYB5(JLON,JLEV,YYTXYB%M_RPRE)*ZXYB(JLON,JLEV,YYTXYB%M_RPRE)
          ZXYB(JLON,JLEV,YYTXYB%M_RPRE)=0.0_JPRB

          PRESH(JLON,1)=PRESH(JLON,JLEV)+1.0_JPRB/PRESH5(JLON,JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_LNPR)
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)+(-1.0_JPRB/PXYB5(JLON,JLEV,YYTXYB%M_DELP)**2)&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RDELP)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=0.0_JPRB

        ENDDO
      ENDDO

      DO JLEV=KFLEV,IFIRST,-1
        !DIR$ IVDEP
        !CDIR NODEP
        DO JLON=KSTART,KPROF

          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=ZXYB(JLON,JLEV,YYTXYB%M_LNPR)+PXYB5(JLON,JLEV,YYTXYB%M_RDELP)**2&
           & *YDVAB%VC(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+(2.0_JPRB*PXYB5(JLON,JLEV,YYTXYB%M_RDELP)&
           & *PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*YDVAB%VC(JLEV)+YDVAB%VDELB(JLEV))&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RTGR)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)-PXYB5(JLON,JLEV,YYTXYB%M_RPP)&
           & *PXYB5(JLON,JLEV,YYTXYB%M_RPRE)*ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-PXYB5(JLON,JLEV,YYTXYB%M_RPP)&
           & *PXYB5(JLON,JLEV-1,YYTXYB%M_RPRE)*ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          ZXYB(JLON,JLEV,YYTXYB%M_RPP)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=ZXYB(JLON,JLEV,YYTXYB%M_LNPR)-PRESH5(JLON,JLEV-1)*&
           & PXYB5(JLON,JLEV,YYTXYB%M_RDELP)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)-PRESH5(JLON,JLEV-1)*&
           & PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-PXYB5(JLON,JLEV,YYTXYB%M_RDELP)*&
           & PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)-PXYB5(JLON,JLEV,YYTXYB%M_RPRE)**2&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RPRE)
          ZXYB(JLON,JLEV,YYTXYB%M_RPRE)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZXYB(JLON,JLEV,YYTXYB%M_LNPR)/PRESH5(JLON,JLEV)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-ZXYB(JLON,JLEV,YYTXYB%M_LNPR)/PRESH5(JLON,JLEV-1)
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)+(-1.0_JPRB/PXYB5(JLON,JLEV,YYTXYB%M_DELP)**2)&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RDELP)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=0.0_JPRB

        ENDDO
      ENDDO

    ELSEIF(YRDYNA%NDLNPR == 1)THEN

      DO JLEV=IFIRST-1,1,-1
        !DIR$ IVDEP
        !CDIR NODEP
        DO JLON=KPROF,KSTART,-1

          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+&
           & 2.0_JPRB*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)**2 *PXYB5(JLON,JLEV,YYTXYB%M_RDELP)&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=ZXYB(JLON,JLEV,YYTXYB%M_LNPR)+&
           & 2.0_JPRB*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*PXYB5(JLON,JLEV,YYTXYB%M_RDELP)**2 &
           & *ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          ZXYB(JLON,JLEV,YYTXYB%M_RPP)=0.0_JPRB

          PRESH(JLON,1)=PRESH(JLON,1)-1.0_JPRB/(PRESH5(JLON,1))**2 *ZXYB(JLON,JLEV,YYTXYB%M_RPRE)
          ZXYB(JLON,JLEV,YYTXYB%M_RPRE)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+YDVAB%VDELB(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RTGR)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=0.0_JPRB
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)-1.0_JPRB/(PXYB5(JLON,JLEV,YYTXYB%M_DELP))**2 &
           & *ZXYB(JLON,JLEV,YYTXYB%M_RDELP)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=0.0_JPRB

        ENDDO
      ENDDO

      DO JLEV=KFLEV,IFIRST,-1
        !DIR$ IVDEP
        !CDIR NODEP
        DO JLON=KPROF,KSTART,-1

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)-1.0_JPRB/(PRESH5(JLON,JLEV))**2 &
           & *ZXYB(JLON,JLEV,YYTXYB%M_RPRE)
          ZXYB(JLON,JLEV,YYTXYB%M_RPRE)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+&
           & (YDVAB%VDELB(JLEV)+YDVAB%VC(JLEV)*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*&
           & PXYB5(JLON,JLEV,YYTXYB%M_RDELP))*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=ZXYB(JLON,JLEV,YYTXYB%M_LNPR)+&
           & PXYB5(JLON,JLEV,YYTXYB%M_RDELP)**2 *YDVAB%VC(JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)+&
           & PXYB5(JLON,JLEV,YYTXYB%M_RDELP)*YDVAB%VC(JLEV)*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)&
           & *ZXYB(JLON,JLEV,YYTXYB%M_RTGR)
          ZXYB(JLON,JLEV,YYTXYB%M_RTGR)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=ZXYB(JLON,JLEV,YYTXYB%M_LNPR)&
           & -PRESH5(JLON,JLEV-1)*PXYB5(JLON,JLEV,YYTXYB%M_RDELP)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=ZXYB(JLON,JLEV,YYTXYB%M_RDELP)&
           & -PRESH5(JLON,JLEV-1)*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)&
           & -PXYB5(JLON,JLEV,YYTXYB%M_RDELP)*PXYB5(JLON,JLEV,YYTXYB%M_LNPR)*ZXYB(JLON,JLEV,YYTXYB%M_ALPH)
          ZXYB(JLON,JLEV,YYTXYB%M_ALPH)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)+&
           & SQRT(PXYB5(JLON,JLEV,YYTXYB%M_RPP))*ZXYB(JLON,JLEV,YYTXYB%M_LNPR)
          ZXYB(JLON,JLEV,YYTXYB%M_RPP)=ZXYB(JLON,JLEV,YYTXYB%M_RPP)+&
           & SQRT(PXYB5(JLON,JLEV,YYTXYB%M_RPP)) *0.5_JPRB*PXYB5(JLON,JLEV,YYTXYB%M_DELP)/&
           & PXYB5(JLON,JLEV,YYTXYB%M_RPP)*ZXYB(JLON,JLEV,YYTXYB%M_LNPR)
          ZXYB(JLON,JLEV,YYTXYB%M_LNPR)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)&
           & -1.0_JPRB/(PRESH5(JLON,JLEV)*PRESH5(JLON,JLEV-1))**2 *&
           & PRESH5(JLON,JLEV-1)*ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)&
           & -1.0_JPRB/(PRESH5(JLON,JLEV)*PRESH5(JLON,JLEV-1))**2 *&
           & PRESH5(JLON,JLEV)*ZXYB(JLON,JLEV,YYTXYB%M_RPP)
          ZXYB(JLON,JLEV,YYTXYB%M_RPP)=0.0_JPRB

          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=ZXYB(JLON,JLEV,YYTXYB%M_DELP)&
           & -1.0_JPRB/(PXYB5(JLON,JLEV,YYTXYB%M_DELP))**2 *ZXYB(JLON,JLEV,YYTXYB%M_RDELP)
          ZXYB(JLON,JLEV,YYTXYB%M_RDELP)=0.0_JPRB

          PRESH(JLON,JLEV)=PRESH(JLON,JLEV)+ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          PRESH(JLON,JLEV-1)=PRESH(JLON,JLEV-1)-ZXYB(JLON,JLEV,YYTXYB%M_DELP)
          ZXYB(JLON,JLEV,YYTXYB%M_DELP)=0.0_JPRB

        ENDDO
      ENDDO

    ENDIF ! NDLNPR

  ENDIF ! LVERTFE

ENDIF

IF (PRESENT(PXYB)) THEN
  PXYB(:,:,:)=0.0_JPRB
ELSE
  DEALLOCATE(ZXYB)
ENDIF

!     ------------------------------------------------------------------

!*       2.    COMPUTES HALF LEVEL PRESSURES
!              -----------------------------

! * compute upper-air PRESH from surface PRESH.
DO JLEV=0,KFLEV-1
  DO JLON=KSTART,KPROF
    PRESH(JLON,KFLEV)=PRESH(JLON,KFLEV)+YDVAB%VBH(JLEV)*PRESH(JLON,JLEV)
    PRESH(JLON,JLEV)=0.0_JPRB
  ENDDO
ENDDO 


!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('GPHPREAD',1,ZHOOK_HANDLE)
END SUBROUTINE GPHPREAD
