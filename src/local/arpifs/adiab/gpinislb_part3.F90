SUBROUTINE GPINISLB_PART3 (LDGWADV, LDNHDYN, LDVERTFE, LDVFE_GW, YDGEOMETRY, YDPTRSLB2, KST, KEN, PVVEL0, PZPRE0F, PB2, PGWFT0, PGDW0, PGWS0)

USE PARKIND1, ONLY : JPIM, JPRB

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PTRSLB2      , ONLY : TPTRSLB2


IMPLICIT NONE

LOGICAL, INTENT (IN) :: LDGWADV
LOGICAL, INTENT (IN) :: LDNHDYN
LOGICAL, INTENT (IN) :: LDVERTFE
LOGICAL, INTENT (IN) :: LDVFE_GW
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TPTRSLB2)    ,INTENT(IN)    :: YDPTRSLB2
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KEN
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVVEL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZPRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB),OPTIONAL,INTENT(IN) :: PGWFT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),OPTIONAL,INTENT(IN) :: PGDW0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB),OPTIONAL,INTENT(IN) :: PGWS0(YDGEOMETRY%YRDIM%NPROMA)

#include "abor1.intfb.h"

INTEGER (KIND=JPIM) :: JLEV


!*          2.     PB2[X] for other purposes.
!                  -------------------------- 

!*          2.1    PB2VVEL.

!cf this do loop is furiously non linear, but useless in tl model
  ! Non linear: TL code is in CPG_GP_TL.
DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
  PB2(KST:KEN,YDPTRSLB2%MSLB2VVEL+JLEV-1)=PVVEL0(KST:KEN,JLEV)*PZPRE0F(KST:KEN,JLEV)
ENDDO

!*          2.2    (gw), (g w_surf) and (g dw).

IF (LDNHDYN.AND.LDGWADV.AND.LDVERTFE.AND.LDVFE_GW) THEN
  ! Not yet coded in the TL code.
  ! Makes sence only for LTWOTL=T and LSLAG=T
  IF (.NOT.(PRESENT(PGWFT0).AND.PRESENT(PGDW0).AND.PRESENT(PGWS0)))&
   & CALL ABOR1(' GPINISLB 2a: missing optional input arguments!')
  DO JLEV=1,YDGEOMETRY%YRDIMV%NFLEVG
    PB2(KST:KEN,YDPTRSLB2%MSLB2GWF+JLEV-1)=PGWFT0(KST:KEN,JLEV)
    PB2(KST:KEN,YDPTRSLB2%MSLB2GDW+JLEV-1)=PGDW0(KST:KEN,JLEV)
  ENDDO
  PB2(KST:KEN,YDPTRSLB2%MSLB2GWS)=PGWS0(KST:KEN)
ENDIF

END SUBROUTINE
