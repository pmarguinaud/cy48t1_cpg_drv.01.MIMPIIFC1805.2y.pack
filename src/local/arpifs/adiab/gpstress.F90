SUBROUTINE GPSTRESS(YDGEOMETRY,YDGMV,YDDPHY,YDRIP,YDDYN,PGMV,PGMVS,POUT,PEXTRA)
! GPSTRESS 
!
! Externals
! ---------

! Method
! ------
!
!          ADD RESOLVED NON-LINEAR DIFFUSION VIA GAUSSIAN FILTER USED IN SIMILARITY MODEL 
!          AND DYNAMIC EVALUATION OF SIMILARITY "CONSTANT"
!          INCLUDE DE-ALIASING OF QUADRATIC TERMS

! Reference
! ---------

! Author
! ------
!   N. Wedi (2011)

! Modifications
! -------------
!   T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!   K. Yessad (July 2014): Move some variables.
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMGMV       , ONLY : TGMV
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMVERT      , ONLY : VP00
USE YOMCST       , ONLY : RA, RD, RG, RPI
USE YOMDYN       , ONLY : TDYN
USE YOMMP0       , ONLY : MYSETV ,NPRTRV
USE YOMDYNA      , ONLY : YRDYNA
USE YOMDPHY      , ONLY : TDPHY
USE YOMCVER      , ONLY : LVERTFE
USE INTDYN_MOD   , ONLY : YYTXYB0, YYTCTY0
USE YOMRIP       , ONLY : TRIP

! -----------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TDPHY)       ,INTENT(IN)    :: YDDPHY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
TYPE(TRIP)        ,INTENT(IN)    :: YDRIP
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMV(:,:,:,:)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(:,:,:)
REAL(KIND=JPRB)   ,OPTIONAL, INTENT(OUT) :: POUT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,OPTIONAL, INTENT(INOUT) :: PEXTRA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDDPHY%NVEXTRDYN,&
 & YDGEOMETRY%YRDIM%NGPBLKS)
INTEGER(KIND=JPIM), PARAMETER :: JFIELDS = 17


! -----------------------------------------------------------------------------

REAL(KIND=JPRB)            :: ZROTSP(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB)            :: ZDIVSP(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2)

REAL(KIND=JPRB)            :: ZGRAD(YDGEOMETRY%YRGEM%NGPTOT,2*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZGRADU(YDGEOMETRY%YRGEM%NGPTOT,2*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZGRADUW(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)            :: ZGRADV(YDGEOMETRY%YRGEM%NGPTOT,2*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZGRADVW(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)            :: ZGRADT(YDGEOMETRY%YRGEM%NGPTOT,2*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZGRADTW(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)            :: ZXYB0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB0%NDIM)
REAL(KIND=JPRB)            :: ZCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM) 

! stress tensor
REAL(KIND=JPRB)            :: ZSTRESS(YDGEOMETRY%YRGEM%NGPTOT,JFIELDS*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZSTRESS1(YDGEOMETRY%YRGEM%NGPTOT,JFIELDS*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZSTRESS2(YDGEOMETRY%YRGEM%NGPTOT,JFIELDS*YDGEOMETRY%YRDIMV%NFLEVG,1)
REAL(KIND=JPRB)            :: ZNLDIFF(YDGEOMETRY%YRGEM%NGPTOT,3*YDGEOMETRY%YRDIMV%NFLEVG,1)
INTEGER(KIND=JPIM)         :: INUMFLD(NPRTRV)
INTEGER(KIND=JPIM), ALLOCATABLE :: IVSETSC(:)
REAL(KIND=JPRB), ALLOCATABLE    :: ZSPSCAL(:,:)
INTEGER(KIND=JPIM)         :: IGG1, ISHIFT1
REAL(KIND=JPRB)            :: ZL11, ZL22, ZL33, ZL12, ZL13, ZL23
REAL(KIND=JPRB)            :: ZT11, ZT22, ZT33, ZT12, ZT13, ZT23
REAL(KIND=JPRB)            :: ZH1, ZH2, ZH3, ZLH1, ZLH2, ZLH3, ZPP, ZMP
REAL(KIND=JPRB)            :: ZDIST1, ZDIST2, ZDELTA1, ZDELTA2, ZDX
REAL(KIND=JPRB)            :: ZCL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZHCL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: Z_PDILEV(YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB)            :: ZN, ZFAC, ZEPS, ZDIV, ZPRES, ZDILEV, ZZW
!! REAL(KIND=JPRB)         :: ZDU, ZDV, ZDT
INTEGER(KIND=JPIM) :: JROF, JKGLO, IBL, IOFF, IST, ICEND, J, JL, IGG, JSP, IN, ISHIFT,&
 & JB, IFLDSIN, IFLD, INLOC, ICUT
LOGICAL :: LLEXTRA

REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZRDELP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPRESH(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPRESF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRPRESF(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSP0L(YDGEOMETRY%YRDIM%NPROMA),ZSP0M(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZMR(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS),&
 & ZM(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZDUM(YDGEOMETRY%YRDIM%NPROMA)

! -----------------------------------------------------------------------------

#include "dir_trans.h"
#include "inv_trans.h"

#include "gpcty.intfb.h"
#include "gphpre.intfb.h"

! -----------------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('GPSTRESS',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,            &
& YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, YDLAP=>YDGEOMETRY%YRLAP,    YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA, &
& YDVFE=>YDGEOMETRY%YRVFE)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, NRESOL=>YDDIM%NRESOL,   NSMAX=>YDDIM%NSMAX, NSPEC2=>YDDIM%NSPEC2,   NFLEVG=>YDDIMV%NFLEVG, &
& NFLSUR=>YDDIMV%NFLSUR,   RCLPOLE=>YDDYN%RCLPOLE, RCLSTRESS=>YDDYN%RCLSTRESS,   NGPTOT=>YDGEM%NGPTOT,                     &
& YT0=>YDGMV%YT0,   NVALUE=>YDLAP%NVALUE,   NBSETLEV=>YDMP%NBSETLEV,   TSTEP=>YDRIP%TSTEP)
! -----------------------------------------------------------------------------

LLEXTRA=.FALSE.
IF( PRESENT(PEXTRA) ) LLEXTRA=.TRUE.

ZEPS = 1.E-08_JPRB

! =============================================
! compute preliminaries for nonlinear diffusion
! =============================================

!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP& PRIVATE (JKGLO,IOFF,IST,IBL,ICEND) &
!$OMP& PRIVATE (ZPRESH,ZSP0L,ZSP0M,ZPRESF,ZRPRESF) &
!$OMP& PRIVATE (ZDUM) &
!$OMP& PRIVATE (ZCTY0,ZFAC) &
!$OMP& PRIVATE (JROF,JL)
DO JKGLO=1,NGPTOT,NPROMA
  IBL=(JKGLO-1)/NPROMA+1
  IOFF=JKGLO
  IST=1
  ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
 
  ! fill stress tensor helpers, GMVT1S --> YT1 (does not work for gradients)
  ! fill stress tensor helpers, GMVS --> YT0
  DO JROF=IST,ICEND
    ZPRESH(JROF,NFLEVG)=EXP(PGMVS(JROF,YT0%MSP,IBL))
    ZSP0L(JROF)=ZPRESH(JROF,NFLEVG)*PGMVS(JROF,YT0%MSPL,IBL)
    ZSP0M(JROF)=ZPRESH(JROF,NFLEVG)*PGMVS(JROF,YT0%MSPM,IBL)
  ENDDO

  ! ZPRESH, ZPRESF, ZXYB0:
  CALL GPHPRE(NPROMA,NFLEVG,IST,ICEND,YDVAB,ZPRESH,PXYB=ZXYB0,PRESF=ZPRESF)

  DO JL=1,NFLEVG
    DO JROF=IST,ICEND
      ZRPRESF(JROF,JL)=1.0_JPRB/ZPRESF(JROF,JL)
      ZRDELP(JROF,JL,IBL)=ZXYB0(JROF,JL,YYTXYB0%M_RDELP)
    ENDDO
  ENDDO

  ! compute etadot for the vertical part
  CALL GPCTY(YDVFE,NPROMA,IST,ICEND,NFLEVG,.FALSE.,YDVAB,YDVETA,&
   & PGMV(:,:,YT0%MU,IBL),PGMV(:,:,YT0%MV,IBL),PGMV(:,:,YT0%MDIV,IBL),ZDUM,&
   & ZXYB0,ZSP0L,ZSP0M,ZRPRESF,ZCTY0)

  ! use etadot * (dp/deta) on full levels to estimate vertical velocity w=-RD*T/(RG*PI)*m*\etadot
  ! 1/m = deta/dp == (YDVETA%VETAH(JLEV)-YDVETA%VETAH(JLEV-1))*ZRDELP
  DO JL=1,NFLEVG
    DO JROF=IST,ICEND  
      ! 1/m
      ZMR(JROF,JL,IBL) = (YDVETA%VETAH(JL)-YDVETA%VETAH(JL-1))*ZRDELP(JROF,JL,IBL)
      ! m
      ZM(JROF,JL,IBL) = 1.0_JPRB/ZMR(JROF,JL,IBL)
    ENDDO
  ENDDO

  ZFAC=-RD/RG
  IF(LVERTFE) THEN
    DO JL=1,NFLEVG
      DO JROF=IST,ICEND
        ! estimate w for ZCL computation (but use etadot * (dp/deta) for gradient)
        ZW(JROF,JL)= ZFAC*ZCTY0(JROF,JL,YYTCTY0%M_VVEL)*PGMV(JROF,JL,YT0%MT,IBL)
      ENDDO
    ENDDO
  ELSE
    DO JL=1,NFLEVG 
      DO JROF=IST,ICEND    
        ! estimate w for ZCL computation (but use etadot * (dp/deta) for gradient)
        ZW(JROF,JL)=&
         & ZFAC*0.5_JPRB*(ZCTY0(JROF,JL,YYTCTY0%M_VVEL)&
         &+ZCTY0(JROF,JL-1,YYTCTY0%M_VVEL))*PGMV(JROF,JL,YT0%MT,IBL)
      ENDDO
    ENDDO
  ENDIF

  ! fill stress tensor helpers

  DO JL=1,NFLEVG
    DO JROF=IST,ICEND
      ! fill stress tensor helpers, GMVT1 --> YT1
      ! fill stress tensor helpers, GMV   --> YT0
      ZSTRESS(IOFF+JROF-1,JL,1)          = PGMV(JROF,JL,YT0%MU,IBL)*PGMV(JROF,JL,YT0%MU,IBL)
      ZSTRESS(IOFF+JROF-1,1*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MV,IBL)*PGMV(JROF,JL,YT0%MV,IBL)
      ZSTRESS(IOFF+JROF-1,2*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MU,IBL)*PGMV(JROF,JL,YT0%MV,IBL)
      ZSTRESS(IOFF+JROF-1,3*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MU,IBL)
      ZSTRESS(IOFF+JROF-1,4*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MV,IBL)
      ! add vertical, use w estimate to get dimensions right ...
      ZSTRESS(IOFF+JROF-1,9*NFLEVG+JL,1) = ZW(JROF,JL)*ZW(JROF,JL)
      ZSTRESS(IOFF+JROF-1,10*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MU,IBL)*ZW(JROF,JL)
      ZSTRESS(IOFF+JROF-1,11*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MV,IBL)*ZW(JROF,JL)
      ZSTRESS(IOFF+JROF-1,12*NFLEVG+JL,1) = ZW(JROF,JL)
      ! add model vertical velocity (etadot * dp/deta) for correct gradient calculation
      IF( LVERTFE ) THEN
        ZSTRESS(IOFF+JROF-1,13*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MU,IBL)*ZCTY0(JROF,JL,YYTCTY0%M_EVEL)
        ZSTRESS(IOFF+JROF-1,14*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MV,IBL)*ZCTY0(JROF,JL,YYTCTY0%M_EVEL)
        ZSTRESS(IOFF+JROF-1,15*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)*ZCTY0(JROF,JL,YYTCTY0%M_EVEL)
        ZSTRESS(IOFF+JROF-1,16*NFLEVG+JL,1) = ZCTY0(JROF,JL,YYTCTY0%M_EVEL)
      ELSE
        ZZW=0.5_JPRB*(ZCTY0(JROF,JL,YYTCTY0%M_EVEL)+ZCTY0(JROF,JL-1,YYTCTY0%M_EVEL))
        ZSTRESS(IOFF+JROF-1,13*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MU,IBL)*ZZW
        ZSTRESS(IOFF+JROF-1,14*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MV,IBL)*ZZW
        ZSTRESS(IOFF+JROF-1,15*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)*ZZW
        ZSTRESS(IOFF+JROF-1,16*NFLEVG+JL,1) = ZZW
      ENDIF
      ! add temperature
      ZSTRESS(IOFF+JROF-1,5*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)*PGMV(JROF,JL,YT0%MU,IBL)
      ZSTRESS(IOFF+JROF-1,6*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)*PGMV(JROF,JL,YT0%MV,IBL)
      ZSTRESS(IOFF+JROF-1,7*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)*ZW(JROF,JL)
      ZSTRESS(IOFF+JROF-1,8*NFLEVG+JL,1) = PGMV(JROF,JL,YT0%MT,IBL)
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

! setup trans
IFLDSIN=JFIELDS*NFLEVG
INUMFLD(:)=IFLDSIN/NPRTRV
DO J=1,MOD(IFLDSIN,NPRTRV)
  INUMFLD(J)=INUMFLD(J)+1
ENDDO

ALLOCATE(IVSETSC(IFLDSIN))
IFLD=0
DO JB=1,NPRTRV
  DO JL=1,INUMFLD(JB)
    IFLD=IFLD+1
    IVSETSC(IFLD)=JB
  ENDDO
ENDDO

! =========================================================
! chop and convert scalars for nonlinear diffusion
! truncation limit 1
! =========================================================

! consider only upto dealiased limit == limit 1
IGG1=NINT(REAL(2*NSMAX,JPRB)/3._JPRB)-1
ICUT = 2*IGG1 - NINT(0.83_JPRB*REAL(NSMAX,JPRB))
ISHIFT1 = NINT(0.83_JPRB*REAL(NSMAX,JPRB))-IGG1

ZDX = (2._JPRB*RPI*RA/REAL(2*NSMAX+1,JPRB))
ZDIST1=6._JPRB*ZDX
ZDIST2=8._JPRB*ZDX

! first filter
ZDELTA1 = (ZDIST1**2)/(24._JPRB*RA*RA)
! second filter
ZDELTA2 = (ZDIST2**2)/(24._JPRB*RA*RA)

ALLOCATE(ZSPSCAL(INUMFLD(MYSETV),NSPEC2))
CALL DIR_TRANS(PSPSCALAR=ZSPSCAL(:,:),KVSETSC=IVSETSC(:),KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZSTRESS)

DO JSP=1,NSPEC2
  IN = NVALUE(JSP)
  IF( IN > ICUT ) THEN
    INLOC = IN + ISHIFT1
    ZFAC = 32._JPRB*(EXP(-0.5_JPRB*REAL(INLOC-NSMAX,JPRB)**2/REAL(INLOC-IGG1,JPRB)**2))
  ELSE
    ZFAC = 0._JPRB
  ENDIF
  ZN = 1._JPRB/(1._JPRB+ZFAC*ZFAC)

  ! filter 1  
  DO JL=1,INUMFLD(MYSETV)
    ZSPSCAL(JL,JSP) = ZN*ZSPSCAL(JL,JSP)*EXP(-ZDELTA1*REAL(IN*(IN+1),JPRB))
  ENDDO
ENDDO

CALL INV_TRANS(PSPSCALAR=ZSPSCAL,KVSETSC=IVSETSC(:),KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZSTRESS1)

! =========================================================
! chop and convert scalars for nonlinear diffusion
! double filter, ie. use ZSTRESS1 as input
! truncation limit 2
! =========================================================

!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP& PRIVATE (JKGLO,IOFF,IST,IBL,ICEND) &
!$OMP& PRIVATE (JROF,JL)
DO JKGLO=1,NGPTOT,NPROMA
  IBL=(JKGLO-1)/NPROMA+1
  IOFF=JKGLO
  IST=1
  ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  
  DO JL=1,NFLEVG
    DO JROF=IST,ICEND
      ! fill stress2 tensor helpers from stress1
      !uu
      ZSTRESS2(IOFF+JROF-1,JL,1) = ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)
      !vv
      ZSTRESS2(IOFF+JROF-1,1*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)
      !uv
      ZSTRESS2(IOFF+JROF-1,2*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)
      !u
      ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)
      !v
      ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)
      ! vertical
      ! ww
      ZSTRESS2(IOFF+JROF-1,9*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)
      ! uw
      ZSTRESS2(IOFF+JROF-1,10*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)
      ! vw
      ZSTRESS2(IOFF+JROF-1,11*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)
      ! w
      ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)
      ! u\etadot*m
      ZSTRESS2(IOFF+JROF-1,13*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,16*NFLEVG+JL,1)
      ! v\etadot*m
      ZSTRESS2(IOFF+JROF-1,14*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,16*NFLEVG+JL,1)
      ! T\etadot*m
      ZSTRESS2(IOFF+JROF-1,15*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,16*NFLEVG+JL,1)
      ! \etadot*m
      ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL,1)= ZSTRESS1(IOFF+JROF-1,16*NFLEVG+JL,1)

      ! temperature
      !uT
      ZSTRESS2(IOFF+JROF-1,5*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
      !vT
      ZSTRESS2(IOFF+JROF-1,6*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
      !wT
      ZSTRESS2(IOFF+JROF-1,7*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
      !T
      ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL,1) = ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

CALL DIR_TRANS(PSPSCALAR=ZSPSCAL(:,:),KVSETSC=IVSETSC(:),KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZSTRESS2)

DO JSP=1,NSPEC2
  IN = NVALUE(JSP)
  
  ! filter 2
  DO JL=1,INUMFLD(MYSETV)
    ZSPSCAL(JL,JSP) = ZSPSCAL(JL,JSP)*EXP(-ZDELTA2*REAL(IN*(IN+1),JPRB))
  ENDDO
ENDDO

CALL INV_TRANS(PSPSCALAR=ZSPSCAL,KVSETSC=IVSETSC(:),KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZSTRESS2)

DEALLOCATE(ZSPSCAL)
  
! start to reduce the influence of ZCL at 10hPa, matched with sponge in suhdf
ZPRESH(1,0)=YDVAB%VAH(0)+YDVAB%VBH(0)*VP00
DO JL=1,NFLEVG
  ZPRESH(1,JL)=YDVAB%VAH(JL)+YDVAB%VBH(JL)*VP00
  ZPRES=0.5_JPRB*(ZPRESH(1,JL)+ZPRESH(1,JL-1))
  ZDILEV=1.0_JPRB+7.5_JPRB*(3._JPRB-LOG10(ZPRES))
  Z_PDILEV(JL)=MAX(1.0_JPRB,ZDILEV)
ENDDO

! ========================================================
! compute dynamic coefficient from difference of 1,2 
! using Germano identity and Lilly procedure of minimizing
! ========================================================
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP& PRIVATE (JKGLO,IOFF,IST,IBL,ICEND) &
!$OMP& PRIVATE (JROF, JL, ZL11, ZL22, ZL33, ZL12, ZL13, ZL23) &
!$OMP& PRIVATE (ZT11, ZT22, ZT33, ZT12, ZT13, ZT23, ZDIV, ZCL) &
!$OMP& PRIVATE (ZHCL, ZH1, ZH2, ZH3, ZLH1, ZLH2, ZLH3, ZPP, ZMP)
DO JKGLO=1,NGPTOT,NPROMA
  IBL=(JKGLO-1)/NPROMA+1
  IOFF=JKGLO
  IST=1
  ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  
  DO JL=1,NFLEVG
    DO JROF=IST,ICEND
      
      ZT11 = ZSTRESS1(IOFF+JROF-1,JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)
      ZT22 = ZSTRESS1(IOFF+JROF-1,1*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)
      ZT33 = ZSTRESS1(IOFF+JROF-1,9*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)

      ZT12 = ZSTRESS1(IOFF+JROF-1,2*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)
      ZT13 = ZSTRESS1(IOFF+JROF-1,10*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)
      ZT23 = ZSTRESS1(IOFF+JROF-1,11*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)

      ZH1 = ZSTRESS1(IOFF+JROF-1,5*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
      ZH2 = ZSTRESS1(IOFF+JROF-1,6*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)
      ZH3 = ZSTRESS1(IOFF+JROF-1,7*NFLEVG+JL,1)&
       & - ZSTRESS1(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS1(IOFF+JROF-1,8*NFLEVG+JL,1)

      ! in the paper these are sampled/averaged over the gridbox as well but this requires
      ! communication/halo which is too expensive/complicated for the moment 
      ! instead I use a bigger delta in the second filter
      ZL11 = ZSTRESS2(IOFF+JROF-1,JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1)
      ZL22 = ZSTRESS2(IOFF+JROF-1,1*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1)
      ZL33 = ZSTRESS2(IOFF+JROF-1,9*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)

      ZL12 = ZSTRESS2(IOFF+JROF-1,2*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1)
      ZL13 = ZSTRESS2(IOFF+JROF-1,10*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)
      ZL23 = ZSTRESS2(IOFF+JROF-1,11*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)

      ! temperature
      ZLH1 = ZSTRESS2(IOFF+JROF-1,5*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL,1)
      ZLH2 = ZSTRESS2(IOFF+JROF-1,6*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL,1)
      ZLH3 = ZSTRESS2(IOFF+JROF-1,7*NFLEVG+JL,1)&
       & - ZSTRESS2(IOFF+JROF-1,12*NFLEVG+JL,1)*ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL,1)

      ! compute exchange coefficient (3D)
      ZDIV = ZL11*ZL11+ZL22*ZL22+ZL33*ZL33+2._JPRB*(ZL13*ZL13+ZL23*ZL23+ZL12*ZL12)
      ZDIV=SIGN(1.0_JPRB,ZDIV)/MAX(ZEPS,ABS(ZDIV))
      ZCL(JROF,JL) = ZDIV*(ZT11*ZL11+ZT22*ZL22+ZT33*ZL33 + 2._JPRB*(ZT12*ZL12+ZT23*ZL23+ZT13*ZL13))

      ! temperature
      ZDIV=ZLH1*ZLH1+ZLH2*ZLH2+ZLH3*ZLH3
      ZDIV=SIGN(1.0_JPRB,ZDIV)/MAX(ZEPS,ABS(ZDIV))
      ZHCL(JROF,JL) = ZDIV*(ZH1*ZLH1+ZH2*ZLH2+ZH3*ZLH3)

      IF( LLEXTRA ) PEXTRA(JROF,JL,1,IBL) = ZCL(JROF,JL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,2,IBL) = ZHCL(JROF,JL)

      ! switch off contributions gradually in the sponge layer ...
      ZCL(JROF,JL)  = SIGN(1.0_JPRB,ZCL(JROF,JL))*MIN(RCLSTRESS,ABS(ZCL(JROF,JL)))/(Z_PDILEV(JL)**2)
      ZHCL(JROF,JL) = SIGN(1.0_JPRB,ZHCL(JROF,JL))*MIN(RCLSTRESS,ABS(ZHCL(JROF,JL)))/(Z_PDILEV(JL)**2)
      
      ! fill horizontal stress tensor for gradient computation
      ! uu
      ZGRADU(IOFF+JROF-1,JL,1) = ZM(JROF,JL,IBL)*ZCL(JROF,JL)*ZL11
      ! uv
      ZGRADU(IOFF+JROF-1,NFLEVG+JL,1) = ZM(JROF,JL,IBL)*ZCL(JROF,JL)*ZL12
      ! vu
      ZGRADV(IOFF+JROF-1,JL,1) = ZM(JROF,JL,IBL)*ZCL(JROF,JL)*ZL12
      ! vv
      ZGRADV(IOFF+JROF-1,NFLEVG+JL,1) = ZM(JROF,JL,IBL)*ZCL(JROF,JL)*ZL22
      ! uT
      ZGRADT(IOFF+JROF-1,JL,1) = ZM(JROF,JL,IBL)*ZHCL(JROF,JL)*ZLH1
      ! vT
      ZGRADT(IOFF+JROF-1,NFLEVG+JL,1) = ZM(JROF,JL,IBL)*ZHCL(JROF,JL)*ZLH2

    ENDDO
  ENDDO
  
  ! vertical part normal case
  DO JL=2,NFLEVG-1
    DO JROF=IST,ICEND
      ! uw
      ZMP = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+JL-1,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL-1,1)
      ZPP = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+JL+1,1)&
       & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+JL+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL+1,1)
      ZGRADUW(IOFF+JROF-1,JL) = ZRDELP(JROF,JL,IBL)*0.5_JPRB*(ZCL(JROF,JL+1)*ZPP-ZCL(JROF,JL-1)*ZMP)
      ! vw
      ZMP = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+JL-1,1)&
       & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL-1,1)
      ZPP = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+JL+1,1)&
       & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+JL+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL+1,1)
      ZGRADVW(IOFF+JROF-1,JL) = ZRDELP(JROF,JL,IBL)*0.5_JPRB*(ZCL(JROF,JL+1)*ZPP-ZCL(JROF,JL-1)*ZMP)
      ! Tw
      ZMP = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+JL-1,1)&
       & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL-1,1)
      ZPP = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+JL+1,1)&
       & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+JL+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+JL+1,1)
      ZGRADTW(IOFF+JROF-1,JL) = ZRDELP(JROF,JL,IBL)*0.5_JPRB*(ZHCL(JROF,JL+1)*ZPP-ZHCL(JROF,JL-1)*ZMP)
    ENDDO
  ENDDO
  
  ! vertical part special case
  DO JROF=IST,ICEND

    !! model top

    ! uw
    ZL13 = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+1,1)&
     & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+1,1)
    ZPP = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+2,1)&
     & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+2,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+2,1)
    ZGRADUW(IOFF+JROF-1,1) = ZRDELP(JROF,1,IBL)*0.5_JPRB*(ZCL(JROF,1)*ZL13+ZCL(JROF,2)*ZPP)
    ! vw
    ZL23 = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+1,1)&
     & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+1,1)
    ZPP = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+2,1)&
     & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+2,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+2,1)
    ZGRADVW(IOFF+JROF-1,1) = ZRDELP(JROF,1,IBL)*0.5_JPRB*(ZCL(JROF,1)*ZL23+ZCL(JROF,2)*ZPP)
    ! Tw
    ZLH3 = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+1,1)&
     & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+1,1)
    ZPP = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+2,1)&
     & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+2,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+2,1)
    ZGRADTW(IOFF+JROF-1,1) = ZRDELP(JROF,1,IBL)*0.5_JPRB*(ZHCL(JROF,1)*ZLH3+ZHCL(JROF,2)*ZPP)

    !! model bottom
    
    ! uw
    ZL13 = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+NFLEVG,1)&
     & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+NFLEVG,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG,1)
    ZMP = ZSTRESS2(IOFF+JROF-1,13*NFLEVG+NFLEVG-1,1)&
     & - ZSTRESS2(IOFF+JROF-1,3*NFLEVG+NFLEVG-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG-1,1)
    ZGRADUW(IOFF+JROF-1,NFLEVG) = -ZRDELP(JROF,NFLEVG,IBL)*0.5_JPRB*(ZCL(JROF,NFLEVG)*ZL13+ZCL(JROF,NFLEVG-1)*ZMP)
    ! vw
    ZL23 = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+NFLEVG,1)&
     & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+NFLEVG,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG,1)
    ZMP = ZSTRESS2(IOFF+JROF-1,14*NFLEVG+NFLEVG-1,1)&
     & - ZSTRESS2(IOFF+JROF-1,4*NFLEVG+NFLEVG-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG-1,1)
    ZGRADVW(IOFF+JROF-1,NFLEVG) = -ZRDELP(JROF,NFLEVG,IBL)*0.5_JPRB*(ZCL(JROF,NFLEVG)*ZL23+ZCL(JROF,NFLEVG-1)*ZMP)
    ! Tw
    ZLH3 = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+NFLEVG,1)&
     & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+NFLEVG,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG,1)
    ZMP = ZSTRESS2(IOFF+JROF-1,15*NFLEVG+NFLEVG-1,1)&
     & - ZSTRESS2(IOFF+JROF-1,8*NFLEVG+NFLEVG-1,1)*ZSTRESS2(IOFF+JROF-1,16*NFLEVG+NFLEVG-1,1)
    ZGRADTW(IOFF+JROF-1,NFLEVG) = -ZRDELP(JROF,NFLEVG,IBL)*0.5_JPRB*(ZHCL(JROF,NFLEVG)*ZLH3+ZHCL(JROF,NFLEVG-1)*ZMP)
  ENDDO

ENDDO
!$OMP END PARALLEL DO

! =======================================================
! compute rhs U == horizontal divergence of truncated stress tensor
! =======================================================

CALL DIR_TRANS(PSPVOR=ZROTSP,PSPDIV=ZDIVSP,KVSETUV=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZGRADU)
! transfer divergence to gridpoint space
CALL INV_TRANS(PSPSCALAR=ZDIVSP,KVSETSC=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,&
 & PGP=ZNLDIFF(:,1:NFLEVG,:))

! =======================================================
! compute rhs V == horizontal divergence of truncated stress tensor
! =======================================================

CALL DIR_TRANS(PSPVOR=ZROTSP,PSPDIV=ZDIVSP,KVSETUV=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZGRADV)
! transfer divergence to gridpoint space
CALL INV_TRANS(PSPSCALAR=ZDIVSP,KVSETSC=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,&
 & PGP=ZNLDIFF(:,NFLEVG+1:2*NFLEVG,:))

! =======================================================
! compute rhs T == horizontal divergence of heat flux
! =======================================================
CALL DIR_TRANS(PSPVOR=ZROTSP,PSPDIV=ZDIVSP,KVSETUV=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,PGP=ZGRADT)
! transfer divergence to gridpoint space
CALL INV_TRANS(PSPSCALAR=ZDIVSP,KVSETSC=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,&
 & PGP=ZNLDIFF(:,2*NFLEVG+1:3*NFLEVG,:))

IF( YRDYNA%LGRADSP ) THEN
! =========================================================
! dealias the new rhs contribution
! =========================================================
! rotational dealiasing adjustments
IGG=NINT(REAL(2*NSMAX,JPRB)/3._JPRB)-1
ISHIFT = NINT(0.83_JPRB*REAL(NSMAX,JPRB))-IGG

CALL DIR_TRANS(PSPVOR=ZROTSP,PSPDIV=ZDIVSP,KVSETUV=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,&
  & PGP=ZNLDIFF(:,1:2*NFLEVG,:))
DO JSP=1,NSPEC2
  IN = NVALUE(JSP) + ISHIFT
  IF( IN > IGG ) THEN
    ZFAC = 32._JPRB*(EXP(-0.5_JPRB*REAL(IN-NSMAX,JPRB)**2/REAL(IN-IGG,JPRB)**2))
  ELSE
    ZFAC = 0._JPRB
  ENDIF
  ZN = 1._JPRB/(1._JPRB+ZFAC*ZFAC)
  
  DO JL=1,NFLSUR
    ZROTSP(JL,JSP) = ZN*ZROTSP(JL,JSP)
  ENDDO
ENDDO
CALL INV_TRANS(PSPVOR=ZROTSP,PSPDIV=ZDIVSP,KVSETUV=NBSETLEV,KRESOL=NRESOL,KPROMA=NGPTOT,&
 &PGP=ZGRAD)

ELSE

  ZGRAD(:,:,1) = ZNLDIFF(:,:,1)

ENDIF

! =========================================================
! calculate the new rhs term contribution
! =========================================================

!!$! limiters, for numerical stability
!!$! turbulent wind production of max XX m/s per hour
!!$ZDU = YRRIP%TSTEP * 2.0_JPRB/3600._JPRB
!!$ZDV = YRRIP%TSTEP * 2.0_JPRB/3600._JPRB
!!$! max 10 K per day
!!$ZDT = YRRIP%TSTEP * 10._JPRB/(24._JPRB*3600._JPRB)

!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP& PRIVATE (JKGLO,IOFF,IST,IBL,ICEND) &
!$OMP& PRIVATE (JROF,JL)
DO JKGLO=1,NGPTOT,NPROMA
  IBL=(JKGLO-1)/NPROMA+1
  IOFF=JKGLO
  IST=1
  ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  
  DO JL=1,NFLEVG
    ! only apply outside sponge ...
    DO JROF=IST,ICEND
      ! moderate effect at the poles > 80. degrees due to occasional instability
      ! likely resulting from spontaneous convection at the poles ... 
      ! (reminiscent of Piotr's Held-Suarez runs ...)
!      IF(ABS(YDGSGEOM_NB%GEMU(IOFF+JROF-1)) < RCLPOLE .AND. (Z_PDILEV(JL) <= 1.1 ) THEN
      IF(ABS(YDGSGEOM_NB%GEMU(IOFF+JROF-1)) < RCLPOLE ) THEN
        ! du in one TSTEP
        POUT(JROF,JL,1,IBL) = -TSTEP*ZMR(JROF,JL,IBL)*ZGRAD(IOFF+JROF-1,JL,1)&
         & -TSTEP*ZGRADUW(IOFF+JROF-1,JL)
        ! dv in one TSTEP
        POUT(JROF,JL,2,IBL) = -TSTEP*ZMR(JROF,JL,IBL)*ZGRAD(IOFF+JROF-1,NFLEVG+JL,1)&
         & -TSTEP*ZGRADVW(IOFF+JROF-1,JL)
        ! dT in one TSTEP
        POUT(JROF,JL,3,IBL) = -TSTEP*ZMR(JROF,JL,IBL)*ZNLDIFF(IOFF+JROF-1,2*NFLEVG+JL,1)&
         & -TSTEP*ZGRADTW(IOFF+JROF-1,JL)
      ELSE
        POUT(JROF,JL,1,IBL) = 0.0_JPRB
        POUT(JROF,JL,2,IBL) = 0.0_JPRB
        POUT(JROF,JL,3,IBL) = 0.0_JPRB
      ENDIF

      IF( LLEXTRA ) PEXTRA(JROF,JL,3,IBL) = POUT(JROF,JL,1,IBL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,4,IBL) = POUT(JROF,JL,2,IBL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,5,IBL) = POUT(JROF,JL,3,IBL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,6,IBL) = -TSTEP*ZGRADUW(IOFF+JROF-1,JL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,7,IBL) = -TSTEP*ZGRADVW(IOFF+JROF-1,JL)
      IF( LLEXTRA ) PEXTRA(JROF,JL,8,IBL) = -TSTEP*ZGRADTW(IOFF+JROF-1,JL)

!!$      POUT(JROF,JL,1,IBL) = SIGN(1.0_JPRB,POUT(JROF,JL,1,IBL))*MIN(ZDU,ABS(POUT(JROF,JL,1,IBL)))
!!$      POUT(JROF,JL,2,IBL) = SIGN(1.0_JPRB,POUT(JROF,JL,1,IBL))*MIN(ZDV,ABS(POUT(JROF,JL,1,IBL)))
!!$      POUT(JROF,JL,3,IBL) = SIGN(1.0_JPRB,POUT(JROF,JL,1,IBL))*MIN(ZDT,ABS(POUT(JROF,JL,1,IBL)))

    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

! -----------------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GPSTRESS',1,ZHOOK_HANDLE)
END SUBROUTINE GPSTRESS

