#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE LARCINATL(&
 ! --- INPUT ---------------------------------------------------------
 & YDGEOMETRY, YDML_DYN,KST,KPROF,YDSL,KSTABUF,LDSLHD,LDSLHDQUAD,LDSLHD_OLD,KTIP,KROT,LDPLANE,PLSDEPI,&
 & KIBL,&
 & PSCO,PLEV,PSCO5,PLEV5,&
 & PKAPPA,PKAPPA5,PKAPPAT,PKAPPAT5,PURL0,PVRL0,PZRL0,PWRL0,PURL05,PVRL05,PZRL05,PWRL05,&
 ! --- OUTPUT --------------------------------------------------------
 & PCCO,PUF0,PVF0,PZF0,PWF0,PCCO5,&
 & KL0,KLH0,KLEV,PLSCAW,PRSCAW,PLSCAW5,PRSCAW5,KNOWENO)  

!**** *LARCINATL - semi-LAgrangian scheme:(Trajectory)
!                                        (tangent-linear version)
!                 Research of the Coordinates (of the medium or origin
!                 point) and INterpolations.

!     Purpose.
!     --------
!       Computes the longitude and latitude of the interpolation
!       point from its cartesian coordinates.
!       Then computes the vector displacement matrix
!                        I po pq I
!                        I       I
!                        I-pq po I
!       from the interpolation point to the grid point.
!       At last determines the interpolation grid:
!       - computation of the latitude and the longitude of the
!         point situated at the upper left corner of the 16 points
!         square, and of the interpolation point.
!       - optionally, interpolates wind field for next iteration
!         of the algorithm to find the medium/departure point.

!**   Interface.
!     ----------
!        *CALL* *LARCINATL(......)

!        Explicit arguments :
!        --------------------

!        INPUT:
!          KST     - first element of arrays where computations are performed.
!          KPROF   - depth of work.
!          YDSL    - SL_STRUCT definition.
!          KSTABUF - for a latitude IGL, KSTABUF(IGL) is the
!                    address of the element corresponding to
!                    (ILON=1,IGL) in the NPROMA arrays.
!          LDSLHD  - key activating SLHD weights precomputation
!          LDSLHDQUAD - key activating quadratic weights precomputation
!          LDSLHD_OLD - use old SLHD interpolator
!          KTIP    - KTIP=1: interpolations for wind to find trajectory.
!                    KTIP=3: (E)LASCAWTL for origin point.
!          KROT    - KROT=1: computation of the elements po and pq
!                    of the wind displacement matrix.
!                    KROT=0: no computation.
!          LDPLANE - switch: .T. = plane geometry; .F. = spherical geometry.
!          PLSDEPI - (Number of points by latitude) / (2 * PI) .
!          KIBL    - index into YRCSGEOM/YRGSGEOM instances in YDGEOMETRY
!          PSCO    - information about geographic position of interpol. point.
!          PLEV    - vertical coordinate of the interpolation point.
!          PSCO5   - cf. PSCO (trajectory)
!          PLEV5   - cf. PLEV (trajectory)
!          PKAPPA  - kappa function ("coefficient of SLHD") based on the
!                    rescaled horizontal deformation of the flow evaluated
!                    at instant "t" for the final point F
!          PKAPPA5 - cf. PKAPPA (trajectory)
!          PKAPPAT/PKAPPAT5 - kappa function for T plus its trajectory
!          PURL0   - U-component of the wind.
!          PVRL0   - V-component of the wind.
!          PZRL0   - Z-component of the wind.
!          PWRL0   - "etadot"-component of the wind.
!          PURL05  - cf. PURL0 (trajectory)
!          PVRL05  - cf. PVRL0 (trajectory)
!          PZRL05  - cf. PZRL0 (trajectory)
!          PWRL05  - cf. PWRL0 (trajectory)
!          PCCO    - information about comput. space position of interpol. point.
!          PCCO5   - cf. PCCO (trajectory)

!        OUTPUT:
!          PUF0    - Interpolated U-wind.
!          PVF0    - Interpolated V-wind.
!          PZF0    - Interpolated Z-wind.
!          PWF0    - Interpolated "etadot"-wind.
!          KL0     - index of the four western points
!                    of the 16 points interpolation grid.
!          KLH0    - second value of index of the four western points
!                    of the 16 points interpolation grid if needed.
!          KLEV    - lower level of the vertical interpolation
!                    grid needed for vertical interpolations.
!          PLSCAW  - linear weights (distances) for interpolations.
!          PRSCAW  - non-linear weights for interpolations.
!          PLSCAW5 - linear weights (distances) for interpolations (trajectory).
!          PRSCAW5 - non-linear weights for interpolations (trajectory).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------

!     Author.
!     -------
!      C. Temperton (ECMWF)
!      Original: 99/07/12

!     Modifications.
!     --------------
!      Modified 01-08-30 by K. YESSAD: pruning and some other cleanings.
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      Modified 05-01-11 by C. Temperton: streamlining.    
!      Modified 06-06-13 by F. Vana: LDPLANE=.T.
!      Modified 08-Jun-27 by F. Vana: weights driven interpolation
!      K. Yessad Nov 2008: rationalisation of dummy argument interfaces
!      K. Yessad (Aug 2009): always use root (QX,QY) for (p,q) variables names
!      K. Yessad (Aug 2009): use RIPI, RSLD
!      K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!      G.Mozdzynski (Jan 2011): OOPS cleaning, use of derived type SL_STRUCT
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived types TGSGEOM, TCSGEOM and TCSGLEG
!      G.Mozdzynski (May 2012): further cleaning
!      F. Vana  13-Feb-2014  kappaT for heat variables
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      B. Bochenek (Apr 2015): Phasing: update
!      F. Vana    21-Nov-2017: Options LSLDP_CURV and LHOISLT
!      F. Vana    July 2018: RK4 scheme for trajectory research.
!      F. Vana October 2018: Extended LSLDP_CURV.
!      F. Vana 25-Feb-2019: Vertical quintic interpolation for RHS.
!     ------------------------------------------------------------------

USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCST             , ONLY : RPI
USE YOMDYNA            , ONLY : YRDYNA
USE EINT_MOD           , ONLY : SL_STRUCT

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_DYNAMICS_TYPE),INTENT(IN):: YDML_DYN
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF
TYPE(SL_STRUCT)   ,INTENT(INOUT) :: YDSL
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTABUF(YDSL%NDGSAH:YDSL%NDGENH)
LOGICAL           ,INTENT(IN)    :: LDSLHD
LOGICAL           ,INTENT(IN)    :: LDSLHDQUAD
LOGICAL           ,INTENT(IN)    :: LDSLHD_OLD
INTEGER(KIND=JPIM),INTENT(IN)    :: KTIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KROT
LOGICAL           ,INTENT(IN)    :: LDPLANE
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSDEPI(YDSL%NDGSAH:YDSL%NDGENH)
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTSCO%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLEV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCO5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTSCO%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLEV5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKAPPA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKAPPA5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKAPPAT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKAPPAT5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PURL0(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRL0(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZRL0(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWRL0(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PURL05(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRL05(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZRL05(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWRL05(YDSL%NASLB1,YDGEOMETRY%YRDIMV%NFLSA:YDGEOMETRY%YRDIMV%NFLEN)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCCO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTCCO%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PZF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PWF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCCO5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTCCO%NDIM)
INTEGER(KIND=JPIM),INTENT(INOUT) :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3)
INTEGER(KIND=JPIM),INTENT(INOUT) :: KLH0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3)
INTEGER(KIND=JPIM),INTENT(INOUT) :: KLEV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTLSCAW%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTRSCAW%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLSCAW5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTLSCAW%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRSCAW5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTRSCAW%NDIM)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNOWENO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IHOR, IWIS, IWENO
REAL(KIND=JPRB) :: ZHOOK_HANDLE
REAL(KIND=JPRB) :: ZDSTRET, ZDEPI, ZPIS2
REAL(KIND=JPRB) :: ZDUM5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZKAPPA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZKAPPA5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZKAPPAT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZKAPPAT5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "laitlitl.intfb.h"
#include "laitritl.intfb.h"
#include "laitri_weno_tl.intfb.h"
#include "elarchetl.intfb.h"
#include "larchetl.intfb.h"
#include "elascawtl.intfb.h"
#include "lascawtl.intfb.h"

!     ------------------------------------------------------------------


IF (LHOOK) CALL DR_HOOK('LARCINATL',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM, &
 & YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,  YDHSLMER=>YDGEOMETRY%YRHSLMER, &
 & YDCSGLEG=>YDGEOMETRY%YRCSGLEG, YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA,  YDVFE=>YDGEOMETRY%YRVFE,  &
 & YDVSLETA=>YDGEOMETRY%YRVSLETA, YDSTA=>YDGEOMETRY%YRSTA, YDLAP=>YDGEOMETRY%YRLAP,  &
 & YDVSPLIP=>YDGEOMETRY%YRVSPLIP, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL),  YDSPGEOM=>YDGEOMETRY%YSPGEOM,  &
 & YDDYN=>YDML_DYN%YRDYN,YDTCCO=>YDML_DYN%YYTCCO,YDTSCO=>YDML_DYN%YYTSCO,YDTLSCAW=>YDML_DYN%YYTLSCAW, &
 & YDTRSCAW=>YDML_DYN%YYTRSCAW)

ASSOCIATE(NPROMA=>YDDIM%NPROMA, &
 & NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA, &
 & NSTTYP=>YDGEM%NSTTYP, R4JP=>YDGEM%R4JP, RC2M1=>YDGEM%RC2M1, &
 & RC2P1=>YDGEM%RC2P1, RLOCEN=>YDGEM%RLOCEN, RMUCEN=>YDGEM%RMUCEN, &
 & RSTRET=>YDGEM%RSTRET, LSLHDHEAT=>YDDYN%LSLHDHEAT, LSLDP_CURV=>YDDYN%LSLDP_CURV, &
 & RIPI=>YDHSLMER%RIPI, RSLD=>YDHSLMER%RSLD, RSLDW=>YDHSLMER%RSLDW, GAMMA_WENO=>YDVSLETA%GAMMA_WENO, &
 & NRLEVX=>YDVSLETA%NRLEVX, NVAUTF=>YDVSLETA%NVAUTF, VCUICO=>YDVSLETA%VCUICO, &
 & VRLEVX=>YDVSLETA%VRLEVX, VSLD=>YDVSLETA%VSLD, VSLDW=>YDVSLETA%VSLDW)
!     ------------------------------------------------------------------

!*       1.    PRELIMINARY INITIALISATIONS.
!              ----------------------------

ZDSTRET=2.0_JPRB*RSTRET
ZDEPI=2.0_JPRB*RPI
ZPIS2=0.5_JPRB*RPI
IHOR=1

IWENO=1

! * Input variable IWIS for LASCAW.
IF (KTIP == 1) THEN
  ! * trajectory research.
  IF (YRDYNA%LHOISLT) THEN
    IWIS=102
    IF (YRDYNA%LSLTVWENO) THEN
      IWENO=3
    ENDIF
  ELSE
    IWIS=101
  ENDIF
ELSEIF (KTIP == 3) THEN
  ! * origin point interpolations.
  IF (YRDYNA%LRHSVWENO) THEN
    IWIS=106
    IWENO=3
  ELSE
    IWIS=103
  ENDIF
ELSE
  IWIS=-999
  CALL ABOR1('LARCINATL: WRONG VALUE FOR IWIS')
ENDIF

!     ------------------------------------------------------------------

!*       2.    COMPUTATION OF LAT LON OF THE INTERPOLATION POINT.
!              IF KROT=1 COMPUTATION OF THE WIND DISPLACEMENT MATRIX
!              FROM THE INTERPOLATION POINT TO THE FINAL POINT.
!              ( T FOR LATITUDE THETA, L FOR LONGITUDE LAMBDA).
!              PCCO(.,.,YDTCCO%M_RQX) = ( 1 / (1+cos(PHI)) )
!                  *( cos(TG)*cos(T) + (1+sin(TG)*sin(T))*cos(L-LG) )
!              PCCO(.,.,YDTCCO%M_RQY) = (-1 / (1+cos(PHI)) )
!                  *( sin(TG)+sin(T) ) * sin(L-LG)
!     ------------------------------------------------------------------

! use always filled ZKAPPA and ZKAPPA5 to avoid problems
! do we however trully need this?
IF (YRDYNA%LSLHD) THEN
  ZKAPPA(KST:KPROF,1:NFLEVG)=PKAPPA(KST:KPROF,1:NFLEVG)
  ZKAPPA5(KST:KPROF,1:NFLEVG)=PKAPPA5(KST:KPROF,1:NFLEVG)
  IF (LSLHDHEAT) THEN
    ZKAPPAT(KST:KPROF,1:NFLEVG)=PKAPPAT(KST:KPROF,1:NFLEVG)
    ZKAPPAT5(KST:KPROF,1:NFLEVG)=PKAPPAT5(KST:KPROF,1:NFLEVG)
  ELSE
    ZKAPPAT(KST:KPROF,1:NFLEVG)=0._JPRB
    ZKAPPAT5(KST:KPROF,1:NFLEVG)=0._JPRB
  ENDIF
ELSE
  ! ZKAPPA and ZKAPPA5 are not used.
  ZKAPPA(KST:KPROF,1:NFLEVG)=0.0_JPRB
  ZKAPPA5(KST:KPROF,1:NFLEVG)=0.0_JPRB
  ZKAPPAT(KST:KPROF,1:NFLEVG)=0.0_JPRB
  ZKAPPAT5(KST:KPROF,1:NFLEVG)=0.0_JPRB
ENDIF

IF (LDPLANE) THEN

  IF (KROT == 1) THEN
    CALL ELARCHETL(YDML_DYN,NPROMA,KST,KPROF,NFLEVG,YDSL,YDGEOMETRY%YREGSL,YDGEOMETRY%YREGEO,YDGSGEOM,PSCO,PCCO,PSCO5,PCCO5)
  ENDIF

  CALL ELASCAWTL(YDDYN,YDSL,NPROMA,KST,KPROF,NFLEVG,&
   & NFLSA,KSTABUF,IWIS,IHOR,&
   & LDSLHD,LDSLHDQUAD,LDSLHD_OLD,&
   & PSCO(1,1,YDTSCO%M_COSCO),PSCO(1,1,YDTSCO%M_SINCO),PLEV,&
   & PSCO5(1,1,YDTSCO%M_COSCO),PSCO5(1,1,YDTSCO%M_SINCO),PLEV5,&
   & YDVETA%VETAF,NVAUTF,&
   & VCUICO,VSLD,VSLDW,NRLEVX,VRLEVX,ZKAPPA,ZKAPPA5,ZKAPPAT,ZKAPPAT5,&
   & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)),PRSCAW(1,1,YDTRSCAW%M_WCLASLD(1)),&
   & PRSCAW(1,1,YDTRSCAW%M_WCLASLT),&
   & PLSCAW(1,1,YDTLSCAW%M_WDLO),PRSCAW(1,1,YDTRSCAW%M_WCLO(1)),PRSCAW(1,1,YDTRSCAW%M_WCLOSLD(1)),&
   & PRSCAW(1,1,YDTRSCAW%M_WCLOSLT),KL0,KLH0,KLEV,&
   & PLSCAW(1,1,YDTLSCAW%M_WDVER),PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW(1,1,YDTRSCAW%M_WVINTWSLD),&
   & PRSCAW(1,1,YDTRSCAW%M_WVINTWSLT),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)),PRSCAW5(1,1,YDTRSCAW%M_WCLASLD(1)),&
   & PRSCAW5(1,1,YDTRSCAW%M_WCLASLT),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)),PRSCAW5(1,1,YDTRSCAW%M_WCLOSLD(1)),&
   & PRSCAW5(1,1,YDTRSCAW%M_WCLOSLT),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDVER),PRSCAW5(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTWSLD),&
   & PRSCAW5(1,1,YDTRSCAW%M_WVINTWSLT))

ELSE

  CALL LARCHETL(YDML_DYN%YYTCCO,YDML_DYN%YYTSCO,NPROMA,KST,KPROF,NFLEVG,&
   & NSTTYP,ZDSTRET,RC2M1,RC2P1,RPI,ZDEPI,&
   & RLOCEN,RMUCEN,YDGSGEOM,YDCSGEOM,&
   & PSCO,PSCO5,KROT,PCCO,PCCO5)

  CALL LASCAWTL(YDSL,NPROMA,KST,KPROF,NFLEVG,&
   & NFLSA,KSTABUF,IWIS,IHOR,IWENO,&
   & LDSLHD,LDSLHDQUAD,LDSLHD_OLD,LSLHDHEAT,&
   & R4JP,ZPIS2,PLSDEPI,YDCSGLEG%RLATI(YDSL%NDGSAH:),&
   & RIPI(YDSL%NDGSAH:YDSL%NDGENH,1:3),RSLD(YDSL%NDGSAH:YDSL%NDGENH,1:3),RSLDW(1,1,YDSL%NDGSAH),&
   & PCCO(1,1,YDTCCO%M_RLON),PCCO(1,1,YDTCCO%M_RLAT),PLEV,&
   & PCCO5(1,1,YDTCCO%M_RLON),PCCO5(1,1,YDTCCO%M_RLAT),PLEV5,&
   & YDVETA%VETAF,NVAUTF,&
   & VCUICO,VSLD,VSLDW,GAMMA_WENO,NRLEVX,VRLEVX,ZKAPPA,ZKAPPA5,ZKAPPAT,ZKAPPAT5,&
   & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)),PRSCAW(1,1,YDTRSCAW%M_WCLASLD(1)),&
   & PRSCAW(1,1,YDTRSCAW%M_WCLASLT),&
   & PLSCAW(1,1,YDTLSCAW%M_WDLO),PRSCAW(1,1,YDTRSCAW%M_WCLO(1)),PRSCAW(1,1,YDTRSCAW%M_WCLOSLD(1)),&
   & PRSCAW(1,1,YDTRSCAW%M_WCLOSLT),KL0,KLH0,KLEV,KNOWENO,PRSCAW(1,1,YDTRSCAW%M_CW),&
   & PLSCAW(1,1,YDTLSCAW%M_WDVER),PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW(1,1,YDTRSCAW%M_WVINTWSLD),&
   & PRSCAW(1,1,YDTRSCAW%M_WVINTWSLT),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)),PRSCAW5(1,1,YDTRSCAW%M_WCLASLD(1)),&
   & PRSCAW5(1,1,YDTRSCAW%M_WCLASLT),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)),PRSCAW5(1,1,YDTRSCAW%M_WCLOSLD(1)),&
   & PRSCAW5(1,1,YDTRSCAW%M_WCLOSLT),PRSCAW5(1,1,YDTRSCAW%M_CW),&
   & PLSCAW5(1,1,YDTLSCAW%M_WDVER),PRSCAW5(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTWSLD),&
   & PRSCAW5(1,1,YDTRSCAW%M_WVINTWSLT))
 
ENDIF

!     ------------------------------------------------------------------

!*       3.    INTERPOLATIONS OF WIND FOR TRAJECTORY RESEARCH.
!              -----------------------------------------------

IF (KTIP == 1) THEN

  !*      Interpolations.
  IF (YRDYNA%LSLTVWENO) THEN
    CALL LAITRI_WENO_TL(YDDYN,YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO) ,PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,KNOWENO,PRSCAW(1,1,YDTRSCAW%M_CW),PRSCAW5(1,1,YDTRSCAW%M_CW), &
     & PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PURL0,PURL05,PUF0,ZDUM5)  
    CALL LAITRI_WENO_TL(YDDYN,YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO) ,PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,KNOWENO,PRSCAW(1,1,YDTRSCAW%M_CW),PRSCAW5(1,1,YDTRSCAW%M_CW), &
     & PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PVRL0,PVRL05,PVF0,ZDUM5)  
    IF (LSLDP_CURV) THEN
      CALL LAITRI_WENO_TL(YDDYN,YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
       & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
       & PLSCAW(1,1,YDTLSCAW%M_WDLO) ,PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
       & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
       & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
       & KL0,KNOWENO,PRSCAW(1,1,YDTRSCAW%M_CW),PRSCAW5(1,1,YDTRSCAW%M_CW), &
       & PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
       & PZRL0,PZRL05,PZF0,ZDUM5)  
    ENDIF
    CALL LAITRI_WENO_TL(YDDYN,YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO) ,PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,KNOWENO,PRSCAW(1,1,YDTRSCAW%M_CW),PRSCAW5(1,1,YDTRSCAW%M_CW), &
     & PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PWRL0,PWRL05,PWF0,ZDUM5)
  ELSEIF (YRDYNA%LHOISLT) THEN
    CALL LAITRITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,.false.,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT), PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO), PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PURL0,PURL05,PUF0,ZDUM5)  
    CALL LAITRITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,.false.,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT), PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO), PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PVRL0,PVRL05,PVF0,ZDUM5)  
    IF (LSLDP_CURV) THEN
      CALL LAITRITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,.false.,&
       & PLSCAW(1,1,YDTLSCAW%M_WDLAT), PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
       & PLSCAW(1,1,YDTLSCAW%M_WDLO), PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
       & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
       & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
       & KL0,PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
       & PZRL0,PZRL05,PZF0,ZDUM5)  
    ENDIF
    CALL LAITRITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,.false.,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT), PRSCAW(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW(1,1,YDTLSCAW%M_WDLO), PRSCAW(1,1,YDTRSCAW%M_WCLO(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PRSCAW5(1,1,YDTRSCAW%M_WCLA(1)), &
     & PLSCAW5(1,1,YDTLSCAW%M_WDLO),PRSCAW5(1,1,YDTRSCAW%M_WCLO(1)), &
     & KL0,PRSCAW(1,1,YDTRSCAW%M_WVINTW),PRSCAW5(1,1,YDTRSCAW%M_WVINTW), &
     & PWRL0,PWRL05,PWF0,ZDUM5)
  ELSE
    CALL LAITLITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PLSCAW(1,1,YDTLSCAW%M_WDLO+1),&
     & KL0(1,1,1),PLSCAW(1,1,YDTLSCAW%M_WDVER),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PLSCAW5(1,1,YDTLSCAW%M_WDLO+1),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDVER),&
     & PURL0,PURL05,PUF0,ZDUM5)  
    CALL LAITLITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PLSCAW(1,1,YDTLSCAW%M_WDLO+1),&
     & KL0(1,1,1),PLSCAW(1,1,YDTLSCAW%M_WDVER),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PLSCAW5(1,1,YDTLSCAW%M_WDLO+1),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDVER),&
     & PVRL0,PVRL05,PVF0,ZDUM5)  
    IF (LSLDP_CURV) THEN
      CALL LAITLITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
       & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PLSCAW(1,1,YDTLSCAW%M_WDLO+1),&
       & KL0(1,1,1),PLSCAW(1,1,YDTLSCAW%M_WDVER),&
       & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PLSCAW5(1,1,YDTLSCAW%M_WDLO+1),&
       & PLSCAW5(1,1,YDTLSCAW%M_WDVER),&
       & PZRL0,PZRL05,PZF0,ZDUM5)  
    ENDIF
    CALL LAITLITL(YDSL%NASLB1,NPROMA,KST,KPROF,NFLEVG,NFLSA,NFLEN,&
     & PLSCAW(1,1,YDTLSCAW%M_WDLAT),PLSCAW(1,1,YDTLSCAW%M_WDLO+1),&
     & KL0(1,1,1),PLSCAW(1,1,YDTLSCAW%M_WDVER),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDLAT),PLSCAW5(1,1,YDTLSCAW%M_WDLO+1),&
     & PLSCAW5(1,1,YDTLSCAW%M_WDVER),&
     & PWRL0,PWRL05,PWF0,ZDUM5)  
  ENDIF

ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('LARCINATL',1,ZHOOK_HANDLE)
END SUBROUTINE LARCINATL
