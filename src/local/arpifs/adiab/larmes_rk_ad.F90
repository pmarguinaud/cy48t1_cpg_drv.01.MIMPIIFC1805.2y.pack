!option! -O nomove
SUBROUTINE LARMES_RK_AD(YDGEOMETRY,YDRIP,YDML_DYN,KSTGLO,KST,KPROF,YDSL,KSTABUF,PB1,PB15,PB2,&
 & PINC,KINC,KDIM1,KDIM2,&
 & PLSDEPI,KIBL,KDEP,KNOWENO,&
 & PSCO,PLEV,PCCO,&
 & KL0,KLOCK,&
 & PSCO5,PLEV5,PCCO5,PUF05,PVF05,PZF05,PWF05,PUS5,PVS5,PZS5,PWS5,&
 & PLSCAW5,PRSCAW5)


!---- should be compiled for Cray with -hcontiguous----

!**** *LARMES_RK_AD - semi-LAgrangian scheme:
!                Research of the origin point on the Sphere by 4th order Runge-Kutta method.

!     Purpose.
!     --------

!      The computation of the location of the interpolation point of
!     the lagrangian trajectory is performed by an 4th order Runge-Kutta 
!     method. 
!     Note the final trajectory is composite of four different trajectories on great circle.
!     Finally we find the departure (origin) point "O".

!**   Interface.
!     ----------
!        *CALL* *LARMES_RK_AD(...)

!        Explicit arguments :
!        --------------------

!        INPUT:
!          KSTGLO   - global offset into nproma buffer.
!          KST      - first element of arrays where computations are performed.
!          KPROF    - depth of work.
!          YDSL     - SL_STRUCT definition
!          KSTABUF  - for a latitude IGL, KSTABUF(IGL) is the
!                     address of the element corresponding to
!                     (ILON=1,IGL) in the NPROMA arrays.
!          KDIM1,KDIM2 - first two dimensions of KINC, PINC
!          PLSDEPI  - (Number of points by latitude) / (2 * PI) .
!          KIBL     - index into YRCSGEOM/YRGSGEOM instances in YDGEOMETRY
!          KDEP     - dependency indicator for the southernost or northermost latitudes (LAM only)
!          KNOWENO  - vertical boundary indicator for the WENO interpolation.
!          KL0      - index of the four western points
!                     of the 16 points interpolation grid.
!          PLSCAW   - linear weights (distances) for interpolations.
!          PRSCAW   - non-linear weights for interpolations.
!          PSCO     - information about geographic position of interpol. point.
!          PLEV     - vertical coordinate of the interpolation point.
!          PCCO     - information about comput. space position of interpol. point.

!        INPUT/OUTPUT:
!          KLOCK    - locking variable used in OpenMP synchronization
!          KINC     - addresses of interpolation increments in global buffer
!          PINC     - interpolation increments to global buffer (vector only)

!        OUTPUT:
!          PB1      - SLBUF1-buffer for interpolations.
!          PB2      - SLBUF2-buf to communicate info from non lag. to lag. dyn.

!        TRAJECTORY (input):
!          PB15     - SLBUF1-buffer for interpolations (trajectory).
!          PSCO5    - information about geographic position of interpol. point. (traj)
!          PLEV5    - vertical coordinate of the interpolation point. (traj)
!          PCCO5    - information about comput. space position of interpol. point. (traj)
!          PUF05    - U-comp of "(a/rs)*wind" interpolated quantity for stages 1 & 2
!          PVF05    - V-comp of "(a/rs)*wind" interpolated quantity for stages 1 & 2
!          PZF05    - Z-comp of "(a/rs)*wind" interpolated quantity for stages 1 & 2
!          PWF05    - Z-comp of "(a/rs)*wind" interpolated quantity for stages 1 & 2
!          PUS5,PVS5,PZS5,PWS5 - stage wind in Cartesian space 
!          PLSCAW5  - linear weights (distances) for interpolations.
!          PRSCAW5  - non-linear weights for interpolations.

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation about semi-Lagrangian scheme.

!     Externals.
!     ----------
!        Calls  LARCINAAD
!        Is called by LAPINEAAD (3D model)

!     Reference.
!     ----------

!     Author.
!     -------
!      F. VANA  after LARMES5 and LARMES_RK
!      Original : MAY 2018

!     Modifications.
!     --------------
!   F. Vana October 2018: Extended LSLDP_CURV.
!   F. Vana  26-Feb-2019: Vertical quintic interpolation.
!   F. Vana  14-Mar-2019: Make the scheme implicit following Lobatto IIIA type

! End Modifications
!     ------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM, JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK

USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMCT0    , ONLY : LTWOTL

USE YOMVAR    , ONLY : LVECADIN
USE YOMRIP    , ONLY : TRIP
USE EINT_MOD  , ONLY : SL_STRUCT

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_DYNAMICS_TYPE),INTENT(IN):: YDML_DYN
TYPE(TRIP)        ,INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO
INTEGER(KIND=JPIM),INTENT(IN)    :: KST
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF
TYPE(SL_STRUCT)   ,INTENT(INOUT) :: YDSL
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTABUF(YDSL%NDGSAH:YDSL%NDGENH)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB1(YDSL%NASLB1,YDML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB15(YDSL%NASLB1,YDML_DYN%YRPTRSLB15%NFLDSLB15)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDML_DYN%YRPTRSLB2%NFLDSLB2)
INTEGER(KIND=JPIM),INTENT(IN)    :: KDIM1
INTEGER(KIND=JPIM),INTENT(IN)    :: KDIM2
INTEGER(KIND=JPIM),INTENT(INOUT) :: KINC(KDIM1,KDIM2,3*(YDML_DYN%YRDYN%NITMP-1))
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PINC(KDIM1,KDIM2,3*(YDML_DYN%YRDYN%NITMP-1))
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSDEPI(YDSL%NDGSAH:YDSL%NDGENH)
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
INTEGER(KIND=JPIM),INTENT(IN)    :: KDEP(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YRDYN%NITMP,3)
INTEGER(KIND=JPIM),INTENT(IN)    :: KNOWENO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YRDYN%NITMP,3)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSCO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                      YDML_DYN%YYTSCO%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLEV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCCO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                      YDML_DYN%YYTCCO%NDIM)
INTEGER(KIND=JPIM),INTENT(INOUT) :: KLOCK(:)
INTEGER(KIND=JPIM),INTENT(IN)    :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3, &
  &                                       YDML_DYN%YRDYN%NITMP,3)


REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCO5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YYTSCO%NDIM,YDML_DYN%YRDYN%NITMP,2:3)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLEV5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YRDYN%NITMP,2:3)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCCO5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YYTCCO%NDIM,YDML_DYN%YRDYN%NITMP,3)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUF05(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       2,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVF05(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       2,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZF05(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       2,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWF05(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       2,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUS5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       3,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVS5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       3,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZS5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       3,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWS5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       3,YDML_DYN%YRDYN%NITMP)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCAW5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YYTLSCAW%NDIM,YDML_DYN%YRDYN%NITMP,3)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRSCAW5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
  &                                       YDML_DYN%YYTRSCAW%NDIM,YDML_DYN%YRDYN%NITMP,3)

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IROT, IINC
INTEGER(KIND=JPIM) :: ITIP
INTEGER(KIND=JPIM) :: JITER
INTEGER(KIND=JPIM) :: JLEV
INTEGER(KIND=JPIM) :: JROF

LOGICAL :: LLO
LOGICAL :: LLSLHD, LLSLHDQUAD, LLSLHD_OLD
LOGICAL :: LLOCK_UP

REAL(KIND=JPRB) :: ZDTS22, ZDTS62, ZDTSA, ZINT, ZLEVO, ZNOR2, ZSPHSV
REAL(KIND=JPRB) :: ZLEVO5, ZNOR25, ZSPHSV5

REAL(KIND=JPRB) :: ZVETAON, ZVETAOX

REAL(KIND=JPRB) :: ZPU, ZPV
REAL(KIND=JPRB) :: ZPU5, ZPV5
REAL(KIND=JPRB) :: ZPU0, ZPV0, ZPZ0, ZPW0
REAL(KIND=JPRB) :: ZPU05, ZPV05, ZPZ05

REAL(KIND=JPRB) :: ZU0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZV0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZZ0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZUF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZVF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZZF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZWF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZUS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3)
REAL(KIND=JPRB) :: ZVS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3)
REAL(KIND=JPRB) :: ZZS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3)
REAL(KIND=JPRB) :: ZWS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3)

REAL(KIND=JPRB) :: ZLSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTLSCAW%NDIM)
REAL(KIND=JPRB) :: ZRSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTRSCAW%NDIM)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

! unused arguments in call to LARCINA:
! a) input arrays - will not be used since LSLHD was set to .FALSE.
!    in LAPINEA before call to LARMES_RK_AD/ELARMES_RK_AD => dimensions can be
!    contracted
REAL(KIND=JPRB) :: ZUN_KAPPA(1),ZUN_KAPPAT(1),ZUN_KAPPA5(1),ZUN_KAPPAT5(1)

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "larcinaad.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('LARMES_RK_AD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 & YDMP=>YDGEOMETRY%YRMP, YDSTA=>YDGEOMETRY%YRSTA, YDVAB=>YDGEOMETRY%YRVAB, &
 & YDVETA=>YDGEOMETRY%YRVETA, YDLAP=>YDGEOMETRY%YRLAP, YDCSGLEG=>YDGEOMETRY%YRCSGLEG,  &
 & YDVSPLIP=>YDGEOMETRY%YRVSPLIP, YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, YDCSGEOM=>YDGEOMETRY%YRCSGEOM(KIBL), &
 & YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL),  &
 & YDSPGEOM=>YDGEOMETRY%YSPGEOM,YDDYN=>YDML_DYN%YRDYN,YDPTRSLB15=>YDML_DYN%YRPTRSLB15,  &
 & YDPTRSLB1=>YDML_DYN%YRPTRSLB1,YDPTRSLB2=>YDML_DYN%YRPTRSLB2)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, &
 & NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA, &
 & LFINDVSEP=>YDDYN%LFINDVSEP, LSVTSM=>YDDYN%LSVTSM, NITMP=>YDDYN%NITMP, &
 & RPRES_SVTSM=>YDDYN%RPRES_SVTSM, VETAON=>YDDYN%VETAON, VETAOX=>YDDYN%VETAOX, &
 & LSLDP_CURV=>YDDYN%LSLDP_CURV, NVSEPC=>YDDYN%NVSEPC, NVSEPL=>YDDYN%NVSEPL, &
 & MSLB1UR0=>YDPTRSLB1%MSLB1UR0, MSLB1VR0=>YDPTRSLB1%MSLB1VR0, &
 & MSLB1ZR0=>YDPTRSLB1%MSLB1ZR0, MSLB1WR0=>YDPTRSLB1%MSLB1WR0, &
 & MSLB1UR00=>YDPTRSLB1%MSLB1UR00, MSLB1VR00=>YDPTRSLB1%MSLB1VR00, &
 & MSLB1ZR00=>YDPTRSLB1%MSLB1ZR00, MSLB1WR00=>YDPTRSLB1%MSLB1WR00, &
 & MSLB1UR05=>YDPTRSLB15%MSLB1UR05, MSLB1VR05=>YDPTRSLB15%MSLB1VR05, &
 & MSLB1ZR05=>YDPTRSLB15%MSLB1ZR05, MSLB1WR05=>YDPTRSLB15%MSLB1WR05, &
 & MSLB1UR005=>YDPTRSLB15%MSLB1UR005, MSLB1VR005=>YDPTRSLB15%MSLB1VR005, &
 & MSLB1ZR005=>YDPTRSLB15%MSLB1ZR005, MSLB1WR005=>YDPTRSLB15%MSLB1WR005, &
 & NFLDSLB15=>YDPTRSLB15%NFLDSLB15, &
 & MSLB2URL5=>YDPTRSLB2%MSLB2URL5, &
 & MSLB2VRL5=>YDPTRSLB2%MSLB2VRL5, MSLB2WRL5=>YDPTRSLB2%MSLB2WRL5, &
 & MSLB2URL=>YDPTRSLB2%MSLB2URL, &
 & MSLB2VRL=>YDPTRSLB2%MSLB2VRL, MSLB2WRL=>YDPTRSLB2%MSLB2WRL, &
 & NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & RDTS22=>YDRIP%RDTS22, RDTS62=>YDRIP%RDTS62, RDTSA=>YDRIP%RDTSA, &
 & RTDT=>YDRIP%RTDT)

!     ------------------------------------------------------------------

!*       1.    PRELIMINARY INITIALISATIONS AND TESTS.
!              --------------------------------------

!*       1.2   Miscellaneous preliminary initialisations.

! Compute number of updates for LARCINAAD:
! Backward engineering:
IF (LVECADIN) THEN
  IINC=(KDIM1-1)/NFLEVG
ELSE
  IINC=KDIM1
ENDIF

! LLO.OR.LELTRA should now be always T for SL2TL.
IF (LTWOTL) THEN
  LLO=.NOT.YDML_DYN%YRDYNA%LELTRA
ELSE
  LLO=.FALSE.
ENDIF

IF(LLO.OR.YDML_DYN%YRDYNA%LELTRA) THEN
  ZDTS22=RDTS22*4._JPRB
  ZDTSA=RDTSA*2._JPRB
  ZDTS62=RDTS62*4._JPRB
ELSE
  ! this case may occur only for SL3TL scheme.
  IF (LTWOTL) CALL ABOR1(' LARMES_RK_AD 1.2 ')
ENDIF

! deactivate computation of SLHD weights
LLSLHD     =.FALSE.
LLSLHDQUAD =.FALSE.
LLSLHD_OLD =.FALSE.

!*       1.3   Computation of weights for smooth interpolation at arrival point.

IF(YDML_DYN%YRDYNA%LRALTVDISP .OR. YDML_DYN%YRDYNA%LELTRA) THEN
  CALL ABOR1('NOT CODED')
ENDIF


!*    ADJOINT code

! Initialization of increments
ZU0 (KST:KPROF,1:NFLEVG)=0._JPRB
ZV0 (KST:KPROF,1:NFLEVG)=0._JPRB
ZZ0 (KST:KPROF,1:NFLEVG)=0._JPRB
ZUF0(KST:KPROF,1:NFLEVG)=0._JPRB
ZVF0(KST:KPROF,1:NFLEVG)=0._JPRB
ZZF0(KST:KPROF,1:NFLEVG)=0._JPRB
ZWF0(KST:KPROF,1:NFLEVG)=0._JPRB
ZUS (KST:KPROF,1:NFLEVG,1:3)=0._JPRB
ZVS (KST:KPROF,1:NFLEVG,1:3)=0._JPRB
ZZS (KST:KPROF,1:NFLEVG,1:3)=0._JPRB
ZWS (KST:KPROF,1:NFLEVG,1:3)=0._JPRB


!     ------------------------------------------------------------------

!*       2.    ITERATIONS.
!              -----------

ZVETAON=(1.0_JPRB-VETAON)*YDVETA%VETAH(0)+VETAON*YDVETA%VETAF(1)
ZVETAOX=(1.0_JPRB-VETAOX)*YDVETA%VETAH(NFLEVG)+VETAOX*YDVETA%VETAF(NFLEVG)

!Interpolation settings
LLOCK_UP=.false.
ITIP=1
IROT=0

!  Iterations starts

DO JITER=NITMP,2,-1

  ! STAGE 3 (departure point, t) =============================================================

  ! Interpolation for stage 3
  IF (JITER /= NITMP) THEN
    CALL LARCINAAD(YDGEOMETRY,YDML_DYN,IINC,KSTGLO,KST,KPROF,YDSL,&
     & KSTABUF,LLSLHD,LLSLHDQUAD,LLSLHD_OLD,ITIP,IROT,.FALSE.,LLOCK_UP,PLSDEPI,&
     & KIBL,KDEP(1,1,JITER,3),KNOWENO(1,1,JITER,3),&
     & PSCO,PLEV,PSCO5(1,1,1,JITER,3),PLEV5(1,1,JITER,3),&
     & ZUN_KAPPA,ZUN_KAPPA5,ZUN_KAPPAT,ZUN_KAPPAT5,&
     & PB1(1,MSLB1UR00),PB1(1,MSLB1VR00),PB1(1,MSLB1ZR00),PB1(1,MSLB1WR00),&
     & PB15(1,MSLB1UR005),PB15(1,MSLB1VR005),PB15(1,MSLB1ZR005),PB15(1,MSLB1WR005),&
     & NVSEPC,NVSEPL,KLOCK,&
     & PINC(1,1,3*(JITER-1)+1),KINC(1,1,3*(JITER-1)+1),KDIM1,KDIM2,&
     & PCCO,ZUS(:,:,3),ZVS(:,:,3),ZZS(:,:,3),ZWS(:,:,3),PCCO5(1,1,1,JITER,3),&
     & KL0(1,1,0,JITER,3),ZLSCAW,ZRSCAW,PLSCAW5(1,1,1,JITER,3),PRSCAW5(1,1,1,JITER,3),LDREP=.TRUE.) ! 00 buffers are used
  ENDIF

  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF

      ! Trajectory

      ! * computations on horizontal plans.
      ZPU05=(PUS5(JROF,JLEV,1,JITER)+4._JPRB*PUS5(JROF,JLEV,2,JITER) &
       &    +PUS5(JROF,JLEV,3,JITER-1))/6._JPRB
      ZPV05=(PVS5(JROF,JLEV,1,JITER)+4._JPRB*PVS5(JROF,JLEV,2,JITER) &
       &    +PVS5(JROF,JLEV,3,JITER-1))/6._JPRB
      ZPZ05=(PZS5(JROF,JLEV,1,JITER)+4._JPRB*PZS5(JROF,JLEV,2,JITER) &
       &    +PZS5(JROF,JLEV,3,JITER-1))/6._JPRB
      ! Convert wind back to lat,lon at Arrival point
      ZPU5= -ZPU05*YDGSGEOM%GESLO(JROF)                     &
       &    +ZPV05*YDGSGEOM%GECLO(JROF)
      ZPV5= -ZPU05*YDGSGEOM%GECLO(JROF)*YDGSGEOM%GEMU(JROF) &
       &    -ZPV05*YDGSGEOM%GESLO(JROF)*YDGSGEOM%GEMU(JROF) &
       &    +ZPZ05*YDGSGEOM%GSQM2(JROF)

      ZNOR25=( ZPU5*ZPU5 + ZPV5*ZPV5 )
      ZSPHSV5=ZDTSA*(1.0_JPRB-ZDTS62*ZNOR25)


      ! Adjoint
      ZSPHSV=-ZPU5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
      ZPU   =-ZSPHSV5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)=PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI) &
       & +YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO) &
       & +YDGSGEOM%GEMU(JROF) *PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
      ZINT=YDGSGEOM%GEMU(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO) &
       & -YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO)=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)=0._JPRB
      ZSPHSV = ZSPHSV+ZPV5*ZINT
      ZPV=ZSPHSV5*ZINT
      !ZINT=0._JPRB
      ZNOR2=-ZDTSA*ZDTS62*ZSPHSV -ZDTS22*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)
      !ZSPHSV=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)=0._JPRB
      ZPU = ZPU + 2.0_JPRB*ZPU5*ZNOR2
      ZPV = ZPV + 2.0_JPRB*ZPV5*ZNOR2
      !ZNOR2=0._JPRB

      ! Convert wind back to lat,lon at Arrival point
      ZPU0 = -YDGSGEOM%GECLO(JROF)*YDGSGEOM%GEMU(JROF)*ZPV -YDGSGEOM%GESLO(JROF)*ZPU
      ZPV0 = -YDGSGEOM%GESLO(JROF)*YDGSGEOM%GEMU(JROF)*ZPV +YDGSGEOM%GECLO(JROF)*ZPU
      ZPZ0 =  YDGSGEOM%GSQM2(JROF)*ZPV
      !ZPU=0._JPRB
      !ZPV=0._JPRB

      ! * computations on horizontal plans.
      ZZS(JROF,JLEV,1)=ZZS(JROF,JLEV,1)+ZPZ0/6._JPRB
      ZZS(JROF,JLEV,2)=ZZS(JROF,JLEV,2)+ZPZ0*4._JPRB/6._JPRB
      ZZS(JROF,JLEV,3)=ZZS(JROF,JLEV,3)+ZPZ0/6._JPRB
      ZVS(JROF,JLEV,1)=ZVS(JROF,JLEV,1)+ZPV0/6._JPRB
      ZVS(JROF,JLEV,2)=ZVS(JROF,JLEV,2)+ZPV0*4._JPRB/6._JPRB
      ZVS(JROF,JLEV,3)=ZVS(JROF,JLEV,3)+ZPV0/6._JPRB
      ZUS(JROF,JLEV,1)=ZUS(JROF,JLEV,1)+ZPU0/6._JPRB
      ZUS(JROF,JLEV,2)=ZUS(JROF,JLEV,2)+ZPU0*4._JPRB/6._JPRB
      ZUS(JROF,JLEV,3)=ZUS(JROF,JLEV,3)+ZPU0/6._JPRB
      !ZPU0=0._JPRB
      !ZPV0=0._JPRB
      !ZPZ0=0._JPRB


      ! * computations on a vertical.
      ZLEVO=PLEV(JROF,JLEV)
      PLEV(JROF,JLEV)=0._JPRB
      ZLEVO5=PLEV5(JROF,JLEV,JITER,3)
      IF (ZLEVO5 <= ZVETAON) THEN
        ZLEVO=0.0_JPRB
      ELSEIF (ZLEVO5 >= ZVETAOX) THEN
        ZLEVO=0.0_JPRB
      ENDIF
      ZPW0=-RTDT*ZLEVO
      ZWS(JROF,JLEV,1)=ZWS(JROF,JLEV,1)+ZPW0/6._JPRB
      ZWS(JROF,JLEV,2)=ZWS(JROF,JLEV,2)+ZPW0*4._JPRB/6._JPRB
      ZWS(JROF,JLEV,3)=ZWS(JROF,JLEV,3)+ZPW0/6._JPRB
      !ZPW0=0._JPRB

    ENDDO
  ENDDO

  ! STAGE 2 (midpoint, t+0.5dt) ==============================================================
  ! Here we however compute the departure point position using then obvious SETTLS averaging into M

  ! New wind stage 2
  ! Stage 2 (middle point, t+0.5dt)
  ! Averaging wind by SETTLS to arrival point in Cartesian space
  PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1)= PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1) &
   &                                               + 0.5_JPRB*ZWS(KST:KPROF,1:NFLEVG,2)
  ZZ0(KST:KPROF,1:NFLEVG)= ZZ0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZZS(KST:KPROF,1:NFLEVG,2)
  ZV0(KST:KPROF,1:NFLEVG)= ZV0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZVS(KST:KPROF,1:NFLEVG,2)
  ZU0(KST:KPROF,1:NFLEVG)= ZU0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZUS(KST:KPROF,1:NFLEVG,2)
  ZWF0(KST:KPROF,1:NFLEVG)=0.5_JPRB*ZWS(KST:KPROF,1:NFLEVG,2)
  ZZF0(KST:KPROF,1:NFLEVG)=0.5_JPRB*ZZS(KST:KPROF,1:NFLEVG,2)
  ZVF0(KST:KPROF,1:NFLEVG)=0.5_JPRB*ZVS(KST:KPROF,1:NFLEVG,2)
  ZUF0(KST:KPROF,1:NFLEVG)=0.5_JPRB*ZUS(KST:KPROF,1:NFLEVG,2)
  ZWS(KST:KPROF,1:NFLEVG,2)=0._JPRB
  ZZS(KST:KPROF,1:NFLEVG,2)=0._JPRB
  ZVS(KST:KPROF,1:NFLEVG,2)=0._JPRB
  ZUS(KST:KPROF,1:NFLEVG,2)=0._JPRB

  ! Interpolation for stage 2
  CALL LARCINAAD(YDGEOMETRY,YDML_DYN,IINC,KSTGLO,KST,KPROF,YDSL,&
   & KSTABUF,LLSLHD,LLSLHDQUAD,LLSLHD_OLD,ITIP,IROT,.FALSE.,LLOCK_UP,PLSDEPI,&
   & KIBL,KDEP(1,1,JITER,2),KNOWENO(1,1,JITER,2),&
   & PSCO,PLEV,PSCO5(1,1,1,JITER,2),PLEV5(1,1,JITER,2),&
   & ZUN_KAPPA,ZUN_KAPPA5,ZUN_KAPPAT,ZUN_KAPPAT5,&
   & PB1(1,MSLB1UR0),PB1(1,MSLB1VR0),PB1(1,MSLB1ZR0),PB1(1,MSLB1WR0),&
   & PB15(1,MSLB1UR05),PB15(1,MSLB1VR05),PB15(1,MSLB1ZR05),PB15(1,MSLB1WR05),&
   & NVSEPC,NVSEPL,KLOCK,&
   & PINC(1,1,3*(JITER-1)),KINC(1,1,3*(JITER-1)),KDIM1,KDIM2,&
   & PCCO,ZUF0,ZVF0,ZZF0,ZWF0,PCCO5(1,1,1,JITER,2),&
   & KL0(1,1,0,JITER,2),ZLSCAW,ZRSCAW,PLSCAW5(1,1,1,JITER,2),PRSCAW5(1,1,1,JITER,2))

  DO JLEV=1,NFLEVG
    DO JROF=KST,KPROF

      ! Trajectory

      ! * computations on horizontal plans.
      ZPU05=(5._JPRB*PUS5(JROF,JLEV,1,JITER)+8._JPRB*PUS5(JROF,JLEV,2,JITER-1) &
       &            -PUS5(JROF,JLEV,3,JITER-1))/12._JPRB
      ZPV05=(5._JPRB*PVS5(JROF,JLEV,1,JITER)+8._JPRB*PVS5(JROF,JLEV,2,JITER-1) &
       &            -PVS5(JROF,JLEV,3,JITER-1))/12._JPRB
      ZPZ05=(5._JPRB*PZS5(JROF,JLEV,1,JITER)+8._JPRB*PZS5(JROF,JLEV,2,JITER-1) &
       &            -PZS5(JROF,JLEV,3,JITER-1))/12._JPRB

      ! Convert wind back to lat,lon at Arrival point
      ZPU5 = -ZPU05*YDGSGEOM%GESLO(JROF)                     &
       &     +ZPV05*YDGSGEOM%GECLO(JROF)
      ZPV5 = -ZPU05*YDGSGEOM%GECLO(JROF)*YDGSGEOM%GEMU(JROF) &
       &     -ZPV05*YDGSGEOM%GESLO(JROF)*YDGSGEOM%GEMU(JROF) &
       &     +ZPZ05*YDGSGEOM%GSQM2(JROF)

      ZNOR25=( ZPU5*ZPU5 + ZPV5*ZPV5 )
      ZSPHSV5=ZDTSA*(1.0_JPRB-ZDTS62*ZNOR25)


      ! Adjoint code
      ZSPHSV=-ZPU5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
      ZPU   =-ZSPHSV5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)=PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI) &
       & +YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO) &
       & +YDGSGEOM%GEMU(JROF) *PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
      ZINT=YDGSGEOM%GEMU(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO) &
       & -YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO)=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)=0._JPRB
      ZSPHSV = ZSPHSV+ZPV5*ZINT
      ZPV=ZSPHSV5*ZINT
      !ZINT=0._JPRB
      ZNOR2=-ZDTSA*ZDTS62*ZSPHSV -ZDTS22*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)
      !ZSPHSV=0._JPRB
      PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)=0._JPRB
      ZPU = ZPU + 2.0_JPRB*ZPU5*ZNOR2
      ZPV = ZPV + 2.0_JPRB*ZPV5*ZNOR2
      !ZNOR2=0._JPRB

      ! Convert wind back to lat,lon at Arrival point
      ZPU0 = -YDGSGEOM%GECLO(JROF)*YDGSGEOM%GEMU(JROF)*ZPV -YDGSGEOM%GESLO(JROF)*ZPU
      ZPV0 = -YDGSGEOM%GESLO(JROF)*YDGSGEOM%GEMU(JROF)*ZPV +YDGSGEOM%GECLO(JROF)*ZPU
      ZPZ0 =  YDGSGEOM%GSQM2(JROF)*ZPV
      !ZPU=0._JPRB
      !ZPV=0._JPRB

      ! * computations on horizontal plans.
      ZZS(JROF,JLEV,1)=ZZS(JROF,JLEV,1)+5._JPRB*ZPZ0/12._JPRB
      ZZS(JROF,JLEV,2)=ZZS(JROF,JLEV,2)+8._JPRB*ZPZ0/12._JPRB
      ZZS(JROF,JLEV,3)=ZZS(JROF,JLEV,3)-ZPZ0/12._JPRB
      ZVS(JROF,JLEV,1)=ZVS(JROF,JLEV,1)+5._JPRB*ZPV0/12._JPRB
      ZVS(JROF,JLEV,2)=ZVS(JROF,JLEV,2)+8._JPRB*ZPV0/12._JPRB
      ZVS(JROF,JLEV,3)=ZVS(JROF,JLEV,3)-ZPV0/12._JPRB
      ZUS(JROF,JLEV,1)=ZUS(JROF,JLEV,1)+5._JPRB*ZPU0/12._JPRB
      ZUS(JROF,JLEV,2)=ZUS(JROF,JLEV,2)+8._JPRB*ZPU0/12._JPRB
      ZUS(JROF,JLEV,3)=ZUS(JROF,JLEV,3)-ZPU0/12._JPRB
      !ZPU0=0._JPRB
      !ZPV0=0._JPRB
      !ZPZ0=0._JPRB

      ! * computations on a vertical.
      ZLEVO=PLEV(JROF,JLEV)
      PLEV(JROF,JLEV)=0._JPRB
      ZLEVO5=PLEV5(JROF,JLEV,JITER,2)
      IF (ZLEVO5 <= ZVETAON) THEN
        ZLEVO=0.0_JPRB
      ELSEIF (ZLEVO5 >= ZVETAOX) THEN
        ZLEVO=0.0_JPRB
      ENDIF
      ZPW0=-RTDT*ZLEVO
      ZWS(JROF,JLEV,1)=ZWS(JROF,JLEV,1)+5._JPRB*ZPW0/12._JPRB
      ZWS(JROF,JLEV,2)=ZWS(JROF,JLEV,2)+8._JPRB*ZPW0/12._JPRB
      ZWS(JROF,JLEV,3)=ZWS(JROF,JLEV,3)-ZPW0/12._JPRB
      !ZPW0=0._JPRB

    ENDDO
  ENDDO

  ! STAGE 1 (arrival point, t+dt) ============================================================
  
  ! Extrapolating wind by SETTLS to arrival point in Cartesian space
  ZU0(KST:KPROF,1:NFLEVG) = ZU0(KST:KPROF,1:NFLEVG) + ZUS(KST:KPROF,1:NFLEVG,1)
  ZV0(KST:KPROF,1:NFLEVG) = ZV0(KST:KPROF,1:NFLEVG) + ZVS(KST:KPROF,1:NFLEVG,1)
  ZZ0(KST:KPROF,1:NFLEVG) = ZZ0(KST:KPROF,1:NFLEVG) + ZZS(KST:KPROF,1:NFLEVG,1)
  PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1) = PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1) &
   &                                                + ZWS(KST:KPROF,1:NFLEVG,1)
  ZUF0(KST:KPROF,1:NFLEVG)= ZUS(KST:KPROF,1:NFLEVG,1)
  ZVF0(KST:KPROF,1:NFLEVG)= ZVS(KST:KPROF,1:NFLEVG,1)
  ZZF0(KST:KPROF,1:NFLEVG)= ZZS(KST:KPROF,1:NFLEVG,1)
  ZWF0(KST:KPROF,1:NFLEVG)= ZWS(KST:KPROF,1:NFLEVG,1)
  ZUS(KST:KPROF,1:NFLEVG,3)=ZUS(KST:KPROF,1:NFLEVG,3)-ZUS(KST:KPROF,1:NFLEVG,1)
  ZVS(KST:KPROF,1:NFLEVG,3)=ZVS(KST:KPROF,1:NFLEVG,3)-ZVS(KST:KPROF,1:NFLEVG,1)
  ZZS(KST:KPROF,1:NFLEVG,3)=ZZS(KST:KPROF,1:NFLEVG,3)-ZZS(KST:KPROF,1:NFLEVG,1)
  ZWS(KST:KPROF,1:NFLEVG,3)=ZWS(KST:KPROF,1:NFLEVG,3)-ZWS(KST:KPROF,1:NFLEVG,1)
  ZUS(KST:KPROF,1:NFLEVG,1)=0._JPRB
  ZVS(KST:KPROF,1:NFLEVG,1)=0._JPRB
  ZZS(KST:KPROF,1:NFLEVG,1)=0._JPRB
  ZWS(KST:KPROF,1:NFLEVG,1)=0._JPRB

  IF (JITER == 2) THEN
    ! Stage 2 (middle point, t+0.5dt)
    ! Averaging wind by SETTLS to arrival point in Cartesian space
    ZUF0(KST:KPROF,1:NFLEVG)=ZUF0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZUS(KST:KPROF,1:NFLEVG,2)
    ZVF0(KST:KPROF,1:NFLEVG)=ZVF0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZVS(KST:KPROF,1:NFLEVG,2)
    ZZF0(KST:KPROF,1:NFLEVG)=ZZF0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZZS(KST:KPROF,1:NFLEVG,2)
    ZWF0(KST:KPROF,1:NFLEVG)=ZWF0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZWS(KST:KPROF,1:NFLEVG,2)
    ZU0(KST:KPROF,1:NFLEVG) = ZU0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZUS(KST:KPROF,1:NFLEVG,2)
    ZV0(KST:KPROF,1:NFLEVG) = ZV0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZVS(KST:KPROF,1:NFLEVG,2)
    ZZ0(KST:KPROF,1:NFLEVG) = ZZ0(KST:KPROF,1:NFLEVG) + 0.5_JPRB*ZZS(KST:KPROF,1:NFLEVG,2)
    PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1)=PB2(KST:KPROF,MSLB2WRL:MSLB2WRL+NFLEVG-1) &
     &                                                + 0.5_JPRB*ZWS(KST:KPROF,1:NFLEVG,2)
    ZUS(KST:KPROF,1:NFLEVG,2)=0._JPRB
    ZVS(KST:KPROF,1:NFLEVG,2)=0._JPRB
    ZZS(KST:KPROF,1:NFLEVG,2)=0._JPRB
    ZWS(KST:KPROF,1:NFLEVG,2)=0._JPRB
  ENDIF

  ! Second interpolation to complete extrapolated wind by SETTLS
  CALL LARCINAAD(YDGEOMETRY,YDML_DYN,IINC,KSTGLO,KST,KPROF,YDSL,&
   & KSTABUF,LLSLHD,LLSLHDQUAD,LLSLHD_OLD,ITIP,IROT,.FALSE.,LLOCK_UP,PLSDEPI,&
   & KIBL,KDEP(1,1,JITER,1),KNOWENO(1,1,JITER,1),&
   & PSCO,PLEV,PSCO5(1,1,1,JITER-1,3),PLEV5(1,1,JITER-1,3),&
   & ZUN_KAPPA,ZUN_KAPPA5,ZUN_KAPPAT,ZUN_KAPPAT5,&
   & PB1(1,MSLB1UR0),PB1(1,MSLB1VR0),PB1(1,MSLB1ZR0),PB1(1,MSLB1WR0),&
   & PB15(1,MSLB1UR05),PB15(1,MSLB1VR05),PB15(1,MSLB1ZR05),PB15(1,MSLB1WR05),&
   & NVSEPC,NVSEPL,KLOCK,&
   & PINC(1,1,3*(JITER-1)-1),KINC(1,1,3*(JITER-1)-1),KDIM1,KDIM2,&
   & PCCO,ZUF0,ZVF0,ZZF0,ZWF0,PCCO5(1,1,1,JITER,1),&
   & KL0(1,1,0,JITER,1),ZLSCAW,ZRSCAW,PLSCAW5(1,1,1,JITER,1),PRSCAW5(1,1,1,JITER,1))

ENDDO  ! end of JITER

! Preparatory stage setting values for 1-th iteration
JITER=1

LLOCK_UP=.true.   ! last call of larcinaad

! Extra interpolation for t+dt extrapolated wind.
! Used for stages 1 (half) and 3 (full)

CALL LARCINAAD(YDGEOMETRY,YDML_DYN,IINC,KSTGLO,KST,KPROF,YDSL,&
 & KSTABUF,LLSLHD,LLSLHDQUAD,LLSLHD_OLD,ITIP,IROT,.FALSE.,LLOCK_UP,PLSDEPI,&
 & KIBL,KDEP(1,1,JITER,3),KNOWENO(1,1,JITER,3),&
 & PSCO,PLEV,PSCO5(1,1,1,JITER,3),PLEV5(1,1,JITER,3),&
 & ZUN_KAPPA,ZUN_KAPPA5,ZUN_KAPPAT,ZUN_KAPPAT5,&
 & PB1(1,MSLB1UR00),PB1(1,MSLB1VR00),PB1(1,MSLB1ZR00),PB1(1,MSLB1WR00),&
 & PB15(1,MSLB1UR005),PB15(1,MSLB1VR005),PB15(1,MSLB1ZR005),PB15(1,MSLB1WR005),&
 & NVSEPC,NVSEPL,KLOCK,&
 & PINC(1,1,1),KINC(1,1,1),KDIM1,KDIM2,&
 & PCCO,ZUS(:,:,3),ZVS(:,:,3),ZZS(:,:,3),ZWS(:,:,3),PCCO5(1,1,1,JITER,3),&
 & KL0(1,1,0,JITER,3),ZLSCAW,ZRSCAW,PLSCAW5(1,1,1,JITER,3),PRSCAW5(1,1,1,JITER,3),LDREP=.TRUE.) ! 00 buffers are used


DO JLEV=1,NFLEVG
  DO JROF=KST,KPROF

    ! * computations on horizontal plans.

    ! Trajectory
    ZPU5=PB2(JROF,MSLB2URL5+JLEV-1)
    ZPV5=PB2(JROF,MSLB2VRL5+JLEV-1)

    ZNOR25=( ZPU5*ZPU5 + ZPV5*ZPV5 )
    ZSPHSV5=ZDTSA*(1.0_JPRB-ZDTS62*ZNOR25)

    ! Adjoint code
    ZPU= -YDGSGEOM%GNORDM(JROF)*ZSPHSV5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
    ZPV= -YDGSGEOM%GNORDL(JROF)*ZSPHSV5*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
    ZSPHSV=-( ZPU5*YDGSGEOM%GNORDM(JROF) -ZPV5*YDGSGEOM%GNORDL(JROF) ) &
     &        *PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)
    PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINCO)=0._JPRB
    ZINT = YDGSGEOM%GEMU(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO) &
     &   -YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
    PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI) = PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI) &
     & + YDGSGEOM%GSQM2(JROF)*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO)  &
     & + YDGSGEOM%GEMU(JROF)* PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)
    PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_SINLA)= 0._JPRB
    PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COSCO)= 0._JPRB
    ZPV = ZPV +YDGSGEOM%GNORDM(JROF)*ZSPHSV5*ZINT
    ZPU = ZPU +YDGSGEOM%GNORDL(JROF)*ZSPHSV5*ZINT
    ZSPHSV = ZSPHSV + (ZPU5*YDGSGEOM%GNORDL(JROF)+ZPV5*YDGSGEOM%GNORDM(JROF))*ZINT
    !ZINT=0._JPRB
    ZNOR2=-ZDTSA*ZDTS62*ZSPHSV -ZDTS22*PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)
    !ZSPHSV=0._JPRB
    PSCO(JROF,JLEV,YDML_DYN%YYTSCO%M_COPHI)=0._JPRB
    ZPU = ZPU + 2.0_JPRB*ZPU5*ZNOR2
    ZPV = ZPV + 2.0_JPRB*ZPV5*ZNOR2
    
    PB2(JROF,MSLB2URL+JLEV-1) = PB2(JROF,MSLB2URL+JLEV-1) + ZPU
    PB2(JROF,MSLB2VRL+JLEV-1) = PB2(JROF,MSLB2VRL+JLEV-1) + ZPV
    !ZPU = 0._JPRB
    !ZPV = 0._JPRB

    ! Arrival point wind converted to Cartesian space
    ZPU0 = -YDGSGEOM%GESLO(JROF)*ZU0(JROF,JLEV) +YDGSGEOM%GECLO(JROF)*ZV0(JROF,JLEV)
    ZPV0 = -YDGSGEOM%GECLO(JROF)*YDGSGEOM%GEMU(JROF)*ZU0(JROF,JLEV) &
     &     -YDGSGEOM%GESLO(JROF)*YDGSGEOM%GEMU(JROF)*ZV0(JROF,JLEV) &
     &     +YDGSGEOM%GSQM2(JROF)*ZZ0(JROF,JLEV)
    !ZU0(JROF,JLEV)=0._JPRB
    !ZV0(JROF,JLEV)=0._JPRB
    !ZZ0(JROF,JLEV)=0._JPRB

    ! convert arrival point wind to Cartesian space for later averaging
    ! Arrival point wind
    PB2(JROF,MSLB2URL+JLEV-1) = PB2(JROF,MSLB2URL+JLEV-1) &
     & + YDGSGEOM%GNORDM(JROF)*ZPU0 + YDGSGEOM%GNORDL(JROF)*ZPV0
    PB2(JROF,MSLB2VRL+JLEV-1) = PB2(JROF,MSLB2VRL+JLEV-1) &
     & - YDGSGEOM%GNORDL(JROF)*ZPU0 + YDGSGEOM%GNORDM(JROF)*ZPV0
    !ZPU0= 0._JPRB
    !ZPV0= 0._JPRB


    ! * computations on a vertical.
    ZLEVO=PLEV(JROF,JLEV)
    PLEV(JROF,JLEV)=0._JPRB
    ZLEVO5=PLEV5(JROF,JLEV,JITER,3)
    IF (ZLEVO5 <= ZVETAON) THEN
      ZLEVO=0.0_JPRB
    ELSEIF (ZLEVO5 >= ZVETAOX) THEN
      ZLEVO=0.0_JPRB
    ENDIF
    ZPW0=-RTDT*ZLEVO
    !ZLEVO=0._JPRB
    PB2(JROF,MSLB2WRL+JLEV-1) = PB2(JROF,MSLB2WRL+JLEV-1) + ZPW0
    !ZPW0=0._JPRB

  ENDDO
ENDDO


!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('LARMES_RK_AD',1,ZHOOK_HANDLE)
END SUBROUTINE LARMES_RK_AD
