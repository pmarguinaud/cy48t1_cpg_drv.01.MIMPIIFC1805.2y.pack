SUBROUTINE LAVABO(YDDYNA, YDGEOMETRY,YGFL,YDDYN,YDPTRSLB1,KST,KPROF,LD2TLFF1,LDSLPHY,PB1)

!**** *LAVABO*   Semi-Lagrangian scheme.
!                VAlues at the BOundaries: Upper and lower extrapolations.

!     Purpose.
!     --------
!          This subroutine do upper and lower extrapolations of
!          semi-lagrangian quantities for extra-levels JLEV=0
!          and JLEV=NFLEVG+1.
!          Abbreviation "vwv" stands for "vertical wind variable".

!**   Interface.
!     ----------
!        *CALL* *LAVABO(......)

!        Explicit arguments :
!        --------------------

!        INPUT:
!          KST      : first element of work.
!          KPROF    : depth of work.
!          LD2TLFF1 : .T./.F.: Refined treatement of (2*Omega Vec r) at
!                    the origin point when there is t-dt (or t in SL2TL)
!                    physics / Other cases.

!        INPUT/OUTPUT:
!          PB1      : "SLBUF1" buffer for interpolations.

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!         None
!         Called by LACDYN.

!     Reference.
!     ----------
!         Arpege documentation Part I  Chapter 3
!                              Part II Chapter 5 and 6
!         Documentation about semi-Lagrangian scheme.

!     Author.
!     -------
!      K. YESSAD (METEO FRANCE/CNRM/GMAP) after routine
!        CPLGDY1 coded by Maurice IMBARD.
!      Original : APRIL 1992.

! Modifications
! -------------
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad Aug 2008: rationalisation of dummy argument interfaces
!   F. Vana   15-Oct-2009 : option NSPLTHOI
!   K. Yessad (Nov 2009): prune lpc_old.
!   F. Vana   15-Oct-2009 : diffusion of phys. tendencies (NSPLTHOI=1)
!   K. Yessad (July 2014): Rename some variables.
!   K. Yessad (Dec 2016): Prune obsolete options.
!   K. Yessad (June 2017): Introduce NHQE model.
!   F. Vana    21-Nov-2017: Option LSLDP_CURV + bugfix on SL physics
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!   F. Vana July 2018: RK4 scheme for trajectory research.
! End Modifications
!------------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMCT0       , ONLY : LNHDYN, LNHEE
USE YOMDYNA      , ONLY : TDYNA
USE YOMDYN       , ONLY : TDYN
USE PTRSLB1      , ONLY : TPTRSLB1
USE YOM_YGFL     , ONLY : TYPE_GFLD

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE (TDYNA), INTENT (IN) :: YDDYNA
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(IN)    :: YDDYN
TYPE(TPTRSLB1)    ,INTENT(IN)    :: YDPTRSLB1
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
LOGICAL           ,INTENT(IN)    :: LD2TLFF1 
LOGICAL           ,INTENT(IN)    :: LDSLPHY
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB1(YDGEOMETRY%YRDIM%NPROMA,YDPTRSLB1%NFLDSLB1)

#include "lavabo_laitvspcqm_part1.intfb.h"
#include "lavabo_laitvspcqm_part2.intfb.h"
#include "lavabo_lphy.intfb.h"

INTEGER (KIND=JPIM) :: JLEV1, JLEV2

!     ------------------------------------------------------------------

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('LAVABO',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NUMFLDS=>YGFL%NUMFLDS, YCOMP=>YGFL%YCOMP, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA, &
 & LSPLTHOIGFL=>YDDYN%LSPLTHOIGFL, NSPDLAG=>YDDYN%NSPDLAG, &
 & NSPLTHOI=>YDDYN%NSPLTHOI, NSVDLAG=>YDDYN%NSVDLAG, NTLAG=>YDDYN%NTLAG, &
 & NVLAG=>YDDYN%NVLAG, NWLAG=>YDDYN%NWLAG, LSLDP_CURV=>YDDYN%LSLDP_CURV, &
 & LSLDP_RK=>YDDYN%LSLDP_RK, &
 & MSLB1C9=>YDPTRSLB1%MSLB1C9, MSLB1C9_SI=>YDPTRSLB1%MSLB1C9_SI, &
 & MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9, MSLB1GFLF9=>YDPTRSLB1%MSLB1GFLF9, &
 & MSLB1GFLP9=>YDPTRSLB1%MSLB1GFLP9, MSLB1NHX9=>YDPTRSLB1%MSLB1NHX9, &
 & MSLB1PD0=>YDPTRSLB1%MSLB1PD0, MSLB1PD9=>YDPTRSLB1%MSLB1PD9, &
 & MSLB1PD9_SI=>YDPTRSLB1%MSLB1PD9_SI, &
 & MSLB1T0=>YDPTRSLB1%MSLB1T0, MSLB1T9=>YDPTRSLB1%MSLB1T9, &
 & MSLB1T9_SI=>YDPTRSLB1%MSLB1T9_SI, MSLB1TF9=>YDPTRSLB1%MSLB1TF9, &
 & MSLB1TP9=>YDPTRSLB1%MSLB1TP9, MSLB1U0=>YDPTRSLB1%MSLB1U0, &
 & MSLB1U9=>YDPTRSLB1%MSLB1U9, MSLB1U9_SI=>YDPTRSLB1%MSLB1U9_SI, &
 & MSLB1UF9=>YDPTRSLB1%MSLB1UF9, MSLB1UP9=>YDPTRSLB1%MSLB1UP9, &
 & MSLB1UR0=>YDPTRSLB1%MSLB1UR0, MSLB1UR9=>YDPTRSLB1%MSLB1UR9, &
 & MSLB1V0=>YDPTRSLB1%MSLB1V0, MSLB1V9=>YDPTRSLB1%MSLB1V9, &
 & MSLB1V9_SI=>YDPTRSLB1%MSLB1V9_SI, MSLB1VD0=>YDPTRSLB1%MSLB1VD0, &
 & MSLB1VD9=>YDPTRSLB1%MSLB1VD9, MSLB1VD9_SI=>YDPTRSLB1%MSLB1VD9_SI, &
 & MSLB1VDF9=>YDPTRSLB1%MSLB1VDF9, MSLB1VF9=>YDPTRSLB1%MSLB1VF9, &
 & MSLB1VP9=>YDPTRSLB1%MSLB1VP9, MSLB1VR0=>YDPTRSLB1%MSLB1VR0, &
 & MSLB1VR9=>YDPTRSLB1%MSLB1VR9, MSLB1WR0=>YDPTRSLB1%MSLB1WR0, &
 & MSLB1ZR0=>YDPTRSLB1%MSLB1ZR0, NFLDSLB1=>YDPTRSLB1%NFLDSLB1, &
 & MSLB1UR00=>YDPTRSLB1%MSLB1UR00, MSLB1VR00=>YDPTRSLB1%MSLB1VR00, &
 & MSLB1ZR00=>YDPTRSLB1%MSLB1ZR00, MSLB1WR00=>YDPTRSLB1%MSLB1WR00)
!     ------------------------------------------------------------------

JLEV1 = 0
JLEV2 = NFLEVG+1

!*       1.    UPPER AND LOWER LATERAL BOUNDARIES CONDITIONS SET TO 0.
!              --------------------------------------------------------

! * P[X]RL0 and P[X]RL9 for [X]=U,V,W:
PB1(KST:KPROF,MSLB1UR0+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1UR0+JLEV2)=0.0_JPRB
PB1(KST:KPROF,MSLB1VR0+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1VR0+JLEV2)=0.0_JPRB
IF (LSLDP_CURV) THEN
  PB1(KST:KPROF,MSLB1ZR0+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1ZR0+JLEV2)=0.0_JPRB
ENDIF
PB1(KST:KPROF,MSLB1WR0+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1WR0+JLEV2)=0.0_JPRB
IF (LSLDP_RK) THEN
  PB1(KST:KPROF,MSLB1UR00+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1UR00+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VR00+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VR00+JLEV2)=0.0_JPRB
  IF (LSLDP_CURV) THEN
    PB1(KST:KPROF,MSLB1ZR00+JLEV1)=0.0_JPRB
    PB1(KST:KPROF,MSLB1ZR00+JLEV2)=0.0_JPRB
  ENDIF
  PB1(KST:KPROF,MSLB1WR00+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1WR00+JLEV2)=0.0_JPRB
ENDIF
IF (LD2TLFF1) THEN
  PB1(KST:KPROF,MSLB1UR9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1UR9+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VR9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VR9+JLEV2)=0.0_JPRB
ENDIF

! * P[X]L0 and P[X]L9 for [X]=U,V,T,advected GFL:
PB1(KST:KPROF,MSLB1U9+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1U9+JLEV2)=0.0_JPRB
PB1(KST:KPROF,MSLB1V9+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1V9+JLEV2)=0.0_JPRB
PB1(KST:KPROF,MSLB1T9+JLEV1)=0.0_JPRB
PB1(KST:KPROF,MSLB1T9+JLEV2)=0.0_JPRB

CALL LAVABO_LAITVSPCQM_PART1 (YDGEOMETRY, YGFL, YDPTRSLB1, KST, KPROF, PB1)

IF (NSPLTHOI /= 0) THEN
  PB1(KST:KPROF,MSLB1UF9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1UF9+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VF9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VF9+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1TF9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1TF9+JLEV2)=0.0_JPRB
ENDIF
IF (NSPLTHOI /= 0.OR.LSPLTHOIGFL) THEN

CALL LAVABO_LAITVSPCQM_PART2 (YDGEOMETRY, YGFL, YDPTRSLB1, KST, KPROF, PB1)

ENDIF

IF (NWLAG /= 2) THEN
  PB1(KST:KPROF,MSLB1U0+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1U0+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1V0+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1V0+JLEV2)=0.0_JPRB
ENDIF
IF (NTLAG /= 2) THEN
  PB1(KST:KPROF,MSLB1T0+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1T0+JLEV2)=0.0_JPRB
ENDIF

! * P[X]L0 and P[X]L9 for [X]=SPD:
IF (LNHEE) THEN
  PB1(KST:KPROF,MSLB1PD9+JLEV1) = 0.0_JPRB
  PB1(KST:KPROF,MSLB1PD9+JLEV2) = 0.0_JPRB
  IF (NSPDLAG /= 2) THEN
    PB1(KST:KPROF,MSLB1PD0+JLEV1) = 0.0_JPRB
    PB1(KST:KPROF,MSLB1PD0+JLEV2) = 0.0_JPRB
  ENDIF
ENDIF

! * P[X]L0 and P[X]L9 for [X]=SVD,NHX:
IF (LNHDYN) THEN
  PB1(KST:KPROF,MSLB1VD9+JLEV1) = 0.0_JPRB
  PB1(KST:KPROF,MSLB1VD9+JLEV2) = 0.0_JPRB
  IF (NSPLTHOI /= 0) THEN
    PB1(KST:KPROF,MSLB1VDF9+JLEV1) = 0.0_JPRB
    PB1(KST:KPROF,MSLB1VDF9+JLEV2) = 0.0_JPRB
  ENDIF
  IF (YDDYNA%NVDVAR == 4 .OR. YDDYNA%NVDVAR ==5) THEN
    PB1(KST:KPROF,MSLB1NHX9+JLEV1) = 0.0_JPRB
    PB1(KST:KPROF,MSLB1NHX9+JLEV2) = 0.0_JPRB
  ENDIF
  IF (NSVDLAG /= 2) THEN
    PB1(KST:KPROF,MSLB1VD0+JLEV1) = 0.0_JPRB
    PB1(KST:KPROF,MSLB1VD0+JLEV2) = 0.0_JPRB
  ENDIF
ENDIF

! * P[X]L0 and P[X]L9 for [X]=C (continuity equation):
IF (NVLAG == 2.OR.NVLAG == 3) THEN
  PB1(KST:KPROF,MSLB1C9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1C9+JLEV2)=0.0_JPRB
  IF (YDDYNA%LSLINLC2) PB1(KST:KPROF,MSLB1C9_SI+JLEV1)=0.0_JPRB
  IF (YDDYNA%LSLINLC2) PB1(KST:KPROF,MSLB1C9_SI+JLEV2)=0.0_JPRB
ENDIF

! * P[X]LP9 for [X]=U,V,T and GFL:
IF( LDSLPHY ) THEN
  ! ky: is it JGFL or YCOMP(JGFL)%MP_SL1 in ISLB1GFLP9?
  ! fv: Yes, it should be JGFL (see the corresponding code in LARCINB). 
  PB1(KST:KPROF,MSLB1UP9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1UP9+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VP9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VP9+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1TP9+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1TP9+JLEV2)=0.0_JPRB

CALL LAVABO_LPHY (YDGEOMETRY, YGFL, YDPTRSLB1, KST, KPROF, PB1)

ENDIF

! * P[X]L9_SI (GMV equations):
IF (YDDYNA%LSLINL) THEN
  PB1(KST:KPROF,MSLB1U9_SI+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1U9_SI+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1V9_SI+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1V9_SI+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1T9_SI+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1T9_SI+JLEV2)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VD9_SI+JLEV1)=0.0_JPRB
  PB1(KST:KPROF,MSLB1VD9_SI+JLEV2)=0.0_JPRB
  IF (LNHEE) THEN
    PB1(KST:KPROF,MSLB1PD9_SI+JLEV1)=0.0_JPRB
    PB1(KST:KPROF,MSLB1PD9_SI+JLEV2)=0.0_JPRB
  ENDIF
ENDIF 

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('LAVABO',1,ZHOOK_HANDLE)
END SUBROUTINE LAVABO

