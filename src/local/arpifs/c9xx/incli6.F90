SUBROUTINE INCLI6(YDGEOMETRY,YDSURF,YDPHY,YDPHY1)

!**** *INCLI6*

!     PURPOSE.
!     --------

!     This routine calculates :
!    -climatological sea surface temperature
!    -climatological land surface temperature
!    -climatological mean soil temperature
!    -climatological surface moisture
!    -climatological soil moisture
!    -an "equivalent-water-depth" snow climatology

!**   INTERFACE.
!     ----------

!     CALL INCLI6

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------

!     GEO923
!     INTER6
!     INTER8
!     INTER10
!     ACSOLW
!     CHK923
!     FA-LFI package (FAITOU,FACILE,FAIENC,LFILAF,FAIRME)
!     CHIEN
!     COORD_DTS
!     PPV923

!     AUTHORS.
!     --------
!      D. GIARD      SEPTEMBER 1997, from INCLI3, INCLIT and INCLIW

!     MODIFICATIONS.
!     --------------
!      D. Giard  12/11/2001 : roughness lengths depending on sea-ice limit
!                             use of allocatable arrays to save memory
!      R. ElKhatib 03-08-18 : Roughness lengths not packed
!      M.Hamrud 01-Oct-2003 : CY28 Cleaning
!      D. Giard 04-09-15    : update, bugfix for SST and LSOLV=.F.
!      F. Taillefer 05-12-05 : add PPV923 to take the nearest point of the dataset
!                              grid in case of nature conflict for Ts and Tp
!                              (fixed values for Sn and Water)
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      K. Yessad (Jan 2010): externalisation of group EGGX in XRD/IFSAUX
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TCSGLEG
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : NQUAD
USE YOMLUN   , ONLY : NULOUT
USE YOMPHY   , ONLY : TPHY
USE YOMSTA   , ONLY : RDTDZ1
USE YOMCST   , ONLY : RG, RTT
USE YOMVERT  , ONLY : VP00
USE YOMCLI   , ONLY : YRCLI  
USE YOMPHY1  , ONLY : TPHY1

IMPLICIT NONE

! JPBX/JPBY : number of extra longitudes/latitudes on each side of the data
!          grid required by interpolation (4 or 12 points -> 2/2)

TYPE(GEOMETRY),INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)   ,INTENT(INOUT) :: YDSURF
TYPE(TPHY)    ,INTENT(INOUT) :: YDPHY
TYPE(TPHY1)   ,INTENT(INOUT) :: YDPHY1
INTEGER(KIND=JPIM) :: JPBX
INTEGER(KIND=JPIM) :: JPBY

PARAMETER(JPBX=2,JPBY=2)

!  Initial and interpolated datasets
REAL(KIND=JPRB),ALLOCATABLE :: ZLSM(:,:), ZREL(:,:), ZMSK(:,:), ZFLD(:,:)
REAL(KIND=JPRB),ALLOCATABLE :: ZAUX1(:), ZAUX2(:), ZLON(:,:), ZLAT(:,:)
!  Interpolated data
REAL(KIND=JPRB) :: ZCMP(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON,6), ZXMP(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON),&
 &  ZCMS(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON)
!  Work array
REAL(KIND=JPRB) :: ZXMS(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON), ZLSR(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON)
!  ARPEGE fields
REAL(KIND=JPRB) :: ZLS(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON), ZGO(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON)&
 & ,ZTS(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON), ZTP(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON)&
 & ,ZWS(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON), ZWP(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON),&
 & ZSN(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON)  
REAL(KIND=JPRB) ,ALLOCATABLE :: ZIV(:), ZAR(:), ZD2(:) ,ZSA(:)&
 & ,ZAL(:) ,ZEM(:) ,Z0D(:) ,Z0T(:)&
 & ,ZWSA(:) ,ZWFC(:) ,ZWWI(:) ,ZWPX(:) ,ZWSX(:)  
!  Geometry
REAL(KIND=JPRB) :: ZBO(YDGEOMETRY%YRDIM%NDGLG+1),ZLONG(YDGEOMETRY%YRDIM%NDGLG),&
 & ZMU(YDGEOMETRY%YRDIM%NDGLG*YDGEOMETRY%YRDIM%NDLON),ZSLA(YDGEOMETRY%YRDIM%NDGLG*YRCLI%NPINT)&
 & ,ZSLO(YDGEOMETRY%YRDIM%NDLON*YRCLI%NPINT,YDGEOMETRY%YRDIM%NDGLG),ZCLO(YDGEOMETRY%YRDIM%NDLON*YRCLI%NPINT,YDGEOMETRY%YRDIM%NDGLG)  
!  Local variables
 REAL(KIND=JPRB) :: ZEPS, ZGST, ZNEI4, ZNEI5, ZTR
 REAL(KIND=JPRB) :: ZHOOK_HANDLE

CHARACTER :: CLNOMC*16,CLNOMF*16,CLFORM*12

INTEGER(KIND=JPIM) :: IARI, IARP, ICO, IDATX, IDATY, IINF, ILENE,&
 & ILENT, IMES, INIQ, INIV, INJQ, INUM, IOS, IREP, IZ, J, JJ  
INTEGER(KIND=JPIM) :: INGRIG,INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL
INTEGER(KIND=JPIM) :: INDMIN(2)

LOGICAL :: LLCOSP, LLHMT, LLIMST, LLOP, LLPOLE, LLRE, LLSOL,&
 & LLSN, LLST, LLTM, LLTP, LLTS, LLWP, LLWS  

!     ------------------------------------------------------------------

#include "chien.h"

#include "abor1.intfb.h"
#include "acsolw.intfb.h"
#include "chk923.intfb.h"
#include "geo923.intfb.h"
#include "inter10.intfb.h"
#include "inter6.intfb.h"
#include "inter8.intfb.h"
#include "ppv923.intfb.h"
#include "coord_dts.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('INCLI6',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,  &
 & YDCSGLEG=>YDGEOMETRY%YRCSGLEG, YDSTA=>YDGEOMETRY%YRSTA, YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA,  &
 & YDVFE=>YDGEOMETRY%YRVFE, YDLAP=>YDGEOMETRY%YRLAP, YDVSPLIP=>YDGEOMETRY%YRVSPLIP,  &
 & YDVSLETA=>YDGEOMETRY%YRVSLETA, YDHSLMER=>YDGEOMETRY%YRHSLMER, YDCSGEOM=>YDGEOMETRY%YRCSGEOM, &
  & YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB, YDGSGEOM=>YDGEOMETRY%YRGSGEOM, YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB,  &
  & YDSPGEOM=>YDGEOMETRY%YSPGEOM)
ASSOCIATE(TMERGL=>YDPHY1%TMERGL, LSOLV=>YDPHY%LSOLV, &
 & NDGENG=>YDDIM%NDGENG, NDGLG=>YDDIM%NDGLG, NDGSAG=>YDDIM%NDGSAG, &
 & NDLON=>YDDIM%NDLON, NSMAX=>YDDIM%NSMAX, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & NHTYP=>YDGEM%NHTYP, NLOENG=>YDGEM%NLOENG, NMENG=>YDGEM%NMENG, &
 & NSTTYP=>YDGEM%NSTTYP, RLOCEN=>YDGEM%RLOCEN, RMUCEN=>YDGEM%RMUCEN, &
 & RSTRET=>YDGEM%RSTRET, &
 & YSP_SBD=>YDSURF%YSP_SBD)
!     ------------------------------------------------------------------

!     1. SET INITIAL VALUES.
!        -------------------

!     1.1 Constants

ZEPS=1.E-6_JPRB
!  Maximum non-permanent snow value.
ZNEI4=1000._JPRB
!  Permanent snow value on ice cap.
ZNEI5=10000._JPRB
!  Vertical temperature gradient
ZGST=RDTDZ1/RG
!  Threshold for temperature in presence of snow
ZTR=RTT+YRCLI%RSTR

!     1.2 Interpolation

ICO=YRCLI%NPINT*YRCLI%NPINT
INJQ=NDGLG*YRCLI%NPINT
INIQ=NDLON*YRCLI%NPINT
ILENT=NDGLG*NDLON
IDATY=YRCLI%NDATY+2*JPBY
IDATX=YRCLI%NDATX+2*JPBX

!     1.3 ARPEGE and data files

INUM=3
IMES=1
IARP=31
IREP=0
IARI=0
CLNOMF='Const.Clim'
CLNOMC='Const.Clim.Surfa'
LLIMST=.TRUE.

IINF=-1
LLPOLE=.TRUE.
LLCOSP=.FALSE.

IF (YRCLI%LIEEE) THEN
  CLFORM='UNFORMATTED'
ELSE
  CLFORM='FORMATTED'
ENDIF

!     1.4 Geometry

ILENE=0
CALL GEO923(YDGEOMETRY,YRCLI%NPINT,ILENE,ZBO,ZLONG,ZMU,ZSLA,ZSLO,ZCLO)

!     ------------------------------------------------------------------

!     2. CHECK REQUIRED FIELDS.
!        ----------------------

!     2.1 Check new datasets

!  Land-Sea mask
LLTM=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='itm_GL',IOSTAT=IOS,EXIST=LLTM,OPENED=LLOP)
LLTM= LLTM .AND. (IOS == 0) .AND. .NOT.LLOP
!  Orography*g
LLRE=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='rel_GL',IOSTAT=IOS,EXIST=LLRE,OPENED=LLOP)
LLRE= LLRE .AND. (IOS == 0) .AND. .NOT.LLOP
!  Sea Surface Temperature
LLST=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='sst_GL',IOSTAT=IOS,EXIST=LLST,OPENED=LLOP)
LLST= LLST .AND. (IOS == 0) .AND. .NOT.LLOP
!  Land Surface Temperature
LLTS=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='tsl_GL',IOSTAT=IOS,EXIST=LLTS,OPENED=LLOP)
LLTS= LLTS .AND. (IOS == 0) .AND. .NOT.LLOP
!  Mean Land Surface Temperature
LLTP=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='tpl_GL',IOSTAT=IOS,EXIST=LLTP,OPENED=LLOP)
LLTP= LLTP .AND. (IOS == 0) .AND. .NOT.LLOP
!  Superficial soil moisture
LLWS=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='wsl_GL',IOSTAT=IOS,EXIST=LLWS,OPENED=LLOP)
LLWS= LLWS .AND. (IOS == 0) .AND. .NOT.LLOP
!  Mean soil moisture
LLWP=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='wpl_GL',IOSTAT=IOS,EXIST=LLWP,OPENED=LLOP)
LLWP= LLWP .AND. (IOS == 0) .AND. .NOT.LLOP
!  Snow cover
LLSN=.FALSE.
LLOP=.FALSE.
IOS= 0
INQUIRE(FILE='snl_GL',IOSTAT=IOS,EXIST=LLSN,OPENED=LLOP)
LLSN= LLSN .AND. (IOS == 0) .AND. .NOT.LLOP

!     2.2 Control and print

IF (LLST) THEN
  IF (.NOT.LLTM) CALL ABOR1(' INCLI6 : LAND-SEA MASK MISSING !')
ELSE
  LLTM=.FALSE.
ENDIF
IF (LLTS.OR.LLTP) THEN
  IF (.NOT.LLRE) CALL ABOR1(' INCLI6 : OROGRAPHY MISSING !')
ELSE
  LLRE=.FALSE.
ENDIF
IF (.NOT.LSOLV) THEN
  WRITE(NULOUT,'('' ONLY SST CAN BE MODIFIED'')')
  LLTS=.FALSE.
  LLTP=.FALSE.
  LLWS=.FALSE.
  LLWP=.FALSE.
  LLSN=.FALSE.
ENDIF
IF (LLTP.AND.YSP_SBD%NLEVS > 1) THEN
  WRITE(NULOUT,'('' SEVERAL TEMP. LAYERS IN SOIL : NOT CODED'')')
  LLTP=.FALSE.
ENDIF
IF (.NOT.(LLST.OR.LLTS.OR.LLTP.OR.LLWS.OR.LLWP.OR.LLSN))&
 & CALL ABOR1(' INCLI6 : NEW SURFACE FIELDS MISSING !')  
LLSOL=LLTS.OR.LLTP.OR.LLWS.OR.LLWP.OR.LLSN

WRITE(NULOUT,'('' NEW FIELDS TO BE USED BY INCLI6 :'')')
IF (LLST) WRITE(NULOUT,'('' SEA SURFACE TEMPERATURE'')')
IF (LLTS) WRITE(NULOUT,'('' LAND SURFACE TEMPERATURE'')')
IF (LLTP) WRITE(NULOUT,'('' MEAN LAND SURFACE TEMPERATURE'')')
IF (LLWS) WRITE(NULOUT,'('' (RELATIVE) SURFACE MOISTURE'')')
IF (LLWP) WRITE(NULOUT,'('' (RELATIVE) MEAN SOIL MOISTURE'')')
IF (LLSN) WRITE(NULOUT,'('' SNOW COVER'')')
IF (LLTS.AND..NOT.LLTP)&
 & WRITE(NULOUT,'('' CAUTION : ONLY Ts IS AVAILABLE !'')')  
IF (LLTP.AND..NOT.LLTS)&
 & WRITE(NULOUT,'('' CAUTION : ONLY Tp IS AVAILABLE -> Ts=Tp'')')  
IF (LLWS.AND..NOT.LLWP)&
 & WRITE(NULOUT,'('' CAUTION : ONLY ws IS AVAILABLE !'')')  
IF (LLWP.AND..NOT.LLWS)&
 & WRITE(NULOUT,'('' CAUTION : ONLY wp IS AVAILABLE -> ws=wp'')')  

WRITE(NULOUT,'(A,F6.2,A,F3.2)')' CONTROL FOR SNOW IF Ts,Tp > ',ZTR,' CONTROL FOR SNOW IF Ts,Tp > ',YRCLI%RSWR  
WRITE(NULOUT,'(A)')' 12/4 POINTS INTERPOLATION FOR SST , WITH LAND-SEA MASK'  
WRITE(NULOUT,'(A)')' 4 POINTS INTERPOLATION FOR LAND VARIABLES' 

!     2.3 Open ARPEGE fields

CALL FAITOU(IREP,INUM,.TRUE.,CLNOMF,'OLD',.TRUE.,LLIMST,&
 & IMES,IARP,IARI,CLNOMC)  
CALL CHIEN(CLNOMC,NSTTYP,RMUCEN,RLOCEN,RSTRET,NSMAX,&
 & NDGLG,NDLON,NLOENG,NMENG,NHTYP,NFLEVG,VP00,YDVAB%VALH,YDVAB%VBH,&
 & NQUAD,IINF,NDGSAG,NDGENG,ZEPS,LLPOLE,NULOUT)  
IF (LLPOLE) CALL ABOR1(' CLIM. FILES MUST NOT HAVE POLES !')

!     2.4 Read ARPEGE fields - surface constants

INIV=0
!  Land-sea mask
CALL FACILE(IREP,INUM,'SURF',INIV,'IND.TERREMER',ZLS,LLCOSP)
DO J=1,ILENE
  ZLSR(J)=1.0_JPRB-ZLS(J)
ENDDO
!  Orography*g (if required for temperature)
IF (LLRE.OR.LLST) THEN
  CALL FACILE(IREP,INUM,'SURF',INIV,'GEOPOTENTIEL',ZGO,LLCOSP)
ENDIF
!  Land-use type and soil characteristics (for controls on land)
IF (LLSOL) THEN
  ALLOCATE ( ZIV(ILENT) )
  ALLOCATE ( ZAR(ILENT) )
  ALLOCATE ( ZSA(ILENT) )
  ALLOCATE ( ZD2(ILENT) )
  CALL FACILE(IREP,INUM,'SURF',INIV,'IND.VEG.DOMI',ZIV,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'PROP.ARGILE ',ZAR,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'PROP.SABLE  ',ZSA,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'EPAIS.SOL   ',ZD2,LLCOSP)
ENDIF
!  Albedo, emissivity, roughness (to be modified according to SST)
IF (LLST.AND.LSOLV) THEN
  ALLOCATE ( ZAL(ILENT) )
  ALLOCATE ( ZEM(ILENT) )
  ALLOCATE ( Z0D(ILENT) )
  ALLOCATE ( Z0T(ILENT) )
  CALL FACILE(IREP,INUM,'SURF',INIV,'ALBEDO      ',ZAL,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'EMISSIVITE  ',ZEM,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'Z0.FOIS.G   ',Z0D,LLCOSP)
  CALL FACILE(IREP,INUM,'SURF',INIV,'GZ0.THERM   ',Z0T,LLCOSP)
ENDIF

!     2.5 Read ARPEGE fields - climatology

INIV=1
CALL FACILE(IREP,INUM,'SURF',INIV,'TEMPERATURE ',ZTS,LLCOSP)
CALL FACILE(IREP,INUM,'PROF',INIV,'TEMPERATURE ',ZTP,LLCOSP)
CALL FACILE(IREP,INUM,'SURF',INIV,'PROP.RMAX.EA',ZWS,LLCOSP)
CALL FACILE(IREP,INUM,'PROF',INIV,'PROP.RMAX.EA',ZWP,LLCOSP)
CALL FACILE(IREP,INUM,'SURF',INIV,'RESERV.NEIGE',ZSN,LLCOSP)

!     2.6 Calculate latitudes and longitudes of the initial grid

ALLOCATE(ZLON(YRCLI%NDATX,YRCLI%NDATY))
ALLOCATE(ZLAT(YRCLI%NDATX,YRCLI%NDATY))
CALL COORD_DTS(YRCLI%NDATX,YRCLI%NDATY,ZLON,ZLAT)

!     ------------------------------------------------------------------

!     3. READ AND INTERPOLATE NEW FIELDS.
!        --------------------------------

!     3.1 Land Sea Mask and Sea Surface Temperature

310 CONTINUE
IF (.NOT.LLST) GO TO 320

ALLOCATE ( ZLSM(IDATX,IDATY) )
ALLOCATE ( ZFLD(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
  ALLOCATE ( ZAUX2(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=11,FILE='itm_GL',FORM=CLFORM)
OPEN(UNIT=13,FILE='sst_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(11) ZAUX1
  READ(13) ZAUX2
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZLSM(J,JJ)=ZAUX1(IZ)
      ZFLD(J,JJ)=ZAUX2(IZ)
    ENDDO
  ENDDO
ELSE
  READ(11,*)((ZLSM(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
  READ(13,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(11)
CLOSE(13)

!  Interpolate temperature with land-sea mask (12/4 points)

CALL INTER6(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,1),ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZLSM,ZLS(1),YRCLI%SMASK)  

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZLSM )
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
  DEALLOCATE ( ZAUX2 )
ENDIF

!  Move to model height

DO J=1,ILENE
  ZCMP(J,1)= ZCMP(J,1) + ZGST*ZGO(J)
ENDDO

!     3.2 g*Orography

320 CONTINUE
IF (.NOT.LLRE) GO TO 330

ALLOCATE ( ZREL(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=12,FILE='rel_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(12) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZREL(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(12,*)((ZREL(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(12)

IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!     3.3 Land Surface Temperature

330 CONTINUE
IF (.NOT.LLTS) GO TO 340

ALLOCATE ( ZFLD(IDATX,IDATY) )
ALLOCATE ( ZMSK(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=14,FILE='tsl_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(14) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZFLD(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(14,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(14)

!  Define mask then reduce to sea level before interpolation

DO JJ=JPBY+1,JPBY+YRCLI%NDATY
  DO J=JPBX+1,JPBX+YRCLI%NDATX
    IF (ZFLD(J,JJ) <= YRCLI%SMANQ) THEN
      ZMSK(J,JJ)= 1.0_JPRB
    ELSE
      ZMSK(J,JJ)= 0.0_JPRB
      ZFLD(J,JJ)= ZFLD(J,JJ) - ZGST*ZREL(J,JJ)
    ENDIF
  ENDDO
ENDDO

!  Interpolate mask

CALL INTER8(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZXMP,ZMSK,ZSLA,ZSLO,ZCLO)  

!  Interpolate with land-sea mask (4 points)

CALL INTER10(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,2),ZCMS,ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZMSK,ZXMP,ZLSR(1),ZXMS,YRCLI%SMASK)  

DO J=1,ILENE
  IF (ZLS(J) <= YRCLI%SMASK) THEN
    ZCMP(J,2)= ZTS(J)
  ELSE
    IF (ZCMP(J,2) <= YRCLI%SMANQ) THEN
      CALL PPV923(YDGEOMETRY,J,YRCLI%NDATX,YRCLI%NDATY,INDMIN,&
                & ZMSK(1+JPBX:YRCLI%NDATX+JPBX,1+JPBY:YRCLI%NDATY+JPBY),&
                & ZLON,ZLAT)
      ZCMP(J,2)=ZFLD(INDMIN(1)+JPBX,INDMIN(2)+JPBY)
    ENDIF
  ENDIF
ENDDO

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZMSK )
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!  Move to model height

DO J=1,ILENE
  ZCMP(J,2)= ZCMP(J,2) + ZGST*ZGO(J)
ENDDO

!     3.4 Mean Soil Temperature

340 CONTINUE
IF (.NOT.LLTP) GO TO 350

ALLOCATE ( ZFLD(IDATX,IDATY) )
ALLOCATE ( ZMSK(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=15,FILE='tpl_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(15) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZFLD(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(15,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(15)

!  Define mask then reduce to sea level before interpolation

DO JJ=JPBY+1,JPBY+YRCLI%NDATY
  DO J=JPBX+1,JPBX+YRCLI%NDATX
    IF (ZFLD(J,JJ) <= YRCLI%SMANQ) THEN
      ZMSK(J,JJ)= 1.0_JPRB
    ELSE
      ZMSK(J,JJ)= 0.0_JPRB
      ZFLD(J,JJ)= ZFLD(J,JJ) - ZGST*ZREL(J,JJ)
    ENDIF
  ENDDO
ENDDO

!  Interpolate mask

CALL INTER8(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZXMP,ZMSK,ZSLA,ZSLO,ZCLO)  

!  Interpolate with land-sea mask (4 points)

CALL INTER10(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,3),ZCMS,ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZMSK,ZXMP,ZLSR(1),ZXMS,YRCLI%SMASK)  

DO J=1,ILENE
  IF (ZLS(J) <= YRCLI%SMASK) THEN
    ZCMP(J,3)= ZTP(J)
  ELSE
    IF (ZCMP(J,3) <= YRCLI%SMANQ) THEN
      CALL PPV923(YDGEOMETRY,J,YRCLI%NDATX,YRCLI%NDATY,INDMIN,&
                & ZMSK(1+JPBX:YRCLI%NDATX+JPBX,1+JPBY:YRCLI%NDATY+JPBY),&
                & ZLON,ZLAT)
      ZCMP(J,3)=ZFLD(INDMIN(1)+JPBX,INDMIN(2)+JPBY)
    ENDIF
  ENDIF
ENDDO

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZMSK )
DEALLOCATE(ZLON)
DEALLOCATE(ZLAT)
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!  Move to model height

DO J=1,ILENE
  ZCMP(J,3)= ZCMP(J,3) + ZGST*ZGO(J)
ENDDO

!     3.5 Surface moisture

350 CONTINUE
IF (LLRE) DEALLOCATE ( ZREL )
IF (.NOT.LLWS) GO TO 360

ALLOCATE ( ZFLD(IDATX,IDATY) )
ALLOCATE ( ZMSK(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=16,FILE='wsl_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(16) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZFLD(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(16,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(16)

!  Define mask

DO JJ=JPBY+1,JPBY+YRCLI%NDATY
  DO J=JPBX+1,JPBX+YRCLI%NDATX
    IF (ZFLD(J,JJ) <= YRCLI%SMANQ) THEN
      ZMSK(J,JJ)= 1.0_JPRB
    ELSE
      ZMSK(J,JJ)= 0.0_JPRB
    ENDIF
  ENDDO
ENDDO

!  Interpolate mask

CALL INTER8(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZXMP,ZMSK,ZSLA,ZSLO,ZCLO)  

!  Interpolate with land-sea mask (4 points)

CALL INTER10(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,4),ZCMS,ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZMSK,ZXMP,ZLSR(1),ZXMS,YRCLI%SMASK)  

DO J=1,ILENE
  IF (ZLS(J) <= YRCLI%SMASK) THEN
    ZCMP(J,4)= 1.0_JPRB
  ELSE
!    IF (ZCMP(J,4) <= SMANQ) ZCMP(J,4)= ZCMS(J)
    IF (ZCMP(J,4) <= YRCLI%SMANQ) ZCMP(J,4)= 0.5_JPRB
  ENDIF
ENDDO

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZMSK )
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!     3.6 Soil moisture

360 CONTINUE
IF (.NOT.LLWP) GO TO 370

ALLOCATE ( ZFLD(IDATX,IDATY) )
ALLOCATE ( ZMSK(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=17,FILE='wpl_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(17) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZFLD(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(17,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(17)

!  Define mask

DO JJ=JPBY+1,JPBY+YRCLI%NDATY
  DO J=JPBX+1,JPBX+YRCLI%NDATX
    IF (ZFLD(J,JJ) <= YRCLI%SMANQ) THEN
      ZMSK(J,JJ)= 1.0_JPRB
    ELSE
      ZMSK(J,JJ)= 0.0_JPRB
    ENDIF
  ENDDO
ENDDO

!  Interpolate mask

CALL INTER8(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZXMP,ZMSK,ZSLA,ZSLO,ZCLO)  

!  Interpolate with land-sea mask (4 points)

CALL INTER10(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,5),ZCMS,ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZMSK,ZXMP,ZLSR(1),ZXMS,YRCLI%SMASK)  

DO J=1,ILENE
  IF (ZLS(J) <= YRCLI%SMASK) THEN
    ZCMP(J,5)= 1.0_JPRB
  ELSE
!    IF (ZCMP(J,5) <= SMANQ) ZCMP(J,5)= ZCMS(J)
    IF (ZCMP(J,5) <= YRCLI%SMANQ) ZCMP(J,5)= 0.5_JPRB
  ENDIF
ENDDO

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZMSK )
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!     3.7 Snow cover

370 CONTINUE
IF (.NOT.LLSN) GO TO 400

ALLOCATE ( ZFLD(IDATX,IDATY) )
ALLOCATE ( ZMSK(IDATX,IDATY) )
IF (YRCLI%LIEEE) THEN
  ALLOCATE ( ZAUX1(YRCLI%NDATX*YRCLI%NDATY) )
ENDIF

!  Read

OPEN(UNIT=18,FILE='snl_GL',FORM=CLFORM)
IF (YRCLI%LIEEE) THEN
  READ(18) ZAUX1
  DO JJ=1+JPBY,YRCLI%NDATY+JPBY
    DO J=1+JPBX,YRCLI%NDATX+JPBX
      IZ=J-JPBX+(JJ-1-JPBY)*YRCLI%NDATX
      ZFLD(J,JJ)=ZAUX1(IZ)
    ENDDO
  ENDDO
ELSE
  READ(18,*)((ZFLD(J,JJ),J=1+JPBX,YRCLI%NDATX+JPBX),JJ=1+JPBY,YRCLI%NDATY+JPBY)
ENDIF
CLOSE(18)

!  Define mask

DO JJ=JPBY+1,JPBY+YRCLI%NDATY
  DO J=JPBX+1,JPBX+YRCLI%NDATX
    IF (ZFLD(J,JJ) <= YRCLI%SMANQ) THEN
      ZMSK(J,JJ)= 1.0_JPRB
    ELSE
      ZMSK(J,JJ)= 0.0_JPRB
    ENDIF
  ENDDO
ENDDO

!  Interpolate mask

CALL INTER8(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZXMP,ZMSK,ZSLA,ZSLO,ZCLO)  

!  Interpolate with land-sea mask (4 points)

CALL INTER10(NDGLG,NDLON,IDATY,IDATX,1,YRCLI%NPINT,ILENE,ICO,INJQ,INIQ,&
 & NLOENG(1:),ILENT,ZCMP(1,6),ZCMS,ZFLD,ZSLA,ZSLO,ZCLO,&
 & ZMSK,ZXMP,ZLSR(1),ZXMS,YRCLI%SMASK)  

DO J=1,ILENE
  IF (ZLS(J) <= YRCLI%SMASK) THEN
    ZCMP(J,6)= 0.0_JPRB
  ELSE
!    IF (ZCMP(J,6) <= SMANQ) ZCMP(J,6)= ZCMS(J)
    IF (ZCMP(J,6) <= YRCLI%SMANQ) ZCMP(J,6)= 0.0_JPRB
  ENDIF
ENDDO

DEALLOCATE ( ZFLD )
DEALLOCATE ( ZMSK )
IF (YRCLI%LIEEE) THEN
  DEALLOCATE ( ZAUX1 )
ENDIF

!     ------------------------------------------------------------------

!     4. MODIFY INITIAL FIELDS.
!        ----------------------

400 CONTINUE

!     4.1 Modifications on sea

IF (LLST) THEN
! New scheme
  IF (LSOLV) THEN
    DO J=1,ILENE
      IF (ZLS(J) <= YRCLI%SMASK) THEN
        ZTS(J)=MAX(0.0_JPRB,ZCMP(J,1))
        ZTP(J)=MAX(0.0_JPRB,ZCMP(J,1))
        IF (ZCMP(J,1) <= TMERGL) THEN
          ZAL(J)= YRCLI%SALBB
          ZEM(J)= YRCLI%SEMIB
          Z0D(J)= YRCLI%SZZ0B*RG
          Z0T(J)= YRCLI%SZZ0B*RG
        ELSE
          ZAL(J)= YRCLI%SALBM
          ZEM(J)= YRCLI%SEMIM
          Z0D(J)= YRCLI%SZZ0M*RG
          Z0T(J)= YRCLI%SZZ0M*RG
        ENDIF
      ENDIF
    ENDDO
! Old scheme
  ELSE
    DO J=1,ILENE
      IF (ZLS(J) <= YRCLI%SMASK) THEN
        ZTS(J)=MAX(0.0_JPRB,ZCMP(J,1))
        ZTP(J)=MAX(0.0_JPRB,ZCMP(J,1))
      ENDIF
    ENDDO
  ENDIF
ENDIF

!     4.2 Modifications on land

IF (LLSOL) THEN
  ALLOCATE ( ZWSA(ILENT) )
  ALLOCATE ( ZWFC(ILENT) )
  ALLOCATE ( ZWWI(ILENT) )
  ALLOCATE ( ZWPX(ILENT) )
  ALLOCATE ( ZWSX(ILENT) )
  LLHMT=.FALSE.
  CALL ACSOLW(YDPHY1,1,ILENE,ILENT,ZAR,ZD2,ZLS,ZIV,ZSA,&
   & LLHMT,ZWFC,ZWPX,ZWSA,ZWSX,ZWWI)

  DO J=1,ILENE
    IF (ZLS(J) > YRCLI%SMASK) THEN
!  Fill new fields
      IF (LLTS) ZTS(J)=MAX(0.0_JPRB,ZCMP(J,2))
      IF (LLTP) ZTP(J)=MAX(0.0_JPRB,ZCMP(J,3))
      IF (LLWS) ZWS(J)=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCMP(J,4)))
      IF (LLWP) ZWP(J)=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCMP(J,5)))
      IF (LLSN) ZSN(J)=MAX(0.0_JPRB,ZCMP(J,6))
!  No superficial fields available
      IF (LLTP.AND..NOT.LLTS) ZTS(J)=ZTP(J)
      IF (LLWP.AND..NOT.LLWS) ZWS(J)=ZWP(J)
!  Control of snow according to temperature and moisture
      IF ((ZTS(J) > ZTR).AND.LLTS) ZSN(J)=0.0_JPRB
      IF ((ZTP(J) > ZTR).AND.LLTP) ZSN(J)=0.0_JPRB
      IF ((ZWP(J) < YRCLI%RSWR).AND.LLWP) ZSN(J)=0.0_JPRB
!  Control of soil moisture
      ZWS(J)=MIN(ZWS(J),ZWFC(J)/ZWSA(J))
      ZWP(J)=MIN(ZWP(J),ZWFC(J)/ZWSA(J))
!  Control for ice cap
      IF (NINT(ZIV(J)) == YRCLI%NTPGLA) THEN
        ZWS(J)= 1.0_JPRB
        ZWP(J)= 1.0_JPRB
        ZSN(J)= ZNEI5
      ELSE
        ZSN(J)= MIN(ZSN(J),ZNEI4)
      ENDIF
    ENDIF
  ENDDO

  DEALLOCATE ( ZWSA )
  DEALLOCATE ( ZWFC )
  DEALLOCATE ( ZWWI )
  DEALLOCATE ( ZWPX )
  DEALLOCATE ( ZWSX )
  DEALLOCATE ( ZIV )
  DEALLOCATE ( ZAR )
  DEALLOCATE ( ZSA )
  DEALLOCATE ( ZD2 )
ENDIF

!     ------------------------------------------------------------------

!     5. WRITE.
!        ------

!     5.1 Albedo, emissivity and roughness lengths (if modified according to SST)

INIV=0
IF (LLST.AND.LSOLV) THEN
  CALL FAIENC(IREP,INUM,'SURF',INIV,'ALBEDO      ',ZAL,LLCOSP)
  CALL FAIENC(IREP,INUM,'SURF',INIV,'EMISSIVITE  ',ZEM,LLCOSP)
! Do not pack roughness lengths
  CALL FAVEUR(IREP,INUM,INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL)
  INGRIG=0
  CALL FAGOTE(IREP,INUM,INGRIG,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL)
  CALL FAIENC(IREP,INUM,'SURF',INIV,'Z0.FOIS.G   ',Z0D,LLCOSP)
  CALL FAIENC(IREP,INUM,'SURF',INIV,'GZ0.THERM   ',Z0T,LLCOSP)
! Reset packing after the treatment of roughness lengths
  CALL FAGOTE(IREP,INUM,INGRIB,INBPDG,INBCSP,ISTRON,IPUILA,IDMOPL)
  DEALLOCATE(ZAL)
  DEALLOCATE(ZEM)
  DEALLOCATE(Z0D)
  DEALLOCATE(Z0T)
ENDIF

!     5.2 Climatological surface fields

INIV=1
CALL FAIENC(IREP,INUM,'SURF',INIV,'TEMPERATURE ',ZTS,LLCOSP)
CALL FAIENC(IREP,INUM,'PROF',INIV,'TEMPERATURE ',ZTP,LLCOSP)
CALL FAIENC(IREP,INUM,'SURF',INIV,'PROP.RMAX.EA',ZWS,LLCOSP)
CALL FAIENC(IREP,INUM,'PROF',INIV,'PROP.RMAX.EA',ZWP,LLCOSP)
CALL FAIENC(IREP,INUM,'SURF',INIV,'RESERV.NEIGE',ZSN,LLCOSP)
IF (LLTP) CALL FAIENC(IREP,INUM,'RELA',INIV,'TEMPERATURE ',ZTP,LLCOSP)
IF (LLWP) CALL FAIENC(IREP,INUM,'RELA',INIV,'PROP.RMAX.EA',ZWP,LLCOSP)

!     5.3 Control and close ARPEGE file

CALL CHK923(YDGEOMETRY,INUM)

CALL LFILAF(IREP,INUM,.TRUE.)
CALL FAIRME(IREP,INUM,'KEEP')

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('INCLI6',1,ZHOOK_HANDLE)
END SUBROUTINE INCLI6
