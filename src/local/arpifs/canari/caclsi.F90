SUBROUTINE CACLSI(YDCST, YDDPHY,YDML_PHY_MF,PVGEOM,YDDIMV,YDSURF,KNBPT,KPROMA,PUT,PVT,PTT,PQT,PLT,PIT,PRT,PST,PGT,PSPT,PGM,&
 & PSP_SG,PSP_RR,PSD_VF,PSD_VV,PSP_CI)

!****-------------------------------------------------------------------
!****   CACLSI : ROUTINE EFFECTUANT L'INITIALISATION DE TABLEAUX
!****   ------   POINTS DE GRILLE NECESSAIRES A *CANARI*.
!****-------------------------------------------------------------------

!*** Arguments d'entree :
!*** ------------------
!               KNBPT  : dimensionnement horizontal utile
!               KPROMA : surdimensionnement horizontal
!               PUT,PVT,PTT,PQT,PLT,PIT,PRT,PST,PSPT : 
!                  parties des champs u,v,t,qv,ql,qi,qr,qs,ps
!               PGM : partie du champ GM (facteur d'echelle)

!*** Sous-programme amont :  CAPOTX
!*** --------------------
!*** Sous-programmes aval :
!*** --------------------
!               - GPHPRE- CALCUL DES NIVEAUX PRESSIONS.
!               - GPRCP - CALCUL DE R, CP ET KAPPA.
!               - GPGEO - CALCUL DES ALTITUDES GEOPOTENTIEL
!               - ACSOLW- CALCULS PRELIMINAIRES POUR ISBA (WFC)
!               - ACHMT - CALCUL DES PARAMETRES AUX HAUTEURS METEO

!*** COMMONS utilises :
!*** ----------------
!              - YOMDIM - DIMENSIONS DES TABLEAUX DE TRAVAIL.
!              - YOMCST - CONSTANTES DE BASE.
!              - YOMPHY - CLES DE LA PHYSIQUE.
!              - YOMPHY2- DEFINITION DES HAUTEURS METEO.
!              - YOMGEM - DEFINITION DE LA GEOMETRIE DU MODELE.

!*** Algorithme :
!*** ----------
!              - APPELLE *ACSOLW* POUR DES CALCULS PRELIMINAIRES AVEC ISBA
!              - APPELLE *ACHMT* POUR LE CACUL DES PARAMETRES
!                AUX ALTITUDES METEOS RANGES DANS *QAGPSF*.

!****  Auteurs :   CB 12/90  BU 08/92  PM xx/xx  VC 02/93,11/94  DG 05/94

!****  Modifications:
!        R. El Khatib : 01-08-07 Pruning options
!        M. Bellus : 2003-04-24 Change of arguments in ACHMT calling
!                               + removing LSOLV and some cleaning
!        M. Tudor : 2003-10-21 change args in ACHMT, reintroduce LSOLV
!        Y. Bouteloup : 2005-01-19 add PLT and PIT in input to initialise ZQL and ZQI
!        Modified 04-02-06: Y. Seity : new arguments to GPRCP
!        Y. Bouteloup : 2005-12-25 add PRT and PST in input to initialise ZQR and ZQS
!        M.Hamrud      01-Jul-2006 Revised surface fields
!        A. Alias : 2007-10-11 New Output in ACHMT (ZRTI=1/R*T) 
!        C.Payan  : 2008-11 Neutral Wind (ACHMT)
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        U. Andrae : 25-04-2012  add PGT in input (graupels) to initialise ZQG
!        K. Yessad (Nov 2012): call GPHPRE.
!****-------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMVERT            , ONLY : TVERTICAL_GEOM
USE YOMDIMV            , ONLY : TDIMV
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST
USE YOMDPHY  , ONLY : TDPHY
USE INTDYN_MOD,ONLY : YYTXYB

!**---------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(TDPHY)          ,INTENT(INOUT) :: YDDPHY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
TYPE(TVERTICAL_GEOM) ,INTENT(IN)    :: PVGEOM
TYPE(TDIMV)          ,INTENT(IN)    :: YDDIMV
TYPE(TSURF)          ,INTENT(INOUT) :: YDSURF
INTEGER(KIND=JPIM)   ,INTENT(IN)    :: KNBPT
INTEGER(KIND=JPIM)   ,INTENT(IN)    :: KPROMA
REAL(KIND=JPRB)      ,INTENT(IN)    :: PUT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PVT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PTT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PQT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PLT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PIT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PRT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PST(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSPT(KPROMA)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGM(KNBPT)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSP_SG(KPROMA,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSP_RR(KPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSD_VF(KPROMA,YDSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSD_VV(KPROMA,YDSURF%YSD_VVD%NDIM)
REAL(KIND=JPRB)      ,INTENT(OUT)   :: PSP_CI(KPROMA,YDSURF%YSP_CID%NDIM)
!**---------------------------------------------------------------------
!     ZPRESF  : PRESSIONS AUX NIVEAUX ENTIERS.
!     ZPRESH  : PRESSIONS AUX DEMI-NIVEAUX.
!     ZQ      : HUMIDITE SPECIFIQUE.
!     ZQI     : EAU GLACE.
!     ZQL     : EAU LIQUIDE.
!     ZQR     : PLUIE.
!     ZQSN    : NEIGE.
!     ZQG     : GRAUPEL.
!     ZT      : TEMPERATURE.
!     ZU      : COMPOSANTE EN X DU VENT.
!     ZV      : COMPOSANTE EN Y DU VENT.
!     ZSNS    : HAUTEUR DE NEIGE.
!     ZTS     : TEMPERATURE DE SURFACE.
!     ZWS     : CONTENU EN EAU DU SOL.
!     ZR      : R(EAU).
!     ZCP     : CP(EAU).
!     ZKAP    : R(EAU)/CP(EAU).
!     ZPHIF   : GEOPOTENTIELS AUX NIVEAUX ENTIERS.
!     ZPHIH   : GEOPOTENTIELS AUX DEMI-NIVEAUX.
!     ZQCLS   : HUMIDITE SPECIFIQUE A 2M.
!     ZTCLS   : TEMPERATURE A 2M.
!     ZRHCLS  : HUMIDITE RELATIVE A 2M.
!     ZUCLS   : COMPOSANTE DU VENT EN X A 10M.
!     ZVCLS   : COMPOSANTE DU VENT EN Y A 10M.
!     ZNUCLS  : COMPOSANTE DU VENT NEUTRE EN X A 10M.
!     ZNVCLS  : COMPOSANTE DU VENT NEUTRE EN Y A 10M.

REAL(KIND=JPRB) :: ZPRESF(KPROMA,YDDIMV%NFLEVG),ZPRESH(KPROMA,0:YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXYB(KPROMA,YDDIMV%NFLEVG,YYTXYB%NDIM)
REAL(KIND=JPRB) :: ZR(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZCP(KPROMA,YDDIMV%NFLEVG),   ZKAP(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQ(KPROMA,YDDIMV%NFLEVG),    ZT(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZU(KPROMA,YDDIMV%NFLEVG),    ZV(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQI(KPROMA,YDDIMV%NFLEVG),   ZQL(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQR(KPROMA,YDDIMV%NFLEVG),   ZQSN(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQG(KPROMA,YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPHIF(KPROMA,YDDIMV%NFLEVG), ZPHIH(KPROMA,0:YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFPLSH(KPROMA,0:YDDIMV%NFLEVG), ZFPLCH(KPROMA,0:YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSNS  (KPROMA), ZTS   (KPROMA), ZWS   (KPROMA)
REAL(KIND=JPRB) :: ZRHCLS(KPROMA), ZTCLS (KPROMA), ZPCLS (KPROMA)
REAL(KIND=JPRB) :: ZUCLS (KPROMA), ZVCLS (KPROMA)
REAL(KIND=JPRB) :: ZNUCLS (KPROMA), ZNVCLS (KPROMA)
REAL(KIND=JPRB) :: ZDPHIT(KPROMA), ZDPHIV(KPROMA), ZWFC  (KPROMA)
REAL(KIND=JPRB) :: ZNBVNO(KPROMA,0:YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZMRIPP(KPROMA,0:YDDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZCD   (KPROMA), ZCDN  (KPROMA), ZCDROV(KPROMA)
REAL(KIND=JPRB) :: ZCH   (KPROMA), ZCHROV(KPROMA), ZCPS  (KPROMA)
REAL(KIND=JPRB) :: ZDQSTS(KPROMA), ZGWDCS(KPROMA), ZGZ0  (KPROMA)
REAL(KIND=JPRB) :: ZGZ0H (KPROMA)
REAL(KIND=JPRB) :: ZNEIJ (KPROMA), ZHQ   (KPROMA)
REAL(KIND=JPRB) :: ZHU   (KPROMA), ZQS   (KPROMA), ZQCLS (KPROMA)
REAL(KIND=JPRB) :: ZQSATS(KPROMA), ZRS   (KPROMA), ZSTAB (KPROMA)
REAL(KIND=JPRB) :: ZRTI  (KPROMA)
REAL(KIND=JPRB) :: ZXDROV(KPROMA), ZXHROV(KPROMA)
REAL(KIND=JPRB) :: ZURAF (KPROMA), ZVRAF (KPROMA)
REAL(KIND=JPRB) :: ZHV   (KPROMA), ZZ0H  (KPROMA), ZZ0F  (KPROMA)
REAL(KIND=JPRB) :: ZFLSM (KPROMA), ZARG  (KPROMA), ZWSI  (KPROMA)
REAL(KIND=JPRB) :: ZD2   (KPROMA), ZIVEG (KPROMA), ZSAB  (KPROMA)
REAL(KIND=JPRB) :: ZGZ0R (KPROMA), ZNEIJG(KPROMA), ZNEIJV(KPROMA)
REAL(KIND=JPRB) :: ZWPMX (KPROMA), ZWSAT (KPROMA), ZWSMX (KPROMA)
REAL(KIND=JPRB) :: ZWWILT(KPROMA), ZVEG  (KPROMA), ZVEG0 (KPROMA)

INTEGER(KIND=JPIM) :: JLEV, JROF

LOGICAL :: LLDCLS, LLDHMT

REAL(KIND=JPRB) :: ZRHMAX, ZRHMIN
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!**---------------------------------------------------------------------

#include "achmt.intfb.h"
#include "actkehmt.intfb.h"
#include "acsolw.intfb.h"
#include "gpgeo.intfb.h"
#include "gphpre.intfb.h"
#include "gprcp.intfb.h"

!**---------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CACLSI',0,ZHOOK_HANDLE)
ASSOCIATE(NFLEVG=>YDDIMV%NFLEVG, &
 & YSD_VF=>YDSURF%YSD_VF, YSD_VFD=>YDSURF%YSD_VFD, YSD_VV=>YDSURF%YSD_VV, &
 & YSD_VVD=>YDSURF%YSD_VVD, YSP_CI=>YDSURF%YSP_CI, YSP_CID=>YDSURF%YSP_CID, &
 & YSP_RR=>YDSURF%YSP_RR, YSP_RRD=>YDSURF%YSP_RRD, YSP_SG=>YDSURF%YSP_SG, &
 & YSP_SGD=>YDSURF%YSP_SGD, &
 & LSOLV=>YDML_PHY_MF%YRPHY%LSOLV, LFGEL=>YDML_PHY_MF%YRPHY%LFGEL, LCOEFKSURF=>YDML_PHY_MF%YRPHY%LCOEFKSURF, &
 & HVCLS=>YDML_PHY_MF%YRPHY2%HVCLS, HTCLS=>YDML_PHY_MF%YRPHY2%HTCLS)
!**---------------------------------------------------------------------
!**  - 1 - Preparation du calcul des parametres aux hauteurs meteo.
!**        -------------------------------------------------------

ZPRESH(:,NFLEVG)=PSPT(:)

! Pressions a tous les niveaux intermediaires et quantites auxiliaires
CALL GPHPRE(KPROMA,NFLEVG,1,KNBPT,PVGEOM%YRVAB,ZPRESH,PXYB=ZXYB,PRESF=ZPRESF)

! Remplissage des tableaux locaux
DO JLEV = 1 , NFLEVG
  DO JROF = 1 , KNBPT
    ZT(JROF,JLEV)  = PTT(JROF,JLEV)
    ZQ(JROF,JLEV)  = PQT(JROF,JLEV)
    ZQL(JROF,JLEV) = PLT(JROF,JLEV)
    ZQI(JROF,JLEV) = PIT(JROF,JLEV)
    ZQR(JROF,JLEV) = PRT(JROF,JLEV)
    ZQG(JROF,JLEV) = PGT(JROF,JLEV)
    ZQSN(JROF,JLEV)= PST(JROF,JLEV)
    ZU(JROF,JLEV)  = PUT(JROF,JLEV) * PGM(JROF)
    ZV(JROF,JLEV)  = PVT(JROF,JLEV) * PGM(JROF)
  ENDDO
ENDDO

DO JROF = 1 , KNBPT
  ZDPHIT(JROF) = YDCST%RG*HTCLS
  ZDPHIV(JROF) = YDCST%RG*HVCLS
  ZPHIH(JROF,NFLEVG) = 0.0_JPRB
  ZSNS (JROF) = PSP_SG(JROF,YSP_SG%YF%MP0)
  ZTS  (JROF) = PSP_RR(JROF,YSP_RR%YT%MP0)
  ZWS  (JROF) = PSP_RR(JROF,YSP_RR%YW%MP0)
  ZVEG (JROF) = PSD_VF(JROF,YSD_VF%YVEG%MP)
  ZZ0F (JROF) = PSD_VF(JROF,YSD_VF%YZ0F%MP)
  ZFLSM(JROF) = PSD_VF(JROF,YSD_VF%YLSM%MP)
ENDDO

! Calcul de R, CP et KAPPA.
CALL GPRCP(KPROMA,1,KNBPT,NFLEVG,&
 & PQ=ZQ,PQI=ZQI,PQL=ZQL,PQR=ZQR,PQS=ZQSN,PQG=ZQG,&
 & PCP=ZCP,PR=ZR,PKAP=ZKAP)

! Calcul du geopotentiel par rapport a la surface a tous les niveaux
CALL GPGEO(KPROMA,1,KNBPT,NFLEVG,ZPHIH,ZPHIF,ZT,ZR,ZXYB(1,1,YYTXYB%M_LNPR),ZXYB(1,1,YYTXYB%M_ALPH),PVGEOM)

LLDCLS = .FALSE.
LLDHMT = .TRUE.

! Calculs preliminaires pour le schema sol-vegetation (WFC,HV)
IF ( LSOLV ) THEN
  DO JROF = 1 , KNBPT
    ZHV   (JROF) = PSD_VV(JROF,YSD_VV%YHV%MP)
    ZARG  (JROF) = PSD_VV(JROF,YSD_VV%YARG%MP)
!          ZD2   (JROF) = 1.
!          ZIVEG (JROF) = 0.
!          ZSAB  (JROF) = PSD_VV(JROF,YSD_VV%YSAB%MP)
    IF ((YSD_VVD%NUMFLDS >= 8).AND.LSOLV) ZZ0H(JROF) = PSD_VV(JROF,YSD_VV%YZ0H%MP)
    IF (LFGEL) ZWSI(JROF) = PSP_RR(JROF,YSP_RR%YRR(4)%MP0)
  ENDDO
  CALL ACSOLW(YDML_PHY_MF%YRPHY1,1,KNBPT,KPROMA,ZARG,ZD2,ZFLSM,ZIVEG,ZSAB,LLDHMT,&
   & ZWFC,ZWPMX,ZWSAT,ZWSMX,ZWWILT)
ENDIF

!**---------------------------------------------------------------------
!**  - 2 - Appel pour le calcul des parametres aux hauteurs meteo.
!**        ------------------------------------------------------

IF (LCOEFKSURF) THEN
  CALL ACTKEHMT(1,KNBPT,KPROMA,NFLEVG,YSD_VVD%NUMFLDS>=8.AND.LSOLV,&
   & ZPHIH,ZPHIF,ZPRESH,ZPRESF,ZCP,ZQ,ZR,ZT,ZU,ZV,ZFPLSH,ZFPLCH,&
   & ZDPHIT,ZDPHIV,ZZ0F,ZZ0H,ZGZ0R,ZHV,ZFLSM,ZNEIJG,ZNEIJV,ZSNS,&
   & ZTS,ZVEG,ZWFC,ZWS,ZWSI,LLDCLS,LLDHMT,ZNBVNO,ZMRIPP,&
   & ZCD,ZCDN,ZCDROV,ZCH,ZCHROV,ZCPS,ZDQSTS,ZGWDCS,&
   & ZGZ0,ZGZ0H,ZHQ,ZHU,ZNEIJ,ZQCLS,ZQS,ZQSATS,ZRHCLS,ZRS,ZRTI,ZSTAB,&
   & ZTCLS,ZUCLS,ZVCLS,ZNUCLS,ZNVCLS,ZPCLS,ZVEG0,ZXDROV,ZXHROV,&
   & ZURAF,ZVRAF)
ELSE
  CALL ACHMT(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY1,YDML_PHY_MF%YRPHY2,&
   & YDCST,1,KNBPT,KPROMA,NFLEVG,YSD_VVD%NUMFLDS>=8.AND.LSOLV,&
   & ZPHIH,ZPHIF,ZPRESH,ZPRESF,ZCP,ZQ,ZR,ZT,ZU,ZV,ZFPLSH,ZFPLCH,&
   & ZDPHIT,ZDPHIV,ZZ0F,ZZ0H,ZGZ0R,ZHV,ZFLSM,ZNEIJG,ZNEIJV,ZSNS,&
   & ZTS,ZVEG,ZWFC,ZWS,ZWSI,LLDCLS,LLDHMT,ZNBVNO,ZMRIPP,&
   & ZCD,ZCDN,ZCDROV,ZCH,ZCHROV,ZCPS,ZDQSTS,ZGWDCS,&
   & ZGZ0,ZGZ0H,ZHQ,ZHU,ZNEIJ,ZQCLS,ZQS,ZQSATS,ZRHCLS,ZRS,ZRTI,ZSTAB,&
   & ZTCLS,ZUCLS,ZVCLS,ZNUCLS,ZNVCLS,ZPCLS,ZVEG0,ZXDROV,ZXHROV,&
   & ZURAF,ZVRAF)
ENDIF

! Initialisation et stockage des champs diagnostics necessaires a l'analyse
ZRHMAX = 1.0_JPRB
ZRHMIN = 0.0_JPRB
DO JROF = 1 , KNBPT
  PSP_CI(JROF,YSP_CI%YCI(5)%MP0) = MAX(ZRHMIN,MIN(ZRHMAX,ZRHCLS(JROF)))
  PSP_CI(JROF,YSP_CI%YCI(4)%MP0) = ZTCLS(JROF)
  PSP_CI(JROF,YSP_CI%YCI(6)%MP0) = ZUCLS(JROF) / PGM(JROF)
  PSP_CI(JROF,YSP_CI%YCI(7)%MP0) = ZVCLS(JROF) / PGM(JROF)
ENDDO

!**---------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CACLSI',1,ZHOOK_HANDLE)
END SUBROUTINE CACLSI
