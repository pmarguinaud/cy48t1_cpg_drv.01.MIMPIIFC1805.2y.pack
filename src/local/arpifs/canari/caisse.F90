SUBROUTINE CAISSE(YDGEOMETRY,KGUESS)
!****------------------------------------------------------------------------
!****   CAISSE : INITIALISATION ECART-TYPES PHI ET HU PAR DES
!****   ------   STATISTIQUES SAUVEGARDEES.
!****
!****    Auteurs :    P. MOLL  10/09/92     F. TAILLEFER  13/11/92
!****------------------------------------------------------------------------
!  BUT : INITIALISATION DES VARIANCES DE PHI ET HU DANS LE CAS D'UN
!  ---   DEMARRAGE A PARTIR D'UN GUESS PREVI (cas LMESSP=.T.).
!        ON LIT LES VALEURS DES ECART-TYPES D'ERREUR D'ANALYSE SUR LE
!        FICHIER "STATISTIQUES ASSIMILEES"; PUIS ON EN DEDUIT LA VALEUR
!        DES ECART-TYPES D'ERREUR DU GUESS.

!  ROUTINE APPELANTE :    *CALIFE*
!  -----------------

!  SOUS-PROGRAMME APPELE :  FACILE
!  ---------------------    CAPDGU CAINSU
!                           GPPRE  SPEREE REESPE
!                           ETIBIHIE EREESPE ESPEREE

!  ARGUMENT D'ENTREE :
!  -----------------
!     Explicite : - KGUESS : numero du guess choisi.
!     Implicite :  YOMDIM YOMCT0 YOMGEM
!                  QACOST QASSET QAGPSF

!  ARGUMENT DE SORTIE :
!  ------------------
!     Explicite : aucun
!     Implicite : - QASSET : tableau ESIG 
!                 - QAGPSF : tableaux RSGPHI et RSGHUM.

!  MODIFICATIONS :
!  -------------
!     01-03-20  S. Ivatek-Sahdan call DISSPEC -> call DISSPEC0
!     03-10-01  M. Hamrud        CY28 Cleaning
!     04-08-04  F. Taillefer     Back to gridpoint file
!     08-04-18  A. Stanesic      Externalise biperiodicisation
!     Apr 2008  K. Yessad        use DISGRID instead of DISGRID_C + cleanings
!     10-04-23  R. El Khatib     use disgrid_mod & diwrgrid_mod
!     12-01-05  F. Taillefer     use new QACOST type structure
!     Feb 2011  G.Mozdzynski     OOPS cleaning, use of derived type TGSGEOM
!     Jun 2012  P.Marguinaud     Fix array bounds
!     K. Yessad (Nov 2012): call GPHPRE.
!     T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      R. El Khatib 17-Aug-2016 remove suarpio
!****------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK
USE YOMLUN   , ONLY : NULOUT   ,NULASE
USE YOMCT0   , ONLY : LELAM
USE YOMMP0   , ONLY : MYPROC   ,MY_REGION_EW, NSTRIN
USE QALORI   , ONLY : RCAPRP   ,NCAMNS   ,NCAPLS
USE QACTAN   , ONLY : NCANLI   ,NCANLS
USE QACOST   , ONLY : TYPE_QACOST
USE QASSET   , ONLY : ESIG
USE QAGPSF   , ONLY : RSGHUM   ,RSGPHI
USE DISGRID_MOD , ONLY : DISGRID_SEND, DISGRID_RECV
USE DIWRGRID_MOD, ONLY : DIWRGRID_SEND, DIWRGRID_RECV

!****------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
INTEGER(KIND=JPIM),INTENT(IN)    :: KGUESS

!****------------------------------------------------------------------------

REAL(KIND=JPRB) :: ZEPHI(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZEHU(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZIPHI(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2),&
 &  ZIHU(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2)
REAL(KIND=JPRB) :: ZGESIG(YDGEOMETRY%YRGEM%NGPTOTG,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZZEPHI(0:YDGEOMETRY%YRDIM%NDLSM,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NDGSAG:YDGEOMETRY%YRDIM%NDGENG)
REAL(KIND=JPRB) :: ZAEPHI(YDGEOMETRY%YRGEM%NGPTOT),ZDATA(YDGEOMETRY%YRGEM%NGPTOTG),&
 & ZFIELD(YDGEOMETRY%YRGEM%NGPTOT,2*YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZBID(YDGEOMETRY%YRDIM%NDLON,YDGEOMETRY%YRDIM%NDGENL,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPRESH(0:YDGEOMETRY%YRDIMV%NFLEVG),ZPRESF(YDGEOMETRY%YRDIMV%NFLEVG),ZPOIDS(YDGEOMETRY%YRDIMV%NFLEVG),ZSTR(2)

INTEGER(KIND=JPIM) :: ITOT(12,YDGEOMETRY%YRDIMV%NFLEVG),ITOTL(12),ITOTV(YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM) :: ICPH(12,YDGEOMETRY%YRDIMV%NFLEVG),ICPHL(12),ICPHV(YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM) :: ICPB(12,YDGEOMETRY%YRDIMV%NFLEVG),ICPBL(12),ICPBV(YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM) :: IAGDIO(2*YDGEOMETRY%YRDIMV%NFLEVG)
INTEGER(KIND=JPIM) , ALLOCATABLE :: ISTAGP(:)
CHARACTER(LEN=10) ::  CLVAR

INTEGER(KIND=JPIM) :: ICAGLN, ICAGLS, ICALOE, ICALOW, ICPBT,IMAXFIELD,&
 & ICPHT, IEND, IGPTSK, IILA, ILEVHU, ILVMNS,&
 & ILVPLS, IM, IND, INUM, IPROC, IREP, IROF,&
 & ISTART, ITOTT, JGL, JIND, JJL, JLA, JLEV,&
 & JLON, JM, JMLOC, JN, JPG, I_NFLEV2

LOGICAL :: LL

TYPE(TYPE_QACOST) :: YLCOST

REAL(KIND=JPRB) :: Z1MALP, ZALPHA, ZARG, ZB, ZCST, ZFONC, ZKZ,&
 & ZKZ1, ZLAT, ZPOTOT, ZRBELL, ZTLUM
REAL(KIND=JPRB) :: ZHOOK_HANDLE
#ifdef __INTEL_COMPILER
INTRINSIC :: KIND ! Fails with Intel compiler as it thinks we use ATLAS kind function
#endif


!****------------------------------------------------------------------------

#include "etibihie.h"

#include "abor1.intfb.h"
#include "cainsu.intfb.h"
#include "capdgu.intfb.h"
#include "ereespe.intfb.h"
#include "esperee.intfb.h"
#include "gphpre.intfb.h"
#include "reespe.intfb.h"
#include "speree.intfb.h"

#include "qastat.func.h"

!****------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CAISSE',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,          &
& YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, YDLAP=>YDGEOMETRY%YRLAP,    YDVAB=>YDGEOMETRY%YRVAB, YDELAP=>YDGEOMETRY%YRELAP&
& )
ASSOCIATE(NDGENL=>YDDIM%NDGENL, NDGLG=>YDDIM%NDGLG,   NDGUNG=>YDDIM%NDGUNG, NDGUXG=>YDDIM%NDGUXG,   NDLON=>YDDIM%NDLON, &
& NDLSM=>YDDIM%NDLSM, NDLUNG=>YDDIM%NDLUNG,   NDLUXG=>YDDIM%NDLUXG, NMSMAX=>YDDIM%NMSMAX, NSMAX=>YDDIM%NSMAX,           &
& NUMP=>YDDIM%NUMP,   NFLEVG=>YDDIMV%NFLEVG, NFLSUR=>YDDIMV%NFLSUR,   NGPTOT=>YDGEM%NGPTOT, NLOENG=>YDGEM%NLOENG,       &
& NSTAGP=>YDGEM%NSTAGP,   MYMS=>YDLAP%MYMS, NASM0=>YDLAP%NASM0,   NONL=>YDMP%NONL, NPTRFLOFF=>YDMP%NPTRFLOFF,           &
& NSTA=>YDMP%NSTA)
!****------------------------------------------------------------------------

!**--------------------------------------------------------------------------
!**   0.    Initialisation provisoire pour le domaine d'impression
!**         (en attendant la refonte de CAPDGU selon le masque d'analyse).
!**         -------------------------------------------------------------

ICALOW = NDLUNG
ICALOE = NDLUXG
ICAGLN = 1
ICAGLS = NDGENL

!  Le tableau ISTAGP est introduit au lieu de NSTAGP parce que pour
!  Canari/Aladin (existence de la zone E) NSTAGP est mal initialise dans
!  SUGEM1B pour LMESSP=.T.
!  De meme, ce tableau ISTAGP a ete introduit dans CABANE.

IF (LELAM) THEN
  ALLOCATE(ISTAGP(1:NDGLG))
  IROF = 1
  DO JGL = 1 , NDGLG
    ISTAGP(JGL)=IROF
    IROF=IROF+NLOENG(JGL)
  ENDDO
ENDIF

!**--------------------------------------------------------------------------
!**   1.    Initialisation des champs d'ecart-types d'erreur d'analyse.
!**         ----------------------------------------------------------

!*    1.1.  Lecture du fichier des ecart-types

WRITE(NULOUT,'(/,'' --> lecture ecart-types d erreur d '',&
 & ''analyse du reseau precedent '')')

!  Initialisation des differents tableaux
ZEPHI(:,:) = 0.0_JPRB
ZEHU (:,:) = 0.0_JPRB
ZIPHI(:,:) = 0.0_JPRB
ZIHU (:,:) = 0.0_JPRB

!  Lecture par les processeurs concernes
IMAXFIELD=SIZE(IAGDIO)
DO JJL=1,IMAXFIELD
  IAGDIO(JJL)=MOD(JJL-1,NSTRIN)+1
ENDDO
INUM=0
DO JLEV = 1 , NFLEVG
!   Traitement du champ concernant PHI: lecture puis envoi
  INUM=INUM+1
  IF (INUM <= IMAXFIELD) THEN
    IF (IAGDIO(INUM) == MYPROC) THEN
      CALL FACILE(IREP,NULASE,'S',JLEV,'ETAPHI',ZDATA,.FALSE.)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZDATA,INUM,ZFIELD(1,JLEV))
    ENDIF
  ELSE
    WRITE(NULOUT,*) ' IMAXFIELD TOO SMALL !'
    CALL ABOR1('CAISSE : ABOR1 CALLED')
  ENDIF
!   Traitement du champ concernant HU: lecture puis envoi
  INUM=INUM+1
  IF (INUM <= IMAXFIELD) THEN
    IF (IAGDIO(INUM) == MYPROC) THEN
      CALL FACILE(IREP,NULASE,'S',JLEV,'ETAHU',ZDATA,.FALSE.)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZDATA,INUM,ZFIELD(1,JLEV+NFLEVG))
    ENDIF
  ELSE
    WRITE(NULOUT,*) ' IMAXFIELD TOO SMALL !'
    CALL ABOR1('CAISSE : ABOR1 CALLED')
  ENDIF
ENDDO

!  Reception par les processeurs qui ne lisent pas
DO JJL = 1 , 2*NFLEVG
  IF (IAGDIO(JJL) /= MYPROC) THEN
    CALL DISGRID_RECV(YDGEOMETRY,IAGDIO(JJL),1,ZFIELD(1,JJL),JJL)
  ENDIF
ENDDO

!*    1.2.  Recuperation dans tableaux pdg locaux

ZEPHI(:,:)=ZFIELD(:,1:NFLEVG)
ZEHU(:,:)=ZFIELD(:,NFLEVG+1:NFLEVG+NFLEVG)

!**--------------------------------------------------------------------------
!**   2.    Calcul des champs d'ecart-types d'erreur du guess.
!**         -------------------------------------------------

WRITE(NULOUT,'(/,'' --> calcul ecart-types d erreur du guess'')')

I_NFLEV2=2*NFLEVG
ESIG(1:NFLEVG,:)=ESIG(1:NFLEVG,:)*3._JPRB
ESIG(NFLEVG+1:I_NFLEV2,:)=ESIG(NFLEVG+1:I_NFLEV2,:)*1.2_JPRB

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=ZEPHI(IROF,JN)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA ANAL'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=ESIG(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA CLIM'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

ZALPHA=1.0_JPRB-REAL(KGUESS,KIND(ZALPHA))*.05_JPRB
Z1MALP=1.0_JPRB-ZALPHA
DO JN = 1 , NFLEVG
  ILEVHU=NFLEVG+JN
  DO JPG = 1 , NGPTOT
    RSGPHI(JN,JPG)=ESIG(JN,JPG)
    RSGHUM(JN,JPG)=ESIG(ILEVHU,JPG)
    ZEPHI(JPG,JN)=MIN(ZEPHI(JPG,JN),ESIG(JN,JPG))
    ESIG(JN,JPG)=&
     & SQRT ( MAX ( Z1MALP*ESIG(JN,JPG)**2+ZALPHA*&
     & ZEPHI(JPG,JN)**2 , 0.09_JPRB*ESIG(JN,JPG)**2 ) )
!         ESIG(ILEVHU,JPG)=
!    s        SQRT ( MAX ( Z1MALP*ESIG(ILEVHU,JPG)**2+ZALPHA*
!    s        ZEHU(JPG,JN)**2 , 0.01*ESIG(ILEVHU,JPG)**2 ) )
    ESIG(ILEVHU,JPG)=MAX ( Z1MALP*ESIG(ILEVHU,JPG)+&
     & ZALPHA*ZEHU(JPG,JN) , ESIG(ILEVHU,JPG)*0.2_JPRB )
    RSGPHI(JN,JPG)=ESIG(JN,JPG)/RSGPHI(JN,JPG)
!         RSGHUM(JN,JPG)=ESIG(ILEVHU,JPG)/RSGHUM(JN,JPG)
  ENDDO
ENDDO

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=ESIG(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA GUES'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=RSGPHI(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA GUES'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=RSGHUM(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMAG HUM'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

!**--------------------------------------------------------------------------
!**   3.    Quelques petits diagnostics sur SIGMA P.
!**         ---------------------------------------

ITOTT      = 0
ICPHT      = 0
ICPBT      = 0
ITOT (:,:) = 0
ITOTL(:)   = 0
ITOTV(:)   = 0
ICPH (:,:) = 0
ICPHL(:)   = 0
ICPHV(:)   = 0
ICPB (:,:) = 0
ICPBL(:)   = 0
ICPBV(:)   = 0

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    IILA=(12*(JLA-1))/NDGENL+1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ITOT(IILA,JN)=ITOT(IILA,JN)+1
      IF (RSGPHI(JN,IROF) > 0.40_JPRB) ICPH(IILA,JN)=ICPH(IILA,JN)+1
      IF (RSGPHI(JN,IROF) < 0.25_JPRB) ICPB(IILA,JN)=ICPB(IILA,JN)+1
    ENDDO
  ENDDO
ENDDO

DO JN = 1 , NFLEVG
  DO JLA = 1 , 12
    ITOTL(JLA)=ITOTL(JLA)+ITOT(JLA,JN)
    ITOTV(JN)=ITOTV(JN)+ITOT(JLA,JN)
    ITOTT=ITOTT+ITOT(JLA,JN)
    ICPHL(JLA)=ICPHL(JLA)+ICPH(JLA,JN)
    ICPHV(JN)=ICPHV(JN)+ICPH(JLA,JN)
    ICPHT=ICPHT+ICPH(JLA,JN)
    ICPBL(JLA)=ICPBL(JLA)+ICPB(JLA,JN)
    ICPBV(JN)=ICPBV(JN)+ICPB(JLA,JN)
    ICPBT=ICPBT+ICPB(JLA,JN)
  ENDDO
ENDDO

WRITE(NULOUT,*) ' '
WRITE(NULOUT,*) '    TOTAL  TOTAL '
WRITE(NULOUT,*) ' '
WRITE(NULOUT,3001) (JLA,JLA=1,12)
DO JN = 1 , NFLEVG
  WRITE(NULOUT,3004) JN,(ITOT(JLA,JN),JLA=1,12),ITOTV(JN)
ENDDO
WRITE(NULOUT,3005) 'TOT',(ITOTL(JLA),JLA=1,12),ITOTT
WRITE(NULOUT,*) ' '
WRITE(NULOUT,*) ' TOTAUX HAUTS POUR SIGMA P '
WRITE(NULOUT,*) ' '
WRITE(NULOUT,3001) (JLA,JLA=1,12)
DO JN = 1 , NFLEVG
  WRITE(NULOUT,3004) JN,(ICPH(JLA,JN),JLA=1,12),ICPHV(JN)
ENDDO
WRITE(NULOUT,3005) 'TOT',(ICPHL(JLA),JLA=1,12),ICPHT
WRITE(NULOUT,*) ' '
WRITE(NULOUT,*) ' POURCENTAGES HAUTS POUR SIGMA P '
WRITE(NULOUT,*) ' '
WRITE(NULOUT,3001) (JLA,JLA=1,12)
DO JN = 1 , NFLEVG
  WRITE(NULOUT,3002) JN,(100._JPRB*REAL(ICPH(JLA,JN),JPRB)/&
   & REAL(ITOT(JLA,JN),JPRB),JLA=1,12),&
   & 100._JPRB*REAL(ICPHV(JN),JPRB)/REAL(ITOTV(JN),JPRB)
ENDDO
WRITE(NULOUT,3003) 'TOT',(100._JPRB*REAL(ICPHL(JLA),JPRB)/&
 & REAL(ITOTL(JLA),JPRB),JLA=1,12),&
 & 100._JPRB*REAL(ICPHT,JPRB)/REAL(ITOTT,JPRB)

WRITE(NULOUT,*) ' '
WRITE(NULOUT,*) ' TOTAUX BAS POUR SIGMA P '
WRITE(NULOUT,*) ' '
WRITE(NULOUT,3001) (JLA,JLA=1,12)
DO JN = 1 , NFLEVG
  WRITE(NULOUT,3004) JN,(ICPB(JLA,JN),JLA=1,12),ICPBV(JN)
ENDDO
WRITE(NULOUT,3005) 'TOT',(ICPBL(JLA),JLA=1,12),ICPBT
WRITE(NULOUT,*) ' '
WRITE(NULOUT,*) ' POURCENTAGES BAS POUR SIGMA P '
WRITE(NULOUT,*) ' '
WRITE(NULOUT,3001) (JLA,JLA=1,12)
DO JN = 1 , NFLEVG
  WRITE(NULOUT,3002) JN,(100._JPRB*REAL(ICPB(JLA,JN),JPRB)/&
   & REAL(ITOT(JLA,JN),JPRB),JLA=1,12),&
   & 100._JPRB*REAL(ICPBV(JN),JPRB)/REAL(ITOTV(JN),JPRB)
ENDDO
WRITE(NULOUT,3003) 'TOT',(100._JPRB*REAL(ICPBL(JLA),JPRB)/&
 & REAL(ITOTL(JLA),JPRB),JLA=1,12),&
 & 100._JPRB*REAL(ICPBT,JPRB)/REAL(ITOTT,JPRB)
WRITE(NULOUT,*) ' '

3001 FORMAT(7X,12(I2,3X))
3002 FORMAT(1X,I2,2X,12(F4.1,1X),F4.1)
3003 FORMAT(1X,A3,1X,12(F4.1,1X),F4.1)
3004 FORMAT(1X,I2,2X,12(I4,1X),I4)
3005 FORMAT(1X,A3,1X,12(I4,1X),I4)

IF ( LELAM ) THEN
!****** doubly-periodicisation of sigma phi file
!****** Passage a un tableau global pour effectuer la biperiodicisation
!****** puis distribution du nouveau tableau sur les proc

  IPROC = 1
  ZZEPHI(:,:,:)=0.0_JPRB
  ZGESIG(:,:)=0.0_JPRB
  DO JLEV = 1 , NFLEVG
    IF (MYPROC /= IPROC) THEN
      ZAEPHI(:)=ESIG(JLEV,:)
      CALL DIWRGRID_SEND(YDGEOMETRY%YRGEM,IPROC,1,ZAEPHI,JLEV)
    ENDIF
  ENDDO
  IF (MYPROC == IPROC) THEN
    DO JLEV = 1 , NFLEVG
      ZAEPHI(:)=ESIG(JLEV,:)
      CALL DIWRGRID_RECV(YDGEOMETRY,1,ZAEPHI,JLEV,ZGESIG(:,JLEV))
      DO JLA = NDGUNG , NDGUXG
        DO JLON = NDLUNG , NDLUXG
          ZZEPHI(JLON,JLEV,JLA)=ZGESIG(JLON+ISTAGP(JLA)-1,JLEV)
        ENDDO
      ENDDO
    ENDDO
    CALL ETIBIHIE(NDLON,NDGLG,NFLEVG,NDLUXG,NDGUXG,0,&
     & NDLSM,ZZEPHI,.TRUE.,.TRUE.,0)
    DO JLEV = 1 , NFLEVG
      DO JLA = 1 , NDGLG
        DO JLON = 1 , NLOENG(JLA)
          ZGESIG(JLON+ISTAGP(JLA)-1,JLEV) = ZZEPHI(JLON,JLEV,JLA)
        ENDDO
      ENDDO
      CALL DISGRID_SEND(YDGEOMETRY,1,ZGESIG(1,JLEV),JLEV,ZAEPHI)
      ESIG(JLEV,:)=ZAEPHI(:)
    ENDDO
  ENDIF
  DO JLEV = 1 , NFLEVG
    IF (MYPROC /= IPROC) THEN
      CALL DISGRID_RECV(YDGEOMETRY,IPROC,1,ZAEPHI,JLEV)
      ESIG(JLEV,:)=ZAEPHI(:)
    ENDIF
  ENDDO
ENDIF

RSGPHI(:,:)=ESIG(1:NFLEVG,:)

!**--------------------------------------------------------------------------
!**   4.    Quelques petits lissages sur SIGMA P.
!**         ------------------------------------

!**** LISSAGE SUR L'HORIZONTALE
!        - passage espace points de grille --> espace spectral
!        - application fonction filtre pour tronquer le spectre
!        - passage espace spectral --> espace points de grille

DO JN = 1 , NFLEVG
  DO JPG = 1 , NGPTOT
    ZEPHI(JPG,JN)=RSGPHI(JN,JPG)
  ENDDO
ENDDO

IF ( .NOT.LELAM ) THEN

  CALL REESPE(YDGEOMETRY,NFLSUR,NFLEVG,ZIPHI,ZEPHI)

  ZCST = -8._JPRB*LOG(0.1_JPRB)
  ZB   = -0.5_JPRB*ZCST/(NSMAX*NSMAX)
  DO JMLOC = 1 , NUMP
    IM=MYMS(JMLOC)
    DO JN = IM , NSMAX
      IND = NASM0(IM)+(JN-IM)*2
      ZFONC = EXP(ZB*JN*JN)
      DO JLEV = 1 , NFLEVG
        ZIPHI(JLEV,IND) = ZIPHI(JLEV,IND)*ZFONC
        ZIPHI(JLEV,IND+1) = ZIPHI(JLEV,IND+1)*ZFONC
      ENDDO
    ENDDO
  ENDDO

  CALL SPEREE(YDGEOMETRY,NFLSUR,NFLEVG,ZIPHI,ZEPHI)

ELSE

  CALL EREESPE(YDGEOMETRY,NFLSUR,NFLEVG,ZIPHI,ZEPHI)

  ZRBELL = -2.0_JPRB*LOG(0.1_JPRB)
  DO JM = 1 , NUMP
    IM = MYMS(JM)
    DO JN = 0 , YDELAP%NCPLM(IM)-1
      ZARG = (REAL(JN,JPRB)/REAL(NSMAX,JPRB))**2 +&
       & (REAL(IM,JPRB)/REAL(NMSMAX,JPRB))**2
      ZTLUM = EXP(-ZRBELL*ZARG)
      ISTART = YDELAP%NESM0(IM)+4*JN
      IEND   = ISTART + 3
      DO JIND = ISTART , IEND
        DO JLEV = 1 , NFLEVG
          ZIPHI(JLEV,JIND) = ZIPHI(JLEV,JIND)*ZTLUM
        ENDDO
      ENDDO
    ENDDO
  ENDDO

  CALL ESPEREE(YDGEOMETRY,NFLSUR,NFLEVG,ZIPHI,ZEPHI)

ENDIF

DO JN = 1 , NFLEVG
  DO JPG = 1 , NGPTOT
    ESIG(JN,JPG)=ZEPHI(JPG,JN)
  ENDDO
ENDDO

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=ESIG(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA GUE2'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

!**** LISSAGE SUR LA VERTICALE

ZSTR(:)=1.0_JPRB
DO JLA = 1 , NDGENL
  IROF=NSTAGP(JLA)-1
  IF (LELAM) IROF=ISTAGP(JLA)-1
  DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
     & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1

    IROF=IROF+1
    ZLAT=YDGSGEOM_NB%GELAT(IROF)
    ZPRESH(NFLEVG)=101325._JPRB
    CALL GPHPRE(1,NFLEVG,1,1,YDVAB,ZPRESH,PRESF=ZPRESF)

    DO JLEV=1,NFLEVG
      ZPRESF(JLEV) = LOG( ZPRESF(JLEV) )
    ENDDO
    IGPTSK = 1
    CALL CAINSU(ZLAT,ZPRESF,ZSTR,NFLEVG,YLCOST)

    DO JLEV = 1 , NFLEVG

      ILVMNS=MAX(1,JLEV-NCAMNS)
      ILVPLS=MIN(NFLEVG,JLEV+NCAPLS)

      ZPOIDS(:)=0.0_JPRB

      ZKZ1=YLCOST%PPOVZ(JLEV)
      DO JJL = ILVMNS , ILVPLS
        IF (JJL == JLEV) CYCLE
        ZKZ=(YLCOST%PPOVZ(JJL)+ZKZ1)*0.5_JPRB
        ZPOIDS(JJL)=RCAPRP*FV(ZPRESF(JLEV),ZPRESF(JJL),ZKZ)
      ENDDO
      ZPOIDS(JLEV)=1.0_JPRB

      ZPOTOT=0.0_JPRB
      DO JJL = ILVMNS , ILVPLS
        ZPOTOT=ZPOTOT+ZPOIDS(JJL)
      ENDDO

      ESIG(JLEV,IROF)=0.0_JPRB
      DO JJL = ILVMNS , ILVPLS
        ESIG(JLEV,IROF)=ESIG(JLEV,IROF)+ZEPHI(IROF,JJL)*ZPOIDS(JJL)/ZPOTOT
      ENDDO

    ENDDO

  ENDDO
ENDDO

DO JN = 1 , NFLEVG
  DO JLA = 1 , NDGENL
    IROF=NSTAGP(JLA)-1
    IF (LELAM) IROF=ISTAGP(JLA)-1
    DO JLON = NSTA(NPTRFLOFF+JLA,MY_REGION_EW) ,&
       & NSTA(NPTRFLOFF+JLA,MY_REGION_EW)+NONL(NPTRFLOFF+JLA,MY_REGION_EW)-1
      IROF=IROF+1
      ZBID(JLON,JLA,JN)=ESIG(JN,IROF)
    ENDDO
  ENDDO
ENDDO

CLVAR='SIGMA GUEF'

LL=.FALSE.
CALL CAPDGU(YDGSGEOM_NB,YDGEOMETRY%YRGEM,NDLON,NDGENL,NFLEVG,ICALOW,ICAGLN,NCANLI,&
 & ICALOE,ICAGLS,NCANLS,NULOUT,CLVAR,ZBID,LL)

IF (ALLOCATED(ISTAGP)) DEALLOCATE(ISTAGP)

!**--------------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CAISSE',1,ZHOOK_HANDLE)
END SUBROUTINE CAISSE

