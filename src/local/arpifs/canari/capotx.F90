SUBROUTINE CAPOTX(YDCST,ROBHDR,ROBODY,YDGEOMETRY,YDSURF, YDDPHY,YDRIP,YDML_PHY_MF,KTSK, KNBPT,&
 & PUT, PVT, PTT, PQT, PLT, PIT, PRT, PST, PGT, PSPT,&
 & PSP_SB,PSP_SG,PSP_RR,PSP_CI,PSP_X2,PSD_VF,PSD_VV,PSD_VX,&
 & PGM, PGELAT, PGELAM, PMORO, PMLSM, PGNORDL, PGNORDM, PRCORI,&
 & PGEMU, PCAGUE, PESIG, PSGPHI, PSGHUM)

!****-------------------------------------------------------------------
!****   CAPOTX : ROUTINE MENU DE L'ANALYSE
!****   ------
!****-------------------------------------------------------------------

!***  ARGUMENTS : IN      KTSK  : numero de la tache (1 si MP)
!***  ---------   IN      KNBPT : nb de points a traiter
!***              IN/OUT  PUT,PVT,PTT,PQT,PLT,PIT,PGT,PSPT : zoom champs du modele
!***              IN/OUT  PSP_SB,PSP_SG,PSP_RR,PSP_CI,PSP_X2,PSD_VF,PSD_VV,PSD_VX : 
!***                      zoom champs buffer physique du modele
!***              IN      PGM,PGELAT,PGELAM,PGEMU,} geometrie du modele
!***                      PGNORDL,PGNORDM,PRCORI  } sur domaine de travail
!***              IN      PCAGUE,PESIG : zoom entrees analyse
!***              OUT     PSGPHI,PSGHUM: extraits diagnostics
!***
!***  Sous-programme amont : SCAN2MSM - SCAN2M
!***  --------------------
!***  Sous-programmes aval : CACLSI-CACDGU-CANEVA
!***  --------------------   CAPSAX-CAVTAX-CAHUAX-CAT2AS-CAHUAS-CAV1AS
!***                         CASNAS-CASTAS-CACSTS

!***  Auteurs :     P.MOLL 05/92, VC 03/93,08/94
!***  -------

!***  Modifications
!***  -------------
!       R. El Khatib : 01-08-07 Pruning options
!       Y. Bouteloup : add QL and QI  (PLT and PIT !) in the call to caclsi
!       Y. Bouteloup : add PRT and PST
!       F. Taillefer : 16-01-12 use new QACOST structure
!       C. Soci      : 27-02-12 Model orog and lsm for Mesan corr fct added
!       U. Andrae    : 25-04-12 add graupels (PGT) in the call to caclsi
!       K. Yessad (Nov 2012): call GPHPRE.
!       D.Puech     03/2019  Cleaning 
!****-------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMDPHY      , ONLY : TDPHY
USE YOMRIP       , ONLY : TRIP
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE QACTEX   , ONLY : LAEPDS   ,LAEUVT   ,LAEHUM   ,LAET2M   ,&
 & LAEH2M   ,LAEV1M   ,LAESNM   ,LAESST   ,LAEICS   ,&
 & LAECDS   ,RCLIMCA  ,RCLISST
USE QADIAG   , ONLY : RCATSL   ,RCARES   ,RCASIG
USE QADOCK   , ONLY : NMXGBA   ,NMXGQA   ,QDSTRA   ,QDSTVA
USE QACOST   , ONLY : TYPE_QACOST
USE IFS_DBASE_VIEW_MOD, ONLY: IFS_DBASE_VIEW
USE YOMCST, ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TCST)        ,INTENT(IN)    :: YDCST
TYPE(IFS_DBASE_VIEW), INTENT(INOUT) :: ROBHDR,ROBODY
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(TDPHY)       ,INTENT(INOUT) :: YDDPHY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
TYPE(TRIP)        ,INTENT(INOUT) :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KNBPT
INTEGER(KIND=JPIM),INTENT(IN)    :: KTSK
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PIT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PST(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSPT(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SB(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS,YDSURF%YSP_SBD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SG(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_RR(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_CI(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_CID%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSP_X2(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_X2D%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VF(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VV(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VVD%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSD_VX(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VXD%NDIM)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAT(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAM(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMORO(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMLSM(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGNORDL(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGNORDM(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORI(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCAGUE(2,KNBPT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PESIG(2*YDGEOMETRY%YRDIMV%NFLEVG+5,KNBPT)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSGPHI(YDGEOMETRY%YRDIMV%NFLEVG,KNBPT)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSGHUM(YDGEOMETRY%YRDIMV%NFLEVG,KNBPT)
!-----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: INPR(KNBPT), JROF
REAL(KIND=JPRB) :: ZOLDT(KNBPT,YDGEOMETRY%YRDIMV%NFLEVG), ZPSPS(YDGEOMETRY%YRDIM%NPROMA), ZPSLN(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZPRESH(KNBPT,0:YDGEOMETRY%YRDIMV%NFLEVG), ZPRESF(KNBPT,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPRESFLN(KNBPT,YDGEOMETRY%YRDIMV%NFLEVG), ZXXX
REAL(KIND=JPRB) :: ZT2INC(KNBPT), ZH2INC(KNBPT)

TYPE(TYPE_QACOST) :: YLCOST(KNBPT)

INTEGER(KIND=JPIM) :: IQUOI, IANTY, IXX(1)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "cacdgu.intfb.h"
#include "caclsi.intfb.h"
#include "cacsts.intfb.h"
#include "cah2as.intfb.h"
#include "cahuax.intfb.h"
#include "caneva.intfb.h"
#include "capsax.intfb.h"
#include "casnas.intfb.h"
#include "castas.intfb.h"
#include "cat2as.intfb.h"
#include "cav1as.intfb.h"
#include "cavtax.intfb.h"
#include "gphpre.intfb.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('CAPOTX',0,ZHOOK_HANDLE)
ASSOCIATE(NPROMA=>YDGEOMETRY%YRDIM%NPROMA, &
 & NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, LDIRCLSMOD=>YDDPHY%LDIRCLSMOD, &
 & YSD_VFD=>YDSURF%YSD_VFD, YSD_VVD=>YDSURF%YSD_VVD, YSD_VXD=>YDSURF%YSD_VXD, &
 & YSP_CI=>YDSURF%YSP_CI, YSP_CID=>YDSURF%YSP_CID, YSP_RRD=>YDSURF%YSP_RRD, &
 & YSP_SBD=>YDSURF%YSP_SBD, YSP_SG=>YDSURF%YSP_SG, YSP_SGD=>YDSURF%YSP_SGD, &
 & YSP_X2D=>YDSURF%YSP_X2D)

!**------------------------------------------------------------------------
!**  - 1 - PREPARATIFS.
!**        -----------

ZPSPS(1:KNBPT) = EXP(PSPT(1:KNBPT))
ZPSLN(1:KNBPT) = PSPT(1:KNBPT)

! calculs de couche limite
IF (( LAET2M .OR. LAEH2M .OR. LAEV1M .OR. LAEICS ).AND.(.NOT.LDIRCLSMOD)) THEN
  CALL CACLSI(YDCST,YDDPHY,YDML_PHY_MF,YDGEOMETRY%YRVERT_GEOM,YDGEOMETRY%YRDIMV, &
   & YDSURF,KNBPT,NPROMA,PUT,PVT,PTT,PQT,PLT,PIT,PRT,PST,PGT, &
   & ZPSPS,PGM,&
   & PSP_SG,PSP_RR,PSD_VF,PSD_VV,PSP_CI)
ENDIF

IF ( LAEHUM ) THEN
  ZOLDT(1:KNBPT,:) = PTT(1:KNBPT,:)
ENDIF
IQUOI = 0
IANTY  = 0

! selection geographique
CALL CANEVA(ROBHDR,ROBODY,IQUOI,IANTY,KTSK,QDSTRA(1),QDSTVA(1),ZXXX,NMXGBA(1),&
 & NMXGQA(1),ZXXX,PCAGUE,PGELAT,PGELAM,PMORO,PMLSM,ZT2INC,KNBPT,1,&
 & IXX,1,IXX,1,IXX,ZT2INC,ZT2INC,ZT2INC,INPR,YLCOST)

IF ( LAEICS ) THEN
  ZT2INC(:) = 0.0_JPRB
  ZH2INC(:) = 0.0_JPRB
ENDIF

!**------------------------------------------------------------------------
!**  - 2 - ANALYSE de PRESSION de SURFACE.
!**        ------------------------------

IF ( LAEPDS ) THEN
  CALL CACDGU(KTSK,'PS GUESS        ',1,ZPSPS,NPROMA,KNBPT,1,1,PGELAM,PGELAT)
  CALL CAPSAX(ROBHDR,ROBODY,YDGEOMETRY%YRDIMV,YDSURF,NPROMA,KNBPT,KTSK,PSP_CI,&
   & PTT(1,NFLEVG),PQT(1,NFLEVG),ZPSPS,ZPSLN,&
   & PESIG,PCAGUE,PGELAT,PGELAM,PMORO,PMLSM)
  PSPT(1:KNBPT) = ZPSLN(1:KNBPT)

  CALL CACDGU(KTSK,'PS ANALYSE      ',2,ZPSPS,NPROMA,KNBPT,1,1,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'PS TAILLE SL    ',3,RCATSL(1,1,KTSK),NPROMA,KNBPT,1,1,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'PS RES ANA      ',4,RCARES(1,1,KTSK),NPROMA,KNBPT,1,1,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'PS SIGA/SIGP    ',5,RCASIG(1,1,KTSK),NPROMA,KNBPT,1,1,PGELAM,PGELAT)
ENDIF

IF ( LAEUVT .OR. LAEHUM ) THEN
  DO JROF = 1 , KNBPT
    ZPRESH(JROF,NFLEVG) = ZPSPS(JROF)
  ENDDO
  CALL GPHPRE(KNBPT,NFLEVG,1,KNBPT,YDGEOMETRY%YRVAB,ZPRESH,PRESF=ZPRESF)
  ZPRESFLN(1:KNBPT,:) = LOG (ZPRESF(1:KNBPT,:))
ENDIF

!**------------------------------------------------------------------------
!**  - 3 - ANALYSE de VENT/TEMPERATURE.
!**        ---------------------------

IF ( LAEUVT ) THEN
  CALL CACDGU(KTSK,'TT GUESS        ',6,PTT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'UU GUESS        ',7,PUT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'VV GUESS        ',8,PVT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CAVTAX(ROBHDR,ROBODY,YDGEOMETRY%YRDIMV,NPROMA,KNBPT,KTSK,&
   & PUT,PVT,PTT,ZPRESFLN,&
   & PGNORDL,PGNORDM,PGM,PGELAT,PGELAM,PMORO,PMLSM,PRCORI,&
   & PCAGUE,PESIG,PSGPHI)
  CALL CACDGU(KTSK,'TT ANALYSE      ', 9,PTT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'UU ANALYSE      ',10,PUT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'VV ANALYSE      ',11,PVT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'TT TAILLE SL    ',12, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
  CALL CACDGU(KTSK,'TT RES ANA      ',13,RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
  CALL CACDGU(KTSK,'TT SIGA/SIGP    ',14,RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 4 - ANALYSE d'HUMIDITE.
!**        -------------------

IF ( LAEHUM ) THEN
  CALL CACDGU(KTSK,'QQ GUESS        ',15,PQT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CAHUAX(ROBHDR,ROBODY,YDGEOMETRY%YRVAB,YDGEOMETRY%YRDIMV,YDML_PHY_MF%YRPHY,NPROMA,KNBPT,KTSK,&
   & PTT,PQT,ZPRESF,ZPRESFLN,ZOLDT,&
   & PGELAT,PGELAM,PMORO,PMLSM,PCAGUE,PESIG,PSGHUM)
  CALL CACDGU(KTSK,'QQ ANALYSE      ',16,PQT,NPROMA,KNBPT,1,NFLEVG,PGELAM,PGELAT)
  CALL CACDGU(KTSK,'HU TAILLE SL    ',17,RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
  CALL CACDGU(KTSK,'HU RES ANA      ',18,RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
  CALL CACDGU(KTSK,'HU SIGA/SIGP    ',19,RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, NFLEVG, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 5 - ANALYSE de T2M.
!**        --------------

IF ( LAET2M ) THEN
  CALL CACDGU(KTSK, 'T2 GUESS        ',20, PSP_CI(1,YSP_CI%YCI(4)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CAT2AS(ROBHDR,ROBODY,& 
   & YDGEOMETRY%YRDIMV,YDSURF,NPROMA,KNBPT,KTSK,ZPSLN,ZT2INC,PSP_CI,PESIG,PCAGUE,&
   & PGELAT,PGELAM,PMORO,PMLSM)
  CALL CACDGU(KTSK, 'T2 ANALYSE      ',21, PSP_CI(1,YSP_CI%YCI(4)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'T2 TAILLE SL    ',22, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'T2 RES ANA      ',23, RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'T2 SIGA/SIGP    ',24, RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 6 - ANALYSE de H2M.
!**        --------------

IF ( LAEH2M ) THEN
  CALL CACDGU(KTSK, 'H2 GUESS        ',25, PSP_CI(1,YSP_CI%YCI(5)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CAH2AS(ROBHDR,ROBODY, &
   & YDGEOMETRY%YRDIMV,YDSURF,NPROMA,KNBPT,KTSK,ZPSLN,ZH2INC,PSP_CI,PESIG,PCAGUE,&
   & PGELAT,PGELAM,PMORO,PMLSM)
  CALL CACDGU(KTSK, 'H2 ANALYSE      ',26, PSP_CI(1,YSP_CI%YCI(5)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'H2 TAILLE SL    ',27, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'H2 RES ANA      ',28, RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'H2 SIGA/SIGP    ',29, RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 7 - ANALYSE de V10M.
!**        ---------------

IF ( LAEV1M ) THEN
  CALL CACDGU(KTSK, 'V1 GUESS        ',30, PSP_CI(1,YSP_CI%YCI(7)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CAV1AS(ROBHDR,ROBODY,YDGEOMETRY%YRDIMV,YDSURF,NPROMA,KNBPT,KTSK,ZPSLN,PSP_CI,PESIG,PCAGUE,&
   & PGELAT,PGELAM,PMORO,PMLSM,PGNORDL,PGNORDM,PGM)
  CALL CACDGU(KTSK, 'V1 ANALYSE      ',31, PSP_CI(1,YSP_CI%YCI(7)%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'V1 TAILLE SL    ',32, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 8 - ANALYSE de HAUTEUR DE NEIGE.
!**        ---------------------------

IF ( LAESNM ) THEN
  CALL CACDGU(KTSK, 'SN GUESS        ',33, PSP_SG(1,YSP_SG%YF%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CASNAS(ROBHDR,ROBODY, &
   & YDGEOMETRY%YRDIMV,YDSURF,NPROMA,KNBPT,KTSK,ZPSLN,PSP_SG,PSD_VF,PESIG,PCAGUE,&
   & PGELAT,PGELAM,PMORO,PMLSM)
  CALL CACDGU(KTSK, 'SN ANALYSE      ',34, PSP_SG(1,YSP_SG%YF%MP0),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'SN TAILLE SL    ',35, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'SN RES ANA      ',36, RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'SN SIGA/SIGP    ',37, RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
ENDIF

!**------------------------------------------------------------------------
!**  - 9 - ANALYSE de SST.
!**        --------------

IF ( LAESST ) THEN
  CALL CASTAS(ROBHDR,ROBODY, &
   & YDGEOMETRY%YRDIMV,YDSURF,YDML_PHY_MF%YRPHY,NPROMA,KNBPT,KTSK,ZPSLN,PSP_SB,PSP_RR,PSD_VF,PESIG,PCAGUE,&
   & PGELAT,PGELAM,PMORO,PMLSM)
  CALL CACDGU(KTSK, 'SST TAILLE SL   ',50, RCATSL(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'SST RES ANA     ',51, RCARES(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
  CALL CACDGU(KTSK, 'SST SIGA/SIGP   ',52, RCASIG(1,1,KTSK),&
   & NPROMA, KNBPT, 1, 1, PGELAM, PGELAT)
ENDIF

!**--------------------------------------------------------------------------
!**  - 10 - CHAMPS DE SURFACE.
!**         -----------------

IF ( LAEICS .OR. RCLIMCA > 0.0_JPRB .OR. RCLISST > 0.0_JPRB ) THEN
  CALL CACSTS(YDSURF,YDRIP,YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY1,NPROMA,KNBPT,KTSK,ZT2INC,ZH2INC,&
 & PSP_SB,PSP_SG,PSP_RR,PSP_CI,PSP_X2,PSD_VF,PSD_VV,PSD_VX,&
 & PGELAT,PGELAM,PGEMU)
ENDIF

!**--------------------------------------------------------------------------
!**  - 11 - DIAGNOSTICS.
!**         -----------

IF ( LAECDS .AND. .NOT.LDIRCLSMOD ) THEN
  CALL CACLSI(YDCST,YDDPHY,YDML_PHY_MF,YDGEOMETRY%YRVERT_GEOM,YDGEOMETRY%YRDIMV,YDSURF,KNBPT,NPROMA, &
   & PUT,PVT,PTT,PQT,PLT,PIT,PRT,PST,PGT, &
   & ZPSPS,PGM,&
   & PSP_SG,PSP_RR,PSD_VF,PSD_VV,PSP_CI)
ENDIF

!**---------------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CAPOTX',1,ZHOOK_HANDLE)
END SUBROUTINE CAPOTX
