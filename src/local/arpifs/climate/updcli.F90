SUBROUTINE UPDCLI(YDGEOMETRY,YDSURF,YDMODEL,KGP)

!**** *UPDCLI*

!     PURPOSE.
!     --------

!     Updates the climatological data (Message passing version)

!**   INTERFACE.
!     ----------

!     CALL UPDCLI(...)

!     KGP - NUMBER OF CLIMATOLOGICAL FIELDS (VARSF+VCLIP+VCLIV+VCLIS
!                                                       +VCLIA+VCLIG)

!     METHOD.
!     -------

!     Reads the monthly values of SST , deep temperature and water content.
!     For a given day, the value is interpolated linearly between the 15th
!     of the nearest months. Month n (n=01 for January to 12 for December) is
!     read on unit Const.Clim.n . The land index, albedo, emissivity, and
!      roughness are modified when transition between sea and sea-ice occurs.

!     EXTERNALS.
!     ----------

!     UPDCAL
!     CHIEN
!     FAITOU
!     FADIES
!     FACILE
!     FAIRME

!     AUTHORS.
!     --------
!      M. DEQUE  JAN 91 .

!     MODIFICATIONS.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!      A.Alias       05-Jan-2006 GMGEC/EAC modif.
!      P. Marquet (2006): remove lines under LMCC03; desactivate lines under LOZONE
!      M. Deque (2006) allows NVCLIS=1 with LOZONE=.F.
!      M. Deque (2006) sulfate aerosols added
!      M.Hamrud      01-Jul-2006 Revised surface fields
!      N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!      K. Yessad (Jan 2010): externalisation of group EGGX in XRD/IFSAUX
!      R. El Khatib : 23-Apr-2010 use disgrid_mod instead of disgrid
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TCSGLEG
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      K. Yessad (July 2014): Move some variables.
!      R. El Khatib 17-Aug-2016 remove suarpio
!      E.Dutra/G.Arduini (Jan 2018): SP_SG 4D, snow multi-layer 
!     ------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1 , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMLUN   , ONLY : NULOUT
USE YOMVERT  , ONLY : VP00
USE YOMCST   , ONLY : RG
USE YOMCT0   , ONLY : NQUAD    
USE YOMRIP0  , ONLY : NINDAT
USE YOMMP0   , ONLY : MYPROC, NSTRIN, MY_REGION_NS, MY_REGION_EW
USE DISGRID_MOD, ONLY : DISGRID_SEND, DISGRID_RECV 

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)    :: KGP 
!     ------------------------------------------------------------------
INTEGER(KIND=JPIM) :: JPCCL

PARAMETER(JPCCL=20)
REAL(KIND=JPRB) ::      ZREALG(YDGEOMETRY%YRDIM%NDLON*(YDGEOMETRY%YRDIM%NDGLG+2))
REAL(KIND=JPRB) ::      ZFIELDBUF(YDGEOMETRY%YRGEM%NGPTOTG)
REAL(KIND=JPRB) ::      ZFIELD(YDGEOMETRY%YRGEM%NGPTOT,KGP)
INTEGER(KIND=JPIM) ::   IFLD(KGP)
INTEGER(KIND=JPIM) ::   ILMOIS(12),IDATEF(11), IAGDIO(YDSURF%NSURFL)
CHARACTER :: CLNOMC*16, CLNOMM*16, CLNOMF*20, CLSTTO*7, CLSTTF*6
CHARACTER :: CLSUO3*20
CHARACTER :: CLVSF(JPCCL)*20,CLVCP(JPCCL)*20,CLVCV(JPCCL)*20&
 & ,CLVCA(JPCCL)*20,CLOST(JPCCL)*20,CLCST(JPCCL)*20&
 & ,CLVCG(JPCCL)*20  

INTEGER(KIND=JPIM) :: IA0, IAN, ICH, IEND, IFIELD,&
 & IINF, IJ0, IJB1, IJB2, IJOUR, IJT1,&
 & IJT2, IM0, IMOIS, IMT1, IMT2,&
 & INBARI, INBARP, INDEX, INIMES, INULCL,&
 & INULCL1, INULCL2, INULCL3, INUM, INUMLAT,&
 & IBL, IREG, IREP, IROF, IST,&
 & IZAEDES, IZAELAN, IZAESEA, IZAESOO, IZCT1,&
 & IZCT2, IZFALBF, IZFALBSF, IZFLAN, IZFEMISF,&
 & IZFGETRL, IZFLSM, IZFSST, IZFVEG, IZFVRLAN,&
 & IZFVRLDI, IZFZ0F, IZFZ0RLF, IZGCHSS, IZGEVAP,&
 & IZGICFR, IZGIRUP, IZGSOUP, IZGTAUX, IZGTAUY,&
 & IZOA, IZOI, IZOSSG, IZOT, IZPTPC1, IZPTPC2,&
 & IZPWPC1, IZPWPC2, IZVARG, IZVD2, IZVIVEG,&
 & IZVLAI1, IZVLAI2, IZVRSMIN, IZVSAB, IZVZ0H,&
 & JCSS, JFLEV, JGL, JKGLO, JM, JNULCL,&
 & JNUM, JROF, JV, IZAESUL, IZAEVOL, IMAXFIELD

LOGICAL :: LLERFA, LLFICP, LLFICQ, LLIMST, LLNOMM,&
 & LLOP

REAL(KIND=JPRB) :: ZALBAN, ZD2BAN, ZEMBAN, ZEPS, ZPOIB1,&
 & ZPOIB2, ZPOID1, ZPOID2, ZWXBAN, ZWXGLA, ZWXMER,&
 & ZWXSUR, ZZ0BAN, ZZHBAN  
REAL(KIND=JPRB) :: ZHOOK_HANDLE
#ifdef __INTEL_COMPILER
INTRINSIC :: SHAPE ! Fails with Intel compiler as it thinks we use ATLAS shape function
#endif


!     ------------------------------------------------------------------

#include "fcttim.func.h"

!     ------------------------------------------------------------------

#include "chien.h"

#include "abor1.intfb.h"
#include "updcal.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('UPDCLI',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,                    &
& YDVAB=>YDGEOMETRY%YRVAB,   YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY,YDMCC=>YDMODEL%YRML_AOC%YRMCC,   YDRIP=>YDMODEL%YRML_GCONF%YRRIP,&
& YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD,    YDPHY1=>YDMODEL%YRML_PHY_MF%YRPHY1,                &
& YDOZO=>YDMODEL%YRML_CHEM%YROZO)

ASSOCIATE(NTVGLA=>YDPHY1%NTVGLA, ALCRIN=>YDPHY1%ALCRIN, NTVMER=>YDPHY1%NTVMER,   ALBMER=>YDPHY1%ALBMER,                      &
& RD2GLA=>YDPHY1%RD2GLA, RD1=>YDPHY1%RD1,   ALRCN1=>YDPHY1%ALRCN1, WSMX=>YDPHY1%WSMX, RZ0MER=>YDPHY1%RZ0MER,                 &
& WPMX=>YDPHY1%WPMX, RZ0GLA=>YDPHY1%RZ0GLA,   EMCRIN=>YDPHY1%EMCRIN, RZHZ0M=>YDPHY1%RZHZ0M, EMMMER=>YDPHY1%EMMMER,           &
& RZHZ0G=>YDPHY1%RZHZ0G, RD2MER=>YDPHY1%RD2MER, TMERGL=>YDPHY1%TMERGL,   ALBGLA=>YDPHY1%ALBGLA, EMMGLA=>YDPHY1%EMMGLA,       &
& NDGENG=>YDDIM%NDGENG, NDGENL=>YDDIM%NDGENL, NDGLG=>YDDIM%NDGLG,   NDGSAG=>YDDIM%NDGSAG, NDGSAL=>YDDIM%NDGSAL,              &
& NDLON=>YDDIM%NDLON,   NPROMA=>YDDIM%NPROMA, NSMAX=>YDDIM%NSMAX,   NFLEVG=>YDDIMV%NFLEVG,   NTOZ2D=>YDDPHY%NTOZ2D,          &
& NTOZ3D=>YDDPHY%NTOZ3D, NVCLIS=>YDDPHY%NVCLIS,   NAER=>YDERAD%NAER,   NGPTOT=>YDGEM%NGPTOT, NGPTOTG=>YDGEM%NGPTOTG,         &
& NHTYP=>YDGEM%NHTYP,   NLOENG=>YDGEM%NLOENG, NMENG=>YDGEM%NMENG, NSTTYP=>YDGEM%NSTTYP,   RLOCEN=>YDGEM%RLOCEN,              &
& RMUCEN=>YDGEM%RMUCEN, RSTRET=>YDGEM%RSTRET,   LMCC02=>YDMCC%LMCC02,   NFRSTLAT=>YDMP%NFRSTLAT, NLSTLAT=>YDMP%NLSTLAT,      &
& NONL=>YDMP%NONL,   NPTRFLOFF=>YDMP%NPTRFLOFF,   NSTADD=>YDRIP%NSTADD,   SD_VA=>YDSURF%SD_VA, SD_VF=>YDSURF%SD_VF,          &
& SD_VP=>YDSURF%SD_VP,   SD_VV=>YDSURF%SD_VV, SP_RR=>YDSURF%SP_RR, SP_SB=>YDSURF%SP_SB,   SP_SG=>YDSURF%SP_SG,               &
& YSD_VA=>YDSURF%YSD_VA, YSD_VAD=>YDSURF%YSD_VAD,   YSD_VF=>YDSURF%YSD_VF, YSD_VFD=>YDSURF%YSD_VFD, YSD_VP=>YDSURF%YSD_VP,   &
& YSD_VPD=>YDSURF%YSD_VPD, YSD_VV=>YDSURF%YSD_VV, YSD_VVD=>YDSURF%YSD_VVD,   YSP_RR=>YDSURF%YSP_RR, YSP_SB=>YDSURF%YSP_SB,   &
& YSP_SBD=>YDSURF%YSP_SBD,   YSP_SG=>YDSURF%YSP_SG, YSP_SGD=>YDSURF%YSP_SGD,  LSOLV=>YDPHY%LSOLV, LRAYFM=>YDPHY%LRAYFM,      &
& LSNV=>YDPHY%LSNV,   LOZONE=>YDPHY%LOZONE)
!     ------------------------------------------------------------------

!     1. SETTING CONSTANT VALUES.
!     ---------------------------

!*    1.1 Arpege files

CLVSF(1)   =  'SURFZ0.FOIS.G   '
CLVSF(2)   =  'SURFALBEDO      '
CLVSF(3)   =  'SURFEMISSIVITE  '
CLVSF(4)   =  'SURFET.GEOPOTENT'
CLVSF(5)   =  'SURFIND.TERREMER'
CLVSF(6)   =  'SURFPROP.VEGETAT'
CLVSF(7)   =  'SURFVAR.GEOP.ANI'
CLVSF(8)   =  'SURFVAR.GEOP.DIR'
CLVSF(9)   =  'SURFZ0REL.FOIS.G'
CLVSF(10)  =  'SURFALBEDO.COMPL'
CLVSF(11)  =  'SURFPROP.TERRE'
CLVSF(12)  =  '-RESERVED-'
CLVCP(1)   =  'RELATEMPERATURE '
CLVCP(2)   =  'RELAPROP.RMAX.EA'
CLVCV(1)   =  'SURFIND.VEG.DOMI'
CLVCV(2)   =  'SURFRESI.STO.MIN'
CLVCV(3)   =  'SURFPROP.ARGILE '
CLVCV(4)   =  'SURFPROP.SABLE  '
CLVCV(5)   =  'SURFEPAIS.SOL   '
CLVCV(6)   =  'SURFIND.FOLIAIRE'
CLVCV(7)   =  'SURFGZ0.THERM'
CLVCA(1)   =  'SURFAEROS.SEA'
CLVCA(2)   =  'SURFAEROS.LAND'
CLVCA(3)   =  'SURFAEROS.SOOT'
CLVCA(4)   =  'SURFAEROS.DESERT'
CLVCA(5)   =  'SURFAEROS.SULFAT'
CLVCA(6)   =  'SURFAEROS.VOLCAN'
CLOST(1)   =  'SURFTEMPOCEAN   '
CLOST(2)   =  'SURFINDOCEAN    '
CLOST(3)   =  'ALBEDO.ICE.OCEAN'
CLCST(1)   =  'SURFTEMPERATURE '
CLVCG(1)   =  'SEICFRACTION'
CLVCG(2)   =  'SEICRAY.SO.UP'
CLVCG(3)   =  'SEICRAY.IR.UP'
CLVCG(4)   =  'SEICCHAL.SENS'
CLVCG(5)   =  'SEICEVAPORATION'
CLVCG(6)   =  'SEICU.STRESS'
CLVCG(7)   =  'SEICV.STRESS'
LLNOMM=.TRUE.
CLSTTO='OLD'
CLSTTF='KEEP'
LLERFA=.TRUE.
LLIMST=.FALSE.
INIMES=1
INBARP=14
CLNOMC='Cadre.Clim'
CLNOMM='Cadre.Coup'
LLFICQ=.FALSE.
LLFICP=.FALSE.
INULCL1=0
INULCL2=0
INULCL3=0
LLOP=.FALSE.

!*    1.2 Positions in ZFIELD

IZFZ0F=1
IZFALBF=2
IZFEMISF=3
IZFGETRL=4
IZFLSM=5
IZFVEG=6
IZFVRLAN=7
IZFVRLDI=8
IZFZ0RLF=9
IZFALBSF=10
IZFLAN=11
IZFSST=12
IF (YSD_VFD%NUMFLDS < 8) THEN
  WRITE(NULOUT,'('' ERROR YSD_VFD%NUMFLDS TOO SMALL'')')
  CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
ENDIF
IF (YSD_VFD%NUMFLDS < 10.AND.LSNV) THEN
  WRITE(NULOUT,'('' ERROR YSD_VFD%NUMFLDS TOO SMALL FOR SNOW'')')
  CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
ENDIF
IZPTPC1=1
IZPWPC1=2
IF (YSD_VPD%NUMFLDS < 2) THEN
  WRITE(NULOUT,'('' ERROR YSD_VPD%NUMFLDS TOO SMALL'')')
  CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
ENDIF
IZVIVEG=1
IZVRSMIN=2
IZVARG=3
IZVSAB=4
IZVD2=5
IZVLAI1=6
IZVLAI2=7
IZVZ0H=8
IF (YSD_VVD%NUMFLDS < 6.AND.LSOLV) THEN
  WRITE(NULOUT,'('' ERROR YSD_VVD%NUMFLDS TOO SMALL'')')
  CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
ENDIF
IZAESEA=1
IZAELAN=2
IZAESOO=3
IZAEDES=4
IZAESUL=5
IZAEVOL=6
IZGICFR=1
IZGSOUP=2
IZGIRUP=3
IZGCHSS=4
IZGEVAP=5
IZGTAUX=6
IZGTAUY=7
IZOT=1
IZOI=2
IZOA=3
IZOSSG=4
IZCT1=1
IZCT2=2

!*    1.3 Calendar

IJ0=NDD(NINDAT)
IM0=NMM(NINDAT)
IA0=NCCAA(NINDAT)
IJOUR=1
IMOIS=1
IAN  =1
CALL UPDCAL (IJ0,IM0,IA0,NSTADD,IJOUR,IMOIS,IAN,ILMOIS,NULOUT)
IF (IJOUR > 15) THEN
  IMT1=IMOIS
  IMT2=1+MOD(IMOIS,12)
  IJT1=15
  IJT2=15+ILMOIS(IMT1)
  IJB1=15
  IJB2=ILMOIS(IMT1)
ELSE
  IMT1=1+MOD(IMOIS+10,12)
  IMT2=IMOIS
  IJT1=15-ILMOIS(IMT1)
  IJT2=15
  IJB1=0
  IJB2=15
ENDIF

!*    1.4 Physics

!        Definition of physical properties of bare sea-ice (GLA) and
!        open sea (MER). When the sea-ice is considered as sea (LMCC02=.F.),
!        the snow reservoir is no more used, and thus the physical
!        properties should include a "mean" snow cover; we introduce
!        thus the sea-ice properties (BAN).

!       Albedo
ZALBAN=(ALBGLA+ALCRIN)*0.5_JPRB
!       Emissivity
ZEMBAN=(EMMGLA+EMCRIN)*0.5_JPRB
!       Dynamical roughness length
ZZ0BAN=(RZ0GLA+ALRCN1)*0.5_JPRB
!       Thermal roughness length
ZZHBAN=ZZ0BAN
!       Soil depth
ZD2BAN=RD2GLA
!       Reservoirs
IF (LSOLV) THEN
  ZWXMER=1000._JPRB*RD2MER
  ZWXGLA=1000._JPRB*RD2GLA
  ZWXBAN=1000._JPRB*ZD2BAN
  ZWXSUR=1000._JPRB*RD1
ELSE
  ZWXMER=WPMX
  ZWXGLA=WPMX
  ZWXBAN=WPMX
  ZWXSUR=WSMX
ENDIF

!*    2. READING MONTHLY VALUES.
!     --------------------------

INUMLAT=NLSTLAT(MY_REGION_NS)-NFRSTLAT(MY_REGION_NS)+1

IF (MYPROC <= NSTRIN) THEN

!*    2.1 Opening Arpege files : month 1

  WRITE(UNIT=CLNOMF,FMT='(''Const.Clim.'',I2.2)') IMT1
  DO JNULCL=1,99
    INQUIRE(UNIT=JNULCL,OPENED=LLOP)
    IF (.NOT.LLOP) EXIT
  ENDDO
  INULCL1=JNULCL
  CALL FAITOU (IREP,INULCL1,LLNOMM,CLNOMF,CLSTTO,&
   & LLERFA,LLIMST,INIMES,INBARP,INBARI,CLNOMC)  
  ZEPS=1.E-10_JPRB
  IINF=0
  CALL CHIEN (CLNOMC,NSTTYP,RMUCEN,RLOCEN,RSTRET,&
   & NSMAX,NDGLG,NDLON,NLOENG,NMENG,NHTYP,NFLEVG,&
   & VP00,YDVAB%VALH,YDVAB%VBH,NQUAD,IINF,NDGSAG,NDGENG,&
   & ZEPS,LLFICP,NULOUT)  
  IF (.NOT.LLFICP) THEN
    CALL ABOR1 ('UPDCLI: ABOR1 CALLED: LLFICP=.F.')
  ENDIF
  CALL FADIES (IREP,INULCL1,IDATEF)
  IF (IDATEF(2) /= IMT1) THEN
    WRITE(NULOUT,'('' ERROR IDATEF '',11I6/'' IMT1 '',I4)')IDATEF,IMT1
    CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
  ENDIF

!*    2.2 Opening Arpege files : month 2

  WRITE(UNIT=CLNOMF,FMT='(''Const.Clim.'',I2.2)') IMT2
  DO JNULCL=1,99
    INQUIRE(UNIT=JNULCL,OPENED=LLOP)
    IF (.NOT.LLOP) EXIT
  ENDDO
  INULCL2=JNULCL
  CALL FAITOU (IREP,INULCL2,LLNOMM,CLNOMF,CLSTTO,&
   & LLERFA,LLIMST,INIMES,INBARP,INBARI,CLNOMC)  
  CALL FADIES (IREP,INULCL2,IDATEF)
  IF (IDATEF(2) /= IMT2) THEN
    WRITE(NULOUT,'('' ERROR IDATEF '',11I6/'' IMT2 '',I4)')IDATEF,IMT2
    CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
  ENDIF

ENDIF   ! MYPROC <= NSTRIN

!*    2.4 Indices for horizontal loops

LLFICP=.TRUE.
IF(LLFICP)THEN
  INDEX=NLOENG(1)+1
ELSE
  INDEX=1
ENDIF

! Reading and Sending fields

INULCL=INULCL1
IFIELD=0
INUM=0

IMAXFIELD=SIZE(IAGDIO)
DO JNUM=1,IMAXFIELD
  IAGDIO(JNUM)=MOD(JNUM-1,NSTRIN)+1
ENDDO

!*    2.5 Reading VARSF

DO JV=1,YSD_VFD%NUMFLDS
  IFIELD=IFIELD+1
  IF (YSD_VF%YVF(JV)%CNAME /= '-RESERVED-') THEN
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF (MYPROC == IAGDIO(INUM)) THEN
      CALL FACILE (IREP,INULCL,CLVSF(JV)(1:4),1,CLVSF(JV)(5:),ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      WRITE(NULOUT,'(2X,A,'' READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') CLVSF(JV),IFIELD  
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
  ENDIF
ENDDO

!*    2.6 Reading VCLIP 

IZPTPC1=IFIELD+1+(IZPTPC1-1)*2
IZPTPC2=IZPTPC1+1
IZPWPC1=IFIELD+1+(IZPWPC1-1)*2
IZPWPC2=IZPWPC1+1
DO JV=1,2 !(YSD_VPD%NUMFLDS=2)
  DO JM=1,2 !(two months)
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
    IF (JM == 1) THEN
      INULCL=INULCL1
    ELSE
      INULCL=INULCL2
    ENDIF
    IFLD(INUM)=IFIELD
    IF (MYPROC == IAGDIO(INUM)) THEN
      CALL FACILE (IREP,INULCL,CLVCP(JV)(1:4),1,CLVCP(JV)(5:),ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      WRITE(NULOUT,'(2X,A,'' READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') CLVCP(JV),IFIELD  
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
  ENDDO   ! JM=1,2
ENDDO   ! JV=1,2

IF (LSOLV) THEN

!*    2.7 Reading VCLIV
!*    In fact VCLIV(7) is not set up here (but in CPG)
!*            VCLIV(6) needs two monthly fields

  IZVIVEG=IFIELD+IZVIVEG
  IZVRSMIN=IFIELD+IZVRSMIN
  IZVARG=IFIELD+IZVARG
  IZVSAB=IFIELD+IZVSAB
  IZVD2=IFIELD+IZVD2
  IZVLAI1=IFIELD+IZVLAI1
  IZVLAI2=IFIELD+IZVLAI2
  IZVZ0H=IFIELD+IZVZ0H

  INULCL=INULCL1
  DO JV=1,6
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF (MYPROC == IAGDIO(INUM)) THEN
      CALL FACILE (IREP,INULCL,CLVCV(JV)(1:4),1,CLVCV(JV)(5:),ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      WRITE(NULOUT,'(2X,'' READ FROM ARPEGE FILE '',A,&
       & '' INTO VCLIV '',I2.2,'' FIELD: '',I3.3)')&
       & CLVCV(JV),JV,IFIELD  
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
  ENDDO   !JV=1,6

  INULCL=INULCL2
  IFIELD=IFIELD+1
  INUM=INUM+1
  IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
  IFLD(INUM)=IFIELD
  IF (MYPROC == IAGDIO(INUM)) THEN
    CALL FACILE (IREP,INULCL,CLVCV(6)(1:4),1,CLVCV(6)(5:),ZREALG,.FALSE.)
    ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
    WRITE(NULOUT,'(2X,'' READ FROM ARPEGE FILE '',A,&
     & '' INTO VCLIV JV=7 , FIELD: '',I3.3)')&
     & CLVCV(6),IFIELD  
    CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
  ENDIF

  INULCL=INULCL1
  DO JV=8,YSD_VVD%NUMFLDS
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF (MYPROC == IAGDIO(INUM)) THEN
      CALL FACILE (IREP,INULCL,CLVCV(7)(1:4),1,CLVCV(7)(5:),ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      WRITE(NULOUT,'(2X,'' READ FROM ARPEGE FILE '',A,&
       & '' INTO VCLIV '',I2.2,'' FIELD: '',I3.3)')&
       & CLVCV(7),JV,IFIELD  
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
  ENDDO     !JV=8,ETC...

ENDIF    !IF (LSOLV)

!*    2.78 Reading VCLIA

IZAESEA=IFIELD+IZAESEA
IZAELAN=IFIELD+IZAELAN
IZAESOO=IFIELD+IZAESOO
IZAEDES=IFIELD+IZAEDES
IZAESUL=IFIELD+IZAESUL
IZAEVOL=IFIELD+IZAEVOL
IF (LRAYFM.AND.NAER /= 0.AND.YSD_VAD%NUMFLDS < 4) THEN
  WRITE(NULOUT,'('' ERROR LRAYFM REQUIRES AEROSOLS '')')
  CALL ABOR1 ('UPDCLI: ABOR1 CALLED')
ENDIF
INULCL=INULCL1
DO JV=1,YSD_VAD%NUMFLDS
  IFIELD=IFIELD+1
  INUM=INUM+1
  IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
  IFLD(INUM)=IFIELD
  IF (MYPROC == IAGDIO(INUM)) THEN
    CALL FACILE(IREP,INULCL,CLVCA(JV)(1:4),1,CLVCA(JV)(5:),ZREALG,.FALSE.)
    ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
    WRITE(NULOUT,'(2X,A,'' READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') CLVCA(JV),IFIELD  
    CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
  ENDIF
ENDDO   !JV=1,YSD_VAD%NUMFLDS

!*       2.9 Reading surface temperature from climatology (2 months)

DO JV=1,1
  DO JM=1,2
    IF (JM == 1) THEN
      INULCL=INULCL1
      IZCT1=IFIELD+IZCT1
    ELSE
      INULCL=INULCL2
      IZCT2=IZCT1+1
    ENDIF
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF (MYPROC == IAGDIO(INUM)) THEN
      CALL FACILE (IREP,INULCL,CLCST(JV)(1:4),1,CLCST(JV)(5:),&
       & ZREALG,.FALSE.)  
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      WRITE(NULOUT,'(2X,A,'' READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') CLCST(JV),IFIELD  
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
  ENDDO   ! JM=1,2
ENDDO   ! JV=1,1

!* Receiving the first part set of fields

CALL FLUSH(NULOUT)

DO JNUM=1,INUM
  IF (MYPROC /= IAGDIO(JNUM)) THEN
    IFIELD=IFLD(JNUM)
    CALL DISGRID_RECV(YDGEOMETRY,IAGDIO(JNUM),1,ZFIELD(1,IFIELD),JNUM)
  ENDIF
ENDDO

!*    3. TIME INTERPOLATION.

ZPOID1=REAL(IJT2-IJOUR,JPRB)/REAL(IJT2-IJT1,JPRB)
ZPOID2=1.0_JPRB-ZPOID1
ZPOIB1=REAL(IJB2-IJOUR,JPRB)/REAL(IJB2-IJB1,JPRB)
ZPOIB2=1.0_JPRB-ZPOIB1

!*    3.1   Reads the buffers


DO JKGLO=1,NGPTOT,NPROMA
  IST=1
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1

!*    3.3   Fills the buffers with climatology :
!     Roughness length (*G)
!     Albedo
!     Emissivity
!     Orography st. dev. (*G)
!     Continentality (1 or 0)
!     Fraction of continent
!     Sea surface temperature (for mixed land/sea points)
!     Fraction of vegetation
!     Anisotropy of orography variance tensor
!     Main direction orography variance tensor
!     Dominant type of vegetation
!     Minimal stomatal resistance
!     Fraction of clay
!     Fraction of sand
!     Depth of soil column
!     Thermal roughness length (if any)
!     Aerosols (sea)
!     Aerosols (land)
!     Aerosols (soot)
!     Aerosols (desert)
!     Sea-ice fraction (only when coupling)
!     Sea-ice upward solar flux
!     Sea-ice upward I.R.  flux
!     Sea-ice sensible heat
!     Sea-ice evaporation
!     Sea-ice stress (U)
!     Sea-ice stress (V)

  DO JROF=IST,IEND
    IROF=JROF+JKGLO-1
    SD_VF(JROF,YSD_VF%YZ0F%MP,IBL   )=ZFIELD(IROF,IZFZ0F  )
    SD_VF(JROF,YSD_VF%YALBF%MP,IBL  )=ZFIELD(IROF,IZFALBF )
    SD_VF(JROF,YSD_VF%YEMISF%MP,IBL )=ZFIELD(IROF,IZFEMISF)
    SD_VF(JROF,YSD_VF%YGETRL%MP,IBL )=ZFIELD(IROF,IZFGETRL)
    SD_VF(JROF,YSD_VF%YLSM%MP,IBL   )=ZFIELD(IROF,IZFLSM  )
    SD_VF(JROF,YSD_VF%YVEG%MP,IBL   )=ZFIELD(IROF,IZFVEG  )
    SD_VF(JROF,YSD_VF%YVRLAN%MP,IBL )=ZFIELD(IROF,IZFVRLAN)
    SD_VF(JROF,YSD_VF%YVRLDI%MP,IBL )=ZFIELD(IROF,IZFVRLDI)
    IF (LSNV) THEN
      SD_VF(JROF,YSD_VF%YZ0RLF%MP,IBL  )=ZFIELD(IROF,IZFZ0RLF)
      SD_VF(JROF,YSD_VF%YALBSF%MP,IBL  )=ZFIELD(IROF,IZFALBSF)
    ENDIF
    IF (LSOLV) THEN
      SD_VV(JROF,YSD_VV%YIVEG%MP,IBL  )=NINT(ZFIELD(IROF,IZVIVEG ))
      SD_VV(JROF,YSD_VV%YRSMIN%MP,IBL )=ZFIELD(IROF,IZVRSMIN)
      SD_VV(JROF,YSD_VV%YARG%MP,IBL   )=ZFIELD(IROF,IZVARG  )
      SD_VV(JROF,YSD_VV%YSAB%MP,IBL   )=ZFIELD(IROF,IZVSAB  )
      SD_VV(JROF,YSD_VV%YD2%MP,IBL    )=ZFIELD(IROF,IZVD2   )
      IF (YSD_VVD%NUMFLDS >= 8) THEN
        SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=ZFIELD(IROF,IZVZ0H  )
      ENDIF
    ENDIF
    IF (YSD_VAD%NUMFLDS >= 4) THEN
      SD_VA(JROF,YSD_VA%YSEA%MP,IBL )=ZFIELD(IROF,IZAESEA )
      SD_VA(JROF,YSD_VA%YLAN%MP,IBL )=ZFIELD(IROF,IZAELAN )
      SD_VA(JROF,YSD_VA%YSOO%MP,IBL )=ZFIELD(IROF,IZAESOO )
      SD_VA(JROF,YSD_VA%YDES%MP,IBL )=ZFIELD(IROF,IZAEDES )
    ENDIF
    IF (YSD_VAD%NUMFLDS >= 5) THEN
      SD_VA(JROF,YSD_VA%YSUL%MP,IBL )=ZFIELD(IROF,IZAESUL )
    ENDIF
    IF (YSD_VAD%NUMFLDS >= 6) THEN
      SD_VA(JROF,YSD_VA%YVOL%MP,IBL )=ZFIELD(IROF,IZAEVOL )
    ENDIF


!*    3.4   Updates some fields :
!      Climatological deep soil temperature (land,sea,sea-ice)
!      Climatological deep soil reservoir (land,sea,sea-ice)
!      Leaf area index (land,sea,sea-ice)
!      Continentality (sea,sea-ice). The sea-ice is considered as land
!      Roughness length  (sea,sea-ice)
!      Albedo  (sea,sea-ice)
!      Emissivity  (sea,sea-ice)
!      Depth of soil column (sea,sea-ice)
!      Dominant type of vegetation (sea,sea-ice)
!      Surface temperature   (sea)
!      Deep soil temperature (sea)
!      Surface water reservoir (sea,sea-ice)
!      Deep soil water reservoir (sea,sea-ice)
!      Snow depth (sea)

    IF (SD_VF(JROF,YSD_VF%YLSM%MP,IBL) == 1.0_JPRB) THEN

!      Land

      SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=ZFIELD(IROF,IZPTPC1)*ZPOID1&
       & +ZFIELD(IROF,IZPTPC2)*ZPOID2  
      SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=(ZFIELD(IROF,IZPWPC1)*ZPOID1&
       & +ZFIELD(IROF,IZPWPC2)*ZPOID2)*WPMX  
              IF (LSOLV) THEN
                SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=ZFIELD(IROF,IZVLAI1)*ZPOID1&
         & +ZFIELD(IROF,IZVLAI2)*ZPOID2  
      ENDIF
    ELSE
!      Sea or Sea Ice : uncoupled mode
      IF (ZFIELD(IROF,IZCT1) <= TMERGL&
         & .AND.ZFIELD(IROF,IZCT2) <= TMERGL) THEN  
!      Sea Ice to Sea Ice
        SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=1.0_JPRB
        SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0GLA*RG
        SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBGLA
        SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMGLA
        SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
        SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXGLA
        SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=TMERGL
        SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
        IF (YSP_SBD%NLEVS >= 3) THEN
          SP_SB(JROF,2,YSP_SB%YT%MP0,IBL)=TMERGL
        ENDIF
        IF (LSOLV) THEN
          SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVGLA,JPRB)+.1_JPRB
          SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2GLA
          SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
          IF (YSD_VVD%NUMFLDS >= 8) THEN
            SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0G*RZ0GLA*RG
          ENDIF
        ENDIF
      ENDIF
      IF (ZFIELD(IROF,IZCT1) <= TMERGL&
         & .AND.ZFIELD(IROF,IZCT2) > TMERGL) THEN  
!            Sea Ice to Sea
        IF (IJOUR > 15) THEN
!            Sea Ice
          SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=1.0_JPRB
          SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0GLA*RG
          SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBGLA
          SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMGLA
          SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
          SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXGLA
          SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=TMERGL
          SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
          IF (LSOLV) THEN
            SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVGLA,JPRB)+.1_JPRB
            SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2GLA
            SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
            IF (YSD_VVD%NUMFLDS >= 8) THEN
              SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0G*RZ0GLA*RG
            ENDIF
          ENDIF
        ELSE
!            Sea
          SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=0.0_JPRB
          SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0MER*RG
          SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBMER
          SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMMER
          SP_RR(JROF,YSP_RR%YT%MP0,IBL)=TMERGL*ZPOIB1+ZFIELD(IROF,IZCT2)*ZPOIB2
          DO JCSS=1,YSP_SBD%NLEVS
            SP_SB(JROF,JCSS,YSP_SB%YT%MP0,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
          ENDDO
          SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
          SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXMER
          SP_SG(JROF,1:YSP_SGD%NLEVS,YSP_SG%YF%MP0,IBL)=0.0_JPRB
          SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
          SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
          IF (LSOLV) THEN
            SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVMER,JPRB)
            SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2MER
            SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
            IF (YSD_VVD%NUMFLDS >= 8) THEN
              SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0M*RZ0MER*RG
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      IF (ZFIELD(IROF,IZCT1) > TMERGL&
         & .AND.ZFIELD(IROF,IZCT2) <= TMERGL) THEN  
!      Sea to Sea Ice
        IF (IJOUR > 15) THEN
!            Sea
          SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=0.0_JPRB
          SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0MER*RG
          SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBMER
          SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMMER
          SP_RR(JROF,YSP_RR%YT%MP0,IBL)=ZFIELD(IROF,IZCT1)*ZPOIB1+TMERGL*ZPOIB2
          DO JCSS=1,YSP_SBD%NLEVS
            SP_SB(JROF,JCSS,YSP_SB%YT%MP0,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
          ENDDO
          SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
          SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXMER
          SP_SG(JROF,1:YSP_SGD%NLEVS,YSP_SG%YF%MP0,IBL)=0.0_JPRB
          SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
          SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
          IF (LSOLV) THEN
            SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVMER,JPRB)
            SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2MER
            SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
            IF (YSD_VVD%NUMFLDS >= 8) THEN
              SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0M*RZ0MER*RG
            ENDIF
          ENDIF
        ELSE
!            Sea Ice
          SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=1.0_JPRB
          SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0GLA*RG
          SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBGLA
          SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMGLA
          SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
          SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXGLA
          SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=TMERGL
          SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
          IF (YSP_SBD%NLEVS >= 3) THEN
            SP_SB(JROF,2,YSP_SB%YT%MP0,IBL)=TMERGL
          ENDIF
          IF (LSOLV) THEN
            SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVGLA,JPRB)+.1_JPRB
            SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2GLA
            SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
            IF (YSD_VVD%NUMFLDS >= 8) THEN
              SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0G*RZ0GLA*RG
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      IF (ZFIELD(IROF,IZCT1) > TMERGL&
         & .AND.ZFIELD(IROF,IZCT2) > TMERGL) THEN  
!     Sea to Sea
        SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=0.0_JPRB
        SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=RZ0MER*RG
        SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ALBMER
        SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=EMMMER
        SP_RR(JROF,YSP_RR%YT%MP0,IBL)=ZFIELD(IROF,IZCT1)*ZPOID1&
         & +ZFIELD(IROF,IZCT2)*ZPOID2  
        DO JCSS=1,YSP_SBD%NLEVS
          SP_SB(JROF,JCSS,YSP_SB%YT%MP0,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
        ENDDO
        SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
        SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXMER
        SP_SG(JROF,1:YSP_SGD%NLEVS,YSP_SG%YF%MP0,IBL)=0.0_JPRB
        SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
        SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
        IF (LSOLV) THEN
          SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVMER,JPRB)
          SD_VV(JROF,YSD_VV%YD2%MP,IBL)=RD2MER
          SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
          IF (YSD_VVD%NUMFLDS >= 8) THEN
            SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=RZHZ0M*RZ0MER*RG
          ENDIF
        ENDIF
      ENDIF
      IF (.NOT.LMCC02.AND.SD_VF(JROF,YSD_VF%YLSM%MP,IBL) == 1) THEN
!            Correction for sea-ice parameters when sea-ice
!            temperature and reservoirs are not modified by Arpege
!            but the atmosphere "sees" the ice below 
        SD_VF(JROF,YSD_VF%YLSM%MP,IBL)=0.0_JPRB
        SD_VF(JROF,YSD_VF%YZ0F%MP,IBL)=ZZ0BAN*RG
        SD_VF(JROF,YSD_VF%YALBF%MP,IBL)=ZALBAN
        SD_VF(JROF,YSD_VF%YEMISF%MP,IBL)=ZEMBAN
        SP_RR(JROF,YSP_RR%YW%MP0,IBL)=ZWXSUR
        SP_SB(JROF,1,YSP_SB%YQ%MP0,IBL)=ZWXBAN
        SP_SG(JROF,1:YSP_SGD%NLEVS,YSP_SG%YF%MP0,IBL)=0.0_JPRB
        SD_VP(JROF,YSD_VP%YTPC%MP,IBL)=TMERGL
        SD_VP(JROF,YSD_VP%YWPC%MP,IBL)=WPMX
        IF (LSOLV) THEN
          SD_VV(JROF,YSD_VV%YIVEG%MP,IBL)=REAL(NTVGLA,JPRB)+.1_JPRB
          SD_VV(JROF,YSD_VV%YD2%MP,IBL)=ZD2BAN
          SD_VV(JROF,YSD_VV%YLAI%MP,IBL)=0.0_JPRB
          IF (YSD_VVD%NUMFLDS >= 8) THEN
            SD_VV(JROF,YSD_VV%YZ0H%MP,IBL)=ZZHBAN
          ENDIF
        ENDIF
!               Uncoupled mode
        SP_RR(JROF,YSP_RR%YT%MP0,IBL)=ZFIELD(IROF,IZCT1)*ZPOID1&
         & +ZFIELD(IROF,IZCT2)*ZPOID2  
        DO JCSS=1,YSP_SBD%NLEVS
          SP_SB(JROF,JCSS,YSP_SB%YT%MP0,IBL)=SP_RR(JROF,YSP_RR%YT%MP0,IBL)
        ENDDO
      ENDIF
    ENDIF       ! SD_VF(JROF,YSD_VF%YLSM%MP,IBL) == 1.
  ENDDO       ! END OF JROF=IST,IEND

ENDDO  ! END OF JKGLO=1,NGPTOT,NPROMA

IF (LOZONE.OR.(NVCLIS == 1)) THEN

!*    4. FIELDS FOR OZONE PARAMETERIZATION (IF ANY).

  IF (NVCLIS /= 7.AND.LOZONE) THEN 
    WRITE(NULOUT,*) ' WRONG NVCLIS (it must be 7) : ', NVCLIS
    CALL ABOR1('SUGRIDA : ABOR1 CALLED (wrong NVCLIS)')
  ENDIF
  IF (NVCLIS*NTOZ3D > 0) THEN
    IF (.NOT.ALLOCATED(YDOZO%TOZ3DBL)) THEN
      ALLOCATE(YDOZO%TOZ3DBL(0:NGPTOT,NFLEVG*NVCLIS*NTOZ3D))
      WRITE(NULOUT,9990) 'TOZ3DBL  ',SIZE(YDOZO%TOZ3DBL),SHAPE(YDOZO%TOZ3DBL)
    ENDIF
  ENDIF
  IF (NVCLIS*NTOZ2D > 0) THEN
    IF (.NOT.ALLOCATED(YDOZO%TOZ2DL)) THEN
      ALLOCATE(YDOZO%TOZ2DL(NDGSAL:NDGENL,NFLEVG*NVCLIS*NTOZ2D))
      WRITE(NULOUT,9990) 'TOZ2DL   ',SIZE(YDOZO%TOZ2DL),SHAPE(YDOZO%TOZ2DL)
    ENDIF
  ENDIF

!* Sending data

  DO JFLEV=1,NFLEVG

    INUM=0
    IFIELD=0
    DO JV=1,NVCLIS
      WRITE(UNIT=CLSUO3,FMT='(''OZONE.A'',I1)')JV
      DO JM=1,2
        IFIELD=IFIELD+1
        IF (JM == 1)  THEN

!*         2.1 Opening Arpege files : month 1

          INULCL=INULCL1
        ELSE

!*         2.2 Opening Arpege files : month 2

          INULCL=INULCL2
        ENDIF
        INUM=INUM+1
        IF (INUM > KGP) CALL ABOR1('UPDCLI: INUM > KGP')
        IFLD(INUM)=IFIELD
        IF (MYPROC == IAGDIO(INUM)) THEN
          CALL FACILE(IREP,INULCL,'S',JFLEV,CLSUO3,ZREALG,.FALSE.)
          ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)

          WRITE(NULOUT,'('' OZONE.A'',I1,'' LEVEL '',I3,&
           & '' RECEIVE     FROM ARPEGE FILE''/&
           & '' FOR 2 CONSECUTIVE MONTHS'')') IFIELD,JFLEV  

          CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
        ENDIF
      ENDDO   ! JM=1,2
    ENDDO   ! JV=1,NVCLIS

!* Receiving data

    DO JNUM=1,INUM
      IF (MYPROC /= IAGDIO(JNUM)) THEN
        IFIELD=IFLD(JNUM)
        CALL DISGRID_RECV(YDGEOMETRY,IAGDIO(JNUM),1,ZFIELD(1,IFIELD),JNUM)
      ENDIF
    ENDDO

!*    4.3 Fills the common

    DO JV=1,NVCLIS
      ICH=(JV-1)*NFLEVG+JFLEV
      IF (NTOZ3D == 1) THEN
!             FULL 3D COMMON
        DO JROF=1,NGPTOT
          YDOZO%TOZ3DBL(JROF,ICH)=ZFIELD(JROF,2*JV-1)*ZPOID1&
           & +ZFIELD(JROF,2*JV)*ZPOID2  
        ENDDO
      ENDIF
      IF (NTOZ2D == 1) THEN
!             LatitudexLevel COMMON
        IF (NSTTYP /= 1) THEN
          WRITE(NULOUT,&
           & FMT='('' OZONE INCONSISTENCY : NTOZ2D='',I4&
           & ,'' NSTTYP='',I4)')NTOZ2D,NSTTYP  
          CALL ABOR1('UPDCLI: ABOR1 CALLED')
        ENDIF
        IROF=1
        DO JGL=1,INUMLAT
          YDOZO%TOZ2DL(JGL,ICH)=ZFIELD(IROF,2*JV-1)*ZPOID1&
           & +ZFIELD(IROF,2*JV)*ZPOID2  
          IROF=IROF+NONL(NPTRFLOFF+JGL,MY_REGION_EW)
        ENDDO
      ENDIF
    ENDDO

  ENDDO   ! JLEV=1,NFLEVG

ENDIF   ! LOZONE=.T.

9990 FORMAT(1X,'ARRAY ',A10,' ALLOCATED ',8I8)

IF (MYPROC <= NSTRIN) THEN
  INQUIRE(UNIT=INULCL1,OPENED=LLOP)
  IF (LLOP) CALL FAIRME (IREG,INULCL1,CLSTTF)
  INQUIRE(UNIT=INULCL2,OPENED=LLOP)
  IF (LLOP) CALL FAIRME (IREG,INULCL2,CLSTTF)
  IF (INULCL3 /= 0) THEN
    INQUIRE(UNIT=INULCL3,OPENED=LLOP)
    IF (LLOP) CALL FAIRME (IREG,INULCL3,CLSTTF)
  ENDIF
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('UPDCLI',1,ZHOOK_HANDLE)
END SUBROUTINE UPDCLI

