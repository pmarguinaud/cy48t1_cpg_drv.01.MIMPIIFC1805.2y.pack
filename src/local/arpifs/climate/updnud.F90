SUBROUTINE UPDNUD(YDGEOMETRY,YDRIP,KGP)

!**** *UPDNUD*

!     PURPOSE.
!     --------

!     Updates the reference fields for nudging - distributed version

!**   INTERFACE.
!     ----------

!     CALL UPDNUD(...)
!                   KGP = number of grid-point fields for nudging (IN)

!     METHOD.
!     -------
!     Reads the analyses for 7 6h-ranges and fills the spectral and gridpoint
!      arrays

!     EXTERNALS.
!     ----------

!     AUTHORS.
!     --------
!      M. DEQUE  JUL 96 .

!     MODIFICATIONS.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M. Deque   05-06-20 : grid point nudging                
!      M.Hamrud      01-Jul-2006 Revised surface fields
!      A. Alias   30-10-2007 : Bug on setting up XVU[..] fields fixed
!                              Distribution of Spectral fields : 1 sender / N receivers
!      K. Yessad 16-04-2009: replace DISSPEC by DISPPEC0 + cleanings
!      A. Alias  07-08-2009 : No use of NAGDIO anymore for distribution to
!                             overcome the limit of MAXFIELD
!                             Use of NFRNUDG instead of NFRHIS
!      K. Yessad (Jan 2010): externalisation of group EGGX in XRD/IFSAUX
!      R. El Khatib : 23-Apr-2010 use disgrid_mod instead of disgrid
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TCSGLEG
!      T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!      K. Yessad (July 2014): Move some variables.
! ----------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : NQUAD
USE YOMCST   , ONLY : RDAY
USE YOMVERT  , ONLY : VP00
USE YOMLUN   , ONLY : NULOUT
USE YOMNUD   , ONLY : NFNUDG, NFRNUDG, LNUDDI, LNUDLP, LNUDRM, LNUDSD,&
 & LNUDSH, LNUDSM, LNUDST, LNUDTE, LNUDVO, XNUDDI, XNUDLP, XNUDRM, XNUDSD,&
 & XNUDSH, XNUDSM, XNUDST, XNUDSV, XNUDTE, XNUDVO, LNUDTG, LNUDQG, LNUDUG,&
 & LNUDVG, LNUDPG, XNUDTG, XNUDQG, XNUDUG, XNUDVG, XNUDPG, XVUST, XVUSM,&
 & XVURM, XVUSD, XVUTG, XVUQG, XVUUG, XVUVG, XVUPG  
USE YOMRIP0  , ONLY : NINDAT, NSSSSS
USE YOMRIP   , ONLY : TRIP
USE YOMSNU   , ONLY : TNUDTE, TNUDSH, TNUDDI, TNUDVO, TNUDLP
USE YOMMP0   , ONLY : NSTRIN, MYPROC, MYSETB, NPRTRW
USE DISGRID_MOD, ONLY : DISGRID_SEND, DISGRID_RECV

! ----------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TRIP)        ,INTENT(INOUT) :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KGP 

! ----------------------------------------------------------------------------

REAL(KIND=JPRB) :: ZREALG(YDGEOMETRY%YRDIM%NDLON*(YDGEOMETRY%YRDIM%NDGLG+2))
REAL(KIND=JPRB) :: ZFIELDBUF(YDGEOMETRY%YRGEM%NGPTOTG)
REAL(KIND=JPRB) :: ZFIELD(YDGEOMETRY%YRGEM%NGPTOT,KGP)
REAL(KIND=JPRB) :: ZSPECG(YDGEOMETRY%YRDIM%NSPEC2G),ZSPECBUF(YDGEOMETRY%YRDIM%NSPEC2G+1)
REAL(KIND=JPRB) :: ZSPEC(YDGEOMETRY%YRDIM%NSPEC2)
INTEGER(KIND=JPIM) :: ILMOIS(12),IDATEF(11)
INTEGER(KIND=JPIM) :: IFLD(KGP)
CHARACTER :: CLNOMC*16, CLNOMF*14, CLSTTO*7, CLSTTF*6

INTEGER(KIND=JPIM) :: IA0, IAN, IBSET, IDAY, IEND, IFIELD,&
 & IHOUR, IINC, IINF, IIYEAR, IJ0, IJOUR, ILOCLV,&
 & IM, IM0, IMOIS, IMONTH, INBARI,&
 & INBARP, INCRH, IND, INDEX, INIMES, INIVAU,&
 & INULCL, INUM, IBLK, IREP, IROF, ISP,&
 & IST, IYEAR, JKGLO, JLEV, JM, JN, JNULCL,&
 & JNUM, JROF, JSTEP, IOPROC, INSPEC

LOGICAL :: LLERFA, LLFICP, LLIMST, LLNOMM, LLOP

REAL(KIND=JPRB) ::  ZEPS 
REAL(KIND=JPRB) :: ZHOOK_HANDLE

! ----------------------------------------------------------------------------

#include "fcttim.func.h"

#include "chien.h"

#include "abor1.intfb.h"
#include "disspec0.intfb.h"
#include "updcal.intfb.h"

! ----------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('UPDNUD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,    &
& YDLAP=>YDGEOMETRY%YRLAP, YDVAB=>YDGEOMETRY%YRVAB)
ASSOCIATE(NDGENG=>YDDIM%NDGENG, NDGLG=>YDDIM%NDGLG, NDGSAG=>YDDIM%NDGSAG,   NDLON=>YDDIM%NDLON, NGPBLKS=>YDDIM%NGPBLKS, &
& NPROMA=>YDDIM%NPROMA,   NSMAX=>YDDIM%NSMAX, NSPEC2=>YDDIM%NSPEC2, NSPEC2G=>YDDIM%NSPEC2G,   NFLEVG=>YDDIMV%NFLEVG,    &
& NGPTOT=>YDGEM%NGPTOT, NGPTOTG=>YDGEM%NGPTOTG, NHTYP=>YDGEM%NHTYP,   NLOENG=>YDGEM%NLOENG, NMENG=>YDGEM%NMENG,         &
& NSTTYP=>YDGEM%NSTTYP,   RLOCEN=>YDGEM%RLOCEN, RMUCEN=>YDGEM%RMUCEN, RSTRET=>YDGEM%RSTRET,   NVALUE=>YDLAP%NVALUE,     &
& RLAPDI=>YDLAP%RLAPDI,   NBSETLEV=>YDMP%NBSETLEV, NBSETSP=>YDMP%NBSETSP, NDIM0G=>YDMP%NDIM0G,   NPOSSP=>YDMP%NPOSSP,   &
& NPTRLL=>YDMP%NPTRLL,   NSTADD=>YDRIP%NSTADD, NSTASS=>YDRIP%NSTASS, TSTEP=>YDRIP%TSTEP)
! ----------------------------------------------------------------------------


! Processor number to send Spectral fields and Grid fields
IOPROC=1

!*
!     1. SETTING CONSTANT VALUES.
!     ---------------------------


!*    1.1 Arpege files
LLNOMM=.TRUE.
CLSTTO='OLD'
CLSTTF='KEEP'
LLERFA=.TRUE.
LLIMST=.FALSE.
INIMES=1
INBARP=14
CLNOMC='Cadre.Ana'
INIVAU=0
LLFICP=.FALSE.

!*    1.2 Calendar

IJ0=NDD(NINDAT)
IM0=NMM(NINDAT)
IA0=NCCAA(NINDAT)
INCRH=(NFRNUDG*NINT(TSTEP))/3600
IJOUR=1
IMOIS=1
IAN  =1
CALL UPDCAL(IJ0,IM0,IA0,NSTADD,IJOUR,IMOIS,IAN,ILMOIS,NULOUT)
IHOUR=(NSSSSS*24)/NINT(RDAY)
IDAY=IJOUR
IMONTH=IMOIS
IYEAR=IAN
IF(NFNUDG == 7) THEN
  IHOUR=IHOUR-INCRH
ELSEIF (NFNUDG == 2) THEN
  IHOUR=IHOUR+(NSTASS*24)/NINT(RDAY)
ENDIF
IF(IHOUR < 0) THEN
  IHOUR=IHOUR+24
  IINC=-1
  CALL UPDCAL(IDAY,IMONTH,IYEAR,IINC,IJOUR,IMOIS,IAN,ILMOIS,NULOUT)
  IDAY=IJOUR
  IMONTH=IMOIS
  IYEAR=IAN
ENDIF
IF(IHOUR >= 24) THEN
  IHOUR=IHOUR-24
  IINC=1
  CALL UPDCAL(IDAY,IMONTH,IYEAR,IINC,IJOUR,IMOIS,IAN,ILMOIS,NULOUT)
  IDAY=IJOUR
  IMONTH=IMOIS
  IYEAR=IAN
ENDIF

!*
!     2. READING 6-HOURLY VALUES.
!     ---------------------------
IF(LNUDST)THEN
  IF(.NOT.ALLOCATED(XVUST)) ALLOCATE(XVUST(NPROMA,NFNUDG,NGPBLKS))
ENDIF
IF(LNUDSM)THEN
  IF(.NOT.ALLOCATED(XVUSM)) ALLOCATE(XVUSM(NPROMA,NFNUDG,NGPBLKS))
ENDIF
IF(LNUDRM)THEN
  IF(.NOT.ALLOCATED(XVURM)) ALLOCATE(XVURM(NPROMA,NFNUDG,NGPBLKS))
ENDIF
IF(LNUDSD)THEN
  IF(.NOT.ALLOCATED(XVUSD)) ALLOCATE(XVUSD(NPROMA,NFNUDG,NGPBLKS))
ENDIF
IF(LNUDTG)THEN
  IF(.NOT.ALLOCATED(XVUTG)) ALLOCATE(XVUTG(NPROMA,NFNUDG,NFLEVG,NGPBLKS))
ENDIF
IF(LNUDQG)THEN
  IF(.NOT.ALLOCATED(XVUQG)) ALLOCATE(XVUQG(NPROMA,NFNUDG,NFLEVG,NGPBLKS))
ENDIF
IF(LNUDUG)THEN
  IF(.NOT.ALLOCATED(XVUUG)) ALLOCATE(XVUUG(NPROMA,NFNUDG,NFLEVG,NGPBLKS))
ENDIF
IF(LNUDVG)THEN
  IF(.NOT.ALLOCATED(XVUVG)) ALLOCATE(XVUVG(NPROMA,NFNUDG,NFLEVG,NGPBLKS))
ENDIF
IF(LNUDPG)THEN
  IF(.NOT.ALLOCATED(XVUPG)) ALLOCATE(XVUPG(NPROMA,NFNUDG,NGPBLKS))
ENDIF

!*    2.1 Beginning of the loop on the 7 (cubic) or 2 (linear) steps

DO JSTEP=1,NFNUDG
WRITE(NULOUT,'('' NUDGING FIELDS: STEP'',I2)')JSTEP
IIYEAR=MOD(IYEAR,100)

  IF (MYPROC <= NSTRIN) THEN

    DO JNULCL=1,99
      INQUIRE(UNIT=JNULCL,OPENED=LLOP)
      IF (.NOT.LLOP) EXIT
    ENDDO
    INULCL=JNULCL
    WRITE(UNIT=CLNOMF,FMT='(''RX'',I4.4,I2.2,I2.2,I2.2)')&
     & IYEAR,IMONTH,IDAY,IHOUR
    WRITE(NULOUT,     FMT='(''RX'',I4.4,I2.2,I2.2,I2.2)')&
     & IYEAR,IMONTH,IDAY,IHOUR
    CALL FAITOU (IREP,INULCL,LLNOMM,CLNOMF,CLSTTO,LLERFA,LLIMST,&
     & INIMES,INBARP,INBARI,CLNOMC)
    ZEPS=1.E-10_JPRB
    IINF=0

    CALL CHIEN (CLNOMC,NSTTYP,RMUCEN,RLOCEN,RSTRET,&
     & NSMAX,NDGLG,NDLON,NLOENG,NMENG,NHTYP,NFLEVG,&
     & VP00,YDVAB%VALH,YDVAB%VBH,NQUAD,IINF,NDGSAG,NDGENG,&
     & ZEPS,LLFICP,NULOUT)

    CALL FADIES(IREP,INULCL,IDATEF)
    WRITE(NULOUT,FMT='(''IDATEF'',11I5)')IDATEF
    IF(((XNUDSH > 0.0_JPRB).OR.(XNUDTE > 0.0_JPRB).OR.(XNUDDI > 0.0_JPRB)&
     & .OR.(XNUDVO > 0.0_JPRB).OR.(XNUDSV > 0.0_JPRB).OR.(XNUDLP > 0.0_JPRB)&
     & .OR.(XNUDST > 0.0_JPRB).OR.(XNUDSM > 0.0_JPRB).OR.(XNUDRM > 0.0_JPRB)&
     & .OR.(XNUDSD > 0.0_JPRB)&
     & .OR.(XNUDTG > 0.0_JPRB).OR.(XNUDQG > 0.0_JPRB).OR.(XNUDUG > 0.0_JPRB)&
     & .OR.(XNUDVG > 0.0_JPRB).OR.(XNUDPG > 0.0_JPRB)&
     & ).AND.((IDATEF(1) /= IYEAR).OR.(IDATEF(2) /= IMONTH)&
     & .OR.(IDATEF(3) /= IDAY).OR.(IDATEF(4) /= IHOUR)&
     & ))THEN
     WRITE(NULOUT,'('' WARNING DATE FILE: '',11I5/'' ACTUAL DATE: '',4I5)')&
      & IDATEF,IYEAR,IMONTH,IDAY,IHOUR
    ENDIF
!      Increment of the date in the analyses
!       IHOUR=IHOUR+6
    IHOUR=IHOUR+INCRH
    IF(IHOUR >= 24) THEN
      IHOUR=IHOUR-24
      IINC=1
      CALL UPDCAL(IDAY,IMONTH,IYEAR,IINC,IJOUR,IMOIS,IAN,ILMOIS,NULOUT)
      IDAY=IJOUR
      IMONTH=IMOIS
      IYEAR=IAN
    ENDIF
  ENDIF   ! MYPROC

!*    2.3 Indices for horizontal loops

  INDEX=1
  IFIELD=0
  INUM=0

!*    2.4 Reading VCLIU

!* Sending the first part set of fields

  IF(LNUDST)THEN
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'SURF',1,'TEMPERATURE ',ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
    WRITE(NULOUT,&
     & '(2X,''SURFTEMPERATURE READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') IFIELD
  ENDIF

  IF(LNUDSM)THEN
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'SURF',1,'RESERV.EAU  ',ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
    WRITE(NULOUT,&
     & '(2X,''SURFRESERV.EAU READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') IFIELD
  ENDIF

  IF(LNUDRM)THEN
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'PROF',1,'RESERV.EAU ',ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
    WRITE(NULOUT,&
     & '(2X,''PROFRESERV.EAU READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') IFIELD
  ENDIF

  IF(LNUDSD)THEN
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'SURF',1,'RESERV.NEIGE',ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
    WRITE(NULOUT,&
     & '(2X,''SURFRESERV.NEIGE READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') IFIELD
  ENDIF

  IF(LNUDTG)THEN
    DO JLEV=1,NFLEVG
      IFIELD=IFIELD+1
      INUM=INUM+1
      IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
      IFLD(INUM)=IFIELD
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'TEMPERATURE',ZREALG,.FALSE.)
        ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
        CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
      ENDIF
      WRITE(NULOUT,&
       & '(2X,''S'',I3,''TEMPERATURE  READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') JLEV,IFIELD
    ENDDO
  ENDIF

  IF(LNUDQG)THEN
    DO JLEV=1,NFLEVG
      IFIELD=IFIELD+1
      INUM=INUM+1
      IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
      IFLD(INUM)=IFIELD
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'HUMI.SPECIFI',ZREALG,.FALSE.)
        ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
        CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
      ENDIF
      WRITE(NULOUT,&
       & '(2X,''S'',I3,''HUMI.SPECIFI READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') JLEV,IFIELD
    ENDDO
  ENDIF

  IF(LNUDUG)THEN
    DO JLEV=1,NFLEVG
      IFIELD=IFIELD+1
      INUM=INUM+1
      IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
      IFLD(INUM)=IFIELD
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'VENT_ZONAL ',ZREALG,.FALSE.)
        ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
        CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
      ENDIF
      WRITE(NULOUT,&
       & '(2X,''S'',I3,''VENT_ZONAL   READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') JLEV,IFIELD
    ENDDO
  ENDIF

  IF(LNUDVG)THEN
    DO JLEV=1,NFLEVG
      IFIELD=IFIELD+1
      INUM=INUM+1
      IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
      IFLD(INUM)=IFIELD
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'VENT_MERIDIE',ZREALG,.FALSE.)
        ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
        CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
      ENDIF
      WRITE(NULOUT,&
       & '(2X,''S'',I3,''VENT_MERIDIE READ FROM ARPEGE FILE'',&
       & '' FIELD:'',I3)') JLEV,IFIELD
    ENDDO
  ENDIF

  IF(LNUDPG)THEN
    IFIELD=IFIELD+1
    INUM=INUM+1
    IF (INUM > KGP) CALL ABOR1('UPDNUD: INUM > KGP')
    IFLD(INUM)=IFIELD
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'SURF',1,'PRESSION',ZREALG,.FALSE.)
      ZFIELDBUF=ZREALG(INDEX:NGPTOTG+INDEX-1)
      CALL DISGRID_SEND(YDGEOMETRY,1,ZFIELDBUF,INUM,ZFIELD(:,IFIELD))
    ENDIF
    WRITE(NULOUT,&
     & '(2X,''SURFPRESSION  READ FROM ARPEGE FILE'',&
     & '' FIELD:'',I3)') IFIELD
  ENDIF

!* Receiving the first part set of fields

  DO JNUM=1,INUM
    IF (MYPROC /= IOPROC) THEN
      IFIELD=IFLD(JNUM)
      CALL DISGRID_RECV(YDGEOMETRY,IOPROC,1,ZFIELD(1,IFIELD),JNUM)
    ENDIF
  ENDDO
!*
!     3. BUFFERS.
!     -----------

!*    3.1  Reads the buffers

  DO JKGLO=1,NGPTOT,NPROMA
    IST=1
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBLK=(JKGLO-1)/NPROMA+1

!*    3.2  Fills the buffers

    DO JROF=IST,IEND
      IROF=JROF+JKGLO-1
      IFIELD=0
      IF(LNUDST)THEN
        IFIELD=IFIELD+1
        XVUST(JROF,JSTEP,IBLK)=ZFIELD(IROF,IFIELD)
      ENDIF
      IF(LNUDSM)THEN
        IFIELD=IFIELD+1
        XVUSM(JROF,JSTEP,IBLK)=ZFIELD(IROF,IFIELD)
      ENDIF
      IF(LNUDRM)THEN
        IFIELD=IFIELD+1
        XVURM(JROF,JSTEP,IBLK)=ZFIELD(IROF,IFIELD)
      ENDIF
      IF(LNUDSD)THEN
        IFIELD=IFIELD+1
        XVUSD(JROF,JSTEP,IBLK)=ZFIELD(IROF,IFIELD)
      ENDIF
      IF(LNUDTG)THEN
        DO JLEV=1,NFLEVG
          IFIELD=IFIELD+1
          XVUTG(JROF,JSTEP,JLEV,IBLK)=ZFIELD(IROF,IFIELD)
        ENDDO
      ENDIF
      IF(LNUDQG)THEN
        DO JLEV=1,NFLEVG
          IFIELD=IFIELD+1
          XVUQG(JROF,JSTEP,JLEV,IBLK)=ZFIELD(IROF,IFIELD)
        ENDDO
      ENDIF
      IF(LNUDUG)THEN
        DO JLEV=1,NFLEVG
          IFIELD=IFIELD+1
          XVUUG(JROF,JSTEP,JLEV,IBLK)=ZFIELD(IROF,IFIELD)
        ENDDO
      ENDIF
      IF(LNUDVG)THEN
        DO JLEV=1,NFLEVG
          IFIELD=IFIELD+1
          XVUVG(JROF,JSTEP,JLEV,IBLK)=ZFIELD(IROF,IFIELD)
        ENDDO
      ENDIF
      IF(LNUDPG)THEN
        IFIELD=IFIELD+1
        XVUPG(JROF,JSTEP,IBLK)=ZFIELD(IROF,IFIELD)
      ENDIF
    ENDDO ! JROF
  ENDDO !JKGLO




!*
!     4. SPECTRAL FIELDS.
!     -------------------
  INSPEC=1
  IF(LNUDLP)THEN
    INUM=(INSPEC-1)*NFLEVG
    INUM=INUM+1
    IF(MYPROC == IOPROC) THEN
      CALL FACILE(IREP,INULCL,'SURF',1,'PRESSION  ',ZSPECG,.TRUE.)
      WRITE(NULOUT,'(2X,''PRESSION'','' READ FROM ARPEGE FILE'')')
      IND=0
      DO JN=0,NSMAX
        DO JM=-JN,JN
          IM = ABS(JM)
          IF(JM < 0) THEN
            ISP = NDIM0G(IM)+(JN-IM)*2+1
          ELSE
            ISP = NDIM0G(IM)+(JN-IM)*2
          ENDIF
          IND=IND+1
          ZSPECBUF(ISP)=ZSPECG(IND)
          IF (JM == 0) ZSPECBUF(ISP+1)=0.0_JPRB
        ENDDO
      ENDDO
      ! Send
      CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,0,NSPEC2G,1,NPOSSP,NPRTRW,ZSPECBUF,&
       & LDC=.TRUE.,KNUM=INUM)
      IF(NBSETSP == MYSETB) THEN
        TNUDLP(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),JSTEP)=&
         & ZSPECBUF(NPOSSP(MYPROC):NPOSSP(MYPROC+1)-1)
      ENDIF
    ELSE
      ! Receive
      IF(NBSETSP == MYSETB) THEN
        CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,1,NSPEC2,1,NPOSSP,NPRTRW,ZSPEC,&
         & LDC=.TRUE.,KNUM=INUM)
        TNUDLP(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),JSTEP)=&
         & ZSPEC(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC))
      ENDIF
    ENDIF
    INSPEC=INSPEC+1
  ENDIF
  IF(LNUDTE)THEN
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'TEMPERATURE',ZSPECG,.TRUE.)
        WRITE(NULOUT,'(2X,''TEMPERATURE LEV'',I3,&
         & '' READ FROM ARPEGE FILE'')') JLEV
        IND=0
        DO JN=0,NSMAX
          DO JM=-JN,JN
            IM = ABS(JM)
            IF(JM < 0) THEN
              ISP = NDIM0G(IM)+(JN-IM)*2+1
            ELSE
              ISP = NDIM0G(IM)+(JN-IM)*2
            ENDIF
            IND=IND+1
            ZSPECBUF(ISP)=ZSPECG(IND)
            IF (JM == 0) ZSPECBUF(ISP+1)=0.0_JPRB
          ENDDO
        ENDDO
        CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,0,NSPEC2G,1,NPOSSP,NPRTRW,ZSPECBUF,&
         & LDC=.TRUE.,KNUM=INUM)
        IF(IBSET == MYSETB) THEN
          TNUDTE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPECBUF(NPOSSP(MYPROC):NPOSSP(MYPROC+1)-1)  
        ENDIF
      ENDIF
    ENDDO
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC /= IOPROC) THEN
        IF(IBSET == MYSETB) THEN
          CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,1,NSPEC2,1,NPOSSP,NPRTRW,ZSPEC,&
           & LDC=.TRUE.,KNUM=INUM)
          TNUDTE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPEC(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC))  
        ENDIF
      ENDIF
    ENDDO
    INSPEC=INSPEC+1
  ENDIF
  IF(LNUDSH)THEN
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'HUMI.SPECIFI',ZSPECG,.TRUE.)
        WRITE(NULOUT,'(2X,''HUMI.SPECIFI LEV'',I3,&
         & '' READ FROM ARPEGE FILE'')') JLEV  
        IND=0
        DO JN=0,NSMAX
          DO JM=-JN,JN
            IM = ABS(JM)
            IF(JM < 0) THEN
              ISP = NDIM0G(IM)+(JN-IM)*2+1
            ELSE
              ISP = NDIM0G(IM)+(JN-IM)*2
            ENDIF
            IND=IND+1
            ZSPECBUF(ISP)=ZSPECG(IND)
            IF (JM == 0) ZSPECBUF(ISP+1)=0.0_JPRB
          ENDDO
        ENDDO
        CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,0,NSPEC2G,1,NPOSSP,NPRTRW,ZSPECBUF,&
         & LDC=.TRUE.,KNUM=INUM)
        IF(IBSET == MYSETB) THEN
          TNUDSH(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPECBUF(NPOSSP(MYPROC):NPOSSP(MYPROC+1)-1)  
        ENDIF
      ENDIF
    ENDDO
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC /= IOPROC) THEN
        IF(IBSET == MYSETB) THEN
          CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,1,NSPEC2,1,NPOSSP,NPRTRW,ZSPEC,&
           & LDC=.TRUE.,KNUM=INUM)
          TNUDSH(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPEC(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC))  
        ENDIF
      ENDIF
    ENDDO
    INSPEC=INSPEC+1
  ENDIF
  IF(LNUDVO)THEN
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'FONC.COURANT',ZSPECG,.TRUE.)
        WRITE(NULOUT,'(2X,''FONC.COURANT LEV'',I3,&
         & '' READ FROM ARPEGE FILE'')') JLEV  
        IND=0
        DO JN=0,NSMAX
          DO JM=-JN,JN
            IM = ABS(JM)
            IF(JM < 0) THEN
              ISP = NDIM0G(IM)+(JN-IM)*2+1
            ELSE
              ISP = NDIM0G(IM)+(JN-IM)*2
            ENDIF
            IND=IND+1
            ZSPECBUF(ISP)=ZSPECG(IND)
            IF (JM == 0) ZSPECBUF(ISP+1)=0.0_JPRB
          ENDDO
        ENDDO
        CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,0,NSPEC2G,1,NPOSSP,NPRTRW,ZSPECBUF,&
         & LDC=.TRUE.,KNUM=INUM)
        IF(IBSET == MYSETB) THEN
          TNUDVO(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPECBUF(NPOSSP(MYPROC):NPOSSP(MYPROC+1)-1)&
           & *RLAPDI(NVALUE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC)))  
        ENDIF
      ENDIF
    ENDDO
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC /= IOPROC) THEN
        IF(IBSET == MYSETB) THEN
          CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,1,NSPEC2,1,NPOSSP,NPRTRW,ZSPEC,&
           & LDC=.TRUE.,KNUM=INUM)
          TNUDVO(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPEC(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC))&
           & *RLAPDI(NVALUE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC)))  
        ENDIF
      ENDIF
    ENDDO
    INSPEC=INSPEC+1
  ENDIF
  IF(LNUDDI)THEN
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC == IOPROC) THEN
        CALL FACILE(IREP,INULCL,'S',JLEV,'POT.VITESSE',ZSPECG,.TRUE.)
        WRITE(NULOUT,'(2X,''POT.VITESSE LEV'',I3,&
         & '' READ FROM ARPEGE FILE'')') JLEV  
        IND=0
        DO JN=0,NSMAX
          DO JM=-JN,JN
            IM = ABS(JM)
            IF(JM < 0) THEN
              ISP = NDIM0G(IM)+(JN-IM)*2+1
            ELSE
              ISP = NDIM0G(IM)+(JN-IM)*2
            ENDIF
            IND=IND+1
            ZSPECBUF(ISP)=ZSPECG(IND)
            IF (JM == 0) ZSPECBUF(ISP+1)=0.0_JPRB
          ENDDO
        ENDDO
        CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,0,NSPEC2G,1,NPOSSP,NPRTRW,ZSPECBUF,&
         & LDC=.TRUE.,KNUM=INUM)
        IF(IBSET == MYSETB) THEN
          TNUDDI(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPECBUF(NPOSSP(MYPROC):NPOSSP(MYPROC+1)-1)&
           & *RLAPDI(NVALUE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC)))  
        ENDIF
      ENDIF
    ENDDO
    INUM=(INSPEC-1)*NFLEVG
    DO JLEV=1,NFLEVG
      IBSET=NBSETLEV(JLEV)
      ILOCLV=JLEV-NPTRLL(NBSETLEV(JLEV))+1
      INUM=INUM+1
      IF(MYPROC /= IOPROC) THEN
        IF(IBSET == MYSETB) THEN
          CALL DISSPEC0(YDGEOMETRY,IOPROC,NBSETSP,1,1,NSPEC2,1,NPOSSP,NPRTRW,ZSPEC,&
           & LDC=.TRUE.,KNUM=INUM)
          TNUDDI(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC),ILOCLV,JSTEP)=&
           & ZSPEC(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC))&
           & *RLAPDI(NVALUE(:NPOSSP(MYPROC+1)-NPOSSP(MYPROC)))  
        ENDIF
      ENDIF
    ENDDO
    INSPEC=INSPEC+1
  ENDIF
!*
!     5. FILE CLOSING.
!     ----------------

  IF(MYPROC <= NSTRIN) THEN
    CALL FAIRME (IREP,INULCL,CLSTTF)
  ENDIF

!      End of the loop on the analysis steps
ENDDO !JSTEP

! ----------------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('UPDNUD',1,ZHOOK_HANDLE)
END SUBROUTINE UPDNUD

