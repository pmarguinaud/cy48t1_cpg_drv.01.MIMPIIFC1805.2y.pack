SUBROUTINE CPREP4

USE PARKIND1 , ONLY : JPIM     ,JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK    ,DR_HOOK
USE YOMLUN          , ONLY : NULOUT, NULNAM, NULERR
USE YOMMP0          , ONLY : NOUTTYPE, LMPOFF, MYPROC
USE MODEL_MOD       , ONLY : MODEL, MODEL_CREATE, MODEL_DELETE, MODEL_STEP
USE FIELDS_MOD      , ONLY : FIELDS, FIELDS_CREATE, FIELDS_DELETE, FIELDS_ZEROS, &
 & FIELDS_ADD, FIELDS_COPY, FIELDS_SUB
USE FIELDS_IO_MOD   , ONLY : READ_FIELDS
USE VARIABLES_MOD   , ONLY : VARIABLES, VARIABLES_CREATE, VARIABLES_DELETE
USE GEOMETRY_MOD    , ONLY : GEOMETRY, GEOMETRY_DELETE
USE IOSTREAM_MIX    , ONLY : CLOSE_IOSTREAM, Y_IOSTREAM_FDB, IOSTREAM_STATS
USE FULLPOS         , ONLY : TFPOS, TFPDATA
USE YOMCT0          , ONLY : CNMEXP
USE YOMVAR          , ONLY : MUPTRA
USE YOMOPH0         , ONLY : CFNISH
USE DATETIME_TMP_MOD, ONLY : DATETIME_TMP
USE GEOMETRY_SETUP_MOD, ONLY : GEOMETRY_SETUP
USE YOMCLI          , ONLY : YRCLI
USE MPL_MODULE

IMPLICIT NONE

CHARACTER(LEN=16) :: CLNAM
CHARACTER(LEN=8)  :: CLNUL

INTEGER(KIND=JPIM) :: ITER, IFPOS, IMMCLI, IOS, IFILE, ISECS, JSTEP, JV, JKGLO, IEND, IBL

LOGICAL :: LLOPENED, LLINEAR, LLUPDSUW

REAL(KIND=JPRB) :: ZT ! apparently dummy from model_create

TYPE(GEOMETRY)     :: YRGEOMETRY
TYPE(MODEL)        :: YRMODEL, YRALTMODEL
TYPE(FIELDS)       :: YRFIELDS_IN
TYPE(VARIABLES)    :: YNLVARS

TYPE(DATETIME_TMP) :: YLTIME

TYPE(GEOMETRY)     :: YRALTGEOMETRY
TYPE(FIELDS)       :: YRFIELDS_OUT
TYPE(VARIABLES)    :: YALTNLVARS

TYPE(FIELDS)       :: YLFIELDS_INCR_IN
TYPE(FIELDS)       :: YLFIELDS_SAVED

TYPE(TFPOS)        :: YLFPOS, YLFPOSBAK
TYPE(TFPDATA)      :: YLFPDATA, YLFPDATABAK

CHARACTER(LEN=32) :: CLCONF=' '

REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "gstats_output_ifs.intfb.h"
#include "final_stats.intfb.h"
#include "subfpos.intfb.h"
#include "sufpdata.intfb.h"
#include "gpnorm_gfl.intfb.h"
#include "gpnorm_gmv.intfb.h"
#include "gpnorm2.intfb.h"
#include "gpnorm3.intfb.h"
#include "fields_fp_change_resol.intfb.h"
#include "suqlimsat.intfb.h"

#include "abor1.intfb.h"

#include "fcttim.func.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CPREP4',0,ZHOOK_HANDLE)

!     ------------------------------------------------------------------

!*       1.    MAIN GEAR CONTROL MADE BY ENVIRONMENT VARIABLES
!              -----------------------------------------------

! FOOMASTER_CONFIGURATION = configuration of work
CALL GET_ENVIRONMENT_VARIABLE("FOOMASTER_CONFIGURATION", CLCONF)
! Possible values :
SELECT CASE (CLCONF)
  CASE ('TEST_CHANGEOFRESOL_PLAINFLDS')
  CASE ('TEST_CHANGEOFRESOL_NULLINCR')
  CASE ('TEST_CHANGEOFRESOL_TRUEINCR')
  CASE DEFAULT
    CALL ABOR1('CPREP4 : UNDEFINED CONFIGURATION')
END SELECT
WRITE(NULOUT,'(''CPREP4 CONFIGURED AS '',A32)') ADJUSTL(CLCONF)

!     ------------------------------------------------------------------

!*       2.    CREATE INPUT GEOMETRY, MODEL AND FIELDS
!              ---------------------------------------

LLINEAR=.FALSE.

! First : choose a namelist filename
WRITE(UNIT=CLNUL,FMT='(I8)') NULNAM
CLNAM=TRIM('fort.'//ADJUSTL(CLNUL))

CALL GEOMETRY_SETUP(YRGEOMETRY, CLNAM, CFNISH)

CALL MODEL_CREATE(YRMODEL,YRGEOMETRY,ZT,CLNAM,LLINEAR,KGFLCONF=1)

CALL VARIABLES_CREATE(YNLVARS, .FALSE.)

CALL FIELDS_CREATE(YRFIELDS_IN,YRGEOMETRY,YRMODEL,YNLVARS)

!     ------------------------------------------------------------------

!*       3.    CREATE ALTERNATIVE GEOMETRY AND MODEL, AND RELATED POST-PROCESSORS
!              ------------------------------------------------------------------

INQUIRE(NULNAM,OPENED=LLOPENED)
IF (LLOPENED) CLOSE(NULNAM)
CLNAM='alt_fort.4'
OPEN(NULNAM,FILE=CLNAM,ACTION='READ',IOSTAT=IOS)
IF (IOS /= 0) THEN
  CALL ABOR1('CPREP4 FAILED TO OPEN NAMELIST FILE '//TRIM(CLNAM))
ENDIF

CALL GEOMETRY_SETUP(YRALTGEOMETRY, CLNAM, ' ', KSUPERSEDE=0)

CALL MODEL_CREATE(YRALTMODEL,YRALTGEOMETRY,ZT,CLNAM,LLINEAR,KGFLCONF=1)

CALL VARIABLES_CREATE(YALTNLVARS, .FALSE.)

CLOSE(NULNAM)

IFPOS=2

INQUIRE(NULNAM,OPENED=LLOPENED)
IF (LLOPENED) CLOSE(NULNAM)
CLNAM='l2h_fort.4'
OPEN(NULNAM,FILE=CLNAM,ACTION='READ',IOSTAT=IOS)
IF (IOS /= 0) THEN
  CALL ABOR1('CPREP4 FAILED TO OPEN NAMELIST FILE '//TRIM(CLNAM))
ENDIF
CALL SUBFPOS(YLFPOS,YRFIELDS_IN%GEOM,YRFIELDS_IN%STATE_MODEL,IFPOS,YDPOSGEOMETRY=YRALTGEOMETRY, &
 & YDPOSVAB=YRALTGEOMETRY%YRVAB,CDNAM=CLNAM)

INQUIRE(NULNAM,OPENED=LLOPENED)
IF (LLOPENED) CLOSE(NULNAM)
CLNAM='h2l_fort.4'
OPEN(NULNAM,FILE=CLNAM,ACTION='READ',IOSTAT=IOS)
IF (IOS /= 0) THEN
  CALL ABOR1('CPREP4 FAILED TO OPEN NAMELIST FILE '//TRIM(CLNAM))
ENDIF
CALL SUBFPOS(YLFPOSBAK,YRALTGEOMETRY,YRALTMODEL,IFPOS,YDPOSGEOMETRY=YRGEOMETRY, &
 & YDPOSVAB=YRGEOMETRY%YRVAB,CDNAM=CLNAM)
CLOSE(NULNAM)

!     ------------------------------------------------------------------

!*       4.    MODEL AND/OR POST-PROCESSING 
!              ----------------------------

ITER=0

! Read initial file
IFILE=0
CALL READ_FIELDS(YRFIELDS_IN,IFILE,YLTIME,ISECS,CNMEXP)

DO

  IF (MUPTRA > 0 .AND. ITER > 0) THEN
    IF (MYPROC == 1) THEN
      WRITE(NULERR,*) '<<UPDATE TRAJECTORY>> ITERATION ',ITER,' ...'
    ENDIF
  ENDIF
  ! Run the model a few time steps
  IF (YRMODEL%YRML_GCONF%YRRIP%NSTOP > 0) THEN
    ! Reset model time
    ISECS=0
    DO JSTEP=0,YRMODEL%YRML_GCONF%YRRIP%NSTOP
      CALL MODEL_STEP(YRMODEL, YRFIELDS_IN, YLTIME, ISECS)
    ENDDO
  ENDIF

  IF (MUPTRA > 0 .AND. ITER == MUPTRA) THEN
    EXIT
  ENDIF

  IF (MYPROC == 1) THEN
    WRITE(NULERR,*) 'CHANGE RESOLUTION FORWARD ...'
  ENDIF

  IMMCLI=NMM(YLTIME%NDATE)
  LLUPDSUW=(ITER==0) ! Sea ice is not used for interpolations weights

  ! First iteration only : 
  ! Initialize post-processor "forward" with auxilary data needed for surface post-processing
  CALL SUFPDATA(YLFPOS,YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF,YRFIELDS_IN%STATE_MODEL, &
   & IMMCLI,ITER,YLFPDATA,LDUPDSUW=LLUPDSUW)

  CALL FIELDS_CREATE(YRFIELDS_OUT,YRALTGEOMETRY,YRALTMODEL,YALTNLVARS)
  CALL FIELDS_ZEROS(YRFIELDS_OUT)
  ! Change geometry forward
  CALL FIELDS_FP_CHANGE_RESOL(YLFPDATA,YRFIELDS_IN,YRFIELDS_OUT)

  IF (CLCONF(20:) == 'PLAINFLDS') THEN
    ! Trajectory arrival at high res can be deleted
    CALL FIELDS_DELETE(YRFIELDS_IN)
  ELSE
    ! save trajectory arrival at low res (= minimisation starting point)
    CALL FIELDS_CREATE(YLFIELDS_SAVED,YRALTGEOMETRY,YRALTMODEL,YALTNLVARS)
    CALL FIELDS_COPY(YLFIELDS_SAVED,YRFIELDS_OUT)
  ENDIF

  ! Continue integration of the model at this alternative resolution (re-stepping, but don't change the time)
  ! Reset model time
  IF (YRALTMODEL%YRML_GCONF%YRRIP%NSTOP > 0) THEN
    ISECS=0 ! NEEDED TO RESTART ! WHY ?? Must need t9 at a time ...
    DO JSTEP=1,YRALTMODEL%YRML_GCONF%YRRIP%NSTOP
      CALL MODEL_STEP(YRALTMODEL, YRFIELDS_OUT, YLTIME, ISECS)
    ENDDO
  ENDIF

  IF (MYPROC == 1) THEN
    WRITE(NULERR,*) 'CHANGE RESOLUTION BACKWARD ...'
  ENDIF

  ! First iteration only : 
  ! Initialize post-processor "backward" with auxilary data needed for surface post-processing 
  ! after target fields object has been filled
  CALL SUFPDATA(YLFPOSBAK,YRFIELDS_OUT%GEOM,YRFIELDS_OUT%YRSURF,YRFIELDS_OUT%STATE_MODEL, &
   & IMMCLI,ITER,YLFPDATABAK,LDUPDSUW=LLUPDSUW)

  IF (CLCONF(20:) == 'PLAINFLDS') THEN
    ! Change geometry backward
    CALL FIELDS_CREATE(YRFIELDS_IN,YRGEOMETRY,YRMODEL,YNLVARS)
    CALL FIELDS_FP_CHANGE_RESOL(YLFPDATABAK,YRFIELDS_OUT,YRFIELDS_IN)
    CALL FIELDS_DELETE(YRFIELDS_OUT)
  ELSE
    IF (CLCONF(20:) == 'TRUEINCR') THEN
      ! compute increments at low res - gp only, not spectral
      CALL FIELDS_SUB(YRFIELDS_OUT,YLFIELDS_SAVED)
    ELSE
      ! test of null increment :
      CALL FIELDS_ZEROS(YRFIELDS_OUT)
    ENDIF
    ! then change resol from low to high
    CALL FIELDS_CREATE(YLFIELDS_INCR_IN,YRGEOMETRY,YRMODEL,YNLVARS)
    CALL FIELDS_FP_CHANGE_RESOL(YLFPDATABAK,YRFIELDS_OUT,YLFIELDS_INCR_IN,YLFIELDS_SAVED)
    CALL FIELDS_DELETE(YRFIELDS_OUT)
    CALL FIELDS_DELETE(YLFIELDS_SAVED)
    WRITE(UNIT=NULOUT,FMT='('' NORMS OF YRFIELDS_IN BEFORE ADDITION OF INCREMENTS '')')
    CALL GPNORM_GMV(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRGMV)
    CALL GPNORM_GFL(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRGFL)
    ! then add increments at high res to high res traj.
    CALL FIELDS_ADD(YRFIELDS_IN,YLFIELDS_INCR_IN)

    ! Control of physical limits after addition of increments
    IF (YRMODEL%YRML_PHY_MF%YRPHY%LMPHYS) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,IEND,IBL)
      DO JKGLO=1,YRFIELDS_IN%GEOM%YRGEM%NGPTOT,YRFIELDS_IN%GEOM%YRDIM%NPROMA
        IEND=MIN(YRFIELDS_IN%GEOM%YRDIM%NPROMA,YRFIELDS_IN%GEOM%YRGEM%NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/YRFIELDS_IN%GEOM%YRDIM%NPROMA+1
        IF (YRFIELDS_IN%YRSURF%YSP_SG%YF%LSET) THEN
          YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YF%MP0,IBL)=&
           & MAX(0._JPRB,YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YF%MP0,IBL))
        ENDIF
        IF (YRFIELDS_IN%YRSURF%YSP_SG%YA%LSET) THEN
          YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YA%MP0,IBL)=&
           & MIN(YRCLI%SALBX,MAX(YRCLI%SALBN,YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YA%MP0,IBL)))
        ENDIF
        IF (YRFIELDS_IN%YRSURF%YSP_SG%YR%LSET) THEN
          YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YR%MP0,IBL)=MIN(YRMODEL%YRML_PHY_MF%YRPHY1%RHOMAX, &
         & MAX(YRMODEL%YRML_PHY_MF%YRPHY1%RHOMIN,YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YR%MP0,IBL)))
        ENDIF
        IF (YRFIELDS_IN%YRSURF%YSP_SG%YT%LSET) THEN
          YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YT%MP0,IBL)=&
           & MIN(YRCLI%SALBX,MAX(YRCLI%SALBN,YRFIELDS_IN%YRSURF%SP_SG(1:IEND,:,YRFIELDS_IN%YRSURF%YSP_SG%YT%MP0,IBL)))
        ENDIF
      ENDDO    
!$OMP END PARALLEL DO
    ENDIF
    IF (.false.) THEN
      CALL SUQLIMSAT(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRGFL,YRFIELDS_IN%YRSPEC,YRFIELDS_IN%STATE_MODEL)
    ENDIF
    IF (YRMODEL%YRML_GCONF%YGFL%YTKE%LACTIVE) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JKGLO,IEND,IBL)
      DO JKGLO=1,YRFIELDS_IN%GEOM%YRGEM%NGPTOT,YRFIELDS_IN%GEOM%YRDIM%NPROMA
        IEND=MIN(YRFIELDS_IN%GEOM%YRDIM%NPROMA,YRFIELDS_IN%GEOM%YRGEM%NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/YRFIELDS_IN%GEOM%YRDIM%NPROMA+1
        YRFIELDS_IN%YRGFL%GFL(1:IEND,:,YRMODEL%YRML_GCONF%YGFL%YTKE%MP,IBL)=&
         & MAX(0.000001_JPRB,YRFIELDS_IN%YRGFL%GFL(1:IEND,:,YRMODEL%YRML_GCONF%YGFL%YTKE%MP,IBL))
      ENDDO    
!$OMP END PARALLEL DO
    ENDIF

    CALL FIELDS_DELETE(YLFIELDS_INCR_IN)
    WRITE(UNIT=NULOUT,FMT='('' NORMS OF YRFIELDS_IN AFTER ADDITION OF INCREMENTS '')')
    CALL GPNORM_GMV(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRGMV)
    CALL GPNORM_GFL(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRGFL)
    IF (YRFIELDS_IN%YRSURF%YSP_SBD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A,I1,A)') ' SOILB   ',YRFIELDS_IN%YRSURF%YSP_SBD%NUMFLDS,' FIELDS '
      DO JV=1,YRFIELDS_IN%YRSURF%YSP_SBD%NUMFLDS
        CALL GPNORM3(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%SP_SB,YRFIELDS_IN%YRSURF%YSP_SBD%NUMFLDS, &
         & JV,KLEVS=YRFIELDS_IN%YRSURF%YSP_SBD%NLEVS,LDLEVELS=.TRUE.)
      ENDDO
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_SGD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A,I1,A)') ' SNOWG   ',YRFIELDS_IN%YRSURF%YSP_SGD%NUMFLDS,' FIELDS '
      DO JV=1,YRFIELDS_IN%YRSURF%YSP_SGD%NUMFLDS
        CALL GPNORM3(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%SP_SG,YRFIELDS_IN%YRSURF%YSP_SGD%NUMFLDS, &
         & JV,KLEVS=YRFIELDS_IN%YRSURF%YSP_SGD%NLEVS,LDLEVELS=.TRUE.)
      ENDDO
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_CID%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' CANRI '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSP_CID%NUMFLDS,1,YRFIELDS_IN%YRSURF%SP_CI)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_CLD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' CLS '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSP_CLD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SP_CL)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_X2D%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' XTRP2 '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSP_X2D%NUMFLDS,1,YRFIELDS_IN%YRSURF%SP_X2)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_SLD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' LAKEB '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSP_SLD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SP_SL)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSP_RRD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' RESVR '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSP_RRD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SP_RR)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_WSD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' FROM WAM '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_WSD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_WS)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_WWD%NUMFLDS>0) THEN
      WRITE(NULOUT,'(A)') ' TO WAM '
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_WWD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_WW)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_VFD%NUMFLDS > 0) THEN
      WRITE(NULOUT,'(A)') ' VARSF'
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_VFD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_VF)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_VVD%NUMFLDS > 0) THEN
      WRITE(NULOUT,'(A)') ' VCLIV'
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_VVD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_VV)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_VAD%NUMFLDS > 0) THEN
      WRITE(NULOUT,'(A)') ' VCLIA'
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_VAD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_VA)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_VCD%NUMFLDS > 0) THEN
      WRITE(NULOUT,'(A)') ' VO3ABC'
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_VCD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_VC)
    ENDIF
    IF (YRFIELDS_IN%YRSURF%YSD_VXD%NUMFLDS > 0) THEN
      WRITE(NULOUT,'(A)') ' VCLIX'
      CALL GPNORM2(YRFIELDS_IN%GEOM,YRFIELDS_IN%YRSURF%YSD_VXD%NUMFLDS,1,YRFIELDS_IN%YRSURF%SD_VX)
    ENDIF
  ENDIF


  IF (MUPTRA > 0) THEN
    ITER=ITER+1
  ELSE
    EXIT
  ENDIF

ENDDO

! Cleaning
CALL MODEL_DELETE(YRALTMODEL)
CALL GEOMETRY_DELETE(YRALTGEOMETRY)
CALL VARIABLES_DELETE(YALTNLVARS)

!     ------------------------------------------------------------------

!*       5.    CPREP4 DOWN
!              --------

!*       5.1   CLOSE FDB AND OTHER POST PROCESSING FILES

IF (NOUTTYPE==2) THEN
  CALL CLOSE_IOSTREAM(Y_IOSTREAM_FDB)
ENDIF

!*       5.2 FINAL STATISTICS

CALL FINAL_STATS('FOOMASTER',ITER)

CALL IOSTREAM_STATS

CALL GSTATS(0,1)

CALL GSTATS_OUTPUT_IFS(YRMODEL%YRML_GCONF%YRRIP)

WRITE(NULOUT,*) ' *** END CPREP4 *** '

!*       5.3 MPI FINALIZATIONS

IF (.NOT.LMPOFF) THEN

  CALL VARIABLES_DELETE(YNLVARS)
  CALL FIELDS_DELETE(YRFIELDS_IN)
  CALL GEOMETRY_DELETE(YRGEOMETRY)
  CALL MODEL_DELETE(YRMODEL)

ENDIF
  
IF (LHOOK) CALL DR_HOOK('CPREP4',1,ZHOOK_HANDLE)

END SUBROUTINE CPREP4
