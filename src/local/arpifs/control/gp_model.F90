#define __FILENAME__ "gp_model.F90"
#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE GP_MODEL(YDGEOMETRY,YDFIELDS,YDMODEL,CDCONF,LD_DFISTEP,PSLBUF1,PSLBUF2,PGRADPHY,&
 & PTRAJEC,PTRAJEC_OOPS)
!****-------------------------------------------------------------------
!**** *GP_MODEL* - Grid-point model
!****-------------------------------------------------------------------
!     Purpose.   Computations in grid-point space
!     --------

!**   Interface.
!     ----------
!        *CALL* *GP_MODEL (..)

!        Explicit arguments :  CDCONF - configuration of work (see doc.)
!        --------------------

!        Implicit arguments :  None.
!        --------------------

!     Method.
!     -------

!     Externals.   See includes below.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        Deborah Salmond   *ECMWF*

! Modifications
! -------------
!   06-Feb-2007 A.Geer   Call 1DVAR rain during final analysis trajectory to get TBs
!   04-Jun-2007 M. Jidane  : Fix NSLCORE bug
!   25-Apr-2008 A. Geer    : Mutilple sensor 4D-Var rain assimilation
!   23-APR-2008 S.Serrar   : A Tracer Mass Fixer is implemented for GEMS/GHG (controlled by LTRCMFIX)
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   01-Oct-2008 A. Geer    : Rainy 4D-Var tidying up
!   GMozdzynski/JJMorcrette 20090129 bugfix dynamical extra fields
!   K. Yessad Dec 2008: merge SLCOMM+SLCOMM1 -> SLCOMM.
!   K. Yessad Dec 2008: merge the different (E)SLEXTPOL.. -> (E)SLEXTPOL.
!   09-Mar-2009 W. Bell    : Add azimuth angle for windsat
!   10-Jun-2009 A. Geer   Rainy 4D-Var passive mode and diagnostics
!   K. Yessad (Nov 2009): prune lpc_old.
!   16-Dec-2009   F. Karbou  Allow surface emissivity handling
!   27-Sep-2010   O. Spaniel,K. Yessad  Fix of the allocation of ZPB[X]P9 arrays
!   21-Sep-2010 A. Geer   Streamline the all-sky code
!   Jan 2011    G.Mozdzynski  OOPS cleaning, use of derived type SL_STRUCT
!   G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM
!   K. Yessad (Dec 2011): various contributions.
!   22-Sep-2011   J. Flemming, GFL mass fixer in separate routine
!   22-Sep-2011   J. Flemming , call to NOx family advection
!   N. Wedi (Nov 2011) : add LGPSTRESS option
!   11-Jan-2011 A. Geer   Remove all-sky operator (MWAVE) - it's in HOP now
!   G. Mozdzynski (May 2012): further cleaning
!   10-Aug-2012 P. Lopez : Added rain gauge assimilation
!   10-May-2012   M. Diamantakis , add code for quasi-monotone mass fixer
!   10-Nov-2012   M. Diamantakis , put mass fixers in separate subroutine
!   K. Yessad (Nov 2012): simplify testings.
!   F. Vana  28-Nov-2013 : Redesigned trajectory handling
!   F. Vana  17-Feb-2014 : Avoid double call of ECMWF radiation with LPHYLIN
!   11-Jan-2014 G. Balsamo : Added lakes buffer
!   M. Ahlgrimm Apr 2014: Add precip fraction buffer for DDH output
!   T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!   2013-11, J. Masek: Passing intermittency arrays for ACRANEB2.
!   K. Yessad (July 2014): Move some variables.
!   K. Yessad (Dec 2016): Prune obsolete options.
!   K. Yessad (June 2017): Introduce NHQE model.
!   M. Diamantakis, W. Deconinck (May 2018): Add code for for multiple (Atlas) grid advection scheme
!   R. Honnert & R. El Khatib (Nov 2019) : add GPGRADIENTS for 3D parametrisations
! End Modifications
!-------------------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELDS_MOD   , ONLY : FIELDS
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMCT0       , ONLY : LSLAG, NCONF, LR2D, LELAM, LNHQE,&
 &                        LTWOTL, LSCREEN, LIFSTRAJ, LIFSMIN, LECMWF
USE YOMDYNA      , ONLY : LPC_FULL, LNHQE_SIHYD
USE YOMMP0       , ONLY : NPROC, LSLDEBUG
USE YOMCT3       , ONLY : NSTEP
USE YOMLCZ       , ONLY : GPFORCEU ,GPFORCEV ,GPFORCET ,GPFORCEQ
USE YOMSMOS      , ONLY : LESMOS_ACTIVE, LESMOS_SEKF, NPOL_MAX, NANG_MAX
USE YOMSEKF      , ONLY : LUSE_SMOS_TB
USE YOMGBRAD     , ONLY : LEGBRAD_ACTIVE
USE YOMRAINGG    , ONLY : LERAINGG_ACTIVE
USE YOMDYNCORE   , ONLY : LHELDSUAREZ
USE INTDYN_MOD   , ONLY : YYTCTY0, YYTRCP0
USE YOMSPSDT     , ONLY : YSPPT
USE YOMTRAJ      , ONLY : TRAJ_TYPE
USE YOMTRAJ_OOPS , ONLY : TRAJ_TYPE_OOPS
USE DDH_MIX      , ONLY : TYP_DDH, STOREDDH, NTOTFIELD, NTOTVAR, NTOTSURF, &
&                         NTOTSVAR, NTOTSVFS
#ifdef WITH_ATLAS
USE ATLAS_MODULE , ONLY : ATLAS_TRACE
#endif
!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(FIELDS)      ,INTENT(INOUT) :: YDFIELDS
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
CHARACTER(LEN=9)  ,INTENT(IN)    :: CDCONF
LOGICAL           ,INTENT(IN)    :: LD_DFISTEP
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSLBUF1(YDMODEL%YRML_DYN%YRSL%NASLB1,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSLBUF2(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB2%NFLDSLB2,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGRADPHY(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_MF%YRARPHY%NGRADIENTS,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(TRAJ_TYPE)        ,OPTIONAL,INTENT(INOUT) :: PTRAJEC
TYPE(TRAJ_TYPE_OOPS)   ,OPTIONAL,INTENT(INOUT) :: PTRAJEC_OOPS
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZEXTRADYN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NVEXTRDYN, &
 & YDGEOMETRY%YRDIM%NGPBLKS)

!   --- PHYSICS TENDENCIES TO BE INTERPOLATED AT THE ORIGIN POINT ---
REAL(KIND=JPRB), ALLOCATABLE :: ZPBUP9(:,:,:),ZPBVP9(:,:,:),ZPBTP9(:,:,:),&
 & ZPBGFLP9(:,:,:,:)
!   --- MISCELLANEOUS ---
REAL(KIND=JPRB) :: ZSLBUF1AUX(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)

INTEGER(KIND=JPIM) :: IBL,ICEND,JKGLO,JROF,JBL,IGPCOMP

INTEGER(KIND=JPIM) :: IVSEPC(YDGEOMETRY%YRDIM%NGPBLKS), IVSEPL(YDGEOMETRY%YRDIM%NGPBLKS)
INTEGER(KIND=JPIM) :: IDUMARR(2)

LOGICAL :: LLINC
LOGICAL :: LL_CALLRADDRV
LOGICAL :: LLCONFX

REAL(KIND=JPRB) :: ZZZ

REAL(KIND=JPRB) :: ZOUT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,3,YDGEOMETRY%YRDIM%NGPBLKS)

!------ Timing and Printout Variables ----------------
CHARACTER(LEN=1) :: CLCONF
INTEGER(KIND=JPIM) :: J

REAL(KIND=JPRB), ALLOCATABLE :: ZSMOS_OBS_BUF(:,:,:), ZSMOS_TB_BUF(:,:,:),&
  & ZSMOS_ANGLE(:,:,:), ZSMOS_FARAD(:,:,:), ZSMOS_GEOMET(:,:,:)
INTEGER(KIND=JPIM), ALLOCATABLE :: ISMOS_BUF(:,:,:)

! Arrays needed MPDATA mgrid advection
REAL(KIND=JPRB), ALLOCATABLE :: ZGEO0(:,:,:), ZRCP0(:,:,:,:), ZPRE0F(:,:,:), ZCTY0(:,:,:,:), ZWRL9(:,:,:)

TYPE(TYP_DDH):: YLDDH

REAL(KIND=JPRB) :: ZHOOK_HANDLE
#ifdef WITH_ATLAS
TYPE(ATLAS_TRACE) :: TRACE
#endif

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "call_sl_drv.intfb.h"
#include "cpg_drv.intfb.h"
#include "cpg2.intfb.h"
#include "cpg2lag.intfb.h"
#include "cpglag.intfb.h"
#include "define_pointers_mp9.intfb.h"
#include "smos_process.intfb.h"
#include "smos_update.intfb.h"
#include "ec_phys_drv.intfb.h"
#include "raddrv.intfb.h"
#include "slcomm.intfb.h"
#include "slextpol.intfb.h"
#include "check_sl_struct.intfb.h"
#include "chem_noyadv.intfb.h"
#include "gpstress.intfb.h"
#include "cuconvca.intfb.h"
#include "cpicgfl.intfb.h"
#include "gpgradients.intfb.h"
!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('GP_MODEL',0,ZHOOK_HANDLE)
#ifdef WITH_ATLAS
TRACE = ATLAS_TRACE(__FILENAME__,__LINE__,"GP_MODEL")
#endif

ASSOCIATE(YDGFL=>YDFIELDS%YRGFL,YDGMV=>YDFIELDS%YRGMV, YDSURF=>YDFIELDS%YRSURF, YDDIM=>YDGEOMETRY%YRDIM, &
 &  YDGEM=>YDGEOMETRY%YRGEM, YDDIMV=>YDGEOMETRY%YRDIMV, YDMP=>YDGEOMETRY%YRMP, YDVAB=>YDGEOMETRY%YRVAB, &
 &  YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, YDSTA=>YDGEOMETRY%YRSTA, YDDYN=>YDMODEL%YRML_DYN%YRDYN, &
 & YDPTRSLB1=>YDMODEL%YRML_DYN%YRPTRSLB1,YDPTRSLB2=>YDMODEL%YRML_DYN%YRPTRSLB2,YDEPHLI=>YDMODEL%YRML_PHY_SLIN%YREPHLI, &
 & YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH,YDSLPHY=>YDMODEL%YRML_PHY_G%YRSLPHY,YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY, &
 & YDRADF=>YDMODEL%YRML_PHY_RAD%YRRADF, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP,YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2,YGFL=>YDMODEL%YRML_GCONF%YGFL, &
 & YDTRC=>YDMODEL%YRML_PHY_RAD%YRTRC,YDMDDH=>YDMODEL%YRML_DIAG%YRMDDH, &
 & YDECUCONVCA=>YDMODEL%YRML_PHY_EC%YRECUCONVCA,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY, &
 & YDGPDDH=>YDMODEL%YRML_DIAG%YRGPDDH,YDCHEM=>YDMODEL%YRML_CHEM%YRCHEM, &
 & YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD)

ASSOCIATE(YCOMP=>YGFL%YCOMP,NUMFLDS=>YGFL%NUMFLDS, &
 & LCHEM_ANAO3=>YDCHEM%LCHEM_ANAO3, KCHEM_NOXADV=>YDCHEM%KCHEM_NOXADV, &
 & NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA,NFLEVG=>YDDIMV%NFLEVG, &
 & NVEXTRDYN=>YDDPHY%NVEXTRDYN, &
 & LFINDVSEP=>YDDYN%LFINDVSEP, LGPSTRESS=>YDDYN%LGPSTRESS, &
 & NCURRENT_ITER=>YDDYN%NCURRENT_ITER, NSITER=>YDDYN%NSITER, &
 & NVSEPC=>YDDYN%NVSEPC, NVSEPL=>YDDYN%NVSEPL, &
 & LCUCONV_CA=>YDECUCONVCA%LCUCONV_CA, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, &
 & LAGPHY=>YDEPHY%LAGPHY, LEGBRAD=>YDEPHY%LEGBRAD, LEPHYS=>YDEPHY%LEPHYS, &
 & LERADI=>YDEPHY%LERADI, LERAIN=>YDEPHY%LERAIN, LERAINGG=>YDEPHY%LERAINGG, &
 & LESMOS=>YDEPHY%LESMOS, NPRACCL=>YDEPHY%NPRACCL, LSLPHY=>YDEPHY%LSLPHY,&
 & NRADFR=>YDERAD%NRADFR, &
 & NGPTOT=>YDGEM%NGPTOT, NGPTOT_CAP=>YDGEM%NGPTOT_CAP, &
 & GFL=>YDGFL%GFL, GFLPC=>YDGFL%GFLPC, GFLPT=>YDGFL%GFLPT, GFLSLP=>YDGFL%GFLSLP, &
 & GMV=>YDGMV%GMV, GMVS=>YDGMV%GMVS, YPH9=>YDGMV%YPH9, YT0=>YDGMV%YT0, &
 & YT1=>YDGMV%YT1, YT9=>YDGMV%YT9, &
 & GFLTNDHD_DDH=>YDGPDDH%GFLTNDHD_DDH, GFLTNDSL_DDH=>YDGPDDH%GFLTNDSL_DDH, &
 & GMVTNDHD_DDH=>YDGPDDH%GMVTNDHD_DDH, GMVTNDSI_DDH=>YDGPDDH%GMVTNDSI_DDH, &
 & GMVTNDSL_DDH=>YDGPDDH%GMVTNDSL_DDH, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1, RPARSL1=>YDPTRSLB1%RPARSL1, &
 & NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & EMTD=>YDRADF%EMTD, EMTU=>YDRADF%EMTU, RMOON=>YDRADF%RMOON, TRSW=>YDRADF%TRSW, &
 & NSTOP=>YDRIP%NSTOP, &
 & SAVTEND=>YDSLPHY%SAVTEND, &
 & SD_DI=>YDSURF%SD_DI, SD_PF=>YDSURF%SD_PF, SD_SFL=>YDSURF%SD_SFL, &
 & SD_SFO=>YDSURF%SD_SFO, SD_SM=>YDSURF%SD_SM, SD_V2=>YDSURF%SD_V2, &
 & SD_V3=>YDSURF%SD_V3, SD_VA=>YDSURF%SD_VA, SD_VC=>YDSURF%SD_VC, &
 & SD_VD=>YDSURF%SD_VD, SD_VF=>YDSURF%SD_VF, SD_VH=>YDSURF%SD_VH, &
 & SD_VN=>YDSURF%SD_VN, SD_VP=>YDSURF%SD_VP, SD_VV=>YDSURF%SD_VV, &
 & SD_WS=>YDSURF%SD_WS, SD_WW=>YDSURF%SD_WW, SD_X2=>YDSURF%SD_X2, &
 & SD_XA=>YDSURF%SD_XA, SP_EP=>YDSURF%SP_EP, SP_OM=>YDSURF%SP_OM, &
 & SP_RR=>YDSURF%SP_RR, SP_SB=>YDSURF%SP_SB, SP_SG=>YDSURF%SP_SG, &
 & SP_SL=>YDSURF%SP_SL, SP_X2=>YDSURF%SP_X2, SP_CL=>YDSURF%SP_CL, &
 & SD_XP=>YDSURF%SD_XP, SD_XP2=>YDSURF%SD_XP2,LMPHYS=>YDPHY%LMPHYS, &
 & TSPHY=>YDPHY2%TSPHY, &
 & SD_VK=>YDSURF%SD_VK, &
 & GDEOSI=>YDTRC%GDEOSI, GUEOSI=>YDTRC%GUEOSI, GMU0=>YDTRC%GMU0, &
 & GMU0_MIN=>YDTRC%GMU0_MIN, GMU0_MAX=>YDTRC%GMU0_MAX, &
 & NFIELDS3D_AUTO=>YDMDDH%NFIELDS3D_AUTO,NFIELDS3D_OFFSET=>YDMDDH%NFIELDS3D_OFFSET, &
 & NFIELDS2D_AUTO=>YDMDDH%NFIELDS2D_AUTO,NFIELDS2D_OFFSET=>YDMDDH%NFIELDS2D_OFFSET, &
 & LFLEXDIA=>YDLDDH%LFLEXDIA,LDDH_OMP=>YDLDDH%LDDH_OMP, YDSL=>YDMODEL%YRML_DYN%YRSL, &                                                               
 & LGRADHPHY=>YDMODEL%YRML_PHY_MF%YRARPHY%LGRADHPHY) 
!     ------------------------------------------------------------------
!*       1.    INITIALISATION
!     ------------------------------------------------------------------

! Total number of grid points - ndglg*ndlon in global / ndguxg*ndlon in LAM
! used for model computations

! Changed NGPTOT into IGPCOMP in order to pass properly
! the scan of NSLCORE values for the LAM models
IGPCOMP=NGPTOT_CAP ! equal to NGPTOT in the global model or if lgptot_cap=.false.

IF (.NOT.(ALLOCATED(YDGMV%GMVT1).AND.ALLOCATED(YDGMV%GMVT1S).AND.ALLOCATED(YDGFL%GFLT1)))&
   CALL ABOR1("GP_MODE: GMV/GFL T1 NOT ALLOCATED")

IF (CDCONF(3:3) == 'A'.OR.CDCONF(3:3) == 'B'.OR.CDCONF(3:3) == '1') THEN
  IF(NUMFLDS>0.AND..NOT.ALLOCATED(YDGFL%GFL)) THEN
    CALL ABOR1('GP_MODEL: GFL NOT ALLOCATED')
  ENDIF
  IF(LPC_FULL.AND.LSLAG.AND.(LEPHYS.OR.LMPHYS).AND..NOT.ALLOCATED(YDGFL%GFLPT)) THEN
    CALL ABOR1('GP_MODEL: GFLPT NOT ALLOCATED')
  ENDIF
  IF(LPC_FULL.AND..NOT.LTWOTL.AND..NOT.ALLOCATED(YDGFL%GFLPC)) THEN
    CALL ABOR1('GP_MODEL: GFLPC NOT ALLOCATED FOR 3TL PC SCHEME')
  ENDIF
ENDIF

CALL GSTATS(1228,0)
IF (YDSL%NSLPAD > 0)THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(J)
  DO J=1,NFLDSLB1
    PSLBUF1(:,J)=HUGE(ZZZ)
  ENDDO
!$OMP END PARALLEL DO
ENDIF

!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(J)
DO J=1,NGPBLKS
  PSLBUF2(:,:,J)=0.0_JPRB
ENDDO
!$OMP END PARALLEL DO
CALL GSTATS(1228,1)

! Create field registry, if it hasn't been set up yet
IF (.NOT. ASSOCIATED(YDFIELDS%VARIABLES)) THEN
  CALL YDFIELDS%REGISTRY%INIT(YDGEOMETRY)
  CALL YDFIELDS%REGISTRY%REGISTER_GEOMETRY(YDFIELDS%VARIABLES, YDGEOMETRY)
  CALL YDFIELDS%REGISTRY%REGISTER_GMV_FIELDS(YDFIELDS%VARIABLES, YDFIELDS%YRGMV)
  CALL YDFIELDS%REGISTRY%REGISTER_GFL_FIELDS(YDFIELDS%VARIABLES, YDFIELDS%YRGFL, YDFIELDS%YRGFL%YGFL)
  CALL YDFIELDS%REGISTRY%REGISTER_SURFACE_FIELDS(YDFIELDS%SURFVARS, YDSURF)
  CALL YDFIELDS%REGISTRY%REGISTER_EC_PHYS_FIELDS(YDFIELDS%VARIABLES, YDFIELDS%YEC_PHYS_FIELDS)
  CALL YDFIELDS%REGISTRY%REGISTER_PHYSICS_RADIATION_FIELDS(YDFIELDS%VARIABLES, YDFIELDS%STATE_MODEL%YRML_PHY_RAD)
END IF

LESMOS_ACTIVE=CDCONF(6:6) == 'V' .AND. LESMOS
LESMOS_SEKF=NCONF==302 .AND. LUSE_SMOS_TB

ALLOCATE( ZSMOS_OBS_BUF(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )
ALLOCATE( ISMOS_BUF(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )
ALLOCATE( ZSMOS_TB_BUF(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )
ALLOCATE( ZSMOS_ANGLE(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )
ALLOCATE( ZSMOS_FARAD(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )
ALLOCATE( ZSMOS_GEOMET(NGPTOT,0:NPOL_MAX-1,NANG_MAX) )

LEGBRAD_ACTIVE=(LIFSTRAJ .OR. LIFSMIN) .AND. NSTEP < NSTOP .AND. LEGBRAD .AND.&
 & MOD((NSTEP+1)*NINT(TSPHY),NPRACCL) == 0

LERAINGG_ACTIVE=(LIFSTRAJ .OR. LIFSMIN) .AND. NSTEP < NSTOP .AND. LERAINGG .AND.&
 & MOD((NSTEP+1)*NINT(TSPHY),3600) == 0

NFIELDS3D_OFFSET=0
YLDDH%NFIELDS3D_AUTO=NFIELDS3D_AUTO
NFIELDS2D_OFFSET=0
YLDDH%NFIELDS2D_AUTO=NFIELDS2D_AUTO

!     ------------------------------------------------------------------
!*       2.    AROME/ALARO EXTERNALIZED SURFACE
!     -----------------------------------------------------------------

! THIS HAS BEEN MOVED UNDER APL_AROME IN THE NPROMA LOOP

IF(LPC_FULL.AND.LTWOTL.AND.LEPHYS)THEN
  CALL DEFINE_POINTERS_MP9(YGFL,YDDYN)
ENDIF

IF (KCHEM_NOXADV > 0) THEN
  ! fill NOXA/CLXA array with NOx / NOy
  !  - has to be done before CPG because SL Buffers are set in there
  CALL CHEM_NOYADV(YDGEOMETRY,YGFL,YDCHEM,1,KCHEM_NOXADV,GFL,YDGFL%GFLT1)
ENDIF

!     ------------------------------------------------------------------
!*        CELLULAR AUTOMATON SCHEME (FOR ALARO CONVECTION)
!     ------------------------------------------------------------------

!  (Must be called outside the SM-parallel region as it issues MPI comms.)
IF(.NOT.LECMWF .AND. LCUCONV_CA) CALL CUCONVCA(YDGEOMETRY,YDGMV,YDMODEL,NSTEP)

!     ------------------------------------------------------------------
!*       3.    DYNAMICS
!     ------------------------------------------------------------------

IF (.NOT.LR2D) THEN

!     ------------------------------------------------------------------
!* Computation of Horizontal Gradients from halo
!     ------------------------------------------------------------------

  IF (LGRADHPHY) THEN
   CALL GPGRADIENTS(YDGEOMETRY,YDMODEL,YDGMV,YDGFL,PGRADPHY)
  ENDIF

  IF (LNHQE.AND.(.NOT.LNHQE_SIHYD)) THEN
    ! ky: GMVS(1:NPROMA,YT9%MPREHYDS,1:NGPBLKS)=EXP(GMVS(1:NPROMA,YT0%MSP,1:NGPBLKS))
    !     And the saved version of "prehyds(t)" is assumed to be untouched by some Asselin filtering.
    !     For LAM models saving must be done for C+I+E (all the NGPTOT points):
    !     this is why this memory transfer is not under CPG_DRV nor in CPG_GP.
    !     The content of GMVS(:,YT9%MPREHYDS,:) is used in (E)SPNHQESI_PRODSKAP.
    DO JBL=1,NGPBLKS
      DO JROF=1,NPROMA
        GMVS(JROF,YT9%MPREHYDS,JBL)=EXP(GMVS(JROF,YT0%MSP,JBL))
      ENDDO
    ENDDO
  ENDIF

  ALLOCATE(ZGEO0(NPROMA,NFLEVG,NGPBLKS))
  ALLOCATE(ZRCP0(NPROMA,NFLEVG,YYTRCP0%NDIM,NGPBLKS))
  ALLOCATE(ZPRE0F(NPROMA,NFLEVG,NGPBLKS))
  ALLOCATE(ZCTY0(NPROMA,0:NFLEVG,YYTCTY0%NDIM,NGPBLKS))
  ALLOCATE(ZWRL9(NPROMA,NFLEVG,NGPBLKS))

  ZEXTRADYN(:,:,:,:)=0.0_JPRB

  CALL CPG_DRV(YDGEOMETRY,YDMODEL,YDFIELDS,YDFIELDS%VARIABLES,CDCONF(4:4),LD_DFISTEP,YDSL,&
   & IGPCOMP,GFLSLP,SAVTEND,GMVTNDHD_DDH,GFLTNDHD_DDH,YSPPT%YGPSDT(1)%GP2D,SD_PF,&
   & PGRADPHY,&
   & GPFORCEU(:,:,:,0) ,GPFORCEV(:,:,:,0), &
   & GPFORCET(:,:,:,0) ,GPFORCEQ(:,:,:,0), &
   & GMV,GMVS,GFL,GFLPC,GFLPT,&
   & SP_SB,SP_SG,SP_RR,SP_CL,SD_VF,SD_VP,SD_VV,SD_VH,SD_VK,SD_VA,SD_VC,&
   & SD_VD,SD_SFL,SD_SFO,SD_XA,SD_DI,SD_XP,SD_XP2,&
   & EMTD,EMTU,TRSW,RMOON,GMVTNDSI_DDH,&
   & YDTRC%GDEOSI,YDTRC%GUEOSI,YDTRC%GMU0,YDTRC%GMU0_MIN,YDTRC%GMU0_MAX,&
   & YDTRC%GDEOTI,YDTRC%GDEOTI2,YDTRC%GUEOTI,YDTRC%GUEOTI2,YDTRC%GEOLT,YDTRC%GEOXT,&
   & YDTRC%GRPROX,YDTRC%GMIXP,YDTRC%GFLUXC,YDTRC%GRSURF,&
   & PSLBUF1,PSLBUF2,YDGMV%GMVT1,YDGMV%GMVT1S,YDGFL%GFLT1,ZEXTRADYN,&
   & GMVTNDSL_DDH,GFLTNDSL_DDH,YLDDH,ZGEO0,ZRCP0,ZPRE0F,ZCTY0,ZWRL9,&
   & PTRAJEC=PTRAJEC,PTRAJEC_OOPS=PTRAJEC_OOPS)

  IF (LFLEXDIA.AND.LDDH_OMP) THEN      
    CALL STOREDDH(YDMDDH,YDMODEL%YRML_DIAG%YRTDDH,YLDDH)
    IF (CDCONF(4:4)=='X') THEN
      ! equivalent of reset_ddhflex for lddhomp
      NTOTFIELD=0
      NTOTVAR=0
      NTOTSURF=0
      NTOTSVAR=0
      NTOTSVFS=0
    ENDIF
  ENDIF

ELSE
IF(PRESENT(PTRAJEC_OOPS)) CALL ABOR1('GP_MODEL: CPG2 not done yet for OOPS')
!$OMP PARALLEL PRIVATE(ICEND,IBL,ZSLBUF1AUX)
!$OMP DO SCHEDULE(STATIC,1)
  DO JKGLO=1,IGPCOMP,NPROMA
    ICEND=MIN(NPROMA,IGPCOMP-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1

    CALL CPG2(YDGEOMETRY,YDGMV,YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN,CDCONF(4:4),ICEND,JKGLO,&
     & ZSLBUF1AUX,PSLBUF2(1,1,IBL),GMV(:,:,:,IBL),GMVS(:,:,IBL),&
     & IBL,&
     & GMV(:,:,YT0%MU,IBL)  ,GMV(:,:,YT0%MV,IBL),&
     & GMV(:,:,YT0%MUL,IBL) ,GMV(:,:,YT0%MVL,IBL),&
     & GMV(:,:,YT0%MDIV,IBL),GMV(:,:,YT0%MVOR,IBL),&
     & GMVS(:,YT0%MSP,IBL)  ,GMVS(:,YT0%MSPL,IBL),&
     & GMVS(:,YT0%MSPM,IBL),&
     & GMV(:,:,YT9%MU,IBL)  ,GMV(:,:,YT9%MV,IBL),&
     & GMV(:,:,YT9%MDIV,IBL),GMV(:,:,YT9%MSPNL,IBL),&
     & GMVS(:,YT9%MSP,IBL)  ,GMVS(:,YT9%MSPL,IBL),&
     & GMVS(:,YT9%MSPM,IBL),&
     & YDGMV%GMVT1(:,:,YT1%MU,IBL),YDGMV%GMVT1(:,:,YT1%MV,IBL),&
     & YDGMV%GMVT1S(:,YT1%MSP,IBL),PTRAJEC%SLAG(IBL))  

    ! move data from blocked form to latitude (NASLB) form
    IF (LSLAG) THEN
      DO JROF=JKGLO,MIN(JKGLO-1+NPROMA,IGPCOMP)
        PSLBUF1(YDSL%NSLCORE(JROF),:)=ZSLBUF1AUX(JROF-JKGLO+1,:)
      ENDDO
    ENDIF
  ENDDO
!$OMP END DO
!$OMP END PARALLEL
ENDIF ! LR2D

!     ------------------------------------------------------------------
!*       4.     ECMWF RADIATION
!     ------------------------------------------------------------------

IF (.NOT.LR2D) THEN
  IF (LEPHYS.AND.LAGPHY.AND.LERADI.AND.(.NOT.LPHYLIN)) THEN
    IF (NRADFR == 0 ) THEN
      CALL ABOR1('GP_MODEL: NRADFR=0 !')
    ELSEIF (MOD(NSTEP,NRADFR) == 0) THEN
      IF (LPC_FULL) THEN
        LL_CALLRADDRV=(NCURRENT_ITER == NSITER)
      ELSE
        LL_CALLRADDRV=.TRUE.
      ENDIF
      IF (LL_CALLRADDRV) THEN
        CALL GSTATS(8,2)
        CALL GSTATS(13,0)
        CALL RADDRV(YDGEOMETRY,YDFIELDS%YRGMV,YDFIELDS%YRSURF,YDMODEL,NGPTOT,NFLDSLB2,NGPBLKS,GMV,GMVS,PSLBUF2,GFL)
        CALL GSTATS(13,1)
        CALL GSTATS(8,3)
        ! UNCOMMENT THE FOLLOWING LINE TO GET FORECAST DAYS PER DAY STATS
        ! SINCE THE PREVIOUS RADIATION STEP (USEFUL WHEN BENCHMARKING)
        ! IF( NCONF==1 ) CALL FORECAST_DAYS_CALC
      ENDIF
    ENDIF
  ENDIF
ENDIF

!IF (LCOUPLO4_ENV) THEN
!  ! call of oasis4 exchange - get chemistry
!  CALL COUPLO4_EXCHANGE(1)
!ENDIF


!     ------------------------------------------------------------------
!*       5.     SEMI-LAGRANGIAN INTERPOLATION
!     ------------------------------------------------------------------

IF (LSLAG) THEN
  IF (LSLPHY) THEN
    ALLOCATE (ZPBUP9(NPROMA,NFLEVG,NGPBLKS))
    ALLOCATE (ZPBVP9(NPROMA,NFLEVG,NGPBLKS))
    ALLOCATE (ZPBTP9(NPROMA,NFLEVG,NGPBLKS))
    ALLOCATE (ZPBGFLP9(NPROMA,NFLEVG,NUMFLDS,NGPBLKS))
  ELSE
    ! minimal alloc to avoid false alarms with bound checking option
    IF (.NOT.LR2D) THEN
      ALLOCATE (ZPBUP9(1,NFLEVG,NGPBLKS))
      ALLOCATE (ZPBVP9(1,NFLEVG,NGPBLKS))
      ALLOCATE (ZPBTP9(1,NFLEVG,NGPBLKS))
      ALLOCATE (ZPBGFLP9(1,NFLEVG,NUMFLDS,NGPBLKS))
    ENDIF
  ENDIF
ENDIF


IF (.NOT.LR2D) THEN

  IVSEPC(:) = 0
  IVSEPL(:) = 0

  IF(LSLAG) THEN
    !--------------------------------------------------------------------
    ! Advection on multiple grids
    !--------------------------------------------------------------------
#ifdef WITH_MGRIDS
    CALL YDMODEL%YRML_DYN%YR_MGRIDS_ADVECTION%EXECUTE( YDGMV, GMV, GFL, ZWRL9, ZGEO0, ZRCP0, ZPRE0F, ZCTY0, YDGFL%GFLT1 )
#endif

    !FIELDS: PSLBUF1 contains PB1 fields to be interpolated (input)
    !FIELDS: PSLBUF2 contains PB2 fields (input and output)

    CALL CALL_SL_DRV(YDGEOMETRY,YDGMV,YDMODEL,YDGFL%GFL,YDSL,IVSEPC,IVSEPL,PSLBUF1,PSLBUF2,&
      & YDGMV%GMVT1,YDGMV%GMVT1S,YDGFL%GFLT1,GFLPC,ZPBUP9,ZPBVP9,ZPBTP9,ZPBGFLP9,ZEXTRADYN)
  ENDIF

  CALL GSTATS(8,2)
  CALL GSTATS(10,0)
! cop
  IF (LCHEM_ANAO3 ) CALL CPICGFL(YDGEOMETRY,YDGMV,YDMODEL%YRML_CHEM,YGFL,GMV,GMVS,YDGFL%GFLT1)

  IF (KCHEM_NOXADV>0) THEN
    ! reset arrays with individual NOx/NOy/CLx components
    CALL CHEM_NOYADV(YDGEOMETRY,YGFL,YDCHEM,2,KCHEM_NOXADV,GFL,YDGFL%GFLT1)
  ENDIF

  ! COLLECT SMOS DATA IN GRID POINT
  IF(LESMOS_ACTIVE.OR.LESMOS_SEKF) THEN
    IF(LSCREEN.OR.LESMOS_SEKF)THEN
      CALL SMOS_PROCESS(YDGEM,ZSMOS_OBS_BUF,ISMOS_BUF,ZSMOS_ANGLE,ZSMOS_FARAD,ZSMOS_GEOMET)
    ENDIF
  ENDIF

  !   ------------------------------------------------------------------
  !*     6.    ECMWF Physics
  !   ------------------------------------------------------------------

  IF( LGPSTRESS ) THEN
    CALL GPSTRESS(YDGEOMETRY,YDGMV,YDDPHY,YDRIP,YDDYN,GMV,GMVS,POUT=ZOUT)
  ENDIF

  IF((LEPHYS.OR.LHELDSUAREZ) .AND. LAGPHY) THEN
    IF ((LPC_FULL.AND.(NCURRENT_ITER == NSITER)).OR.(.NOT.LPC_FULL)) THEN
      IF (LCUCONV_CA) CALL CUCONVCA(YDGEOMETRY,YDGMV, YDMODEL,NSTEP) !update CA
      CLCONF=CDCONF(4:4)
      CALL EC_PHYS_DRV(YDGEOMETRY,YDFIELDS,YDFIELDS%VARIABLES,YDGMV,YDSURF,YDMODEL,CLCONF,&
          & PSLBUF2,ZPBUP9,ZPBVP9,ZPBTP9,ZPBGFLP9,ZEXTRADYN,SD_PF,&
          & GMV,GMVS,YDGMV%GMVT1,YDGMV%GMVT1S,&
          & YDGFL%GFLT1,GFLPT,GFL,GFLSLP,&
          & SP_SB,SP_SG,SP_SL,SP_RR,&
          & SP_EP,SP_X2,&
          & SD_VF,SD_VN,SD_VD,&
          & SD_WS,SD_WW,&
          & SD_XA,SD_X2,&
          & SD_SM,&
          & SP_OM,SD_V2,SD_V3,&!KPP
          & YDFIELDS%YEC_PHYS_FIELDS%YRTILEPROP,&
          & YDFIELDS%YEC_PHYS_FIELDS%YRPHYSMWAVE,&
          & ZSMOS_OBS_BUF,ZSMOS_TB_BUF,ZSMOS_ANGLE,ZSMOS_FARAD,ZSMOS_GEOMET,&
          & ISMOS_BUF,&
          & PTRAJEC=PTRAJEC,PTRAJEC_OOPS=PTRAJEC_OOPS)
    ENDIF
  ENDIF

  IF (ALLOCATED(ZPBUP9)) DEALLOCATE(ZPBUP9)
  IF (ALLOCATED(ZPBVP9)) DEALLOCATE(ZPBVP9)
  IF (ALLOCATED(ZPBTP9)) DEALLOCATE(ZPBTP9)
  IF (ALLOCATED(ZPBGFLP9)) DEALLOCATE(ZPBGFLP9)
  IF (ALLOCATED(ZWRL9)) DEALLOCATE(ZWRL9)
  IF (ALLOCATED(ZRCP0)) DEALLOCATE(ZRCP0)
  IF (ALLOCATED(ZPRE0F)) DEALLOCATE(ZPRE0F)
  IF (ALLOCATED(ZCTY0)) DEALLOCATE(ZCTY0)
  IF (ALLOCATED(ZGEO0)) DEALLOCATE(ZGEO0)

  IF (LESMOS_ACTIVE)THEN
    IF (LSCREEN) THEN
       CALL SMOS_UPDATE(YDGEM,ZSMOS_TB_BUF,ISMOS_BUF)
    ENDIF
  ENDIF

! IF (LCOUPLO4_ENV) THEN
!   ! call of oasis4 exchange - put meteo
!   CALL COUPLO4_EXCHANGE(2)
! ENDIF

  !   ------------------------------------------------------------------
  !*     7.    TRANSFER TO PUT1 ARRAYS
  !   ------------------------------------------------------------------


  LLCONFX=(CDCONF(4:4)=='X')

  CALL GSTATS(1040,0)

!$OMP PARALLEL PRIVATE(JKGLO,IBL)
!$OMP DO SCHEDULE(DYNAMIC,1) FIRSTPRIVATE(YLDDH) LASTPRIVATE(YLDDH)
  DO JKGLO=1,IGPCOMP,NPROMA
    IBL=(JKGLO-1)/NPROMA+1
    CALL CPGLAG(YDGEOMETRY,YDGMV,YDMODEL%YRML_DIAG,YDMODEL%YRML_GCONF,YDDYN,YDPTRSLB2,&
     & IGPCOMP,JKGLO,LLCONFX,&
     & GMV(:,:,:,IBL),GMVS(:,:,IBL),PSLBUF2(:,:,IBL),YDGFL%GFLT1(:,:,:,IBL),YDGMV%GMVT1(:,:,:,IBL), &
     & YDGMV%GMVT1S(:,:,IBL),GFL(:,:,:,IBL),&
     & GMVTNDSL_DDH,GFLTNDSL_DDH,YLDDH,PGPSTRESS=ZOUT(:,:,:,IBL),PTRAJEC=PTRAJEC,PTRAJEC_OOPS=PTRAJEC_OOPS)
  ENDDO
!$OMP END DO
!$OMP END PARALLEL

  CALL GSTATS(1040,1)

  IF (LFLEXDIA.AND.LDDH_OMP) THEN
    CALL STOREDDH(YDMDDH,YDMODEL%YRML_DIAG%YRTDDH,YLDDH)
    IF (CDCONF(4:4)=='X') THEN
      ! equivalent of reset_ddhflex for lddhomp
      NTOTFIELD=0
      NTOTVAR=0
      NTOTSURF=0
      NTOTSVAR=0
      NTOTSVFS=0
    ENDIF
  ENDIF

  CALL GSTATS(10,1)
  CALL GSTATS(8,3)

  IF (LFINDVSEP) THEN
    DO IBL=1,NGPBLKS
      NVSEPC=MAX(NVSEPC,IVSEPC(IBL))
      NVSEPL=MAX(NVSEPL,IVSEPL(IBL))
    ENDDO
  ENDIF

ELSE

  IF (LSLAG) THEN
    IF (NPROC > 1) THEN
      LLINC = .FALSE.
      CALL SLCOMM(YDSL,IDUMARR,NFLDSLB1,LLINC,0,PSLBUF1)
    ENDIF
    CALL SLEXTPOL(YDDIM,YDSL,NFLDSLB1,IDUMARR,1,RPARSL1,PSLBUF1)
    IF( LSLDEBUG) CALL CHECK_SL_STRUCT(YDSL,'SLEXTPOL')
  ENDIF
  DO JKGLO=1,NGPTOT,NPROMA
    ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    CALL CPG2LAG(YDGEOMETRY,YDGMV,YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN,ICEND,YDSL,IBL,&
     & PSLBUF1,PSLBUF2(1,1,IBL),GMV(:,:,:,IBL),GMVS(:,:,IBL),&
     & YDGMV%GMVT1(:,:,YT1%MU,IBL),YDGMV%GMVT1(:,:,YT1%MV,IBL),YDGMV%GMVT1S(:,YT1%MSP,IBL))
  ENDDO

ENDIF ! LR2D

DEALLOCATE(ZSMOS_OBS_BUF,ISMOS_BUF,ZSMOS_TB_BUF,ZSMOS_ANGLE,&
          & ZSMOS_FARAD,ZSMOS_GEOMET)

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
#ifdef WITH_ATLAS
CALL TRACE%FINAL()
#endif
IF (LHOOK) CALL DR_HOOK('GP_MODEL',1,ZHOOK_HANDLE)
END SUBROUTINE GP_MODEL
