SUBROUTINE JMGFIXER(YDGEOMETRY,YDGMV,YDDPHY,YGFL,YDDYN,YDPTRSLB1,KL0,PB1,YDSL,PGMVS,PGMVT1S,PGFL,PGFLT1,PEXTRADYN)
!
!     Purpose.   Correct mass field to satisfy global conservation.
!                Preserves positive definitiness.
!     --------   
!
!*   Interface.
!     ----------
!
!     *CALL* *JMGFIXER(KL0,PB1,YDSL,PGMVS,PGMVT1S,PEXTRADYN)
!
!     Explicit arguments :
!     --------------------
!
!
!!     INPUT:
!     -------------
!        KL0         : indices of the four western points of the 16 point
!                      interpolation grid
!        PB1         : field to be interpolated
!        YDSL        : SL_STRUCT definition
!        PGMVS       : surface GMV variables at t and t-dt.
!        PGMVT1S     : surface GMV variables at t +dt 
!        PGFL        : GFL variables buffer t0 t1
!
!     OUTPUT:
!     -------
!        PGFLT1       : GFL variables buffer t + dt
!        PEXTRADYN    : mass fixer diagnostics (correction field)
!
!        Implicit arguments :  None.
!        --------------------
!
!     Method.
!     -------
!     - Code based on John Mac Gregor's mass fixing scheme,
!       CSIRO Atmospheric Research Technical Paper No. 70, 2005
!
!     Externals.   See includes below.
!     ----------
!
!     Reference.
!     ----------
!     Diamantakis M., Flemming J., 2014: Global mass fixer algorithms for conservative tracer transport 
!                                        in the ECMWF model
!
!     Author.
!     -------
!        Michail Diamantakis   *ECMWF*

! Modifications
! -------------
!  K. Yessad (Apr 2013): replace GPPREH by GPHPRE.
!  M. Diamantakis (Feb 2014): Put improved LTRCMFIX_PS=T code in separate subroutine
!  T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
! -----------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOM_YGFL     , ONLY : TYPE_GFLD
USE YOMDYN       , ONLY : TDYN
USE YOMGMV       , ONLY : TGMV
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMLUN       , ONLY : NULOUT
USE YOMMP0       , ONLY : MYPROC, NPROC
USE EINT_MOD     , ONLY : SL_STRUCT
USE PTRSLB1      , ONLY : TPTRSLB1
USE YOMDPHY      , ONLY : TDPHY
USE MPL_MODULE   , ONLY : MPL_ALLREDUCE

! -----------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TDPHY)       ,INTENT(INOUT) :: YDDPHY
TYPE(TDYN)        ,INTENT(INOUT) :: YDDYN
TYPE(TYPE_GFLD)   ,INTENT(INOUT) :: YGFL
TYPE(TPTRSLB1)    ,INTENT(INOUT) :: YDPTRSLB1
INTEGER(KIND=JPIM),INTENT(IN)    :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(SL_STRUCT)   ,INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB1(YDSL%NASLB1,YDPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM, &
 &                                       YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM1, &
 &                                         YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEXTRADYN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDDPHY%NVEXTRDYN,&
 &                                            YDGEOMETRY%YRDIM%NGPBLKS)
! -----------------------------------------------------------------------
INTEGER(KIND=JPIM) :: IBL,JKGLO,JROF, JFIX
INTEGER(KIND=JPIM) :: IST, IEND, JLEV, JGFL, IOVER, IUNDER, INEG
INTEGER(KIND=JPIM) :: JOVER, JUNDER, JNEG

REAL(KIND=JPRB) :: ZPRE0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPRE1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPSURF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS),&
 & ZPSURF1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS)

REAL(KIND=JPRB) :: ZFIELD3DT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD3DT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD2DT1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD2DT0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD1D(YDGEOMETRY%YRDIM%NPROMA,2*YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPOS3(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZNEG3(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPOS2(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZNEG2(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPOSNEG(YDGEOMETRY%YRDIM%NPROMA,2*YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZDIFPOS(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZDIFNEG(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZMINVFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZMAXVFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPRES0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPRES1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPSCAL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZDM(YGFL%NFLDSFIX), ZMAXW(YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZRMSDIF(YGFL%NFLDSFIX), ZRMSFLD(YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZTOTMASS(3,2*YGFL%NFLDSFIX), ZSUMPOSNEG(3,2*YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZDFLD, ZGFLMIN, ZF1, ZDELP0, ZDELP1, ZMAX, ZMIN
REAL(KIND=JPRB) :: ZDIFF, ZDMRATIO, ZRATIO, ZMAXDIF, ZALPHA, ZINVAL
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "pfixer.intfb.h"
#include "gpnorm1.intfb.h"
#include "gppwc.intfb.h"
#include "gppwcvfe.intfb.h"
#include "gphpre.intfb.h"
#include "laminmaxint.intfb.h"

! -----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('JMGFIXER',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,   YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB&
&  )

ASSOCIATE(LEXTRADF=>YGFL%LEXTRADF, LTRCMFIX_PS=>YGFL%LTRCMFIX_PS,   NFLDSFIX=>YGFL%NFLDSFIX,   NMFDIAGLEV=>YGFL%NMFDIAGLEV, &
& NMFIXFLDS=>YGFL%NMFIXFLDS,   NOPTVFE=>YGFL%NOPTVFE, YCOMP=>YGFL%YCOMP, ZMFIXEPS=>YGFL%ZMFIXEPS,   NPROMA=>YDDIM%NPROMA,   &
& NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA,   NVEXTRDYN=>YDDPHY%NVEXTRDYN,   NGPTOT=>YDGEM%NGPTOT,   &
& NGPTOTG=>YDGEM%NGPTOTG,   YT0=>YDGMV%YT0, YT1=>YDGMV%YT1,   MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9)
! -----------------------------------------------------------------------

! Initializations
IST=1
ZGFLMIN=MAX(100.0_JPRB*TINY(1.0_JPRB),1.0E-25_JPRB)

!------------------------------------------------------------------
! Compute 3d pressure field from 2d surf pressure
!------------------------------------------------------------------

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPSURF0(JROF,IBL) = EXP(PGMVS(JROF,YT0%MSP,IBL))
    ZPSURF1(JROF,IBL) = EXP(PGMVT1S(JROF,YT1%MSP,IBL))
  ENDDO
ENDDO
!$OMP END PARALLEL DO

IF (LTRCMFIX_PS) CALL PFIXER(YDGEOMETRY,YDGMV,YGFL,YDDYN,ZPSURF0,ZPSURF1,PGMVT1S)

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZPRE0,ZPRE1)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPRE0(JROF,NFLEVG) = ZPSURF0(JROF,IBL)
    ZPRE1(JROF,NFLEVG) = ZPSURF1(JROF,IBL)
  ENDDO
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE0) 
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE1)
  DO JLEV=0,NFLEVG
    DO JROF=IST,IEND
      ZPRES0(JROF,JLEV,IBL) = ZPRE0(JROF,JLEV)
      ZPRES1(JROF,JLEV,IBL) = ZPRE1(JROF,JLEV)
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO

!------------------------------------------------------------------
! Compute scaling factor for pressure as required by the algorithm
!------------------------------------------------------------------

IF (NOPTVFE==1) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZDELP0,ZDELP1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    ! Compute scaling factor for pressure
    DO JLEV=1,NFLEVG
      DO JROF=IST,IEND
        ZDELP0=YDVAB%VDELA(JLEV) + YDVAB%VDELB(JLEV)*ZPRES0(JROF,NFLEVG,IBL)
        ZDELP1=YDVAB%VDELA(JLEV) + YDVAB%VDELB(JLEV)*ZPRES1(JROF,NFLEVG,IBL)
        ZPSCAL(JROF,JLEV,IBL)= ZDELP0/ZDELP1
      ENDDO
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
ELSE
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZDELP0,ZDELP1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    DO JLEV=1,NFLEVG
      DO JROF=IST,IEND
        ZDELP0=ZPRES0(JROF,JLEV,IBL)-ZPRES0(JROF,JLEV-1,IBL)
        ZDELP1=ZPRES1(JROF,JLEV,IBL)-ZPRES1(JROF,JLEV-1,IBL)
        ZPSCAL(JROF,JLEV,IBL)= ZDELP0/ZDELP1
      ENDDO
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
ENDIF
   
!------------------------------------------------------------------
! Compute total column content for each grid-point
!------------------------------------------------------------------

IF (NMFDIAGLEV==0) THEN
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZDFLD,ZPOS3,ZNEG3,ZPRE1,ZPOS2,ZNEG2)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          ZDFLD=MAX(PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL),ZGFLMIN)&
                       & -ZPSCAL(JROF,JLEV,IBL)*PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
          ZPOS3(JROF,JLEV)=MAX(ZDFLD,0.0_JPRB)
          ZNEG3(JROF,JLEV)=MIN(ZDFLD,0.0_JPRB)
          ZDIFPOS(JROF,JLEV,JFIX,IBL)=ZPOS3(JROF,JLEV)
          ZDIFNEG(JROF,JLEV,JFIX,IBL)=ZNEG3(JROF,JLEV)
          ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
        ENDDO
      ENDDO
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZPOS2,ZPOS3,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZNEG2,ZNEG3,ZPRE1)
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZPOS2,ZPOS3,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZNEG2,ZNEG3,ZPRE1)
      ENDIF
      ZPOSNEG(IST:IEND,2*JFIX-1,IBL)=ZPOS2(IST:IEND,NFLEVG)
      ZPOSNEG(IST:IEND,2*JFIX,IBL)=ZNEG2(IST:IEND,NFLEVG)
    ENDDO
!$OMP END PARALLEL DO
  ENDDO
  CALL GPNORM1(YDGEOMETRY,ZPOSNEG,2*NFLDSFIX,.FALSE.,PNORMS=ZSUMPOSNEG)
ELSE
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZFIELD3DT0,ZFIELD3DT1,ZDFLD,ZPOS3,ZNEG3,ZPRE0,ZPRE1) &
!$OMP& PRIVATE(ZFIELD2DT0,ZFIELD2DT1,ZPOS2,ZNEG2)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      ZPRE0(IST:IEND,0)=ZPRES0(IST:IEND,0,IBL)
      ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
! FIELD3DT0 AND FIELD3DT1 ARE THE FIELDS BEFORE AND AFTER ADVECTION
          ZFIELD3DT0(JROF,JLEV)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
          ZFIELD3DT1(JROF,JLEV)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          ZDFLD=MAX(PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL),ZGFLMIN)&
              & -ZPSCAL(JROF,JLEV,IBL)*PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
          ZPOS3(JROF,JLEV)=MAX(ZDFLD,0.0_JPRB)
          ZNEG3(JROF,JLEV)=MIN(ZDFLD,0.0_JPRB)
          ZDIFPOS(JROF,JLEV,JFIX,IBL)=ZPOS3(JROF,JLEV)
          ZDIFNEG(JROF,JLEV,JFIX,IBL)=ZNEG3(JROF,JLEV)
          ZPRE0(JROF,JLEV)=ZPRES0(JROF,JLEV,IBL)
          ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
        ENDDO
      ENDDO
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZPOS2,ZPOS3,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZNEG2,ZNEG3,ZPRE1)
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZPOS2,ZPOS3,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZNEG2,ZNEG3,ZPRE1)
      ENDIF
      ZFIELD1D(IST:IEND,2*JFIX-1,IBL)=ZFIELD2DT0(IST:IEND,NFLEVG)
      ZFIELD1D(IST:IEND,2*JFIX,IBL)=ZFIELD2DT1(IST:IEND,NFLEVG)
      ZPOSNEG(IST:IEND,2*JFIX-1,IBL)=ZPOS2(IST:IEND,NFLEVG)
      ZPOSNEG(IST:IEND,2*JFIX,IBL)=ZNEG2(IST:IEND,NFLEVG)
    ENDDO
!$OMP END PARALLEL DO
  ENDDO
  CALL GPNORM1(YDGEOMETRY,ZFIELD1D,2*NFLDSFIX,.FALSE.,PNORMS=ZTOTMASS)
  CALL GPNORM1(YDGEOMETRY,ZPOSNEG,2*NFLDSFIX,.FALSE.,PNORMS=ZSUMPOSNEG)
ENDIF

! Correct field
DO JFIX=1,NFLDSFIX
  JGFL=NMFIXFLDS(JFIX)
  IF (LEXTRADF.AND.(JFIX<=NVEXTRDYN)) THEN
    PEXTRADYN(:,:,JFIX,:)=PGFLT1(:,:,YCOMP(JGFL)%MP1,:) 
  ENDIF
  IF (NMFDIAGLEV>1) THEN
    ZDM(JFIX)=ZTOTMASS(1,2*JFIX)-ZTOTMASS(1,2*JFIX-1)
    ZMAXW(JFIX)=0.0_JPRB
    ZRMSDIF(JFIX)=0.0_JPRB
    ZRMSFLD(JFIX)=0.0_JPRB
  ENDIF
  IF (ZSUMPOSNEG(1,2*JFIX-1)>ZGFLMIN) THEN
    ZRATIO=-ZSUMPOSNEG(1,2*JFIX)/ZSUMPOSNEG(1,2*JFIX-1)
    ZALPHA=MIN(ZRATIO,SQRT(ZRATIO))
    ZINVAL=1.0_JPRB/MAX(1.0_JPRB,ZALPHA)
    IF (NMFDIAGLEV>1) THEN
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZDIFF=ZALPHA*ZDIFPOS(JROF,JLEV,JFIX,IBL)+ZINVAL*ZDIFNEG(JROF,JLEV,JFIX,IBL)
            ZF1=ZPSCAL(JROF,JLEV,IBL)*PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)+ZDIFF
            ZDIFF=ZF1-PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZRMSFLD(JFIX)=ZRMSFLD(JFIX)+PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)*&
                                      & PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZRMSDIF(JFIX)=ZRMSDIF(JFIX)+ZDIFF*ZDIFF
            ZMAXW(JFIX)=MAX(ABS(ZDIFF),ZMAXW(JFIX))     
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZF1
          ENDDO
        ENDDO
      ENDDO
    ELSE
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZDIFF)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZDIFF=ZALPHA*ZDIFPOS(JROF,JLEV,JFIX,IBL)+ZINVAL*ZDIFNEG(JROF,JLEV,JFIX,IBL)
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=&
                   & ZPSCAL(JROF,JLEV,IBL)*PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)+ZDIFF
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
    ENDIF
! ADD CORRECTIONS TO DIAGNOSTIC ARRAY IF NEEDED
    IF (LEXTRADF.AND.(JFIX<=NVEXTRDYN)) THEN
      PEXTRADYN(:,:,JFIX,:)=PGFLT1(:,:,YCOMP(JGFL)%MP1,:)-PEXTRADYN(:,:,JFIX,:)
    ENDIF
  ENDIF
ENDDO

IF (NMFDIAGLEV>1) THEN
  IF (NPROC>1) THEN
    CALL MPL_ALLREDUCE(ZRMSDIF,'SUM',LDREPROD=.FALSE.,CDSTRING='JMGFIXER:')
    CALL MPL_ALLREDUCE(ZRMSFLD,'SUM',LDREPROD=.FALSE.,CDSTRING='JMGFIXER:')
    CALL MPL_ALLREDUCE(ZMAXW,'MAX',LDREPROD=.FALSE.,CDSTRING='JMGFIXER:')
  ENDIF
! write header for fixer output: mass imbalance, imbalance ratio to total mass x 100, max fixer correction
! over RMS norm of corrected field x 100, ratio of rms norm of correction field to norm of field x 100
  WRITE(NULOUT,*) '----------------------------------------------------------------------------'
  WRITE(NULOUT,*) '                  MASS FIXER GLOBAL DIAGNOSTICS                             '
  WRITE(NULOUT,*) '    FIELD         DM       DM/Mtot %    max(dij)/|field| %   |dij|/|field| %'
  WRITE(NULOUT,*) '----------------------------------------------------------------------------'
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    ZRMSFLD(JFIX)=MAX(ZGFLMIN,SQRT(ZRMSFLD(JFIX)/(NGPTOTG*NFLEVG)))
    ZRMSDIF(JFIX)=SQRT(ZRMSDIF(JFIX)/(NGPTOTG*NFLEVG))
    ZDMRATIO=100.0_JPRB*ZDM(JFIX)/MAX(ZGFLMIN,ZTOTMASS(1,2*JFIX-1))
    ZMAXDIF=100.0_JPRB*ZMAXW(JFIX)/ZRMSFLD(JFIX)
    ZRMSDIF(JFIX)=100.0_JPRB*ZRMSDIF(JFIX)/ZRMSFLD(JFIX)
    WRITE(NULOUT,'(A13,2X,E10.4,1X,F9.3,6X,F10.3,11X,F9.3,3(1X,E10.4))') TRIM(YCOMP(JGFL)%CNAME),&
                & ZDM(JFIX),ZDMRATIO,ZMAXDIF,ZRMSDIF(JFIX)
  ENDDO
ENDIF

!TEST FOR OVERSHOOTS/UNDERSHOOTS
IF (NMFDIAGLEV>1) THEN
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    IOVER=0
    IUNDER=0
    INEG=0
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV,JOVER,JUNDER,JNEG) &
!$OMP& PRIVATE(ZMAX,ZMIN,ZF1,ZMINVFLD,ZMAXVFLD) &
!$OMP& REDUCTION(+:IOVER,IUNDER,INEG)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      JOVER=0
      JUNDER=0
      JNEG=0
      CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
             & KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
             & ZMINVFLD,PMAXVFLD=ZMAXVFLD)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          ZMAX=ZMAXVFLD(JROF,JLEV)
          ZMIN=ZMINVFLD(JROF,JLEV)
          ZF1=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          IF (ZF1>ZMAX+100.0_JPRB*ZMFIXEPS) THEN
            JOVER=JOVER+1
          ENDIF
          IF (ZF1<ZMIN-100.0_JPRB*ZMFIXEPS) THEN
            JUNDER=JUNDER+1
          ENDIF
          IF (ZF1<0.0_JPRB) THEN
            JNEG=JNEG+1
          ENDIF
        ENDDO
      ENDDO
      IOVER=IOVER+JOVER
      IUNDER=IUNDER+JUNDER
      INEG=INEG+JNEG
    ENDDO
!$OMP END PARALLEL DO
    IF (NPROC>1) THEN
      CALL MPL_ALLREDUCE(IOVER,'SUM',CDSTRING='JMGFIXER:')
      CALL MPL_ALLREDUCE(IUNDER,'SUM',CDSTRING='JMGFIXER:')
      CALL MPL_ALLREDUCE(INEG,'SUM',CDSTRING='JMGFIXER:')
    ENDIF
    IF (MYPROC==1) THEN
      IF (IOVER>0) THEN
        WRITE(NULOUT,'(A23,1X,A13,A29,I7,1X,A7)') 'SUB JMGFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
               & ' FIXER OVERSHOOTS MAXIMUM AT',IOVER,'POINTS!'
!'
        WRITE(NULOUT,'(A15,F8.2,A11)') 'OVERSHOOTS AT',100.*IOVER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
      IF (IUNDER>0) THEN
        WRITE(NULOUT,'(A23,1X,A13,A30,I7,1X,A7)') 'SUB JMGFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
               & ' FIXER UNDERSHOOTS MINIMUM AT',IUNDER,'POINTS!'
!'
        WRITE(NULOUT,'(A16,F8.2,A11)') 'UNDERSHOOTS AT',100.*IUNDER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
      IF (INEG>0) THEN
        WRITE(NULOUT,'(A23,1X,A13,A22,I7,1X,A7)') 'SUB JMGFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
                      & ' FIXER CREATED -VE AT', INEG,'POINTS!'
!'
        WRITE(NULOUT,'(A15,F8.2,A11)') '-VE VALUES AT',100.*INEG/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
    ENDIF
  ENDDO ! loop JGFL
ENDIF

! -----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('JMGFIXER',1,ZHOOK_HANDLE)
END SUBROUTINE JMGFIXER
