SUBROUTINE NEGFIXER(YDGEOMETRY,YDGMV,YDML_GCONF,YDPTRSLB1,KL0,PB1,YDSL,PGMVS,PGMVT1S,PGFL,PGFLT1)
!
!     Purpose.   Eliminate negatives from advected field. Gathers relevant diagnostics.
!     --------   
!
!*   Interface.
!     ----------
!
!        *CALL* *NEGFIXER(KL0,PB1,YDSL,PGMVS,PGMVT1S,PGFL,PGFLT1)
!
!     Explicit arguments :
!     --------------------
!
!
!!     INPUT:
!     -------------
!        KL0         : indices of the four western points of the 16 point
!                      interpolation grid
!        PB1         : field to be interpolated
!        YDSL        : SL_STRUCT definition
!        PGMVS       : surface GMV variables at t and t-dt.
!        PGMVT1S     : surface GMV variables at t +dt 
!
!     OUTPUT:
!     -------
!        PGFL         : GFL variables buffer t 
!        PGFLT1       : GFL variables buffer t + dt
!
!        Implicit arguments :  None.
!        --------------------
!
!     Externals.   See includes below.
!     ----------
!
!     Reference..
!     -----------
!     Rasch P.J. and Williamson D.L., Computational aspects of moisture transport in 
!     global models of the atmosphere, Q.J.R. Meteorol. Soc. (1990), 116
!
!     Author.
!     -------
!        Michail Diamantakis   *ECMWF*

! Modifications
! -------------
!  K. Yessad (Apr 2013): replace GPPREH by GPHPRE.
!  T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
! ---------------------------------------------------------------------------

USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE YOMGMV                 , ONLY : TGMV
USE PARKIND1               , ONLY : JPIM, JPRB
USE YOMHOOK                , ONLY : LHOOK, DR_HOOK
USE YOMLUN                 , ONLY : NULOUT
USE YOMMP0                 , ONLY : MYPROC, NPROC
USE EINT_MOD               , ONLY : SL_STRUCT
USE PTRSLB1                , ONLY : TPTRSLB1
USE MPL_MODULE             , ONLY : MPL_ALLREDUCE

! ---------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT):: YDML_GCONF
TYPE(TPTRSLB1)    ,INTENT(INOUT) :: YDPTRSLB1
INTEGER(KIND=JPIM),INTENT(IN)    :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(SL_STRUCT)   ,INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB1(YDSL%NASLB1,YDPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_GCONF%YGFL%NDIM, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_GCONF%YGFL%NDIM1, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
! ---------------------------------------------------------------------------
INTEGER(KIND=JPIM) :: IST, IEND, JLEV, JGFL
INTEGER(KIND=JPIM) :: IBL,JKGLO,JROF,JFIX
INTEGER(KIND=JPIM) :: INEGTOT(YDML_GCONF%YGFL%NNEGAFIX), IPOSTOT(YDML_GCONF%YGFL%NNEGAFIX)

REAL(KIND=JPRB), ALLOCATABLE :: ZPRE0(:,:), ZPRE1(:,:), ZPRES0(:,:,:), ZPRES1(:,:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZFIELD2DT0(:,:), ZFIELD2DT1(:,:), ZFIELD2DT2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZFIELD3DT0(:,:), ZFIELD3DT1(:,:), ZFIELD3DT2(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZFIELD2DNEG(:,:), ZFIELD3DNEG(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZFIELD1D(:,:,:), ZFIELD1DNEG(:,:,:)

REAL(KIND=JPRB)    :: ZSPT1(YDGEOMETRY%YRDIM%NPROMA),ZSPT0(YDGEOMETRY%YRDIM%NPROMA),&
 & ZMINVFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    :: ZDM1,ZDM2,ZFLD,ZDMRATIO1,ZDMRATIO2,ZNERATIO,ZGFLMIN,ZMFIXEPS
REAL(KIND=JPRB)    :: ZTOTMASS(3,3*YDML_GCONF%YGFL%NNEGAFIX),ZTOTNEG(3,YDML_GCONF%YGFL%NNEGAFIX), &
 & ZSTORENEG(YDML_GCONF%YGFL%NNEGAFIX)
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

#include "gpnorm1.intfb.h"
#include "gppwc.intfb.h"
#include "gppwcvfe.intfb.h"
#include "gphpre.intfb.h"
#include "laminmaxint.intfb.h"

! ---------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('NEGFIXER',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,   YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB,   &
& YGFL=>YDML_GCONF%YGFL)

ASSOCIATE(NMFDIAGLEV=>YGFL%NMFDIAGLEV,   NNEGAFIX=>YGFL%NNEGAFIX, NNEGFLDS=>YGFL%NNEGFLDS,   NOPTNEGFIX=>YGFL%NOPTNEGFIX, &
& NOPTVFE=>YGFL%NOPTVFE, YCOMP=>YGFL%YCOMP,   NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA,   NFLEN=>YDDIMV%NFLEN,        &
& NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA,   NGPTOT=>YDGEM%NGPTOT, NGPTOTG=>YDGEM%NGPTOTG,   YT0=>YDGMV%YT0,           &
& YT1=>YDGMV%YT1,   MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9)
! ---------------------------------------------------------------------------

! Initializations
ZMFIXEPS=2.0_JPRB*EPSILON(1.0_JPRB)
ZGFLMIN=0.0_JPRB
IST=1

! Simple calculation - no diagnostics
IF ( NMFDIAGLEV==0 ) THEN
  IF (NOPTNEGFIX==1) THEN
    DO JFIX=1,NNEGAFIX
      JGFL=NNEGFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=MAX(PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL),ZGFLMIN)
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
    ENDDO
  ELSE
    DO JFIX=1,NNEGAFIX
      JGFL=NNEGFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZMINVFLD,ZFLD)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
          &      KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
          &      ZMINVFLD)
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZFLD=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            IF (ZFLD<ZGFLMIN) THEN
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZMINVFLD(JROF,JLEV)
            ENDIF
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
    ENDDO
  ENDIF
ELSE
! More complex calculation with diagnostics
  ALLOCATE(ZFIELD1D(NPROMA,3*NNEGAFIX,NGPBLKS))
  ALLOCATE(ZFIELD1DNEG(NPROMA,NNEGAFIX,NGPBLKS))
  ALLOCATE(ZPRE0(NPROMA,0:NFLEVG))
  ALLOCATE(ZPRE1(NPROMA,0:NFLEVG))
  ALLOCATE(ZPRES0(NPROMA,0:NFLEVG,NGPBLKS))
  ALLOCATE(ZPRES1(NPROMA,0:NFLEVG,NGPBLKS))
  ALLOCATE(ZFIELD2DT0(NPROMA,0:NFLEVG))
  ALLOCATE(ZFIELD2DT1(NPROMA,0:NFLEVG))
  ALLOCATE(ZFIELD2DT2(NPROMA,0:NFLEVG))
  ALLOCATE(ZFIELD3DT0(NPROMA,NFLEVG))
  ALLOCATE(ZFIELD3DT1(NPROMA,NFLEVG))
  ALLOCATE(ZFIELD3DT2(NPROMA,NFLEVG))
  ALLOCATE(ZFIELD2DNEG(NPROMA,0:NFLEVG))
  ALLOCATE(ZFIELD3DNEG(NPROMA,NFLEVG))
! First calculate pressure on interface for t & t + 1
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZSPT0,ZSPT1,ZPRE0,ZPRE1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    DO JROF=IST,IEND
      ZSPT0(JROF)=PGMVS(JROF,YT0%MSP,IBL)
      ZPRE0(JROF,NFLEVG) = EXP(ZSPT0(JROF))
      ZSPT1(JROF)=PGMVT1S(JROF,YT1%MSP,IBL)
      ZPRE1(JROF,NFLEVG) = EXP(ZSPT1(JROF))
    ENDDO
    CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE0) 
    CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE1)
    DO JLEV=0,NFLEVG
      DO JROF=IST,IEND
        ZPRES0(JROF,JLEV,IBL) = ZPRE0(JROF,JLEV)
        ZPRES1(JROF,JLEV,IBL) = ZPRE1(JROF,JLEV)
      ENDDO
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
  IF (NOPTNEGFIX==1) ZMINVFLD(:,:)=ZGFLMIN
  DO JFIX=1,NNEGAFIX
    JGFL=NNEGFLDS(JFIX)
    INEGTOT(JFIX)=0
    IPOSTOT(JFIX)=0
    ZSTORENEG(JFIX)=0.0_JPRB
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      IF (NOPTNEGFIX==2) THEN
        CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
          &      KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
          &      ZMINVFLD)
      ENDIF
      ZPRE0(IST:IEND,0)=ZPRES0(IST:IEND,0,IBL)
      ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          ZFLD=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          IF (ZFLD<ZGFLMIN) THEN
            ZFIELD3DNEG(JROF,JLEV)=ZFLD
            ZSTORENEG(JFIX)=MIN(ZSTORENEG(JFIX),ZFLD)
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZMINVFLD(JROF,JLEV)
            INEGTOT(JFIX)=INEGTOT(JFIX)+1
          ELSE
            ZFIELD3DNEG(JROF,JLEV)=0.0_JPRB
            IPOSTOT(JFIX)=IPOSTOT(JFIX)+1
          ENDIF
          ZFIELD3DT0(JROF,JLEV)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
          ZFIELD3DT1(JROF,JLEV)=ZFLD
          ZFIELD3DT2(JROF,JLEV)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          ZPRE0(JROF,JLEV)=ZPRES0(JROF,JLEV,IBL)
          ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
        ENDDO
      ENDDO
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT2,ZFIELD3DT2,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DNEG,ZFIELD3DNEG,ZPRE1)
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT2,ZFIELD3DT2,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DNEG,ZFIELD3DNEG,ZPRE1)
      ENDIF
      ZFIELD1D(IST:IEND,3*JFIX-2,IBL) = ZFIELD2DT0(IST:IEND,NFLEVG)
      ZFIELD1D(IST:IEND,3*JFIX-1,IBL) = ZFIELD2DT1(IST:IEND,NFLEVG)
      ZFIELD1D(IST:IEND,3*JFIX,IBL)   = ZFIELD2DT2(IST:IEND,NFLEVG)
      ZFIELD1DNEG(IST:IEND,JFIX,IBL)  = ZFIELD2DNEG(IST:IEND,NFLEVG)
    ENDDO
  ENDDO
  
  CALL GPNORM1(YDGEOMETRY,ZFIELD1D,3*NNEGAFIX,.FALSE.,PNORMS=ZTOTMASS)
  CALL GPNORM1(YDGEOMETRY,ZFIELD1DNEG,NNEGAFIX,.FALSE.,PNORMS=ZTOTNEG)
  
  WRITE(NULOUT,*) '--------------------------------------------------------------------'
  WRITE(NULOUT,*) '         NEG  FIXER GLOBAL DIAGNOSTICS                              '
  WRITE(NULOUT,*) '    FIELD         DM        DM/Mtot %    DMfix/Mtot %   Mneg/Mtot % '
  WRITE(NULOUT,*) '--------------------------------------------------------------------'

  DO JFIX=1,NNEGAFIX
    JGFL=NNEGFLDS(JFIX)
    ZDM1=ZTOTMASS(1,3*JFIX-1)-ZTOTMASS(1,3*JFIX-2)
    ZDM2=ZTOTMASS(1,3*JFIX)-ZTOTMASS(1,3*JFIX-1)
    ZDMRATIO1 =  100.0_JPRB*ZDM1/MAX(ZMFIXEPS,ZTOTMASS(1,3*JFIX-1))
    ZDMRATIO2 =  100.0_JPRB*ZDM2/MAX(ZMFIXEPS,ZTOTMASS(1,3*JFIX-1))
    ZNERATIO  = -100.0_JPRB*ZTOTNEG(1,JFIX)/MAX(ZMFIXEPS,ZTOTMASS(1,3*JFIX-1))
    WRITE(NULOUT,'(A13,2X,E10.4,3(3X,F9.3))') TRIM(YCOMP(JGFL)%CNAME),&
      &           ZDM1,ZDMRATIO1,ZDMRATIO2,ZNERATIO
  ENDDO

  IF (NMFDIAGLEV>1) THEN
    
    DO JFIX=1,NNEGAFIX
      JGFL=NNEGFLDS(JFIX)
      IF (NPROC>1) THEN
        CALL MPL_ALLREDUCE(INEGTOT(JFIX),'SUM',CDSTRING='NEGFIXER:')
        CALL MPL_ALLREDUCE(IPOSTOT(JFIX),'SUM',CDSTRING='NEGFIXER:')
        CALL MPL_ALLREDUCE(ZSTORENEG(JFIX),'MIN',CDSTRING='NEGFIXER:')
      ENDIF
      IF (MYPROC==1) THEN
        IF (INEGTOT(JFIX)>0) THEN
          WRITE(NULOUT,'(A23,A13,A22,2(I9,1X,A8))') 'SUB NEGFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
            &             ' HAD', INEGTOT(JFIX),'-VE PTS ',IPOSTOT(JFIX),' +VE PTS'
          WRITE(NULOUT,'(A15,F8.2,A11)') '-VE VALUES AT',100.*INEGTOT(JFIX)/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
          WRITE(NULOUT,'(A15,F8.2,A11)') '+VE VALUES AT',100.*IPOSTOT(JFIX)/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
          WRITE(NULOUT,'(A20,E10.4)')   'MOST -VE VALUE IS:',ZSTORENEG(JFIX)
        ENDIF
      ENDIF
    ENDDO ! loop JFIX
    
  ENDIF

  DEALLOCATE(ZFIELD2DT0)
  DEALLOCATE(ZFIELD2DT1)
  DEALLOCATE(ZFIELD2DT2)
  DEALLOCATE(ZFIELD3DT0)
  DEALLOCATE(ZFIELD3DT1)
  DEALLOCATE(ZFIELD3DT2)
  DEALLOCATE(ZFIELD2DNEG)
  DEALLOCATE(ZFIELD3DNEG)
  DEALLOCATE(ZFIELD1D)
  DEALLOCATE(ZFIELD1DNEG)
  DEALLOCATE(ZPRE0)
  DEALLOCATE(ZPRE1)
  DEALLOCATE(ZPRES0)
  DEALLOCATE(ZPRES1)
  
ENDIF

! ---------------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('NEGFIXER',1,ZHOOK_HANDLE)
END SUBROUTINE NEGFIXER
