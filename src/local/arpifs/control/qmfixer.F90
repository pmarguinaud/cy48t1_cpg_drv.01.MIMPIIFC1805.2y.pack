SUBROUTINE QMFIXER(YDGEOMETRY,YDGMV,YDCOMPO,YDDPHY,YGFL,YDML_DYN,KL0,PLSCAW,PB1,YDSL,PGMVS,PGMVT1S,PGFL,&
                 & PGFLT1,PEXTRADYN)
!
!     Purpose.   Correct mass field to satisfy global conservation.
!                Preserves quasi-monotonicity.
!     --------   
!
!*   Interface.
!     ----------
!
!        *CALL* *QMFIXER(...)*
!
!     Explicit arguments :
!     --------------------
!
!
!!     INPUT:
!     -------------
!        KL0         : indices of the four western points of the 16 point
!                      interpolation grid
!        PLSCAW      : linear weights (distances) for interpolations.
!        PB1         : field to be interpolated
!        YDSL        : SL_STRUCT definition
!        PGMVS       : surface GMV variables at t and t-dt.
!        PGMVT1S     : surface GMV variables at t +dt 
!        PGFL        : GFL variables buffer t0 t1
!
!     OUTPUT:
!     -------
!        PGFLT1       : GFL variables buffer t + dt
!        PEXTRADYN    : mass fixer diagnostics (correction field)
!
!        Implicit arguments :  None.
!        --------------------
!
!     Method.
!     -------
!     - Code based on Bermejo & Conde scheme MWR Feb 2002
!
!     Externals.   See includes below.
!     ----------

!     Reference.
!     ----------
!     Diamantakis M., Flemming J., 2014: Global mass fixer algorithms for conservative tracer transport 
!                                       in the ECMWF model
!
!     Author.
!     -------
!     Michail Diamantakis   *ECMWF*

!     Modifications
!     -------------
!     M. Diamantakis  01-12-2012  Add openMP directives, improve code structure & 
!                                 diagnostic output
!     K. Yessad (Apr 2013): replace GPPREH by GPHPRE.
!     M. Diamantakis (Feb 2014): Put improved LTRCMFIX_PS=T code in separate subroutine
!  
!     T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!
!     M. Diamantakis (Feb 2015): modifications:
!                                 
!                                (1) addition of NOPTMFBC=3,4 i.e. multiplicative version
!                                    of BC/Ze fixer 
!                                (2) allow mass fixing when a general exponential factor beta>0 
!                                    is used to compute the weight which determines how much to correct 
!                                (3) allow use of different exponential factor beta for different species
!                                (4) incorporate proportional fixer and allow combination of BC fixers 
!                                    with simple proportional fixer
!
!     M. Diamantakis (Feb 2016): add vertical scaling for weighting factor to improve stratospheric transport
!
!     M. Diamantakis (Mar 2017): add embedded limiter to be always positive 
!                                definite and quasi-monotone
!                                use alternative vertical scaling factor 
!                                proportional to gribox mass
! End Modifications
!------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE YOM_YGFL           , ONLY : TYPE_GFLD
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM , JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT
USE YOMMP0             , ONLY : MYPROC, NPROC
USE EINT_MOD           , ONLY : SL_STRUCT
USE YOMDPHY            , ONLY : TDPHY
USE YOMDYNA            , ONLY : YRDYNA
USE MPL_MODULE         , ONLY : MPL_ALLREDUCE
USE YOMCOMPO           , ONLY : TCOMPO

!------------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)           ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)               ,INTENT(INOUT) :: YDGMV
TYPE(TCOMPO)             ,INTENT(INOUT) :: YDCOMPO
TYPE(TDPHY)              ,INTENT(INOUT) :: YDDPHY
TYPE(MODEL_DYNAMICS_TYPE),INTENT(INOUT) :: YDML_DYN
TYPE(TYPE_GFLD)          ,INTENT(INOUT) :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(SL_STRUCT)   ,INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTLSCAW%NDIM, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB1(YDSL%NASLB1,YDML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM1, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEXTRADYN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDDPHY%NVEXTRDYN,&
 & YDGEOMETRY%YRDIM%NGPBLKS)
!------------------------------------------------------------------------------
INTEGER(KIND=JPIM) :: IST,  IEND, IBL, JKGLO, JROF, JLEV
INTEGER(KIND=JPIM) :: JFIX, JGFL, IOVER, IUNDER, INEG, JOVER, JUNDER, JNEG

LOGICAL :: LLTRCMFQM=.TRUE. ! Hardwired flag - used to ensure global mass
                            ! conservation is restored after limiter is used
                            ! Normally the effect of limiter is negligible
                            ! and could be switched off.
LOGICAL :: LLMFPROP(YGFL%NFLDSFIX)

REAL(KIND=JPRB), ALLOCATABLE :: ZGMVT1(:,:,:,:)

REAL(KIND=JPRB) :: ZPSURF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS),&
 & ZPSURF1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZFIELD3DT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD3DT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD3DW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD2DT1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD2DT0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD2DW(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD1D(YDGEOMETRY%YRDIM%NPROMA,2*YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZFIELD1DW(YDGEOMETRY%YRDIM%NPROMA,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZWEIGHT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPRES0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZPRES1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB) :: ZLIN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZMINVFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZMAXVFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDM(YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZLAMBDA(YGFL%NFLDSFIX), ZFAC(YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZTOTMASS(3,2*YGFL%NFLDSFIX),ZSUMW(3,YGFL%NFLDSFIX),ZTOTMASS1(3,YGFL%NFLDSFIX)
REAL(KIND=JPRB) :: ZBETAMFBC, ZDIFF, ZDIFW, ZINC, ZDMRATIO, ZQM, ZSCALE, ZGFLMIN
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!------------------------------------------------------------------------------

#include "pfixer.intfb.h"
#include "gpnorm1.intfb.h"
#include "gppwc.intfb.h"
#include "gppwcvfe.intfb.h"
#include "gphpre.intfb.h"
#include "laitli.intfb.h"
#include "laminmaxint.intfb.h"

!------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('QMFIXER',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,   YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB, &
& YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB,  YDPTRSLB1=>YDML_DYN%YRPTRSLB1 ) 

ASSOCIATE(LEXTRADF=>YGFL%LEXTRADF, LTRCMFIX_PS=>YGFL%LTRCMFIX_PS,   LTRCMFP=>YGFL%LTRCMFP, NFLDSFIX=>YGFL%NFLDSFIX, &
& NMFDIAGLEV=>YGFL%NMFDIAGLEV,   NMFIXFLDS=>YGFL%NMFIXFLDS, NOPTMFBC=>YGFL%NOPTMFBC, NOPTVFE=>YGFL%NOPTVFE,         &
& YCOMP=>YGFL%YCOMP,   NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA,   NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG,  &
& NFLSA=>YDDIMV%NFLSA,   NVEXTRDYN=>YDDPHY%NVEXTRDYN,   NGPTOT=>YDGEM%NGPTOT, NGPTOTG=>YDGEM%NGPTOTG,               &
& YT0=>YDGMV%YT0, YT1=>YDGMV%YT1,   MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9)
!------------------------------------------------------------------------------

! Initializations
IST=1
ZGFLMIN=MAX(100.0_JPRB*TINY(1.0_JPRB),1.0E-25_JPRB)
LLMFPROP(:)=.FALSE.
ZFAC(:)=1.0_JPRB
IF (LEXTRADF.OR.NMFDIAGLEV>1) THEN 
  ALLOCATE(ZGMVT1(NPROMA,NFLEVG,NFLDSFIX,NGPBLKS))
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    ZGMVT1(1:NPROMA,1:NFLEVG,JFIX,1:NGPBLKS)=PGFLT1(1:NPROMA,1:NFLEVG,YCOMP(JGFL)%MP1,1:NGPBLKS)
  ENDDO
ENDIF

!------------------------------------------------------------------
! Compute 3d pressure field from 2d surf pressure
!------------------------------------------------------------------

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPSURF0(JROF,IBL) = EXP(PGMVS(JROF,YT0%MSP,IBL))
    ZPSURF1(JROF,IBL) = EXP(PGMVT1S(JROF,YT1%MSP,IBL))
  ENDDO
ENDDO
!$OMP END PARALLEL DO

IF (LTRCMFIX_PS) CALL PFIXER(YDGEOMETRY,YDGMV,YGFL,YDML_DYN%YRDYN,ZPSURF0,ZPSURF1,PGMVT1S)

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPRES0(JROF,NFLEVG,IBL) = ZPSURF0(JROF,IBL)
    ZPRES1(JROF,NFLEVG,IBL) = ZPSURF1(JROF,IBL)
  ENDDO
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRES0(1,0,IBL)) 
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRES1(1,0,IBL))
ENDDO
!$OMP END PARALLEL DO
   
!------------------------------------------------------------------
! Compute total tracer mass before and after SL advection
!------------------------------------------------------------------

DO JFIX=1,NFLDSFIX
  JGFL=NMFIXFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZFIELD2DT0,ZFIELD2DT1,ZFIELD3DT0,ZFIELD3DT1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    DO JLEV=1,NFLEVG
      DO JROF=IST,IEND
        ZFIELD3DT0(JROF,JLEV)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
        ZFIELD3DT1(JROF,JLEV)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
      ENDDO
    ENDDO
    IF (NOPTVFE==1) THEN
      CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRES0(1,0,IBL))
      CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRES1(1,0,IBL))
    ELSE
      CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRES0(1,0,IBL))
      CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRES1(1,0,IBL))
    ENDIF
    ZFIELD1D(IST:IEND,2*JFIX-1,IBL)=ZFIELD2DT0(IST:IEND,NFLEVG)
    ZFIELD1D(IST:IEND,2*JFIX,IBL)=ZFIELD2DT1(IST:IEND,NFLEVG)
  ENDDO
!$OMP END PARALLEL DO
ENDDO

CALL GPNORM1(YDGEOMETRY,ZFIELD1D,2*NFLDSFIX,.FALSE.,PNORMS=ZTOTMASS)

DO JFIX=1,NFLDSFIX
  ZDM(JFIX)=ZTOTMASS(1,2*JFIX)-ZTOTMASS(1,2*JFIX-1)
! compute a scaling factor for the weight to allow calculation of correction for small numbers
  IF (ZDM(JFIX)>=0.0_JPRB) THEN
    ZSCALE=MAX(ZDM(JFIX),1.0E-8_JPRB)
  ELSE
    ZSCALE=MIN(ZDM(JFIX),-1.0E-8_JPRB)
  ENDIF
  JGFL=NMFIXFLDS(JFIX)
  ZBETAMFBC=YCOMP(JGFL)%BETAMFBC
  IF (LTRCMFP.OR.ZBETAMFBC<0.0_JPRB) LLMFPROP(JFIX)=.TRUE.
  IF (.NOT.LLMFPROP(JFIX)) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZQM,ZDIFF,ZDIFW,ZLIN,ZFIELD3DW,ZFIELD2DW)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      IF (YRDYNA%LCOMAD_GFL.AND.YCOMP(JGFL)%LCOMAD) THEN
        CALL LAITLI(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,&
          & NFLEN,PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLAMAD,IBL),&
          & PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLOMAD+1,IBL),&
          & KL0(1,1,1,IBL),PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDVERMAD,IBL),&
          & PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),ZLIN)
      ELSE
        CALL LAITLI(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,&
                  & NFLEN,PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLAT,IBL),&
                  & PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLO+1,IBL),&
                  & KL0(1,1,1,IBL),PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDVER,IBL),&
                  & PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),ZLIN)
      ENDIF
      IF (NOPTMFBC==1.OR.NOPTMFBC==3) THEN
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZQM=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZDIFF=(ZQM-ZLIN(JROF,JLEV))/ZSCALE
            ZDIFW=0.0_JPRB
            IF (ZDIFF>ZGFLMIN) THEN
              ZDIFW=(ZDIFF**ZBETAMFBC)*ABS(YDGSGEOM_NB%GAW(JKGLO-1+JROF)* &
     &               (ZPRES1(JROF,JLEV,IBL)-ZPRES1(JROF,JLEV-1,IBL)))
              IF (NOPTMFBC==3) ZDIFW=ZDIFW*ZQM
            ENDIF
            ZWEIGHT(JROF,JLEV,JFIX,IBL)=ZDIFW
            ZFIELD3DW(JROF,JLEV)=ZWEIGHT(JROF,JLEV,JFIX,IBL)
          ENDDO
        ENDDO
      ELSEIF (NOPTMFBC==2.OR.NOPTMFBC==4) THEN
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZQM=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZDIFF=ABS((ZQM-ZLIN(JROF,JLEV))/ZSCALE)
            ZDIFW=(ZDIFF**ZBETAMFBC)*ABS(YDGSGEOM_NB%GAW(JKGLO-1+JROF)* &
     &               (ZPRES1(JROF,JLEV,IBL)-ZPRES1(JROF,JLEV-1,IBL)))
            IF (NOPTMFBC==4) ZDIFW=ZDIFW*ABS(ZQM)
            ZWEIGHT(JROF,JLEV,JFIX,IBL)=ZDIFW
            ZFIELD3DW(JROF,JLEV)=ZWEIGHT(JROF,JLEV,JFIX,IBL)
          ENDDO
        ENDDO
      ENDIF
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DW,ZFIELD3DW,ZPRES1(1,0,IBL))
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DW,ZFIELD3DW,ZPRES1(1,0,IBL))
      ENDIF
      ZFIELD1DW(IST:IEND,JFIX,IBL)=ZFIELD2DW(IST:IEND,NFLEVG)
    ENDDO
!$OMP END PARALLEL DO
  ELSE
    ZDMRATIO=1.0_JPRB
    IF (ZTOTMASS(1,2*JFIX)>ZGFLMIN) ZDMRATIO=ZTOTMASS(1,2*JFIX-1)/ZTOTMASS(1,2*JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZDMRATIO*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
        ENDDO
      ENDDO
! if LTRCMFP is used for all species no need to set ZFIELD1DW (not used)
      IF (.NOT.LTRCMFP) ZFIELD1DW(IST:IEND,JFIX,IBL)=0.0_JPRB
    ENDDO
!$OMP END PARALLEL DO
  ENDIF
ENDDO

IF (.NOT.LTRCMFP) CALL GPNORM1(YDGEOMETRY,ZFIELD1DW,NFLDSFIX,.FALSE.,PNORMS=ZSUMW)

!--------------------------------------------------------------------------------
! - Compute mass correction for BC fixer 
! - Limit any overshoots/undershoots due to fixer
!--------------------------------------------------------------------------------
DO JFIX=1,NFLDSFIX
  JGFL=NMFIXFLDS(JFIX)
  IF (.NOT.LLMFPROP(JFIX)) THEN
    ZLAMBDA(JFIX)=ZDM(JFIX)/MAX(ZSUMW(1,JFIX),ZGFLMIN)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV,ZMINVFLD,ZMAXVFLD,ZINC)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      IF (NOPTMFBC==1.OR.NOPTMFBC==3) THEN
        CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
          & KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
          & ZMINVFLD,PMAXVFLD=ZMAXVFLD)
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZINC=-ZLAMBDA(JFIX)*ZWEIGHT(JROF,JLEV,JFIX,IBL)
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)= &
          &    PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL) + ZINC
            IF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)<ZMINVFLD(JROF,JLEV) &
              &  .AND.ZINC<0.0_JPRB) THEN
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZMINVFLD(JROF,JLEV)
            ELSEIF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)>ZMAXVFLD(JROF,JLEV) &
              &  .AND.ZINC>0.0_JPRB) THEN
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZMAXVFLD(JROF,JLEV)
            ENDIF
          ENDDO
        ENDDO
      ELSE ! Ze fixer case (positive definite only)
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZINC=-ZLAMBDA(JFIX)*ZWEIGHT(JROF,JLEV,JFIX,IBL)
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)= &
            &  PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL) + ZINC
            IF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)<ZGFLMIN &
            &  .AND.ZINC<0.0_JPRB) THEN
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZGFLMIN
            ENDIF
          ENDDO
        ENDDO
      ENDIF
    ENDDO
!$OMP END PARALLEL DO
  ENDIF
ENDDO

!-------------------------------------------------------------------------
! The corrected overshoots/undershoots BC fixer may have generated
! have broken mass cons and gridpoint values need to be re-adjusted. 
! Simple proportional MF is used now as correction would be very small
!-------------------------------------------------------------------------
IF (.NOT.LTRCMFP.AND.LLTRCMFQM) THEN
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    IF (.NOT.LLMFPROP(JFIX)) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZFIELD2DT1,ZFIELD3DT1)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZFIELD3DT1(JROF,JLEV)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          ENDDO
        ENDDO
        IF (NOPTVFE==1) THEN
          CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRES1(1,0,IBL))
        ELSE
          CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRES1(1,0,IBL))
        ENDIF
        ZFIELD1DW(IST:IEND,JFIX,IBL)=ZFIELD2DT1(IST:IEND,NFLEVG)
      ENDDO
!$OMP END PARALLEL DO
    ENDIF
  ENDDO
  
  CALL GPNORM1(YDGEOMETRY,ZFIELD1DW,NFLDSFIX,.FALSE.,PNORMS=ZTOTMASS1)

  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    IF (.NOT.LLMFPROP(JFIX)) THEN
      ZFAC(JFIX)=ZTOTMASS(1,2*JFIX-1)/ZTOTMASS1(1,JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZFAC(JFIX)*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
    ENDIF
  ENDDO
ENDIF

!----------------------------------------------------------------------------
! compute output file diagnostics if needed
!-----------------------------------------------------------------------------
IF (NMFDIAGLEV>1) THEN
  WRITE(NULOUT,*) '------------------------------------------------------'
  WRITE(NULOUT,*) '           MASS FIXER GLOBAL DIAGNOSTICS              '
  WRITE(NULOUT,*) '     FIELD        DM       DM/Mtot %    DMlim/DMadv   '
  WRITE(NULOUT,*) '------------------------------------------------------'
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    ZDMRATIO=100.0_JPRB*ZDM(JFIX)/(ZGFLMIN+ZTOTMASS(1,2*JFIX-1))
    IF (.NOT.LLMFPROP(JFIX)) THEN
      WRITE(NULOUT,'(A13,2X,E10.4,1X,F9.3,5X,E10.4)') TRIM(YCOMP(JGFL)%CNAME),&
        & ZDM(JFIX),ZDMRATIO,(ZTOTMASS1(1,JFIX)-ZTOTMASS(1,2*JFIX-1))/(ABS(ZDM(JFIX))+ZGFLMIN)
    ELSE
      WRITE(NULOUT,'(A13,2X,E10.4,1X,F9.3,5X,E10.4)') TRIM(YCOMP(JGFL)%CNAME),&
        & ZDM(JFIX),ZDMRATIO,1.0_JPRB
    ENDIF
  ENDDO
ENDIF

! ADD CORRECTIONS TO DIAGNOSTIC ARRAY IF NEEDED
IF (LEXTRADF) THEN
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    IF (JFIX<=NVEXTRDYN) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            PEXTRADYN(JROF,JLEV,JFIX,IBL)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)-ZGMVT1(JROF,JLEV,JFIX,IBL)
          ENDDO
        ENDDO
      ENDDO
!$OMP END PARALLEL DO
    ENDIF
  ENDDO
ENDIF

!TEST FOR OVERSHOOTS/UNDERSHOOTS
IF (NMFDIAGLEV>1) THEN
  DO JFIX=1,NFLDSFIX
    JGFL=NMFIXFLDS(JFIX)
    IOVER=0
    IUNDER=0
    INEG=0
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV,JOVER,JUNDER,JNEG) &
!$OMP& PRIVATE(ZMAXVFLD,ZMINVFLD) &
!$OMP& REDUCTION(+:IOVER,IUNDER,INEG)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      JOVER=0
      JUNDER=0
      JNEG=0
      CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
             & KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
             & ZMINVFLD,PMAXVFLD=ZMAXVFLD)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          IF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)>ZFAC(JFIX)*ZMAXVFLD(JROF,JLEV)) THEN
            JOVER=JOVER+1
          ENDIF
          IF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)<ZFAC(JFIX)*ZMINVFLD(JROF,JLEV)) THEN
            JUNDER=JUNDER+1
          ENDIF
          IF (PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)<0.0_JPRB.AND. & 
    &                                       (ZGMVT1(JROF,JLEV,JFIX,IBL)>=0.0_JPRB)) THEN
            JNEG=JNEG+1
          ENDIF
        ENDDO
      ENDDO
      IOVER=IOVER+JOVER
      IUNDER=IUNDER+JUNDER
      INEG=INEG+JNEG
    ENDDO
!$OMP END PARALLEL DO
    IF (NPROC>1) THEN
      CALL MPL_ALLREDUCE(IOVER,'SUM',CDSTRING='QMFIXER:')
      CALL MPL_ALLREDUCE(IUNDER,'SUM',CDSTRING='QMFIXER:')
      CALL MPL_ALLREDUCE(INEG,'SUM',CDSTRING='QMFIXER:')
    ENDIF
    IF (MYPROC==1) THEN
      IF (IOVER>0) THEN
        WRITE(NULOUT,'(A23,A13,A29,I8,1X,A7)') 'SUB QMFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
            & ' FIXER OVERSHOOTS MAXIMUM AT',IOVER,'PTS!'
!'
        WRITE(NULOUT,'(A15,F8.2,A11)') 'OVERSHOOTS AT',100.*IOVER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
      IF (IUNDER>0) THEN
        WRITE(NULOUT,'(A23,A13,A30,I8,1X,A7)') 'SUB QMFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
            & ' FIXER UNDERSHOOTS MINIMUM AT',IUNDER,'PTS!'
!'
        WRITE(NULOUT,'(A16,F8.2,A11)') 'UNDERSHOOTS AT',100.*IUNDER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
      IF (INEG>0) THEN
        WRITE(NULOUT,'(A23,A13,A22,I8,1X,A7)') 'SUB QMFIXER() - FLD:',TRIM(YCOMP(JGFL)%CNAME),&
                   & ' FIXER CREATED -VE AT', INEG,'PTS!'
!'
        WRITE(NULOUT,'(A15,F8.2,A11)') '-VE VALUES AT',100.*INEG/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
      ENDIF
    ENDIF
  ENDDO ! loop JGFL
ENDIF

IF (LEXTRADF.OR.NMFDIAGLEV>1) DEALLOCATE(ZGMVT1)

!* END PART FOR  QM FIXER

!------------------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('QMFIXER',1,ZHOOK_HANDLE)
END SUBROUTINE QMFIXER
