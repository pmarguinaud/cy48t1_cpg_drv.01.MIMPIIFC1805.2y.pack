SUBROUTINE QMFIXER2(YDGEOMETRY,YDGMV,YDDPHY,YGFL,YDML_DYN,KL0,PLSCAW,PB1,YDSL,PGMVS,PGMVT1S,PGFL,PGFLT1,PEXTRADYN)
!
!     Purpose.   Correct mass field to satisfy global conservation.
!                Preserves quasi-monotonicity.
!     --------   
!
!*   Interface.
!     ----------
!
!        *CALL* *QMFIXER2(...)*
!
!     Explicit arguments :
!     --------------------
!
!
!!     INPUT:
!     -------------
!        KL0         : indices of the four western points of the 16 point
!                      interpolation grid
!        PLSCAW      : linear weights (distances) for interpolations.
!        PB1         : field to be interpolated
!        YDSL         : SL_STRUCT definition
!        PGMVS       : surface GMV variables at t and t-dt.
!        PGMVT1S     : surface GMV variables at t +dt 
!        PGFL        : GFL variables buffer t0 t1
!
!     OUTPUT:
!     -------
!        PGFLT1       : GFL variables buffer t + dt
!        PEXTRADYN    : mass fixer diagnostics (correction field)
!
!        Implicit arguments :  None.
!        --------------------
!
!     Method.
!     -------
!     - Code based on Priestley algorithm MWR Feb 1993
!
!     Externals.   See includes below.
!     ----------
!
!     Reference.
!     ----------
!     Diamantakis M., Flemming J., 2014: Global mass fixer algorithms for conservative tracer transport 
!                                        in the ECMWF model
!
!     Author.
!     -------
!        Michail Diamantakis   *ECMWF*

! Modifications
! -------------
!  K. Yessad (Apr 2013): replace GPPREH by GPHPRE.
!  M. Diamantakis (Feb 2014): Put improved LTRCMFIX_PS=T code in separate subroutine
!  T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
! ------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MODEL_DYNAMICS_MOD , ONLY : MODEL_DYNAMICS_TYPE
USE YOM_YGFL           , ONLY : TYPE_GFLD
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT, NULERR
USE YOMCT3             , ONLY : NSTEP
USE YOMMP0             , ONLY : MYPROC, NPROC
USE EINT_MOD           , ONLY : SL_STRUCT
USE YOMDYNA            , ONLY : YRDYNA 
USE YOMDPHY            , ONLY : TDPHY
USE MPL_MODULE         , ONLY : MPL_ALLREDUCE

! ------------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TDPHY)       ,INTENT(INOUT) :: YDDPHY
TYPE(MODEL_DYNAMICS_TYPE),INTENT(INOUT):: YDML_DYN
TYPE(TYPE_GFLD)          ,INTENT(INOUT):: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KL0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,0:3,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(SL_STRUCT)   ,INTENT(IN)    :: YDSL
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCAW(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDML_DYN%YYTLSCAW%NDIM, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB1(YDSL%NASLB1,YDML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NDIM1, &
 & YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEXTRADYN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDDPHY%NVEXTRDYN,&
 & YDGEOMETRY%YRDIM%NGPBLKS)
! ------------------------------------------------------------------------------
LOGICAL :: LLCONVRG(YGFL%NFLDSFIX), LLMCHANGED(YGFL%NFLDSFIX), LLMINMAX

INTEGER(KIND=JPIM) :: IST, IEND, JLEV, JGFL
INTEGER(KIND=JPIM) :: JOVER, JUNDER, JNEG, IOVER, IUNDER, INEG
INTEGER(KIND=JPIM) :: IBL,JKGLO,JROF, JFIX, ITER, ITOTGTAVG
INTEGER(KIND=JPIM) :: IFLAG(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
INTEGER(KIND=JPIM) :: ITERMAX=10

REAL(KIND=JPRB)    :: ZPRE0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPRE1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPSURF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS),&
 & ZPSURF1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIM%NGPBLKS)

REAL(KIND=JPRB) :: ZFIELD3DT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD3DT0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD3DB(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD3DL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD2DT1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD2DT0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFIELD2DB(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZFIELD2DL(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    :: ZMINFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZMAXFLD(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZPRES0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZPRES1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZLIN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)

REAL(KIND=JPRB)    :: ZMAXW(YGFL%NFLDSFIX)
REAL(KIND=JPRB)    :: ZRMSDIF(YGFL%NFLDSFIX), ZRMSFLD(YGFL%NFLDSFIX)
REAL(KIND=JPRB)    :: ZCSTAR(YGFL%NFLDSFIX), ZINTBETA(YGFL%NFLDSFIX)
REAL(KIND=JPRB)    :: ZALPHA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZAMAX(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZBETA(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZBETA0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZBETA1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZFIELD1D(YDGEOMETRY%YRDIM%NPROMA,2*YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZFIELD1DB(YDGEOMETRY%YRDIM%NPROMA,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZFIELD1DL(YDGEOMETRY%YRDIM%NPROMA,YGFL%NFLDSFIX,YDGEOMETRY%YRDIM%NGPBLKS) 
REAL(KIND=JPRB)    :: ZBUF0(YDGEOMETRY%YRDIM%NPROMA,1,YDGEOMETRY%YRDIM%NGPBLKS),&
 & ZBUF1(YDGEOMETRY%YRDIM%NPROMA,1,YDGEOMETRY%YRDIM%NGPBLKS)
REAL(KIND=JPRB)    :: ZTOTMASS(3,2*YGFL%NFLDSFIX),ZSUMB(3,YGFL%NFLDSFIX),ZSUML(3,YGFL%NFLDSFIX), &
 & ZSURPL(YGFL%NFLDSFIX) 
REAL(KIND=JPRB)    :: ZSUMB0(3,1),ZSUMB1(3,1), ZDM(YGFL%NFLDSFIX)
REAL(KIND=JPRB)    :: ZAVG, ZDIFF,  ZDMRATIO, ZMAXDIF, ZQM, ZC, ZL, ZMIN, ZMAX
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

! ------------------------------------------------------------------------------

#include "pfixer.intfb.h"
#include "gpnorm1.intfb.h"
#include "gppwc.intfb.h"
#include "gppwcvfe.intfb.h"
#include "gphpre.intfb.h"
#include "laitli.intfb.h"
#include "laminmaxint.intfb.h"

! ------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('QMFIXER2',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,   YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB, &
& YDPTRSLB1=>YDML_DYN%YRPTRSLB1 )

ASSOCIATE(LEXTRADF=>YGFL%LEXTRADF, LTRCMFIX_PS=>YGFL%LTRCMFIX_PS,   NFLDSFIX=>YGFL%NFLDSFIX,   NMFDIAGLEV=>YGFL%NMFDIAGLEV,    &
& NMFIXFLDS=>YGFL%NMFIXFLDS,   NOPTMFPR=>YGFL%NOPTMFPR, NOPTVFE=>YGFL%NOPTVFE, YCOMP=>YGFL%YCOMP,   ZMFIXEPS=>YGFL%ZMFIXEPS,   &
& NPROMA=>YDDIM%NPROMA,   NFLEN=>YDDIMV%NFLEN, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA,   NVEXTRDYN=>YDDPHY%NVEXTRDYN,      &
& NGPTOT=>YDGEM%NGPTOT, NGPTOTG=>YDGEM%NGPTOTG,   YT0=>YDGMV%YT0, YT1=>YDGMV%YT1,   MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9             &
& )
! ------------------------------------------------------------------------------

! Initializations
IST=1

!------------------------------------------------------------------
! Compute 3d pressure field from 2d surf pressure
!------------------------------------------------------------------

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPSURF0(JROF,IBL) = EXP(PGMVS(JROF,YT0%MSP,IBL))
    ZPSURF1(JROF,IBL) = EXP(PGMVT1S(JROF,YT1%MSP,IBL))
  ENDDO
ENDDO
!$OMP END PARALLEL DO

IF (LTRCMFIX_PS) CALL PFIXER(YDGEOMETRY,YDGMV,YGFL,YDML_DYN%YRDYN,ZPSURF0,ZPSURF1,PGMVT1S)

!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZPRE0,ZPRE1)
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  DO JROF=IST,IEND
    ZPRE0(JROF,NFLEVG) = ZPSURF0(JROF,IBL)
    ZPRE1(JROF,NFLEVG) = ZPSURF1(JROF,IBL)
  ENDDO
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE0) 
  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE1)
  DO JLEV=0,NFLEVG
    DO JROF=IST,IEND
      ZPRES0(JROF,JLEV,IBL) = ZPRE0(JROF,JLEV)
      ZPRES1(JROF,JLEV,IBL) = ZPRE1(JROF,JLEV)
    ENDDO
  ENDDO
ENDDO
!$OMP END PARALLEL DO
   
!------------------------------------------------------------------
! Compute total column content for each grid-point
!------------------------------------------------------------------

DO JFIX=1,NFLDSFIX
  JGFL=NMFIXFLDS(JFIX)
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZPRE0,ZPRE1,ZFIELD2DT0,ZFIELD2DT1,ZFIELD3DT0,ZFIELD3DT1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    ZPRE0(IST:IEND,0)=ZPRES0(IST:IEND,0,IBL)
    ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
    DO JLEV=1,NFLEVG
      DO JROF=IST,IEND
        ZFIELD3DT0(JROF,JLEV)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP,IBL)
        ZFIELD3DT1(JROF,JLEV)=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
        ZPRE0(JROF,JLEV)=ZPRES0(JROF,JLEV,IBL) 
        ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
      ENDDO
    ENDDO
    IF (NOPTVFE==1) THEN
      CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
      CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
    ELSE
      CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT0,ZFIELD3DT0,ZPRE0)
      CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DT1,ZFIELD3DT1,ZPRE1)
    ENDIF
    ZFIELD1D(IST:IEND,2*JFIX-1,IBL)=ZFIELD2DT0(IST:IEND,NFLEVG)
    ZFIELD1D(IST:IEND,2*JFIX,IBL)=ZFIELD2DT1(IST:IEND,NFLEVG)
  ENDDO
!$OMP END PARALLEL DO
ENDDO


CALL GPNORM1(YDGEOMETRY,ZFIELD1D,2*NFLDSFIX,.FALSE.,PNORMS=ZTOTMASS)

LLMCHANGED=.TRUE.
LLMINMAX=(NOPTMFPR==2).OR.(NMFDIAGLEV>1)

! compute a weighting function for mass fixer weight which depends local smoothness 
DO JFIX=1,NFLDSFIX
  ZDM(JFIX)=ZTOTMASS(1,2*JFIX)-ZTOTMASS(1,2*JFIX-1)
  JGFL=NMFIXFLDS(JFIX)
  IF (ABS(ZDM(JFIX))<10.0_JPRB*ZMFIXEPS) LLMCHANGED(JFIX)=.FALSE.
  IF ( LLMCHANGED(JFIX) ) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZC,ZL,ZFIELD3DB,ZFIELD3DL,ZPRE1,ZMAX,ZMIN,ZFIELD2DB,ZFIELD2DL)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      IF (YRDYNA%LCOMAD_GFL.AND.YCOMP(JGFL)%LCOMAD) THEN
        CALL LAITLI(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,&
          & NFLEN,PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLAMAD,IBL),&
          & PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLOMAD+1,IBL),&
          & KL0(1,1,1,IBL),PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDVERMAD,IBL),&
          & PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),ZLIN(1,1,JFIX,IBL))
      ELSE
        CALL LAITLI(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,&
                  & NFLEN,PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLAT,IBL),&
                  & PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDLO+1,IBL),&
                  & KL0(1,1,1,IBL),PLSCAW(1,1,YDML_DYN%YYTLSCAW%M_WDVER,IBL),&
                  & PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),ZLIN(1,1,JFIX,IBL))
      ENDIF
      IF ( LLMINMAX ) THEN
        CALL LAMINMAXINT(YDSL%NASLB1,NPROMA,IST,IEND,NFLEVG,NFLSA,NFLEN,&
               & KL0(1,1,0,IBL),PB1(1,MSLB1GFL9+(YCOMP(JGFL)%MP_SL1-1)*(NFLEN-NFLSA+1)),&
               & ZMINFLD(1,1,JFIX,IBL),PMAXVFLD=ZMAXFLD(1,1,JFIX,IBL))
      ENDIF
      ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
      IF (NOPTMFPR==1) THEN
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZC=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZL=ZLIN(JROF,JLEV,JFIX,IBL)
            ZAMAX(JROF,JLEV,JFIX,IBL)=1.0_JPRB
            ZBETA(JROF,JLEV,JFIX,IBL)=ZC-ZL
            ZFIELD3DB(JROF,JLEV)=ZAMAX(JROF,JLEV,JFIX,IBL)*ZBETA(JROF,JLEV,JFIX,IBL)
            ZFIELD3DL(JROF,JLEV)=ZL
            ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
          ENDDO
        ENDDO
      ELSEIF (NOPTMFPR==2) THEN
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZC=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            ZL=ZLIN(JROF,JLEV,JFIX,IBL)
            ZMAX=ZMAXFLD(JROF,JLEV,JFIX,IBL)
            ZMIN=ZMINFLD(JROF,JLEV,JFIX,IBL)
            ZBETA(JROF,JLEV,JFIX,IBL)=ZC-ZL
            IF (ABS(ZBETA(JROF,JLEV,JFIX,IBL))>ZMFIXEPS) THEN
              IF (ZC>ZMAX) THEN
                ZAMAX(JROF,JLEV,JFIX,IBL)=(ZMAX-ZL)/(ZC-ZL)
              ELSEIF (ZC<ZMIN) THEN
                ZAMAX(JROF,JLEV,JFIX,IBL)=(ZMIN-ZL)/(ZC-ZL)
              ELSE
                ZAMAX(JROF,JLEV,JFIX,IBL)=1.0_JPRB
              ENDIF
            ELSE
              ZAMAX(JROF,JLEV,JFIX,IBL)=1.0_JPRB
            ENDIF
            ZFIELD3DB(JROF,JLEV)=ZAMAX(JROF,JLEV,JFIX,IBL)*ZBETA(JROF,JLEV,JFIX,IBL)
            ZFIELD3DL(JROF,JLEV)=ZL
            ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
          ENDDO
        ENDDO
      ENDIF
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DB,ZFIELD3DB,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DL,ZFIELD3DL,ZPRE1)
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DB,ZFIELD3DB,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DL,ZFIELD3DL,ZPRE1)
      ENDIF
      ZFIELD1DB(IST:IEND,JFIX,IBL)=ZFIELD2DB(IST:IEND,NFLEVG)
      ZFIELD1DL(IST:IEND,JFIX,IBL)=ZFIELD2DL(IST:IEND,NFLEVG)
    ENDDO
!$OMP END PARALLEL DO
  ELSE
    ZFIELD1DB(:,JFIX,:)=0.0_JPRB
    ZFIELD1DL(:,JFIX,:)=0.0_JPRB
  ENDIF
ENDDO

CALL GPNORM1(YDGEOMETRY,ZFIELD1DB,NFLDSFIX,.FALSE.,PNORMS=ZSUMB)
CALL GPNORM1(YDGEOMETRY,ZFIELD1DL,NFLDSFIX,.FALSE.,PNORMS=ZSUML)

LLCONVRG=.FALSE.

DO JFIX=1,NFLDSFIX
  ZINTBETA(JFIX)=ZSUMB(1,JFIX)
  ZCSTAR(JFIX)=ZTOTMASS(1,2*JFIX-1)-ZSUML(1,JFIX)
  IF (.NOT.LLMCHANGED(JFIX).OR.(ABS(ZCSTAR(JFIX)-ZINTBETA(JFIX))<ZMFIXEPS)) THEN
    LLCONVRG(JFIX)=.TRUE.
  ELSEIF (ZINTBETA(JFIX)<ZCSTAR(JFIX)) THEN
    ZCSTAR(JFIX)=-ZCSTAR(JFIX)
    ZBETA(:,:,JFIX,:)=-ZBETA(:,:,JFIX,:)
  ENDIF
  IF (.NOT.LLCONVRG(JFIX)) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          IF (ZBETA(JROF,JLEV,JFIX,IBL)<=0.0_JPRB) THEN
            IFLAG(JROF,JLEV,JFIX,IBL)=1
            ZALPHA(JROF,JLEV,JFIX,IBL)=ZAMAX(JROF,JLEV,JFIX,IBL)
          ELSE
            IFLAG(JROF,JLEV,JFIX,IBL)=0
            ZALPHA(JROF,JLEV,JFIX,IBL)=0.0_JPRB
          ENDIF
        ENDDO
      ENDDO
    ENDDO
!$OMP END PARALLEL DO
  ENDIF
ENDDO
  
DO JFIX=1,NFLDSFIX
  JGFL=NMFIXFLDS(JFIX)
  ITER=0
  DO WHILE (.NOT. LLCONVRG(JFIX).AND.ITER<=ITERMAX)
    ITER=ITER+1
! Compute surplus
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZFIELD3DL,ZFIELD3DB,ZPRE1,ZFIELD2DL,ZFIELD2DB)
    DO JKGLO=1,NGPTOT,NPROMA
      IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      ZPRE1(IST:IEND,0)=ZPRES1(IST:IEND,0,IBL)
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          IF (IFLAG(JROF,JLEV,JFIX,IBL)==1) THEN
            ZBETA0(JROF,JLEV,JFIX,IBL)=0.0_JPRB
            ZBETA1(JROF,JLEV,JFIX,IBL)=ZBETA(JROF,JLEV,JFIX,IBL)
          ELSEIF (IFLAG(JROF,JLEV,JFIX,IBL)==0) THEN
            ZBETA0(JROF,JLEV,JFIX,IBL)=ZBETA(JROF,JLEV,JFIX,IBL)
            ZBETA1(JROF,JLEV,JFIX,IBL)=0.0_JPRB
          ENDIF
          ZFIELD3DL(JROF,JLEV)=ZBETA0(JROF,JLEV,JFIX,IBL)
          ZFIELD3DB(JROF,JLEV)=ZALPHA(JROF,JLEV,JFIX,IBL)*ZBETA1(JROF,JLEV,JFIX,IBL)
          ZPRE1(JROF,JLEV)=ZPRES1(JROF,JLEV,IBL)
        ENDDO
      ENDDO
      IF (NOPTVFE==1) THEN
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DB,ZFIELD3DB,ZPRE1)
        CALL GPPWCVFE(YDGEOMETRY,NPROMA,IST,IEND,NFLEVG,ZFIELD2DL,ZFIELD3DL,ZPRE1)
      ELSE
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DB,ZFIELD3DB,ZPRE1)
        CALL GPPWC(NPROMA,IST,IEND,NFLEVG,ZFIELD2DL,ZFIELD3DL,ZPRE1)
      ENDIF
      ZBUF0(IST:IEND,1,IBL)=ZFIELD2DL(IST:IEND,NFLEVG)
      ZBUF1(IST:IEND,1,IBL)=ZFIELD2DB(IST:IEND,NFLEVG)
    ENDDO
!$OMP END PARALLEL DO
    CALL GPNORM1(YDGEOMETRY,ZBUF0,1,.FALSE.,PNORMS=ZSUMB0)
    CALL GPNORM1(YDGEOMETRY,ZBUF1,1,.FALSE.,PNORMS=ZSUMB1)
    
    ZSURPL(JFIX)=ZCSTAR(JFIX)-ZSUMB1(1,1)

! Compute alpha average but check for singularity first
    IF (ZSURPL(JFIX)<ZMFIXEPS.OR.ABS(ZSUMB0(1,1))<ZMFIXEPS) THEN

! Algorithm cannot converge - terminate iterations but set alpha coefficients
! to 1, i.e. use field as it is
      LLCONVRG(JFIX)=.TRUE.
      ZALPHA(:,:,JFIX,:)=1.0_JPRB
      IFLAG(:,:,JFIX,:)=1
      WRITE(NULERR,*) 'PRIESTLEY FIXER DIVERGES ON GFL VAR:',TRIM(YCOMP(JGFL)%CNAME)
      WRITE(NULERR,*) 'AT STEP',NSTEP,'AFTER',ITER,'ITERATIONS'

    ELSE 

      ZAVG=ZSURPL(JFIX)/ZSUMB0(1,1)
! Number of points where alpha_max<zavg
      ITOTGTAVG=0
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            IF (IFLAG(JROF,JLEV,JFIX,IBL)==0 .AND.&
                      & ZAVG>ZAMAX(JROF,JLEV,JFIX,IBL)) THEN
              IFLAG(JROF,JLEV,JFIX,IBL)=1
              ZALPHA(JROF,JLEV,JFIX,IBL)=ZAMAX(JROF,JLEV,JFIX,IBL)
              ITOTGTAVG=ITOTGTAVG+1
            ENDIF
          ENDDO
        ENDDO
      ENDDO
      
      CALL MPL_ALLREDUCE(ITOTGTAVG,'SUM',LDREPROD=.FALSE.,CDSTRING='QMFIXER2:')

! If alpha_max > zavg for all points s.t. iflag=0 algorithm has converged      
      IF (ITOTGTAVG==0) LLCONVRG(JFIX)=.TRUE.    

    ENDIF

  ENDDO ! END WHILE

  IF (LLCONVRG(JFIX)) THEN

! Initialize diagnostic with original (uncorrected) field
    IF (LEXTRADF.AND.(JFIX<=NVEXTRDYN)) THEN
      PEXTRADYN(:,:,JFIX,:)=PGFLT1(:,:,YCOMP(JGFL)%MP1,:) 
    ENDIF

    IF (NMFDIAGLEV>1) THEN
      ZMAXW(JFIX)=0.0_JPRB
      ZRMSDIF(JFIX)=0.0_JPRB
      ZRMSFLD(JFIX)=0.0_JPRB
      IF (LLMCHANGED(JFIX)) THEN
        DO JKGLO=1,NGPTOT,NPROMA
          IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
          IBL=(JKGLO-1)/NPROMA+1
          DO JLEV=1,NFLEVG
            DO JROF=IST,IEND
              IF (IFLAG(JROF,JLEV,JFIX,IBL)==0) ZALPHA(JROF,JLEV,JFIX,IBL)=ZAVG
              ZC=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
              ZL=ZLIN(JROF,JLEV,JFIX,IBL)
              ZQM=ZALPHA(JROF,JLEV,JFIX,IBL)*(ZC-ZL) + ZL
              IF (ZQM>-ZMFIXEPS) ZQM=MAX(0.0_JPRB,ZQM)
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZQM
              ZDIFF=ZQM-ZC
              ZRMSDIF(JFIX)=ZRMSDIF(JFIX)+ZDIFF*ZDIFF
              ZRMSFLD(JFIX)=ZRMSFLD(JFIX)+ZC*ZC
              ZMAXW(JFIX)=MAX(ABS(ZDIFF),ZMAXW(JFIX))
            ENDDO
          ENDDO
        ENDDO
      ENDIF
    ELSE
      IF (LLMCHANGED(JFIX)) THEN
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV) &
!$OMP& PRIVATE(ZC,ZL,ZQM)
        DO JKGLO=1,NGPTOT,NPROMA
          IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
          IBL=(JKGLO-1)/NPROMA+1
          DO JLEV=1,NFLEVG
            DO JROF=IST,IEND
              IF (IFLAG(JROF,JLEV,JFIX,IBL)==0) ZALPHA(JROF,JLEV,JFIX,IBL)=ZAVG
              ZC=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
              ZL=ZLIN(JROF,JLEV,JFIX,IBL)
              ZQM=ZALPHA(JROF,JLEV,JFIX,IBL)*(ZC-ZL) + ZL
              IF (ZQM>-ZMFIXEPS) ZQM=MAX(0.0_JPRB,ZQM)
              PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)=ZQM
            ENDDO
          ENDDO
        ENDDO
!$OMP END PARALLEL DO
      ENDIF
    ENDIF

! ADD CORRECTIONS TO DIAGNOSTIC ARRAY IF NEEDED
    IF (LEXTRADF.AND.(JFIX<=NVEXTRDYN)) THEN
      PEXTRADYN(:,:,JFIX,:)=PGFLT1(:,:,YCOMP(JGFL)%MP1,:)-PEXTRADYN(:,:,JFIX,:)
    ENDIF

    IF (MYPROC==1) THEN
      WRITE(NULOUT,*) 'PRIESTLEY FIXER CONVERGED ON GFL VAR:',TRIM(YCOMP(JGFL)%CNAME)
      WRITE(NULOUT,*) 'AT STEP',NSTEP,'AFTER',ITER,'ITERATIONS'
    ENDIF
  ELSE
    WRITE(NULERR,*) 'PRIESTLEY FIXER DOES NOT CONVERGE ON GFL VAR:',TRIM(YCOMP(JGFL)%CNAME)
    WRITE(NULERR,*) 'AT STEP',NSTEP,'AFTER',ITER,'ITERATIONS'
  ENDIF
  
ENDDO

IF (NMFDIAGLEV>1) THEN
  IF (NPROC>1) THEN
    CALL MPL_ALLREDUCE(ZRMSDIF,'SUM',LDREPROD=.FALSE.,CDSTRING='QMFIXER2:')
    CALL MPL_ALLREDUCE(ZRMSFLD,'SUM',LDREPROD=.FALSE.,CDSTRING='QMFIXER2:')
    CALL MPL_ALLREDUCE(ZMAXW,'MAX',LDREPROD=.FALSE.,CDSTRING='QMFIXER2:')
  ENDIF
  IF (MYPROC==1) THEN  
    WRITE(NULOUT,*) '----------------------------------------------------------------------------'
    WRITE(NULOUT,*) '                  MASS FIXER GLOBAL DIAGNOSTICS                             '
    WRITE(NULOUT,*) '    FIELD         DM       DM/Mtot %    max(dij)/|field| %   |dij|/|field| %'
    WRITE(NULOUT,*) '----------------------------------------------------------------------------'
  ENDIF
  DO JFIX=1,NFLDSFIX
    IF (LLMCHANGED(JFIX)) THEN
      JGFL=NMFIXFLDS(JFIX)    
      ZRMSDIF(JFIX)=SQRT(ZRMSDIF(JFIX)/(NGPTOTG*NFLEVG))
      ZRMSFLD(JFIX)=SQRT(ZRMSFLD(JFIX)/(NGPTOTG*NFLEVG))
      ZDMRATIO=100.0_JPRB*ZDM(JFIX)/MAX(ZTOTMASS(1,2*JFIX-1),ZMFIXEPS)
      ZMAXDIF=100.0_JPRB*ZMAXW(JFIX)/MAX(ZRMSFLD(JFIX),ZMFIXEPS)
      ZRMSDIF(JFIX)=100.0_JPRB*ZRMSDIF(JFIX)/MAX(ZRMSFLD(JFIX),ZMFIXEPS)
      IF (MYPROC==1) THEN
        WRITE(NULOUT,'(A13,2X,E10.4,1X,F9.3,6X,F9.3,12X,F9.3)') TRIM(YCOMP(JGFL)%CNAME),&
                   & ZDM(JFIX),ZDMRATIO,ZMAXDIF,ZRMSDIF(JFIX)
      ENDIF
    ENDIF
  ENDDO
ENDIF

!TEST FOR OVERSHOOTS/UNDERSHOOTS
IF (NMFDIAGLEV>1) THEN
  DO JFIX=1,NFLDSFIX
    IF (LLMCHANGED(JFIX)) THEN
      JGFL=NMFIXFLDS(JFIX)
      IOVER=0
      IUNDER=0
      INEG=0
!$OMP PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JKGLO,IEND,IBL,JROF,JLEV,JOVER,JUNDER,JNEG) &
!$OMP& PRIVATE(ZMAX,ZMIN,ZQM) &
!$OMP& REDUCTION(+:IOVER,IUNDER,INEG)
      DO JKGLO=1,NGPTOT,NPROMA
        IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
        IBL=(JKGLO-1)/NPROMA+1
        JOVER=0
        JUNDER=0
        JNEG=0
        DO JLEV=1,NFLEVG
          DO JROF=IST,IEND
            ZMAX=ZMAXFLD(JROF,JLEV,JFIX,IBL)
            ZMIN=ZMINFLD(JROF,JLEV,JFIX,IBL)
            ZQM=PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1,IBL)
            IF (ZQM>ZMAX+ZMFIXEPS) THEN
              JOVER=JOVER+1
            ENDIF
            IF (ZQM<ZMIN-ZMFIXEPS) THEN
              JUNDER=JUNDER+1
            ENDIF
            IF (ZQM<0.0_JPRB) THEN
              JNEG=JNEG+1
            ENDIF
          ENDDO
        ENDDO
      IOVER=IOVER+JOVER
      IUNDER=IUNDER+JUNDER
      INEG=INEG+JNEG
      ENDDO
!$OMP END PARALLEL DO
      IF (NPROC>1) THEN
        CALL MPL_ALLREDUCE(IOVER,'SUM',CDSTRING='QMFIXER2:')
        CALL MPL_ALLREDUCE(IUNDER,'SUM',CDSTRING='QMFIXER2:')
        CALL MPL_ALLREDUCE(INEG,'SUM',CDSTRING='QMFIXER2:')
      ENDIF
      IF (MYPROC==1) THEN
        IF (IOVER>0) THEN
          WRITE(NULOUT,'(A23,I2,A29,I7,1X,A7)') 'SUB QMFIXER2() - FLD:',JGFL,&
                 & ' FIXER OVERSHOOTS MAXIMUM AT',IOVER,'POINTS!'
!'
          WRITE(NULOUT,'(A15,F8.2,A11)') 'OVERSHOOTS AT',100.0_JPRB*IOVER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
        ENDIF
        IF (IUNDER>0) THEN
          WRITE(NULOUT,'(A23,I3,A30,I7,1X,A7)') 'SUB QMFIXER2() - FLD:',JGFL,&
                 & ' FIXER UNDERSHOOTS MINIMUM AT',IUNDER,'POINTS!'
!'
          WRITE(NULOUT,'(A16,F8.2,A11)') 'UNDERSHOOTS AT',100.0_JPRB*IUNDER/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
        ENDIF
        IF (INEG>0) THEN
          WRITE(NULOUT,'(A23,I3,A22,I7,1X,A7)') 'SUB QMFIXER2() - FLD:',JGFL,&
                        & ' FIXER CREATED -VE AT', INEG,'POINTS!'
!'
          WRITE(NULOUT,'(A15,F8.2,A11)') '-VE VALUES AT',100.0_JPRB*INEG/DBLE(NFLEVG*NGPTOTG),'% OF POINTS'
        ENDIF
      ENDIF
    ENDIF
  ENDDO ! loop JGFL
ENDIF

! ------------------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('QMFIXER2',1,ZHOOK_HANDLE)
END SUBROUTINE QMFIXER2
