SUBROUTINE SPCM(YDGEOMETRY,YDMODEL,CDCONF,YDSP,YDGMV)

!------------------------------------------------------------------------------
! SPCM* - Interface to spectral calculations

! Purpose
! -------

! Interface
! ---------
!   CDCONF - configuration of work (see doc.)
!            CDCONF='0': nothing done
!            CDCONF='A': SI scheme and horizontal diffusion done.
!            CDCONF='I': SI scheme done; no horizontal diffusion.

! Externals
! ---------

! Method
! ------

! Reference
! ---------
!   ECMWF Research Department documentation of the IFS
!   Documentation about FULL-POS.

! Author
! ------
!   24-Nov-1987 Mats Hamrud and Philippe Courtier (ECMWF)

! Modifications
! -------------
!   T.Wilhelmsson 09-09-25: Remove LFULLM requirement for LIMPF
!   K. Yessad (Feb 2012): simplifications for tests and CDCONF.
!   01-Nov-2011 N. Wedi :     introduce LGRADSP
!   R. El Khatib 18-Jul-2012 Fullpos move away
!   P. Marguinaud (Sep 2012): make ZSPAUXG argument of SPCSI optional 
!                             to avoid passing a NULL pointer
!   18-Oct-2012 G. Mozdzynski : improve NH performance at scale
!   J. Hague      (Aug 2013): add YDSL for OOPS
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   O. Marsden       May-2016 Removed redundant geometry arguments
!   K. Yessad (Dec 2016): Prune obsolete options.
!   K. Yessad (June 2017): Introduce NHQE model.
!   K. Yessad (June 2018): Alternate NHEE SI scheme elimination.
!------------------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT
USE YOMCT0             , ONLY : LR3D, LNHDYN, LNHEE, LNHQE
USE YOMDYNA            , ONLY : LGRADSP, LNHQE_SIHYD, LSI_NHEE
USE YOMMP0             , ONLY : MYSETN, MYSETW
USE YOMSP              , ONLY : SPVOR_FLT, SPDIV_FLT
USE SPECTRAL_FIELDS_MOD, ONLY : SPECTRAL_FIELD, ASSIGNMENT(=)
USE YOMGMV             , ONLY : TGMV

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)      ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL)         ,INTENT(INOUT) :: YDMODEL
CHARACTER(LEN=1)    ,INTENT(IN)    :: CDCONF 
TYPE(SPECTRAL_FIELD),INTENT(INOUT) :: YDSP
TYPE(TGMV),INTENT(INOUT)           :: YDGMV

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IEND, IM, IMLOC, ISTA, JMLOC, ISPEC2V
INTEGER(KIND=JPIM) :: IPART_2A, IPART_2B2C, IPART_2D, JITER
INTEGER(KIND=JPIM) :: IDIMV, IDIMH

LOGICAL :: LLONEM
LOGICAL :: LLMASCOR, LLDO, LLDOSI, LLDOHD
LOGICAL :: LL_CALL_SPCIMPF
LOGICAL :: LLDO_PARTSI(3,0:YDMODEL%YRML_DYN%YRDYN%NITERHELM+1)

REAL(KIND=JPRB),ALLOCATABLE ::  ZSPVORG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPDIVG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTG  (:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPSPDG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPSVDG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPSPG (:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPAUX (:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPAUXG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPBUFG(:,:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTNDSI_VORG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTNDSI_DIVG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTNDSI_TG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTNDSI_SPDG(:,:)
REAL(KIND=JPRB),ALLOCATABLE ::  ZSPTNDSI_SVDG(:,:)

REAL(KIND=JPRB),ALLOCATABLE :: ZZPS(:)

REAL(KIND=JPRB) ::  ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "spcimpfinit.intfb.h"
#include "spcsi.intfb.h"
#include "spcimpfpost.intfb.h"
#include "spchor.intfb.h"
#include "spcmascor.intfb.h"
#include "spnhsi.intfb.h"
#include "spnheesi.intfb.h"
#include "spnhqesi.intfb.h"
#include "spnhqesi_prodskap.intfb.h"
#include "trmtos.intfb.h"
#include "trstom.intfb.h"
#include "spfilt.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SPCM',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 & YDMP=>YDGEOMETRY%YRMP, YDLAP=>YDGEOMETRY%YRLAP, YDDYN=>YDMODEL%YRML_DYN%YRDYN, &
 & YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH, YDSPDDH=>YDMODEL%YRML_DIAG%YRSPDDH)

ASSOCIATE(NSMAX=>YDDIM%NSMAX, NSPEC2=>YDDIM%NSPEC2, NUMP=>YDDIM%NUMP, &
 & NFLEVG=>YDDIMV%NFLEVG, NFLSUR=>YDDIMV%NFLSUR, &
 & LGPMASCOR=>YDDYN%LGPMASCOR, LIMPF=>YDDYN%LIMPF, LMASCOR=>YDDYN%LMASCOR, &
 & LSIDG=>YDDYN%LSIDG, NCURRENT_ITER=>YDDYN%NCURRENT_ITER, NSITER=>YDDYN%NSITER, &
 & NITERHELM=>YDDYN%NITERHELM, &
 & LRSIDDH=>YDLDDH%LRSIDDH, &
 & NPROCM=>YDMP%NPROCM, NPSP=>YDMP%NPSP, NPTRMF=>YDMP%NPTRMF, &
 & NSPEC2V=>YDMP%NSPEC2V, NSPEC2VF=>YDMP%NSPEC2VF, NSPSTAF=>YDMP%NSPSTAF, &
 & SPTNDHD_DIV=>YDSPDDH%SPTNDHD_DIV, SPTNDHD_GFL=>YDSPDDH%SPTNDHD_GFL, &
 & SPTNDHD_SNHX=>YDSPDDH%SPTNDHD_SNHX, SPTNDHD_SPD=>YDSPDDH%SPTNDHD_SPD, &
 & SPTNDHD_SVD=>YDSPDDH%SPTNDHD_SVD, SPTNDHD_T=>YDSPDDH%SPTNDHD_T, &
 & SPTNDHD_VOR=>YDSPDDH%SPTNDHD_VOR, SPTNDSI_DIV=>YDSPDDH%SPTNDSI_DIV, &
 & SPTNDSI_SPD=>YDSPDDH%SPTNDSI_SPD, SPTNDSI_SVD=>YDSPDDH%SPTNDSI_SVD, &
 & SPTNDSI_T=>YDSPDDH%SPTNDSI_T, SPTNDSI_VOR=>YDSPDDH%SPTNDSI_VOR)
!     ------------------------------------------------------------------

ALLOCATE(ZZPS(NSPEC2))
IF(YDMP%NPSP==1)ZZPS=YDSP%SP

LL_CALL_SPCIMPF=LIMPF.AND.(.NOT.(LNHEE.AND.(.NOT.LSI_NHEE)))

IF (LSIDG) THEN
  ! (lsidg,limpf)=(T,F)
  LLONEM=.TRUE.
ELSEIF (LIMPF) THEN
  ! (lsidg,limpf)=(F,T)
  LLONEM=.NOT.LL_CALL_SPCIMPF
ELSE
  ! (lsidg,limpf)=(F,F)
  LLONEM=.FALSE.
ENDIF

ISPEC2V=MAX(NSPEC2V,NSPEC2VF)
IDIMV=UBOUND(YDSP%DIV,1)
IDIMH=UBOUND(YDSP%DIV,2)

IPART_2A=1
IPART_2B2C=2
IPART_2D=3
IF (LNHDYN.AND.LNHQE.AND.(.NOT.LNHQE_SIHYD)) THEN
  IF (NITERHELM==0) THEN
    ! Predictor:
    LLDO_PARTSI(IPART_2A,0)=.TRUE.
    LLDO_PARTSI(IPART_2B2C,0)=.TRUE.
    LLDO_PARTSI(IPART_2D,0)=.FALSE.
    ! Corrector:
    LLDO_PARTSI(IPART_2A,1)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,1)=.FALSE.
    LLDO_PARTSI(IPART_2D,1)=.TRUE.
  ELSEIF (NITERHELM>0) THEN
    ! Predictor:
    LLDO_PARTSI(IPART_2A,0)=.TRUE.
    LLDO_PARTSI(IPART_2B2C,0)=.TRUE.
    LLDO_PARTSI(IPART_2D,0)=.FALSE.
    ! Intermediate iterations:
    LLDO_PARTSI(IPART_2A,1:NITERHELM)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,1:NITERHELM)=.TRUE.
    LLDO_PARTSI(IPART_2D,1:NITERHELM)=.FALSE.
    ! Corrector:
    LLDO_PARTSI(IPART_2A,NITERHELM+1)=.FALSE.
    LLDO_PARTSI(IPART_2B2C,NITERHELM+1)=.FALSE.
    LLDO_PARTSI(IPART_2D,NITERHELM+1)=.TRUE.
  ENDIF
ELSE
  LLDO_PARTSI(:,:)=.TRUE.
ENDIF

IF (CDCONF == 'A'.OR.CDCONF == 'I') THEN

 IF (LR3D) THEN

  ! * SPECTRAL MASS CORRECTOR.
  !   ncurrent_iter=nsiter: always done if lmascor=T.
  !   ncurrent_iter<nsiter: never done.

  LLDO = (MYSETW == NPROCM(0).AND.(NPSP==1))
  LLMASCOR=LMASCOR.AND.(.NOT.LGPMASCOR).AND.(CDCONF == 'A'.OR.CDCONF == 'I')&
  &               .AND.LLDO.AND.(NCURRENT_ITER==NSITER)
  IF( LLMASCOR ) CALL SPCMASCOR(YDLAP,YDDYN,ZZPS)

  ! * SEMI-IMPLICIT SCMEME AND TRANSPOSITIONS.
  LLDOSI=(CDCONF == 'A'.OR.CDCONF == 'I')

  IF (LLDOSI) THEN

    ALLOCATE(ZSPVORG(NFLEVG,ISPEC2V))
    ALLOCATE(ZSPDIVG(NFLEVG,ISPEC2V))
    ALLOCATE(ZSPTG  (NFLEVG,ISPEC2V))
    ALLOCATE(ZSPSPDG(NFLEVG,ISPEC2V))
    ALLOCATE(ZSPSVDG(NFLEVG,ISPEC2V))
    ALLOCATE(ZSPSPG (ISPEC2V))
    IF (LNHQE.AND.(.NOT.LNHQE_SIHYD)) ALLOCATE(ZSPBUFG(NFLEVG,ISPEC2V,3))

    IF (LRSIDDH) THEN
      ALLOCATE(ZSPTNDSI_VORG(NFLEVG,ISPEC2V))
      ALLOCATE(ZSPTNDSI_DIVG(NFLEVG,ISPEC2V))
      ALLOCATE(ZSPTNDSI_TG(NFLEVG,ISPEC2V))
      IF (LNHDYN) ALLOCATE(ZSPTNDSI_SPDG(NFLEVG,ISPEC2V))
      IF (LNHDYN) ALLOCATE(ZSPTNDSI_SVDG(NFLEVG,ISPEC2V))
    ELSE
      ALLOCATE(ZSPTNDSI_VORG(1,1))
      ALLOCATE(ZSPTNDSI_DIVG(1,1))
      ALLOCATE(ZSPTNDSI_TG(1,1))
      IF (LNHDYN) ALLOCATE(ZSPTNDSI_SPDG(1,1))
      IF (LNHDYN) ALLOCATE(ZSPTNDSI_SVDG(1,1))
    ENDIF
      
    IF (LL_CALL_SPCIMPF) THEN
      ALLOCATE(ZSPAUX(NFLSUR,NSPEC2))
      ALLOCATE(ZSPAUXG(NFLEVG,ISPEC2V))
    ENDIF

    CALL GSTATS(9,0)
    IF (LL_CALL_SPCIMPF) THEN
      CALL GSTATS(1022,0)
!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOC,IM,ISTA,IEND)
      DO JMLOC=1,NUMP
        IM=YDLAP%MYMS(JMLOC)
        ISTA=YDLAP%NASM0(IM)
        IEND=ISTA+2*(NSMAX+1-IM)-1
        CALL SPCIMPFINIT(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,ISTA,IEND,YDSP%VOR,ZSPAUX)
      ENDDO
!$OMP END PARALLEL DO
      CALL GSTATS(1022,1)
    ENDIF

    !     - Transpose spectral data to vertical columns

    CALL GSTATS(9,2)
    CALL GSTATS(54,0)
    ! Could be optimized by transposing spvor only if (limpf and lnhdyn)
    IF (LL_CALL_SPCIMPF) THEN
      CALL TRMTOS(YDGEOMETRY,PSPVOR=YDSP%VOR,PSPDIV=YDSP%DIV,PSPT=YDSP%T,PSPSPD=YDSP%SPD,&
        & PSPSVD=YDSP%SVD,PSPSP=ZZPS,PSPAUX=ZSPAUX,&
        & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
        & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,PSPAUXG=ZSPAUXG,&
        & LDFULLM=LLONEM)  
    ELSE
      CALL TRMTOS(YDGEOMETRY,PSPVOR=YDSP%VOR,PSPDIV=YDSP%DIV,PSPT=YDSP%T,PSPSPD=YDSP%SPD,&
        & PSPSVD=YDSP%SVD,PSPSP=ZZPS,&
        & PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
        & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
        & LDFULLM=LLONEM)
    ENDIF
    CALL GSTATS(54,2)
    CALL GSTATS(9,3)

    IF (LLONEM) THEN

      !     - Each zonal wave number has to be treated separately

      CALL GSTATS(1023,0)

      IF (LNHEE.AND.(.NOT.LSI_NHEE)) THEN

        DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
          IM=YDLAP%MYMS(JMLOC)
          ISTA=NSPSTAF(IM)
          IEND=ISTA+2*(NSMAX+1-IM)-1
          CALL SPNHSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,JMLOC,ISTA,IEND,LLONEM,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
           & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
        ENDDO

      ELSEIF (LNHEE.AND.LSI_NHEE) THEN

        IF (LIMPF) THEN
          DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
            IM=YDLAP%MYMS(JMLOC)
            ISTA=NSPSTAF(IM)
            IEND=ISTA+2*(NSMAX+1-IM)-1
            CALL SPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,JMLOC,ISTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG,PSPAUXG=ZSPAUXG)
          ENDDO
        ELSE
          DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
            IM=YDLAP%MYMS(JMLOC)
            ISTA=NSPSTAF(IM)
            IEND=ISTA+2*(NSMAX+1-IM)-1
            CALL SPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,JMLOC,ISTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
          ENDDO
        ENDIF

      ELSEIF (LNHQE.AND.(.NOT.LNHQE_SIHYD)) THEN

        DO JITER=0,NITERHELM+1

          IF (JITER == 0) THEN
            ZSPBUFG(:,:,1)=ZSPDIVG(:,:)
          ENDIF
          CALL SPNHQESI_PRODSKAP(LLONEM,YDGEOMETRY,YDGMV,IDIMV,IDIMH,ISPEC2V,ZSPBUFG(1,1,1),ZSPBUFG(1,1,3))

          IF (LIMPF) THEN
            DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
              IM=YDLAP%MYMS(JMLOC)
              ISTA=NSPSTAF(IM)
              IEND=ISTA+2*(NSMAX+1-IM)-1
              CALL SPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,&
               & IM,JMLOC,ISTA,IEND,LLONEM,LLDO_PARTSI(1,JITER),&
               & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,ISTA:IEND,1:3),&
               & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG,PSPAUXG=ZSPAUXG)
            ENDDO
          ELSE
            DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
              IM=YDLAP%MYMS(JMLOC)
              ISTA=NSPSTAF(IM)
              IEND=ISTA+2*(NSMAX+1-IM)-1
              CALL SPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,&
               & IM,JMLOC,ISTA,IEND,LLONEM,LLDO_PARTSI(1,JITER),&
               & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,ISTA:IEND,1:3),&
               & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
            ENDDO
          ENDIF

        ENDDO ! JITER

      ELSE

        IF (LIMPF) THEN
          DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
            IM=YDLAP%MYMS(JMLOC)
            ISTA=NSPSTAF(IM)
            IEND=ISTA+2*(NSMAX+1-IM)-1
            CALL SPCSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,JMLOC,ISTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPTNDSI_VORG,&
             & ZSPTNDSI_DIVG,ZSPTNDSI_TG,PSPAUXG=ZSPAUXG)
          ENDDO
        ELSE
          DO JMLOC=NPTRMF(MYSETN),NPTRMF(MYSETN+1)-1
            IM=YDLAP%MYMS(JMLOC)
            ISTA=NSPSTAF(IM)
            IEND=ISTA+2*(NSMAX+1-IM)-1
            CALL SPCSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,JMLOC,ISTA,IEND,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPTNDSI_VORG,&
             & ZSPTNDSI_DIVG,ZSPTNDSI_TG)
          ENDDO
        ENDIF
        IF(LNHQE.AND.LNHQE_SIHYD) ZSPSPDG(:,:)=0.0_JPRB

      ENDIF

      CALL GSTATS(1023,1)

    ELSE

      !     - All spectral colunms can be done in one go

      IF (NSPEC2V > 0) THEN
        IM=-999
        IMLOC=-999

        IF (LNHEE.AND.(.NOT.LSI_NHEE)) THEN

          CALL SPNHSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,IMLOC,1,NSPEC2V,LLONEM,&
           & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
           & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)

        ELSEIF (LNHEE.AND.LSI_NHEE) THEN

          IF (LIMPF) THEN
            CALL SPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,IMLOC,1,NSPEC2V,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG,PSPAUXG=ZSPAUXG)
          ELSE
            CALL SPNHEESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,IMLOC,1,NSPEC2V,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,&
             & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
          ENDIF

        ELSEIF (LNHQE.AND.(.NOT.LNHQE_SIHYD)) THEN

          DO JITER=0,NITERHELM+1

            IF (JITER == 0) THEN
              ZSPBUFG(:,:,1)=ZSPDIVG(:,:)
            ENDIF
            CALL SPNHQESI_PRODSKAP(LLONEM,YDGEOMETRY,YDGMV,IDIMV,IDIMH,ISPEC2V,ZSPBUFG(1,1,1),ZSPBUFG(1,1,3))

            IF (LIMPF) THEN
              CALL SPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,&
               & IM,IMLOC,1,NSPEC2V,LLONEM,LLDO_PARTSI(1,JITER),&
               & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,1:NSPEC2V,1:3),&
               & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG,PSPAUXG=ZSPAUXG)
            ELSE
              CALL SPNHQESI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,&
               & IM,IMLOC,1,NSPEC2V,LLONEM,LLDO_PARTSI(1,JITER),&
               & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPSPDG,ZSPSVDG,ZSPBUFG(1:NFLEVG,1:NSPEC2V,1:3),&
               & ZSPTNDSI_VORG,ZSPTNDSI_DIVG,ZSPTNDSI_TG,ZSPTNDSI_SPDG,ZSPTNDSI_SVDG)
            ENDIF

          ENDDO ! JITER

        ELSE

          IF (LIMPF) THEN
            CALL SPCSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,IMLOC,1,NSPEC2V,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPTNDSI_VORG,&
             & ZSPTNDSI_DIVG,ZSPTNDSI_TG,PSPAUXG=ZSPAUXG)
          ELSE
            CALL SPCSI(YDMODEL%YRCST,YDGEOMETRY,YDLDDH,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,IMLOC,1,NSPEC2V,LLONEM,&
             & ZSPVORG,ZSPDIVG,ZSPTG,ZSPSPG,ZSPTNDSI_VORG,&
             & ZSPTNDSI_DIVG,ZSPTNDSI_TG)
          ENDIF
          IF(LNHQE.AND.LNHQE_SIHYD) ZSPSPDG(:,:)=0.0_JPRB

        ENDIF
      ENDIF ! NSPEC2V > 0

    ENDIF ! LLONEM

    !     - Transpose spectral data back to level structure

    CALL GSTATS(9,2)
    CALL GSTATS(54,3)
    ! Could be optimized by transposing zspvorg only if (limpf and lnhdyn)
    CALL TRSTOM(&
       & YDGEOMETRY,PSPVORG=ZSPVORG,PSPDIVG=ZSPDIVG,PSPTG=ZSPTG,PSPSPDG=ZSPSPDG,&
       & PSPSVDG=ZSPSVDG,PSPSPG=ZSPSPG,&
       & PSPVOR=YDSP%VOR,PSPDIV=YDSP%DIV,PSPT=YDSP%T,PSPSPD=YDSP%SPD,&
       & PSPSVD=YDSP%SVD,PSPSP=ZZPS,&
       & LDFULLM=LLONEM,LDNEEDPS=.TRUE.)  

    IF (LRSIDDH) THEN
      CALL TRSTOM(&
       & YDGEOMETRY,PSPVORG=ZSPTNDSI_VORG,PSPDIVG=ZSPTNDSI_DIVG,PSPTG=ZSPTNDSI_TG,&
       & PSPSPDG=ZSPTNDSI_SPDG,PSPSVDG=ZSPTNDSI_SVDG,&
       & PSPVOR=SPTNDSI_VOR,PSPDIV=SPTNDSI_DIV,PSPT=SPTNDSI_T,&
       & PSPSPD=SPTNDSI_SPD,PSPSVD=SPTNDSI_SVD,&
       & LDFULLM=LLONEM,LDNEEDPS=.FALSE.)
    ENDIF

    CALL GSTATS(54,1)
    CALL GSTATS(9,3)

    IF (LL_CALL_SPCIMPF) THEN
      CALL GSTATS(1024,0)

      IF (LRSIDDH) SPTNDSI_VOR(:,:)=SPTNDSI_VOR(:,:)-YDSP%VOR(:,:)
!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JMLOC,IM,ISTA,IEND)
        DO JMLOC=1,NUMP
          IM=YDLAP%MYMS(JMLOC)
          ISTA=YDLAP%NASM0(IM)
          IEND=ISTA+2*(NSMAX+1-IM)-1
          CALL SPCIMPFPOST(YDGEOMETRY,YDMODEL%YRML_GCONF%YRRIP,YDDYN,IM,ISTA,IEND,YDSP%DIV,YDSP%VOR)
        ENDDO
!$OMP END PARALLEL DO
      IF (LRSIDDH) SPTNDSI_VOR(:,:)=SPTNDSI_VOR(:,:)+YDSP%VOR(:,:)
      CALL GSTATS(1024,1)

    ENDIF
  
    IF (ALLOCATED(ZSPVORG)) DEALLOCATE(ZSPVORG)
    IF (ALLOCATED(ZSPDIVG)) DEALLOCATE(ZSPDIVG)
    IF (ALLOCATED(ZSPTG))   DEALLOCATE(ZSPTG)
    IF (ALLOCATED(ZSPSPDG)) DEALLOCATE(ZSPSPDG)
    IF (ALLOCATED(ZSPSVDG)) DEALLOCATE(ZSPSVDG)
    IF (ALLOCATED(ZSPSPG))  DEALLOCATE(ZSPSPG)
    IF (ALLOCATED(ZSPBUFG)) DEALLOCATE(ZSPBUFG)

    IF (ALLOCATED(ZSPTNDSI_VORG)) DEALLOCATE(ZSPTNDSI_VORG)
    IF (ALLOCATED(ZSPTNDSI_DIVG)) DEALLOCATE(ZSPTNDSI_DIVG)
    IF (ALLOCATED(ZSPTNDSI_TG))   DEALLOCATE(ZSPTNDSI_TG)
    IF (ALLOCATED(ZSPTNDSI_SPDG)) DEALLOCATE(ZSPTNDSI_SPDG)
    IF (ALLOCATED(ZSPTNDSI_SVDG)) DEALLOCATE(ZSPTNDSI_SVDG)

    IF (ALLOCATED(ZSPAUX))  DEALLOCATE(ZSPAUX)
    IF (ALLOCATED(ZSPAUXG)) DEALLOCATE(ZSPAUXG)

  ELSE
    CALL GSTATS(9,0)
  ENDIF ! LLDOSI

  ! * HORIZONTAL DIFFUSION, FILTERING ECPOS FIELDS, NUDGING.
  LLDOHD=(CDCONF == 'A')

  IF (LLDOHD .AND. NSPEC2 > 0) THEN

    IF( LGRADSP ) THEN
      CALL SPFILT(YDGEOMETRY,1,NSPEC2,SPVOR_FLT,SPDIV_FLT)
    ENDIF

    CALL SPCHOR(YDGEOMETRY,YDMODEL,1,NSPEC2,&
       & YDSP%VOR,YDSP%DIV,YDSP%T,YDSP%SPD,YDSP%SVD,YDSP%NHX,&
       & YDSP%GFL,ZZPS,SPTNDHD_VOR,SPTNDHD_DIV,SPTNDHD_T,&
       & SPTNDHD_SPD,SPTNDHD_SVD,SPTNDHD_SNHX,SPTNDHD_GFL)
    
!   !     - To make sure SPSP(:) is not used inadvertently if NPSP.NE.1
!   IF (NPSP /= 1) YDSP%SP(:)=HUGE(Z)

  ENDIF
  CALL GSTATS(9,1)

 ELSE

  CALL ABOR1('JJJ SPCM MUST NOT BE CALLED FOR THIS VALUE OF NCONF')

 ENDIF ! LR3D

ELSE

  IF (CDCONF /= '0') THEN
    WRITE (NULOUT,*) 'CDCONF(9:9) = ', CDCONF
    CALL ABOR1('JJJ SPCM: CONFIGURATION NOT ALLOWED: CHECK CDCONF')
  ENDIF

ENDIF ! CDCONF

IF(YDMP%NPSP==1)YDSP%SP=ZZPS
DEALLOCATE(ZZPS)

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SPCM',1,ZHOOK_HANDLE)
END SUBROUTINE SPCM
