SUBROUTINE DFI2(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,KDFITYP,LDIAB,LDCOR,KSTDFI,PSTEP,KSTDFIA,PSTEPA,LDIFH,YDVARBC,YDTCV,YDJOT,YDGOM5,YDODB)

!**** *DFI2*  - Controls integration for digital filter initialization

!     Purpose.
!     --------
!     CONTROLS  DIGITAL FILTER INITIALIZATION ON LEVEL 2

!**   Interface.
!     ----------
!        *CALL* *DFI2(...)*

!        Explicit arguments :
!        --------------------
!        KDFITYP: 1..7 option for DFI type
!        LDIAB  : controls if filter is diabatic
!        LDCOR  : controls if correction is necessary in case of diabatic filter
!        KSTDFI : number of diab. integration steps in one direction
!        PSTEP  : diabatic time step for dfi
!        KSTDFIA: number of adiab. integration steps in one direction
!        PSTEPA : adiabatic time step for dfi
!        LDIFH  : controls horizontal diffusion for adiabatic integrations

!        Implicit arguments :
!        --------------------
!        None

!     Method.
!     -------

!     Externals.
!     ----------
!       DFI3   : deepest level DFI control routine
!       ZEROACU: sets accumulation arrays zero
!       COPSP  : duplicates spectral state vectors
!       DIFSP  : calculates difference of two sets of fields
!       CORSP  : calculates difference of two sets of fields
!       REAST  : resets astronomical constants after each integration session
!       SUGRIDF : initialize gridpoint fields

!     Reference.
!     ----------
!     ARPEGE/ALADIN DOCUMENTATION

!     Author.
!     -------
!      Gabor Radnoti GMAP/MICECO
!      Original : 92-12-24

!     Modifications.
!     --------------
!      P. Smolikova 02-09-30 : interface to SUSPEC for d4 in NH
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!      M.Hamrud      01-Dec-2005 Generalized IO scheme
!      R. El Khatib  26-Nov-2008  Cleaning
!      K. Yessad: Sep 2010 : organigramme simplification.
!      cycles before cy37
!      T. Aspelien May 2011: DFI3 needs two different calls depending on YQ%LGP
!      K. Yessad (July 2014): Move some variables.
!      A. Geer      27-Jul-2015   VarBC is now an object passed by argument, for OOPS
!      O. Marsden    Aug 2016  Removed use of SPA3
!      F. Suzat      apr-18  Pass optional YDGOM5 to dfi3
!      A. Ambar, Feb-2018: CY46 cleaning (ZSPUB and ZSPVB removed)
!     ------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE FIELDS_MOD   , ONLY : FIELDS
USE MTRAJ_MOD    , ONLY : MTRAJ
USE PARKIND1     , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMCST       , ONLY : RDAY
USE JO_TABLE_MOD , ONLY : JO_TABLE
USE YOMCT0       , ONLY : LELAM, LOBSC1, LSPRT, LNHDYN
USE YOMCT2       , ONLY : NSTAR2, NSTOP2

USE YOMRIP0      , ONLY : NINDAT, NSSSSS
USE YOMINI       , ONLY : LBIAS, LINCR, LSCRINI
USE YOMLUN       , ONLY : NULOUT
USE VARBC_CLASS  , ONLY : CLASS_VARBC
USE TOVSCV_MOD   , ONLY : TOVSCV
USE SPECTRAL_FIELDS_MOD, ONLY : SPECTRAL_FIELD, ASSIGNMENT(=)
USE SUPERGOM_CLASS     , ONLY : CLASS_SUPERGOM
USE DBASE_MOD, ONLY : DBASE

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(INOUT) :: YDGEOMETRY  !! INOUT needed for call to SUSPEC
TYPE(FIELDS)      ,INTENT(INOUT) :: YDFIELDS
TYPE(MTRAJ)       ,INTENT(INOUT) :: YDMTRAJ
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
CLASS(DBASE), INTENT(INOUT), OPTIONAL    :: YDODB
INTEGER(KIND=JPIM),INTENT(IN)    :: KDFITYP 
LOGICAL           ,INTENT(IN)    :: LDIAB
LOGICAL           ,INTENT(IN)    :: LDCOR 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTDFI
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTEP
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTDFIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTEPA
LOGICAL           ,INTENT(IN)    :: LDIFH
TYPE(CLASS_VARBC) ,INTENT(INOUT) :: YDVARBC
TYPE(CLASS_SUPERGOM), INTENT(INOUT), OPTIONAL :: YDGOM5
TYPE(TOVSCV)      ,INTENT(IN),    OPTIONAL :: YDTCV
TYPE(JO_TABLE)    ,INTENT(INOUT), OPTIONAL :: YDJOT
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZDSPA3(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2,YDMODEL%YRML_GCONF%YRDIMF%NS3D), &
 &                 ZCSPA3(YDGEOMETRY%YRDIMV%NFLSUR,YDGEOMETRY%YRDIM%NSPEC2,YDMODEL%YRML_GCONF%YRDIMF%NS3D)
REAL(KIND=JPRB) :: ZDSPD2D(YDGEOMETRY%YRDIM%NSPEC2,YDMODEL%YRML_GCONF%YRDIMF%NFD2D),   ZCSPD2D(YDGEOMETRY%YRDIM%NSPEC2, &
 & YDMODEL%YRML_GCONF%YRDIMF%NFD2D)
REAL(KIND=JPRB) :: ZDSPA1(YDGEOMETRY%YRDIMV%NFLEVL,2),   ZCSPA1(YDGEOMETRY%YRDIMV%NFLEVL,2)

INTEGER(KIND=JPIM) :: IDEC, IDFI, ININDAT, INSSSSS, ISTAR2, ISTOP, ISTOP2, JL
INTEGER(KIND=JPIM) :: IIDEC, IA, ID, IDAT, IDAY, IM, ISSS
INTEGER(KIND=JPIM) :: ILMOIS(12)

LOGICAL :: LLEPHYS, LLMPHYS, LLSPOR

REAL(KIND=JPRB) :: ZEPS, ZTSTEP, ZEPL
REAL(KIND=JPRB),ALLOCATABLE :: ZDGFLGPQ(:,:),ZCGFLGPQ(:,:)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "copsp.intfb.h"
#include "corsp.intfb.h"
#include "dfi3.intfb.h"
#include "difsp.intfb.h"
#include "reast.intfb.h"
#include "updcal.intfb.h"
#include "zeroacu.intfb.h"
#include "sugridf.intfb.h"
#include "suspec.intfb.h"
#include "copgfl.intfb.h"
#include "corgfl.intfb.h"
#include "especrt.intfb.h"
#include "specrt.intfb.h"


#include "fcttim.func.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('DFI2',0,ZHOOK_HANDLE)
ASSOCIATE(YDGFL5=>YDMTRAJ%YRGFL5,YDGMV5=>YDMTRAJ%YRGMV5, YDGFL=>YDFIELDS%YRGFL,YDGMV=>YDFIELDS%YRGMV, &
 &  YDSURF=>YDFIELDS%YRSURF, &
 & YDDIM=>YDGEOMETRY%YRDIM, YDDIMV=>YDGEOMETRY%YRDIMV, YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP, &
 & YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY,YDRIP=>YDMODEL%YRML_GCONF%YRRIP, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY,YDDIMF=>YDMODEL%YRML_GCONF%YRDIMF)

ASSOCIATE(NS3D=>YDDIMF%NS3D, NFD2D=>YDDIMF%NFD2D, &
 & NSPEC2=>YDDIM%NSPEC2, &
 & NGPTOT=>YDGEM%NGPTOT, &
 & YQ=>YGFL%YQ, &
 & NFLSUR=>YDDIMV%NFLSUR, NFLEVL=>YDDIMV%NFLEVL, NFLEVG=>YDDIMV%NFLEVG, &
 & LMPHYS=>YDPHY%LMPHYS, LREASUR=>YDPHY%LREASUR, &
 & LEPHYS=>YDEPHY%LEPHYS, NSTOP=>YDRIP%NSTOP, TSTEP=>YDRIP%TSTEP)
!     ------------------------------------------------------------------


!*       0.    Initialize local variables and arrays
!              -------------------------------------
IF (YQ%LGP) THEN
    ALLOCATE(ZDGFLGPQ(NGPTOT,NFLEVG))
    ALLOCATE(ZCGFLGPQ(NGPTOT,NFLEVG))
ENDIF

ZEPS= 1.E-14_JPRB
ZEPL= 1._JPRB
LLSPOR =.FALSE.

WRITE (NULOUT,*) 'CALL TO DFI',KDFITYP


CALL ZEROACU(YDGEOMETRY,YDMODEL%YRML_GCONF,ZDSPA3,ZDSPD2D,ZDSPA1,ZDGFLGPQ)
CALL COPSP(YDGEOMETRY,YDDIMF,ZCSPA3,ZCSPD2D,ZCSPA1,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D)

!*       1. save constants to local ones before overwriting
!           -----------------------------------------------

LLMPHYS  = LMPHYS
LLEPHYS  = LEPHYS
ZTSTEP   = TSTEP
ISTAR2   = NSTAR2
ISTOP2   = NSTOP2
ISTOP    = NSTOP
NSTAR2   = 999
NSTOP2   = 999
NSTOP    = 999
ININDAT  = NINDAT
INSSSSS  = NSSSSS

!*       2. Launching different DFI sessions
!           --------------------------------

IF (KDFITYP < 6) THEN
!                              *********************************
!        2.1   DFI session:    * BACKWARD:      IDFI=====1     *
!                              *********************************

  IDFI=1
  IF (YQ%LGP) THEN
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFIA,IDFI,LDIFH,PSTEPA,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFIA,IDFI,LDIFH,PSTEPA,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT)
  ENDIF
  IF (.NOT.LDIAB) THEN
    CALL COPSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZCSPA3,ZCSPD2D,ZCSPA1)
  IF (YQ%LGP) THEN
    CALL COPGFL(YDGEOMETRY,YDGFL,YGFL%YQ,ZCGFLGPQ)
  ENDIF
  ENDIF

!                             *********************************
!*       2.2   DFI session:   * FORWARD/1:     IDFI=====2     *
!                             *********************************
  IDFI=2
  IF (YQ%LGP) THEN
    IF (LDIAB) CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    IF (LDIAB) CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT)
  ENDIF
  IF (LDIAB.AND.LDCOR) THEN
    CALL CORSP(YDGEOMETRY,YDDIMF,ZCSPA3,ZCSPD2D,ZCSPA1,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D)
    CALL CORGFL(YDGEOMETRY,YDGFL,YGFL%YQ,ZCGFLGPQ)
  ENDIF

!                             *********************************
!        2.3   DFI session:   * FORWARD/2:     IDFI=====3     *
!                             *********************************

  IDFI=3
  IF (YQ%LGP) THEN
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT)
  ENDIF
  IF (.NOT.LDIAB .AND. ABS(PSTEP-PSTEPA) > ZEPS)&
   & CALL ABOR1('DFI2 :  DIAB. AND ADIAB. TIMESTEPS DIFFER')  

ELSEIF (KDFITYP == 6) THEN

!                           *********************************
!        2.4   DFI session: * FORWARD  :     IDFI=====4     *
!                           *********************************

  LSCRINI= LOBSC1
  IF (LSCRINI .AND. ABS(ISTOP*ZTSTEP - 2*KSTDFI*PSTEP) >= ZEPL)&
   & CALL ABOR1('DFI2 : SPANS FOR FINALISATION AND SCREENING DIFFER')  

  IDFI=4
  IF (YQ%LGP) THEN
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT,YDGOM5=YDGOM5,YDODB=YDODB)
  ENDIF

ELSEIF (KDFITYP == 7) THEN

!                              *********************************
!        2.5   DFI session:    * BACKWARD+FORWARD : IDFI=====6 *
!                              *********************************

  IDFI=6
  IF (YQ%LGP) THEN
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,.FALSE.,KSTDFIA,IDFI,LDIFH,PSTEPA,LLMPHYS,LLEPHYS,&
   & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,.FALSE.,KSTDFIA,IDFI,LDIFH,PSTEPA,LLMPHYS,LLEPHYS,&
   & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT)
  ENDIF
  CALL COPSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZDSPA3,ZDSPD2D,ZDSPA1)
  IF (YQ%LGP) THEN
   CALL COPGFL(YDGEOMETRY,YDGFL,YGFL%YQ,ZDGFLGPQ)
  ENDIF
  WRITE (NULOUT,*) 'SET ACCUMULATED VARIABLES TO ZERO',KDFITYP
  CALL ZEROACU(YDGEOMETRY,YDMODEL%YRML_GCONF,ZDSPA3,ZDSPD2D,ZDSPA1,ZDGFLGPQ)
  IF (YQ%LGP) THEN
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,PGFLGPQ=ZDGFLGPQ,YDTCV=YDTCV,YDJOT=YDJOT)
  ELSE
    CALL DFI3(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDMODEL,YDVARBC,LDIAB,KSTDFI,IDFI,LDIFH,PSTEP,LLMPHYS,LLEPHYS,&
     & ININDAT,INSSSSS,ZDSPA3,ZDSPD2D,ZDSPA1,YDTCV=YDTCV,YDJOT=YDJOT)
  ENDIF
ENDIF

!*       3. Reset original constants and surface fields
!           -------------------------------------------

IF (KDFITYP == 6) THEN
  IDFI=998
ELSE
  IDFI=999
ENDIF
CALL REAST(YDRIP,IDFI,KSTDFI,ININDAT,INSSSSS,IDEC)
IF (LREASUR) THEN
  IF (IDFI == 999) THEN
    CALL SUGRIDF(YDGEOMETRY,YDFIELDS%YRSURF,YDMODEL,0,YDFIELDS%YRSPEC%OROG,LLSPOR)
  ELSE
    IDAT= NINDAT
    ISSS= NSSSSS

    ID= NDD(IDAT)
    IM= NMM(IDAT)
    IA= NCCAA(IDAT)
    IDAY= NINT(RDAY)
    IIDEC= ISSS/IDAY
    IF (IIDEC /= 0) THEN
      CALL UPDCAL(ID,IM,IA,IIDEC,ID,IM,IA,ILMOIS,NULOUT)
      IDAT= IA*10000+IM*100+ID
      ISSS= MOD(ISSS,IDAY)
    ENDIF

    NINDAT= IDAT
    NSSSSS= ISSS
    CALL SUGRIDF(YDGEOMETRY,YDFIELDS%YRSURF,YDMODEL,8,YDFIELDS%YRSPEC%OROG,LLSPOR)
  ENDIF
ENDIF
LMPHYS = LLMPHYS
LEPHYS = LLEPHYS
TSTEP  = ZTSTEP
NSTAR2 = ISTAR2
NSTOP2 = ISTOP2
NSTOP  = ISTOP

!*       4. Compute final spectral fields
!           -----------------------------

IF ((LDCOR.AND.LDIAB) .OR. (LBIAS.AND.IDFI == 999)) THEN
  CALL DIFSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZDSPA3,ZDSPD2D,ZDSPA1,&
   & ZCSPA3,ZCSPD2D,ZCSPA1)
ELSEIF (LBIAS) THEN
  CALL SUSPEC(YDGEOMETRY,YDFIELDS%YRGFL,YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_LBC, &
 &            YDFIELDS%YRSPEC,KFILE=8)
  IF (LSPRT) THEN
    IF (LELAM) THEN
      CALL ESPECRT(YDGEOMETRY,YDFIELDS%YRGFL,YGFL,0,YDFIELDS%YRSPEC)
    ELSE
      CALL SPECRT(YDGEOMETRY,YDFIELDS%YRGFL,YGFL,0,YDFIELDS%YRSPEC)
    ENDIF
  ENDIF
  CALL CORSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZDSPA3,ZDSPD2D,ZDSPA1)
  IF (YQ%LGP) THEN
    CALL CORGFL(YDGEOMETRY,YDGFL,YGFL%YQ,ZDGFLGPQ)
  ENDIF
ELSEIF (LINCR) THEN
   CALL SUSPEC(YDGEOMETRY,YDFIELDS%YRGFL,YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_LBC, &
 &             YDFIELDS%YRSPEC,KFILE=3)
   IF (LSPRT) THEN
     IF (LELAM) THEN
       CALL ESPECRT(YDGEOMETRY,YDFIELDS%YRGFL,YGFL,0,YDFIELDS%YRSPEC)
     ELSE
       CALL SPECRT(YDGEOMETRY,YDFIELDS%YRGFL,YGFL,0,YDFIELDS%YRSPEC)
     ENDIF
   ENDIF
   IF (LNHDYN) THEN
!     cannot trust NH increments from file, reset their bias to zero
      YDFIELDS%YRSPEC%SPD(:,:) = 0._JPRB
      YDFIELDS%YRSPEC%SVD(:,:) = 0._JPRB
      IF(YDMODEL%YRML_DYN%YRDYNA%LNHX) YDFIELDS%YRSPEC%NHX(:,:) = 0._JPRB
   ENDIF
   CALL CORSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZDSPA3,ZDSPD2D,ZDSPA1)
   IF (YQ%LGP) THEN
      CALL COPGFL(YDGEOMETRY,YDGFL,YGFL%YQ,ZDGFLGPQ)
   ENDIF
ELSE
   CALL COPSP(YDGEOMETRY,YDDIMF,YDFIELDS%YRSPEC%SP3D,YDFIELDS%YRSPEC%SP2D,YDFIELDS%YRSPEC%SP1D,ZDSPA3,ZDSPD2D,ZDSPA1)
ENDIF
IF (LBIAS .OR. IDFI /= 999) THEN
   NSTOP =0
   NSTOP2=0
   NSTAR2=0
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('DFI2',1,ZHOOK_HANDLE)
END SUBROUTINE DFI2
