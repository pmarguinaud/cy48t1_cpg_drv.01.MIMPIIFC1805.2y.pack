SUBROUTINE SUNDDH(YDGEOMETRY,YDSURF,YDMODEL)
!****
!     ------------------------------------------------------------------

!     SUNDDH * INITIALISE LES ELEMENTS DE CONTROLE
!     DIAGNOSTICS PAR DOMAINES HORIZONTAUX
!     ------------------------------------------------------------------

!       ---------------------------------------------------------
!     BUT
!     ---
!     DIMENSIONS PAR DEFAUTS ET PERMANENTES
!     LECTURE, EDITION DE LA NAMELIST NAMDDH
!     - LOGIQUES STOCKES DANS /YOMLDDH/
!     - DIMENSIONS STOCKEES DANS /DDHDIM/. UNE PARTIE DES DIMENSIONS EST
!     DEDUITE DE LA NAMELIST, ET NON LUE
!     -------

!     ARGUMENTS D ENTREE
!     ------------------

!     ARGUMENTS IMPLICITES
!     --------------------
!       UNITES NULNAM, NULOUT
!       INDICATEURS LOGIQUES, COMMON /YOMLDDH/
!       COMDECK * YOMMDDH *, STOCKAGE DES DIMENSIONS, DES MASQUES, ETC...

!     AUTRES ENTREES
!     --------------
!       NAMELIST NAMDDH

!     SORTIES
!     -------
!       VALEURS PAR DEFAUT OU LUES SUR NAMELIST NAMDDH
!       DES INDICATEURS LOGIQUES ET DES CARACTERISTIQUES BRUTES
!       DES DOMAINES HORIZONTAUX UTILISATEUR :
!         /YOMLDDH/, BDEDDH, NDHKD
!       DIMENSIONS DEDUITES : NDHNPU, NDHBPU, NDHBPX, NDHNOM, NDHDDX
!        ET POUR LES TABLEAUX DE CUMULS
!        NDHCV, NDHCS, NDHCVSU, NDHCSSU ET LES INDICATEURS DE
!        DE POSITION (POINTEURS LOGIQUES) PERMENENTS ET DE SITUATIONS

!     ORIGINAL
!     --------
!     91-03-22, Alain Joly.

!     MODIFICATIONS
!     -------------
!     01-01-26 R. El Khatib sucddh moved from su0yoma and called under LECMWF
!     R. El Khatib : 01-08-07 Pruning options
!     02-09-30  JJMorcrette: ECMWF PAR, UV, CAPE
!     M.Hamrud      01-Oct-2003 CY28 Cleaning
!     2003-12-05, F. Vinit : new number of free-style variables and fluxes for Meteo-France.
!     05-06-25 F. Bouyssel: Lopez's prognostic scheme
!     06-03-23 T.Kovacic : AROME physics and common dynamics-physics interface 
!     06-01-25 F. Bouyssel: Change default of NHDQLNFP
!     JJMorcrette 20060721 PP of clear-sky PAR and TOA incident solar radiation
!     JJMorcrette 20060807 PP of VIMD
!     05-09-26 S. Serrar: NDHVFS and NDHFFS incremented to include GHG + SF6 diagnostics
!     20061002 JJMorcrette DDH for aerosol physics
!     07-01-15 G. Balsamo: NDHFWLS increased to include condensation diagnostics
!     07-07-17 S. Serrar: New GEMS diagnostics for methane
!     07-05-01  T.Kovacic : changed the number of phy. fluxes and variables if L3MT.OR.LSTRAPRO)
!     1-oct-08 O.Riviere Activates new data flow in Arome (LFLEXDIA set to TRUE)
!     18-dec-08: O.Riviere Possibility of having new data flow in Arpege/Aladin also
!     K. Yessad (Jan 2010): remove useless variables.
!     JJMorcrette 20100212 PP of CBASE, ODEGL and VISIH
!     M. Ahlgrimm 31-Oct-2011 Add rain, snow and PEXTRA to DDH output
!     M. Ahlgrimm 31-Oct-2011 Clear-sky downward radiation at surface
!     R Engelen 20111024 Clean-up SF6
!     26-10-11, L. Jones: MACC: Bug fix and removal of duplicate logic.
!      R. El Khatib 22-Mar-2012 Fix uninitialized variables
!      R. El Khatib 06-Mar-2015 define CFPATHDDH
!     M. Ahlgrimm Apr 2014: Add lake variables and precip fraction to DDH output
!     P. Bechtold: 10-Nov-2015: Add 4 more freestyle variables, cleaning
!     A. Bozzo:   Jan 2016 Add solar direct beam at surface
!     F. Vana  02/03/2018  Remove superfluous CFPATHDDH assignment.
!     M. Hrastinski, Sep-2019: TKE and TTE terms for ALARO DDH + fixed bug
!                              for water related quantities
!     E. Bazile, Feb., 2021 : Add Tsurf, fcs, fcll in free style over sea
!------------------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK    ,DR_HOOK
USE YOMLUN   , ONLY : NULOUT   ,NULNAM   ,NULERR 
USE YOMMDDH  , ONLY : JPDHXPU,JPDHNOX
USE YOMCT0   , ONLY : LNHDYN   ,LECMWF
USE YOMDYNA  , ONLY : YRDYNA
USE YOMPHY   , ONLY : JPHYMF,JPHYARO
USE DDH_MIX  , ONLY : SUDDHFLEX

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY),INTENT(INOUT) :: YDGEOMETRY 
TYPE(TSURF)   ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)   ,INTENT(INOUT),TARGET :: YDMODEL
INTEGER(KIND=JPIM) :: IMASK, JBOX, JJ, JMASK
INTEGER(KIND=JPIM) :: IPT
LOGICAL :: LLCUMUL, LLERR
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "posnam.intfb.h"
#include "sucddh.intfb.h"


LOGICAL, POINTER :: LHDGLB, LHDZON, LHDDOP, LHDHKS, LHDMCI, LHDENT, LHDEFG,&
 & LHDEFZ, LHDEFD, LHDPRG, LHDPRZ, LHDPRD, LHDLIST, LHDFIL, LONLYVAR,&
 & LHDORIGP, LHDCDPI, LFLEXDIA, LRSLDDH, LRSIDDH, LRHDDDH, LDDH_OMP
INTEGER(KIND=JPIM), POINTER :: NDHZPR, NDHKD
CHARACTER(LEN=200), POINTER :: CFPATHDDH
REAL(KIND=JPRB), POINTER  :: BDEDDH(:,:)

#include "namddh.nam.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUNDDH',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP, &
 &  YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY,YDSDDH=>YDMODEL%YRML_DIAG%YRSDDH,YDMDDH=>YDMODEL%YRML_DIAG%YRMDDH, &
 & YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,YDARPHY=>YDMODEL%YRML_PHY_MF%YRARPHY, &
  & YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH,YGFL=>YDMODEL%YRML_GCONF%YGFL,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY, &
  & YDDIMF=>YDMODEL%YRML_GCONF%YRDIMF)
ASSOCIATE(NACTAERO=>YGFL%NACTAERO, NGHG=>YGFL%NGHG, &
 & NUMSPFLDS=>YGFL%NUMSPFLDS, YQ=>YGFL%YQ, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFTHER=>YDDIMF%NFTHER, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & NCSI=>YDDPHY%NCSI, NCSNEC=>YDDPHY%NCSNEC, NCSS=>YDDPHY%NCSS, &
 & NTILES=>YDDPHY%NTILES, NVEXTR=>YDDPHY%NVEXTR, NVECOUT=>YDDPHY%NVECOUT, &
 & LAGPHY=>YDEPHY%LAGPHY, LEFLAKE=>YDEPHY%LEFLAKE, LEPCLD=>YDEPHY%LEPCLD, &
 & LHDLFA=>YDLDDH%LHDLFA, LHDOUFD=>YDLDDH%LHDOUFD, LHDOUFG=>YDLDDH%LHDOUFG, &
 & LHDOUFZ=>YDLDDH%LHDOUFZ, LHDOUP=>YDLDDH%LHDOUP,  LHDPR=>YDLDDH%LHDPR, &
 & LRDDHDYN=>YDLDDH%LRDDHDYN, LSDDH=>YDLDDH%LSDDH, &
 & MHDDDH_NHX=>YDMDDH%MHDDDH_NHX, &
 & MHDDDH_PD=>YDMDDH%MHDDDH_PD, MHDDDH_Q=>YDMDDH%MHDDDH_Q, &
 & MHDDDH_T=>YDMDDH%MHDDDH_T, MHDDDH_U=>YDMDDH%MHDDDH_U, &
 & MHDDDH_V=>YDMDDH%MHDDDH_V, MHDDDH_VD=>YDMDDH%MHDDDH_VD, &
 & MSIDDH_PD0=>YDMDDH%MSIDDH_PD0, MSIDDH_PD1=>YDMDDH%MSIDDH_PD1, &
 & MSIDDH_PD9=>YDMDDH%MSIDDH_PD9, MSIDDH_T0=>YDMDDH%MSIDDH_T0, &
 & MSIDDH_T1=>YDMDDH%MSIDDH_T1, MSIDDH_T9=>YDMDDH%MSIDDH_T9, &
 & MSIDDH_U0=>YDMDDH%MSIDDH_U0, MSIDDH_U1=>YDMDDH%MSIDDH_U1, &
 & MSIDDH_U9=>YDMDDH%MSIDDH_U9, MSIDDH_V0=>YDMDDH%MSIDDH_V0, &
 & MSIDDH_V1=>YDMDDH%MSIDDH_V1, MSIDDH_V9=>YDMDDH%MSIDDH_V9, &
 & MSIDDH_VD0=>YDMDDH%MSIDDH_VD0, MSIDDH_VD1=>YDMDDH%MSIDDH_VD1, &
 & MSIDDH_VD9=>YDMDDH%MSIDDH_VD9, MSLDDH_NHX=>YDMDDH%MSLDDH_NHX, &
 & MSLDDH_PD=>YDMDDH%MSLDDH_PD, MSLDDH_T=>YDMDDH%MSLDDH_T, &
 & MSLDDH_U=>YDMDDH%MSLDDH_U, MSLDDH_V=>YDMDDH%MSLDDH_V, &
 & MSLDDH_VD=>YDMDDH%MSLDDH_VD, NDHAEND=>YDMDDH%NDHAEND, &
 & NDHAENP=>YDMDDH%NDHAENP, NDHAHKD=>YDMDDH%NDHAHKD, NDHAHKP=>YDMDDH%NDHAHKP, &
 & NDHAMCD=>YDMDDH%NDHAMCD, NDHAMCP=>YDMDDH%NDHAMCP, NDHAVD=>YDMDDH%NDHAVD, &
 & NDHAVP=>YDMDDH%NDHAVP, NDHBEND=>YDMDDH%NDHBEND, NDHBENP=>YDMDDH%NDHBENP, &
 & NDHBHKD=>YDMDDH%NDHBHKD, NDHBHKP=>YDMDDH%NDHBHKP, NDHBMCD=>YDMDDH%NDHBMCD, &
 & NDHBMCP=>YDMDDH%NDHBMCP, NDHBPU=>YDMDDH%NDHBPU, NDHBPX=>YDMDDH%NDHBPX, &
 & NDHBVD=>YDMDDH%NDHBVD, NDHBVP=>YDMDDH%NDHBVP, NDHCS=>YDMDDH%NDHCS, &
 & NDHCSSU=>YDMDDH%NDHCSSU, NDHCV=>YDMDDH%NDHCV, NDHCVSU=>YDMDDH%NDHCVSU, &
 & NDHCVSUL=>YDMDDH%NDHCVSUL, NDHCVSUN=>YDMDDH%NDHCVSUN, NDHDDX=>YDMDDH%NDHDDX, &
 & NDHFEND=>YDMDDH%NDHFEND, NDHFENP=>YDMDDH%NDHFENP, NDHFFS=>YDMDDH%NDHFFS, &
 & NDHFHKD=>YDMDDH%NDHFHKD, NDHFHKP=>YDMDDH%NDHFHKP, NDHFIIS=>YDMDDH%NDHFIIS, &
 & NDHFMCD=>YDMDDH%NDHFMCD, NDHFMCP=>YDMDDH%NDHFMCP, NDHFSD=>YDMDDH%NDHFSD, &
 & NDHFSP=>YDMDDH%NDHFSP, NDHFSSS=>YDMDDH%NDHFSSS, NDHFTIS=>YDMDDH%NDHFTIS, &
 & NDHFTLS=>YDMDDH%NDHFTLS, NDHFTSS=>YDMDDH%NDHFTSS, NDHFTTS=>YDMDDH%NDHFTTS, &
 & NDHFVD=>YDMDDH%NDHFVD, NDHFVP=>YDMDDH%NDHFVP, NDHFWLS=>YDMDDH%NDHFWLS, &
 & NDHIDH=>YDMDDH%NDHIDH, NDHNOM=>YDMDDH%NDHNOM, &
 & NDHNPU=>YDMDDH%NDHNPU, NDHTEN=>YDMDDH%NDHTEN, NDHTHK=>YDMDDH%NDHTHK, &
 & NDHTMC=>YDMDDH%NDHTMC, NDHVEN=>YDMDDH%NDHVEN, NDHVFS=>YDMDDH%NDHVFS, &
 & NDHVHK=>YDMDDH%NDHVHK, NDHVIIS=>YDMDDH%NDHVIIS, NDHVMC=>YDMDDH%NDHVMC, &
 & NDHVS=>YDMDDH%NDHVS, NDHVSSS=>YDMDDH%NDHVSSS, NDHVTIS=>YDMDDH%NDHVTIS, &
 & NDHVTLS=>YDMDDH%NDHVTLS, NDHVTSS=>YDMDDH%NDHVTSS, NDHVTTS=>YDMDDH%NDHVTTS, &
 & NDHVV=>YDMDDH%NDHVV, NDHVWLS=>YDMDDH%NDHVWLS, &
 & NDIMHDGFL=>YDMDDH%NDIMHDGFL, NDIMSIGMV=>YDMDDH%NDIMSIGMV, &
 & NSPEC2V=>YDMP%NSPEC2V, NSPEC2VDDH=>YDMP%NSPEC2VDDH, NSPEC2VF=>YDMP%NSPEC2VF, &
 & LHDPAS=>YDSDDH%LHDPAS, LHDQLN=>YDSDDH%LHDQLN, NHDPASFD=>YDSDDH%NHDPASFD, &
 & NHDPASFP=>YDSDDH%NHDPASFP, NHDPASTD=>YDSDDH%NHDPASTD, &
 & NHDPASTP=>YDSDDH%NHDPASTP, NHDPASVA=>YDSDDH%NHDPASVA, &
 & NHDQLNFD=>YDSDDH%NHDQLNFD, NHDQLNFP=>YDSDDH%NHDQLNFP, &
 & NHDQLNTD=>YDSDDH%NHDQLNTD, NHDQLNTP=>YDSDDH%NHDQLNTP, &
 & NHDQLNVA=>YDSDDH%NHDQLNVA, &
 & YSD_VF=>YDSURF%YSD_VF, YSP_SL=>YDSURF%YSP_SL, &
 & LBUFLUX=>YDARPHY%LBUFLUX, &
 & LMPA=>YDARPHY%LMPA, L3MT=>YDPHY%L3MT, LCONDWT=>YDPHY%LCONDWT, &
 & LPROCLD=>YDPHY%LPROCLD, LSTRAPRO=>YDPHY%LSTRAPRO, NDPSFI=>YDPHY%NDPSFI, &
 & NPHY=>YDPHY%NPHY,LPTKE=>YDPHY%LPTKE, LCOEFK_PTTE=>YDPHY%LCOEFK_PTTE, &
 & NFIELDS3D_AUTO=>YDMDDH%NFIELDS3D_AUTO, NFIELDS2D_AUTO=>YDMDDH%NFIELDS2D_AUTO, & 
 & NFIELDSMAX=>YDMDDH%NFIELDSMAX)
! Associate pointers for variables in namelist
LHDGLB   => YDLDDH%LHDGLB
LHDZON   => YDLDDH%LHDZON
LHDDOP   => YDLDDH%LHDDOP
LHDHKS   => YDLDDH%LHDHKS
LHDMCI   => YDLDDH%LHDMCI
LHDENT   => YDLDDH%LHDENT
LHDEFG   => YDLDDH%LHDEFG
LHDEFZ   => YDLDDH%LHDEFZ
LHDEFD   => YDLDDH%LHDEFD
LHDPRG   => YDLDDH%LHDPRG
LHDPRZ   => YDLDDH%LHDPRZ
LHDPRD   => YDLDDH%LHDPRD
NDHZPR   => YDMODEL%YRML_DIAG%YRMDDH%NDHZPR
LHDLIST  => YDLDDH%LHDLIST
NDHKD    => YDMODEL%YRML_DIAG%YRMDDH%NDHKD
BDEDDH   => YDMODEL%YRML_DIAG%YRMDDH%BDEDDH
CFPATHDDH => YDMODEL%YRML_DIAG%YRMDDH%CFPATHDDH
LHDFIL   => YDLDDH%LHDFIL
LONLYVAR => YDLDDH%LONLYVAR
LHDORIGP => YDLDDH%LHDORIGP
LHDCDPI  => YDLDDH%LHDCDPI
LFLEXDIA => YDLDDH%LFLEXDIA
LRSLDDH  => YDLDDH%LRSLDDH
LRSIDDH  => YDLDDH%LRSIDDH
LRHDDDH  => YDLDDH%LRHDDDH
LDDH_OMP => YDLDDH%LDDH_OMP

!     ------------------------------------------------------------------

WRITE (NULOUT,*) ' ---------------------------------------'
WRITE (NULOUT,*) ' DDH * DDH * DDH * DDH * DDH *'
WRITE (NULOUT,*) ' DIAGNOSTICS PAR DOMAINES HORIZONTAUX'

!     ------------------------------------------------------------------

!**   1. VALEURS PAR DEFAUT

!        1.1 Set implicit default values

!        PAS DE DIAGNOSTICS PAR DEFAUT
LHDGLB = .FALSE.
LHDZON = .FALSE.
LHDDOP = .FALSE.

LHDLFA=.TRUE.
LFLEXDIA=.FALSE.
LHDHKS = .FALSE.
LHDMCI = .FALSE.
LHDENT = .FALSE.

LHDPRG = .FALSE.
LHDPRZ = .FALSE.
LHDPRD = .FALSE.
LHDEFG = .FALSE.
LHDEFZ = .FALSE.
LHDEFD = .FALSE.

LHDLIST = .FALSE.

LRDDHDYN = .FALSE.
LRSLDDH  = .FALSE.
LRSIDDH  = .FALSE.
LRHDDDH  = .FALSE.

LDDH_OMP = .FALSE.

LONLYVAR = NPHY==JPHYARO
LHDORIGP = .TRUE.
LHDCDPI = .FALSE.

DO JMASK = 1, JPDHXPU
  NDHBPU(JMASK) = 0
ENDDO
DO JBOX = 1, JPDHNOX
  DO JJ = 1, 10
    BDEDDH(JJ,JBOX) = 0.0_JPRB
  ENDDO
ENDDO
!     BANDE ZONALE IMPRIMEE PAR DEFAUT
NDHZPR = 1

!     LHDFIL = IMPRESS. DE LA LISTE DES ARTICLES DES FICHIERS DE SORTIE.
LHDFIL = .FALSE.

!     INITIALISATION DE SECURITE DES INDICATEURS DE SORTIES EFFECTIVES
!     (GERES AU NIVEAU DE CNT4 )
LHDOUFG=.FALSE.
LHDOUFZ=.FALSE.
LHDOUFD=.FALSE.
LHDOUP = .FALSE.

!     INITIALISATION PAR DEFAUT PROVISOIRE DES DIMENSIONS
NDHKD = 0
NDHNPU = 0
NDHBPX = 0
NDHNOM = 0
NDHDDX = 0
NDHIDH = 0

NDIMHDGFL = 0
NDIMSIGMV = 0

MSIDDH_U0 = 0
MSIDDH_V0 = 0
MSIDDH_T0 = 0
MSIDDH_PD0 = 0
MSIDDH_VD0 = 0

MSIDDH_U9 = 0
MSIDDH_V9 = 0
MSIDDH_T9 = 0
MSIDDH_PD9 = 0
MSIDDH_VD9 = 0

MSIDDH_U1 = 0
MSIDDH_V1 = 0
MSIDDH_T1 = 0
MSIDDH_PD1 = 0
MSIDDH_VD1 = 0

MHDDDH_U = 0
MHDDDH_V = 0
MHDDDH_T = 0
MHDDDH_Q = 0
MHDDDH_PD = 0
MHDDDH_VD = 0
MHDDDH_NHX = 0

MSLDDH_U = 0
MSLDDH_V = 0
MSLDDH_T = 0
MSLDDH_PD = 0
MSLDDH_VD = 0
MSLDDH_NHX = 0

CFPATHDDH=' '

NFIELDS3D_AUTO = 10
NFIELDS2D_AUTO = 10

NHDQLNVA = 0
NHDQLNTD = 0
NHDQLNFD = 0
NHDQLNFP = 0
NHDQLNTP = 0

!        1.2 Modify default values according to LECMWF

IF (LECMWF) THEN
  LHDLFA=.FALSE.
ELSE
ENDIF

!     ------------------------------------------------------------------
!**   2. LECTURE SUR NAMELIST
CALL POSNAM(NULNAM,'NAMDDH')
READ (NULNAM,NAMDDH)

!     ------------------------------------------------------------------
!**   3. INITIALISATION DES DIMENSIONS DEDUITES

!*    INITIALISATION DES POINTEURS LOGIQUES PERMANENTS
!     ------------------------------------------------------------------
!     LES ENTIERS DEFINIS ICI DOIVENT ETRE RIGOUREUSEMENT EN
!     PHASE AVEC LE CODE SCIENTIFIQUE :
!       CPDYDDH : CALCULS DES CONTIBUTIONS DYNAMQIUES
!       CPPHDDH : CALCUL DES CONTRIBUTIONS PHYIQUES
!       POSDDH ET AUTRES SS-PRGM DE SORTIES (PPFIDH, PPEDDH)


LHDQLN=.FALSE.

IF(LHDHKS) THEN
  SELECT CASE (NPHY)
  CASE(JPHYARO)
    LHDQLN=.TRUE.
  CASE DEFAULT
!             - ARPEGE/ALADIN -
    IF ((LECMWF.AND.LEPCLD).OR.(.NOT.LECMWF.AND.(.NOT.LMPA))) THEN
      !        DIAGNOSTIC D'EAU LIQUIDE ET NEIGEUSE.
      LHDQLN=.TRUE.
      IF ( L3MT.OR.LSTRAPRO )THEN
        NHDQLNVA= 4  ! total number of variables for water
        NHDQLNTD= 4  ! number of dyn. tendencies
        NHDQLNFD= 4  ! number of dyn. fluxes
        ! number of physical fluxes
        IF (NDPSFI==1) THEN
          NHDQLNFP= 22 
        ELSE
          NHDQLNFP= 20 
        ENDIF
        NHDQLNTP= 0  ! number of physical tendencies
      ELSE
        NHDQLNVA=4  ! total number of variables for water
        NHDQLNTD=4  ! number of dynamical tendendies for water
        NHDQLNFD=4  ! number of dynamical fluxes for water

        IF (LCONDWT.AND.LPROCLD) THEN
          NHDQLNFP=14
        ELSE
          NHDQLNFP=14
        ENDIF
        NHDQLNTP=0
      ENDIF
    ELSE
      LHDQLN=.FALSE.
      NHDQLNVA=0
      NHDQLNTD=0
      NHDQLNFD=0
      NHDQLNFP=0
      NHDQLNTP=0
    ENDIF
  END SELECT
  IF (LHDCDPI) THEN
!     - Common Dynamics Physics Interface -
    IF (.NOT.LONLYVAR) THEN
      IF (LBUFLUX) THEN
        NHDQLNFP= NHDQLNFP+11
      ELSE
        NHDQLNTP= NHDQLNTP+11
      ENDIF
    ENDIF
  ENDIF
ENDIF


IF (LECMWF) THEN
   !switch on option for passive extra variables - M. Ahlgrimm
   IF(LHDHKS.AND.NVEXTR >= 1) THEN
      !        DIAGNOSTIC DES VARIABLES PASSIVES (extra-GFL).
      LHDPAS=.TRUE.
      NHDPASVA=NVEXTR+NVECOUT
      NHDPASTD=0
      NHDPASFD=0
      NHDPASFP=0
      NHDPASTP=0
   ELSE
      ! NEW: always switch on passive output for precip fraction M Ahlgrimm
      LHDPAS=.TRUE.
      NHDPASVA=NVECOUT
      NHDPASTD=0
      NHDPASFD=0
      NHDPASFP=0
      NHDPASTP=0
   ENDIF
ELSE
   !IF(LHDHKS.AND..NOT.LHDQLN.AND.NGFL_EXT >= 1) THEN
   !switch on option for passive extra variables - M. Ahlgrimm
   IF(LHDHKS.AND.NVEXTR >= 1) THEN
      
      !        DIAGNOSTIC DES VARIABLES PASSIVES (extra-GFL).
      LHDPAS=.TRUE.
      !original
      !  NHDPASVA=NGFL_EXT
      NHDPASVA=NVEXTR
      NHDPASTD=0
      NHDPASFD=0
      NHDPASFP=0
      NHDPASTP=0
   ELSE
      LHDPAS=.FALSE.
      NHDPASVA=0
      NHDPASTD=0
      NHDPASFD=0
      NHDPASFP=0
      NHDPASTP=0
   ENDIF
ENDIF

!     VARIABLES SI LHDHKS
SELECT CASE (NPHY)
CASE DEFAULT
  NDHVHK = 10+NHDQLNVA+NHDPASVA
  IF (LPTKE) THEN
    NDHVHK = NDHVHK + 1
    IF (LCOEFK_PTTE) THEN
      NDHVHK = NDHVHK + 1
    ENDIF
  ENDIF
END SELECT


!     VARIABLES SI LHDMCI
NDHVMC = 3
!     VARIABLES SI LHDENT
NDHVEN = 1

!     TENDANCES DYNAMIQUES SI LHDHKS
SELECT CASE (NPHY)
CASE DEFAULT
  NDHAHKD = 13+NHDQLNTD+NHDPASTD
  IF (LPTKE) THEN
    NDHAHKD = NDHAHKD + 1
    IF (LCOEFK_PTTE) THEN
      NDHAHKD = NDHAHKD + 1
    ENDIF
  ENDIF
END SELECT

!     TENDANCES DYNAMIQUES SI LHDMCI
NDHAMCD = 8
!     TENDANCES DYNAMIQUES SI LHDENT
NDHAEND = 2

!     FLUX DYNAMIQUES SI LHDHKS
SELECT CASE (NPHY)
CASE DEFAULT
  NDHBHKD = 10+NHDQLNFD+NHDPASFD
  IF (LPTKE) THEN
    NDHBHKD = NDHBHKD + 1
    IF (LCOEFK_PTTE) THEN
      NDHBHKD = NDHBHKD + 1
    ENDIF
  ENDIF
END SELECT
!     FLUX DYNAMIQUES SI LHDMCI
NDHBMCD = 9
!     FLUX DYNAMIQUES SI LHDENT
NDHBEND = 2

!     FLUX PHYSIQUES SI LHDHKS
IF (LMPA)  THEN
ELSE
ENDIF
SELECT CASE (NPHY)
CASE(JPHYARO)
CASE DEFAULT
  NDHAHKP = 26+NHDQLNFP+NHDPASFP
  IF ( (L3MT.OR.LSTRAPRO).AND.(NDPSFI==1) ) NDHAHKP = NDHAHKP + 2
  IF (LPTKE) THEN
    NDHAHKP = NDHAHKP + 4
    IF(LCOEFK_PTTE) THEN
      NDHAHKP = NDHAHKP + 2
    ENDIF
  ENDIF
END SELECT
!     FLUX PHYSIQUES SI LHDMCI
NDHAMCP = 9
!     FLUX PHYSIQUES SI LHDENT
NDHAENP = 0

!     TENDANCES PHYSIQUES SI LHDHKS
NDHBHKP = 3+NHDQLNTP+NHDPASTP
!     TENDANCES PHYSIQUES SI LHDMCI
NDHBMCP = 0
!     TENDANCES PHYSIQUES SI LHDENT
NDHBENP = 6

!     VARIABLES AU SOL

IF (LECMWF) THEN

!      number of variables in individual classes

  NDHVTLS = 3
  NDHVTSS = 6
  NDHVTTS = 4
  NDHVTIS = 4
  NDHVSSS = 1
  NDHVIIS = 1
  NDHVWLS = 2

!      total number of soil variables

  NDHVS =  NDHVTLS*NTILES+(&
   & NDHVTSS+NDHVSSS)*NCSNEC+(&
   & NDHVTTS+NDHVWLS)*NCSS+&
   & NDHVTIS*NCSI+NDHVIIS  
  
!      number of free-style variables
! GHG fields included in DDH if present (if NGHG/=0)

  NDHVFS = 29 + NGHG + NACTAERO

ELSE

  NDHVS = 6
  NDHVFS = 12

ENDIF

!     FLUX/TENDANCES DYNAMIQUES AU SOL

NDHFSD = 0

!     FLUX TENDANCES PHYSIQUES AU SOL

IF (LECMWF) THEN

!      number of fluxes in individual classes
  NDHFTLS = 9
  NDHFTSS = 9
  NDHFTTS = 11
  NDHFTIS = 9
  NDHFSSS = 4
  NDHFIIS = 3
  NDHFWLS = 8

!      total number of soil fluxes

  NDHFSP =  NDHFTLS*NTILES+(&
   & NDHFTSS+NDHFSSS)*NCSNEC+(&
   & NDHFTTS+NDHFWLS)*NCSS+&
   & NDHFTIS*NCSI+NDHFIIS  
  
!      number of free-style fluxes

   NDHFFS = 35
!  add NACTAERO*7 flux variables to get DDH of aerosol physics
  IF (NACTAERO > 0) NDHFFS = NDHFFS + NACTAERO * 7

!-- add 9 lake variables if LEFLAKE is on
  IF (LEFLAKE) THEN
     IF (YSP_SL%YLMLT%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLMLD%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLBLT%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLTLT%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLSHF%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLICT%LSET) NDHFFS = NDHFFS + 1
     IF (YSP_SL%YLICD%LSET) NDHFFS = NDHFFS + 1
     IF (YSD_VF%YCLK%LSET) NDHFFS = NDHFFS + 1
     IF (YSD_VF%YDL%LSET) NDHFFS = NDHFFS + 1
  ENDIF

  IF (YSD_VF%YCO2O%LSET) NDHFFS = NDHFFS + 1
  IF (YSD_VF%YCO2B%LSET) NDHFFS = NDHFFS + 1
  IF (YSD_VF%YCO2A%LSET) NDHFFS = NDHFFS + 1
  IF (YSD_VF%YCO2F%LSET) NDHFFS = NDHFFS + 1
  IF (YSD_VF%YCH4AG%LSET) NDHFFS = NDHFFS + 1
  IF (YSD_VF%YCH4F%LSET) NDHFFS = NDHFFS + 1

ELSE

  NDHFSP = 17
  NDHFFS = 5

ENDIF

!     ------------------------------------------------------------------
NDHFHKD = NDHAHKD + NDHBHKD
NDHFHKP = NDHAHKP + NDHBHKP
NDHFENP = NDHAENP + NDHBENP
NDHFMCD = NDHAMCD + NDHBMCD
NDHFMCP = NDHAMCP + NDHBMCP
NDHFEND = NDHAEND + NDHBEND
!     ------------------------------------------------------------------
NDHDDX = 1

!     BANDES DE LATITUDES
IF ( LHDZON ) THEN
  IF ( NDHKD == 0 ) THEN
    WRITE (NULERR,*)&
     & ' ERREUR DDH  CONTRADICTION BANDES ZONALES ',&
     & ' NB DE BANDES NUL. MOYENNES ZONALES SUPPRIMEES '  
    WRITE (NULERR,*) ' EXPERIENCE TERMINEE '
    CALL ABOR1('ERREUR DDH  CONTRADICTION BANDES ZONALES')
  ELSE
    NDHDDX = NDHDDX*NDHKD
  ENDIF
ENDIF

!     DOMAINES LIMITES ET POINTS
!     LES DIMENSIONS PRATIQUES SONT DEDUITES DE LA LECTURE DU
!     TABLEAU BDEDDH : NB DE MASQUES, NB DE DOMAINES PAR MASQUE, ETC...

IF ( LHDDOP ) THEN
  NDHNOM = 0
  NDHNPU = 0
  DO JBOX = 1, JPDHNOX
    IF ( BDEDDH(1,JBOX) /= 0.0_JPRB ) THEN
      NDHNOM = NDHNOM + 1
      IMASK = NINT( BDEDDH(2,JBOX) )
      NDHNPU = MAX( NDHNPU,IMASK )
    ENDIF
  ENDDO

  IF ( NDHNPU > JPDHXPU ) THEN
    WRITE (NULERR,*) ' ERREUR DDH PAS ASSEZ DE PLANS ',&
     & ' PARAMETER JPDHXPU = ',JPDHXPU,' DEMANDE NAMELIST = ',&
     & NDHNPU  
    CALL ABOR1('ERREUR DDH PAS ASSEZ DE PLANS')
  ENDIF

  NDHBPX = 0
  DO JMASK = 1, NDHNPU
    NDHBPU( JMASK ) = 0
    DO JBOX = 1, NDHNOM
      IMASK = NINT( BDEDDH(2,JBOX) )
      IF ( IMASK == JMASK ) THEN
        NDHBPU(JMASK) = NDHBPU(JMASK) + 1
      ENDIF
    ENDDO
    NDHBPX = MAX( NDHBPX,NDHBPU(JMASK) )
    IF ( NDHBPU(JMASK) == 0 ) THEN
      WRITE (NULERR,*) ' ERREUR DDH PLAN VIDE ',JMASK
      CALL ABOR1(' ERREUR DDH PLAN VIDE ')
    ENDIF
    NDHDDX = NDHDDX*( NDHBPU(JMASK)+1 )
  ENDDO

ENDIF

!     DIMENSIONS LIEES AU NOMBRE DE CHAMPS

NDHCS = 0
NDHTHK = 0
NDHTMC = 0
NDHTEN = 0
NDHFVD = 0
NDHFVP = 0
NDHCV = 0
NDHVV = 0
NDHAVD = 0
NDHBVD = 0
NDHAVP = 0
NDHBVP = 0
IF ( .NOT.LHDHKS ) THEN
  NDHVS = 0
  NDHFSD = 0
  NDHFSP = 0
ENDIF
IF ( LHDHKS ) THEN
  NDHCS = NDHVS + NDHVFS + NDHFSD + NDHFSP + NDHFFS
  NDHTHK = NDHVHK + NDHFHKD + NDHFHKP
  NDHCV = NDHCV + NDHTHK
  NDHVV = NDHVV + NDHVHK
  NDHAVD = NDHAVD + NDHAHKD
  NDHBVD = NDHBVD + NDHBHKD
  NDHAVP = NDHAVP + NDHAHKP
  NDHBVP = NDHBVP + NDHBHKP
  NDHFVD = NDHFVD + NDHFHKD
  NDHFVP = NDHFVP + NDHFHKP
ENDIF

IF ( LHDMCI ) THEN
  NDHTMC = NDHVMC + NDHFMCD + NDHFMCP
  NDHCV = NDHCV + NDHTMC
  NDHVV = NDHVV + NDHVMC
  NDHAVD = NDHAVD + NDHAMCD
  NDHBVD = NDHBVD + NDHBMCD
  NDHAVP = NDHAVP + NDHAMCP
  NDHBVP = NDHBVP + NDHBMCP
  NDHFVD = NDHFVD + NDHFMCD
  NDHFVP = NDHFVP + NDHFMCP
ENDIF
IF ( LHDENT ) THEN
  NDHTEN = NDHVEN + NDHFEND + NDHFENP
  NDHCV = NDHCV + NDHTEN
  NDHVV = NDHVV + NDHVEN
  NDHAVD = NDHAVD + NDHAEND
  NDHBVD = NDHBVD + NDHBEND
  NDHAVP = NDHAVP + NDHAENP
  NDHBVP = NDHBVP + NDHBENP
  NDHFVD = NDHFVD + NDHFEND
  NDHFVP = NDHFVP + NDHFENP
ENDIF
!     DIMENSIONS LIEES AU NOMBRE DE CHAMPS
!      INSUR = 1 - MOD(NDHCV,2)
!      NDHCVSU = NDHCV+INSUR
NDHCVSU = MAX(NDHCV,1)
NDHCSSU = MAX(NDHCS,1)
IF(LAGPHY) THEN
  NDHCVSUN= MAX(NDHVV + NDHFVD,1)
  NDHCVSUL= MAX(NDHCVSU-(NDHVV + NDHFVD),1)
ELSE
  NDHCVSUN= NDHCVSU
  NDHCVSUL = 1
ENDIF


NFIELDSMAX=NDHCVSU


!     ------------------------------------------------------------------
!**   4. CONTROLE DE COHERENCE DES OPTIONS LOGIQUES

!      INDICATEURS LOGIQUES GLOBAUX:
!         LSDDH: INDICATEUR DE DIAGNOSTICS DEMANDES
!         LHDPR: INDICATEUR DE SORTIES LISTING  DEMANDEES

LSDDH = LHDGLB .OR. LHDZON .OR. LHDDOP
LHDPR = LHDPRG .OR. LHDPRZ .OR. LHDPRD

!     CONTROLE DE COHERENCE

LLERR = .FALSE.
IF ( LSDDH ) THEN
!         DES DIAGNOSTICS SONT DEMANDES
  IF ( .NOT.LHDHKS.AND..NOT.LHDMCI.AND..NOT.LHDENT ) THEN
    WRITE(NULERR,*) ' ERREUR DDH *** '
    WRITE(NULERR,*) ' DIAGNOSTICS DEMANDES MAIS AUCUN',&
     & ' CHAMP SCIENTIFIQUE VALIDE'  
    LLERR=.TRUE.
  ENDIF
ENDIF
!      COHERENCE DOMAINE/SORTIE
LLCUMUL=            (LHDGLB.EQV.(LHDPRG.OR.LHDEFG))
LLCUMUL=LLCUMUL.AND.(LHDZON.EQV.(LHDPRZ.OR.LHDEFZ))
LLCUMUL=LLCUMUL.AND.(LHDDOP.EQV.(LHDPRD.OR.LHDEFD))
IF ( .NOT.LLCUMUL ) THEN
  WRITE(NULERR,*) ' ERREUR DDH *** '
  WRITE(NULERR,*) ' INCOHERENCE ENTRE DOMAINE(S) ET',&
   & ' SORTIE(S) ASSOCIEE(S) DEMANDES'  
  LLERR=.TRUE.
ENDIF

IF (.NOT. LFLEXDIA .AND. LDDH_OMP) THEN
  WRITE(NULERR,*) ' ERREUR DDH *** '
  WRITE(NULERR,*) ' OPEN_MP OPTION ACTIVE FOR DDH FLEXIBLE ONLY'
  LLERR = .TRUE.
ENDIF

IF ( LLERR ) THEN
!        UNE INCOHERENCE AU MOINS DETECTEE.
  WRITE (NULERR,*) ' EXPERIENCE TERMINEE '
  CALL ABOR1('SUNDDH - UNE INCOHERENCE AU MOINS DETECTEE')
ENDIF

! activates flexible data flow for ddh in arome
IF (LSDDH .AND. NPHY==JPHYARO) THEN
  LFLEXDIA=.TRUE.
  NDHCV=180 ! hopefully it is enough 
  NDHCVSU=NDHCV
  NDHCVSUN= NDHCVSU
  IF (LDDH_OMP) THEN
    NFIELDSMAX=NDHCVSU
  ENDIF
ENDIF

IF (LSDDH .AND. NPHY==JPHYMF .AND. LFLEXDIA) THEN
  !
  ! NDHCV IS THE NUMBER OF INITIAL VARIABLES + NUMBER OF TENDENCIES/FLUXES.
  ! NDHVV IS THE NUMBER OF FINAL   VARIABLES.
  NDHCV=MAX(NDHCV,600)
  NDHVV=MAX(NDHVV,60)
  NDHCVSU=NDHCV
  NDHCVSUN= NDHCVSU
  IF (LDDH_OMP) THEN
    NFIELDSMAX=NDHCVSU
  ENDIF
  WRITE (NULOUT,*) ' DDH FORCE DIMENSIONNING IN CASE LFLEXDIA=T : NDHCV=',NDHCV
  WRITE (NULOUT,*) '                                              NDHVV=',NDHVV
ENDIF


IF(LFLEXDIA) CALL SUDDHFLEX(NPROMA,NFLEVG)
!     ------------------------------------------------------------------
!**   4. DIMENSIONS LIEES AUX TENDANCES DYNAMIQUES SI,  HD, et SL

LRDDHDYN= LRSIDDH .OR. LRHDDDH .OR. LRSLDDH

IPT=1
MSIDDH_U1 = IPT
IPT=IPT+1
MSIDDH_V1 = IPT
IPT=IPT+1
IF (NFTHER > 0) THEN
  MSIDDH_T1 = IPT
  IPT=IPT+1
  IF (LNHDYN) THEN
    MSIDDH_PD1=IPT
    IPT=IPT+1
    MSIDDH_VD1=IPT
    IPT=IPT+1
  ENDIF
ENDIF
MSIDDH_U0 = IPT
IPT=IPT+1
MSIDDH_V0 = IPT
IPT=IPT+1
IF (NFTHER > 0) THEN
  MSIDDH_T0 = IPT
  IPT=IPT+1
  IF (LNHDYN) THEN
    MSIDDH_PD0=IPT
    IPT=IPT+1
    MSIDDH_VD0=IPT
    IPT=IPT+1
  ENDIF
ENDIF
MSIDDH_U9 = IPT
IPT=IPT+1
MSIDDH_V9 = IPT
IPT=IPT+1
IF (NFTHER > 0) THEN
  MSIDDH_T9 = IPT
  IPT=IPT+1
  IF (LNHDYN) THEN
    MSIDDH_PD9=IPT
    IPT=IPT+1
    MSIDDH_VD9=IPT
    IPT=IPT+1
  ENDIF
ENDIF

NDIMSIGMV=IPT-1

IPT=1
MHDDDH_U = IPT
IPT=IPT+1
MHDDDH_V = IPT
IPT=IPT+1
IF (NFTHER > 0) THEN
  MHDDDH_T = IPT
  IPT=IPT+1
  IF (LNHDYN) THEN
    MHDDDH_PD = IPT
    IPT=IPT+1
    MHDDDH_VD = IPT
    IPT=IPT+1
  ENDIF
  IF (YRDYNA%LNHX) THEN
    MHDDDH_NHX = IPT
    IPT=IPT+1
  ENDIF
ENDIF

IPT=1
IF (YQ%LSP) MHDDDH_Q = IPT
NDIMHDGFL =  NUMSPFLDS

IPT=1
MSLDDH_U = IPT
IPT=IPT+1
MSLDDH_V = IPT
IPT=IPT+1
IF (NFTHER > 0) THEN
  MSLDDH_T = IPT
  IPT=IPT+1
  IF (LNHDYN) THEN
    MSLDDH_PD = IPT
    IPT=IPT+1
    MSLDDH_VD = IPT
    IPT=IPT+1
  ENDIF
  IF (YRDYNA%LNHX) THEN
    MSLDDH_NHX = IPT
    IPT=IPT+1
  ENDIF
ENDIF

IF (LRHDDDH.OR.LRSIDDH) THEN
  NSPEC2VDDH=MAX(NSPEC2V,NSPEC2VF)
ELSE
  NSPEC2VDDH=0
ENDIF
!     ------------------------------------------------------------------
!**   5. IMPRESSIONS

!MH       WRITE (NULOUT,*) ' NAMELIST NAMDDH'
!MH       WRITE (NULOUT,*) ' ---------------'
!MH       WRITE (NULOUT,NAMDDH)
WRITE (NULOUT,*) ' DOMAINE GLOBAL = ',LHDGLB,' BANDES ZONALES =',LHDZON
WRITE (NULOUT,*) ' DOMAINES LIMITES OU POINTS = ',LHDDOP
WRITE (NULOUT,*) ' MASSES - ENERGIES - SOL = ',LHDHKS
WRITE (NULOUT,*) ' ENTROPIE = ',LHDENT
WRITE (NULOUT,*) ' MOMENT CINETIQUE = ',LHDMCI
WRITE (NULOUT,*) ' IMPRESSIONS GLOBE = ',LHDPRG
WRITE (NULOUT,*) ' IMPRESSIONS 1 BANDE ZONALE = ',LHDPRZ,&
 & ' BANDE NDHZPR = ',NDHZPR  
WRITE (NULOUT,*) ' IMPRESSIONS DOMAINES ET POINTS = ',LHDPRD
WRITE (NULOUT,*) ' ECRITURE GLOBE = ',LHDEFG,' BANDES ZONALES =',LHDEFZ
WRITE (NULOUT,*) ' ECRITURE DOMAINES LIMITES ET POINTS ',LHDEFD
WRITE (NULOUT,*) ' LHDQLN = ',LHDQLN,' LHDPAS = ',LHDPAS

!      CIF LSDDH IF
!      ON N'IMPRIME LE DETAIL DES OPTIONS
!      QUE SI LES DIAGNOSTICS SONT ACTIVES.

IF ( LSDDH ) THEN

  IF ( LHDLIST )WRITE (NULOUT,*) ' IMPRESSIONS ETENDUES '
  IF ( .NOT.LHDLIST )WRITE (NULOUT,*) ' IMPRESSIONS MINIMALES '
  WRITE (NULOUT,*) ' DIMENSIONS POUR DDH '
  WRITE (NULOUT,*) ' ------------------- '
  WRITE (NULOUT,*) ' NB DE PLANS-MASQUES MAXI = ',JPDHXPU,&
   & ' DONT ',NDHNPU,' UTILISES '  
  WRITE (NULOUT,*) ' NB DE DOMAINES LIMITES MAXI = ',JPDHNOX,&
   & ' DONT ',NDHNOM,' UTILISES '  
  WRITE (NULOUT,*) ' NB DE BANDES DE LATITUDE ',NDHKD
  DO JMASK = 1, NDHNPU
    WRITE (NULOUT,*) ' NB DE DOMAINES LIMITES DANS LE PLAN ',&
     & JMASK,' = ',NDHBPU(JMASK)  
  ENDDO
  WRITE (NULOUT,*) ' NB MAXI DE DOMAINES LIMITES PAR PLAN =',NDHBPX
  WRITE (NULOUT,*) ' NB DE DOMAINES INTERNES POSSIBLES =',NDHDDX
  IF ( LHDDOP ) THEN
    WRITE (NULOUT,*) ' CARACTERISTIQUES DES DOMAINES LIMITES '
    WRITE (NULOUT,*) ' ------------------------------------- '
    DO JBOX = 1, JPDHNOX
      WRITE (NULOUT,5001) (BDEDDH(JJ,JBOX),JJ=1,10)
      5001 FORMAT ( T5,10(F8.2,1X) )
    ENDDO
  ENDIF
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' NOMBRES DE CHAMPS PAR CATEGORIES '
  WRITE (NULOUT,*) ' -------------------------------- '
  WRITE (NULOUT,*) '        VARIABLES  FLUX DYN  FLUX PHY'
  WRITE (NULOUT,*) ' SOL    ',NDHVS,NDHFSD,NDHFSP
  WRITE (NULOUT,*) ' LHDHKS ',NDHVHK,NDHFHKD,' DONT ',NDHAHKD,&
   & NDHBHKD,' ET ',NDHFHKP,' DONT ',NDHAHKP,NDHBHKP  
  WRITE (NULOUT,*) ' LHDMCI ',NDHVMC,NDHFMCD,' DONT ',NDHAMCD,&
   & NDHBMCD,' ET ',NDHFMCP,' DONT ',NDHAMCP,NDHBMCP  
  WRITE (NULOUT,*) ' LHDENT ',NDHVEN,NDHFEND,' DONT ',NDHAEND,&
   & NDHBEND,' ET ',NDHFENP,' DONT ',NDHAENP,NDHBENP  
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' NB TOTAL DE CHAMPS VERTICAUX NDHCV = ',NDHCV
  WRITE (NULOUT,*) ' DONT NDHVV = ',NDHVV,' VARIABLES '
  WRITE (NULOUT,*) '      NDHFVD = ',NDHFVD,' FLUX DYNAMIQUES ',&
   & ' DONT ',NDHAVD,' TENDANCES ET ',NDHBVD,' FLUX '  
  WRITE (NULOUT,*) '      NDHFVP = ',NDHFVP,' FLUX PHYSIQUES ',&
   & ' DONT ',NDHAVP,' FLUX ET ',NDHBVP,' TENDANCES '  
  WRITE (NULOUT,*) ' (VERIF) SOIT BIEN ',NDHVV+NDHFVD+NDHFVP,' CHAMPS'
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' NB TOTAL DE CHAMPS AU SOL NDHCS = ',NDHCS
  WRITE (NULOUT,*) ' DONT NDHVS = ',NDHVS,' VARIABLES '
  WRITE (NULOUT,*) '      NDHFSD = ',NDHFSD,' FLUX DYNAMIQUES'
  WRITE (NULOUT,*) '      NDHFSP = ',NDHFSP,' FLUX PHYSIQUES'
  WRITE (NULOUT,*) '      NDHFFS = ',NDHFFS,' FREE-STYLE FLUXES'
  WRITE (NULOUT,*) '      NDHVFS = ',NDHVFS,' FREE-STYLE VARIABLES'
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) '        NB TOTAUX PAR TYPE SCIENTIFIQUE'
  WRITE (NULOUT,*) ' (SOL    ',NDHCS,')'
  WRITE (NULOUT,*) ' LHDHKS ',NDHTHK
  WRITE (NULOUT,*) ' LHDMCI ',NDHTMC
  WRITE (NULOUT,*) ' LHDENT ',NDHTEN
  WRITE (NULOUT,*) ' '
  WRITE (NULOUT,*) ' DIMENSIONS POUR TABLEAUX LOCAUX'
  WRITE (NULOUT,*) ' NDHCVSU = ',NDHCVSU
  WRITE (NULOUT,*) ' NDHCSSU = ',NDHCSSU
  WRITE (NULOUT,*) ' NDHCVSUN = ',NDHCVSUN
  WRITE (NULOUT,*) ' NDHCVSUL = ',NDHCVSUL
  WRITE (NULOUT,*) ' '

!      CIF LSDDH ENDIF

ENDIF

WRITE (NULOUT,*) ' '
WRITE (NULOUT,*) ' LOGIQUE GLOBALE LSDDH = ',LSDDH
WRITE (NULOUT,*) ' DDH FLEXIBLES LFLEXDIA = ',LFLEXDIA
WRITE (NULOUT,*) ' LOGIQUE IMPRESSION LHDPR = ',LHDPR
WRITE (NULOUT,*) ' NSPEC2VDDH = ',NSPEC2VDDH
WRITE (NULOUT,'('' LHDLFA = '',L2,'' CFPATHDDH = '',A)') LHDLFA, CFPATHDDH

WRITE (NULOUT,*) ' '
WRITE (NULOUT,*) ' ---------------------------------- DDH -'
WRITE (NULOUT,*) ' '

IF (LECMWF) THEN
  CALL SUCDDH(YDSURF,YDMODEL)
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUNDDH',1,ZHOOK_HANDLE)
END SUBROUTINE SUNDDH


