SUBROUTINE WRRADCOEF(YDGEOMETRY,YDRCOEF,KSTA,KEND,KJUMP,KFIELDSS, &
& PCOR  , PRAB3C, PRAB3N, PRAB4C, PRAB4N, PRAB6C, PRAB6N, &
& PRAT1C, PRAT1N, PRAT2C, PRAT2N, PRAT3C, PRAT3N, PRAT4C, &
& PRAT4N, PRAT5C, PRAT5N, PAC)

!**** *WRRADCOEF* - Write out the constants for solar and thermal radiation
!                   MF physics-dynamics interface, level 1

!     Purpose.  Write out the constants for solar and thermal radiation
!     --------  (grid-point transmission coefficients) from work file.

!**   Interface.
!     ----------
!        *CALL* *WRRADCOEF(...)*

!        Explicit arguments :
!        --------------------

!          KSTA          : start adress               (input)
!          KEND          : end adress                 (input)
!          KJUMP         : offset into gp array       (input)
!          KFIELDSS      : number of fields           (input)
!          PRADTC and PAC: transmission coefficients  (input, inout for PAC)

!        Implicit arguments :  None
!        --------------------

!     Method.
!     -------

!     Externals. FILLB - interface to packing routines
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        M. Janiskova

!     Modifications.
!     --------------
!        Original : 99-02-05
!        Modified :
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!        K.Yessad (Feb 2010): use YM_RADTC and PRADTC.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMRCOEF , ONLY : TRCOEF
USE MODULE_RADTC_MIX, ONLY : YM_RADTC

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TRCOEF)      ,INTENT(IN), TARGET :: YDRCOEF
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND 
INTEGER(KIND=JPIM),INTENT(IN)    :: KJUMP 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFIELDSS 
REAL(KIND=JPRB),   INTENT(IN)    :: PCOR   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB3C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB3N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB4C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB4N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB6C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAB6N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT1C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT1N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT2C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT2N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT3C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT3N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT4C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT4N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT5C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(IN)    :: PRAT5N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAC(YDGEOMETRY%YRDIMV%NFLEVG+1,YDGEOMETRY%YRDIMV%NFLEVG+1) 

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IJUMP, ISTAGPSB,&
 & ISTAGPTB, IWORDS, IWORDT, JLEV, JJ, JK, JF
TYPE(TRCOEF), POINTER :: YLRCOEF
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "fillb.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('WRRADCOEF',0,ZHOOK_HANDLE)

YLRCOEF => YDRCOEF

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & LGLOBRAD=>YLRCOEF%LGLOBRAD, NLATWR=>YLRCOEF%NLATWR, &
 & NLENGTRB=>YLRCOEF%NLENGTRB, SOLRAD=>YLRCOEF%SOLRAD, THERRAD=>YLRCOEF%THERRAD, &
 & TRMATSUM=>YLRCOEF%TRMATSUM, TRWEIGHT=>YLRCOEF%TRWEIGHT)
!     ------------------------------------------------------------------

!*       1.    WRITE OUT GRIDPOINT ARRAY.
!              --------------------------

IF (KFIELDSS /= NFLEVG*YM_RADTC%NDIM) THEN
  ! YM_RADTC%NDIM should be equal to NG3SR if LRCOEF=T.
  CALL ABOR1('WRRADCOEF 1.1: YM_RADTC%NDIM should be equal to NG3SR')
ENDIF

NLATWR=NLATWR+1

!*       1.1       CALCULATE LENGTHS

IWORDS = KEND-KSTA+1
IWORDT = NFLEVG+1

!*       1.2       WRITE OUT BUFFER

IJUMP=KJUMP-1
ISTAGPSB = 1+IJUMP*KFIELDSS
ISTAGPTB = 1

!        1.3      SOLAR RADIATION COEFFICIENTS, CORRECTION FACTOR
!                 FOR THERMAL RADIATION AND MATRIX OF THERMAL
!                 RADIATION COEFFICIENTS

! * Radiation transmission coefficients other than Curtis matrix.
DO JLEV=1,NFLEVG
  CALL FILLB (IWORDS,PRAB3C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAB4C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAB6C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAB3N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAB4N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAB6N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT1C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT2C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT3C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT4C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT5C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT1N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT2N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT3N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT4N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PRAT5N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL FILLB (IWORDS,PCOR  (KSTA,JLEV),SOLRAD(:),ISTAGPSB)
ENDDO

! * Curtis matrix for thermal radiation for clear sky.
IF (LGLOBRAD) THEN
  IF (NLATWR == NLENGTRB) THEN
    DO JJ=1,NFLEVG+1
      DO JK=1,NFLEVG+1
        PAC(JJ,JK)=TRMATSUM(JJ,JK)/TRWEIGHT(JJ,JK)
      ENDDO
    ENDDO
    DO JLEV=1,NFLEVG+1
      CALL FILLB(IWORDT,PAC(1,JLEV),THERRAD(1,:),ISTAGPTB)  
    ENDDO
  ENDIF
ELSE
  DO JLEV=1,NFLEVG+1
    CALL FILLB(IWORDT,PAC(1,JLEV),THERRAD(NLATWR,:),ISTAGPTB)  
  ENDDO
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('WRRADCOEF',1,ZHOOK_HANDLE)
END SUBROUTINE WRRADCOEF

