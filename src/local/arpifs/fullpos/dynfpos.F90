SUBROUTINE DYNFPOS(YDQTYPE,YDCLIMO,YDNAMFPINT,YDNAMFPSCI,YDAFN,KFPXFLD,YDFPGEOMETRY,YDFPWSTD,YDFPSTRUCT,KFPDOM, &
 & LDHPOS,LDEZO,YDGEOMETRY,YDGMV,YDGFL,YDSURF,YDXFU,KCUFNR,PMCUFGP,YDMODEL,CDVCONF,KPXLEV,PXLEV, &
 & PBUF,PGPP,PAUX,P2DFIELDS,P3DFIELDS,P3DPHYS,KP3FIELDS)

!****-------------------------------------------------------------------
!**** *DYNFPOS* - Fullpos grid-point space computations
!****-------------------------------------------------------------------
!     Purpose.   post-processing in grid-point space
!     --------   

!**   Interface.
!     ----------
!        *CALL* *DYNFPOS (..)

!        Explicit arguments :  
!        --------------------  
!        KFPXFLD : maximum number of fields to extract at a time
!        P2DFIELDS : alternative 2D output array to PBUF
!        P3DFIELDS : alternative 3D output array to PBUF
!        P3DPHYS : alternative 3D output array to PBUF (preallocated)

!        Implicit arguments :  None.
!        --------------------

!     Method.
!     -------

!     Externals.   See includes below.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        Ryad El Khatib *Meteo-France* 

! Modifications
! -------------
!   original 18-Jul-2012 from SCAN2M
!   R. El Khatib  04-Dec-2012 Fix bounds checking issues created by recent "cleanings"
!   G. Balsamo 14-Jan-2014 add lake prognostics SP_SL
!   R. El Khatib 13-Dec-2012 Fullpos buffers reshaping
!   T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!   R. El Khatib 27-Jul-2016 interpolations over C+I+E
!   R. El Khatib 20-Sep-2016 Split HPOS
!   R. El Khatib 20-Aug-2020 Cleaning
!-----------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT
USE YOMCT0             , ONLY : LELAM
USE YOMMP0             , ONLY : NPROC
USE EINT_MOD           , ONLY : SL_STRUCT
USE YOMGMV             , ONLY : TGMV
USE YOMGFL             , ONLY : TGFL
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE YOMXFU             , ONLY : TXFU
USE TYPE_MODEL         , ONLY : MODEL
USE TYPE_GFLFLDS       , ONLY : TYPE_IGFLFLDD, TYPE_LGFLFLDD
USE TYPE_GMVS          , ONLY : TYPE_T0
USE YOMFPGEOMETRY      , ONLY : TFPGEOMETRY, LFPOSBUF, LFPDISTRIB
USE YOMFPC             , ONLY : TNAMFPSCI, TNAMFPINT, LALLOFP
USE YOMWFPB            , ONLY : TFPWSTD
USE YOMAFN             , ONLY : TAFN
USE TYPE_FPOSBUF       , ONLY : FPOSBUF
USE TYPE_FPRQDYNS, ONLY : TYPE_FPRQDYN

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE (TYPE_FPRQDYN),  INTENT(IN) :: YDQTYPE
TYPE(FPOSBUF)     ,INTENT(IN) :: YDCLIMO
TYPE(TNAMFPINT),   INTENT(IN) :: YDNAMFPINT
TYPE (TNAMFPSCI),  INTENT(IN) :: YDNAMFPSCI
TYPE(TAFN)        ,INTENT(IN) :: YDAFN
INTEGER(KIND=JPIM),INTENT(IN) :: KFPXFLD
TYPE(TFPGEOMETRY), INTENT(IN) :: YDFPGEOMETRY
TYPE(TFPWSTD),     INTENT(IN) :: YDFPWSTD
TYPE(SL_STRUCT),   INTENT(IN) :: YDFPSTRUCT
INTEGER(KIND=JPIM),INTENT(IN) :: KFPDOM
LOGICAL           ,INTENT(IN) :: LDHPOS
LOGICAL           ,INTENT(IN) :: LDEZO
TYPE(GEOMETRY)    ,INTENT(IN) :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(IN) :: YDGMV
TYPE(TGFL)        ,INTENT(IN) :: YDGFL
TYPE(TSURF)       ,INTENT(IN) :: YDSURF
TYPE(TXFU)        ,INTENT(IN) :: YDXFU
INTEGER(KIND=JPIM),INTENT(IN) :: KCUFNR
REAL(KIND=JPRB),   INTENT(IN) :: PMCUFGP(YDGEOMETRY%YRDIM%NPROMA,KCUFNR,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(MODEL)       ,INTENT(IN) :: YDMODEL
CHARACTER(LEN=1)  ,INTENT(IN) :: CDVCONF
INTEGER(KIND=JPIM),INTENT(IN) :: KPXLEV
REAL(KIND=JPRB),   INTENT(IN) :: PXLEV(KPXLEV)
INTEGER(KIND=JPIM),INTENT(IN) :: KP3FIELDS
REAL(KIND=JPRB) ,POINTER, INTENT(OUT) :: PBUF(:,:,:)
REAL(KIND=JPRB) ,ALLOCATABLE, INTENT(INOUT), OPTIONAL :: PGPP(:,:,:)
REAL(KIND=JPRB) ,ALLOCATABLE, INTENT(INOUT), OPTIONAL :: PAUX(:,:,:)
REAL(KIND=JPRB) ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P2DFIELDS(:,:,:)
REAL(KIND=JPRB) ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P3DFIELDS(:,:,:,:)
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT), OPTIONAL :: P3DPHYS(YDFPGEOMETRY%YFPGEO%NFPROMA,KPXLEV,KP3FIELDS,YDFPGEOMETRY%YFPGEO%NFPBLOCS)


REAL(KIND=JPRB) :: ZCORE(YDGEOMETRY%YRDIM%NPROMA,YDQTYPE%NFPGT0B+YDQTYPE%NFPAUXB), ZPARITY(YDQTYPE%NFPGT0B+YDQTYPE%NFPAUXB)
REAL(KIND=JPRB) :: ZAUX(YDGEOMETRY%YRDIM%NPROMA,YDQTYPE%NFPAUXB)
REAL(KIND=JPRB) :: ZGPP(YDGEOMETRY%YRDIM%NPROMA,YDQTYPE%NFPGT1)
REAL(KIND=JPRB) :: ZWSXI, ZWDXI

REAL(KIND=JPRB), ALLOCATABLE :: ZHALO(:,:), ZBUF_DEP(:,:,:)

TYPE(TYPE_IGFLFLDD) :: YLIN_GFL
TYPE(TYPE_LGFLFLDD) :: YLGFL
TYPE(TYPE_T0)       :: YLIN_GMV

INTEGER(KIND=JPIM) :: IBL, IEND, JKGLO, IPTR, JF, IJ, JL, IFLDIN, IFIELDS
INTEGER(KIND=JPIM) :: IDUMARR(2), ICOD(YDQTYPE%NFPDYNB)

CHARACTER(LEN=1) :: CLBIP   ! config for biperiodicization
CHARACTER(LEN=35) :: CLFMT1='(1X,''ARRAY '',A10,'' ALLOCATED '',8I8)'
CHARACTER(LEN=33) :: CLFMT0='(1X,''ARRAY '',A10,'' DEALLOCATED '')'
LOGICAL :: LLINC, LLBIP(YDQTYPE%NFPDYNB)

REAL(KIND=JPRB) :: ZHOOK_HANDLE
!REAL(KIND=JPRB) :: ZHOOK_HANDLE1
!     ------------------------------------------------------------------

#include "vpos_prep.intfb.h"
#include "vpos.intfb.h"
#include "hpos_dyn.intfb.h"
#include "fphalo.intfb.h"
#include "slcomm.intfb.h"
#include "slextpol.intfb.h"
#include "eslextpol.intfb.h"
#include "fpezo2h.intfb.h"
#include "fpboyd.intfb.h"
#include "fposhor.intfb.h"
#include "fptrdtoa.intfb.h"
#include "fposhorlag.intfb.h"
#include "abor1.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('DYNFPOS',0,ZHOOK_HANDLE)
ASSOCIATE(YDFPGEO_DEP=>YDFPGEOMETRY%YFPGEO_DEP, YDFPGEO=>YDFPGEOMETRY%YFPGEO, YDFPGIND=>YDFPGEOMETRY%YFPGIND, &
 & YDFPUSERGEO=>YDFPGEOMETRY%YFPUSERGEO, LFPOSHOR=>YDFPGEOMETRY%LFPOSHOR, TFP_DYNDS=>YDAFN%TFP_DYNDS, &
 & NFPGT1=>YDQTYPE%NFPGT1, NFPAUXB=>YDQTYPE%NFPAUXB, NFPDYNB=>YDQTYPE%NFPDYNB, NFPGT0B=>YDQTYPE%NFPGT0B, &
 & YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV, YDGEM=>YDGEOMETRY%YRGEM, YGFL=>YDMODEL%YRML_GCONF%YGFL)
ASSOCIATE(NFPBOYD=>YDNAMFPSCI%NFPBOYD, RFPBSCAL=>YDNAMFPSCI%RFPBSCAL, &
 & NDIM=>YGFL%NDIM, NPROMA=>YDDIM%NPROMA, NGPBLKS=>YDDIM%NGPBLKS, NGPTOT=>YDGEM%NGPTOT, GFL=>YDGFL%GFL, GMV=>YDGMV%GMV,  &
 & GMVS=>YDGMV%GMVS, NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, &
 & NFPBLOCS=>YDFPGEO%NFPBLOCS, NFPROMA=>YDFPGEO%NFPROMA, &
 & NFPBLOCS_DEP=>YDFPGEO_DEP%NFPBLOCS, NFPROMA_DEP=>YDFPGEO_DEP%NFPROMA)

IFLDIN=NFPGT0B+NFPAUXB

IF (PRESENT(PGPP)) THEN
  IF (.NOT.ALLOCATED(PGPP)) CALL ABOR1('DYNFPOS : PGPP NOT ALLOCATED')
  IF (NFPGT0B /= SIZE(PGPP,2)) CALL ABOR1('DYNFPOS : PGPP WRONG SECOND DIMENSION')
ENDIF

IF (NFPGT1 == 0) THEN
  CALL VPOS_PREP(YDGMV,YGFL,YLIN_GFL,YLGFL,YLIN_GMV)
ENDIF

IF (LFPOSBUF(YDFPGEOMETRY)) THEN

  ALLOCATE(ZHALO(YDFPSTRUCT%NASLB1,IFLDIN))

  ZPARITY(:)=1.0_JPRB

  CALL GSTATS(1434,0)
!$OMP PARALLEL PRIVATE(JKGLO,IEND,IBL,ZCORE,ZAUX,ZGPP)
!$OMP DO SCHEDULE(DYNAMIC,1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    IF (NFPGT1 > 0) THEN
      CALL HPOS_DYN(YDQTYPE,YDNAMFPSCI,YDAFN,LFPOSHOR,YDGEOMETRY,IEND,YDGEOMETRY%YRGSGEOM(IBL), &
       & NFPGT0B,NFPAUXB,PGPP(:,:,IBL),PAUX(:,:,IBL),IFLDIN,ZPARITY,ZCORE)
    ELSE
      CALL VPOS(YDMODEL%YRCST,YDQTYPE,YDNAMFPSCI,YDAFN,LDHPOS,YDGEOMETRY%YRVAB,YDGEOMETRY,YDSURF,YDXFU,KCUFNR,PMCUFGP(:,:,IBL),YDMODEL%YRML_GCONF, &
       & YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_PHY_MF,1,IEND, &
       & JKGLO,NDIM,NDIMGMV,NDIMGMVS,CDVCONF,KPXLEV,PXLEV,YLIN_GFL,YLGFL,YLIN_GMV,&
       & GMV(:,:,:,IBL),GMVS(:,:,IBL),GFL(:,:,:,IBL),NFPGT1,NFPAUXB,ZAUX,ZGPP)
      CALL HPOS_DYN(YDQTYPE,YDNAMFPSCI,YDAFN,LFPOSHOR,YDGEOMETRY,IEND,YDGEOMETRY%YRGSGEOM(IBL), &
       & NFPGT0B,NFPAUXB,ZGPP,ZAUX,IFLDIN,ZPARITY,ZCORE)
    ENDIF
    CALL FPHALO(YDFPSTRUCT,NPROMA,IFLDIN,JKGLO,IEND,IFLDIN,ZCORE,ZHALO)
  ENDDO
!$OMP END DO
!$OMP END PARALLEL
  CALL GSTATS(1434,1)

  IF (PRESENT(PGPP)) THEN
    DEALLOCATE(PGPP)
    IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'PGPP '
  ENDIF
  IF (NFPGT1 > 0) THEN
    DEALLOCATE(PAUX)
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT0) 'PAUX   '
    ENDIF
  ENDIF

  IF (YDFPSTRUCT%NSLWIDE > 0) THEN

!*          COMMUNICATION BETWEEN PROCESSORS TO MAKE HALOS

    IF (NPROC > 1) THEN
      LLINC=.FALSE.
      CALL SLCOMM(YDFPSTRUCT,IDUMARR,IFLDIN,LLINC,0,ZHALO)  
    ENDIF

!*        POLAR OR E-ZONE EXTENSIONS

    IF (LELAM) THEN
      CALL ESLEXTPOL(YDGEOMETRY,YDFPSTRUCT,IFLDIN,IDUMARR,1,ZHALO)
    ELSE
      CALL SLEXTPOL(YDGEOMETRY%YRDIM,YDFPSTRUCT,IFLDIN,IDUMARR,1,ZPARITY,ZHALO)
    ENDIF        

  ENDIF        

!*       HORIZONTAL INTERPOLATIONS AND BIPERIODICIZATION 

  IF (LFPDISTRIB(YDFPGEOMETRY)) THEN
    ALLOCATE(ZBUF_DEP(NFPROMA_DEP,NFPDYNB,NFPBLOCS_DEP))
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT1) 'ZBUF_DEP ',SIZE(ZBUF_DEP),SHAPE(ZBUF_DEP)
    ENDIF
    CALL FPOSHOR(YDQTYPE,YDNAMFPINT,TFP_DYNDS,YDFPGEOMETRY,YDFPWSTD,YDFPSTRUCT,YDFPGEO_DEP,IFLDIN,ZHALO,NFPDYNB,ZBUF_DEP)
    DEALLOCATE(ZHALO)
    IF (PRESENT(P2DFIELDS)) THEN
      ALLOCATE(P2DFIELDS(NFPROMA,NFPDYNB,NFPBLOCS))
      PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P2DFIELDS
    ELSEIF (PRESENT(P3DFIELDS)) THEN
      IFIELDS=NFPDYNB/KPXLEV
      IF (NFPDYNB /= KPXLEV*IFIELDS) CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*IFIELDS')
      ALLOCATE(P3DFIELDS(NFPROMA,KPXLEV,IFIELDS,NFPBLOCS))
      PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P3DFIELDS
    ELSEIF (PRESENT(P3DPHYS)) THEN
      IF (NFPDYNB /= KPXLEV*KP3FIELDS) THEN
        CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*KP3FIELDS')
      ELSE
        PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P3DPHYS
      ENDIF
    ELSE
      ALLOCATE(PBUF(NFPROMA,NFPDYNB,NFPBLOCS))
    ENDIF
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT1) 'PBUF ',SIZE(PBUF),SHAPE(PBUF)
    ENDIF
    IF (LDEZO .AND. NFPBOYD /= 0) THEN
      CALL FPBOYD(RFPBSCAL,KFPXFLD,YDFPGEO_DEP,YDFPGIND,YDFPGEO,YDFPUSERGEO,ZBUF_DEP,PBUF)
    ELSE
      CALL FPTRDTOA(KFPXFLD,YDFPGEO_DEP,YDFPGIND,YDFPGEO,NFPDYNB,ZBUF_DEP,PBUF)
    ENDIF
    DEALLOCATE(ZBUF_DEP)
  ELSE
    IF (PRESENT(P2DFIELDS)) THEN
      ALLOCATE(P2DFIELDS(NFPROMA,NFPDYNB,NFPBLOCS))
      PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P2DFIELDS
    ELSEIF (PRESENT(P3DFIELDS)) THEN
      IFIELDS=NFPDYNB/KPXLEV
      IF (NFPDYNB /= KPXLEV*IFIELDS) CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*IFIELDS')
      ALLOCATE(P3DFIELDS(NFPROMA,KPXLEV,IFIELDS,NFPBLOCS))
      PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P3DFIELDS
    ELSEIF (PRESENT(P3DPHYS)) THEN
      IF (NFPDYNB /= KPXLEV*KP3FIELDS) THEN
        CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*KP3FIELDS')
      ELSE
        PBUF(1:NFPROMA,1:NFPDYNB,1:NFPBLOCS) => P3DPHYS
      ENDIF
    ELSE
      ALLOCATE(PBUF(NFPROMA,NFPDYNB,NFPBLOCS))
    ENDIF
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT1) 'PBUF',SIZE(PBUF),SHAPE(PBUF)
    ENDIF
    CALL FPOSHOR(YDQTYPE,YDNAMFPINT,TFP_DYNDS,YDFPGEOMETRY,YDFPWSTD,YDFPSTRUCT,YDFPGEO_DEP,IFLDIN,ZHALO,NFPDYNB,PBUF)
    DEALLOCATE(ZHALO)
  ENDIF


ELSE

  IF (PRESENT(P2DFIELDS)) THEN
    ALLOCATE(P2DFIELDS(NPROMA,NFPDYNB,NGPBLKS))
    PBUF(1:NPROMA,1:NFPDYNB,1:NGPBLKS) => P2DFIELDS
  ELSEIF (PRESENT(P3DFIELDS)) THEN
    IFIELDS=NFPDYNB/KPXLEV
    IF (NFPDYNB /= KPXLEV*IFIELDS) CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*IFIELDS')
    ALLOCATE(P3DFIELDS(NPROMA,KPXLEV,IFIELDS,NGPBLKS))
    PBUF(1:NPROMA,1:NFPDYNB,1:NGPBLKS) => P3DFIELDS
  ELSEIF (PRESENT(P3DPHYS)) THEN
    IF (NFPDYNB /= KPXLEV*KP3FIELDS) THEN
      CALL ABOR1('DYNFPOS : NFPDYNB /= KPXLEV*KP3FIELDS')
    ELSE
      PBUF(1:NPROMA,1:NFPDYNB,1:NGPBLKS) => P3DPHYS
    ENDIF
  ELSE
    ALLOCATE(PBUF(NPROMA,NFPDYNB,NGPBLKS))
  ENDIF
  IF (LALLOFP) THEN
    WRITE(NULOUT,CLFMT1) 'PBUF',SIZE(PBUF),SHAPE(PBUF)
  ENDIF
  CALL GSTATS(1434,0)
!$OMP PARALLEL PRIVATE(JKGLO,IEND,IBL,ZAUX,ZGPP)
!$OMP DO SCHEDULE(DYNAMIC,1)
  DO JKGLO=1,NGPTOT,NPROMA
    IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
    IBL=(JKGLO-1)/NPROMA+1
    IF (NFPGT1 > 0) THEN
      CALL HPOS_DYN(YDQTYPE,YDNAMFPSCI,YDAFN,LFPOSHOR,YDGEOMETRY,IEND,YDGEOMETRY%YRGSGEOM(IBL), &
       & NFPGT0B,NFPAUXB,PGPP(:,:,IBL),PAUX(:,:,IBL),IFLDIN,ZPARITY,PBUF(:,:,IBL))
    ELSE
      CALL VPOS(YDMODEL%YRCST,YDQTYPE,YDNAMFPSCI,YDAFN,LDHPOS,YDGEOMETRY%YRVAB,YDGEOMETRY,YDSURF,YDXFU,KCUFNR,PMCUFGP(:,:,IBL),YDMODEL%YRML_GCONF, &
       & YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_PHY_MF,1,IEND, &
       & JKGLO,NDIM,NDIMGMV,NDIMGMVS,CDVCONF,KPXLEV,PXLEV,YLIN_GFL,YLGFL,YLIN_GMV,&
       & GMV(:,:,:,IBL),GMVS(:,:,IBL),GFL(:,:,:,IBL),NFPGT1,NFPAUXB,ZAUX,ZGPP)
      CALL HPOS_DYN(YDQTYPE,YDNAMFPSCI,YDAFN,LFPOSHOR,YDGEOMETRY,IEND,YDGEOMETRY%YRGSGEOM(IBL), &
       & NFPGT0B,NFPAUXB,ZGPP,ZAUX,IFLDIN,ZPARITY,PBUF(:,:,IBL))
    ENDIF
  ENDDO
!$OMP END DO
!$OMP END PARALLEL
  CALL GSTATS(1434,1)

  IF (PRESENT(PGPP)) THEN
    DEALLOCATE(PGPP)
    IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'PGPP '
  ENDIF
  IF (NFPGT1 > 0) THEN
    DEALLOCATE(PAUX)
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT0) 'PAUX   '
    ENDIF
  ENDIF

ENDIF

IF (LFPOSHOR) THEN
  CALL FPOSHORLAG(YDQTYPE,YDNAMFPSCI,YDAFN,YDFPGEO,NFPDYNB,PBUF)
ENDIF

IF (LFPOSBUF(YDFPGEOMETRY)) THEN
  IF (LDEZO.AND. NFPBOYD == 0) THEN
    CLBIP='B'
    LLBIP(:)=.TRUE.
    IPTR=0
    DO JF=1,YDQTYPE%NFPOSDYN
      IJ=YDQTYPE%NFPTRDYN(JF)
      DO JL=1,YDQTYPE%ILEV(IJ)
        IPTR=IPTR+1
        ICOD(IPTR)=IJ
      ENDDO
    ENDDO
    IF (IPTR /= NFPDYNB) CALL ABOR1('DYNFPOS : INTERNAL ERROR IN IPTR')
    ZWSXI=HUGE(1._JPRB)
    ZWDXI=HUGE(1._JPRB)
    CALL FPEZO2H(YDQTYPE,NFPDYNB,ICOD,LLBIP,YDCLIMO,ZWSXI,ZWDXI,YDNAMFPSCI,YDAFN,KFPXFLD,YDFPGEO,YDMODEL%YRML_PHY_EC%YREPHY, &
     & YDMODEL%YRML_PHY_MF,YDFPUSERGEO,CLBIP,LFPOSHOR,PBUF)
  ENDIF
ENDIF
!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('DYNFPOS',1,ZHOOK_HANDLE)
END SUBROUTINE DYNFPOS
