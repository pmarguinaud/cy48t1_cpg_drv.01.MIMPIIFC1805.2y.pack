SUBROUTINE ENDVPOS(YDCST, KLMOD,YDQTYPE,YDAUX,YDCLIMO,YDNAMFPSCI,YDAFN,YDFPGEO,YDFPVAB,YDGEOMETRY,YDML_GCONF,YDML_PHY_MF,LDZ0H,CDCONF, &
 & KPXLEV,PXLEV,PABUF,KCUFNR,KAPTR,PGP,PFP)

!**** *ENDVPOS*  - END OF VERTICAL POST-PROCESSING

!     PURPOSE.
!     --------
!        TRANSFER OR COMPUTE POST-PROCESSED FIELDS ON HEIGT LEVELS OR ETA
!        LEVELS FROM THE HORIZONTALLY INTERPOLATED MODEL FIELDS

!**   INTERFACE.
!     ----------
!       *CALL* *ENDVPOS*

!        EXPLICIT ARGUMENTS
!        --------------------
!          KLMOD : model/post-processing dynamic primitive variable indicator for each fullpos field :
!                  2 : upper air primitive dynamic variable
!                  1 : surface primitive dynamic variable
!                  0 : not a primitive variable
!           KPXLEV : number of post-processing levels
!           PXLEV  : pp levels
!           KAPTR  : input fields pointers
!           CDCONF    : configuration of work

!        IMPLICIT ARGUMENTS
!        --------------------

!     METHOD.
!     -------
!        SEE DOCUMENTATION
!        pre-processed fields are the horizontally interpolated model fields

!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     AUTHOR.
!     -------
!      RYAD EL KHATIB *METEO-FRANCE*
!      ORIGINAL : 94-04-08

!     MODIFICATIONS.
!     --------------
!      R. El Khatib : 01-05-04 HUn, HUx
!      R. El Khatib : 01-08-07 Pruning options
!      R. El Khatib : 02-21-20 Fullpos B-level distribution + remove IO scheme
!      R. El Khatib : 03-08-13 gfl
!      R. El Khatib : 03-04-17 Fullpos improvemnts
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!      R. Engelen      15-Mar-2005 Greenhouse Gases
!      A. Untch      15-Mar-2005 Aerosols
!      Y. Bouteloup      28-Jan-2005 Introduction of precipitation
!      G. Hello          28-Jan-2005 Snow, graupel and rain too
!      K. Yessad (Aug 2009): rewrite vertical interpolator in post-processing.
!      Y. Bouteloup  : 14-07-2009 Add radiative cloud water and ice (YIRAD and YLRAD)
!      R. El Khatib      09-Feb-2010 OMP parallelisation
!      R. El Khatib 13-Dec-2012 Fullpos buffers reshaping
!      R. El Khatib 15-Mar-2013 Enable CUF post-processing in NFPOS=2
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      O. Marsden: June 2015 CY42 YRGMV, YRGFL, YRSURF, YRGMV5, and YRGFL5 are now passed by argument
!      R. El Khatib 07-Apr-2015 Bugfix to allow the computation of certain fields without climatology nor interpolation)
!      R. El Khatib 28-Jul-2016 Recode LWIDER_DOM
!      Y. Bouteloup: 11-Jan-2017 Add deep convective variables
!     ------------------------------------------------------------------

USE YOMVERT  , ONLY : TVAB
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE YOMFPGEO, ONLY : TFPGEO
USE TYPE_FPOSBUF, ONLY : FPOSBUF
USE YOMFPC   , ONLY : TNAMFPSCI
USE TYPE_FPRQDYNS, ONLY : TYPE_FPRQDYN
USE YOMAFN, ONLY : TAFN
USE TYPE_GFLFLDS, ONLY: TYPE_IGFLFLD
USE YOMCST   , ONLY : TCST
USE YOMSTA   , ONLY : RDTDZ1
USE PARFPOS  , ONLY : JPOSDYN
USE YOMFP4L, ONLY : IFPSEARCH

!     ------------------------------------------------------------------
IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KLMOD(JPOSDYN)
TYPE (TYPE_FPRQDYN),  INTENT(IN) :: YDQTYPE
TYPE (FPOSBUF),  INTENT(IN) :: YDAUX
TYPE (FPOSBUF),  INTENT(IN) :: YDCLIMO
TYPE (TNAMFPSCI),  INTENT(IN) :: YDNAMFPSCI
TYPE (TAFN),  INTENT(IN) :: YDAFN
TYPE (TFPGEO),  INTENT(IN) :: YDFPGEO
TYPE(TVAB)        ,INTENT(IN)    :: YDFPVAB
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(IN) :: YDML_GCONF
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN) :: YDML_PHY_MF
LOGICAL           ,INTENT(IN)    :: LDZ0H
CHARACTER(LEN=1)  ,INTENT(IN)    :: CDCONF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPXLEV
REAL(KIND=JPRB)   ,INTENT(IN)    :: PXLEV(KPXLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PABUF(:,:,:)
INTEGER(KIND=JPIM),INTENT(IN)    :: KCUFNR
INTEGER(KIND=JPIM),INTENT(IN)    :: KAPTR(JPOSDYN)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGP(YDFPGEO%NFPROMA,YDQTYPE%NFPAUXB,YDFPGEO%NFPBLOCS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFP(YDFPGEO%NFPROMA,YDQTYPE%NFPGT1,YDFPGEO%NFPBLOCS)

!     ------------------------------------------------------------------

!     ==== CONTROL VARIABLES ===
!     IDFP    : pointer of GDFP (output data)
! NB : (.,0) = adress of non-requested fields
!     ==== WORKING ARRAYS ===
! pre-processed surface pressure
REAL(KIND=JPRB) :: ZPST2(YDFPGEO%NFPROMA)
! pre-processed half levels true vertical velocity
REAL(KIND=JPRB) :: ZWHT2(YDFPGEO%NFPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
! Mixed surface temperature : output and interpolated
REAL(KIND=JPRB) :: ZTS(YDFPGEO%NFPROMA)
! Output orography :
REAL(KIND=JPRB) :: ZOROG(YDFPGEO%NFPROMA)
! Temperature correction :
REAL(KIND=JPRB) :: ZDT(YDFPGEO%NFPROMA), ZDTSGDZ
! Climatology fields (or so) :
REAL(KIND=JPRB) :: ZCLSM(YDFPGEO%NFPROMA), ZCVEG(YDFPGEO%NFPROMA), ZCIVG(YDFPGEO%NFPROMA), ZCARG(YDFPGEO%NFPROMA)
REAL(KIND=JPRB) :: ZCD2(YDFPGEO%NFPROMA), ZCSAB(YDFPGEO%NFPROMA)

TYPE(TYPE_IGFLFLD) :: YLIN_GFL   ! GFL
INTEGER(KIND=JPIM) :: IUT2   ! U
INTEGER(KIND=JPIM) :: IVT2   ! V
INTEGER(KIND=JPIM) :: ITT2   ! T
INTEGER(KIND=JPIM) :: IWVE2  ! True vertical velocity ON UPPER HALF LEVELS
INTEGER(KIND=JPIM) :: IPDE2  ! NH Pressure departure
INTEGER(KIND=JPIM) :: ILNPT2 ! Ln(Ps)
INTEGER(KIND=JPIM) :: IFIST2 ! Surface geopotential (interpolated)
INTEGER(KIND=JPIM) :: IWWST2 ! True vertical velocity ON SURFACE

INTEGER(KIND=JPIM) :: IST, IEND, JBLOC, JLEV, JI, IXFIS, IEDR

INTEGER(KIND=JPIM) :: IATS0, IATSI, IATXI, IATNI, IASD0, IASW0, IASF0, IAHV0
INTEGER(KIND=JPIM) :: IASR0, IAT2M, IAQ2M, IATR0, IAHXI, IAHNI, IASST
INTEGER(KIND=JPIM) :: ICFIS ,ICLSM ,ICVEG ,ICIVG ,ICARG ,ICD2 ,ICSAB, IALAN
INTEGER(KIND=JPIM) :: IALSM, IAVEG, IAIVG, IAARG, IAD2, IASAB
INTEGER(KIND=JPIM) :: IFPCUFNR ! Number of CUF fields
INTEGER(KIND=JPIM) :: ICUFCOD(KCUFNR), ICUFT2(KCUFNR)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "endpos.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ENDVPOS',0,ZHOOK_HANDLE)
ASSOCIATE(GFPBUF=>YDAUX%FPBUF, YDRQAUX=>YDAUX%YRQPHY, &
 & RFPBUF=>YDCLIMO%FPBUF, YDRQCLI=>YDCLIMO%YRQPHY, &
 & NFPMASK=>YDNAMFPSCI%NFPMASK, TFP=>YDAFN%TFP, GFP=>YDAFN%GFP, &
 & NFPBLOCS=>YDFPGEO%NFPBLOCS, NFPROMA=>YDFPGEO%NFPROMA, NFPEND=>YDFPGEO%NFPEND, &
 & RFPMASK=>YDFPGEO%RFPMASK, RFPGM=>YDFPGEO%RFPGM,  &
 & NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)
!     ------------------------------------------------------------------

ZDTSGDZ=RDTDZ1/YDCST%RG

!*       1. INITIALIZE LOCAL POINTERS FOR INPUT FIELDS
!           ------------------------------------------

! GFL:
YLIN_GFL%IQ=KAPTR(TFP%Q%ICOD)
YLIN_GFL%IL=KAPTR(TFP%L%ICOD)
YLIN_GFL%ILRAD=KAPTR(TFP%LRAD%ICOD)
YLIN_GFL%II=KAPTR(TFP%I%ICOD)
YLIN_GFL%IIRAD=KAPTR(TFP%IRAD%ICOD)
YLIN_GFL%IA=KAPTR(TFP%CLF%ICOD)
YLIN_GFL%IO3=KAPTR(TFP%O3MX%ICOD)
YLIN_GFL%ICPF=KAPTR(TFP%CPF%ICOD)
YLIN_GFL%ISPF=KAPTR(TFP%SPF%ICOD)
YLIN_GFL%IS=KAPTR(TFP%SN%ICOD)
YLIN_GFL%IRR=KAPTR(TFP%RR%ICOD)
YLIN_GFL%IG=KAPTR(TFP%GR%ICOD)
YLIN_GFL%IH=KAPTR(TFP%HL%ICOD)

YLIN_GFL%ILCONV=KAPTR(TFP%LCONV%ICOD)
YLIN_GFL%IICONV=KAPTR(TFP%ICONV%ICOD)
YLIN_GFL%IRCONV=KAPTR(TFP%RCONV%ICOD)
YLIN_GFL%ISCONV=KAPTR(TFP%SCONV%ICOD)
YLIN_GFL%ITKE=KAPTR(TFP%TKE%ICOD)
YLIN_GFL%IGHG=KAPTR(TFP%GHG(1)%ICOD)
YLIN_GFL%IAERO=KAPTR(TFP%AERO(1)%ICOD)
YLIN_GFL%ICHEM=KAPTR(TFP%CHEM(1)%ICOD)
YLIN_GFL%IEXT=KAPTR(TFP%EXT(1)%ICOD)
YLIN_GFL%IUAL=KAPTR(TFP%UAL%ICOD)
YLIN_GFL%IUOM=KAPTR(TFP%UOM%ICOD)
YLIN_GFL%IDAL=KAPTR(TFP%DAL%ICOD)
YLIN_GFL%IDOM=KAPTR(TFP%DOM%ICOD)


! GMV and GMVS:
IUT2  =KAPTR(TFP%U%ICOD)
IVT2  =KAPTR(TFP%V%ICOD)
ITT2  =KAPTR(TFP%T%ICOD)
IPDE2 =KAPTR(TFP%PD%ICOD)
IWVE2 =KAPTR(TFP%VW%ICOD)
ILNPT2=KAPTR(TFP%LNSP%ICOD)

! Other quantities:
IATS0=IFPSEARCH(YDRQAUX,GFP%ST%ICOD)
IATSI=IFPSEARCH(YDRQAUX,GFP%RDST%ICOD)
IATXI=IFPSEARCH(YDRQAUX,GFP%INCTX%ICOD)
IATNI=IFPSEARCH(YDRQAUX,GFP%INCTN%ICOD)
IASD0=IFPSEARCH(YDRQAUX,GFP%SD%ICOD)
IASW0=IFPSEARCH(YDRQAUX,GFP%SSW%ICOD)
IASF0=IFPSEARCH(YDRQAUX,GFP%FSSW%ICOD)
IAHV0=IFPSEARCH(YDRQAUX,GFP%HV%ICOD)
IASR0=IFPSEARCH(YDRQAUX,GFP%IDZ0%ICOD)
IAT2M=IFPSEARCH(YDRQAUX,GFP%X2T%ICOD)
IAQ2M=IFPSEARCH(YDRQAUX,GFP%X2RH%ICOD)
IATR0=IFPSEARCH(YDRQAUX,GFP%ITZ0%ICOD)
IAHXI=IFPSEARCH(YDRQAUX,GFP%INCHX%ICOD)
IAHNI=IFPSEARCH(YDRQAUX,GFP%INCHN%ICOD)
IASST=IFPSEARCH(YDRQAUX,GFP%SST%ICOD)
IALAN=IFPSEARCH(YDRQAUX,GFP%LAN%ICOD)

IALSM=IFPSEARCH(YDRQAUX,GFP%LSM%ICOD)
IAVEG=IFPSEARCH(YDRQAUX,GFP%VEG%ICOD)
IAIVG=IFPSEARCH(YDRQAUX,GFP%IVEG%ICOD)
IAARG=IFPSEARCH(YDRQAUX,GFP%ARG%ICOD)
IAD2 =IFPSEARCH(YDRQAUX,GFP%D2%ICOD)
IASAB=IFPSEARCH(YDRQAUX,GFP%SAB%ICOD)

IXFIS=IFPSEARCH(YDRQAUX,GFP%SFIS%ICOD)

ICFIS=IFPSEARCH(YDRQCLI,GFP%GFIS%ICOD)
ICLSM=IFPSEARCH(YDRQCLI,GFP%LSM%ICOD)
ICVEG=IFPSEARCH(YDRQCLI,GFP%VEG%ICOD)
ICIVG=IFPSEARCH(YDRQCLI,GFP%IVEG%ICOD)
ICARG=IFPSEARCH(YDRQCLI,GFP%ARG%ICOD)
ICD2 =IFPSEARCH(YDRQCLI,GFP%D2%ICOD)
ICSAB=IFPSEARCH(YDRQCLI,GFP%SAB%ICOD)

IFIST2=KAPTR(TFP%FIS%ICOD)
IWWST2=KAPTR(TFP%WWS%ICOD)

IFPCUFNR=0
IF (YDQTYPE%LL(TFP%CUF1%ICOD)) THEN
  IFPCUFNR=IFPCUFNR+1
  ICUFCOD(IFPCUFNR)=TFP%CUF1%ICOD
  ICUFT2(IFPCUFNR)=KAPTR(TFP%CUF1%ICOD)
ENDIF
IF (YDQTYPE%LL(TFP%CUF2%ICOD)) THEN
  IFPCUFNR=IFPCUFNR+1
  ICUFCOD(IFPCUFNR)=TFP%CUF2%ICOD
  ICUFT2(IFPCUFNR)=KAPTR(TFP%CUF2%ICOD)
ENDIF
IF (YDQTYPE%LL(TFP%CUF3%ICOD)) THEN
  IFPCUFNR=IFPCUFNR+1
  ICUFCOD(IFPCUFNR)=TFP%CUF3%ICOD
  ICUFT2(IFPCUFNR)=KAPTR(TFP%CUF3%ICOD)
ENDIF
IF (YDQTYPE%LL(TFP%CUF4%ICOD)) THEN
  IFPCUFNR=IFPCUFNR+1
  ICUFCOD(IFPCUFNR)=TFP%CUF4%ICOD
  ICUFT2(IFPCUFNR)=KAPTR(TFP%CUF4%ICOD)
ENDIF
IF (YDQTYPE%LL(TFP%CUF5%ICOD)) THEN
  IFPCUFNR=IFPCUFNR+1
  ICUFCOD(IFPCUFNR)=TFP%CUF5%ICOD
  ICUFT2(IFPCUFNR)=KAPTR(TFP%CUF5%ICOD)
ENDIF

IEDR=KAPTR(TFP%EDR%ICOD)

!*       2. LOOP ON SUBROWS OF EACH PP. PACKET
!           ----------------------------------

!$OMP PARALLEL PRIVATE(JBLOC,IST,IEND,JI,ZPST2,JLEV,ZWHT2,ZTS,ZOROG,ZDT,ZCLSM,ZCVEG,ZCIVG,ZCARG,ZCD2,ZCSAB)
!$OMP DO SCHEDULE(DYNAMIC,1)
DO JBLOC=1,NFPBLOCS

  IST =1
  IEND=NFPEND(JBLOC)

  !      2.1 READ INPUT DATA

  !      2.1.3 Auxilary physical fields

  !      2.2 COMPUTE OUTPUT FIELDS

  IF (ICFIS > 0) THEN
    ZOROG(IST:IEND)=RFPBUF(IST:IEND,ICFIS,JBLOC)
    ZDT(IST:IEND)=0._JPRB
  ELSEIF (IFIST2 > 0) THEN
    IF (YDQTYPE%IFIT == 2) THEN
!     The spectrally fitted orography replaces the missing output orography on this kind of surface :
      ZOROG(IST:IEND)=GFPBUF(IST:IEND,IXFIS,JBLOC)
      DO JI=IST,IEND
        ZDT(JI)=ZDTSGDZ*(ZOROG(JI)-PABUF(JI,IFIST2,JBLOC))
      ENDDO
    ELSE
      ZOROG(IST:IEND)=PABUF(IST:IEND,IFIST2,JBLOC)
      ZDT(IST:IEND)=0._JPRB
    ENDIF
  ELSE
    ZOROG(IST:IEND)=HUGE(1._JPRB)
    ZDT(IST:IEND)=HUGE(1._JPRB)
  ENDIF

  IF (ILNPT2 > 0) THEN
    DO JI=IST,IEND
      ZPST2(JI)=EXP(PABUF(JI,ILNPT2,JBLOC))
    ENDDO
  ENDIF

  IF (IWVE2 > 0) THEN
    DO JLEV=0,NFLEVG-1
      ZWHT2(:,JLEV)=PABUF(:,IWVE2+JLEV,JBLOC)
    ENDDO
  ELSE
    DO JLEV=0,NFLEVG-1
      ZWHT2(:,JLEV)=0._JPRB
    ENDDO
  ENDIF
  IF (IWWST2 > 0) THEN
!DEC$ IVDEP
    ZWHT2(:,NFLEVG)=PABUF(:,IWWST2,JBLOC)
  ELSE
!DEC$ IVDEP
    ZWHT2(:,NFLEVG)=0._JPRB 
  ENDIF
  IF (IATS0 > 0) THEN
    IF (NFPMASK <= 1) THEN
      DO JI=IST,IEND
        ZTS(JI)=GFPBUF(JI,IATS0,JBLOC)+ZDT(JI)
      ENDDO
    ELSE
!     GFPBUF(.,IATSI,.) is already a combination of interpolated Ts and SST
      IF (IALAN > 0 .AND. IASST > 0) THEN
        DO JI=IST,IEND
          ZTS(JI)=GFPBUF(JI,IALAN,JBLOC)*GFPBUF(JI,IATS0,JBLOC)&
           & +(1._JPRB-GFPBUF(JI,IALAN,JBLOC))*GFPBUF(JI,IASST,JBLOC) + ZDT(JI)
        ENDDO
      ENDIF
    ENDIF  
  ENDIF
  IF (ICLSM > 0) THEN
    ZCLSM(IST:IEND)=RFPBUF(IST:IEND,ICLSM,JBLOC)
  ELSEIF(IALSM > 0) THEN
    ZCLSM(IST:IEND)=GFPBUF(IST:IEND,IALSM,JBLOC)
  ELSE
    ZCLSM(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  IF (ICVEG > 0) THEN
    ZCVEG(IST:IEND)=RFPBUF(IST:IEND,ICVEG,JBLOC)
  ELSEIF(IAVEG > 0) THEN
    ZCVEG(IST:IEND)=GFPBUF(IST:IEND,IAVEG,JBLOC)
  ELSE
    ZCVEG(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  IF (ICIVG > 0) THEN
    ZCIVG(IST:IEND)=RFPBUF(IST:IEND,ICIVG,JBLOC)
  ELSEIF(IAIVG > 0) THEN
    ZCIVG(IST:IEND)=GFPBUF(IST:IEND,IAIVG,JBLOC)
  ELSE
    ZCIVG(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  IF (ICARG > 0) THEN
    ZCARG(IST:IEND)=RFPBUF(IST:IEND,ICARG,JBLOC)
  ELSEIF(IAARG > 0) THEN
    ZCARG(IST:IEND)=GFPBUF(IST:IEND,IAARG,JBLOC)
  ELSE
    ZCARG(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  IF (ICD2 > 0) THEN
    ZCD2(IST:IEND)=RFPBUF(IST:IEND,ICD2,JBLOC)
  ELSEIF(IAD2 > 0) THEN
    ZCD2(IST:IEND)=GFPBUF(IST:IEND,IAD2,JBLOC)
  ELSE
    ZCD2(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  IF (ICSAB > 0) THEN
    ZCSAB(IST:IEND)=RFPBUF(IST:IEND,ICSAB,JBLOC)
  ELSEIF(IASAB > 0) THEN
    ZCSAB(IST:IEND)=GFPBUF(IST:IEND,IASAB,JBLOC)
  ELSE
    ZCSAB(IST:IEND)=HUGE(1._JPRB)
  ENDIF
  PGP(:,:,JBLOC) = 0.0_JPRB
  CALL ENDPOS(YDCST,YDQTYPE,YDNAMFPSCI,YDAFN,KLMOD,YDFPVAB,YDGEOMETRY,YDML_GCONF,YDML_PHY_MF,LDZ0H,NFPROMA,IST,IEND,YDQTYPE%NFPAUXB,KPXLEV,CDCONF, &
   & PXLEV,YDQTYPE%NFPGT1,YLIN_GFL,PABUF(:,:,JBLOC),IUT2,IVT2,ITT2,IPDE2,ZWHT2,IFIST2,IFPCUFNR,ICUFCOD(:),ICUFT2(:),IEDR,ZPST2,ZTS, &
   & GFPBUF(:,:,JBLOC),IATSI,IASD0,IATXI,IATNI,IASW0,IASF0,IAHV0,IASR0,IAT2M,IAQ2M,IATR0,IAHXI,IAHNI,ZOROG(:),ZCLSM,ZCVEG, &
   & ZCIVG,ZCARG,ZCD2,ZCSAB,RFPGM(:,JBLOC),PGP(:,:,JBLOC),PFP(:,:,JBLOC),RFPMASK(:,JBLOC))

ENDDO
!$OMP END DO
!$OMP END PARALLEL

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ENDVPOS',1,ZHOOK_HANDLE)
END SUBROUTINE ENDVPOS
