SUBROUTINE FPCORPHY(YDRQCLI,YDNAMFPSCI,YDAFN,YDEPHY,YDPHY,YDPHY1,KST,KEND,KFPROMA,LDFPOSHOR,KFIELDS,KCOD,PROW,KAUX,PAUX,YDRQAUX, &
 & KCLI,PCLI,PWSXI,PWDXI,LDALT,KWIC)

!**** *FPCORPHY*  - FULL-POS corrector on physical fields after
!                   horizontal interpolations

!     PURPOSE.
!     --------
!        To correct physical fields after the horizontal interpolations or
!        biperiodicisations that may create overshoots,
!        or to recover the requested fields after relative interpolations or
!        biperiodicisations.

!**   INTERFACE.
!     ----------
!       *CALL* *FPCORPHY*

!        EXPLICIT ARGUMENTS
!        --------------------

!        INPUT:
!         KST    : first output point in row
!         KEND   : last output point in row
!         KFPROMA: length of the output row
!         LDFPOSHOR: .true. if actual horizontal interpolations
!         KFIELDS: number of fields in output row
!         KAUX   : number of fields in auxilary row
!         PAUX   : auxilary fields row
!         KCLI   : number of fields in climatology row
!         PCLI   : climatology fields row
!         LDALT  : force or not altitude correction
!         KWIC   : indicator of missing lake/island

!        OUTPUT:
!         PROW  : output row for physics

!        IMPLICIT ARGUMENTS
!        --------------------
!          None

!     METHOD.
!     -------
!        SEE DOCUMENTATION

!     EXTERNALS.
!     ----------
!       FPHOR12 , ACSOLW

!     REFERENCE.
!     ----------
!        ECMWF Research Department documentation of the IFS
!        See documentation about FULL-POS.

!     AUTHOR.
!     -------
!      RYAD EL KHATIB *METEO-FRANCE*
!      ORIGINAL : 98-09-07 from FPINTPHY

!     MODIFICATIONS.
!     --------------
!      R. El Khatib : 03-04-17 Fullpos improvemnts
!      R. El Khatib : 03-04-23 Options ICOR reorganized
!      R. El Khatib : 03-05-26 Fix for overshoot on soil wetness prop. (e923)
!      R. El Khatib : 03-08-04 Fix : .NOT.LDFPOSHOR => IANO=0 (see HPOS)
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      P, Viterbo :  24-05-2004   Change surface units
!      R. El Khatib :  17-May-2005 SST for Meteo-France
!      Y. Seity :  06-March-2007 add Graupel and Hail precipitations 
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      R. El Khatib :  04-Feb-2010 Bugfix : do not call surf_inc if susurf has not
!                    been called in the setup
!      K. Yessad (Oct 2011): encapsulated FPLAKE.
!      T. Aspelien : 11-04-11 Using option 7 if LCRITSNOWTEMP=.false.
!      R. El khatib 26-Sep-2012 optional argument LDALT
!      F. Bouyssel (Dec 2012): Fix for IANO=0 on surface temperature.
!      R. El Khatib 13-Dec-2012 Fullpos buffers reshaping
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      R. El Khatib 28-Jul-2016 Recode LWIDER_DOM
!      R. El Khatib 17-Aug-2016 Interoperability IFS vs ARPEGE
!      R. El Khatib 09-Sep-2016 More interoperability + cleanings
!      R. El Khatib 09-Sep-2016 use option 1 with the proper arguments instead of 7 for .not.LCRITSNOWTEMP
!      R. El Khatib 30-Jan-2018 Remove sursaturation of relative moisture even when computed without surface moisture
!      R. El Khatib 12-Mar-2018 total albedo
!      R. El Khatib 15-Jun-2018 Fix Tcls and RHcls if .not.ldfposhor and nfpcli >0
!      R. Brozkova Sep-2018 Added mean radiant temperature
!     ------------------------------------------------------------------

USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMAFN   , ONLY : TAFN
USE YOMFPC   , ONLY : TNAMFPSCI
USE YOMCST   , ONLY : RG, RTT, RPI
USE YOMSTA   , ONLY : RDTDZ1
USE YOMPHY1  , ONLY : TPHY1
USE YOMPHY   , ONLY : TPHY
USE YOEPHY   , ONLY : TEPHY
USE YOMFP4L  , ONLY : TRQFP, IFPSEARCH
USE YOMCLI   , ONLY : YRCLI 

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(TRQFP)       ,INTENT(IN)    :: YDRQCLI
TYPE(TNAMFPSCI)   ,INTENT(IN)    :: YDNAMFPSCI
TYPE(TAFN)        ,INTENT(IN)    :: YDAFN
TYPE(TEPHY)       ,INTENT(IN)    :: YDEPHY
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPHY1)       ,INTENT(IN)    :: YDPHY1
INTEGER(KIND=JPIM),INTENT(IN)    :: KST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFPROMA 
LOGICAL           ,INTENT(IN)    :: LDFPOSHOR
INTEGER(KIND=JPIM),INTENT(IN)    :: KFIELDS 
INTEGER(KIND=JPIM),INTENT(IN)    :: KCOD(KFIELDS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PROW(KFPROMA,KFIELDS) 
INTEGER(KIND=JPIM),INTENT(IN)    :: KAUX 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAUX(KFPROMA,KAUX) 
TYPE(TRQFP),INTENT(IN)           :: YDRQAUX
INTEGER(KIND=JPIM),INTENT(IN)    :: KCLI 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCLI(KFPROMA,0:KCLI) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSXI
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWDXI
LOGICAL           ,INTENT(IN), OPTIONAL    :: LDALT
INTEGER(KIND=JPIM),INTENT(IN), OPTIONAL :: KWIC(KFPROMA) 

!     ------------------------------------------------------------------

!     ZTT   : Threshold
!     ZSCAL : scaling factor
!     ZMIN  : minimum value of the field
!     ZMAX  : maximum value of the field
!     ZSEA  : field value over sea
!     ZLSM  : land-sea mask
!     ZLAN  : Land proportion
!     ZRHS  : surface relative moisture
!     ZTS   : surface temperature
!     ZTD   : deep soil temperature
!     ZTCLS : cls temperature
!     ZRHCLS: cls relative moisture
!     ZSTDV : standard deviation of orography
!     ZD2   : soil depth
!     ZARG  : Clay
!     ZCLI  : working array
!     ZALT  : altitude correction for temperature
!     ZCSN  : climatology snow depth
!     ZCSSW : climatology relative surface soil water content
!     ZCDSW : climatology relative deep soil water content
!     ZDTSGDZ: (1./g)*(dT/dz)
!     ZLAI  : climatology leaf area index
!     ZRSM  : climatology stomatal minimum resistance
!     ZVEG  : climatology vegetation
!     ZTSA  : interpolated surface temperature + altitude correction

REAL(KIND=JPRB) :: ZTT (KFPROMA),ZSCAL(KFPROMA),ZMIN (KFPROMA),ZMAX  (KFPROMA)
REAL(KIND=JPRB) :: ZSEA(KFPROMA),ZLSM (KFPROMA),ZCLI (KFPROMA),ZALT  (KFPROMA)
REAL(KIND=JPRB) :: ZRHS(KFPROMA),ZTS  (KFPROMA),ZSTDV(KFPROMA),ZARG  (KFPROMA)
REAL(KIND=JPRB) :: ZSAB(KFPROMA),ZIVG (KFPROMA),ZD2  (KFPROMA),ZWSMX (KFPROMA)
REAL(KIND=JPRB) :: ZWFC(KFPROMA),ZWPMX(KFPROMA),ZWSAT(KFPROMA),ZWWILT(KFPROMA)
REAL(KIND=JPRB) :: ZCSN(KFPROMA),ZCSSW(KFPROMA),ZCDSW(KFPROMA),ZTCLS (KFPROMA)
REAL(KIND=JPRB) :: ZLAI(KFPROMA),ZVEG (KFPROMA),ZTSA (KFPROMA),ZRHCLS(KFPROMA)
REAL(KIND=JPRB) :: ZRSM(KFPROMA),ZTD  (KFPROMA)
REAL(KIND=JPRB) :: ZTMP(KFPROMA),ZLAN (KFPROMA),ZSST (KFPROMA)
REAL(KIND=JPRB) :: ZUVRAF(KFPROMA)
REAL(KIND=JPRB), ALLOCATABLE :: ZRVLAI(:)

INTEGER(KIND=JPIM) :: IATS0, IAHS0, IAFIS, IALSM, IASDO, IAT2M, IAQ2M, IATSI
INTEGER(KIND=JPIM) :: IALAN, IAUGST, IAVGST,IAUGST2, IAVGST2, IADIAH
INTEGER(KIND=JPIM) :: IXTS0, IXHS0, IXFIS, IXLSM, IXSDO, IXT2M, IXQ2M, IXTSI
INTEGER(KIND=JPIM) :: IXSST, IASST, IXLAN, IXUGST, IXVGST,IXDIAH, IXTD, IATD

INTEGER(KIND=JPIM) :: IAARG, IAD2, IADSW, IAIVG, IASAB, IASSW, IAVEG, IALAI, IARSM
INTEGER(KIND=JPIM) :: ICARG, ICD2, ICDSW, ICIVG, ICSAB, ICSSW, ICVEG, ICLAI, ICRSM
INTEGER(KIND=JPIM) :: IXLAI, IXVEG

INTEGER(KIND=JPIM) :: ICFIS, ICLSM, ICSDO, ICSD, ICTS, ICDT, ICLAN

INTEGER(KIND=JPIM) :: ICOR, JF, JI, ICOD, IANO, IX, IA, IVTYPES

LOGICAL :: LLALT, LLARG, LLCDSW, LLCSN, LLCSSW, LLD2, LLHMT, LLIVG, LLRHS,&
 & LLSAB, LLSM, LLSTDV, LLTS, LLDONE, LLVEG, LLLAI, LLRSM ,LLTCLS, LL, LLSST,&
 & LLRHCLS, LLTSA, LLAND, LLGST, LLGST2, LLDIAGH , LLTD

REAL(KIND=JPRB) :: ZDELTA, ZPGLACE
REAL(KIND=JPRB) :: ZDTSGDZ
REAL(KIND=JPRB) :: ZRWSAT,ZRWLMAX,ZRALFMINSN
REAL(KIND=JPRB) :: ZRALFMAXSN,ZRHOMINSN,ZRHOMAXSN,ZRALBSEAD
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "surf_inq.h"

#include "abor1.intfb.h"
#include "acsolw.intfb.h"
#include "fphor12.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('FPCORPHY',0,ZHOOK_HANDLE)
ASSOCIATE(NFPMASK=>YDNAMFPSCI%NFPMASK, LCRITSNOWTEMP=>YDNAMFPSCI%LCRITSNOWTEMP, TSRESERV1=>YDNAMFPSCI%TSRESERV1, &
 & TSRESERV2=>YDNAMFPSCI%TSRESERV2, TDELTA1=>YDNAMFPSCI%TDELTA1, TDELTA2=>YDNAMFPSCI%TDELTA2, &
 & NFPLAKE=>YDNAMFPSCI%NFPLAKE, NFPCLI=>YDNAMFPSCI%NFPCLI, NFPSWI=>YDNAMFPSCI%NFPSWI, &
 & GFP=>YDAFN%GFP, GFP_PHYDS=>YDAFN%GFP_PHYDS, &
 & GWLMX=>YDPHY1%GWLMX, ALBMIN=>YDPHY1%ALBMIN, RHOMIN=>YDPHY1%RHOMIN, &
 & GWPIMX=>YDPHY1%GWPIMX, RHOMAX=>YDPHY1%RHOMAX, ALBMAX=>YDPHY1%ALBMAX, &
 & RD1=>YDPHY1%RD1, GCONV=>YDPHY1%GCONV, LMPHYS=>YDPHY%LMPHYS, LEPHYS=>YDEPHY%LEPHYS, YSURF=>YDEPHY%YSURF)
!     ------------------------------------------------------------------

!        0. POINTERS
!           --------

! Auxilary buffer:

IF (KAUX > 0) THEN
  IXTS0=IFPSEARCH(YDRQAUX,GFP%ST%ICOD)
  IXSST=IFPSEARCH(YDRQAUX,GFP%SST%ICOD)
  IXTD =IFPSEARCH(YDRQAUX,GFP%DST%ICOD)
  IXHS0=IFPSEARCH(YDRQAUX,GFP%PSRHU%ICOD)
  IXFIS=IFPSEARCH(YDRQAUX,GFP%SFIS%ICOD)
  IXLSM=IFPSEARCH(YDRQAUX,GFP%LSM%ICOD)
  IXLAN=IFPSEARCH(YDRQAUX,GFP%LAN%ICOD)
  IXSDO=IFPSEARCH(YDRQAUX,GFP%SDOG%ICOD)
  IXT2M=IFPSEARCH(YDRQAUX,GFP%X2T%ICOD)
  IXQ2M=IFPSEARCH(YDRQAUX,GFP%X2RH%ICOD)
  IXTSI=IFPSEARCH(YDRQAUX,GFP%RDST%ICOD)
  IXUGST=IFPSEARCH(YDRQAUX,GFP%XUGST%ICOD)
  IXVGST=IFPSEARCH(YDRQAUX,GFP%XVGST%ICOD)
  IXDIAH=IFPSEARCH(YDRQAUX,GFP%XXDIAGH%ICOD)
  IXLAI=IFPSEARCH(YDRQAUX,GFP%LAI%ICOD)
  IXVEG=IFPSEARCH(YDRQAUX,GFP%VEG%ICOD)
ELSE
  IXTS0=0
  IXSST=0
  IXTD =0
  IXHS0=0
  IXFIS=0
  IXLSM=0
  IXLAN=0
  IXSDO=0
  IXT2M=0
  IXQ2M=0
  IXTSI=0
  IXUGST=0
  IXVGST=0
  IXDIAH=0
  IXLAI=0
  IXVEG=0
ENDIF

! Climatology buffer:

IF (NFPCLI > 0) THEN
  ICARG=IFPSEARCH(YDRQCLI,GFP%ARG%ICOD)
  ICD2 =IFPSEARCH(YDRQCLI,GFP%D2%ICOD)
  ICDSW=IFPSEARCH(YDRQCLI,GFP%CDSW%ICOD)
  ICIVG=IFPSEARCH(YDRQCLI,GFP%IVEG%ICOD)
  ICSAB=IFPSEARCH(YDRQCLI,GFP%SAB%ICOD)
  ICSSW=IFPSEARCH(YDRQCLI,GFP%CSSW%ICOD)
  ICVEG=IFPSEARCH(YDRQCLI,GFP%VEG%ICOD)
  ICLAI=IFPSEARCH(YDRQCLI,GFP%LAI%ICOD)
  ICRSM=IFPSEARCH(YDRQCLI,GFP%RSMIN%ICOD)
  ICLSM=IFPSEARCH(YDRQCLI,GFP%LSM%ICOD)
  ICLAN=IFPSEARCH(YDRQCLI,GFP%LAN%ICOD)
  ICFIS=IFPSEARCH(YDRQCLI,GFP%GFIS%ICOD)
  ICSDO=IFPSEARCH(YDRQCLI,GFP%SDOG%ICOD)
  ICTS =IFPSEARCH(YDRQCLI,GFP%ST%ICOD)
  ICDT =IFPSEARCH(YDRQCLI,GFP%DST%ICOD)
  ICSD =IFPSEARCH(YDRQCLI,GFP%SD%ICOD)
ELSE
  ICARG=0
  ICD2 =0
  ICDSW=0
  ICIVG=0
  ICSAB=0
  ICSSW=0
  ICVEG=0
  ICLAI=0
  ICRSM=0
  ICLSM=0
  ICLAN=0
  ICFIS=0
  ICSDO=0
  ICTS =0
  ICDT =0
  ICSD =0
ENDIF


!*       1. PREPARATIONS
!           ------------

CALL SURF_INQ(YSURF,KNVTYPES=IVTYPES)
ALLOCATE(ZRVLAI(0:IVTYPES))
CALL SURF_INQ(YSURF,PRWSAT=ZRWSAT,PRWLMAX=ZRWLMAX,PRALFMINSN=ZRALFMINSN,&
 & PRALFMAXSN=ZRALFMAXSN,PRHOMINSN=ZRHOMINSN,PRHOMAXSN=ZRHOMAXSN,&
 & PRALBSEAD=ZRALBSEAD,PRVLAI=ZRVLAI)

!     Search some fields in the working row itself :
IAFIS=-999
IALSM=-999
IALAN=-999
IASDO=-999
IATS0=-999
IASST=-999
IATD =-999
IAHS0=-999
IAD2 =-999
IAARG=-999
IASAB=-999
IAIVG=-999
IASSW=-999
IADSW=-999
IALAI=-999
IARSM=-999
IAVEG=-999
IAT2M=-999
IAQ2M=-999
IATSI=-999
IADIAH=-999
IAVGST=-999
IAUGST=-999
IAUGST2=-999
IAVGST2=-999


DO JF=1,KFIELDS
  ICOD=KCOD(JF)
  IF (ICOD == GFP%SFIS%ICOD) THEN
!         Interpolated orography
    IAFIS=JF
  ELSEIF (ICOD == GFP%X2T%ICOD) THEN
!         Cls temperature
    IAT2M=JF
  ELSEIF (ICOD == GFP%X2RH%ICOD) THEN
!         Cls relative moisture
    IAQ2M=JF
  ELSEIF (ICOD == GFP%LSM%ICOD) THEN
!         Land-sea mask
    IALSM=JF
  ELSEIF (ICOD == GFP%LAN%ICOD) THEN
!         Land proportion
    IALAN=JF
  ELSEIF (ICOD == GFP%SDOG%ICOD) THEN
!         standard deviation of orography
    IASDO=JF
  ELSEIF (ICOD == GFP%ST%ICOD) THEN
!         Surface temperature
    IATS0=JF
  ELSEIF (ICOD == GFP%SST%ICOD) THEN
!         SST
    IASST=JF
  ELSEIF (ICOD == GFP%DST%ICOD) THEN
!         Td
    IATD=JF
  ELSEIF (ICOD == GFP%PSRHU%ICOD) THEN
!         Surface relative moisture
    IAHS0=JF
  ELSEIF (ICOD == GFP%D2%ICOD) THEN
!         Soil depth
    IAD2 =JF
  ELSEIF (ICOD == GFP%ARG%ICOD) THEN
!         Clay
    IAARG=JF
  ELSEIF (ICOD == GFP%SAB%ICOD) THEN
!         Sand
    IASAB=JF
  ELSEIF (ICOD == GFP%IVEG%ICOD) THEN
!         Index of vegetation
    IAIVG=JF
  ELSEIF (ICOD == GFP%CSSW%ICOD) THEN
!         Proportion of surface water content
    IASSW=JF
  ELSEIF (ICOD == GFP%CDSW%ICOD) THEN
!         Proportion of deep soil water content
    IADSW=JF
  ELSEIF (ICOD == GFP%LAI%ICOD) THEN
!         Leaf area index
    IALAI=JF
  ELSEIF (ICOD == GFP%RSMIN%ICOD) THEN
!         Stomatal Minimum resistance
    IARSM=JF
  ELSEIF (ICOD == GFP%VEG%ICOD) THEN
!        Vegetation 
    IAVEG=JF
  ELSEIF (ICOD == GFP%RDST%ICOD) THEN
!        Interpolated surface temperature
    IATSI=JF
  ELSEIF (ICOD == GFP%XUGST%ICOD) THEN
!        Interpolated U-component gust
    IAUGST=JF
  ELSEIF (ICOD == GFP%XVGST%ICOD) THEN
!       Interpolated V-component gust 
    IAVGST=JF
  ELSEIF (ICOD == GFP%XUGST2%ICOD) THEN
!        Interpolated U-component gust
    IAUGST2=JF
  ELSEIF (ICOD == GFP%XVGST2%ICOD) THEN
!       Interpolated V-component gust 
    IAVGST2=JF
  ELSEIF (ICOD == GFP%XXDIAGH%ICOD) THEN
!        Interpolated DIAGHAIL
    IADIAH=JF
  ENDIF
ENDDO

!*       2. CORRECT MAIN CLIMATOLOGY CONSTANTS
!           ----------------------------------

!     search in clim row, then in working row and at last in auxilary row.

!     2.1 Land-sea mask

LLSM=.TRUE.
LLDONE=.FALSE.
IF (ICLSM > 0) THEN
  ZLSM(KST:KEND)=PCLI(KST:KEND,ICLSM)
ELSEIF(IXLSM > 0) THEN
  ZLSM(KST:KEND)=PAUX(KST:KEND,IXLSM)
  LLDONE=.TRUE.
ELSEIF (IALSM > 0) THEN
  ZLSM(KST:KEND)=PROW(KST:KEND,IALSM)
ELSE
  LLSM=.FALSE.
ENDIF
IF (LLSM.AND..NOT.LLDONE) THEN
  IF (LMPHYS) THEN
    ICOR=5
    ZTT(KST:KEND)=YRCLI%SMASK
    CALL FPHOR12(1,KST,KEND,ZLSM,1,KFPROMA,ICOR,ZSEA,ZCLI,ZMIN,&
     & ZMAX,ZSCAL,ZTT)  
  ENDIF
ENDIF
IF (IALSM > 0) THEN
  PROW(KST:KEND,IALSM)=ZLSM(KST:KEND)
ENDIF

!     2.1bis Land proportion

LLAND=.TRUE.
LLDONE=.FALSE.
IF (ICLAN > 0) THEN
  ZLAN(KST:KEND)=PCLI(KST:KEND,ICLAN)
ELSEIF(IXLAN > 0) THEN
  ZLAN(KST:KEND)=PAUX(KST:KEND,IXLAN)
  LLDONE=.TRUE.
ELSEIF (IALAN > 0) THEN
  ZLAN(KST:KEND)=PROW(KST:KEND,IALAN)
ELSE
  LLAND=.FALSE.
ENDIF
IF (LLAND.AND..NOT.LLDONE) THEN
  IF (LMPHYS) THEN
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB
    CALL FPHOR12(1,KST,KEND,ZLAN,1,KFPROMA,ICOR,ZSEA,ZCLI,ZMIN,&
     & ZMAX,ZSCAL,ZTT)
  ENDIF
ENDIF
IF (IALAN > 0) THEN
  PROW(KST:KEND,IALAN)=ZLAN(KST:KEND)
ENDIF

!     2.2 Standard deviation of orography

LLSTDV=.TRUE.
LLDONE=.FALSE.
IF (ICSDO > 0) THEN
  ZSTDV(KST:KEND)=PCLI(KST:KEND,ICSDO)
ELSEIF(IXSDO > 0) THEN
  ZSTDV(KST:KEND)=PAUX(KST:KEND,IXSDO)
  LLDONE=.TRUE.
ELSEIF (IASDO > 0) THEN
  ZSTDV(KST:KEND)=PROW(KST:KEND,IASDO)
ELSE
  LLSTDV=.FALSE.
ENDIF
IF (LLSTDV.AND..NOT.LLDONE) THEN
  IF (LMPHYS) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB
    CALL FPHOR12(1,KST,KEND,ZSTDV,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,&
     & ZMAX,ZSCAL,ZTT)  
  ENDIF
ENDIF
IF (IASDO > 0) THEN
  PROW(KST:KEND,IASDO)=ZSTDV(KST:KEND)
ENDIF

!     2.3 Soil depth

LLD2=.TRUE.
IF (ICD2 > 0) THEN
  ZD2(KST:KEND)=PCLI(KST:KEND,ICD2)
ELSEIF (IAD2 > 0) THEN
  ZD2(KST:KEND)=PROW(KST:KEND,IAD2)
ELSE
  LLD2=.FALSE.
ENDIF
IF (LLD2) THEN
  ICOR=3
  ZMIN(KST:KEND)=YRCLI%SDEPN
  ZMAX(KST:KEND)=YRCLI%SDEPX
  ZSEA(KST:KEND)=YRCLI%SDEPX
  ZSCAL(KST:KEND)=1.0_JPRB
  IF (.NOT.LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZD2,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IAD2 > 0) THEN
  PROW(KST:KEND,IAD2)=ZD2(KST:KEND)
ENDIF

!     2.4 Clay

LLARG=.TRUE.
IF (ICARG > 0) THEN
  ZARG(KST:KEND)=PCLI(KST:KEND,ICARG)
ELSEIF (IAARG > 0) THEN
  ZARG(KST:KEND)=PROW(KST:KEND,IAARG)
ELSE
  LLARG=.FALSE.
ENDIF
IF (LLARG) THEN
  ICOR=3
  ZMIN(KST:KEND)=YRCLI%SARGN
  ZMAX(KST:KEND)=YRCLI%SARGX
  ZSEA(KST:KEND)=YRCLI%SARGN
  ZSCAL(KST:KEND)=1.0_JPRB
  IF (.NOT.LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZARG,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IAARG > 0) THEN
  PROW(KST:KEND,IAARG)=ZARG(KST:KEND)
ENDIF

!     2.5 Sand

LLSAB=.TRUE.
IF (ICSAB > 0) THEN
  ZSAB(KST:KEND)=PCLI(KST:KEND,ICSAB)
ELSEIF (IASAB > 0) THEN
  ZSAB(KST:KEND)=PROW(KST:KEND,IASAB)
ELSE
  LLSAB=.FALSE.
ENDIF
IF (LLSAB) THEN
  ICOR=3
  ZMIN(KST:KEND)=YRCLI%SSABN
  ZMAX(KST:KEND)=YRCLI%SSABX
  ZSEA(KST:KEND)=YRCLI%SSABN
  ZSCAL(KST:KEND)=1.0_JPRB
  IF (.NOT.LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZSAB,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IASAB > 0) THEN
  PROW(KST:KEND,IASAB)=ZSAB(KST:KEND)
ENDIF

!     2.6 Index of vegetation

LLIVG=.TRUE.
IF (ICIVG > 0) THEN
  ZIVG(KST:KEND)=PCLI(KST:KEND,ICIVG)
ELSEIF (IAIVG > 0) THEN
  ZIVG(KST:KEND)=PROW(KST:KEND,IAIVG)
ELSE
  LLIVG=.FALSE.
ENDIF
IF (LLIVG) THEN
  ! protection against pseudo-interpolations/extrapolations :
  DO JI=KST,KEND
    ZIVG(JI)=MAX(1,MIN(5,NINT(ZIVG(JI))))
  ENDDO
ENDIF
IF (IAIVG > 0) THEN
  PROW(KST:KEND,IAIVG)=ZIVG(KST:KEND)
ENDIF

IF (LLIVG) THEN
  IF (LEPHYS) THEN
    ! "binary" index to compute the soil water/ice content from TESSEL to ISBA
    IF (LLSM) THEN
      DO JI=KST,KEND
        IF (ZLSM(JI) > YRCLI%SMASK) THEN
          ZIVG(JI)=YRCLI%NTPDES
        ELSE
          ZIVG(JI)=YRCLI%NTPMER
        ENDIF
      ENDDO
    ELSE
      CALL ABOR1('FPCORPHY : NEEDS LAND-SEA MASK TO COMPUTE PSEUDO-VEGETATION INDEX') 
    ENDIF
  ENDIF
ENDIF

!     2.7 Vegetation 

LLVEG=.TRUE.
LLDONE=.FALSE.
IF (ICVEG > 0) THEN
  ZVEG(KST:KEND)=PCLI(KST:KEND, ICVEG)  
ELSEIF (IXVEG > 0) THEN
  ZVEG(KST:KEND)=PAUX(KST:KEND,IXVEG) 
  LLDONE=.TRUE.
ELSEIF (IAVEG > 0) THEN
  ZVEG(KST:KEND)=PROW(KST:KEND,IAVEG) 
ELSE
  LLVEG=.FALSE.
ENDIF
IF (LLVEG.AND..NOT.LLDONE) THEN
  ICOR=3
  ZMIN(KST:KEND)=0.0_JPRB
  ZMAX(KST:KEND)=1.0_JPRB
  ZSEA(KST:KEND)=0.0_JPRB
  ZSCAL(KST:KEND)=1.0_JPRB
  IF (.NOT. LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK') 
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZVEG,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IAVEG > 0) THEN
  PROW(KST:KEND,IAVEG)=ZVEG(KST:KEND)
ENDIF

!     2.8 Leaf area index 

LLLAI=.TRUE.
LLDONE=.FALSE.
IF (ICLAI > 0) THEN
  ZLAI(KST:KEND)=PCLI(KST:KEND, ICLAI)  
ELSEIF (IXLAI > 0) THEN
  ZLAI(KST:KEND)=PAUX(KST:KEND,IXLAI) 
  LLDONE=.TRUE.
ELSEIF (IALAI > 0) THEN
  ZLAI(KST:KEND)=PROW(KST:KEND,IALAI) 
ELSE
  LLLAI=.FALSE.
ENDIF
IF (LLLAI.AND..NOT.LLDONE) THEN
  ICOR=1
  ZMIN(KST:KEND)=0.0_JPRB
  ZSEA(KST:KEND)=0.0_JPRB
  IF (.NOT. LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK') 
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZLAI,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IALAI > 0) THEN
  PROW(KST:KEND,IALAI)=ZLAI(KST:KEND)
ENDIF

!     2.9 Stomatal minimum resistance

LLRSM=.TRUE.
IF (ICRSM > 0) THEN
  ZRSM(KST:KEND)=PCLI(KST:KEND, ICRSM)  
ELSEIF (IARSM > 0) THEN
  ZRSM(KST:KEND)=PROW(KST:KEND,IARSM) 
ELSE
  LLRSM=.FALSE.
ENDIF
IF (LLRSM) THEN
  ICOR=3
  ZMIN(KST:KEND)=YRCLI%SRSMN
  ZMAX(KST:KEND)=YRCLI%SRSMX
  ZSEA(KST:KEND)=YRCLI%SRSMX
  ZSCAL(KST:KEND)=1.0_JPRB
  IF (.NOT. LLSM) THEN
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK') 
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZRSM,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IARSM > 0) THEN
  PROW(KST:KEND,IARSM)=ZRSM(KST:KEND)
ENDIF


!*       3. CORRECT AUXILARY QUANTITIES
!           ---------------------------

!     3.2 Relative surface moisture & cls moisture

LLRHS=.TRUE.
LLDONE=.FALSE.
IF(IXHS0 > 0) THEN
  ZRHS(KST:KEND)=PAUX(KST:KEND,IXHS0)
  LLDONE=.TRUE.
ELSEIF (IAHS0 > 0) THEN
  ZRHS(KST:KEND)=PROW(KST:KEND,IAHS0)
ELSE
  LLRHS=.FALSE.
ENDIF
IF (LLRHS.AND..NOT.LLDONE) THEN
  ZMIN(KST:KEND)=0.0_JPRB
  ZMAX(KST:KEND)=1.0_JPRB
  ZSCAL(KST:KEND)=1.0_JPRB
  ZSEA(KST:KEND)=1.0_JPRB
  IF (LLSM) THEN
    ICOR=3
    CALL FPHOR12(1,KST,KEND,ZRHS,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,&
     & ZMAX,ZSCAL,ZTT)  
  ELSE
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
  ENDIF
ENDIF
IF (IAHS0 > 0) THEN
  PROW(KST:KEND,IAHS0)=ZRHS(KST:KEND)
ENDIF

LLRHCLS=.TRUE.
LLDONE=.FALSE.
IF(IXQ2M > 0) THEN
  ZRHCLS(KST:KEND)=PAUX(KST:KEND,IXQ2M)
  LLDONE=.TRUE.
ELSEIF (IAQ2M > 0) THEN
  ZRHCLS(KST:KEND)=PROW(KST:KEND,IAQ2M)
ELSE
  LLRHCLS=.FALSE.
ENDIF
IF (LLRHCLS.AND..NOT.LLDONE) THEN
  IF (LLRHS) THEN
    ! Anomaly HU2m-HUs => HU2m
    IF ((LDFPOSHOR.OR.NFPCLI >0).AND.GFP%X2RH%IANO > 0) THEN
      ZRHCLS(KST:KEND)=ZRHCLS(KST:KEND)+ZRHS(KST:KEND)
    ENDIF
  ELSEIF (GFP%X2RH%IANO > 0) THEN 
    CALL ABOR1('FPCORPHY : CANNOT FIND SURF. REL MOISTURE')
  ENDIF
  IF (LMPHYS) THEN
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB
    CALL FPHOR12(1,KST,KEND,ZRHCLS,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)  
  ENDIF
ENDIF

IF (IAQ2M > 0) THEN
  PROW(KST:KEND,IAQ2M)=ZRHCLS(KST:KEND)
ENDIF

!     3.3 Altitude correction for interpolated fields

IF (PRESENT(LDALT)) THEN
  LLALT=LDALT
ELSE
  LLALT=.TRUE.
  IF (ICFIS > 0) THEN
    ZDTSGDZ=RDTDZ1/RG
    IF (IXFIS > 0) THEN
      DO JI=KST,KEND
        ZALT(JI)=ZDTSGDZ*(PCLI(JI,ICFIS)-PAUX(JI,IXFIS))
      ENDDO
    ELSEIF (IAFIS > 0) THEN
      DO JI=KST,KEND
        ZALT(JI)=ZDTSGDZ*(PCLI(JI,ICFIS)-PROW(JI,IAFIS))
      ENDDO
    ELSE
      LLALT=.FALSE.
    ENDIF
  ELSE
    LLALT=.FALSE.
  ENDIF
ENDIF

!     3.4 SST (for the time being : it is an "interpolated" SST)

IX=IXSST
IA=IASST
IANO=GFP%SST%IANO

LL=.TRUE.
LLDONE=.FALSE.
IF(IX > 0) THEN
  ZTMP(KST:KEND)=PAUX(KST:KEND,IX)
  LLDONE=.TRUE.
ELSEIF (IA > 0) THEN
  ZTMP(KST:KEND)=PROW(KST:KEND,IA)
ELSE
  LL=.FALSE.
ENDIF

IF (LL.AND..NOT.LLDONE) THEN
  IF (IANO == 0 .OR. NFPCLI <= 2) THEN
    IF (NFPCLI >= 1) THEN
      IF (LLALT) THEN
        ZTMP(KST:KEND)=ZTMP(KST:KEND)+ZALT(KST:KEND)
      ELSE
        IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
      ENDIF
    ENDIF
  ELSE
    CALL ABOR1('FPCORPHY: SST + NFPCLI >= 3 NOT YET CODED')
  ENDIF
  ZMIN(KST:KEND)=0.0_JPRB
  ICOR=4
  CALL FPHOR12(1,KST,KEND,ZTMP,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF

IF (IA > 0) THEN
  PROW(KST:KEND,IA)=ZTMP(KST:KEND)
ENDIF


LLSST=LL
IF (LLSST) THEN
  ZSST(KST:KEND)=ZTMP(KST:KEND)
ENDIF


!     3.4 Interpolated Surface temperature + altitude correction
!         (for internal use only)

LLTSA=.TRUE.
IF (IXTSI > 0) THEN
  ZTSA(KST:KEND)=PAUX(KST:KEND,IXTSI)
ELSEIF (IATSI > 0) THEN
  ZTSA(KST:KEND)=PROW(KST:KEND,IATSI)
ELSE
  LLTSA=.FALSE.
ENDIF

IF (LLTSA) THEN
! If 2 masks are used, make a combination of interpolated Ts and SST :
  IF (NFPMASK >= 2) THEN
    IF (.NOT.LLSST) CALL ABOR1('FPCORPHY:CAN T FIND SST')
    IF (.NOT.LLAND) CALL ABOR1('FPCORPHY:CAN T FIND LAND PROPORTION')
    DO JI=KST,KEND
      ZTSA(JI)=ZLAN(JI)*ZTSA(JI)+(1._JPRB-ZLAN(JI))*ZSST(JI)
    ENDDO
  ENDIF
ENDIF
IF (IATSI > 0) THEN
  PROW(KST:KEND,IATSI)=ZTSA(KST:KEND)
ENDIF

!     3.4 Surface temperature

IX=IXTS0
IA=IATS0
IANO=GFP%ST%IANO

LL=.TRUE.
LLDONE=.FALSE.
IF(IX > 0) THEN
  ZTMP(KST:KEND)=PAUX(KST:KEND,IX)
  LLDONE=.TRUE.
ELSEIF (IA > 0) THEN
  ZTMP(KST:KEND)=PROW(KST:KEND,IA)
ELSE
  LL=.FALSE.
ENDIF

IF (LL.AND..NOT.LLDONE) THEN
  IF (.NOT. (IANO == 0 .OR. NFPCLI <= 2)) THEN
    IF (ICTS > 0) THEN
      ! Anomaly Ts-T*  => Ts
      ZTMP(KST:KEND)=ZTMP(KST:KEND)+PCLI(KST:KEND,ICTS)
    ELSE
      CALL ABOR1('FPCORPHY : NEEDS CLIM. SURFTEMPERATURE')
    ENDIF
  ELSEIF (NFPCLI > 0) THEN
    IF (LLALT) THEN
      ZTMP(KST:KEND)=ZTMP(KST:KEND)+ZALT(KST:KEND)
    ELSE
      IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
    ENDIF
  ENDIF
  IF (NFPLAKE == 1) THEN
    IF (ICTS > 0) THEN
      ! correction for lakes & islands with climatology
      CALL FPLAKE(PCLI(1,ICTS),ZTMP)
    ELSE
      CALL ABOR1('FPCORPHY : NEEDS CLIM. SURFTEMPERATURE')
    ENDIF
  ELSEIF (NFPLAKE == -1) THEN
    ! correction for lakes & islands with interpolated field
    IF (LLTSA) THEN
      CALL FPLAKE(ZTSA,ZTMP)
    ELSE
      CALL ABOR1('FPCORPHY : NEEDS INTERPOLATED SURFTEMPERATURE')
    ENDIF
  ENDIF
  ZMIN(KST:KEND)=0.0_JPRB
  ICOR=4
  CALL FPHOR12(1,KST,KEND,ZTMP,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IA > 0) THEN
  PROW(KST:KEND,IA)=ZTMP(KST:KEND)
ENDIF

LLTS=LL
IF (LLTS) THEN
  ZTS(KST:KEND)=ZTMP(KST:KEND)
ENDIF

!     4.1 Deep soil temperature

IX=IXTD
IA=IATD
IANO=GFP%DST%IANO

LL=.TRUE.
LLDONE=.FALSE.
IF(IX > 0) THEN
  ZTMP(KST:KEND)=PAUX(KST:KEND,IX)
  LLDONE=.TRUE.
ELSEIF (IA > 0) THEN
  ZTMP(KST:KEND)=PROW(KST:KEND,IA)
ELSE
  LL=.FALSE.
ENDIF

IF (LL.AND..NOT.LLDONE) THEN
  IF (LLSM.AND.LLTS) THEN
    IF (IANO > 0) THEN
      IF (NFPCLI <= 2) THEN
!       Anomaly Tp-Ts  => Tp
        ZTMP(KST:KEND)=ZTMP(KST:KEND)+ZTS(KST:KEND)
      ELSEIF (ICDT > 0) THEN
!       Anomaly Tp-Tp*  => Tp
        DO JI=KST,KEND
          ZTMP(JI)=ZTMP(JI)+PCLI(JI,ICDT)
        ENDDO
        IF (NFPLAKE /= 0) THEN
!         correction for lakes & islands
          CALL FPLAKE(PCLI(1,ICDT),ZTMP)
        ENDIF
      ELSE
        CALL ABOR1('FPCORPHY : NEEDS CLIM. DEEP SOIL TEMPERATURE')
      ENDIF
    ENDIF
    ICOR=3
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=+99999._JPRB
    ZSEA(KST:KEND)=ZTS(KST:KEND)
    ZSCAL(KST:KEND)=1.0_JPRB
    CALL FPHOR12(1,KST,KEND,ZTMP,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
  ELSE
    CALL ABOR1('FPCORPHY : TD NEEDS LSM AND TS')
  ENDIF
ENDIF
IF (IA > 0) THEN
  PROW(KST:KEND,IA)=ZTMP(KST:KEND)
ENDIF

LLTD=LL
IF (LLTD) THEN
  ZTD(KST:KEND)=ZTMP(KST:KEND)
ENDIF




!     3.5 Climatology snow depth

IF (ICSD > 0) THEN
  LLCSN=.TRUE.
  IF (LLSM) THEN
    IF (LCRITSNOWTEMP) THEN
      ICOR=2
      ZSEA(KST:KEND)=PCLI(KST:KEND,ICTS)
      ZTT(KST:KEND)=RTT
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      ICOR=1
      ZSEA(KST:KEND)=0.0_JPRB
      ZMIN(KST:KEND)=0.0_JPRB
    ENDIF
    ZCSN(KST:KEND)=PCLI(KST:KEND,ICSD)
  ELSE
    CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
  ENDIF
  CALL FPHOR12(1,KST,KEND,ZCSN,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ELSE
  LLCSN=.FALSE.
ENDIF

!     3.5 Proportion of surface water content

LLCSSW=.TRUE.
IF (ICSSW > 0) THEN
  ZCSSW(KST:KEND)=PCLI(KST:KEND,ICSSW)
ELSEIF (IASSW > 0) THEN
  ZCSSW(KST:KEND)=PROW(KST:KEND,IASSW)
ELSE
  LLCSSW=.FALSE.
ENDIF
IF (LLCSSW) THEN
  IF (LMPHYS) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=1.0_JPRB
      ZSEA(KST:KEND)=1.0_JPRB
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF
    CALL FPHOR12(1,KST,KEND,ZCSSW,1,KFPROMA,ICOR,ZSEA,ZLSM,&
     & ZMIN,ZMAX,ZSCAL,ZTT)  
  ENDIF
ENDIF
IF (IASSW > 0) THEN
  PROW(KST:KEND,IASSW)=ZCSSW(KST:KEND)
ENDIF

!     3.6 Proportion of deep soil water content

LLCDSW=.TRUE.
IF (ICDSW > 0) THEN
  ZCDSW(KST:KEND)=PCLI(KST:KEND,ICDSW)
ELSEIF (IADSW > 0) THEN
  ZCDSW(KST:KEND)=PROW(KST:KEND,IADSW)
ELSE
  LLCDSW=.FALSE.
ENDIF
IF (LLCDSW) THEN
  IF (LMPHYS) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=1.0_JPRB
      ZSEA(KST:KEND)=1.0_JPRB
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF
    CALL FPHOR12(1,KST,KEND,ZCDSW,1,KFPROMA,ICOR,ZSEA,ZLSM,&
     & ZMIN,ZMAX,ZSCAL,ZTT)  
  ENDIF
ENDIF
IF (IADSW > 0) THEN
  PROW(KST:KEND,IADSW)=ZCDSW(KST:KEND)
ENDIF

!     3.3 Cls temperature

LLTCLS=.TRUE.
LLDONE=.FALSE.
IF (IXT2M > 0) THEN
  ZTCLS(KST:KEND)=PAUX(KST:KEND,IXT2M)
  LLDONE=.TRUE.
ELSEIF (IAT2M > 0) THEN
  ZTCLS(KST:KEND)=PROW(KST:KEND,IAT2M)
ELSE
  LLTCLS=.FALSE.
ENDIF

IF (LLTCLS.AND..NOT.LLDONE) THEN
  IF ((LDFPOSHOR.OR.NFPCLI >0) .AND. GFP%X2T%IANO > 0) THEN
!   Anomaly T2m-Ts => T2m
    IF (LLTS) THEN
      ZTCLS(KST:KEND)=ZTCLS(KST:KEND)+ZTS(KST:KEND)
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND SURFACE TEMPERATURE')
    ENDIF
  ENDIF
  ZMIN(KST:KEND)=0.0_JPRB
  ICOR=4
  CALL FPHOR12(1,KST,KEND,ZTCLS,1,KFPROMA,ICOR,ZSEA,ZLSM,ZMIN,ZMAX,ZSCAL,ZTT)
ENDIF
IF (IAT2M > 0) THEN
  PROW(KST:KEND,IAT2M)=ZTCLS(KST:KEND)
ENDIF

!    3.4.1 U,V Gust of Wind components 

LLGST=.TRUE.
LLDONE=.FALSE.
IF (IXUGST > 0) THEN
  ZUVRAF(KST:KEND)=PAUX(KST:KEND,IXUGST)
  LLDONE=.TRUE.
ELSEIF (IAUGST > 0) THEN
  ZUVRAF(KST:KEND)=PROW(KST:KEND,IAUGST)
ELSE
  LLGST=.FALSE.
ENDIF
IF (IAUGST > 0) THEN
  PROW(KST:KEND,IAUGST)=ZUVRAF(KST:KEND)
ENDIF

LLGST=.TRUE.
LLDONE=.FALSE.
IF (IXVGST > 0) THEN
  ZUVRAF(KST:KEND)=PAUX(KST:KEND,IXVGST)
  LLDONE=.TRUE.
ELSEIF (IAVGST > 0) THEN
  ZUVRAF(KST:KEND)=PROW(KST:KEND,IAVGST)
ELSE
  LLGST=.FALSE.
ENDIF
IF (IAVGST > 0) THEN
  PROW(KST:KEND,IAVGST)=ZUVRAF(KST:KEND)
ENDIF

!    3.4.2 U,V Gust2 of Wind components 

LLGST2=.TRUE.
LLDONE=.FALSE.
IF (IAUGST2 > 0) THEN
  ZUVRAF(KST:KEND)=PROW(KST:KEND,IAUGST2)
ELSE
  LLGST2=.FALSE.
ENDIF
IF (IAUGST2 > 0) THEN
  PROW(KST:KEND,IAUGST2)=ZUVRAF(KST:KEND)
ENDIF

LLGST2=.TRUE.
LLDONE=.FALSE.
IF (IAVGST2 > 0) THEN
  ZUVRAF(KST:KEND)=PROW(KST:KEND,IAVGST2)
ELSE
  LLGST2=.FALSE.
ENDIF
IF (IAVGST2 > 0) THEN
  PROW(KST:KEND,IAVGST2)=ZUVRAF(KST:KEND)
ENDIF


!*       4. CURRENT CORRECTIONS ON OTHER FIELDS
!           -----------------------------------

DO JF=1,KFIELDS

!       Security presets :
  ICOR=0
  ZSEA (KST:KEND)=-99999._JPRB
  ZMIN (KST:KEND)=+99999._JPRB
  ZMAX (KST:KEND)=-99999._JPRB
  ZSCAL(KST:KEND)=+10000._JPRB
  ZTT  (KST:KEND)=-99999._JPRB

  ICOD=KCOD(JF)
  IF (LDFPOSHOR.OR.NFPCLI >0) THEN
    IANO=GFP_PHYDS(ICOD)%IANO
  ELSE
!   No interpolation nor change of physiography => only buffers transfers
    IANO=0
  ENDIF

!     4.2 Surface water content

  IF (ICOD == GFP%SSW%ICOD) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      IF (LLARG .AND. LLD2 .AND. LLIVG .AND. LLSAB) THEN
        LLHMT=.FALSE.
        CALL ACSOLW(YDPHY1,KST,KEND,KFPROMA,ZARG,ZD2,ZLSM,ZIVG,ZSAB,LLHMT,ZWFC,ZWPMX,ZWSAT,ZWSMX,ZWWILT)
      ELSEIF(LMPHYS) THEN
        ! Wsmax is the max. value from input field
        ZWSMX(KST:KEND)=PWSXI
      ELSEIF(LEPHYS) THEN
        CALL ABOR1('FPCORPHY : MISSING FIELDS TO CALL ACSOLW FOR SURFACE SOIL WATER CONTENT !')
      ENDIF
      IF (LEPHYS) THEN
        ZSEA(KST:KEND)=1.0_JPRB
        ZMAX(KST:KEND)=1.0_JPRB
        IF (LLTS) THEN
          IF (NFPSWI == 0) THEN
            ZSCAL(KST:KEND)=ZWSMX(KST:KEND)
            DO JI=KST,KEND
              IF (ZTS(JI) > (TSRESERV1+TDELTA1)) THEN
                ZPGLACE=0.0_JPRB
              ELSEIF (ZTS(JI) < (TSRESERV1-TDELTA1)) THEN
                ZPGLACE=1.0_JPRB
              ELSE
                ZDELTA=((ZTS(JI)-TSRESERV1)/(2.0*TDELTA1))-0.5_JPRB
                ZPGLACE=(3.0_JPRB*ZDELTA**2)+(2.0_JPRB*ZDELTA**3)
              ENDIF
              ZPGLACE=MIN(ZPGLACE,1.0_JPRB)
              PROW(JI,JF)=PROW(JI,JF)*(1._JPRB-ZPGLACE)
            ENDDO
          ELSE
            ZSCAL(KST:KEND)=ZWFC(KST:KEND)*RD1*GCONV
            DO JI=KST,KEND
              IF (ZTS(JI) > TSRESERV1+TDELTA1) THEN
                ZPGLACE=0.0_JPRB
              ELSEIF (ZTS(JI) < TSRESERV1-TDELTA1) THEN
                ZPGLACE=1.0_JPRB
              ELSE
                ZDELTA = RPI*(ZTS(JI)-TSRESERV1)/(2.0_JPRB*TDELTA1)
                ZPGLACE = 0.5_JPRB* (1.0_JPRB-SIN(ZDELTA))
              ENDIF
              PROW(JI,JF)=PROW(JI,JF)*(1.0_JPRB-ZPGLACE)
            ENDDO
          ENDIF
        ELSE
          CALL ABOR1('FPCORPHY : TS IS NEEDED')
        ENDIF
      ELSEIF (LMPHYS) THEN
        IF (IANO == 0) THEN
          ZSEA(KST:KEND)=ZWSMX(KST:KEND)
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=1.0_JPRB
        ELSE
          ZSEA(KST:KEND)=1.0_JPRB
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=ZWSMX(KST:KEND)
          IF (NFPCLI > 2) THEN
            IF (LLCSSW) THEN
              ! (Ws/Wsmax - Wsr) => Ws/Wsmax
              PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZCSSW(KST:KEND)
            ELSE
              CALL ABOR1('FPCORPHY : CLIM.PROP. OF SURF.WATER CONTENT MISSING')
            ENDIF
          ENDIF
        ENDIF
        IF (NFPLAKE /= 0) THEN
          IF (LLCSSW) THEN
            ! Correction for lakes & islands
            CALL FPLAKE(ZCSSW,PROW(1,JF))
          ELSE
            CALL ABOR1('FPCORPHY : CLIM.PROP. OF SURF.WATER CONTENT MISSING FOR LAKE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : LAND-SEA MASK IS NEEDED FOR WATER CONTENTS')
    ENDIF

!     4.3 Surface frost

  ELSEIF (ICOD == GFP%FSSW%ICOD) THEN
    IF (LLSM) THEN
      ZMIN(KST:KEND)=0.0_JPRB
      ZSEA(KST:KEND)=0.0_JPRB
      IF (LLARG .AND. LLD2 .AND. LLIVG .AND. LLSAB) THEN
        LLHMT=.FALSE.
        CALL ACSOLW(YDPHY1,KST,KEND,KFPROMA,ZARG,ZD2,ZLSM,ZIVG,ZSAB,LLHMT,ZWFC,ZWPMX,ZWSAT,ZWSMX,ZWWILT)
      ELSEIF(LMPHYS) THEN
        ! Wsmax is the max. value from input field
        ZWSMX(KST:KEND)=PWSXI
      ELSEIF(NFPSWI == 0) THEN
        CALL ABOR1('FPCORPHY : MISSING FIELDS TO CALL ACSOLW FOR SURFACE SOIL ICE CONTENT !')
      ENDIF
      IF(LEPHYS) THEN
        ICOR=3
        IF (LLTS) THEN
          IF (NFPSWI == 0) THEN
            ZSCAL(KST:KEND)=ZWSMX(KST:KEND)
            ZMAX(KST:KEND)=1.0_JPRB
            DO JI=KST,KEND
              IF (ZTS(JI) > (TSRESERV1+TDELTA1)) THEN
                ZPGLACE=0.0_JPRB
              ELSEIF (ZTS(JI) < (TSRESERV1-TDELTA1)) THEN
                ZPGLACE=1.0_JPRB
              ELSE
                ZDELTA=((ZTS(JI)-TSRESERV1)/(2.0*TDELTA1))-0.5_JPRB
                ZPGLACE=(3.0_JPRB*ZDELTA**2)+(2.0_JPRB*ZDELTA**3)
              ENDIF
              ZPGLACE=MIN(ZPGLACE,1.0_JPRB)
              PROW(JI,JF)=PROW(JI,JF)*ZPGLACE
            ENDDO
          ELSE
            ZSCAL(KST:KEND)=ZWFC(KST:KEND)*RD1*GCONV
            ! The max value is relative (applied before scaling)
            ZMAX(KST:KEND)=GWPIMX/ZSCAL(KST:KEND)
            ! Part of surface water content layer #1
            DO JI=KST,KEND
              IF (ZTS(JI) > TSRESERV1+TDELTA1) THEN
                ZPGLACE=0.0_JPRB
              ELSEIF (ZTS(JI) < TSRESERV1-TDELTA1) THEN
                ZPGLACE=1.0_JPRB
              ELSE
                ZDELTA = RPI*(ZTS(JI)-TSRESERV1)/(2.0_JPRB*TDELTA1)
                ZPGLACE = 0.5_JPRB* (1.0_JPRB-SIN(ZDELTA))
              ENDIF
              PROW(JI,JF)=PROW(JI,JF)*ZPGLACE
            ENDDO
          ENDIF
        ELSE
          CALL ABOR1('FPCORPHY : TS IS NEEDED')
        ENDIF
      ELSEIF (LMPHYS) THEN
        IF (IANO == 0) THEN
          ICOR=1
        ELSE
          ICOR=3
          ZSCAL(KST:KEND)=ZWSMX(KST:KEND)
          ZMAX(KST:KEND)=1.0_JPRB
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : CAN T COMPUTE WATER CONTENTS')
    ENDIF

!     4.4 Deep soil water content

  ELSEIF (ICOD == GFP%DSW%ICOD) THEN
    IF (LLSM) THEN
      ICOR=3
      IF (LLARG .AND. LLD2 .AND. LLIVG .AND. LLSAB) THEN
        LLHMT=.FALSE.
        CALL ACSOLW(YDPHY1,KST,KEND,KFPROMA,ZARG,ZD2,ZLSM,ZIVG,ZSAB,LLHMT,ZWFC,ZWPMX,ZWSAT,ZWSMX,ZWWILT)
      ELSEIF (LMPHYS) THEN
        ! Wsmax is the max. value from input field
        ZWPMX(KST:KEND)=PWDXI
      ELSEIF (LEPHYS) THEN
        CALL ABOR1('FPCORPHY : MISSING FIELDS TO CALL ACSOLW FOR DEEP SOIL WATER CONTENT !')
      ENDIF
      IF (LEPHYS) THEN
        IF (NFPSWI > 0) THEN
          ZMIN(KST:KEND)=ZWWILT(KST:KEND)
          ZSEA(KST:KEND)=ZWFC(KST:KEND)
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=ZD2(KST:KEND)*GCONV
          IF (NFPSWI == 1) THEN
            IF (LLVEG) THEN
              ! Does OI trick
              DO JI=KST,KEND
                PROW(JI,JF) = PROW(JI,JF)*(ZWFC(JI)-ZVEG(JI)*ZWWILT(JI)) + ZVEG(JI)*ZWWILT(JI)
              ENDDO
            ELSE
              CALL ABOR1('FPCORPHY : PROP. OF VEGETATION IS NEEDED WITH NFPSWI > 0 !')
            ENDIF
          ENDIF
        ELSEIF (LLTD) THEN
          ZMIN(KST:KEND)=0.0_JPRB
          ZSEA(KST:KEND)=1.0_JPRB
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=ZWPMX(KST:KEND)
!DEC$ IVDEP
          DO JI=KST,KEND
            IF (ZTD(JI) > (TSRESERV2+TDELTA2)) THEN
              ZPGLACE=0.0_JPRB
            ELSEIF (ZTD(JI) < (TSRESERV2-TDELTA2)) THEN
              ZPGLACE=1.0_JPRB
            ELSE
              ZDELTA=((ZTD(JI)-TSRESERV2)/(2.0_JPRB*TDELTA2)) - 0.5_JPRB
              ZPGLACE=(3.0_JPRB*ZDELTA**2) + (2.0_JPRB*ZDELTA**3)
            ENDIF
            ZPGLACE=MIN(ZPGLACE,GWPIMX/ZWPMX(JI))
            PROW(JI,JF)=PROW(JI,JF)*(1._JPRB-ZPGLACE)
          ENDDO
        ELSE
          CALL ABOR1('FPCORPHY : TD IS NEEDED !')
        ENDIF
      ELSEIF (LMPHYS) THEN
        ZMIN(KST:KEND)=0.0_JPRB
        IF (IANO == 0) THEN
          ZSEA(KST:KEND)=ZWPMX(KST:KEND)
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=1.0_JPRB
        ELSE
          ZSEA(KST:KEND)=1.0_JPRB
          ZMAX(KST:KEND)=ZSEA(KST:KEND)
          ZSCAL(KST:KEND)=ZWPMX(KST:KEND)
          IF (NFPCLI > 2) THEN
            IF (LLCDSW) THEN
              ! (Wp/Wpmax - Wpr) => Wp/Wpmax
              PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZCDSW(KST:KEND)
            ELSE
              CALL ABOR1('FPCORPHY : CLIM.PROP. OF DEEP SOIL.WATER MISSING')
            ENDIF
          ENDIF
        ENDIF
        IF (NFPLAKE /= 0) THEN
          IF (LLCDSW) THEN
            !  Correction for lakes & islands
            CALL FPLAKE(ZCDSW,PROW(1,JF))
          ELSE
            CALL ABOR1('FPCORPHY : CLIM.PROP. OF DEEP SOIL WATER CONTENT MISSING FOR LAKE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : CAN T COMPUTE WATER CONTENTS')
    ENDIF

!     4.5 Deep soil ice content

  ELSEIF (ICOD == GFP%FDSW%ICOD) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      ZSEA(KST:KEND)=0.0_JPRB
      IF (LLARG .AND. LLD2 .AND. LLIVG .AND. LLSAB) THEN
        LLHMT=.FALSE.
        CALL ACSOLW(YDPHY1,KST,KEND,KFPROMA,ZARG,ZD2,ZLSM,ZIVG,ZSAB,LLHMT,ZWFC,ZWPMX,ZWSAT,ZWSMX,ZWWILT)
      ELSEIF (LMPHYS) THEN
        ! Wsmax is the max. value from input field
        ZWPMX(KST:KEND)=PWDXI
      ELSEIF (LEPHYS) THEN
        CALL ABOR1('FPCORPHY : MISSING FIELDS TO CALL ACSOLW FOR DEEP SOIL ICE CONTENT !')
      ENDIF
      IF (LEPHYS) THEN
        IF (NFPSWI > 0) THEN
          ZSCAL(KST:KEND)=GCONV*ZD2(KST:KEND)
        ELSEIF (LLTD) THEN
          ZSCAL(KST:KEND)=ZWPMX(KST:KEND)
          DO JI=KST,KEND
            IF (ZTD(JI) > (TSRESERV2+TDELTA2)) THEN
              ZPGLACE=0.0_JPRB
            ELSEIF (ZTD(JI) < (TSRESERV2-TDELTA2)) THEN
              ZPGLACE=1.0_JPRB
            ELSE
              ZDELTA=((ZTD(JI)-TSRESERV2)/(2.0_JPRB*TDELTA2)) - 0.5_JPRB
              ZPGLACE=(3.0_JPRB*ZDELTA**2) + (2.0_JPRB*ZDELTA**3)
            ENDIF
            ZPGLACE=MIN(ZPGLACE,1.0_JPRB)
            PROW(JI,JF)=PROW(JI,JF)*ZPGLACE
          ENDDO
        ELSE
          CALL ABOR1('FPCORPHY : TD IS NEEDED !')
        ENDIF
        ! Better unscale before ..
        ZMAX(KST:KEND)=MIN(1.0_JPRB,GWPIMX/ZSCAL(KST:KEND))
      ELSEIF (LMPHYS) THEN
        IF (IANO == 0) THEN
          ZSCAL(KST:KEND)=1.0_JPRB
          ZMAX(KST:KEND)=GWPIMX
        ELSE
          ZSCAL(KST:KEND)=ZWPMX(KST:KEND)
          ! Better unscale before ..
          ZMAX(KST:KEND)=MIN(1.0_JPRB,GWPIMX/ZSCAL(KST:KEND))
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : CAN T COMPUTE WATER CONTENTS')
    ENDIF

!     4.6 ECMWF extra fields : soil reservoir contents

  ELSEIF (ICOD == GFP%SRC%ICOD) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=MAXVAL(ZRVLAI(1:IVTYPES))*ZRWLMAX
      ZSEA(KST:KEND)=0.0_JPRB
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF

!     4.7 ECMWF extra fields : Charnock parameter

  ELSEIF (ICOD == GFP%CHAR%ICOD) THEN
!         => use option #1 by reversing land and sea roles :
    IF (LLSM) THEN
      ICOR=1
      DO JI=KST,KEND
        ZCLI(JI)=1.0_JPRB-ZLSM(JI)
      ENDDO
      ZSEA(KST:KEND)=0.018_JPRB
      ZMIN(KST:KEND)=0.005_JPRB
      CALL FPHOR12(JF,KST,KEND,PROW,KFIELDS,KFPROMA,ICOR,ZSEA,&
       & ZCLI,ZMIN,ZMAX,ZSCAL,ZTT)  
      ICOR=0
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF

!     4.8 ECMWF extra fields SWL1, SWL2, SWL3, SWL4

  ELSEIF (ICOD == GFP%SWL1%ICOD .OR. ICOD == GFP%SWL2%ICOD .OR.&
    &     ICOD == GFP%SWL3%ICOD .OR. ICOD == GFP%SWL4%ICOD) THEN
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=ZRWSAT
      ZSEA(KST:KEND)=0.0_JPRB
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF

!     4.9 ECMWF extra fields STL1, STL2, STL3, STL4

  ELSEIF (ICOD == GFP%STL1%ICOD .OR. ICOD == GFP%STL2%ICOD .OR.&
    &     ICOD == GFP%STL3%ICOD .OR. ICOD == GFP%STL4%ICOD) THEN
    IF ((.NOT.LDFPOSHOR) .OR. IANO == 0 .OR. NFPCLI <= 1) THEN
      ICOR=0
      IF (NFPCLI == 1) THEN
        IF (LEPHYS) THEN
          IF (LLALT) THEN
            PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZALT(KST:KEND)
          ELSE
            IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY: STL + NFPCLI >= 2 NOT YET CODED')
    ENDIF

!     4.10 ECMWF extra fields ISTL1, ISTL2, ISTL3, ISTL4

  ELSEIF (ICOD == GFP%ISTL1%ICOD .OR. ICOD == GFP%ISTL2%ICOD .OR.&
    &     ICOD == GFP%ISTL3%ICOD .OR. ICOD == GFP%ISTL4%ICOD) THEN
    IF ((.NOT.LDFPOSHOR) .OR. IANO == 0 .OR. NFPCLI <= 1) THEN
      ICOR=0
      IF (NFPCLI == 1) THEN
        IF (LEPHYS) THEN
          IF (LLALT) THEN
            PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZALT(KST:KEND)
          ELSE
            IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY: ISTL + NFPCLI >= 2 NOT YET CODED')
    ENDIF

!     4.11 Skin temperature

  ELSEIF (ICOD == GFP%SKT%ICOD) THEN
    IF ((.NOT.LDFPOSHOR) .OR. IANO == 0 .OR. NFPCLI <= 1) THEN
      ICOR=0
      IF (NFPCLI == 1) THEN
        IF (LEPHYS) THEN
          IF (LLALT) THEN
            PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZALT(KST:KEND)
          ELSE
            IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY: SKT + NFPCLI >= 2 NOT YET CODED')
    ENDIF

!     4.12 Snow temperature

  ELSEIF (ICOD == GFP%TSN%ICOD) THEN
    IF ((.NOT.LDFPOSHOR) .OR. IANO == 0 .OR. NFPCLI <= 1) THEN
      ICOR=0
      IF (NFPCLI == 1) THEN
        IF (LEPHYS) THEN
          IF (LLALT) THEN
            PROW(KST:KEND,JF)=MIN(PROW(KST:KEND,JF)+ZALT(KST:KEND),RTT)
          ELSE
            IF (.NOT.PRESENT(LDALT)) CALL ABOR1('FPCORPHY : CAN T FIND ALTITUDE CORRECTION')
          ENDIF
        ENDIF
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY: TSN + NFPCLI >= 2 NOT YET CODED')
    ENDIF

  ELSEIF (ICOD == GFP%ASN%ICOD) THEN
!         ECMWF Snow albedo
    IF (LLSM) THEN
      ICOR=3
      ZMIN(KST:KEND)=ZRALFMINSN
      ZMAX(KST:KEND)=ZRALFMAXSN
      ZSEA(KST:KEND)=ZRALBSEAD
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF
  ELSEIF (ICOD == GFP%CI%ICOD) THEN
!         Ice concentration
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB
  ELSEIF (ICOD == GFP%RSN%ICOD) THEN
!         Snow density
    ICOR=6
    ZMIN(KST:KEND)=ZRHOMINSN
    ZMAX(KST:KEND)=ZRHOMAXSN
  ELSEIF (ICOD == GFP%CVH%ICOD) THEN
!         High vegetation fraction
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB
  ELSEIF (ICOD == GFP%CVL%ICOD) THEN
!         Low vegetation fraction
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB

!     4.13 Snow cover

  ELSEIF (ICOD == GFP%SD%ICOD) THEN
    IF (LLSM) THEN
      IF (LCRITSNOWTEMP) THEN
        IF (LLTS) THEN
          ICOR=2
          ZSEA(KST:KEND)=ZTS(KST:KEND)
          ZTT(KST:KEND)=RTT
          ZSCAL(KST:KEND)=1.0_JPRB
        ELSE
          CALL ABOR1('FPCORPHY : CANNOT FIND SURF.TEMPERATURE')
        ENDIF
      ELSE
        ICOR=1
        ZSEA(KST:KEND)=0.0_JPRB
        ZMIN(KST:KEND)=0.0_JPRB
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
    ENDIF
    IF (.NOT.(IANO == 0 .OR. NFPCLI <= 2)) THEN
      IF (NFPCLI > 2) THEN
        ! Snow cover anomaly => Snow cover
        IF (LLCSN) THEN
          PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZCSN(KST:KEND)
        ELSE
          CALL ABOR1('FPCORPHY : CANNOT FIND CLIM. SNOW DEPTH')
        ENDIF
      ENDIF
    ENDIF

!     4.14 Albedo

  ELSEIF (ICOD == GFP%AL%ICOD  .OR. ICOD == GFP%BAAL%ICOD .OR. ICOD == GFP%ALBHIS%ICOD .OR. &
     & ICOD == GFP%ALS%ICOD .OR. ICOD == GFP%ALV%ICOD  ) THEN  
    IF (LMPHYS) THEN
      ICOR=6
      ZMIN(KST:KEND)=YRCLI%SALBN
      ZMAX(KST:KEND)=YRCLI%SALBX
    ELSE
      ICOR=0
    ENDIF

!     4.15 Emissivity

  ELSEIF (ICOD == GFP%EMIS%ICOD) THEN
    IF (LMPHYS) THEN
      ICOR=6
      ZMIN(KST:KEND)=YRCLI%SEMIN
      ZMAX(KST:KEND)=YRCLI%SEMIX
    ELSE
      ICOR=0
    ENDIF

!     4.16 Maximum proportion of vegetation

  ELSEIF (ICOD == GFP%Z0V%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=3
        ZMIN(KST:KEND)=0.0_JPRB
        ZMAX(KST:KEND)=1.0_JPRB
        ZSEA(KST:KEND)=0.0_JPRB
        ZSCAL(KST:KEND)=1.0_JPRB 
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.17 Model Roughness lenth * g

  ELSEIF (ICOD == GFP%IDZ0%ICOD .OR. ICOD == GFP%ITZ0%ICOD) THEN
    IF (LMPHYS) THEN
      ICOR=4
      ZMIN(KST:KEND)=0.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.17 Climatology Roughness lenth * g

  ELSEIF (ICOD == GFP%SR%ICOD .OR. ICOD == GFP%BSR%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=1
        ZMIN(KST:KEND)=YRCLI%SZZ0N*RG
        ZSEA(KST:KEND)=YRCLI%SZZ0M*RG
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.18 Climatology Thermal roughness lenth * g

  ELSEIF (ICOD == GFP%Z0H%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=1
        ZMIN(KST:KEND)=YRCLI%STHER*YRCLI%SZZ0N*RG
        ZSEA(KST:KEND)=YRCLI%SZZ0M*RG
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.19 Leaf Area Index ; vegetation roughness lenth * g

  ELSEIF (ICOD == GFP%LAI%ICOD .OR. ICOD == GFP%PVGMX%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=1
        ZMIN(KST:KEND)=0.0_JPRB
        ZSEA(KST:KEND)=0.0_JPRB
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.20 Stomatal Minimum resistance

  ELSEIF (ICOD == GFP%RSMIN%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=3
        ZMIN(KST:KEND)=YRCLI%SRSMN
        ZMAX(KST:KEND)=YRCLI%SRSMX
        ZSEA(KST:KEND)=YRCLI%SRSMX
        ZSCAL(KST:KEND)=1.0_JPRB
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.21 Meteo-France Snow albedo

  ELSEIF (ICOD == GFP%ALSN%ICOD) THEN
    ICOR=6
    ZMIN(KST:KEND)=ALBMIN
    ZMAX(KST:KEND)=ALBMAX

!     4.22 Snow density

  ELSEIF (ICOD == GFP%SNDE%ICOD) THEN
    ICOR=6
    ZMIN(KST:KEND)=RHOMIN
    ZMAX(KST:KEND)=RHOMAX

!     4.23 Anisotropy coefficient of topography

  ELSEIF (ICOD == GFP%ACOT%ICOD) THEN
    IF (LMPHYS) THEN
      ICOR=4
      ZMIN(KST:KEND)=0.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.24 Direction of principal axis of topography

  ELSEIF (ICOD == GFP%DPAT%ICOD) THEN
    DO JI=KST,KEND
      PROW(JI,JF)=ATAN(TAN(PROW(JI,JF)))
    ENDDO
    ICOR=0

!     4.25 Percentage of urbanisation

  ELSEIF (ICOD == GFP%PURB%ICOD) THEN
    IF (LMPHYS) THEN
      ICOR=6
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=1.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.26 Maximum soil depth

  ELSEIF (ICOD == GFP%D2MX%ICOD) THEN
    IF (LMPHYS) THEN
      ICOR=3
      ZMIN(KST:KEND)=YRCLI%SDEPN
      ZMAX(KST:KEND)=YRCLI%SDEPX
      ZSEA(KST:KEND)=YRCLI%SDEPX
      ZSCAL(KST:KEND)=1.0_JPRB
      IF (.NOT.LLSM) THEN
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.27 Relaxation relative deep soil wetness

  ELSEIF (ICOD == GFP%RDSW%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=3
        ZMIN(KST:KEND)=0.0_JPRB
        ZMAX(KST:KEND)=1.0_JPRB
        ZSEA(KST:KEND)=1.0_JPRB
        ZSCAL(KST:KEND)=1.0_JPRB
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.28 Components of vector anisotropy

  ELSEIF (ICOD == GFP%PADOU%ICOD .OR. ICOD == GFP%PADOV%ICOD) THEN
    IF (LLSTDV) THEN
      ICOR=3
      ZMIN(KST:KEND)=-ZSTDV(KST:KEND)**2
      ZMAX(KST:KEND)=+ZSTDV(KST:KEND)**2
      ZSEA(KST:KEND)=0.0_JPRB
      ZSCAL(KST:KEND)=1.0_JPRB
    ELSE
      CALL ABOR1('FPCORPHY : MISSING STD.DEV OF OROG.')
    ENDIF

!     4.29 Precipitations

  ELSEIF (ICOD == GFP%CLSP%ICOD  .OR. ICOD == GFP%CCP%ICOD  .OR.&
     & ICOD == GFP%CLSS%ICOD  .OR. ICOD == GFP%CCSF%ICOD .OR.&
     & ICOD == GFP%CLSG%ICOD  .OR. ICOD == GFP%CCSG%ICOD .OR.&
     & ICOD == GFP%CLSH%ICOD  .OR. ICOD == GFP%CCSH%ICOD .OR.&
     & ICOD == GFP%CDUTP%ICOD .OR. ICOD == GFP%XLSP%ICOD .OR.&
     & ICOD == GFP%XCP%ICOD   .OR. ICOD == GFP%XLSS%ICOD .OR.&
     & ICOD == GFP%XCSF%ICOD  .OR. ICOD == GFP%XLSG%ICOD .OR.&
     & ICOD == GFP%XLSH%ICOD  .OR. ICOD == GFP%XCSG%ICOD .OR.&
     & ICOD == GFP%XCSH%ICOD ) THEN
   
    IF (LMPHYS) THEN
      ICOR=4
      ZMIN(KST:KEND)=0.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.30 Solar or lunar fluxes

  ELSEIF (ICOD == GFP%CSSR%ICOD .OR. ICOD == GFP%CTSR%ICOD .OR.&
     & ICOD == GFP%XSSR%ICOD .OR. ICOD == GFP%XTSR%ICOD .OR.&
     & ICOD == GFP%CSMR%ICOD) THEN  
    IF (LMPHYS) THEN
      ICOR=4
      ZMIN(KST:KEND)=0.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.31 Extrema of temperature at 2 meters

  ELSEIF (ICOD == GFP%XX2T%ICOD .OR. ICOD == GFP%XN2T%ICOD) THEN
    IF (IANO > 0) THEN
      IF (LLTCLS) THEN
        PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZTCLS(KST:KEND)
      ELSE
        CALL ABOR1('FPCORPHY : CAN T FIND CLS TEMPERATURE')
      ENDIF
    ENDIF
    ZMIN(KST:KEND)=0.0_JPRB
    ICOR=4

!     4.32 Extrema of relative moisture at 2 meters

  ELSEIF (ICOD == GFP%XX2HU%ICOD .OR. ICOD == GFP%XN2HU%ICOD) THEN
    IF (IANO > 0) THEN
      IF (LLRHCLS) THEN
        PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZRHCLS(KST:KEND)
      ELSE
        CALL ABOR1('FPCORPHY : CAN T FIND CLS RELATIVE MOISTURE')
      ENDIF
    ENDIF
    ICOR=6
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=1.0_JPRB

!     4.33  Mean radiant temperature

  ELSEIF (ICOD == GFP%XMRT%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.34 Cloudiness

  ELSEIF (ICOD == GFP%XTCC%ICOD .OR. ICOD == GFP%XCCC%ICOD .OR.&
     & ICOD == GFP%XHCC%ICOD .OR. ICOD == GFP%XMCC%ICOD .OR.&
     & ICOD == GFP%XLCC%ICOD) THEN  
    IF (LMPHYS) THEN
      ICOR=6
      ZMIN(KST:KEND)=0.0_JPRB
      ZMAX(KST:KEND)=1.0_JPRB
    ELSE
      ICOR=0
    ENDIF

!     4.35 PBL wind velocity

  ELSEIF (ICOD == GFP%X10FF%ICOD .OR. ICOD == GFP%XGUST%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.35 Model CAPE

  ELSEIF (ICOD == GFP%XCAPE%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.35b Top of deep convection

   ELSEIF (ICOD == GFP%XCTOP%ICOD) THEN
     ICOR=4
     ZMIN(KST:KEND)=1.0_JPRB
     ZMAX(KST:KEND)=1100.0_JPRB

!     4.35c Height of the PBL

  ELSEIF (ICOD == GFP%XCLPH%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.35d Ventilation index in PBL

  ELSEIF (ICOD == GFP%XVEIN%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.36 Interception content 

  ELSEIF (ICOD == GFP%IC%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        IF (LLLAI) THEN
          IF (LLVEG) THEN
            ICOR=3
            ZMIN(KST:KEND)=0.0_JPRB
            ZMAX(KST:KEND)=GWLMX*ZLAI(KST:KEND)*ZVEG(KST:KEND)
            ZSEA(KST:KEND)=0.0_JPRB
            ZSCAL(KST:KEND)=1.0_JPRB
          ELSE
            CALL ABOR1('FPCORPHY : VEGETATION CLIMATOLOGY IS MISSING')
          ENDIF
        ELSE
          CALL ABOR1('FPCORPHY : LAI CLIMATOLOGY IS MISSING')
        ENDIF
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.37 Resistance to evapotranspiration

  ELSEIF (ICOD == GFP%HV%ICOD) THEN
    IF (LMPHYS) THEN
      IF (LLSM) THEN
        ICOR=3
        ZMIN(KST:KEND)=0.0_JPRB
        ZMAX(KST:KEND)=1.0_JPRB
        ZSEA(KST:KEND)=1.0_JPRB
        ZSCAL(KST:KEND)=1.0_JPRB
      ELSE
        CALL ABOR1('FPCORPHY : CANNOT FIND LAND-SEA MASK')
      ENDIF
    ELSE
      ICOR=0
    ENDIF

!     4.38 Lake depth, lake mix layer depth and lake ice depth

  ELSEIF (ICOD == GFP%DL%ICOD .OR. ICOD == GFP%LMLD%ICOD .OR.&
     & ICOD == GFP%LICD%ICOD) THEN
    ICOR=4
    ZMIN(KST:KEND)=0.0_JPRB

!     4.39 Lake shape factor

  ELSEIF (ICOD == GFP%LSHF%ICOD) THEN
    ICOR=6
    ZMIN(KST:KEND)=0.5_JPRB
    ZMAX(KST:KEND)=0.8_JPRB

    !     4.40 Wet bulb temperature at 2m

  ELSEIF (ICOD == GFP%X2TPW%ICOD) THEN
    IF (LLTCLS) THEN
      IF (IANO > 0) THEN
        PROW(KST:KEND,JF)=PROW(KST:KEND,JF)+ZTCLS(KST:KEND)
      ELSE
        PROW(KST:KEND,JF)=PROW(KST:KEND,JF)
      ENDIF
    ELSE
      CALL ABOR1('FPCORPHY : CAN T FIND CLS TEMPERATURE')
    ENDIF
    ZMIN(KST:KEND)=0.0_JPRB
    ZMAX(KST:KEND)=ZTCLS(KST:KEND)
    ICOR=6



  ELSE

!    Else, nothing.

    ICOR=0

  ENDIF

  IF (ICOR /= 0) THEN
    CALL FPHOR12(JF,KST,KEND,PROW,KFIELDS,KFPROMA,ICOR,ZSEA,ZLSM,&
     & ZMIN,ZMAX,ZSCAL,ZTT)  
  ENDIF

ENDDO

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('FPCORPHY',1,ZHOOK_HANDLE)
!     ------------------------------------------------------------------

CONTAINS

SUBROUTINE FPLAKE(PCLI,PROW)
!         PCLI   : climatology field to use
!         PROW  : output row for physics
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCLI(KFPROMA)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PROW(KFPROMA)

INTEGER(KIND=JPIM) :: JI
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('FPCORPHY:FPLAKE',0,ZHOOK_HANDLE)

IF (.NOT.PRESENT(KWIC)) THEN
  CALL ABOR1('FPCORPHY : ISOLATED LAKE/ISLAND INDICATOR IS MISSING !')
ENDIF
DO JI=KST,KEND
  IF (KWIC(JI) /= 0) PROW(JI)=PCLI(JI)
ENDDO

IF (LHOOK) CALL DR_HOOK('FPCORPHY:FPLAKE',1,ZHOOK_HANDLE)
END SUBROUTINE FPLAKE

!     ------------------------------------------------------------------
END SUBROUTINE FPCORPHY


