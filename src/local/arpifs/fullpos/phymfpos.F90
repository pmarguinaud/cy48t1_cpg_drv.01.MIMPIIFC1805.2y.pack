SUBROUTINE PHYMFPOS(YDCST, YDQTYPE,YDNAMFPSCI,YDAFN,LDHPOS,YDGEOMETRY,YDSURF,YDRIP,YDML_PHY_MF,KPROMA,KST,KND,KFLDAUX,KFLDSPP,KGFL, &
 & YDIN,YDGFL,POROG,PGM,PGEMU,PGELAM,PUT0,PVT0,PDIVT0,PTT0,PSPDT0, &
 & PGRHL,PGFL,PSPT0,PSP_RR,PSP_SG,PSD_VF,PSD_VV,PXTCLS,PXRHCLS,PXQCLS,&
 & PXUCLS,PXVCLS,PXNUCLS,PXNVCLS,PAUX,PGPP)  

!**** *PHYMFPOS* - post-processing

!     PURPOSE.
!     -------
!           Physico-dynamic post-processing interface for Meteo-France.

!**   INTERFACE.
!     ---------
!        *CALL* *PHYMFPOS(...)

!        EXPLICIT ARGUMENTS :
!        ------------------
!        * INPUT :
!        LDHPOS    : .TRUE. if any horizontal gridpoint post-processing (core or E-zone)
!        KPROMA    : horizontal dimension 
!        KST       : first point to post-process 
!        KND       : last point to post-process 
!        KFLDAUX   : number of fields in output array (not fitted)
!        KFLDSPP   : number of fields in output array (fitted)
!        KGFL      : Number of GFL fields in PGFL
!        YDIN      : Pointers allowing to retrieve individual fields in PGFL
!        YDGFL     : says if GFL is active or not
!        POROG     : Surface geopotential
!        PGM       : Map factor
!        PGEMU     : Sine of geographic latitude
!        PGELAM    : geographic longitude
!        PUT0      : zonal wind time t
!        PVT0      : meridional wind time t
!        PDIVT0    : divergence time t
!        PTT0      : temperature time t
!        PSPDT0    : pressure departure variable
!        PGRHL     : Graupels+Hail
!        PGFL      : GFL
!        PSPT0     : surface pressure variable time t
!        PSP_RR    : RR=RESVR surface buffer (soil quantities for the surface
!                    and sometimes the first (upper) reservoir)
!        PSP_SG    : SG=SNOWG surface buffer (quantities linked to surface snow)
!        PSD_VF    : VF=VARSF surface buffer (climatological/geographical parameters)
!        PSD_VV    : VV=VCLIV surface buffer (vegetation parameters)
!        PXTCLS    : Tcls from model
!        PXRHCLS   : RHcls from model
!        PXQCLS    : QCLS from model
!        PXUCLS    : Ucls from model
!        PXVCLS    : Vcls from model
!        PXNUCLS   : neutral Ucls from model
!        PXNVCLS   : neutral Vcls from model

!        * OUTPUT (post-processed fields) :
!        PAUX      : fields to remain gridpoint
!        PGPP      : fields to be spectrally fitted

!     EXTERNALS.
!     ---------

!     AUTHOR.
!     -------
!        RYAD EL KHATIB *METEO-FRANCE*
!        ORIGINAL : 2000-10-06

!     MODIFICATIONS.
!     -------------
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad (Aug 2009): rewrite vertical interpolator in post-processing.
!   Y. Bouteloup : 01-05-2009 Add Total (radiative) cloud water and ice
!   F. Bouyssel  : 15-07-2009 Add LFPCAPEX
!   Y. Seity     : 15-06-2010 correction for ISPs in AROME Fullpos offline
!   R. El Khatib 22-Aug-2012 cfpfmt=model replaced by .not.lhpos
!   K. Yessad (Nov 2012): call GPHPRE.
!   K. Yessad (July 2014): Move some variables.
!   R. El Khatib 04-Aug-2014 Pruning of the conf. 927/928
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!   09-2018 R. Brozkova, A. Bucanek: MOCON diagnostics in offline fullpos.
!   09-2018 R. Brozkova: Dataflow for convective temperature.
!   H Petithomme (Dec 2020): optimisation on gphpre
!     ------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE YOMDIMV            , ONLY : TDIMV
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCT0   , ONLY : LNHDYN
USE YOMRIP   , ONLY : TRIP
USE YOMCST   , ONLY : TCST
USE YOMFPC, ONLY : TNAMFPSCI
USE YOMMTS   , ONLY : LMTS
USE TYPE_FPRQDYNS, ONLY : TYPE_FPRQDYN
USE YOMAFN, ONLY : TAFN
USE TYPE_GFLFLDS, ONLY: TYPE_IGFLFLDD,TYPE_LGFLFLDD
USE INTDYN_MOD,ONLY : YYTXYB

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE (TYPE_FPRQDYN),  INTENT(IN) :: YDQTYPE
TYPE (TNAMFPSCI),  INTENT(IN) :: YDNAMFPSCI
TYPE(TAFN)         ,INTENT(IN)    :: YDAFN
LOGICAL            ,INTENT(IN)    :: LDHPOS
TYPE(GEOMETRY)     ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)        ,INTENT(IN)    :: YDSURF
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TRIP)         ,INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KST 
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KND 
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KFLDAUX
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KFLDSPP
INTEGER(KIND=JPIM) ,INTENT(IN)    :: KGFL
TYPE(TYPE_IGFLFLDD),INTENT(IN)    :: YDIN
TYPE(TYPE_LGFLFLDD),INTENT(IN)    :: YDGFL
REAL(KIND=JPRB)    ,INTENT(IN)    :: POROG(KPROMA) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PGM(KPROMA) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PGEMU(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PGELAM(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PUT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PVT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIVT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PTT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSPDT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PGRHL(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PGFL(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG,KGFL)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSPT0(KPROMA) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSP_RR(KPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSP_SG(KPROMA,YDSURF%YSP_SGD%NLEVS,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSD_VF(KPROMA,YDSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PSD_VV(KPROMA,YDSURF%YSD_VVD%NDIM)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXTCLS(KPROMA) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXRHCLS(KPROMA) 
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXQCLS(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXUCLS(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXVCLS(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXNUCLS(KPROMA)
REAL(KIND=JPRB)    ,INTENT(IN)    :: PXNVCLS(KPROMA)
REAL(KIND=JPRB)    ,INTENT(OUT)   :: PAUX(KPROMA,KFLDAUX)
REAL(KIND=JPRB)    ,INTENT(OUT)   :: PGPP(KPROMA,KFLDSPP)
!     ------------------------------------------------------------------
! === LOGICAL VARIABLES COMPUTED IN PART 1 =============================
LOGICAL :: LLPBL   ! computation of PBL fields
LOGICAL :: LLICV   ! to activate the computation of CAPE & CIEN & TCVS
LOGICAL :: LLMUMLCAPE! to activate the computation of MUMLCAPE            
LOGICAL :: LLMLCAPE! to activate the computation of MLCAPE            
LOGICAL :: LLSMC   ! to activate the computation of Surface MOCON
LOGICAL :: LLASMC  ! to activate the computation of analysed surface MOCON
LOGICAL :: LLVSMC  ! to activate the computation of varpack-purpose surface MOCON
LOGICAL :: LLMTS   ! to activate the computation of model radiances
LOGICAL :: LL_MOCO ! to activate the computation of moisture convergence
LOGICAL :: LLSRH  ! to activate the computation of storm relateve helicity
LOGICAL :: LL_PCLDCEIL ! Pressure of cloud ceiling
LOGICAL :: LL_HCLDCEIL ! Height of cloud ceiling
LOGICAL :: LL_PCLDBASE ! Pressure of cloud base
LOGICAL :: LL_HCLDBASE ! Height of cloud base
LOGICAL :: LL_PCLDTOP  ! Pressure of top of clouds
LOGICAL :: LLCLD   ! to activate the computation of base and top of clouds 
LOGICAL :: LLZ0H

! === REAL VARIABLES COMPUTED IN PART 2.1 ==============================
REAL(KIND=JPRB) :: ZUT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZVT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDIVT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZPDT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQT0L(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZQT0M(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZLT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZIT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZLRADT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZIRADT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRRT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZSNT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGRHLT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZAT0(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZDELP(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! === REAL VARIABLES COMPUTED IN PART 2.2 ==============================
REAL(KIND=JPRB) :: ZCP(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZR(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZUSL_KAP(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPRESH(KPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),ZPRESF(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXYB(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB%NDIM)
REAL(KIND=JPRB) :: ZNHPREF(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZRNHPPI(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUSL_NHPPI(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZUSL_PDEP(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZRRED(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZGEOPH(KPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),ZGEOPF(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! === VARIABLES COMPUTED IN PART 2.3 ===================================
! * Working arrays:
REAL(KIND=JPRB) :: ZXQCLS(KPROMA) ! (analysed) specific moisture at PBL height
REAL(KIND=JPRB) :: ZTS0(KPROMA) ! local soil temperature 
REAL(KIND=JPRB) :: Z1SGM(KPROMA)  ! Reverse map factor
REAL(KIND=JPRB) :: ZQO3(KPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
! * Local post-processed fields:
REAL(KIND=JPRB) :: ZUCLS(KPROMA),ZVCLS(KPROMA),ZFCLS(KPROMA),ZTCLS(KPROMA)
REAL(KIND=JPRB) :: ZNUCLS(KPROMA),ZNVCLS(KPROMA)
REAL(KIND=JPRB) :: ZUGST(KPROMA),ZVGST(KPROMA),ZFGST(KPROMA),ZUSMC(KPROMA)
REAL(KIND=JPRB) :: ZQCLS(KPROMA),ZRHCLS(KPROMA),ZPCLS(KPROMA),ZVSMC(KPROMA)
REAL(KIND=JPRB) :: ZCAPE(KPROMA),ZCIN(KPROMA),ZTCVS(KPROMA),ZMOCO(KPROMA)
REAL(KIND=JPRB) :: ZPCLDCEIL(KPROMA)         ! Pressure of cloud ceiling 
REAL(KIND=JPRB) :: ZHCLDCEIL(KPROMA)         ! Height of cloud ceiling 
REAL(KIND=JPRB) :: ZPCLDBASE(KPROMA)         ! Pressure of cloud base 
REAL(KIND=JPRB) :: ZHCLDBASE(KPROMA)         ! Height of cloud base 
REAL(KIND=JPRB) :: ZPCLDTOP(KPROMA)          ! Pressure of top of clouds
REAL(KIND=JPRB) :: ZMUMLCAPE(KPROMA)
REAL(KIND=JPRB) :: ZMLCAPE(KPROMA)
REAL(KIND=JPRB) :: ZSTRMMU(KPROMA),ZSTRMMV(KPROMA), ZSRH(KPROMA)
REAL(KIND=JPRB) :: ZTBMSAT7(KPROMA,2),ZTBMSAT8(KPROMA,8),ZTBMSAT9(KPROMA,8)
REAL(KIND=JPRB) :: ZTBMSAT10(KPROMA,8),ZTBMSAT11(KPROMA,8)
REAL(KIND=JPRB) :: ZTBGOES11(KPROMA,4),ZTBGOES12(KPROMA,4),ZTBGOES15(KPROMA,4)
REAL(KIND=JPRB) :: ZTBGOES16(KPROMA,10),ZTBGOES17(KPROMA,10)
REAL(KIND=JPRB) :: ZTBMTSAT1(KPROMA,4)
REAL(KIND=JPRB) :: ZTBHIMA8(KPROMA,10)

INTEGER(KIND=JPIM) :: ILCL(KPROMA) ! Lifting condensation level
INTEGER(KIND=JPIM) :: IFCL(KPROMA) ! Free convection level
INTEGER(KIND=JPIM) :: IEL(KPROMA)  ! Equilibrium level
REAL(KIND=JPRB) :: ZLCL(KPROMA) !  Lifting condensation level (height)
REAL(KIND=JPRB) :: ZFCL(KPROMA) ! Free convection level (height)
REAL(KIND=JPRB) :: ZEL(KPROMA)  ! Equilibrium level (height)
REAL(KIND=JPRB) :: ZQS(KPROMA)
REAL(KIND=JPRB) :: ZTHETAV(KPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZTHETAVS(KPROMA) 
REAL(KIND=JPRB) :: ZZCLPH(KPROMA), ZZVEIN(KPROMA), ZZUGST(KPROMA), ZZVGST(KPROMA)
INTEGER(KIND=JPIM) :: ICLPH(KPROMA), ITDIA

! === OTHER VARIABLES ==================================================
REAL(KIND=JPRB) :: ZDELTA, ZES
INTEGER(KIND=JPIM) :: JLEV, ICAPE, JROF
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "acclph.intfb.h"
#include "fpachmt.intfb.h"
#include "fpcica.intfb.h"
#include "gpgeo.intfb.h"
#include "gnhpre.intfb.h"
#include "gphpre.intfb.h"
#include "gprcp.intfb.h"
#include "mts_phys.intfb.h"
#include "fcttrm.ycst.h"
#include "gppcloud.intfb.h"
#include "fpsrh.intfb.h"
#include "fpstrmm.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('PHYMFPOS',0,ZHOOK_HANDLE)
ASSOCIATE(TFP=>YDAFN%TFP, TFP_DYNDS=>YDAFN%TFP_DYNDS, YDVAB=>YDGEOMETRY%YRVAB,   YDPHY=>YDML_PHY_MF%YRPHY,   &
& YDPHY0=>YDML_PHY_MF%YRPHY0, YDPHY2=>YDML_PHY_MF%YRPHY2,   YDARPHY=>YDML_PHY_MF%YRARPHY)
ASSOCIATE(NFPCAPE=>YDNAMFPSCI%NFPCAPE, LFPCAPEX=>YDNAMFPSCI%LFPCAPEX, RENTRA=>YDNAMFPSCI%RENTRA,   RMLDEP=>YDNAMFPSCI%RMLDEP,   &
& NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, NSTOP=>YDRIP%NSTOP, YSD_VF=>YDSURF%YSD_VF,   YSD_VV=>YDSURF%YSD_VV,                         &
& YSP_RR=>YDSURF%YSP_RR, YSP_SG=>YDSURF%YSP_SG,   LNEIGE=>YDPHY%LNEIGE,YDDIMV=>YDGEOMETRY%YRDIMV)
!     ------------------------------------------------------------------

!*       1.    INITIALIZATIONS.
!              ---------------

LLSMC =YDQTYPE%LL(TFP%SMC%ICOD)
LLASMC=YDQTYPE%LL(TFP%ASMC%ICOD)
LLVSMC=YDQTYPE%LL(TFP%VSMC%ICOD)
LLMUMLCAPE= (.NOT.LDHPOS) .AND. (YDQTYPE%LL(TFP%MUMLCAPE%ICOD))
LLMLCAPE=   (.NOT.LDHPOS) .AND. (YDQTYPE%LL(TFP%MLCAPE%ICOD))
LL_PCLDCEIL=YDQTYPE%LL(TFP%PCLDCEIL%ICOD)
LL_HCLDCEIL=YDQTYPE%LL(TFP%HCLDCEIL%ICOD)
LL_PCLDBASE=YDQTYPE%LL(TFP%PCLDBASE%ICOD)
LL_HCLDBASE=YDQTYPE%LL(TFP%HCLDBASE%ICOD)
LL_PCLDTOP =YDQTYPE%LL(TFP%PCLDTOP %ICOD)

LLCLD=(.NOT.LDHPOS) .AND. (LL_PCLDCEIL.OR.LL_HCLDCEIL.OR.&
  &LL_PCLDBASE.OR.LL_HCLDBASE.OR.LL_PCLDTOP)
LLICV = (.NOT.LDHPOS) .AND. (     &
 & YDQTYPE%LL(TFP%CAPE%ICOD) .OR. &
 & YDQTYPE%LL(TFP%CIEN%ICOD) .OR. &
 & YDQTYPE%LL(TFP%TCVS%ICOD) )

LLSRH=YDQTYPE%LL(TFP%SRH%ICOD)

LLMTS = YDQTYPE%LL(TFP%MSAT7C1%ICOD) .OR. YDQTYPE%LL(TFP%MSAT7C2%ICOD) .OR.&
      & YDQTYPE%LL(TFP%MSAT8C1%ICOD) .OR. YDQTYPE%LL(TFP%MSAT8C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT8C3%ICOD) .OR. YDQTYPE%LL(TFP%MSAT8C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT8C5%ICOD) .OR. YDQTYPE%LL(TFP%MSAT8C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT8C7%ICOD) .OR. YDQTYPE%LL(TFP%MSAT8C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT9C1%ICOD) .OR. YDQTYPE%LL(TFP%MSAT9C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT9C3%ICOD) .OR. YDQTYPE%LL(TFP%MSAT9C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT9C5%ICOD) .OR. YDQTYPE%LL(TFP%MSAT9C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT9C7%ICOD) .OR. YDQTYPE%LL(TFP%MSAT9C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT10C1%ICOD) .OR. YDQTYPE%LL(TFP%MSAT10C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT10C3%ICOD) .OR. YDQTYPE%LL(TFP%MSAT10C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT10C5%ICOD) .OR. YDQTYPE%LL(TFP%MSAT10C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT10C7%ICOD) .OR. YDQTYPE%LL(TFP%MSAT10C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT11C1%ICOD) .OR. YDQTYPE%LL(TFP%MSAT11C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT11C3%ICOD) .OR. YDQTYPE%LL(TFP%MSAT11C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT11C5%ICOD) .OR. YDQTYPE%LL(TFP%MSAT11C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%MSAT11C7%ICOD) .OR. YDQTYPE%LL(TFP%MSAT11C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES11C1%ICOD) .OR. YDQTYPE%LL(TFP%GOES11C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES11C3%ICOD) .OR. YDQTYPE%LL(TFP%GOES11C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES12C1%ICOD) .OR. YDQTYPE%LL(TFP%GOES12C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES12C3%ICOD) .OR. YDQTYPE%LL(TFP%GOES12C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES15C1%ICOD) .OR. YDQTYPE%LL(TFP%GOES15C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES15C3%ICOD) .OR. YDQTYPE%LL(TFP%GOES15C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES16C1%ICOD) .OR. YDQTYPE%LL(TFP%GOES16C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES16C3%ICOD) .OR. YDQTYPE%LL(TFP%GOES16C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES16C5%ICOD) .OR. YDQTYPE%LL(TFP%GOES16C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES16C7%ICOD) .OR. YDQTYPE%LL(TFP%GOES16C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES16C9%ICOD) .OR. YDQTYPE%LL(TFP%GOES16C10%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES17C1%ICOD) .OR. YDQTYPE%LL(TFP%GOES17C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES17C3%ICOD) .OR. YDQTYPE%LL(TFP%GOES17C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES17C5%ICOD) .OR. YDQTYPE%LL(TFP%GOES17C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES17C7%ICOD) .OR. YDQTYPE%LL(TFP%GOES17C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%GOES17C9%ICOD) .OR. YDQTYPE%LL(TFP%GOES17C10%ICOD).OR.&
      & YDQTYPE%LL(TFP%MTSAT1C1%ICOD) .OR. YDQTYPE%LL(TFP%MTSAT1C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%MTSAT1C3%ICOD) .OR. YDQTYPE%LL(TFP%MTSAT1C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%HIMA8C1%ICOD) .OR. YDQTYPE%LL(TFP%HIMA8C2%ICOD).OR.&
      & YDQTYPE%LL(TFP%HIMA8C3%ICOD) .OR. YDQTYPE%LL(TFP%HIMA8C4%ICOD).OR.&
      & YDQTYPE%LL(TFP%HIMA8C5%ICOD) .OR. YDQTYPE%LL(TFP%HIMA8C6%ICOD).OR.&
      & YDQTYPE%LL(TFP%HIMA8C7%ICOD) .OR. YDQTYPE%LL(TFP%HIMA8C8%ICOD).OR.&
      & YDQTYPE%LL(TFP%HIMA8C9%ICOD) .OR. YDQTYPE%LL(TFP%HIMA8C10%ICOD)
LLMTS  =LLMTS.AND.LMTS
LL_MOCO=YDQTYPE%LL(TFP%MOCO%ICOD)
LLPBL  =LLSMC.OR.LLASMC.OR.LL_MOCO.OR.LLMUMLCAPE.OR.LLMLCAPE.OR.&
 & (LLICV.AND.(.NOT.LFPCAPEX)).OR.((.NOT.LDHPOS).AND. &
 & YDQTYPE%LL(TFP%UCLS%ICOD).OR.YDQTYPE%LL(TFP%VCLS%ICOD).OR. &
 & YDQTYPE%LL(TFP%TCLS%ICOD).OR.YDQTYPE%LL(TFP%QCLS%ICOD).OR. &
 & YDQTYPE%LL(TFP%RCLS%ICOD).OR.YDQTYPE%LL(TFP%FCLS%ICOD).OR. &
 & YDQTYPE%LL(TFP%UGST%ICOD).OR.YDQTYPE%LL(TFP%VGST%ICOD).OR. &
 & YDQTYPE%LL(TFP%FGST%ICOD))  

!     ------------------------------------------------------------------

!*       2.    CALCULATIONS.
!              -------------

! Nothing is done if (LLSMC,LLASMC,LLVSMC,LLICV,LLMTS,LLPBL,LLCLD)=(F,F,F,F,F,F)
IF (LLSMC.OR.LLASMC.OR.LLVSMC.OR.LLICV.OR.LLMTS.OR.LLPBL.OR.LLSRH.OR.LLCLD) THEN

  !*      2.1   MEMORY TRANSFERS

  ! GMV:
  IF (LLPBL .OR. LLMTS) THEN
    DO JLEV=1,NFLEVG
      DO JROF=KST,KND
        ZUT0(JROF,JLEV)=PUT0(JROF,JLEV)*PGM(JROF)
        ZVT0(JROF,JLEV)=PVT0(JROF,JLEV)*PGM(JROF)
        ZDIVT0(JROF,JLEV)=PDIVT0(JROF,JLEV)*PGM(JROF)*PGM(JROF)
      ENDDO
    ENDDO
  ENDIF
  IF (LLPBL .OR. LLICV .OR. LLMTS) THEN
    DO JLEV=1,NFLEVG
      DO JROF=KST,KND
        ZTT0(JROF,JLEV)=PTT0(JROF,JLEV)
        IF (LNHDYN) ZPDT0(JROF,JLEV)=PSPDT0(JROF,JLEV)
      ENDDO
    ENDDO
  ENDIF

  ! GFL:
  IF (LLPBL .OR. LLICV .OR. LLMTS .OR. LLVSMC .OR. LLCLD) THEN
    DO JLEV=1,NFLEVG
      IF (YDGFL%LLQ) THEN
        DO JROF=KST,KND
          ZQT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IQ)
        ENDDO
      ELSE
        ZQT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      IF (YDGFL%LLQL) THEN
        DO JROF=KST,KND
          ZQT0L(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IQL)*PGM(JROF)
        ENDDO
      ELSE
        ZQT0L(KST:KND,JLEV)=0.0_JPRB
        IF (LL_MOCO) CALL ABOR1("PHYMFPOS: LL_MOCO needs gradient of humidity")
      ENDIF
      IF (YDGFL%LLQM) THEN
        DO JROF=KST,KND
          ZQT0M(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IQM)*PGM(JROF)
        ENDDO
      ELSE
        ZQT0M(KST:KND,JLEV)=0.0_JPRB
        IF (LL_MOCO) CALL ABOR1("PHYMFPOS: LL_MOCO needs gradient of humidity")
      ENDIF
      IF (YDGFL%LLL) THEN
        DO JROF=KST,KND
          ZLT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IL)
        ENDDO
      ELSE
        ZLT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      !Fullpos inline case
      IF (NSTOP > 0_JPIM) THEN
        IF (YDGFL%LLLRAD) THEN
          DO JROF=KST,KND
            ZLRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%ILRAD)
          ENDDO
        ELSE
          ZLRADT0(KST:KND,JLEV)=0.0_JPRB
        ENDIF
      ELSE
      !Fullpos offline case
        IF (YDGFL%LLLRAD) THEN
          DO JROF=KST,KND
            ZLRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%ILRAD)
          ENDDO
        ELSEIF (YDGFL%LLL) THEN
          DO JROF=KST,KND
            ZLRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IL)
          ENDDO
        ELSE
          ZLRADT0(KST:KND,JLEV)=0.0_JPRB
        ENDIF
      ENDIF

      IF (YDGFL%LLI) THEN
        DO JROF=KST,KND
          ZIT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%II)
        ENDDO
      ELSE
        ZIT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      !Fullpos inline case
      IF (NSTOP > 0_JPIM) THEN
        IF (YDGFL%LLIRAD) THEN
          DO JROF=KST,KND
            ZIRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IIRAD)
          ENDDO
        ELSE 
          ZIRADT0(KST:KND,JLEV)=0.0_JPRB
        ENDIF
      ELSE
      !Fullpos offline case
        IF (YDGFL%LLIRAD) THEN
          DO JROF=KST,KND
            ZIRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IIRAD)
          ENDDO
        ELSEIF (YDGFL%LLI) THEN
          DO JROF=KST,KND
            ZIRADT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%II)
          ENDDO
        ELSE
          ZIRADT0(KST:KND,JLEV)=0.0_JPRB
        ENDIF
      ENDIF   
      IF (YDGFL%LLRR) THEN
        DO JROF=KST,KND
          ZRRT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IRR)
        ENDDO
      ELSE
        ZRRT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      IF (YDGFL%LLS) THEN
        DO JROF=KST,KND
          ZSNT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IS)
        ENDDO
      ELSE
        ZSNT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      IF (YDGFL%LLG) THEN
        DO JROF=KST,KND
          ZGRHLT0(JROF,JLEV)=PGRHL(JROF,JLEV)
        ENDDO
      ELSE
        ZGRHLT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
      IF (YDGFL%LLA) THEN
        DO JROF=KST,KND
          ZAT0(JROF,JLEV)=PGFL(JROF,JLEV,YDIN%IA)
        ENDDO
      ELSE
        ZAT0(KST:KND,JLEV)=0.0_JPRB
      ENDIF
    ENDDO
  ENDIF

  !*      2.2   DYNAMICS

  ! GP.. and GNH.. routines must be called there;
  ! Calculations must remain consistent with CPG_GP and POS ones.
  
  IF (LLPBL.OR.LLICV.OR.LLMTS.OR.LLSRH.OR.LLCLD) THEN

    ! Compute R and Cp.
    CALL GPRCP(KPROMA,KST,KND,NFLEVG,PQ=ZQT0,PQI=ZIT0,&
     & PQL=ZLT0,PQR=ZRRT0,PQS=ZSNT0,PQG=ZGRHLT0,&
     & PCP=ZCP,PR=ZR,PKAP=ZUSL_KAP)

    ! Compute pressures.
    DO JROF=KST,KND 
      ZPRESH(JROF,NFLEVG)=EXP(PSPT0(JROF))
    ENDDO

    CALL GPHPRE(KPROMA,NFLEVG,KST,KND,YDVAB,ZPRESH,PXYB=ZXYB,PRESF=ZPRESF,LDELP=LL_MOCO,&
     & LRTGR=.FALSE.,LRPP=.FALSE.)
    ! Compute NH pressure departure, and ZRRED=(pre/prehyd)*R
    IF (LLPBL.OR.LLSRH) THEN
      IF (LNHDYN) THEN
        ! ZPDT0 contains pressure departure prognostic variable.
        CALL GNHPRE(YDGEOMETRY,KPROMA,NFLEVG,KST,KND,ZPDT0,ZPRESF,&
         & PNHPREF=ZNHPREF,PNHPPI=ZUSL_NHPPI,PRNHPPI=ZRNHPPI,PDEP=ZUSL_PDEP)
        DO JLEV=1,NFLEVG
          DO JROF=KST,KND
            ZRRED(JROF,JLEV)=ZR(JROF,JLEV)*ZRNHPPI(JROF,JLEV)
          ENDDO
        ENDDO
      ELSE
        ZNHPREF(KST:KND,1:NFLEVG)=ZPRESF(KST:KND,1:NFLEVG)
        ZRNHPPI(KST:KND,1:NFLEVG)=1.0_JPRB
        ZRRED(KST:KND,1:NFLEVG)=ZR(KST:KND,1:NFLEVG)
      ENDIF
    ENDIF

    ! JC compute delta of pressures between to levels - for helicity and storm motion
    DO JLEV=1,NFLEVG
      DO JROF=KST,KND
        ZDELP(JROF,JLEV)=ZPRESH(JROF,JLEV-1)-ZPRESH(JROF,JLEV)
      ENDDO
    ENDDO

    ! Compute geopotential height.
    IF (LLPBL.OR.LLSRH.OR.LLCLD) THEN
      ! Geopotential height with surface orography=POROG
      ZGEOPH(KST:KND,NFLEVG)=POROG(KST:KND)
      CALL GPGEO(KPROMA,KST,KND,NFLEVG,ZGEOPH,ZGEOPF,ZTT0,ZRRED,ZXYB(1,1,YYTXYB%M_LNPR),ZXYB(1,1,YYTXYB%M_ALPH),&
       & YDGEOMETRY%YRVERT_GEOM)
    ENDIF

  ENDIF

  DO JROF=KST,KND
    Z1SGM(JROF)=1.0_JPRB/PGM(JROF)
  ENDDO

  !*      2.3   POST-PROCESSING

  ! No GP.. and GNH.. routine call is allowed in this part

  ! * PBL fields:
  IF ((LLPBL).AND.(.NOT. YDARPHY%LMSE)) THEN
    LLZ0H=YDSURF%YSD_VVD%NUMFLDS>=8
    CALL FPACHMT(YDCST,YDML_PHY_MF,LLZ0H,KPROMA,KST,KND,NFLEVG,&
     & ZTT0,ZQT0,ZUT0,ZVT0,&
     & ZPRESH,ZPRESF,ZCP,ZR,ZGEOPH,ZGEOPF,&
     & PSD_VF(1,YSD_VF%YLSM%MP),PSP_RR(1,YSP_RR%YT%MP0),PSD_VF(1,YSD_VF%YZ0F%MP),&
     & PSD_VV(1,YSD_VV%YZ0H%MP),PSD_VF(1,YSD_VF%YVEG%MP),&
     & PSD_VV(1,YSD_VV%YARG%MP),PSD_VV(1,YSD_VV%YD2%MP),PSD_VV(1,YSD_VV%YIVEG%MP),PSD_VV(1,YSD_VV%YSAB%MP),&
     & PSP_SG(1,1,YSP_SG%YF%MP0),PSP_RR(1,YSP_RR%YW%MP0),PSP_RR(1,YSP_RR%YRR(4)%MP0),PSD_VV(1,YSD_VV%YHV%MP),&
     & ZQCLS,ZRHCLS,ZTCLS,ZUCLS,ZVCLS,ZNUCLS,ZNVCLS,&
     & ZFCLS,ZUGST,ZVGST,ZFGST,ZPCLS,ZQS)

    IF (.NOT.LDHPOS) THEN
      CALL STORE_DATA(TFP%UCLS%ICOD,1,ZUCLS)
      CALL STORE_DATA(TFP%VCLS%ICOD,1,ZVCLS)
      CALL STORE_DATA(TFP%UGST%ICOD,1,ZUGST)
      CALL STORE_DATA(TFP%VGST%ICOD,1,ZVGST)
      CALL STORE_DATA(TFP%FCLS%ICOD,1,ZFCLS)
      CALL STORE_DATA(TFP%FGST%ICOD,1,ZFGST)
      CALL STORE_DATA(TFP%TCLS%ICOD,1,ZTCLS)
      CALL STORE_DATA(TFP%QCLS%ICOD,1,ZQCLS)
      CALL STORE_DATA(TFP%RCLS%ICOD,1,ZRHCLS)
    ENDIF
  ENDIF

  ! * Convection indices:
  IF (LLICV) THEN
    ICAPE=NFPCAPE
    IF (LFPCAPEX) THEN
      DO JROF=KST,KND
        ZPCLS(JROF)=EXP(PSPT0(JROF))
      ENDDO
      CALL FPCICA(YDML_PHY_MF,KST,KND,KPROMA,NFLEVG,ICAPE,RENTRA,RMLDEP,PXTCLS,ZPCLS,PXRHCLS,ZTT0,ZPRESF,&
       & ZQT0,ZCAPE,ZCIN,ZTCVS,ILCL,IFCL,IEL)
    ELSE
      CALL FPCICA(YDML_PHY_MF,KST,KND,KPROMA,NFLEVG,ICAPE,RENTRA,RMLDEP,ZTCLS,ZPCLS,ZRHCLS,ZTT0,ZPRESF,&
       & ZQT0,ZCAPE,ZCIN,ZTCVS,ILCL,IFCL,IEL)
    ENDIF
    CALL STORE_DATA(TFP%CAPE%ICOD,1,ZCAPE)
    CALL STORE_DATA(TFP%CIEN%ICOD,1,ZCIN)
    CALL STORE_DATA(TFP%TCVS%ICOD,1,ZTCVS)
    ZLCL=ILCL
    ZFCL=IFCL
    ZEL=IEL
    CALL STORE_DATA(TFP%LCL%ICOD ,1,ZLCL)
    CALL STORE_DATA(TFP%FCL%ICOD ,1,ZFCL)
    CALL STORE_DATA(TFP%EL%ICOD ,1,ZEL)
  ENDIF
  IF (LLMUMLCAPE.OR.LLMLCAPE) THEN
    ICAPE=6
    CALL FPCICA(YDML_PHY_MF,KST,KND,KPROMA,NFLEVG,ICAPE,RENTRA,RMLDEP,ZTCLS,ZPCLS,ZRHCLS,ZTT0,ZPRESF,&
     & ZQT0,ZMUMLCAPE,ZCIN,ZTCVS,ILCL,IFCL,IEL, ZMLCAPE)
    CALL STORE_DATA(TFP%MUMLCAPE%ICOD,1,ZMUMLCAPE)
    CALL STORE_DATA(TFP%MLCAPE%ICOD,1,ZMLCAPE)
  ENDIF

  IF (LLSRH) THEN
    ! * compute storm motion diagnostics and storm relative helicity
    CALL FPSTRMM(KST,KND,KPROMA,NFLEVG,&
       & ZUT0,ZVT0,ZDELP,ZPRESF,POROG,ZGEOPF,ZSTRMMU,ZSTRMMV)
    CALL FPSRH(KST,KND,KPROMA,NFLEVG, &
       & ZUT0,ZVT0,ZDELP,ZPRESF,POROG,ZGEOPF,ZSTRMMU,ZSTRMMV,ZSRH)

    CALL STORE_DATA(TFP%STRMMU%ICOD,1,ZSTRMMU)
    CALL STORE_DATA(TFP%STRMMV%ICOD,1,ZSTRMMV)
    CALL STORE_DATA(TFP%SRH%ICOD,1,ZSRH)
  ENDIF

  ! * Surface moisture convergence:
  IF (LLSMC) THEN
    DO JROF=KST,KND
      ZUSMC(JROF)=-ZQCLS(JROF)*ZUCLS(JROF)
      ZVSMC(JROF)=-ZQCLS(JROF)*ZVCLS(JROF)
    ENDDO
    CALL STORE_DATA(TFP%SMC%ICOD,1,ZUSMC)
    CALL STORE_DATA(TFP%SMC%ICOD,2,ZVSMC)
  ENDIF

  IF (LLVSMC) THEN
    ! specific mocon computation for variational diagnostic purposes
    DO JROF=KST,KND
      ZUSMC(JROF)=-ZQT0(JROF,NFLEVG-1)*PUT0(JROF,NFLEVG)*PGM(JROF)
      ZVSMC(JROF)=-ZQT0(JROF,NFLEVG-1)*PVT0(JROF,NFLEVG)*PGM(JROF)
    ENDDO
    CALL STORE_DATA(TFP%VSMC%ICOD,1,ZUSMC)
    CALL STORE_DATA(TFP%VSMC%ICOD,2,ZVSMC)
  ENDIF

  ! * Moisture convergence
  IF (LL_MOCO) THEN
    ! get index of nearest model level to PBL
    ITDIA=1_JPIM
    DO JLEV=ITDIA,NFLEVG
      DO JROF=KST,KND
        ZTHETAV(JROF,JLEV)=ZTT0(JROF,JLEV)*(1.0_JPRB+YDCST%RETV*ZQT0(JROF,JLEV)) &
         & *(YDCST%RATM/ZPRESF(JROF,JLEV))**YDCST%RKAPPA  
      ENDDO
    ENDDO
    DO JROF=KST,KND
      ZTHETAVS(JROF)=PSP_RR(JROF,YSP_RR%YT%MP0)*(1.0_JPRB+YDCST%RETV*ZQS(JROF)) &
       & *(YDCST%RATM/ZPRESH(JROF,NFLEVG))**YDCST%RKAPPA  
    ENDDO
    CALL ACCLPH(YDCST,YDPHY0,YDPHY2,KST,KND,KPROMA,ITDIA,NFLEVG, &
     & ZTHETAV,ZGEOPH,ZGEOPF,ZUT0,ZVT0,ZTHETAVS,           &
     & ICLPH,ZZCLPH,ZZVEIN,ZZUGST,ZZVGST)
    
    ZMOCO(KST:KND)=0.0_JPRB
    DO JLEV=1,NFLEVG
      DO JROF=KST,KND
        ZMOCO(JROF)=ZMOCO(JROF)+(ZQT0M(JROF,JLEV)*ZVT0(JROF,JLEV)+           &
         & ZQT0L(JROF,JLEV)*ZUT0(JROF,JLEV)+                                 &
         & ZQT0(JROF,JLEV)*ZDIVT0(JROF,JLEV))*ZXYB(JROF,JLEV,YYTXYB%M_DELP)* &
         & MAX(0,SIGN(1,JLEV-ICLPH(JROF)))
      ENDDO
    ENDDO
    DO JROF=KST,KND
      ZMOCO(JROF)=-ZMOCO(JROF)/(ZPRESH(JROF,NFLEVG)-ZPRESH(JROF,ICLPH(JROF)-1))
    ENDDO
    CALL STORE_DATA(TFP%MOCO%ICOD,1,ZMOCO)
  ENDIF

  IF (LLASMC) THEN
    DO JROF=KST,KND
      ! Compute specific moisture at PBL height 
      IF(LNEIGE) THEN
        ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-PXTCLS(JROF)))
      ELSE
        ZDELTA=0.0_JPRB
      ENDIF
      IF (YDARPHY%LMSE) THEN
        ZPCLS(JROF)=EXP(PSPT0(JROF))
      ENDIF
      ZES=FOEW(MAX(PXTCLS(JROF),1.0_JPRB),ZDELTA)
      ZXQCLS(JROF)=-PXRHCLS(JROF)*ZES /(&
       & YDCST%RETV*PXRHCLS(JROF)*ZES-ZPCLS(JROF)*(YDCST%RETV+1.0_JPRB))  
      ! Compute moisture convergence not reduced :
      ZUSMC(JROF)=-ZXQCLS(JROF)*PXUCLS(JROF)*PGM(JROF)
      ZVSMC(JROF)=-ZXQCLS(JROF)*PXVCLS(JROF)*PGM(JROF)
    ENDDO
    CALL STORE_DATA(TFP%ASMC%ICOD,1,ZUSMC)
    CALL STORE_DATA(TFP%ASMC%ICOD,2,ZVSMC)
  ENDIF

  ! * Model Radiances:
  IF (LLMTS) THEN
    ! this O3 initialization is scratched by the  RTTOV climatology in MTS_PHYS
    ZQO3(:,:) = 1.E-9_JPRB 
    DO JROF=KST,KND
      ZTS0(JROF)=MIN(MAX(PSP_RR(JROF,YSP_RR%YT%MP0),200._JPRB),350._JPRB)
    ENDDO
    IF ((.NOT. LLPBL).OR.(YDARPHY%LMSE)) THEN
      ! these local are necessary for the points belonging to the unphysical extension 
      !  domain of ALADIN or AROME 
      DO JROF=KST,KND
        ZUCLS(JROF)=MIN(MAX(PXUCLS(JROF),-70._JPRB),70._JPRB)
        ZVCLS(JROF)=MIN(MAX(PXVCLS(JROF),-70._JPRB),70._JPRB)
        ZNUCLS(JROF)=MIN(MAX(PXNUCLS(JROF),-70._JPRB),70._JPRB)
        ZNVCLS(JROF)=MIN(MAX(PXNVCLS(JROF),-70._JPRB),70._JPRB)
        ZTCLS(JROF)=MIN(MAX(PXTCLS(JROF),200._JPRB),350._JPRB)
        ZQCLS(JROF)=MIN(MAX(PXQCLS(JROF),1.E-9_JPRB),0.04_JPRB)
      ENDDO
    ENDIF 

    CALL MTS_PHYS(YDDIMV,YDRIP,KST,KND,KPROMA,NFLEVG,PGEMU,PGELAM,POROG,ZPRESH,ZPRESF,&
    & ZXYB(1,1,YYTXYB%M_LNPR),ZXYB(1,1,YYTXYB%M_ALPH),&
    & ZUT0,ZVT0,ZTT0,ZQT0,ZQO3,ZLRADT0,ZIRADT0,ZAT0,ZTS0,PSD_VF(1,YSD_VF%YLSM%MP),ZUCLS,ZVCLS,&
    & ZTCLS,ZQCLS,&
    & ZTBMSAT7,ZTBMSAT8,ZTBMSAT9,ZTBMSAT10,ZTBMSAT11,&
    & ZTBGOES11,ZTBGOES12,ZTBGOES15,ZTBGOES16,ZTBGOES17,ZTBMTSAT1,ZTBHIMA8)
    
    IF(YDQTYPE%LL(TFP%MSAT7C1%ICOD)) CALL STORE_DATA(TFP%MSAT7C1%ICOD,1,ZTBMSAT7(:,1))
    IF(YDQTYPE%LL(TFP%MSAT7C2%ICOD)) CALL STORE_DATA(TFP%MSAT7C2%ICOD,1,ZTBMSAT7(:,2))
    IF(YDQTYPE%LL(TFP%MSAT8C1%ICOD)) CALL STORE_DATA(TFP%MSAT8C1%ICOD,1,ZTBMSAT8(:,1))
    IF(YDQTYPE%LL(TFP%MSAT8C2%ICOD)) CALL STORE_DATA(TFP%MSAT8C2%ICOD,1,ZTBMSAT8(:,2))
    IF(YDQTYPE%LL(TFP%MSAT8C3%ICOD)) CALL STORE_DATA(TFP%MSAT8C3%ICOD,1,ZTBMSAT8(:,3))
    IF(YDQTYPE%LL(TFP%MSAT8C4%ICOD)) CALL STORE_DATA(TFP%MSAT8C4%ICOD,1,ZTBMSAT8(:,4))
    IF(YDQTYPE%LL(TFP%MSAT8C5%ICOD)) CALL STORE_DATA(TFP%MSAT8C5%ICOD,1,ZTBMSAT8(:,5))
    IF(YDQTYPE%LL(TFP%MSAT8C6%ICOD)) CALL STORE_DATA(TFP%MSAT8C6%ICOD,1,ZTBMSAT8(:,6))
    IF(YDQTYPE%LL(TFP%MSAT8C7%ICOD)) CALL STORE_DATA(TFP%MSAT8C7%ICOD,1,ZTBMSAT8(:,7))
    IF(YDQTYPE%LL(TFP%MSAT8C8%ICOD)) CALL STORE_DATA(TFP%MSAT8C8%ICOD,1,ZTBMSAT8(:,8))
    IF(YDQTYPE%LL(TFP%MSAT9C1%ICOD)) CALL STORE_DATA(TFP%MSAT9C1%ICOD,1,ZTBMSAT9(:,1))
    IF(YDQTYPE%LL(TFP%MSAT9C2%ICOD)) CALL STORE_DATA(TFP%MSAT9C2%ICOD,1,ZTBMSAT9(:,2))
    IF(YDQTYPE%LL(TFP%MSAT9C3%ICOD)) CALL STORE_DATA(TFP%MSAT9C3%ICOD,1,ZTBMSAT9(:,3))
    IF(YDQTYPE%LL(TFP%MSAT9C4%ICOD)) CALL STORE_DATA(TFP%MSAT9C4%ICOD,1,ZTBMSAT9(:,4))
    IF(YDQTYPE%LL(TFP%MSAT9C5%ICOD)) CALL STORE_DATA(TFP%MSAT9C5%ICOD,1,ZTBMSAT9(:,5))
    IF(YDQTYPE%LL(TFP%MSAT9C6%ICOD)) CALL STORE_DATA(TFP%MSAT9C6%ICOD,1,ZTBMSAT9(:,6))
    IF(YDQTYPE%LL(TFP%MSAT9C7%ICOD)) CALL STORE_DATA(TFP%MSAT9C7%ICOD,1,ZTBMSAT9(:,7))
    IF(YDQTYPE%LL(TFP%MSAT9C8%ICOD)) CALL STORE_DATA(TFP%MSAT9C8%ICOD,1,ZTBMSAT9(:,8))
    IF(YDQTYPE%LL(TFP%MSAT10C1%ICOD)) CALL STORE_DATA(TFP%MSAT10C1%ICOD,1,ZTBMSAT10(:,1))
    IF(YDQTYPE%LL(TFP%MSAT10C2%ICOD)) CALL STORE_DATA(TFP%MSAT10C2%ICOD,1,ZTBMSAT10(:,2))
    IF(YDQTYPE%LL(TFP%MSAT10C3%ICOD)) CALL STORE_DATA(TFP%MSAT10C3%ICOD,1,ZTBMSAT10(:,3))
    IF(YDQTYPE%LL(TFP%MSAT10C4%ICOD)) CALL STORE_DATA(TFP%MSAT10C4%ICOD,1,ZTBMSAT10(:,4))
    IF(YDQTYPE%LL(TFP%MSAT10C5%ICOD)) CALL STORE_DATA(TFP%MSAT10C5%ICOD,1,ZTBMSAT10(:,5))
    IF(YDQTYPE%LL(TFP%MSAT10C6%ICOD)) CALL STORE_DATA(TFP%MSAT10C6%ICOD,1,ZTBMSAT10(:,6))
    IF(YDQTYPE%LL(TFP%MSAT10C7%ICOD)) CALL STORE_DATA(TFP%MSAT10C7%ICOD,1,ZTBMSAT10(:,7))
    IF(YDQTYPE%LL(TFP%MSAT10C8%ICOD)) CALL STORE_DATA(TFP%MSAT10C8%ICOD,1,ZTBMSAT10(:,8))
    IF(YDQTYPE%LL(TFP%MSAT11C1%ICOD)) CALL STORE_DATA(TFP%MSAT11C1%ICOD,1,ZTBMSAT11(:,1))
    IF(YDQTYPE%LL(TFP%MSAT11C2%ICOD)) CALL STORE_DATA(TFP%MSAT11C2%ICOD,1,ZTBMSAT11(:,2))
    IF(YDQTYPE%LL(TFP%MSAT11C3%ICOD)) CALL STORE_DATA(TFP%MSAT11C3%ICOD,1,ZTBMSAT11(:,3))
    IF(YDQTYPE%LL(TFP%MSAT11C4%ICOD)) CALL STORE_DATA(TFP%MSAT11C4%ICOD,1,ZTBMSAT11(:,4))
    IF(YDQTYPE%LL(TFP%MSAT11C5%ICOD)) CALL STORE_DATA(TFP%MSAT11C5%ICOD,1,ZTBMSAT11(:,5))
    IF(YDQTYPE%LL(TFP%MSAT11C6%ICOD)) CALL STORE_DATA(TFP%MSAT11C6%ICOD,1,ZTBMSAT11(:,6))
    IF(YDQTYPE%LL(TFP%MSAT11C7%ICOD)) CALL STORE_DATA(TFP%MSAT11C7%ICOD,1,ZTBMSAT11(:,7))
    IF(YDQTYPE%LL(TFP%MSAT11C8%ICOD)) CALL STORE_DATA(TFP%MSAT11C8%ICOD,1,ZTBMSAT11(:,8))
    IF(YDQTYPE%LL(TFP%GOES11C1%ICOD)) CALL STORE_DATA(TFP%GOES11C1%ICOD,1,ZTBGOES11(:,1))
    IF(YDQTYPE%LL(TFP%GOES11C2%ICOD)) CALL STORE_DATA(TFP%GOES11C2%ICOD,1,ZTBGOES11(:,2))
    IF(YDQTYPE%LL(TFP%GOES11C3%ICOD)) CALL STORE_DATA(TFP%GOES11C3%ICOD,1,ZTBGOES11(:,3))
    IF(YDQTYPE%LL(TFP%GOES11C4%ICOD)) CALL STORE_DATA(TFP%GOES11C4%ICOD,1,ZTBGOES11(:,4))
    IF(YDQTYPE%LL(TFP%GOES12C1%ICOD)) CALL STORE_DATA(TFP%GOES12C1%ICOD,1,ZTBGOES12(:,1))
    IF(YDQTYPE%LL(TFP%GOES12C2%ICOD)) CALL STORE_DATA(TFP%GOES12C2%ICOD,1,ZTBGOES12(:,2))
    IF(YDQTYPE%LL(TFP%GOES12C3%ICOD)) CALL STORE_DATA(TFP%GOES12C3%ICOD,1,ZTBGOES12(:,3))
    IF(YDQTYPE%LL(TFP%GOES12C4%ICOD)) CALL STORE_DATA(TFP%GOES12C4%ICOD,1,ZTBGOES12(:,4))
    IF(YDQTYPE%LL(TFP%GOES15C1%ICOD)) CALL STORE_DATA(TFP%GOES15C1%ICOD,1,ZTBGOES15(:,1))
    IF(YDQTYPE%LL(TFP%GOES15C2%ICOD)) CALL STORE_DATA(TFP%GOES15C2%ICOD,1,ZTBGOES15(:,2))
    IF(YDQTYPE%LL(TFP%GOES15C3%ICOD)) CALL STORE_DATA(TFP%GOES15C3%ICOD,1,ZTBGOES15(:,3))
    IF(YDQTYPE%LL(TFP%GOES15C4%ICOD)) CALL STORE_DATA(TFP%GOES15C4%ICOD,1,ZTBGOES15(:,4))
    IF(YDQTYPE%LL(TFP%GOES16C1%ICOD)) CALL STORE_DATA(TFP%GOES16C1%ICOD,1,ZTBGOES16(:,1))
    IF(YDQTYPE%LL(TFP%GOES16C2%ICOD)) CALL STORE_DATA(TFP%GOES16C2%ICOD,1,ZTBGOES16(:,2))
    IF(YDQTYPE%LL(TFP%GOES16C3%ICOD)) CALL STORE_DATA(TFP%GOES16C3%ICOD,1,ZTBGOES16(:,3))
    IF(YDQTYPE%LL(TFP%GOES16C4%ICOD)) CALL STORE_DATA(TFP%GOES16C4%ICOD,1,ZTBGOES16(:,4))
    IF(YDQTYPE%LL(TFP%GOES16C5%ICOD)) CALL STORE_DATA(TFP%GOES16C5%ICOD,1,ZTBGOES16(:,5))
    IF(YDQTYPE%LL(TFP%GOES16C6%ICOD)) CALL STORE_DATA(TFP%GOES16C6%ICOD,1,ZTBGOES16(:,6))
    IF(YDQTYPE%LL(TFP%GOES16C7%ICOD)) CALL STORE_DATA(TFP%GOES16C7%ICOD,1,ZTBGOES16(:,7))
    IF(YDQTYPE%LL(TFP%GOES16C8%ICOD)) CALL STORE_DATA(TFP%GOES16C8%ICOD,1,ZTBGOES16(:,8))
    IF(YDQTYPE%LL(TFP%GOES16C9%ICOD)) CALL STORE_DATA(TFP%GOES16C9%ICOD,1,ZTBGOES16(:,9))
    IF(YDQTYPE%LL(TFP%GOES16C10%ICOD)) CALL STORE_DATA(TFP%GOES16C10%ICOD,1,ZTBGOES16(:,10))
    IF(YDQTYPE%LL(TFP%GOES17C1%ICOD)) CALL STORE_DATA(TFP%GOES17C1%ICOD,1,ZTBGOES17(:,1))
    IF(YDQTYPE%LL(TFP%GOES17C2%ICOD)) CALL STORE_DATA(TFP%GOES17C2%ICOD,1,ZTBGOES17(:,2))
    IF(YDQTYPE%LL(TFP%GOES17C3%ICOD)) CALL STORE_DATA(TFP%GOES17C3%ICOD,1,ZTBGOES17(:,3))
    IF(YDQTYPE%LL(TFP%GOES17C4%ICOD)) CALL STORE_DATA(TFP%GOES17C4%ICOD,1,ZTBGOES17(:,4))
    IF(YDQTYPE%LL(TFP%GOES17C5%ICOD)) CALL STORE_DATA(TFP%GOES17C5%ICOD,1,ZTBGOES17(:,5))
    IF(YDQTYPE%LL(TFP%GOES17C6%ICOD)) CALL STORE_DATA(TFP%GOES17C6%ICOD,1,ZTBGOES17(:,6))
    IF(YDQTYPE%LL(TFP%GOES17C7%ICOD)) CALL STORE_DATA(TFP%GOES17C7%ICOD,1,ZTBGOES17(:,7))
    IF(YDQTYPE%LL(TFP%GOES17C8%ICOD)) CALL STORE_DATA(TFP%GOES17C8%ICOD,1,ZTBGOES17(:,8))
    IF(YDQTYPE%LL(TFP%GOES17C9%ICOD)) CALL STORE_DATA(TFP%GOES17C9%ICOD,1,ZTBGOES17(:,9))
    IF(YDQTYPE%LL(TFP%GOES17C10%ICOD)) CALL STORE_DATA(TFP%GOES17C10%ICOD,1,ZTBGOES17(:,10))
    IF(YDQTYPE%LL(TFP%MTSAT1C1%ICOD)) CALL STORE_DATA(TFP%MTSAT1C1%ICOD,1,ZTBMTSAT1(:,1))
    IF(YDQTYPE%LL(TFP%MTSAT1C2%ICOD)) CALL STORE_DATA(TFP%MTSAT1C2%ICOD,1,ZTBMTSAT1(:,2))
    IF(YDQTYPE%LL(TFP%MTSAT1C3%ICOD)) CALL STORE_DATA(TFP%MTSAT1C3%ICOD,1,ZTBMTSAT1(:,3))
    IF(YDQTYPE%LL(TFP%MTSAT1C4%ICOD)) CALL STORE_DATA(TFP%MTSAT1C4%ICOD,1,ZTBMTSAT1(:,4))
    IF(YDQTYPE%LL(TFP%HIMA8C1%ICOD)) CALL STORE_DATA(TFP%HIMA8C1%ICOD,1,ZTBHIMA8(:,1))
    IF(YDQTYPE%LL(TFP%HIMA8C2%ICOD)) CALL STORE_DATA(TFP%HIMA8C2%ICOD,1,ZTBHIMA8(:,2))
    IF(YDQTYPE%LL(TFP%HIMA8C3%ICOD)) CALL STORE_DATA(TFP%HIMA8C3%ICOD,1,ZTBHIMA8(:,3))
    IF(YDQTYPE%LL(TFP%HIMA8C4%ICOD)) CALL STORE_DATA(TFP%HIMA8C4%ICOD,1,ZTBHIMA8(:,4))
    IF(YDQTYPE%LL(TFP%HIMA8C5%ICOD)) CALL STORE_DATA(TFP%HIMA8C5%ICOD,1,ZTBHIMA8(:,5))
    IF(YDQTYPE%LL(TFP%HIMA8C6%ICOD)) CALL STORE_DATA(TFP%HIMA8C6%ICOD,1,ZTBHIMA8(:,6))
    IF(YDQTYPE%LL(TFP%HIMA8C7%ICOD)) CALL STORE_DATA(TFP%HIMA8C7%ICOD,1,ZTBHIMA8(:,7))
    IF(YDQTYPE%LL(TFP%HIMA8C8%ICOD)) CALL STORE_DATA(TFP%HIMA8C8%ICOD,1,ZTBHIMA8(:,8))
    IF(YDQTYPE%LL(TFP%HIMA8C9%ICOD)) CALL STORE_DATA(TFP%HIMA8C9%ICOD,1,ZTBHIMA8(:,9))
    IF(YDQTYPE%LL(TFP%HIMA8C10%ICOD)) CALL STORE_DATA(TFP%HIMA8C10%ICOD,1,ZTBHIMA8(:,10))
  ENDIF

  IF (LLCLD) THEN
    IF (LL_PCLDTOP.OR.LL_HCLDCEIL.OR.LL_PCLDCEIL) THEN
      CALL GPPCLOUD(KPROMA,KST,KND,NFLEVG,0.5_JPRB,ZPRESF,ZAT0,ZGEOPF,POROG,&
       & PPTOP=ZPCLDTOP,PPBASE=ZPCLDCEIL,PHBASE=ZHCLDCEIL)
      IF(YDQTYPE%LL(TFP%PCLDTOP%ICOD))  CALL STORE_DATA(TFP%PCLDTOP%ICOD,1,ZPCLDTOP)
      IF(YDQTYPE%LL(TFP%PCLDCEIL%ICOD)) CALL STORE_DATA(TFP%PCLDCEIL%ICOD,1,ZPCLDCEIL)
      IF(YDQTYPE%LL(TFP%HCLDCEIL%ICOD)) CALL STORE_DATA(TFP%HCLDCEIL%ICOD,1,ZHCLDCEIL)
    ENDIF
    IF (LL_HCLDCEIL.OR.LL_PCLDCEIL) THEN
      CALL GPPCLOUD(KPROMA,KST,KND,NFLEVG,0.01_JPRB,ZPRESF,ZAT0,ZGEOPF,POROG,&
       & PPBASE=ZPCLDBASE,PHBASE=ZHCLDBASE)
      IF(YDQTYPE%LL(TFP%PCLDBASE%ICOD)) CALL STORE_DATA(TFP%PCLDBASE%ICOD,1,ZPCLDBASE)
      IF(YDQTYPE%LL(TFP%HCLDBASE%ICOD)) CALL STORE_DATA(TFP%HCLDBASE%ICOD,1,ZHCLDBASE)
    ENDIF  
  ENDIF
ENDIF

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('PHYMFPOS',1,ZHOOK_HANDLE)

!------------------------------------------------------------------
CONTAINS

SUBROUTINE STORE_DATA(K,KPOS,PDATA)

INTEGER(KIND=JPIM), INTENT(IN) :: K             ! Field code/index
INTEGER(KIND=JPIM), INTENT(IN) :: KPOS          ! Relative adress of input data
REAL(KIND=JPRB),    INTENT(IN) :: PDATA(KPROMA) ! input data

INTEGER(KIND=JPIM) :: ILOC ! Absolute adress of data in output array
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('PHYMFPOS:STORE_DATA',0,ZHOOK_HANDLE)
ASSOCIATE(TFP_DYNDS=>YDAFN%TFP_DYNDS)

IF (YDQTYPE%LL(K)) THEN
  SELECT CASE (YDQTYPE%ISF(K))
  CASE (0)
    ! Not fitted data 
    ILOC=YDQTYPE%IGPX(K)
    SELECT CASE (ABS(TFP_DYNDS(K)%IORDR))
    CASE (0)
      DO JROF=KST,KND
        PAUX(JROF,ILOC) = PDATA(JROF)
      ENDDO
    CASE (1)
      DO JROF=KST,KND
        PAUX(JROF,ILOC) = PDATA(JROF)*Z1SGM(JROF)
      ENDDO
    CASE (2)
      DO JROF=KST,KND
        PAUX(JROF,ILOC) = PDATA(JROF)*(Z1SGM(JROF)**2)
      ENDDO
    END SELECT
  CASE (1:)
    ! Fitted data 
    IF (KPOS > 0 .AND. KPOS <= YDQTYPE%ISKP(K)) THEN
      ILOC=YDQTYPE%IGT1(K,KPOS)
    ELSE
      CALL ABOR1('PHYMFPOS.STORE_DATA : INTERNAL ERROR KPOS')
    ENDIF
    SELECT CASE (ABS(TFP_DYNDS(K)%IORDR))
    CASE (0)
      DO JROF=KST,KND
        PGPP(JROF,ILOC) = PDATA(JROF)
      ENDDO
    CASE (1)
      DO JROF=KST,KND
        PGPP(JROF,ILOC) = PDATA(JROF)*Z1SGM(JROF)
      ENDDO
    CASE (2)
      DO JROF=KST,KND
        PGPP(JROF,ILOC) = PDATA(JROF)*(Z1SGM(JROF)**2)
      ENDDO
    END SELECT
  END SELECT 
ENDIF

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('PHYMFPOS:STORE_DATA',1,ZHOOK_HANDLE)
END SUBROUTINE STORE_DATA

!     ------------------------------------------------------------------

END SUBROUTINE PHYMFPOS

