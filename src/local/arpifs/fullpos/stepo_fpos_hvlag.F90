SUBROUTINE STEPO_FPOS_HVLAG(KLMOD,YDQTYPE,YDAUX,YDCLIMO,YDNAMFPINT,YDNAMFPSCI,KFPXFLD,YDFPOS,KFPDOM,YDGEOMETRY,YDSURF, &
 & KCUFNR,YDMODEL,CDV,KPXLEV,PXLEV,KP3FIELDS,KGPX,PABUF,PFPBUF,PSPEC,P2DFIELDS,P3DFIELDS,P3DPHYS)

!**** *STEPO_FPOS_HVLAG*  - Controls Fullpos job at lowest level - vertical interpolations and spectral treatments
!                        for surface-dependent post-processing levels

!     Purpose.
!     --------
!        CONTROLS THE POST-PROCESSING.

!**   Interface.
!     ----------
!        *CALL* *STEPO_FPOS_HVLAG(CDV)

!        Explicit arguments :
!        --------------------
!          KLMOD : model/post-processing dynamic primitive variable indicator for each fullpos field :
!                  2 : upper air primitive dynamic variable
!                  1 : surface primitive dynamic variable
!                  0 : not a primitive variable
!         KFPXFLD : maximum number of fields to extract at a time
!         KFPDOM : number of subdomains
!         CDV : configuration of vertical post-processing (in)
!         P2DFIELDS : alternative 2D output array to PFPBUF
!         P3DFIELDS : alternative 3D output array to PFPBUF (not allocated)
!         P3DPHYS : alternative 3D output array to PFPBUF for surface fields (preallocated)
!         KP3FIELDS : number of fields in P3DPHYS
!         KGPX : fields pointers in PABUF

!        Implicit arguments :
!        --------------------
!        None

!     Method.
!     -------
!        See documentation

!     Externals.    see includes below.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Ryad El Khatib  *Meteo-France*
!      Original : 16-Jul-2012 from STEPO

! Modifications
! -------------
!      R. El Khatib 13-Dec-2012 Fullpos buffers reshaping, memory savings
!      R. El Khatib 17-Jul-2013 FABEC post-processing
!      R. El Khatib 27-Sep-2013 Boyd periodization in Fullpos-2
!      R. El Khatib & D. Salmond 03-Jun-2014 Fix deallocate issues
!      R. El Khatib 08-Dec-2015 Move out I/Os.
!      R. El Khatib 09-Aug-2016 Cleaning of biperiodicization management
!      R. El Khatib 29-May-2019 split from stepo_fpos
! End Modifications
!------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE TYPE_MODEL         , ONLY : MODEL
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT
USE PARFPOS            , ONLY : JPOSDYN
USE YOMFPC             , ONLY : TNAMFPSCI, TNAMFPINT, LTRACEFP, LALLOFP
USE TYPE_FPRQDYNS      , ONLY : TYPE_FPRQDYN
USE YOMVERT            , ONLY : TVAB
USE FULLPOS            , ONLY : TFPOS
USE TYPE_FPOSBUF       , ONLY : FPOSBUF

!      -----------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN) :: KLMOD(JPOSDYN)
TYPE (TYPE_FPRQDYN), INTENT(IN) :: YDQTYPE
TYPE(FPOSBUF)     ,INTENT(IN) :: YDAUX
TYPE(FPOSBUF)     ,INTENT(IN) :: YDCLIMO
TYPE(TNAMFPINT)   ,INTENT(IN) :: YDNAMFPINT
TYPE(TNAMFPSCI)   ,INTENT(IN) :: YDNAMFPSCI
INTEGER(KIND=JPIM),INTENT(IN) :: KFPXFLD
TYPE (TFPOS)      ,INTENT(IN) :: YDFPOS
INTEGER(KIND=JPIM),INTENT(IN) :: KFPDOM
TYPE(GEOMETRY)    ,INTENT(IN) :: YDGEOMETRY
TYPE(TSURF)       ,INTENT(IN) :: YDSURF
INTEGER(KIND=JPIM),INTENT(IN) :: KCUFNR
TYPE(MODEL)       ,INTENT(IN) :: YDMODEL
CHARACTER(LEN=1)  ,INTENT(IN) :: CDV
INTEGER(KIND=JPIM),INTENT(IN) :: KPXLEV
REAL(KIND=JPRB)   ,INTENT(IN) :: PXLEV(KPXLEV)
INTEGER(KIND=JPIM),INTENT(IN) :: KP3FIELDS
INTEGER(KIND=JPIM),INTENT(IN) :: KGPX(JPOSDYN)
REAL(KIND=JPRB)   ,POINTER, INTENT(INOUT) :: PABUF(:,:,:)
REAL(KIND=JPRB)   ,INTENT(OUT), POINTER :: PFPBUF(:,:,:)
REAL(KIND=JPRB)   ,INTENT(OUT), ALLOCATABLE :: PSPEC(:,:)
REAL(KIND=JPRB)   ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P2DFIELDS(:,:,:)
REAL(KIND=JPRB)   ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P3DFIELDS(:,:,:,:)
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT), OPTIONAL :: &
 & P3DPHYS(YDFPOS%YFPGEOMETRY%YFPGEO%NFPROMA,KPXLEV,KP3FIELDS,YDFPOS%YFPGEOMETRY%YFPGEO%NFPBLOCS)

! CLCONF(character*9) :
! configuration of post-processing computation built from dimensionning and arguments
! 1  : vertical post-processing
! 2  : model direct Legendre / North/South Fourier transforms
! 3  : spectral filters in model space
! 4  : model inverse Legendre / North/South Fourier transforms
! 5  : horizontal post processing
! 6  : lagged vertical post-processing 
! 7  : output direct Legendre / North/South Fourier transforms
! 8  : spectral filters in output spectral space
! 9  : output inverse Legendre / East/West Fourier transforms

!     ZAVEG,ZMING,ZMAXG  : global norms of the fields (mean val, min val, max val)

CHARACTER(LEN=9)  :: CLCONF
CHARACTER(LEN=35) :: CLFMT1='(1X,''ARRAY '',A10,'' ALLOCATED '',8I8)'
CHARACTER(LEN=33) :: CLFMT0='(1X,''ARRAY '',A10,'' DEALLOCATED '')'

LOGICAL :: LLSPOSGF, LLSPOSLP, LLZ0H
INTEGER(KIND=JPIM) :: IRESOL, IFIELDS

REAL(KIND=JPRB), ALLOCATABLE :: ZSPSCA(:,:), ZSPVOR(:,:), ZSPDIV(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZMEANU(:), ZMEANV(:), ZFPUMEAN(:), ZFPVMEAN(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZGPP(:,:,:)


REAL(KIND=JPRB) :: ZHOOK_HANDLE
#ifdef __INTEL_COMPILER
INTRINSIC :: SHAPE ! Fails with Intel compiler as it thinks we use ATLAS shape function
#endif


!      -----------------------------------------------------------

#include "abor1.intfb.h"
#include "transdir_fp.intfb.h"
#include "transinv_fp.intfb.h"
#include "endvpos.intfb.h"
#include "sposgf.intfb.h"
#include "spos.intfb.h"
#include "fptsa_dir.intfb.h"
#include "fptsa_inv.intfb.h"

!      -----------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_HVLAG',0,ZHOOK_HANDLE)
ASSOCIATE(YDFPGEOMETRY=>YDFPOS%YFPGEOMETRY, YDFPFILTERS=>YDFPOS%YFPFILTERS, YDFPVAB=>YDFPOS%YFPVAB, &
 & YDFPSTRUCT=>YDFPOS%YFPSTRUCT, YDFPWSTD=>YDFPOS%YFPWSTD, YDFPCNT=>YDFPOS%YFPCNT, YDAFN=>YDFPOS%YAFN)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, &
 & YDFPGEO_DEP=>YDFPGEOMETRY%YFPGEO_DEP, YDFPGEO=>YDFPGEOMETRY%YFPGEO, TFP_DYNDS=>YDAFN%TFP_DYNDS, TFP=>YDAFN%TFP, &
 & YDFPGIND=>YDFPGEOMETRY%YFPGIND, YDFPUSERGEO=>YDFPGEOMETRY%YFPUSERGEO, NFPCONF=>YDFPCNT%NFPCONF)
ASSOCIATE(NFPROMA=>YDFPGEO%NFPROMA, NFPBLOCS=>YDFPGEO%NFPBLOCS, &
 & NFPRESOL=>YDFPUSERGEO(:)%NFPRESOL, NFPSPEC2=>YDFPUSERGEO(:)%NSPEC2)
!      -----------------------------------------------------------

!*       0.    VARIOUS INITIALIZATION
!              ----------------------

CLCONF(1:9)='000000000'

IF (LTRACEFP) THEN
  CALL PRINT_DIMS(CLCONF(5:5),CDV,YDQTYPE)
ENDIF


!     ------------------------------------------------------------------

!*       5.    VERTICAL INTERPOLATIONS AFTER HORIZONTAL ONES
!              ---------------------------------------------

CALL GSTATS(8,0)

CLCONF(6:6)=CDV

IF (PRESENT(P2DFIELDS)) THEN
  ALLOCATE(P2DFIELDS(NFPROMA,YDQTYPE%NFPAUXB,NFPBLOCS))
  PFPBUF(1:NFPROMA,1:YDQTYPE%NFPAUXB,1:NFPBLOCS) => P2DFIELDS
ELSEIF (PRESENT(P3DFIELDS)) THEN
  IFIELDS=YDQTYPE%NFPAUXB/KPXLEV
  IF (YDQTYPE%NFPAUXB /= KPXLEV*IFIELDS) CALL ABOR1('STEPO_FPOS_HVLAG : NFPAUXB /= KPXLEV*IFIELDS')
  ALLOCATE(P3DFIELDS(NFPROMA,KPXLEV,IFIELDS,NFPBLOCS))
  PFPBUF(1:NFPROMA,1:YDQTYPE%NFPAUXB,1:NFPBLOCS) => P3DFIELDS
ELSEIF (PRESENT(P3DPHYS)) THEN
  IF (YDQTYPE%NFPAUXB /= KPXLEV*KP3FIELDS) THEN
    CALL ABOR1('STEPO_FPOS_HVLAG : NFPAUXB /= KPXLEV*IFIELDS')
  ELSE
    PFPBUF(1:NFPROMA,1:YDQTYPE%NFPAUXB,1:NFPBLOCS) => P3DPHYS
  ENDIF
ELSE
  ALLOCATE(PFPBUF(NFPROMA,YDQTYPE%NFPAUXB,NFPBLOCS))
ENDIF
IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'PFPBUF    ',SIZE(PFPBUF),SHAPE(PFPBUF)
ENDIF
ALLOCATE(ZGPP(NFPROMA,YDQTYPE%NFPGT1,NFPBLOCS))
IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'ZGPP       ',SIZE(ZGPP),SHAPE(ZGPP)
ENDIF

LLZ0H=YDSURF%YSD_VVD%NUMFLDS>=8
CALL ENDVPOS(YDMODEL%YRCST,KLMOD,YDQTYPE,YDAUX,YDCLIMO,YDNAMFPSCI,YDAFN,YDFPGEO,YDFPVAB,YDGEOMETRY,YDMODEL%YRML_GCONF,YDMODEL%YRML_PHY_MF, &
 & LLZ0H,CDV,KPXLEV,PXLEV,PABUF,KCUFNR,KGPX,PFPBUF,ZGPP)

! Un peu ad hoc ...
IF (YDQTYPE%IFIT==2) THEN
  DEALLOCATE(PABUF)
  NULLIFY(PABUF)
ENDIF

IF (YDQTYPE%NFPGT1 == 0) THEN
  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '
ENDIF

CALL GSTATS(8,1)

!     ------------------------------------------------------------------

!*       7.    OUTPUT DIRECT TRANSFORMS.
!              ------------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN

  CLCONF(7:7)='F'

  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_HVLAG : NOT READY FOR MULTI-SPECTRAL TRANSFORMS')
  IRESOL=NFPRESOL(1)
  ALLOCATE(ZSPSCA(YDQTYPE%NFPSCA,NFPSPEC2(1)),ZSPVOR(YDQTYPE%NFPVEC,NFPSPEC2(1)),ZSPDIV(YDQTYPE%NFPVEC,NFPSPEC2(1)))
  ALLOCATE(ZMEANU(YDQTYPE%NFPVEC),ZMEANV(YDQTYPE%NFPVEC))

  CALL TRANSDIR_FP(YDQTYPE,IRESOL,ZGPP,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '

ENDIF

IF (ALLOCATED(PSPEC)) DEALLOCATE(PSPEC,ZFPUMEAN,ZFPVMEAN)
ALLOCATE(PSPEC(YDQTYPE%NFPSPB,NFPSPEC2(1)),ZFPUMEAN(YDQTYPE%NFPUVMN),ZFPVMEAN(YDQTYPE%NFPUVMN))
IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'PSPEC    ',SIZE(PSPEC),SHAPE(PSPEC)
  WRITE(NULOUT,CLFMT1) 'ZFPUMEAN    ',SIZE(ZFPUMEAN),SHAPE(ZFPUMEAN)
  WRITE(NULOUT,CLFMT1) 'ZFPVMEAN    ',SIZE(ZFPVMEAN),SHAPE(ZFPVMEAN)
ENDIF

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN

  CALL GSTATS(1992,0)
  CALL FPTSA_DIR(YDQTYPE,TFP,KRESOL=IRESOL,PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:),PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),PVMEANFP=ZFPVMEAN(:))
  CALL GSTATS(1992,1)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

ENDIF

!     ------------------------------------------------------------------

!*       8.    SPECTRAL FILTERS IN OUTPUT SPACE
!              --------------------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN
! Gaussian filter
  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_HVLAG : NOT READY FOR MULTI-SPECTRAL FILTERS')
  IRESOL=NFPRESOL(1)
! Gaussian filter
  CALL SPOSGF(YDQTYPE,IRESOL,TFP_DYNDS(:)%ZFK,PSPEC,LLSPOSGF)
! Low-pass filter
  CALL SPOS(YDQTYPE,YDFPFILTERS,IRESOL,NFPSPEC2(1),PSPEC,LLSPOSLP)
  IF (LLSPOSGF.AND.LLSPOSLP) THEN
    CLCONF(8:8)='P'
  ELSEIF(LLSPOSGF) THEN
    CLCONF(8:8)='G'
  ELSEIF(LLSPOSLP) THEN
    CLCONF(8:8)='T'
  ELSE
    CLCONF(8:8)='0'
  ENDIF
ENDIF

!     ------------------------------------------------------------------

!*       9.    OUTPUT INVERSE TRANSFORMS.
!              -------------------------

IF (YDQTYPE%NFPGT0B > 0 .AND. YDQTYPE%IFIT==2) THEN

  CLCONF(9:9)='F'

  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_HVLAG : NOT READY FOR MULTI-SPECTRAL TRANSFORMS')
  IRESOL=NFPRESOL(1)
  ALLOCATE(ZSPSCA(YDQTYPE%NFPISCA,NFPSPEC2(1)),ZSPVOR(YDQTYPE%NFPIVEC,NFPSPEC2(1)),ZSPDIV(YDQTYPE%NFPIVEC,NFPSPEC2(1)))
  ALLOCATE(ZMEANU(YDQTYPE%NFPIVEC),ZMEANV(YDQTYPE%NFPIVEC))

  CALL FPTSA_INV(YDQTYPE,KRESOL=IRESOL,PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),&
   & PVMEANFP=ZFPVMEAN(:),PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:))

  DEALLOCATE(PSPEC,ZFPUMEAN,ZFPVMEAN)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'PSPEC    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPUMEAN    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPVMEAN    '

  ALLOCATE (ZGPP(NFPROMA,YDQTYPE%NFPGT0B,NFPBLOCS))
  IF (LALLOFP) THEN
    WRITE(NULOUT,CLFMT1) 'ZGPP       ',SIZE(ZGPP),SHAPE(ZGPP)
  ENDIF

  CALL TRANSINV_FP(YDQTYPE,IRESOL,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV,ZGPP)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

!  CALL ABOR1('STEPO_FPOS_HVLAG:ZGPP TO BE TRANSFERRED TO PFPBUF')

  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '

ENDIF

WRITE(NULOUT,'('' STEPO_FPOS_HVLAG WAS : '',A9)') CLCONF

! Deallocate local arrays
!
IF(ALLOCATED(ZGPP))DEALLOCATE(ZGPP)
IF(ALLOCATED(ZSPSCA))DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV)
IF(ALLOCATED(ZMEANU))DEALLOCATE(ZMEANU,ZMEANV)

END ASSOCIATE
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_HVLAG',1,ZHOOK_HANDLE)

CONTAINS

SUBROUTINE PRINT_DIMS(CD1,CD2,YDQTYPE)

TYPE (TYPE_FPRQDYN),  INTENT(IN) :: YDQTYPE
CHARACTER(LEN=1),INTENT(IN) :: CD1
CHARACTER(LEN=1),INTENT(IN) :: CD2
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_HVLAG:PRINT_DIMS',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & NFPAUXB=>YDQTYPE%NFPAUXB, NFPDYNB=>YDQTYPE%NFPDYNB, NFPGT0B=>YDQTYPE%NFPGT0B, NFPGT1=>YDQTYPE%NFPGT1, &
 & NFPUVMN=>YDQTYPE%NFPUVMN, NFPSCA=>YDQTYPE%NFPSCA, NFPSPB=>YDQTYPE%NFPSPB, NFPVEC=>YDQTYPE%NFPVEC, &
 & NFPISCA=>YDQTYPE%NFPISCA, NFPIVEC=>YDQTYPE%NFPIVEC, NFPSPD=>YDQTYPE%NFPSPD, NFPVECG=>YDQTYPE%NFPVECG, &
 & NFPSCAG=>YDQTYPE%NFPSCAG, NFPIVECG=>YDQTYPE%NFPIVECG, NFPISCAG=>YDQTYPE%NFPISCAG)

  WRITE(UNIT=NULOUT,FMT='(/,'' FULLPOS DIMENSIONNING FOR CONFIGURATION '',2A1)') CD1,CD2
  WRITE(UNIT=NULOUT,FMT='('' NFPGT1 = '',I4,'' NFPAUXB = '',I4,'' NFPSPD = '',I4,&
   & '' NFPSPB = '',I4,'' NFPVEC = '',I4,'' NFPSCA = '',I4,'' NFPGT0B = '',I4,&
   & '' NFPDYNB = '',I4)') NFPGT1,NFPAUXB,NFPSPD,NFPSPB,NFPVEC,NFPSCA,NFPGT0B,NFPDYNB
  WRITE(UNIT=NULOUT,FMT='('' NFPVEC = '',I4,'' NFPSCA = '',I4,'' NFPVECG = '',I4,&
   & '' NFPSCAG = '',I4)') NFPVEC,NFPSCA,NFPVECG,NFPSCAG
  WRITE(UNIT=NULOUT,FMT='('' NFPIVEC = '',I4,'' NFPISCA = '',I4,'' NFPIVECG = '',&
   & I4,'' NFPISCAG = '',I4)') NFPIVEC,NFPISCA,NFPIVECG,NFPISCAG
  WRITE(UNIT=NULOUT,FMT='('' NFPUVMN = '',I4)') NFPUVMN

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_HVLAG:PRINT_DIMS',1,ZHOOK_HANDLE)

END SUBROUTINE PRINT_DIMS

END SUBROUTINE STEPO_FPOS_HVLAG
