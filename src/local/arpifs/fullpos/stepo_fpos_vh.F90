SUBROUTINE STEPO_FPOS_VH(YDQTYPE,YDAUX,YDCLIMO,YDNAMFPINT,YDNAMFPSCI,KFPXFLD,YDFPOS,KFPDOM,YDGEOMETRY,YDGMV,YDGFL,YDSURF,YDXFU,KCUFNR, &
 & PMCUFGP,YDMODEL,CDV,CDH,KPXLEV,PXLEV,LDHPOS,LDEZO,PFPBUF,PSPEC,P2DFIELDS,P3DFIELDS,P3DPHYS,KP3FIELDS)

!**** *STEPO_FPOS_VH*  - Controls Fullpos job at lowest level - vertical interpolations then horizontal interpolations
!                        for surface-independent post-processing levels
!     Purpose.
!     --------
!        CONTROLS THE POST-PROCESSING.

!**   Interface.
!     ----------
!        *CALL* *STEPO_FPOS_VH(CDV,CDH)

!        Explicit arguments :
!        --------------------
!         KFPXFLD : maximum number of fields to extract at a time
!         KFPDOM : number of subdomains
!         CDV : configuration of vertical post-processing (in)
!         CDH : configuration of horizontal post-processing (in)
!         LDHPOS : .true. if any horizontal post-processing
!         LDEZO : .true. if biperiodicization active
!         P2DFIELDS : alternative 2D output array to PFPBUF
!         P3DFIELDS : alternative 3D output array to PFPBUF (not allocated)
!         P3DPHYS : alternative 3D output array to PFPBUF for surface fields (preallocated)
!         KP3FIELDS : number of fields in P3DPHYS

!        Implicit arguments :
!        --------------------
!        None

!     Method.
!     -------
!        See documentation

!     Externals.    see includes below.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Ryad El Khatib  *Meteo-France*
!      Original : 16-Jul-2012 from STEPO

! Modifications
! -------------
!      R. El Khatib 13-Dec-2012 Fullpos buffers reshaping, memory savings
!      R. El Khatib 17-Jul-2013 FABEC post-processing
!      R. El Khatib 27-Sep-2013 Boyd periodization in Fullpos-2
!      R. El Khatib & D. Salmond 03-Jun-2014 Fix deallocate issues
!      R. El Khatib 08-Dec-2015 Move out I/Os.
!      R. El Khatib 09-Aug-2016 Cleaning of biperiodicization management
!      R. El Khatib 29-May-2019 split from stepo_fpos
! End Modifications
!------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE TYPE_MODEL         , ONLY : MODEL
USE YOMGMV             , ONLY : TGMV
USE YOMGFL             , ONLY : TGFL
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE YOMXFU             , ONLY : TXFU
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT
USE YOMFPC             , ONLY : TNAMFPSCI, TNAMFPINT, LTRACEFP, LALLOFP
USE TYPE_FPRQDYNS      , ONLY : TYPE_FPRQDYN
USE YOMVERT            , ONLY : TVAB
USE FULLPOS            , ONLY : TFPOS
USE TYPE_FPOSBUF       , ONLY : FPOSBUF

!      -----------------------------------------------------------

IMPLICIT NONE

TYPE (TYPE_FPRQDYN), INTENT(IN) :: YDQTYPE
TYPE(FPOSBUF)     ,INTENT(IN) :: YDAUX
TYPE(FPOSBUF)     ,INTENT(IN) :: YDCLIMO
TYPE(TNAMFPINT)   ,INTENT(IN) :: YDNAMFPINT
TYPE(TNAMFPSCI)   ,INTENT(IN) :: YDNAMFPSCI
INTEGER(KIND=JPIM),INTENT(IN) :: KFPXFLD
TYPE (TFPOS)      ,INTENT(IN) :: YDFPOS
INTEGER(KIND=JPIM),INTENT(IN) :: KFPDOM
TYPE(GEOMETRY)    ,INTENT(IN) :: YDGEOMETRY
TYPE(TGMV)        ,INTENT(IN) :: YDGMV
TYPE(TGFL)        ,INTENT(IN) :: YDGFL
TYPE(TSURF)       ,INTENT(IN) :: YDSURF
TYPE(TXFU)        ,INTENT(IN) :: YDXFU
INTEGER(KIND=JPIM),INTENT(IN) :: KCUFNR
REAL(KIND=JPRB)   ,INTENT(IN) :: PMCUFGP(YDGEOMETRY%YRDIM%NPROMA,KCUFNR,YDGEOMETRY%YRDIM%NGPBLKS)
TYPE(MODEL)       ,INTENT(IN) :: YDMODEL
CHARACTER(LEN=1)  ,INTENT(IN) :: CDV
CHARACTER(LEN=1)  ,INTENT(IN) :: CDH
INTEGER(KIND=JPIM),INTENT(IN) :: KPXLEV
REAL(KIND=JPRB)   ,INTENT(IN) :: PXLEV(KPXLEV)
INTEGER(KIND=JPIM),INTENT(IN) :: KP3FIELDS
LOGICAL           ,INTENT(IN) :: LDHPOS
LOGICAL           ,INTENT(IN) :: LDEZO
REAL(KIND=JPRB)   ,INTENT(OUT), POINTER :: PFPBUF(:,:,:)
REAL(KIND=JPRB)   ,INTENT(OUT), ALLOCATABLE :: PSPEC(:,:)
REAL(KIND=JPRB)   ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P2DFIELDS(:,:,:)
REAL(KIND=JPRB)   ,ALLOCATABLE, TARGET, INTENT(INOUT), OPTIONAL :: P3DFIELDS(:,:,:,:)
REAL(KIND=JPRB)   ,TARGET, INTENT(INOUT), OPTIONAL :: &
 & P3DPHYS(YDFPOS%YFPGEOMETRY%YFPGEO%NFPROMA,KPXLEV,KP3FIELDS,YDFPOS%YFPGEOMETRY%YFPGEO%NFPBLOCS)

! CLCONF(character*9) :
! configuration of post-processing computation built from dimensionning and arguments
! 1  : vertical post-processing
! 2  : model direct Legendre / North/South Fourier transforms
! 3  : spectral filters in model space
! 4  : model inverse Legendre / North/South Fourier transforms
! 5  : horizontal post processing
! 6  : lagged vertical post-processing 
! 7  : output direct Legendre / North/South Fourier transforms
! 8  : spectral filters in output spectral space
! 9  : output inverse Legendre / East/West Fourier transforms

!     ZAVEG,ZMING,ZMAXG  : global norms of the fields (mean val, min val, max val)

CHARACTER(LEN=9)  :: CLCONF
CHARACTER(LEN=35) :: CLFMT1='(1X,''ARRAY '',A10,'' ALLOCATED '',8I8)'
CHARACTER(LEN=33) :: CLFMT0='(1X,''ARRAY '',A10,'' DEALLOCATED '')'

LOGICAL :: LLSPOSGF, LLSPOSLP
INTEGER(KIND=JPIM) :: IRESOL, IFIELDS

REAL(KIND=JPRB), ALLOCATABLE :: ZSPSCA(:,:), ZSPVOR(:,:), ZSPDIV(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZMEANU(:), ZMEANV(:), ZFPUMEAN(:), ZFPVMEAN(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZAUX(:,:,:), ZGPP(:,:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE
#ifdef __INTEL_COMPILER
INTRINSIC :: SHAPE ! Fails with Intel compiler as it thinks we use ATLAS shape function
#endif


!      -----------------------------------------------------------

#include "abor1.intfb.h"
#include "transdir_fp.intfb.h"
#include "transinv_fp.intfb.h"
#include "scan2m_vpos.intfb.h"
#include "dynfpos.intfb.h"
#include "sposgf.intfb.h"
#include "spos.intfb.h"
#include "fptsa_dir.intfb.h"
#include "fptsa_inv.intfb.h"

!      -----------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_VH',0,ZHOOK_HANDLE)
ASSOCIATE(YDFPGEOMETRY=>YDFPOS%YFPGEOMETRY, YDFPFILTERS=>YDFPOS%YFPFILTERS, YDFPVAB=>YDFPOS%YFPVAB, &
 & YDFPSTRUCT=>YDFPOS%YFPSTRUCT, YDFPWSTD=>YDFPOS%YFPWSTD, YDFPCNT=>YDFPOS%YFPCNT, YDAFN=>YDFPOS%YAFN)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG, &
 & YDFPGEO_DEP=>YDFPGEOMETRY%YFPGEO_DEP, YDFPGEO=>YDFPGEOMETRY%YFPGEO, TFP_DYNDS=>YDAFN%TFP_DYNDS, TFP=>YDAFN%TFP, &
 & YDFPGIND=>YDFPGEOMETRY%YFPGIND, YDFPUSERGEO=>YDFPGEOMETRY%YFPUSERGEO, NFPCONF=>YDFPCNT%NFPCONF)
ASSOCIATE(NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA, NSPEC2=>YDDIM%NSPEC2, NRESOL=>YDDIM%NRESOL, &
 & NFPROMA=>YDFPGEO%NFPROMA, NFPBLOCS=>YDFPGEO%NFPBLOCS, &
 & NFPRESOL=>YDFPUSERGEO(:)%NFPRESOL, NFPSPEC2=>YDFPUSERGEO(:)%NSPEC2)
!      -----------------------------------------------------------

!*       0.    VARIOUS INITIALIZATION
!              ----------------------

IF (LTRACEFP) THEN
  CALL PRINT_DIMS(CDV,CDH,YDQTYPE)
ENDIF

CLCONF(1:9)='000000000'

!     ------------------------------------------------------------------

!*       1.   VERTICAL INTERPOLATION
!             ----------------------

CLCONF(1:1)=CDV

ALLOCATE(ZGPP(NPROMA,YDQTYPE%NFPGT1,NGPBLKS))
IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'ZGPP       ',SIZE(ZGPP),SHAPE(ZGPP)
ENDIF

IF (YDQTYPE%NFPGT1 > 0 .OR. CDH == '0') THEN

  IF (CDH == 'A') THEN

    ALLOCATE(ZAUX(NPROMA,YDQTYPE%NFPAUXB,NGPBLKS))
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT1) 'ZAUX    ',SIZE(ZAUX),SHAPE(ZAUX)
    ENDIF

    CALL SCAN2M_VPOS(YDMODEL%YRCST,YDQTYPE,YDNAMFPSCI,YDAFN,LDHPOS,YDFPVAB,YDGEOMETRY,YDGMV,YDGFL,YDSURF,YDXFU,KCUFNR,PMCUFGP, &
     & YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_PHY_MF,CDV,KPXLEV,PXLEV,TFP_DYNDS,ZAUX,ZGPP)

  ELSE

    IF (PRESENT(P2DFIELDS)) THEN
      ALLOCATE(P2DFIELDS(NPROMA,YDQTYPE%NFPAUXB,NGPBLKS))
      PFPBUF(1:NPROMA,1:YDQTYPE%NFPAUXB,1:NGPBLKS) => P2DFIELDS
    ELSEIF (PRESENT(P3DFIELDS)) THEN
      IFIELDS=YDQTYPE%NFPAUXB/KPXLEV
      IF (YDQTYPE%NFPAUXB /= KPXLEV*IFIELDS) CALL ABOR1('STEPO_FPOS_VH : NFPAUXB /= KPXLEV*IFIELDS')
      ALLOCATE(P3DFIELDS(NPROMA,KPXLEV,IFIELDS,NGPBLKS))
      PFPBUF(1:NPROMA,1:YDQTYPE%NFPAUXB,1:NGPBLKS) => P3DFIELDS
    ELSEIF (PRESENT(P3DPHYS)) THEN
      IF (YDQTYPE%NFPAUXB /= KPXLEV*KP3FIELDS) THEN
        CALL ABOR1('STEPO_FPOS_VH : NFPAUXB /= KPXLEV*IFIELDS')
      ELSE
        PFPBUF(1:NPROMA,1:YDQTYPE%NFPAUXB,1:NGPBLKS) => P3DPHYS
      ENDIF
    ELSE
      ALLOCATE(PFPBUF(NPROMA,YDQTYPE%NFPAUXB,NGPBLKS))
    ENDIF
    IF (LALLOFP) THEN
      WRITE(NULOUT,CLFMT1) 'PFPBUF    ',SIZE(PFPBUF),SHAPE(PFPBUF)
    ENDIF

    CALL SCAN2M_VPOS(YDMODEL%YRCST,YDQTYPE,YDNAMFPSCI,YDAFN,LDHPOS,YDFPVAB,YDGEOMETRY,YDGMV,YDGFL,YDSURF,YDXFU,KCUFNR,PMCUFGP, &
     & YDMODEL%YRML_GCONF,YDMODEL%YRML_DYN%YRDYN,YDMODEL%YRML_PHY_MF,CDV,KPXLEV,PXLEV,TFP_DYNDS,PFPBUF,ZGPP)

  ENDIF

ENDIF

IF (CDH == '0' .AND. YDQTYPE%NFPGT1 == 0) THEN
  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '
ENDIF


!     ------------------------------------------------------------------

!*       2.    MODEL DIRECT TRANSFORMS.
!              -----------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. (YDQTYPE%IFIT==1)) THEN

  CLCONF(2:2)='P'
  IRESOL=NRESOL
  ALLOCATE(ZSPSCA(YDQTYPE%NFPSCA,NSPEC2),ZSPVOR(YDQTYPE%NFPVEC,NSPEC2),ZSPDIV(YDQTYPE%NFPVEC,NSPEC2))
  ALLOCATE(ZMEANU(YDQTYPE%NFPVEC),ZMEANV(YDQTYPE%NFPVEC))

  CALL TRANSDIR_FP(YDQTYPE,IRESOL,ZGPP,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '

ENDIF

ALLOCATE(PSPEC(YDQTYPE%NFPSPB,NSPEC2),ZFPUMEAN(YDQTYPE%NFPUVMN),ZFPVMEAN(YDQTYPE%NFPUVMN))

IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'PSPEC    ',SIZE(PSPEC),SHAPE(PSPEC)
  WRITE(NULOUT,CLFMT1) 'ZFPUMEAN    ',SIZE(ZFPUMEAN),SHAPE(ZFPUMEAN)
  WRITE(NULOUT,CLFMT1) 'ZFPVMEAN    ',SIZE(ZFPVMEAN),SHAPE(ZFPVMEAN)
ENDIF

IF (YDQTYPE%NFPGT1 > 0 .AND. (YDQTYPE%IFIT==1)) THEN

  CALL GSTATS(1992,0)
  CALL FPTSA_DIR(YDQTYPE,TFP,KRESOL=IRESOL,PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:),PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),PVMEANFP=ZFPVMEAN(:))
  CALL GSTATS(1992,1)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

ENDIF


!     ------------------------------------------------------------------

!*       3.    SPECTRAL FILTERS IN MODEL SPACE
!              -------------------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. (YDQTYPE%IFIT==1)) THEN
  ! Gaussian filter
  CALL SPOSGF(YDQTYPE,IRESOL,TFP_DYNDS(:)%ZFK,PSPEC,LLSPOSGF)
  ! Low-pass filter
  CALL SPOS(YDQTYPE,YDFPFILTERS,IRESOL,NSPEC2,PSPEC,LLSPOSLP)
  IF (LLSPOSGF.AND.LLSPOSLP) THEN
    CLCONF(3:3)='P'
  ELSEIF(LLSPOSGF) THEN
    CLCONF(3:3)='G'
  ELSEIF(LLSPOSLP) THEN
    CLCONF(3:3)='T'
  ELSE
    CLCONF(3:3)='0'
  ENDIF
ENDIF

!     ------------------------------------------------------------------

!*       4.    MODEL INVERSE TRANSFORMS.
!              ------------------------

IF (CDH == 'A' .AND. YDQTYPE%NFPGT0B > 0 .AND. (YDQTYPE%IFIT==1)) THEN

  CLCONF(4:4)='P'
  ALLOCATE(ZSPSCA(YDQTYPE%NFPISCA,NSPEC2),ZSPVOR(YDQTYPE%NFPIVEC,NSPEC2),ZSPDIV(YDQTYPE%NFPIVEC,NSPEC2))
  ALLOCATE(ZMEANU(YDQTYPE%NFPIVEC),ZMEANV(YDQTYPE%NFPIVEC))

  CALL FPTSA_INV(YDQTYPE,KRESOL=IRESOL,PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),&
   & PVMEANFP=ZFPVMEAN(:),PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:))

  DEALLOCATE(PSPEC,ZFPUMEAN,ZFPVMEAN)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'PSPEC    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPUMEAN    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPVMEAN    '

  ALLOCATE (ZGPP(NPROMA,YDQTYPE%NFPGT0B,NGPBLKS))
  IF (LALLOFP) THEN
    WRITE(NULOUT,CLFMT1) 'ZGPP       ',SIZE(ZGPP),SHAPE(ZGPP)
  ENDIF

  CALL TRANSINV_FP(YDQTYPE,IRESOL,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV,ZGPP)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

ENDIF

!     ------------------------------------------------------------------

!*       5.    HORIZONTAL INTERPOLATIONS
!              -------------------------

CALL GSTATS(8,0)

CLCONF(5:5)=CDH

IF (CDH == 'A') THEN

! HORIZONTAL INTERPOLATIONS ONLY

  CALL DYNFPOS(YDQTYPE,YDCLIMO,YDNAMFPINT,YDNAMFPSCI,YDAFN,KFPXFLD,YDFPGEOMETRY,YDFPWSTD,YDFPSTRUCT,KFPDOM, &
   & LDHPOS,LDEZO,YDGEOMETRY,YDGMV,YDGFL,YDSURF,YDXFU,KCUFNR,PMCUFGP,YDMODEL,CDV,KPXLEV,PXLEV,&
   & PFPBUF,PGPP=ZGPP,PAUX=ZAUX,P2DFIELDS=P2DFIELDS,P3DFIELDS=P3DFIELDS,P3DPHYS=P3DPHYS,KP3FIELDS=KP3FIELDS)

ENDIF

CALL GSTATS(8,1)

!     ------------------------------------------------------------------

!*       7.    OUTPUT DIRECT TRANSFORMS.
!              ------------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN

  CLCONF(7:7)='F'

  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_VH : NOT READY FOR MULTI-SPECTRAL TRANSFORMS')
  IRESOL=NFPRESOL(1)
  ALLOCATE(ZSPSCA(YDQTYPE%NFPSCA,NFPSPEC2(1)),ZSPVOR(YDQTYPE%NFPVEC,NFPSPEC2(1)),ZSPDIV(YDQTYPE%NFPVEC,NFPSPEC2(1)))
  ALLOCATE(ZMEANU(YDQTYPE%NFPVEC),ZMEANV(YDQTYPE%NFPVEC))

  CALL TRANSDIR_FP(YDQTYPE,IRESOL,ZGPP,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '

ENDIF

IF (ALLOCATED(PSPEC)) DEALLOCATE(PSPEC,ZFPUMEAN,ZFPVMEAN)
ALLOCATE(PSPEC(YDQTYPE%NFPSPB,NFPSPEC2(1)),ZFPUMEAN(YDQTYPE%NFPUVMN),ZFPVMEAN(YDQTYPE%NFPUVMN))
IF (LALLOFP) THEN
  WRITE(NULOUT,CLFMT1) 'PSPEC    ',SIZE(PSPEC),SHAPE(PSPEC)
  WRITE(NULOUT,CLFMT1) 'ZFPUMEAN    ',SIZE(ZFPUMEAN),SHAPE(ZFPUMEAN)
  WRITE(NULOUT,CLFMT1) 'ZFPVMEAN    ',SIZE(ZFPVMEAN),SHAPE(ZFPVMEAN)
ENDIF

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN

  CALL GSTATS(1992,0)
  CALL FPTSA_DIR(YDQTYPE,TFP,KRESOL=IRESOL,PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:),PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),PVMEANFP=ZFPVMEAN(:))
  CALL GSTATS(1992,1)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

ENDIF

!     ------------------------------------------------------------------

!*       8.    SPECTRAL FILTERS IN OUTPUT SPACE
!              --------------------------------

IF (YDQTYPE%NFPGT1 > 0 .AND. YDQTYPE%IFIT==2) THEN
! Gaussian filter
  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_VH : NOT READY FOR MULTI-SPECTRAL FILTERS')
  IRESOL=NFPRESOL(1)
! Gaussian filter
  CALL SPOSGF(YDQTYPE,IRESOL,TFP_DYNDS(:)%ZFK,PSPEC,LLSPOSGF)
! Low-pass filter
  CALL SPOS(YDQTYPE,YDFPFILTERS,IRESOL,NFPSPEC2(1),PSPEC,LLSPOSLP)
  IF (LLSPOSGF.AND.LLSPOSLP) THEN
    CLCONF(8:8)='P'
  ELSEIF(LLSPOSGF) THEN
    CLCONF(8:8)='G'
  ELSEIF(LLSPOSLP) THEN
    CLCONF(8:8)='T'
  ELSE
    CLCONF(8:8)='0'
  ENDIF
ENDIF

!     ------------------------------------------------------------------

!*       9.    OUTPUT INVERSE TRANSFORMS.
!              -------------------------

IF (YDQTYPE%NFPGT0B > 0 .AND. YDQTYPE%IFIT==2) THEN

  CLCONF(9:9)='F'

  IF (KFPDOM > 1) CALL ABOR1 ('STEPO_FPOS_VH : NOT READY FOR MULTI-SPECTRAL TRANSFORMS')
  IRESOL=NFPRESOL(1)
  ALLOCATE(ZSPSCA(YDQTYPE%NFPISCA,NFPSPEC2(1)),ZSPVOR(YDQTYPE%NFPIVEC,NFPSPEC2(1)),ZSPDIV(YDQTYPE%NFPIVEC,NFPSPEC2(1)))
  ALLOCATE(ZMEANU(YDQTYPE%NFPIVEC),ZMEANV(YDQTYPE%NFPIVEC))

  CALL FPTSA_INV(YDQTYPE,KRESOL=IRESOL,PSPBFP=PSPEC(:,:),PUMEANFP=ZFPUMEAN(:),&
   & PVMEANFP=ZFPVMEAN(:),PSCA=ZSPSCA(:,:),PVOR=ZSPVOR(:,:),PDIV=ZSPDIV(:,:),&
   & PUMEAN=ZMEANU(:),PVMEAN=ZMEANV(:))

  DEALLOCATE(PSPEC,ZFPUMEAN,ZFPVMEAN)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'PSPEC    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPUMEAN    '
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZFPVMEAN    '

  ALLOCATE (ZGPP(NPROMA,YDQTYPE%NFPGT0B,NGPBLKS))
  IF (LALLOFP) THEN
    WRITE(NULOUT,CLFMT1) 'ZGPP       ',SIZE(ZGPP),SHAPE(ZGPP)
  ENDIF

  CALL TRANSINV_FP(YDQTYPE,IRESOL,ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV,ZGPP)

  DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV,ZMEANU,ZMEANV)

!  CALL ABOR1('STEPO_FPOS_VH:ZGPP TO BE TRANSFERRED TO PFPBUF')

  DEALLOCATE(ZGPP)
  IF (LALLOFP) WRITE(NULOUT,CLFMT0) 'ZGPP '

ENDIF

WRITE(NULOUT,'('' STEPO_FPOS_VH WAS : '',A9)') CLCONF

! Deallocate local arrays
!
IF(ALLOCATED(ZGPP))DEALLOCATE(ZGPP)
IF(ALLOCATED(ZSPSCA))DEALLOCATE(ZSPSCA,ZSPVOR,ZSPDIV)
IF(ALLOCATED(ZMEANU))DEALLOCATE(ZMEANU,ZMEANV)

END ASSOCIATE
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_VH',1,ZHOOK_HANDLE)

CONTAINS

SUBROUTINE PRINT_DIMS(CD1,CD2,YDQTYPE)

TYPE (TYPE_FPRQDYN),  INTENT(IN) :: YDQTYPE
CHARACTER(LEN=1),INTENT(IN) :: CD1
CHARACTER(LEN=1),INTENT(IN) :: CD2
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_VH:PRINT_DIMS',0,ZHOOK_HANDLE)
ASSOCIATE(&
 & NFPAUXB=>YDQTYPE%NFPAUXB, NFPDYNB=>YDQTYPE%NFPDYNB, NFPGT0B=>YDQTYPE%NFPGT0B, NFPGT1=>YDQTYPE%NFPGT1, &
 & NFPUVMN=>YDQTYPE%NFPUVMN, NFPSCA=>YDQTYPE%NFPSCA, NFPSPB=>YDQTYPE%NFPSPB, NFPVEC=>YDQTYPE%NFPVEC, &
 & NFPISCA=>YDQTYPE%NFPISCA, NFPIVEC=>YDQTYPE%NFPIVEC, NFPSPD=>YDQTYPE%NFPSPD, NFPVECG=>YDQTYPE%NFPVECG, &
 & NFPSCAG=>YDQTYPE%NFPSCAG, NFPIVECG=>YDQTYPE%NFPIVECG, NFPISCAG=>YDQTYPE%NFPISCAG)

  WRITE(UNIT=NULOUT,FMT='(/,'' FULLPOS DIMENSIONNING FOR CONFIGURATION '',2A1)') CD1,CD2
  WRITE(UNIT=NULOUT,FMT='('' NFPGT1 = '',I4,'' NFPAUXB = '',I4,'' NFPSPD = '',I4,&
   & '' NFPSPB = '',I4,'' NFPVEC = '',I4,'' NFPSCA = '',I4,'' NFPGT0B = '',I4,&
   & '' NFPDYNB = '',I4)') NFPGT1,NFPAUXB,NFPSPD,NFPSPB,NFPVEC,NFPSCA,NFPGT0B,NFPDYNB
  WRITE(UNIT=NULOUT,FMT='('' NFPVEC = '',I4,'' NFPSCA = '',I4,'' NFPVECG = '',I4,&
   & '' NFPSCAG = '',I4)') NFPVEC,NFPSCA,NFPVECG,NFPSCAG
  WRITE(UNIT=NULOUT,FMT='('' NFPIVEC = '',I4,'' NFPISCA = '',I4,'' NFPIVECG = '',&
   & I4,'' NFPISCAG = '',I4)') NFPIVEC,NFPISCA,NFPIVECG,NFPISCAG
  WRITE(UNIT=NULOUT,FMT='('' NFPUVMN = '',I4)') NFPUVMN

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('STEPO_FPOS_VH:PRINT_DIMS',1,ZHOOK_HANDLE)

END SUBROUTINE PRINT_DIMS

END SUBROUTINE STEPO_FPOS_VH
