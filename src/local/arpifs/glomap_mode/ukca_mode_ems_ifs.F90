SUBROUTINE UKCA_MODE_EMS_IFS &
! Interface routine for UKCA_MODE_EMS, called from CALLPAR in the IFS
! Inputs:
 &( YDGEOMETRY, YDMODEL, KDIM, &
 &  KSTEP, KSTART, KTRAC, KAERO, &
 &  PAREA, PGELAM, PGELAT, PCI, PLSM, PAZ0M, &
 &  PAERUST, PGAW, &
 &  PBCBF, PBCFF, PBCGF, POMBF, POMFF, POMGF, &
 &  PSO2L, PSO2H, PSOGF, PVOLC, PVOLE, PSOA, PDMS, &
 &  PVOLCALTI, PLAIL, PLAIH, PFRTI, PWS1, &
 &  PT, PQ, PRS1, PAPRSF, PAPHI, &
 &  PTS, PTSPHY, PSST, PINJF, PBLH,&
 &  KSW, PALB, PALBD, PWIND, PAERGUST,PHSDFOR, PSNS, &
 &  PAERLTS,PAERSCC, PZ0M, PDSF, PSO2DD, PCEN, &
 &  PSO4, PNH4, PNH3, PHNO3, PNO3, &
! Outputs:
 &  PCFLX, PTENC, PEXTRA, &
 &  PGLO_MASS_EMS, PGLO_NUM_EMS, &
 &  PAERSRC, PAERDDP, &
 &  PDMSO, PLDAY, PLISS, PSO2, PTDMS, &
 &  PDMSI, PODMS, PTHNO3, PTNH3 )

! .. Below are input variables for UKCA_MODE_EMS_IFS ***INTERFACE***

! KDIM%KSTGLO    : global offset (used in message passing version)
! PAREA  grid area                      (m^2)
! PGELAM longitude                      (radians)
! PGELAT latitude                       (radians)
! PCI    sea-ice fraction               (0-1)
! PLSM   land-sea mask                  (0-1)
! PAZ0M : roughness length for heat flux (m)

! PAERUST  : Surface friction velocity  (m s-1)

! PBCBF      : black carbon biogenic             (kg m-2 s-1)
! PBCFF      : black carbon fossil fuel
! PBCGF      : black carbon GFED (fires)
! POMBF      : organic matter biogenic
! POMFF      : organic matter fossil fuel
! POMGF      : organic matter GFED (fires)
! PSO2L      : SO2 low-level (in mol/m2/s ?? See aer_src)
! PSO2H      : SO2 higher-level (in mol/m2/s ?? See aer_src)
! PSOGF      : SO2 GFED (fires) (in mol/m2/s ?? See aer_src)
! PVOLC      : volcanic continuous
! PINJF      : BB aerosol injection height
! PBLH       : Boundary layer Height
! PVOLE      : volcanic explosive
! PSOA       : secondary organic aerosols
! PDSF       : dust source function
! PSO2DD     : SO2 dry dep velocities from SUM0
! PDMS       : DMS
! PLAIL      : Leaf area index for low vegetation (m2/m2)
! PLAIH      : Leaf area index for high vegetation (m2/m2)
! PFRTI      : Tile fractions:
!            1 : WATER                  5 : SNOW ON LOW-VEG+BARE-SOIL
!            2 : ICE                    6 : DRY SNOW-FREE HIGH-VEG
!            3 : WET SKIN               7 : SNOW UNDER HIGH-VEG
!            4 : DRY SNOW-FREE LOW-VEG  8 : BARE SOIL
! PWS1       : Soil wetness (m3/m3)
! PWIND      : 10-m wind
! PAPRSF     : PRESSURE ON FULL LEVELS.
! PRS1       : PRESSURE ON HALF LEVELS.
! PT         : TEMPERATURE.

! Outputs:
! --------

! PCFLX(KDIM%KLON,KTRAC)         : SURFACE FLUX OF TRACERS              (xx m-2)

!
!--------------------------------------------------------------------
USE PARKIND1  ,ONLY : JPIM, JPRB, JPRD
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE YOMLUN    , ONLY : NULOUT

USE YOMPHYDER ,ONLY : DIMENSION_TYPE

USE UKCA_CONSTANTS, ONLY: GG, MM_DA, ZBOLTZ
USE UKCA_MODE_SETUP, ONLY: NMODES, CP_BC, CP_CL, CP_DU, CP_OC, CP_SU, CP_NI, CP_AM, NCP, MODE, COMPONENT, MM

USE YOMCST, ONLY : RPI, RDAY, RG, RD, YRCST
USE YOETHF, ONLY : YRTHF
USE YOMCT3, ONLY : NSTEP
USE YOMRIP0      , ONLY : NINDAT, NSSSSS
USE YOMDYNCORE,ONLY : RPLRADI
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE TYPE_MODEL   , ONLY : MODEL
USE YOMCHEM   ,ONLY : IEXTR_EM

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)  :: YDGEOMETRY
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
TYPE (DIMENSION_TYPE) , INTENT (IN)   :: KDIM

INTEGER(KIND=JPIM),INTENT(IN)  :: KTRAC, KSTEP, KSTART
INTEGER(KIND=JPIM),INTENT(IN)  :: KAERO(YDMODEL%YRML_GCONF%YGFL%NAERO)
INTEGER(KIND=JPIM),INTENT(IN)  :: KSW

REAL(KIND=JPRB)   ,INTENT(IN)  :: PALB(KDIM%KLON) , PALBD(KDIM%KLON,KSW)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAERGUST(KDIM%KLON) , PWIND(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PHSDFOR(KDIM%KLON) , PWS1(KDIM%KLON),PSNS(KDIM%KLON)


REAL(KIND=JPRB)   ,INTENT(IN)  :: PAREA(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PGELAM(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PGELAT(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PCI(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PVOLCALTI(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PLSM(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAZ0M(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAERUST(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PGAW(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PBCBF(KDIM%KLON), PBCFF(KDIM%KLON), PBCGF(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: POMBF(KDIM%KLON), POMFF(KDIM%KLON), POMGF(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PSO2L(KDIM%KLON), PSO2H(KDIM%KLON), PSOGF(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PINJF(KDIM%KLON), PBLH(KDIM%KLON), PSST(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PVOLC(KDIM%KLON), PVOLE(KDIM%KLON), PSOA(KDIM%KLON) , PDMS(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PLAIL(KDIM%KLON), PLAIH(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PFRTI(KDIM%KLON,KDIM%KTILES)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAPRSF(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PRS1(KDIM%KLON,0:KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAPHI(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PT(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PQ(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PTS(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PTSPHY
REAL(KIND=JPRB)   ,INTENT(IN)  :: PAERLTS(KDIM%KLON),PAERSCC(KDIM%KLON),PZ0M(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PDSF(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PSO2DD(KDIM%KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT)  :: PCEN(KDIM%KLON,KDIM%KLEV,KTRAC)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PSO4(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PNH3(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PNH4(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PHNO3(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)  :: PNO3(KDIM%KLON,KDIM%KLEV)



REAL(KIND=JPRB)   ,INTENT(OUT) :: PCFLX(KDIM%KLON,KTRAC)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENC(KDIM%KLON,KDIM%KLEV,KTRAC)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEXTRA(KDIM%KLON,KDIM%KLEVX,KDIM%KFLDX)
REAL(KIND=JPRB)   ,INTENT(OUT) :: PGLO_MASS_EMS(KDIM%KLON,NMODES+35), PGLO_NUM_EMS(KDIM%KLON,NMODES+21)
REAL(KIND=JPRB)   ,INTENT(OUT) :: PAERSRC(KDIM%KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB)   ,INTENT(OUT) :: PAERDDP(KDIM%KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB),INTENT(OUT)   :: PDMSO(KDIM%KLON), PLDAY(KDIM%KLON), PLISS(KDIM%KLON), PSO2(KDIM%KLON), PTDMS(KDIM%KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PDMSI(KDIM%KLON), PODMS(KDIM%KLON)
REAL(KIND=JPRB), INTENT(INOUT) :: PTHNO3(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PTNH3(KDIM%KLON,KDIM%KLEV)




! .. Below are local variables which are inputs for UKCA_MODE_EMS
! .. These are either collected from IFS and variable names mapped,
! .. or read in as per offline model
REAL(KIND=JPRB) :: ZDLONARR(KDIM%KLON)
REAL(KIND=JPRB) :: ZDENSAIR(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB) :: ZDLATARR(KDIM%KLON)
REAL(KIND=JPRB) :: ZSMG(KDIM%KLON)           ! total air mass in gridbox (kg)
REAL(KIND=JPRB) :: ZLAND_FRAC(KDIM%KLON)     ! land fraction in, 0=sea, 1=land
REAL(KIND=JPRB) :: ZSURTPG(KDIM%KLON)        ! Surface type [0/1=at sea/land-surf,2/3=above-sea/land]
REAL(KIND=JPRB) :: ZNOTG(KDIM%KLON)         ! Surface roughness length (m)
REAL(KIND=JPRB) :: ZSEAICE(KDIM%KLON)        ! Fraction of gridbox area containing seaice
REAL(KIND=JPRB) :: ZAIRDG(KDIM%KLON)         ! Number density of air (per cc)
REAL(KIND=JPRB) :: ZRHOAG(KDIM%KLON)         ! Air density (kg/m3)
REAL(KIND=JPRB) :: ZUSTR(KDIM%KLON)          ! Surface friction velocity (m/s)
REAL(KIND=JPRB) :: ZUS10M(KDIM%KLON)         ! Scalar wind at 10m (ms-1)
REAL(KIND=JPRB) :: ZEMANSO2G(KDIM%KLON,6)    ! Anthrop. SO2 ems rates, low sources (kg(SO2)/gridbox/s)
REAL(KIND=JPRB) :: ZEMBIOMSO2G(KDIM%KLON)    ! Biomass burning SO2 ems rates (kg(SO2)/gridbox/s)
REAL(KIND=JPRB) :: ZEMVOLCONSO2G(KDIM%KLON)  ! Natural SO2 ems rates (from cont. volc. src) (kg(SO2)/gridbox/s)
REAL(KIND=JPRB) :: ZEMVOLEXPSO2G(KDIM%KLON)  ! Natural SO2 ems rates (from expl. volc. src) (kg(SO2)/gridbox/s)
REAL(KIND=JPRB) :: ZEMCARG(KDIM%KLON,4)      ! Carbon emissions (kg(C)/gridbox/s)  1: BC - Bio fuel   2: BC - Fossil fuel
REAL(KIND=JPRB) :: ZTAERI(KDIM%KLON,KDIM%KLEV,KTRAC)
                                             !                                     3: OC - Bio fuel      4: OC - Fossil fuel
REAL(KIND=JPRB) :: ZEMCARBMG(KDIM%KLON,2)    ! Carbon emissions from biomass burning (kg(C)/gridbox/s)   1: BC - biomass burning
                                             !                                                           2: OC - biomass burning
REAL(KIND=JPRB) :: ZSNOWICE(KDIM%KLON)       ! Fraction of gridbox *not* covered by snow/ice
REAL(KIND=JPRB) :: ZLAI(KDIM%KLON)           ! Leaf area index
REAL(KIND=JPRB) :: ZMODE2_NUMBER(KDIM%KLON) ! number concentration for mode 2-accumulation (no/gridbox/s) (AeroCom dust ems)
REAL(KIND=JPRB) :: ZMODE3_NUMBER(KDIM%KLON) ! number concentration for mode 3-coarse (no/gridbox/s) (AeroCom dust ems)
REAL(KIND=JPRB) :: ZMODE2_RADIUS(KDIM%KLON) ! geomtric mean radius for mode 2-accumulation (microns) (AeroCom dust ems)
REAL(KIND=JPRB) :: ZMODE3_RADIUS(KDIM%KLON) ! geomtric mean radius for mode 3-coarse (microns) (AeroCom dust ems)
REAL(KIND=JPRB) :: ZDELP(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB) :: ZRHCL(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB) :: ZQSAT(KDIM%KLON,KDIM%KLEV)

REAL(KIND=JPRB) :: ZRMOIS               ! float month
REAL(KIND=JPRB) :: ZRJOUR               ! float day of month

INTEGER(KIND=JPIM) :: ISO2EMS          ! SO2 emissions switch
INTEGER(KIND=JPIM) :: ISSEMS           ! SS emissions switch (1=older Gong et al 2003, 2= newer Grythe et al 2014)
INTEGER(KIND=JPIM) :: IDUSTEMS         ! Dust emissions switch NOW SET IN GEMS_SETUP SCRIPT
INTEGER(KIND=JPIM) :: IPRIMSU_ON        ! Include primary sulphate emissions?
INTEGER(KIND=JPIM) :: IPRIMSOA_ON       ! Include primary SOA production into OC?
INTEGER(KIND=JPIM) :: IPRIMCAR_ON       ! Include primary BC and OC emissions?
INTEGER(KIND=JPIM) :: IPRIMSS_ON        ! Include sea-spray emissions?
INTEGER(KIND=JPIM) :: IPRIMDU_ON        ! Include dust emissions?
INTEGER(KIND=JPIM) :: IPRODNI_ON        ! Include nitrate production?
INTEGER(KIND=JPIM) :: IVERBOSE          ! Level of verbosity

! .. Below are local variables which are outputs from UKCA_MODE_EMS
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMSU(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMBC(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMOC(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMSOA(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMBCBB(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMOCBB(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMSS(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRIMDU(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRODNI(KDIM%KLON,KDIM%KLEV,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_MAS_PRODAM(KDIM%KLON,KDIM%KLEV,NMODES)

REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMSU(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMSOA(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMCAR(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMCARBB(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMSS(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRIMDU(KDIM%KLON,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRODNI(KDIM%KLON,KDIM%KLEV,NMODES)
REAL(KIND=JPRB) :: ZBUD_AER_NUM_PRODAM(KDIM%KLON,KDIM%KLEV,NMODES)

REAL(KIND=JPRB) :: ZND(KDIM%KLON,KDIM%KLEV,NMODES)


! .. Below are local variables used in code.
INTEGER(KIND=JPIM) :: JCP, JMODE, ITRA
INTEGER(KIND=JPIM) :: ITEST_ICP
REAL(KIND=JPRB) :: ZSURFG(KDIM%KLON)
REAL(KIND=JPRB), PARAMETER :: ZSO2MSS=64.058E-03_JPRB
REAL(KIND=JPRB) :: ZLOCALTIM   , ZDIURN(KDIM%KLON), ZDEGRAD

!! .. extra local vars needed for DMS
INTEGER(KIND=JPIM) :: IGLGLO
INTEGER(KIND=JPIM) :: JL, JAER, JK
!-- QnD oceanic DMS
REAL(KIND=JPRB)    :: ZLAT
REAL(KIND=JPRB)    :: ZCOS0, ZSIN0, Z_S_SO2
REAL(KIND=JPRB)    :: ZDMSMIN
REAL(KIND=JPRB)    :: ZDMSO(KDIM%KLON), ZGEMU(KDIM%KLON)

!-- Injection height for biomass burning emissions
INTEGER(KIND=JPIM) :: ILINJ1, ILINJ2, IX(1)
REAL(KIND=JPRB)    :: ZTOTDELP,ZTMP


!-- volcano-related variables
INTEGER(KIND=JPIM) :: IBASPL, ITOPPL, INBAER
INTEGER(KIND=JPIM) :: JVOLC, JVOLE
INTEGER(KIND=JPIM) :: IVDAYS, IVSECNDS, IVDAYE, IVSECNDE
INTEGER(KIND=JPIM) :: IYY, IMM, IDD, IMDATE
INTEGER(KIND=JPIM) :: IY0, IM0, ID0, INC, IMON(12)

REAL(KIND=JPRB)    :: ZGLAT(KDIM%KLON), ZGLON(KDIM%KLON)
REAL(KIND=JPRB)    :: ZGDLAT(KDIM%KLON), ZGDLON(KDIM%KLON)
REAL(KIND=JPRB)    :: ZVOLCASH(KDIM%KLON), ZVOLEASH(KDIM%KLON)
REAL(KIND=JPRB)    :: ZVOLCSO2(KDIM%KLON), ZVOLESO2(KDIM%KLON)
REAL(KIND=JPRB)    :: ZAREA, ZDEEPLUME, ZDLAT, ZDLON, ZLATSQ, ZLONSQ, ZEQUATOR
REAL(KIND=JPRB)    :: ZVOLUME, ZRA, ZGELAA, ZGELAB, ZGELOA, ZGELOB
REAL(KIND=JPRB)    :: ZALT(KDIM%KLON,0:KDIM%KLEV)
REAL(KIND=JPRB)    :: ZDZ(KDIM%KLON,KDIM%KLEV)
REAL(KIND=JPRB)    :: Z1GP
REAL(KIND=JPRB)    :: ZVDEP, ZHOURLT, ZSCALE, ZDVMAX



#include "fcttim.func.h"
#include "aer_dmso.intfb.h"
#include "aer_volce.intfb.h"
#include "updcal.intfb.h"
#include "chem_inext.intfb.h"
#include "ukca_mode_ems.intfb.h"
#include "satur.intfb.h"


REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('UKCA_MODE_EMS_IFS',0,ZHOOK_HANDLE)

ASSOCIATE(YDCOMPO=>YDMODEL%YRML_CHEM%YRCOMPO, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL, &
 & YDEPHLI=>YDMODEL%YRML_PHY_SLIN%YREPHLI, &
 & YDEAERSRC=>YDMODEL%YRML_PHY_AER%YREAERSRC, &
 & YDEAERATM=>YDMODEL%YRML_PHY_RAD%YREAERATM, &
 & YDEAERVOL=>YDMODEL%YRML_PHY_AER%YREAERVOL)
ASSOCIATE(NACTAERO=>YGFL%NACTAERO, NAERO=>YGFL%NAERO,&
 & NDMSO=>YDEAERSRC%NDMSO, NPIST=>YDEAERSRC%NPIST, RDMSMIN=>YDEAERSRC%RDMSMIN,&
 & RCODECA=>YDEAERSRC%RCODECA, RSIDECA=>YDEAERSRC%RSIDECA, &
 & RCOVSRA=>YDEAERSRC%RCOVSRA, RSIVSRA=>YDEAERSRC%RSIVSRA, &
 & RHGMT=>YDRIP%RHGMT, &
 & LVOLC_ALTI=>YDCOMPO%LVOLC_ALTI, &
 & NDGLG=>YDGEOMETRY%YRDIM%NDGLG, &
 & LAERNITRATE => YDCOMPO%LAERNITRATE, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, &
 & LAERVOL=>YDEAERATM%LAERVOL, &
 & NAERVOLC=>YDEAERVOL%NAERVOLC, NAERVOLE=>YDEAERVOL%NAERVOLE, &
 & NVOLDATS=>YDEAERVOL%NVOLDATS, NVOLDATE=>YDEAERVOL%NVOLDATE, &
 & NVOLERUP=>YDEAERVOL%NVOLERUP, &
 & RAERVOLC=>YDEAERVOL%RAERVOLC, RAERVOLE=>YDEAERVOL%RAERVOLE, &
 & NSTASS=>YDRIP%NSTASS, &
 & NGLOBALAT=>YDGEOMETRY%YRMP%NGLOBALAT,&
 & YDCSGLEG=>YDGEOMETRY%YRCSGLEG, &
 & NLOENG=>YDGEOMETRY%YRGEM%NLOENG, &
 & RSTATI=>YDRIP%RSTATI, &
 & LCHEM_DIA=>YDCOMPO%LCHEM_DIA, &
 & NCHEM=>YGFL%NCHEM,&
 & LCOMPO_DCBB=>YDCOMPO%LCOMPO_DCBB, LINJ=>YDCOMPO%LINJ, &
 & LOCNDMS=>YDEAERSRC%LOCNDMS, LFIRE=>YDCOMPO%LFIRE)


IY0=NCCAA(NINDAT)
IM0=NMM(NINDAT)
ID0=NDD(NINDAT)
INC=(NSSSSS + NINT(RSTATI))/NINT(RDAY)
CALL UPDCAL (ID0, IM0, IY0, INC,  IDD, IMM, IYY, IMON, -1)
IMDATE=IYY*10000+IMM*100+IDD
ZRA=6371229._JPRB*RPLRADI


! Component emission switches
IPRIMSU_ON  = 1
IPRIMCAR_ON = 1
IPRIMSOA_ON = 1
IPRIMSS_ON  = 1
IPRIMDU_ON  = 1
IPRODNI_ON  = 0
IF(LAERNITRATE) THEN
  IPRODNI_ON  = 1
ENDIF
IVERBOSE    = 1

IF (NSTEP == YDRIP%NSTART) THEN
  WRITE(UNIT=NULOUT,FMT='('' GLOMAP EMISSIONS IPRIMSU_ON = '',I1 &
     & ,'' IPRIMCAR_ON = '',I1 &
     & ,'' IPRIMSOA_ON = '',I1 &
     & ,'' IPRIMSS_ON = '',I1 &
     & ,'' IPRIMDU_ON = '',I1 &
     & ,'' IPRODNI_ON = '',I1 &
     & ,'' IVERBOSE = '',I1 &
     & )')&
     & IPRIMSU_ON,IPRIMCAR_ON,IPRIMSOA_ON,IPRIMSS_ON,IPRIMDU_ON,IPRODNI_ON,IVERBOSE
ENDIF





DO JL=KDIM%KIDIA,KDIM%KFDIA
 DO JK=1,KDIM%KLEV
   ZDELP(JL,JK)= PRS1(JL,JK) - PRS1(JL,JK-1)
   ZDENSAIR(JL,JK)= PAPRSF(JL,JK)/(PT(JL,JK)*RD)
   ZDZ(JL,JK)= ZDELP(JL,JK) / (ZDENSAIR(JL,JK)*RG)
 ENDDO
ENDDO
DO JL=KDIM%KIDIA,KDIM%KFDIA
  ZALT(JL,KDIM%KLEV)=PAPHI(JL,KDIM%KLEV)/RG
ENDDO
DO JK=KDIM%KLEV-1,0,-1
  DO JL=KDIM%KIDIA,KDIM%KFDIA
    ZALT(JL,JK)=ZALT(JL,JK+1)+ZDZ(JL,JK+1)
  ENDDO
ENDDO

ZTAERI(:,:,:)=PTENC(:,:,:)


! Maps IFS variable names onto what is used in UKCA_MODE_EMS, and adjust units if required
ZSURTPG(KDIM%KIDIA:KDIM%KFDIA)    = PLSM(KDIM%KIDIA:KDIM%KFDIA) ! land-sea mask (from 0 to 1: 0=sea, 1=land)
ZLAND_FRAC(KDIM%KIDIA:KDIM%KFDIA) = PLSM(KDIM%KIDIA:KDIM%KFDIA)
ZDLONARR(KDIM%KIDIA:KDIM%KFDIA)   = PGELAM(KDIM%KIDIA:KDIM%KFDIA) ! in radians
ZDLATARR(KDIM%KIDIA:KDIM%KFDIA)   = PGELAT(KDIM%KIDIA:KDIM%KFDIA) ! in radians
ZSURFG(KDIM%KIDIA:KDIM%KFDIA)     = PAREA(KDIM%KIDIA:KDIM%KFDIA) ! in m^2
ZRHOAG(KDIM%KIDIA:KDIM%KFDIA)     = ZDENSAIR(KDIM%KIDIA:KDIM%KFDIA,KDIM%KLEV) ! in kg/m3
ZSMG(KDIM%KIDIA:KDIM%KFDIA)       = ZSURFG(KDIM%KIDIA:KDIM%KFDIA)*ZDELP(KDIM%KIDIA:KDIM%KFDIA,KDIM%KLEV)/GG ! in kg/box
ZSEAICE(KDIM%KIDIA:KDIM%KFDIA)    = PCI(KDIM%KIDIA:KDIM%KFDIA)
ZAIRDG(KDIM%KIDIA:KDIM%KFDIA) = PAPRSF(KDIM%KIDIA:KDIM%KFDIA,KDIM%KLEV)/(PT(KDIM%KIDIA:KDIM%KFDIA,KDIM%KLEV)*ZBOLTZ*1.0E6) ! per cc


CALL SATUR (YRTHF, YRCST, KDIM%KIDIA , KDIM%KFDIA , KDIM%KLON  , KDIM%KTDIA , KDIM%KLEV, LPHYLIN, &
  & PAPRSF, PT    , ZQSAT , 2)

ZRHCL(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV)=PQ(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV) &
                                        & /(MAX(1.E-30_JPRB, ZQSAT(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV)))

DO JL=KDIM%KIDIA,KDIM%KFDIA
  ZUSTR(JL)      = PAERUST(JL)
  ZNOTG(JL)     = PAZ0M(JL)
ENDDO
ZUS10M(KDIM%KIDIA:KDIM%KFDIA)     = PWIND(KDIM%KIDIA:KDIM%KFDIA)
ISSEMS=2

! Init emission arrays
ZBUD_AER_MAS_PRIMSU (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMSOA (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMBC (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMOC (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMBCBB (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMOCBB (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMSS (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRIMDU (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB ! only emitted into two modes - indexes 6 and 7 - ins.acc/ins.coa
ZBUD_AER_MAS_PRODNI (KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,1:NMODES) = 0.0_JPRB
ZBUD_AER_MAS_PRODAM (KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,1:NMODES) = 0.0_JPRB


ZBUD_AER_NUM_PRIMSU (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRIMSOA (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRIMCAR(KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRIMCARBB(KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRIMSS (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRIMDU (KDIM%KIDIA:KDIM%KFDIA,1:NMODES) = 0.0_JPRB ! only emitted into two modes - indexes 6 and 7 - ins.acc/ins.coa
ZBUD_AER_NUM_PRODNI (KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,1:NMODES) = 0.0_JPRB
ZBUD_AER_NUM_PRODAM (KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,1:NMODES) = 0.0_JPRB


!---------------
!*       0.2   A LENGTH OF DAY INDEX
!---------------------

Z_S_SO2  = 0.5_JPRB
ZDMSMIN  = 5.E-11_JPRB
ZDEGRAD= 180._JPRB/RPI

DO JL=KDIM%KIDIA,KDIM%KFDIA

  IGLGLO=NGLOBALAT(KDIM%KSTGLO+JL-1)
  ZGEMU(JL) = YDCSGLEG%RMU(IGLGLO)                           ! sine of latitude
  ZGLON(JL)=PGELAM(JL)*ZDEGRAD
  ZCOS0 = 1._JPRB
  ZSIN0 = 0._JPRB
  PLDAY(JL) = MAX( RSIDECA*ZGEMU(JL)&
   & -RCODECA*RCOVSRA*SQRT(1.0_JPRB-ZGEMU(JL)**2)* ZCOS0 &
   & +RCODECA*RSIVSRA*SQRT(1.0_JPRB-ZGEMU(JL)**2)* ZSIN0 &
   & ,0.0_JPRB)
  PDMSO(JL) = 0._JPRB
  PLISS(JL) = 0._JPRB
  PSO2(JL)  = 0._JPRB
  PTDMS(JL) = 0._JPRB
  PODMS(JL) = 0._JPRB
  ZLOCALTIM =RHGMT + ZGLON(JL)/360._JPRB*RDAY
  ZDIURN(JL)=COS( ((ZLOCALTIM-54000._JPRB)/RDAY) * 2._JPRB*RPI)+1._JPRB
ENDDO

! call DMS routine AER_DMSO to return PDMSO -- that's the flux we need!
IF (NDMSO /= 0 .AND. LOCNDMS) THEN
   CALL AER_DMSO (YDEAERSRC, KDIM%KIDIA, KDIM%KFDIA, KDIM%KLON, PCI, PDMS, PLDAY, PLSM,  PTS, PWIND, ZDMSO, PLISS, PTDMS, PODMS)
   IF (NDMSO == 1) THEN
      DO JL=KDIM%KIDIA,KDIM%KFDIA
         PDMSO(JL)=ZDMSO(JL)
      ENDDO
   ELSEIF (NDMSO == 2) THEN
      DO JL=KDIM%KIDIA,KDIM%KFDIA
         PDMSO(JL)=PODMS(JL)
      ENDDO
   ENDIF
ENDIF

! NOTE: This flag is used slightly wrongly here: it switches the diurnal cycle
! for more than just biomass-burning, but there isn't a proper flag for what
! the old LAERODIU logic was doing... -ZK
IF (.NOT.LCOMPO_DCBB) THEN
  ZDIURN(KDIM%KIDIA:KDIM%KFDIA)=1.0_JPRB
ENDIF



!---------------
!! NEED TO ADD IN DIURNAL CYCLE FOR PSO2L AS DONE IN GEMS !!!
!! follow approach as in aer_src
ISO2EMS=3
ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,1)=PSO2L(KDIM%KIDIA:KDIM%KFDIA)*ZDIURN(KDIM%KIDIA:KDIM%KFDIA)
ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,2)=PSO2H(KDIM%KIDIA:KDIM%KFDIA)
ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,3:6)=0.0
IF(LFIRE) THEN
   ZEMBIOMSO2G(KDIM%KIDIA:KDIM%KFDIA)=PSOGF(KDIM%KIDIA:KDIM%KFDIA)*ZSO2MSS*ZDIURN(KDIM%KIDIA:KDIM%KFDIA) !Sulphate GFED
   ZEMCARBMG(KDIM%KIDIA:KDIM%KFDIA,1)=PBCGF(KDIM%KIDIA:KDIM%KFDIA)
   ZEMCARBMG(KDIM%KIDIA:KDIM%KFDIA,2)=POMGF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon as required
ELSE
   ZEMBIOMSO2G(KDIM%KIDIA:KDIM%KFDIA)=0.0_JPRB
   ZEMCARBMG(KDIM%KIDIA:KDIM%KFDIA,1)=0.0_JPRB
   ZEMCARBMG(KDIM%KIDIA:KDIM%KFDIA,2)=0.0_JPRB
ENDIF
ZEMVOLCONSO2G(KDIM%KIDIA:KDIM%KFDIA)=0.0
ZEMVOLEXPSO2G(KDIM%KIDIA:KDIM%KFDIA)=0.0
ZEMCARG(KDIM%KIDIA:KDIM%KFDIA,1)=PBCBF(KDIM%KIDIA:KDIM%KFDIA)
ZEMCARG(KDIM%KIDIA:KDIM%KFDIA,2)=PBCFF(KDIM%KIDIA:KDIM%KFDIA)
ZEMCARG(KDIM%KIDIA:KDIM%KFDIA,3)=POMBF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon as required
ZEMCARG(KDIM%KIDIA:KDIM%KFDIA,4)=POMFF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon as required
! 1: Black carbon   - Bio fuel
! 2: Black carbon   - Fossil fuel
! 3: Organic carbon - Bio fuel
! 4: Organic carbon - Fossil fuel



! Dust emission scheme variables
IDUSTEMS=1
! AG01 / Pringle inputs
ZRMOIS = 12.0_JPRB                         ! Float month
ZRJOUR = 1.0_JPRB                         ! Float day of month
!ZLAI(KDIM%KIDIA:KDIM%KFDIA)     = ZLAIL(KDIM%KIDIA:KDIM%KFDIA) + ZLAIH(KDIM%KIDIA:KDIM%KFDIA)
ZLAI(KDIM%KIDIA:KDIM%KFDIA)     = PLAIL(KDIM%KIDIA:KDIM%KFDIA)
ZSNOWICE(KDIM%KIDIA:KDIM%KFDIA) = 1.0_JPRB - (PFRTI(KDIM%KIDIA:KDIM%KFDIA,2) + PFRTI(KDIM%KIDIA:KDIM%KFDIA,5) &
                                             & + PFRTI(KDIM%KIDIA:KDIM%KFDIA,7)) ! Fraction of gridbox *not* covered by snow/ice
DO JL=KDIM%KIDIA,KDIM%KFDIA
   !write(nulout,  *) 'UKCA_MODE_EMS_IFS, ZSNOWICE, PFRTI 2, 5, 7', ZSNOWICE(JL), PFRTI(JL,2), PFRTI(JL,5),PFRTI(,7)
ENDDO

! Aerocom inputs
ZMODE2_NUMBER(KDIM%KIDIA:KDIM%KFDIA)=0.0_JPRB
ZMODE3_NUMBER(KDIM%KIDIA:KDIM%KFDIA)=0.0_JPRB
ZMODE2_RADIUS(KDIM%KIDIA:KDIM%KFDIA)=0.0_JPRB
ZMODE3_RADIUS(KDIM%KIDIA:KDIM%KFDIA)=0.0_JPRB

! All inputs available and converted, call UKCA_MODE_EMS

CALL UKCA_MODE_EMS(YDGEOMETRY,YDMODEL,KDIM%KIDIA,KDIM%KFDIA,KDIM%KLON,KDIM%KLEV,KDIM%KTILES,&
     &    KDIM%KSTGLO,ZDLONARR,ZDLATARR,ZSMG,ZLAND_FRAC,&
     &    ZSURTPG,ZNOTG,ZSEAICE,ZAIRDG,ZRHOAG,ZUSTR,ZUS10M, PSST, ZSNOWICE, &
     &    ZEMANSO2G,ZEMBIOMSO2G,ZEMVOLCONSO2G,ZEMVOLEXPSO2G,&
     &    ZEMCARG,ZEMCARBMG,PSOA, &
     &    ISO2EMS, ISSEMS, &
     &    IPRIMSU_ON,IPRIMSOA_ON, IPRIMCAR_ON,IPRIMSS_ON,IPRIMDU_ON,IPRODNI_ON, IVERBOSE,&
     &    KSW, PALB, PALBD, PWIND, PAERGUST,PHSDFOR, &
     &    PWS1, PSNS, PFRTI, PAERLTS, PAERSCC,PZ0M, PDSF,&
     &    PSO4, PNH4, PNH3, PHNO3, PNO3, PT, ZDELP,ZDENSAIR,ZRHCL,PTSPHY,&
     &    ZBUD_AER_MAS_PRIMSU,ZBUD_AER_MAS_PRIMSOA, ZBUD_AER_MAS_PRIMBC,&
     &    ZBUD_AER_MAS_PRIMOC, &
     &    ZBUD_AER_MAS_PRIMBCBB, ZBUD_AER_MAS_PRIMOCBB, ZBUD_AER_MAS_PRIMSS,ZBUD_AER_MAS_PRIMDU,&
     &    ZBUD_AER_MAS_PRODNI, ZBUD_AER_MAS_PRODAM, &
     &    ZBUD_AER_NUM_PRIMSU,ZBUD_AER_NUM_PRIMSOA, ZBUD_AER_NUM_PRIMCAR,ZBUD_AER_NUM_PRIMCARBB, &
     &    ZBUD_AER_NUM_PRIMSS,ZBUD_AER_NUM_PRIMDU,&
     &    ZBUD_AER_NUM_PRODNI,ZBUD_AER_NUM_PRODAM, PTHNO3, PTNH3)


DO JAER=1,NACTAERO
  DO JL=KDIM%KIDIA,KDIM%KFDIA
    PCFLX(JL,KAERO(JAER))=0._JPRB
  ENDDO
ENDDO
PAERSRC(KDIM%KIDIA:KDIM%KFDIA,1:NACTAERO) = 0.0_JPRB
PAERDDP(KDIM%KIDIA:KDIM%KFDIA,1:NACTAERO) = 0.0_JPRB

! diagnostics
PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,:) = 0.0_JPRB
PGLO_NUM_EMS(KDIM%KIDIA:KDIM%KFDIA,:) = 0.0_JPRB
ITEST_ICP = 1


ITRA=0
DO JMODE=1,NMODES
 IF(MODE(JMODE)) THEN

  ITRA=ITRA+1
! ... update aerosol tracers for mixing
! .. First set PCFLX for total number flux from all primary emissions
  ! print*,'JMODE,ITRA=',JMODE,ITRA
  !WRITE(*,*) 'JMODE,ITRA=',JMODE,ITRA
!  PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=- &
  PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_NUM_PRIMSU(KDIM%KIDIA:KDIM%KFDIA,JMODE)- &
&                          ZBUD_AER_NUM_PRIMCAR(KDIM%KIDIA:KDIM%KFDIA,JMODE)-&
&                          ZBUD_AER_NUM_PRIMSS (KDIM%KIDIA:KDIM%KFDIA,JMODE)-&
&                          ZBUD_AER_NUM_PRIMSOA (KDIM%KIDIA:KDIM%KFDIA,JMODE)-&
&                          ZBUD_AER_NUM_PRIMDU (KDIM%KIDIA:KDIM%KFDIA,JMODE)

  ZND(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,JMODE) = PCEN(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA)) &
                                               & *ZDENSAIR(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV)

  IF (LAERNITRATE) THEN
  !  WHERE(ZBUD_AER_NUM_PRODNI(:,:,IMODE) > 0._JPRB)
      PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))=PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA)) &
                                                          & +ZBUD_AER_NUM_PRODNI(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,JMODE)
  !  END WHERE
  !  WHERE(ZBUD_AER_NUM_PRODAM(:,:,IMODE) > 0._JPRB)
      PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))=PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA)) &
                                                          & +ZBUD_AER_NUM_PRODAM(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,JMODE)
  !  END WHERE
     !write (*,*) "PRODNI",ITRA,KAERO(ITRA),PTENC(KDIM%KIDIA:10,KDIM%KLEV,KAERO(ITRA)), &
     ! & BUD_AER_NUM_PRODNI(KDIM%KIDIA:10,KDIM%KLEV,KAERO(ITRA)),BUD_AER_NUM_PRODAM(KDIM%KIDIA:10,KDIM%KLEV,KAERO(ITRA))
  ENDIF


  !DO JL=KDIM%KIDIA,KDIM%KFDIA
  ! write (*,*)"EMSIFS",IMODE,JL,ITRA,BUD_AER_NUM_PRIMSU(JL,IMODE),BUD_AER_NUM_PRIMCAR(JL,IMODE), &
  !  & BUD_AER_NUM_PRIMSS(JL,IMODE),BUD_AER_NUM_PRIMDU(JL,IMODE),BUD_AER_NUM_PRIMSOA(JL,IMODE)
  !ENDDO
  ! Height of injection for biomass burning emissions : update tendency
  IF (LINJ) THEN
    DO JL=KDIM%KIDIA,KDIM%KFDIA
    !write (*,*) "EMSIFS",IMODE,BUD_AER_NUM_PRIMCARBB(JL,IMODE),BUD_AER_MAS_PRIMBCBB(JL,IMODE)
    IF (PINJF(JL) > 200._JPRB .AND. PBLH(JL) > 1500._JPRB) THEN
      IX=MINLOC( ABS( (PAPHI(JL,1:KDIM%KLEV)-PAPHI(JL,KDIM%KLEV))/RG - PINJF(JL)))
      ILINJ1=IX(1)
      ILINJ2=ILINJ1
      ! calculate total deltap over injected levels
      ZTOTDELP=0.0_JPRB
      DO JK = ILINJ1, ILINJ2
         ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
      ENDDO
      DO JK = ILINJ1, ILINJ2
        ZTMP=PTENC(JL,JK,KAERO(ITRA))
        PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) + &
        &  ZBUD_AER_NUM_PRIMCARBB(JL,JMODE) * RG * &
        & ZDIURN(JL) / ZTOTDELP
      ENDDO
    ELSE
      ZTOTDELP=0.0_JPRB
      DO JK = KDIM%KLEV-3, KDIM%KLEV-2
        ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
      ENDDO
      DO JK = KDIM%KLEV-3, KDIM%KLEV-2
        PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) + &
            &  ZBUD_AER_NUM_PRIMCARBB(JL,JMODE)*RG * &
            & ZDIURN(JL) / ZTOTDELP
      ENDDO
    ENDIF
    ENDDO
    ELSE  ! LINJ
      PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA)) &
       & - ZBUD_AER_NUM_PRIMCARBB(KDIM%KIDIA:KDIM%KFDIA,JMODE)
  ENDIF

  DO JCP=1,NCP
   IF(COMPONENT(JMODE,JCP)) THEN

    ITRA=ITRA+1
!    WRITE(*,*) 'JCP,ITRA=',JCP,ITRA

! .. First set PCFLX for primary sulphate emissions
    IF(JCP == CP_SU) THEN
       PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_MAS_PRIMSU(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,ITEST_ICP) = ZBUD_AER_MAS_PRIMSU(KDIM%KIDIA:KDIM%KFDIA,JMODE)
       ITEST_ICP = ITEST_ICP + 1
    ENDIF

     IF (LAERNITRATE) THEN
! .. Then set PCFLX for nitrate production
      IF(JCP == CP_NI) THEN
       ! WHERE (ZND(:,:,IMODE) > 0._JPRB)
          PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))=PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))+ &
                & ZBUD_AER_MAS_PRODNI(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,JMODE)*MM(JCP)/MM_DA
       !  END WHERE
       ITEST_ICP = ITEST_ICP + 1
      ENDIF
! .. Then set PCFLX for ammonium production
      IF(JCP == CP_AM) THEN
       ! WHERE (ZND(:,:,IMODE) > 0._JPRB)
          PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))=PTENC(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,KAERO(ITRA))+ &
                & ZBUD_AER_MAS_PRODAM(KDIM%KIDIA:KDIM%KFDIA,1:KDIM%KLEV,JMODE)*MM(JCP)/MM_DA
        ! END WHERE
       ITEST_ICP = ITEST_ICP + 1
      ENDIF
    ENDIF
! .. Then set PCFLX for primary BC emissions
    IF(JCP == CP_BC) THEN
       PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_MAS_PRIMBC(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,ITEST_ICP) = ZBUD_AER_MAS_PRIMBC(KDIM%KIDIA:KDIM%KFDIA,JMODE) &
        & + ZBUD_AER_MAS_PRIMBCBB(KDIM%KIDIA:KDIM%KFDIA,JMODE)
       ! Height of injection for biomass burning emissions : update tendency
      IF (LINJ) THEN
        DO JL=KDIM%KIDIA,KDIM%KFDIA
        IF (PINJF(JL) > 200._JPRB .AND. PBLH(JL) > 1500._JPRB) THEN
          IX=MINLOC( ABS( (PAPHI(JL,1:KDIM%KLEV)-PAPHI(JL,KDIM%KLEV))/RG - PINJF(JL)))
          ILINJ1=IX(1)
          ILINJ2=ILINJ1
          ! calculate total deltap over injected levels
          ZTOTDELP=0.0_JPRB
          DO JK = ILINJ1, ILINJ2
             ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
          ENDDO
          DO JK = ILINJ1, ILINJ2
            ZTMP=PTENC(JL,JK,KAERO(ITRA))
            PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) + &
            &  ZBUD_AER_MAS_PRIMBCBB(JL,JMODE)*MM(JCP)/MM_DA * RG * &
            & ZDIURN(JL) / ZTOTDELP
          ENDDO
        ELSE
          ZTOTDELP=0.0_JPRB
          DO JK = KDIM%KLEV-3, KDIM%KLEV-2
               ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
          ENDDO
          DO JK = KDIM%KLEV-3, KDIM%KLEV-2
            PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) + &
            &  ZBUD_AER_MAS_PRIMBCBB(JL,JMODE)*MM(JCP)/MM_DA * RG * &
            & ZDIURN(JL) / ZTOTDELP
          ENDDO
         ENDIF
         ENDDO
       ELSE  ! LINJ
         PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA)) &
          & - ZBUD_AER_MAS_PRIMBCBB(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       ENDIF
       ITEST_ICP = ITEST_ICP + 1
    ENDIF

! .. Then set PCFLX for primary OM emissions
    IF(JCP == CP_OC) THEN
       PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_MAS_PRIMOC(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA - &
                                                & ZBUD_AER_MAS_PRIMSOA(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,ITEST_ICP) = ZBUD_AER_MAS_PRIMOC(KDIM%KIDIA:KDIM%KFDIA,JMODE) &
        & + ZBUD_AER_MAS_PRIMOCBB(KDIM%KIDIA:KDIM%KFDIA,JMODE) + ZBUD_AER_MAS_PRIMSOA(KDIM%KIDIA:KDIM%KFDIA,JMODE)
      ! Height of injection for biomass burning emissions : update tendency
      IF (LINJ) THEN
        DO JL=KDIM%KIDIA,KDIM%KFDIA
        IF (PINJF(JL) > 200._JPRB .AND. PBLH(JL) > 1500._JPRB) THEN
          IX=MINLOC( ABS( (PAPHI(JL,1:KDIM%KLEV)-PAPHI(JL,KDIM%KLEV))/RG - PINJF(JL)))
          ILINJ1=IX(1)
          ILINJ2=ILINJ1
          ! calculate total deltap over injected levels
          ZTOTDELP=0.0_JPRB
          DO JK = ILINJ1, ILINJ2
             ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
          ENDDO
          DO JK = ILINJ1, ILINJ2
            PTENC(JL,JK,KAERO(ITRA)) =PTENC(JL,JK,KAERO(ITRA)) + &
            &  ZBUD_AER_MAS_PRIMOCBB(JL,JMODE)*MM(JCP)/MM_DA *RG * &
            & ZDIURN(JL) / ZTOTDELP
          ENDDO
        ELSE
          ZTOTDELP=0.0_JPRB
          DO JK = KDIM%KLEV-3, KDIM%KLEV-2
               ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
          ENDDO
          DO JK = KDIM%KLEV-3, KDIM%KLEV-2
            PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) + &
            &  ZBUD_AER_MAS_PRIMOCBB(JL,JMODE)*MM(JCP)/MM_DA *RG * &
            & ZDIURN(JL) / ZTOTDELP
          ENDDO
        ENDIF
        ENDDO
       ELSE  ! LINJ
         PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA)) &
          & - ZBUD_AER_MAS_PRIMOCBB(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       ENDIF
       ITEST_ICP = ITEST_ICP + 1
    ENDIF

! .. Then set PCFLX for primary sea-spray emissions
    IF(JCP == CP_CL) THEN
       PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_MAS_PRIMSS(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,ITEST_ICP) = ZBUD_AER_MAS_PRIMSS(KDIM%KIDIA:KDIM%KFDIA,JMODE)
       ITEST_ICP = ITEST_ICP + 1
    ENDIF


! .. Then set PCFLX for primary dust emissions
    ! Only present in indexes ITRA == 24, 26
    IF(JCP == CP_DU) THEN
       PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=-ZBUD_AER_MAS_PRIMDU(KDIM%KIDIA:KDIM%KFDIA,JMODE)*MM(JCP)/MM_DA
       PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,ITEST_ICP) = ZBUD_AER_MAS_PRIMDU(KDIM%KIDIA:KDIM%KFDIA,JMODE)
       ITEST_ICP = ITEST_ICP + 1
    ENDIF

   ENDIF ! if component
  ENDDO ! loop over components
 ENDIF ! if MODE
ENDDO ! loop over modes


! Now update gas-phase tracer emissions
! DMS
ITRA=ITRA+1

! SO2
ITRA=ITRA+1

PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))= &
     ! add on DMS emissions flux
   &  - PDMSO(KDIM%KIDIA:KDIM%KFDIA) &
   !-----------
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,1)  &
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,2)  &
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,3)  &
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,4)  &
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,5)  &
   &  - ZEMANSO2G(KDIM%KIDIA:KDIM%KFDIA,6)  &
   &  - ZEMBIOMSO2G(KDIM%KIDIA:KDIM%KFDIA)  &
   &  - ZEMVOLCONSO2G(KDIM%KIDIA:KDIM%KFDIA)  &
   &  - ZEMVOLEXPSO2G(KDIM%KIDIA:KDIM%KFDIA)
! lift SO2 volcanic emissions
DO JL=KDIM%KIDIA,KDIM%KFDIA
  IF (LVOLC_ALTI) THEN
    IF (PVOLCALTI(JL) > 0._JPRB) THEN
         IX=MINLOC( ABS( (PAPHI(JL,1:KDIM%KLEV)-PAPHI(JL,KDIM%KLEV))/RG - PVOLCALTI(JL)))
         ILINJ1=IX(1)-6
         ILINJ2=IX(1)-1
         ! write (*,*) "VOLCALTI",JL,PVOLCALTI(JL),ILINJ1,ILINJ2
         ! calculate total detltap over injected levels
         ZTOTDELP=0.0_JPRB
         DO JK = ILINJ1, ILINJ2
           ZTOTDELP = ZTOTDELP + ZDELP(JL,JK)
         ENDDO
         DO JK = ILINJ1, ILINJ2
           PTENC(JL,JK,KAERO(ITRA)) = PTENC(JL,JK,KAERO(ITRA)) +&
           &  (- ZEMBIOMSO2G(JL) - PCFLX(JL,KAERO(ITRA)))*RG / ZTOTDELP
         ENDDO
         PCFLX(JL,KAERO(ITRA))= - ZEMBIOMSO2G(JL)
     ENDIF
  ENDIF
ENDDO


!-----------------------------------------------------------------------

!*             FLY ASH AND SO2 FROM VOLCANIC ERUPTIONS
!              ---------------------------------------


IF (LAERVOL) THEN

  ZEQUATOR = 2._JPRB * RPI * ZRA
  ZLATSQ = ZEQUATOR / (2*NDGLG)  ! length (m) of the latitude side of the grid


!  PCFLX(KDIM%KIDIA:KDIM%KFDIA,KAERO(ITRA))=0._JPRB
!-- for the NAERVOLC continuous volcanoes, no time/height information is assumed  
  IF (NAERVOLC /= 0) THEN
    DO JVOLC=1,NAERVOLC
!-- find closest latitude points on the model grid

      DO JL=KDIM%KIDIA,KDIM%KFDIA
        IGLGLO=NGLOBALAT(KDIM%KSTGLO+JL-1)
        Z1GP=1.0_JPRB/REAL(NLOENG(IGLGLO),JPRB)
        ZDLON=Z1GP*2.0_JPRB*RPI      ! distance in radians between longitude points on a given latitude line
        ZLAT=ASIN(YDCSGLEG%RMU(IGLGLO))             ! latitude in radians
        ZDLAT = 180._JPRB / NDGLG
        ZGLON(JL)=PGELAM(JL)*ZDEGRAD
        ZGLAT(JL)=ZLAT*ZDEGRAD
        ZGDLAT(JL)=ZDLAT
        ZGDLON(JL)=360._JPRB*Z1GP
        ZVOLCASH(JL)=0._JPRB
        ZVOLCSO2(JL)=0._JPRB
        IBASPL=KDIM%KLEV
        ITOPPL=1
        ZGELAA = MIN( 90._JPRB, RAERVOLE(JVOLE,1)+ZGDLAT(JL)*0.55_JPRB)
        ZGELAB = MAX(-90._JPRB, RAERVOLE(JVOLE,1)-ZGDLAT(JL)*0.55_JPRB)
        ZGELOA = MAX(  0._JPRB, RAERVOLE(JVOLE,2)-ZGDLON(JL)*0.55_JPRB)
        ZGELOB = MIN(360._JPRB, RAERVOLE(JVOLE,2)+ZGDLON(JL)*0.55_JPRB)

        IF (ZGLON(JL) >= ZGELOA .AND. ZGLON(JL) < ZGELOB .AND.&
           & ZGLAT(JL) <= ZGELAA .AND. ZGLAT(JL) > ZGELAB ) THEN

          !ZLONSQ = ZDIST(JL)*1000._JPRB ! length (m) of the longitude side of the grid
          ZLONSQ=ZEQUATOR * ZDLON / (2._JPRB*RPI) ! length (m) of the longitude side of the grid
          ZAREA=ZLATSQ*ZLONSQ
          WRITE(NULOUT,*) ' CONTINUOUS VOLCANO', JVOLC, 'OVER AREA', ZLATSQ, '*', ZLONSQ, '=', ZAREA
          DO JK=1,KDIM%KLEV
            IF (RAERVOLC(JVOLC,5) < ZALT(JL,JK) .AND. RAERVOLC(JVOLC,5) >= ZALT(JL,JK+1)) THEN
              IBASPL=JK
            ENDIF
            IF (RAERVOLC(JVOLC,6) < ZALT(JL,JK) .AND. RAERVOLC(JVOLC,6) >= ZALT(JL,JK+1)) THEN
              ITOPPL=JK
            ENDIF
          ENDDO
          IF (IBASPL == ITOPPL) THEN
            ITOPPL=ITOPPL-1
          ENDIF
          ZDEEPLUME=(PAPHI(JL,ITOPPL)-PAPHI(JL,IBASPL))/RG       ! m
          WRITE(NULOUT,*) ' CONTINUOUS VOLCANO', JVOLC, 'OVER DEPTH', ZDEEPLUME
          ZVOLUME=ZAREA*ZDEEPLUME                                  ! m^3
          WRITE(NULOUT,*) ' CONTINUOUS VOLCANO', JVOLC, 'OVER VOLUME', ZVOLUME
          ZVOLCASH(JL)=RAERVOLC(JVOLC,3)*RAERVOLC(JVOLC,7)/ZVOLUME ! kg m-3 s-1
          ZVOLCSO2(JL)=RAERVOLC(JVOLC,4)*RAERVOLC(JVOLC,8)/ZVOLUME ! kg m-3 s-1
          WRITE(NULOUT,*) ' CONTINUOUS VOLCANO', JVOLC, ZVOLCASH(JL), 'kg m-3 s-1 ASH', ZVOLCSO2(JL), 'kg m-3 s-1 SO2'
          DO JK=ITOPPL,IBASPL
!- updating fly ash tendency (should be in variable 13)
!            PTENC(JL,JK,KAERO(INBAER+1))=PTENC(JL,JK,KAERO(INBAER+1))+&
!              & ZVOLCASH(JL)/PRHO(JL,JK) ! kg kg-1 s-1
!- updating volcanic SO2 tendency (should be in variable 15)
            PTENC(JL,JK,KAERO(ITRA))=PTENC(JL,JK,KAERO(ITRA))+&
              & ZVOLCSO2(JL)/ZDENSAIR(JL,JK) ! kg kg-1 s-1
          ENDDO
        ENDIF
      ENDDO

    ENDDO
  ENDIF
!-- end of contribution by continous volcanoes

  
!-- Explosive volcanoes but no time/height information is assumed  
  IF (NAERVOLE /= 0 .AND. NVOLERUP == 1) THEN    
    DO JVOLE=1,NAERVOLE
!-- find closest latitude points on the model grid
      IVDAYS=NVOLDATS(JVOLE)/100
      IVSECNDS=(NVOLDATS(JVOLE)-IVDAYS*100)*3600

      IVDAYE=NVOLDATE(JVOLE)/100
      IVSECNDE=(NVOLDATE(JVOLE)-IVDAYE*100)*3600
      IF ( (IMDATE > IVDAYS .OR. (IMDATE == IVDAYS .AND. NSTASS >= IVSECNDS)) .AND. &
         & (IVDAYE == 0 .OR. IMDATE < IVDAYE .OR. (IMDATE == IVDAYE .AND. NSTASS <  IVSECNDE )) ) THEN 
        
        DO JL=KDIM%KIDIA,KDIM%KFDIA
          IGLGLO=NGLOBALAT(KDIM%KSTGLO+JL-1)
          Z1GP=1.0_JPRB/REAL(NLOENG(IGLGLO),JPRB)
          ZDLON=Z1GP*2.0_JPRB*RPI      ! distance in radians between longitude points on a given latitude line
          ZLAT=ASIN(YDCSGLEG%RMU(IGLGLO))             ! latitude in radians
          ZDLAT = 180._JPRB / NDGLG
          ZGLON(JL)=PGELAM(JL)*ZDEGRAD
          ZGLAT(JL)=ZLAT*ZDEGRAD
          ZGDLAT(JL)=ZDLAT
          ZGDLON(JL)=360._JPRB*Z1GP
          ZVOLEASH(JL)=0._JPRB
          ZVOLESO2(JL)=0._JPRB
          IBASPL=KDIM%KLEV
          ITOPPL=1
          ZGELAA = MIN( 90._JPRB, RAERVOLE(JVOLE,1)+ZGDLAT(JL)*0.55_JPRB)
          ZGELAB = MAX(-90._JPRB, RAERVOLE(JVOLE,1)-ZGDLAT(JL)*0.55_JPRB)
          ZGELOA = MAX(  0._JPRB, RAERVOLE(JVOLE,2)-ZGDLON(JL)*0.55_JPRB)
          ZGELOB = MIN(360._JPRB, RAERVOLE(JVOLE,2)+ZGDLON(JL)*0.55_JPRB)
          IF (ZGLON(JL) >= ZGELOA .AND. ZGLON(JL) < ZGELOB .AND.&
           & ZGLAT(JL) <= ZGELAA .AND. ZGLAT(JL) > ZGELAB ) THEN

            ZLONSQ=ZEQUATOR * ZDLON / (2._JPRB*RPI) ! length (m) of the longitude side of the grid
            ZAREA=ZLATSQ*ZLONSQ
            ZAREA = ZLATSQ*ZLONSQ
            WRITE(NULOUT,*) ' EXPLOSIVE VOLCANO', JVOLE, 'OVER AREA', ZLATSQ, '*', ZLONSQ, '=', ZAREA
            DO JK=1,KDIM%KLEV
              IF (RAERVOLE(JVOLE,5) < ZALT(JL,JK) .AND. RAERVOLE(JVOLE,5) >= ZALT(JL,JK+1)) THEN
                IBASPL=JK
              ENDIF
              IF (RAERVOLE(JVOLE,6) < ZALT(JL,JK) .AND. RAERVOLE(JVOLE,6) >= ZALT(JL,JK+1)) THEN
                ITOPPL=JK
              ENDIF
            ENDDO
            IBASPL=IBASPL-1
            IF (IBASPL == ITOPPL) THEN
              ITOPPL=ITOPPL-1
            ENDIF

            ZDEEPLUME=(PAPHI(JL,ITOPPL)-PAPHI(JL,IBASPL))/RG       ! m
            WRITE(NULOUT,*) ' EXPLOSIVE VOLCANO', JVOLE, 'OVER DEPTH', ZDEEPLUME
            ZVOLUME=ZAREA*ZDEEPLUME                                  ! m^3
            WRITE(NULOUT,*) ' EXPLOSIVE VOLCANO', JVOLE, 'OVER VOLUME', ZVOLUME
            ZVOLEASH(JL)=RAERVOLE(JVOLE,3)*RAERVOLE(JVOLE,7)/ZVOLUME ! kg m-3 s-1
            ZVOLESO2(JL)=RAERVOLE(JVOLE,4)*RAERVOLE(JVOLE,8)/ZVOLUME ! kg m-3 s-1
            WRITE(NULOUT,*) ' EXPLOSIVE VOLCANO', JVOLE, ZVOLEASH(JL), 'kg m-3 s-1 ASH', ZVOLESO2(JL), 'kg m-3 s-1 SO2'

            DO JK=ITOPPL,IBASPL
!- updating fly ash tendency (should be in variable 13)
              !PTENC(JL,JK,KAERO(INBAER+1))=PTENC(JL,JK,KAERO(INBAER+1))+&
              ! & ZVOLEASH(JL)/PRHO(JL,JK) ! kg kg-1 s-1
!- updating volcanic SO2 tendency (should be in variable 15)
              PTENC(JL,JK,KAERO(ITRA))=PTENC(JL,JK,KAERO(ITRA))+&
               & ZVOLESO2(JL)/ZDENSAIR(JL,JK) ! kg kg-1 s-1
            ENDDO

          ENDIF
        ENDDO
      ENDIF
    ENDDO
!-- end of contribution by explosive volcano without emission details

!-- Explosive volcanoes but some volcanoes without and some with time/height information 
  ELSEIF (NAERVOLE /= 0 .AND. NVOLERUP == 2) THEN

    INBAER=KAERO(ITRA)
    CALL AER_VOLCE&
     &(YDGEOMETRY, YDEAERATM,YDEAERVOL,YDMODEL%YRML_GCONF, KDIM%KIDIA, KDIM%KFDIA, KDIM%KLON  , &
     & KDIM%KTDIA, KDIM%KLEV , KSTART, KSTEP, KDIM%KSTGLO,&
     &KTRAC, KAERO, INBAER ,&
     &ZALT, PAPHI, PCEN  , ZGLAT, ZGLON, ZDENSAIR  ,&
     &PCFLX, PTENC&
     &)
 
  ENDIF
ENDIF

DO JAER=1,NACTAERO
  DO JL=KDIM%KIDIA,KDIM%KFDIA
    PAERSRC(JL,JAER)=PCFLX(JL,KAERO(JAER))  ! store PCFLX
  ENDDO
ENDDO

! SO2 dry deposition (SR, Jan 2018)

ZDVMAX=30.0_JPRB/PTSPHY

DO JL=KDIM%KIDIA,KDIM%KFDIA
  ! use SUMO dry dep velocity for SO2
  ZVDEP=PSO2DD(JL)
  ZHOURLT  =  (RPI - YDRIP%RWSOVR) - PGELAM(JL)
! limit max der dep velocity based on 30 m box height (it is 10-15 m)
  ! Difference w.r.t to longitude of sza max
  ZSCALE = 1.0 + COS(ZHOURLT) * 0.7_JPRB
  ZVDEP = ZSCALE * MIN(ZDVMAX,ZSCALE*ZVDEP)
  PCFLX(JL,KAERO(ITRA))=PCFLX(JL,KAERO(ITRA))+ &
   & ZVDEP*ZDENSAIR(JL,KDIM%KLEV)*(PCEN(JL,KDIM%KLEV,KAERO(ITRA))+PTSPHY*PTENC(JL,KDIM%KLEV,KAERO(ITRA)))
  PAERDDP(JL,ITRA)=ZVDEP*ZDENSAIR(JL,KDIM%KLEV)*(PCEN(JL,KDIM%KLEV,KAERO(ITRA))+PTSPHY*PTENC(JL,KDIM%KLEV,KAERO(ITRA)))
ENDDO

! H2SO4 updated by chemistry later, following oxidation from SO2
ITRA=ITRA+1

! MONOTERPENES
ITRA=ITRA+1

! SEC_ORG
ITRA=ITRA+1
! Treat PSOA as flux into SEC_ORG tracer for condensation.
! Quick solution, following what is done in IFS-GEMS scheme
! Ultimately want to treat chemistry, either simple parameterized oxidation of monoterpenes,
! or full chemistry through C-IFS...

IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KDIM%KIDIA , KDIM%KFDIA  , KDIM%KLON , KDIM%KLEV , NACTAERO, NACTAERO ,  &
   &    ZDELP, PTSPHY, PTENC(:,:,KAERO(1):KAERO(NACTAERO)) , ZTAERI, PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_EM))
ENDIF

! .. units of ZBUD_AER_NUM_PRIMSU (per cc/m2/s)
!
! Units of output should not need changing, boundary layer mixing scheme should be able to handle them
!  Units subsequently remain consistent with what is expected in UKCA_AERO_STEP

! .. for MODE aerosol tracers in PGFL/ZCEN, component-masses are mass-mixing-ratios (kg/kg)
! ..                                      , number-concns are equiv.-number-mixing-ratios (ptcls/molecule)
! .. this then matches use in UM.

! 2D mass and number emission fields, for output.

 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)            = ZBUD_AER_MAS_PRIMSU(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+1:NMODES+7)   = ZBUD_AER_MAS_PRIMBC(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+8:NMODES+14)  = ZBUD_AER_MAS_PRIMOC(KDIM%KIDIA:KDIM%KFDIA,1:NMODES) &
  & + ZBUD_AER_MAS_PRIMSOA(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+15:NMODES+21) = ZBUD_AER_MAS_PRIMSS(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+22:NMODES+28) = ZBUD_AER_MAS_PRIMDU(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)

! NMODES=7 as default, so dust in indexes 29:35
! Last 3 modes insoluble, w/ last two featurin dust only
! Therefore indexes 34 & 35 for acc. and coarse du


 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+29)=PSOA(KDIM%KIDIA:KDIM%KFDIA)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+30)=PBCFF(KDIM%KIDIA:KDIM%KFDIA)
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+31)=PBCBF(KDIM%KIDIA:KDIM%KFDIA)
 IF(LFIRE) THEN
    PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+32)=PBCGF(KDIM%KIDIA:KDIM%KFDIA)
 ELSE
    PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+32)=0.0_JPRB
 ENDIF
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+33)=POMFF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon
 PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+34)=POMBF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon
 IF(LFIRE) THEN
    PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+35)=POMGF(KDIM%KIDIA:KDIM%KFDIA)/1.4 ! so that in mass of carbon
 ELSE
    PGLO_MASS_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+35)=0.0_JPRB
 ENDIF
 PGLO_NUM_EMS(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)            =ZBUD_AER_NUM_PRIMSU(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_NUM_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+1:NMODES+7)   =ZBUD_AER_NUM_PRIMCAR(KDIM%KIDIA:KDIM%KFDIA,1:NMODES) &
  & + ZBUD_AER_NUM_PRIMSOA(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_NUM_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+8:NMODES+14)  =ZBUD_AER_NUM_PRIMSS(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)
 PGLO_NUM_EMS(KDIM%KIDIA:KDIM%KFDIA,NMODES+15:NMODES+21) =ZBUD_AER_NUM_PRIMDU(KDIM%KIDIA:KDIM%KFDIA,1:NMODES)

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('UKCA_MODE_EMS_IFS',1,ZHOOK_HANDLE)
END SUBROUTINE UKCA_MODE_EMS_IFS
