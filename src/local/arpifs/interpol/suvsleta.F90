#ifdef RS6K
! compiler bug on RS6000
@PROCESS NOOPTIMIZE
#endif
SUBROUTINE SUVSLETA(YDGEOMETRY,LDVERBOSE)

!**** *SUVSLETA*  - Routine to initialize structure VSLETA
!                   (externalisable part of interpolator)

!     Purpose.
!     --------
!           Routine to initialize structure VSLETA.
!           Intermediate quantities used for vertical interpolations.

!**   Interface.
!     ----------

!     *CALL* SUVSLETA

!        Explicit arguments :
!        --------------------
!        LDVERBOSE : more printings.

!        Implicit arguments :
!        --------------------
!        see the modules used above.

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF* (87-10-15) in routine SUVERT.
!      K. Yessad (2012) after SUVERT.

!     Modifications.
!     --------------
!      F. Vana 13-Feb-2014 option LSLHDVER
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      K. Yessad (March 2017): simplify level numbering in interpolator.
!      F. Vana    21-Nov-2017: Option LHOISLT
!      F. Vana    20-Feb-2019: Fix for WENO weights
! End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK

USE YOMLUN_IFSAUX, ONLY : NULOUT

! arp/ifs dependencies to be solved later.
USE YOMDYNA  , ONLY : YRDYNA
USE YOMCT0   , ONLY : LREGETA

!-------------------------------------------------------------------------------

IMPLICIT NONE 
TYPE(GEOMETRY),INTENT(INOUT) :: YDGEOMETRY
LOGICAL       ,INTENT(IN)    :: LDVERBOSE

!-------------------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IREF, IINCR, JL, JLEV
LOGICAL :: LLWENO

REAL(KIND=JPRB) :: ZD1, ZD2, ZD3, ZD4, ZD5, ZD6, ZFAC, ZDVETAH
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUVSLETA',0,ZHOOK_HANDLE)
ASSOCIATE(YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVSLETA=>YDGEOMETRY%YRVSLETA)
ASSOCIATE(NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG,   NRLEVX=>YDVSLETA%NRLEVX,   VRLEVX=>YDVSLETA%VRLEVX)
!     ------------------------------------------------------------------

!*            1. SET UP VERTICAL SYSTEM: YRVSLETA.
!             ------------------------------------

LLWENO = (YRDYNA%LSLTVWENO.OR.YRDYNA%LRHSVWENO)

! * attributes VCUICO, VSLD, VSLDW:
ALLOCATE(YDVSLETA%VCUICO(4,0:NFLEVG-1))
ALLOCATE(YDVSLETA%VSLD(3,0:NFLEVG-1))
ALLOCATE(YDVSLETA%VSLDW(3,3,0:NFLEVG-1))

IINCR=0
DO JLEV=1,NFLEVG-3
  ZD1=YDVETA%VETAF(JLEV  +IINCR)-YDVETA%VETAF(JLEV+1+IINCR)
  ZD2=YDVETA%VETAF(JLEV  +IINCR)-YDVETA%VETAF(JLEV+2+IINCR)
  ZD3=YDVETA%VETAF(JLEV  +IINCR)-YDVETA%VETAF(JLEV+3+IINCR)
  ZD4=YDVETA%VETAF(JLEV+1+IINCR)-YDVETA%VETAF(JLEV+2+IINCR)
  ZD5=YDVETA%VETAF(JLEV+1+IINCR)-YDVETA%VETAF(JLEV+3+IINCR)
  ZD6=YDVETA%VETAF(JLEV+2+IINCR)-YDVETA%VETAF(JLEV+3+IINCR)

  YDVSLETA%VCUICO(1,JLEV)= 1.0_JPRB/(ZD1*ZD2*ZD3)
  YDVSLETA%VCUICO(2,JLEV)=-1.0_JPRB/(ZD1*ZD4*ZD5)
  YDVSLETA%VCUICO(3,JLEV)= 1.0_JPRB/(ZD2*ZD4*ZD6)
  YDVSLETA%VCUICO(4,JLEV)=-1.0_JPRB/(ZD3*ZD5*ZD6)

  IF (YRDYNA%LSLHDQUAD.OR.(YRDYNA%LHOISLT.AND.((YRDYNA%HOISLTH /= 0._JPRB).OR.(YRDYNA%HOISLTV /= 0._JPRB)))) THEN
    YDVSLETA%VSLD(1,JLEV)=(ZD6-ZD2)/ZD4
    YDVSLETA%VSLD(2,JLEV)=(ZD1-ZD5)/ZD4
    YDVSLETA%VSLD(3,JLEV)=-ZD4*ZD4/(ZD1*ZD2+ZD5*ZD6)
  ELSE
    YDVSLETA%VSLD(1,JLEV)=0._JPRB
    YDVSLETA%VSLD(2,JLEV)=0._JPRB
    YDVSLETA%VSLD(3,JLEV)=0._JPRB
  ENDIF

  IF (YRDYNA%LSLHD) THEN
    IF (YRDYNA%LSLHDVER) THEN
      YDVSLETA%VSLDW(1,1,JLEV)=1.0_JPRB-YRDYNA%SLHDEPSV*(YDVAB%VBH(JLEV-1)+YDVAB%VBH(JLEV))*ZD4/ZD1
      YDVSLETA%VSLDW(1,2,JLEV)=YRDYNA%SLHDEPSV*(YDVAB%VBH(JLEV-1)+YDVAB%VBH(JLEV))*ZD4/ZD5
      YDVSLETA%VSLDW(1,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(2,1,JLEV)=YRDYNA%SLHDEPSV*(YDVAB%VBH(JLEV-1)+YDVAB%VBH(JLEV))*ZD4/ZD2
      YDVSLETA%VSLDW(2,2,JLEV)=1.0_JPRB-YRDYNA%SLHDEPSV*(YDVAB%VBH(JLEV-1)+YDVAB%VBH(JLEV))*ZD4/ZD6
      YDVSLETA%VSLDW(2,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(3,1,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(3,2,JLEV)=YRDYNA%SLHDEPSV*(YDVAB%VBH(JLEV-1)+YDVAB%VBH(JLEV))*(ZD4*ZD4)/(ZD5*ZD6)
      YDVSLETA%VSLDW(3,3,JLEV)=1.0_JPRB
    ELSE
      YDVSLETA%VSLDW(1,1,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD1
      YDVSLETA%VSLDW(1,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD5
      YDVSLETA%VSLDW(1,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(2,1,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD2
      YDVSLETA%VSLDW(2,2,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD6
      YDVSLETA%VSLDW(2,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(3,1,JLEV)=0.0_JPRB
      YDVSLETA%VSLDW(3,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*(ZD4*ZD4)/(ZD5*ZD6)
      YDVSLETA%VSLDW(3,3,JLEV)=1.0_JPRB
    ENDIF
  ELSE
    YDVSLETA%VSLDW(1,1,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(1,2,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(1,3,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(2,1,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(2,2,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(2,3,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(3,1,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(3,2,JLEV)=0.0_JPRB
    YDVSLETA%VSLDW(3,3,JLEV)=0.0_JPRB
  ENDIF

ENDDO

IF (LDVERBOSE) THEN
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VCUICO at full levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICO(1,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICO(1,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICO(2,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICO(2,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICO(3,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICO(3,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICO(4,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICO(4,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VSLD at full levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLD(1,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLD(1,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLD(2,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLD(2,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLD(3,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLD(3,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VSLDW at full levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''    VSLDW(1,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')JLEV,YDVSLETA%VSLDW(1,:,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''    VSLDW(2,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')JLEV,YDVSLETA%VSLDW(2,:,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''    VSLDW(3,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')JLEV,YDVSLETA%VSLDW(3,:,JLEV)
  ENDDO
ENDIF

! * attributes VCUICOH, VSLDH, VSLDWH:
ALLOCATE(YDVSLETA%VCUICOH(4,0:NFLEVG-1))
ALLOCATE(YDVSLETA%VSLDH(3,0:NFLEVG-1))
ALLOCATE(YDVSLETA%VSLDWH(3,3,0:NFLEVG-1))

IINCR=-1
DO JLEV=1,NFLEVG-3
  ZD1=YDVETA%VETAH(JLEV  +IINCR)-YDVETA%VETAH(JLEV+1+IINCR)
  ZD2=YDVETA%VETAH(JLEV  +IINCR)-YDVETA%VETAH(JLEV+2+IINCR)
  ZD3=YDVETA%VETAH(JLEV  +IINCR)-YDVETA%VETAH(JLEV+3+IINCR)
  ZD4=YDVETA%VETAH(JLEV+1+IINCR)-YDVETA%VETAH(JLEV+2+IINCR)
  ZD5=YDVETA%VETAH(JLEV+1+IINCR)-YDVETA%VETAH(JLEV+3+IINCR)
  ZD6=YDVETA%VETAH(JLEV+2+IINCR)-YDVETA%VETAH(JLEV+3+IINCR)

  YDVSLETA%VCUICOH(1,JLEV)= 1.0_JPRB/(ZD1*ZD2*ZD3)
  YDVSLETA%VCUICOH(2,JLEV)=-1.0_JPRB/(ZD1*ZD4*ZD5)
  YDVSLETA%VCUICOH(3,JLEV)= 1.0_JPRB/(ZD2*ZD4*ZD6)
  YDVSLETA%VCUICOH(4,JLEV)=-1.0_JPRB/(ZD3*ZD5*ZD6)

  IF (YRDYNA%LSLHDQUAD) THEN
    YDVSLETA%VSLDH(1,JLEV)=(ZD6-ZD2)/ZD4
    YDVSLETA%VSLDH(2,JLEV)=(ZD1-ZD5)/ZD4
    YDVSLETA%VSLDH(3,JLEV)=-ZD4*ZD4/(ZD1*ZD2+ZD5*ZD6)
  ELSE
    YDVSLETA%VSLDH(1,JLEV)=0._JPRB
    YDVSLETA%VSLDH(2,JLEV)=0._JPRB
    YDVSLETA%VSLDH(3,JLEV)=0._JPRB
  ENDIF

  IF (YRDYNA%LSLHD) THEN
    IF (YRDYNA%LSLHDVER) THEN
      YDVSLETA%VSLDWH(1,1,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*YDVAB%VBH(JLEV)*ZD4/ZD1
      YDVSLETA%VSLDWH(1,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*YDVAB%VBH(JLEV)*ZD4/ZD5
      YDVSLETA%VSLDWH(1,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(2,1,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*YDVAB%VBH(JLEV)*ZD4/ZD2
      YDVSLETA%VSLDWH(2,2,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*YDVAB%VBH(JLEV)*ZD4/ZD6
      YDVSLETA%VSLDWH(2,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(3,1,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(3,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*YDVAB%VBH(JLEV)*(ZD4*ZD4)/(ZD5*ZD6)
      YDVSLETA%VSLDWH(3,3,JLEV)=1.0_JPRB
    ELSE
      YDVSLETA%VSLDWH(1,1,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD1
      YDVSLETA%VSLDWH(1,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD5
      YDVSLETA%VSLDWH(1,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(2,1,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD2
      YDVSLETA%VSLDWH(2,2,JLEV)=1.0_JPRB-2.0_JPRB*YRDYNA%SLHDEPSV*ZD4/ZD6
      YDVSLETA%VSLDWH(2,3,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(3,1,JLEV)=0.0_JPRB
      YDVSLETA%VSLDWH(3,2,JLEV)=2.0_JPRB*YRDYNA%SLHDEPSV*(ZD4*ZD4)/(ZD5*ZD6)
      YDVSLETA%VSLDWH(3,3,JLEV)=1.0_JPRB
    ENDIF
  ELSE
    YDVSLETA%VSLDWH(1,1,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(1,2,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(1,3,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(2,1,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(2,2,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(2,3,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(3,1,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(3,2,JLEV)=0._JPRB
    YDVSLETA%VSLDWH(3,3,JLEV)=0._JPRB
  ENDIF
ENDDO

IF (LDVERBOSE) THEN
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VCUICOH at half levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICOH(1,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICOH(1,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICOH(2,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICOH(2,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICOH(3,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICOH(3,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VCUICOH(4,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VCUICOH(4,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VSLDH at half levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLDH(1,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLDH(1,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLDH(2,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLDH(2,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''     VSLDH(3,JLEV)'')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,(5X,F20.10))')JLEV,YDVSLETA%VSLDH(3,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * VSLDWH at half levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''   VSLDWH(1,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')&
      & JLEV,YDVSLETA%VSLDWH(1,:,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''   VSLDWH(2,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')&
      & JLEV,YDVSLETA%VSLDWH(2,:,JLEV)
  ENDDO
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,5X,''   VSLDWH(3,:,JLEV) '')')
  DO JLEV=1,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(5X,F20.10))')&
      & JLEV,YDVSLETA%VSLDWH(3,:,JLEV)
  ENDDO
ENDIF

! * attributes GAMMA_WENO
!   NOTE: is done for the full level interpolation only as there is no half level trajectory
!          research available
ALLOCATE(YDVSLETA%GAMMA_WENO(NFLEVG,3))
  
IF (LLWENO.AND.(.NOT. LREGETA)) THEN
  DO JLEV=2,NFLEVG-4
    ! eliminated from i-1
    YDVSLETA%GAMMA_WENO(JLEV,3)=1._JPRB/((YDVETA%VETAF(JLEV-1)-YDVETA%VETAF(JLEV+3)) &
      &                               *  (YDVETA%VETAF(JLEV-1)-YDVETA%VETAF(JLEV+4)) )
    ! eliminated from i+4
    YDVSLETA%GAMMA_WENO(JLEV,2)=1._JPRB/((YDVETA%VETAF(JLEV+4)-YDVETA%VETAF(JLEV-1)) &
      &                               *  (YDVETA%VETAF(JLEV+4)-YDVETA%VETAF(JLEV  )) )
    ! eliminated from i
    ! (probably just a bit more sophisticated way to define gamma1 = -(gamma2+gamma3)
    YDVSLETA%GAMMA_WENO(JLEV,1)=(1._JPRB - YDVSLETA%GAMMA_WENO(JLEV,3)      &
      &  * (YDVETA%VETAF(JLEV)-YDVETA%VETAF(JLEV+3))*(YDVETA%VETAF(JLEV)-YDVETA%VETAF(JLEV+4)) ) &
      &  /((YDVETA%VETAF(JLEV)-YDVETA%VETAF(JLEV-1))*(YDVETA%VETAF(JLEV)-YDVETA%VETAF(JLEV+4)))
  ENDDO
ENDIF

IF (LDVERBOSE.AND.LLWENO) THEN
  WRITE(NULOUT,*) ''
  WRITE(NULOUT,*) ' * GAMMA_WENO at full levels:'
  WRITE(NULOUT,*) ''
  WRITE(UNIT=NULOUT,FMT='(1X,''JLEV'',1X,2X,''  GAMMA_WENO(JLEV,1)'', &
    &  2X,''  GAMMA_WENO(JLEV,2)'',2X,''  GAMMA_WENO(JLEV,3)'')' )
  DO JLEV=2,NFLEVG-3
    WRITE(UNIT=NULOUT,FMT='(1X,I3,2X,3(2X,F20.10))') JLEV,YDVSLETA%GAMMA_WENO(JLEV,1), &
      & YDVSLETA%GAMMA_WENO(JLEV,2),YDVSLETA%GAMMA_WENO(JLEV,3)
  ENDDO
ENDIF


! * attributes NFLEVX and VRLEVX.
! * compute optimal value of NRLEVX; set to (2 / min(delta eta)) + 1
ZDVETAH=1._JPRB
DO JLEV=1,NFLEVG
  ZDVETAH=MIN(ZDVETAH,YDVETA%VETAH(JLEV)-YDVETA%VETAH(JLEV-1))
ENDDO
NRLEVX=NINT(2.0_JPRB/ZDVETAH + 0.5_JPRB)+1
WRITE(NULOUT,*) ''
WRITE(NULOUT,*) ' SUSVSLETA: NRLEVX=',NRLEVX

VRLEVX=REAL(NRLEVX,JPRB)
ZFAC=(YDVETA%VETAF(NFLEVG+1)-YDVETA%VETAF(0))/VRLEVX

! * attribute NVAUTF.
ALLOCATE(YDVSLETA%NVAUTF(0:NRLEVX))

IREF=1
DO JL=0,NRLEVX
  IF(JL*ZFAC >= YDVETA%VETAF(IREF+1).AND.IREF < NFLEVG-1) IREF=IREF+1
  YDVSLETA%NVAUTF(JL)=IREF
ENDDO

! * attribute NVAUTH.
ALLOCATE(YDVSLETA%NVAUTH(0:NRLEVX))

IREF=1
DO JL=0,NRLEVX
  IF(JL*ZFAC >= YDVETA%VETAH(IREF).AND.IREF < NFLEVG-1) IREF=IREF+1
  YDVSLETA%NVAUTH(JL)=IREF
ENDDO

! * attribute VRDETAR.
ALLOCATE(YDVSLETA%VRDETAR(NFLEVG))

DO JLEV=NFLEVG,2,-1
  YDVSLETA%VRDETAR(JLEV)=(YDVETA%VETAH(JLEV)-YDVETA%VETAH(JLEV-1))/(YDVETA%VETAH(JLEV-1)-YDVETA%VETAH(JLEV-2))
ENDDO
YDVSLETA%VRDETAR(1)=1.0_JPRB

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUVSLETA',1,ZHOOK_HANDLE)
END SUBROUTINE SUVSLETA
