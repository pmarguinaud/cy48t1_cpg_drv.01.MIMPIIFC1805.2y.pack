MODULE ECPHYS_PERTURB_TYPE_MOD

USE PARKIND1, ONLY : JPIM, JPRB
USE FIELD_MODULE, ONLY: FIELD_2D, FIELD_3D
USE FIELD_REGISTRY_MOD, ONLY: FIELD_REGISTRY, CREATE_TEMPORARY, DELETE_TEMPORARY
USE TYPE_MODEL, ONLY : MODEL
USE STOPH_MIX, ONLY : TSTOPH
USE YOMSPSDT, ONLY : YSPPT_CONFIG, YSPPT
USE SPP_MOD, ONLY : YSPP_CONFIG, YSPP
USE YOMLCZ, ONLY : GPFORCEU, GPFORCEV, GPFORCET, GPFORCEQ

IMPLICIT NONE

TYPE PERTURB_TYPE
  REAL(KIND=JPRB), DIMENSION(:),     POINTER, CONTIGUOUS :: PSTOPHCA ! CA pattern 
  REAL(KIND=JPRB), DIMENSION(:,:,:), POINTER, CONTIGUOUS :: PGP2DSDT ! SPPT pattern
  REAL(KIND=JPRB), DIMENSION(:,:,:), POINTER, CONTIGUOUS :: PGP2DSPP ! SPP  patterns
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PVORT, PVORTGRADX, PVORTGRADY ! vorticity and its horizontal gradients
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PTOTDISS_SMOOTH ! smoothed total dissipation rate
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PFORCEU, PFORCEV, PFORCET, PFORCEQ ! nonlinear stochastic forcing terms 
  REAL(KIND=JPRB), DIMENSION(:),     POINTER, CONTIGUOUS :: PCUCONVCA   ! CA state
  REAL(KIND=JPRB), DIMENSION(:),     POINTER, CONTIGUOUS :: PNLCONVCA   ! indicates if CA was alive at a gridpoint before
  REAL(KIND=JPRB), DIMENSION(:),     POINTER, CONTIGUOUS :: PCAPECONVCA ! 0 or NLIVES dependeing on threshold (CIN defined at the moment)
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PSTREAM     ! Streamfunction forcing/spectral pattern
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PTEMP       ! Temperature forcing/spectral pattern
  REAL(KIND=JPRB), DIMENSION(:,:),   POINTER, CONTIGUOUS :: PTOTDISS    ! total dissipation rate
  REAL(KIND=JPRB), DIMENSION(:,:,:,:), POINTER, CONTIGUOUS :: PSPPTGFIX   ! tendencies for spptfix

CONTAINS
  PROCEDURE :: UPDATE_VIEW => PERTURB_TYPE_UPDATE_VIEW
  PROCEDURE :: FINAL => PERTURB_TYPE_FINAL
END TYPE PERTURB_TYPE

TYPE PERTURB_LOCAL_TYPE
  REAL(KIND=JPRB), DIMENSION(:)  , POINTER, CONTIGUOUS :: ZWMEAN   ! vertically averaged conv. updraught speed (M/S)
  REAL(KIND=JPRB), DIMENSION(:,:), POINTER, CONTIGUOUS :: ZDISSCU  ! Dissipation (J/M2) (convection)
  REAL(KIND=JPRB), DIMENSION(:)  , POINTER, CONTIGUOUS :: ZCUCONVCA ! scaled CA to pass to convection
  REAL(KIND=JPRB), DIMENSION(:,:), POINTER, CONTIGUOUS :: ZDISSGW  ! kinetic energy/unit mass dissipated in one t-step (gravity wave drag)

  ! Storage fields to provide thread-local views
  TYPE(FIELD_2D), POINTER :: F_ZWMEAN, F_ZCUCONVCA
  TYPE(FIELD_3D), POINTER :: F_ZDISSCU, F_ZDISSGW

CONTAINS
  PROCEDURE :: INIT => PERTURB_LOCAL_TYPE_INIT
  PROCEDURE :: UPDATE_VIEW => PERTURB_LOCAL_TYPE_UPDATE_VIEW
  PROCEDURE :: FINAL => PERTURB_LOCAL_TYPE_FINAL
END TYPE PERTURB_LOCAL_TYPE

CONTAINS

  SUBROUTINE PERTURB_TYPE_UPDATE_VIEW(THIS, YDMODEL, YDSTOPH, IBL, IOFF, ICEND, NPROMA, NFLEVG, ZSPPTGFIX)
    ! Set up local view pointers for perturbation variables
    !
    ! M. Lange: Since many variables here are incompatible with the
    ! new FIELD data structures, we delay switching this
    ! block to a later date. The poitner setup has been moved here to
    ! adhere to the block-parallel view setup API.
    ! R. El Khatib 26-02-2021 fix bounds violation

    CLASS(PERTURB_TYPE) :: THIS
    TYPE(MODEL), TARGET, INTENT(INOUT) :: YDMODEL
    TYPE(TSTOPH), TARGET, INTENT(INOUT) :: YDSTOPH
    INTEGER(KIND=JPIM), INTENT(IN) :: IBL, IOFF, ICEND, NPROMA, NFLEVG
    REAL(KIND=JPRB), DIMENSION(:,:,:,:,:), TARGET :: ZSPPTGFIX
    INTEGER(KIND=JPIM) :: J2D, JARP

    IF(ALLOCATED(YDSTOPH%RSTOPHCA))THIS%PSTOPHCA=> YDSTOPH%RSTOPHCA(IOFF:IOFF+ICEND-1)
    IF (YSPPT_CONFIG%LSPSDT) THEN
      ALLOCATE(THIS%PGP2DSDT(NPROMA,YSPPT%YGPSDT(1)%NG2D,YSPPT%N2D))
      DO J2D=1,YSPPT%N2D
        THIS%PGP2DSDT(:,:,J2D) = YSPPT%YGPSDT(J2D)%GP2D(:,:,IBL)
      ENDDO
    ELSE
      THIS%PGP2DSDT => NULL()
    ENDIF
    IF (YSPP_CONFIG%LSPP) THEN
      ALLOCATE(THIS%PGP2DSPP(NPROMA,YSPP%GP_ARP(1)%NG2D,YSPP%N2D))
      DO JARP=1,YSPP%N2D
        THIS%PGP2DSPP(:,:,JARP) = YSPP%GP_ARP(JARP)%GP2D(:,:,IBL)
      ENDDO
    ELSE
      THIS%PGP2DSPP=> NULL()
    ENDIF
    IF(ALLOCATED(YDSTOPH%GPTOTDISS))&
     & THIS%PTOTDISS_SMOOTH => YDSTOPH%GPTOTDISS_SMOOTH(:,:,IBL)
    IF(ALLOCATED(YDSTOPH%GPVORTGRAD))&
     & THIS%PVORT           => YDSTOPH%GPVORTGRAD(:,1:NFLEVG,IBL)
    IF(ALLOCATED(YDSTOPH%GPVORTGRAD))&
     & THIS%PVORTGRADX      => YDSTOPH%GPVORTGRAD(:,NFLEVG+1:2*NFLEVG,IBL)
    IF(ALLOCATED(YDSTOPH%GPVORTGRAD))&
     & THIS%PVORTGRADY      => YDSTOPH%GPVORTGRAD(:,2*NFLEVG+1:3*NFLEVG,IBL)
    IF(YDSTOPH%LFORCENL)THEN
      THIS%PFORCEU         => GPFORCEU(:,:,IBL,0)
      THIS%PFORCEV         => GPFORCEV(:,:,IBL,0)
      THIS%PFORCET         => GPFORCET(:,:,IBL,0)
      THIS%PFORCEQ         => GPFORCEQ(:,:,IBL,0)
    ENDIF
    IF (YSPPT_CONFIG%LSPSDT.AND.YSPPT_CONFIG%LSPPTGFIX) THEN
      THIS%PSPPTGFIX  => ZSPPTGFIX(:,:,:,:,IBL)
    ENDIF
    IF(YDMODEL%YRML_PHY_EC%YRECUCONVCA%LCUCONV_CA) THEN
      THIS%PCUCONVCA => YDMODEL%YRML_PHY_EC%YRECUCONVCA%RCUCONVCA(IOFF:IOFF+ICEND-1)
      THIS%PNLCONVCA => YDMODEL%YRML_PHY_EC%YRECUCONVCA%RNLCONVCA(IOFF:IOFF+ICEND-1)
      THIS%PCAPECONVCA => YDMODEL%YRML_PHY_EC%YRECUCONVCA%RCAPECONVCA(IOFF:IOFF+ICEND-1)
    ENDIF
    IF(ALLOCATED(YDSTOPH%GPSTREAM)) THIS%PSTREAM => YDSTOPH%GPSTREAM(:,:,IBL)
    IF(YDSTOPH%LSTOPH_SPBS_T) THEN
      THIS%PTEMP => YDSTOPH%GPTEMP(:,:,IBL)
    ELSE
      ALLOCATE(THIS%PTEMP(1,1))
    ENDIF
    IF(ALLOCATED(YDSTOPH%GPTOTDISS)) THIS%PTOTDISS => YDSTOPH%GPTOTDISS(:,:,IBL)

  END SUBROUTINE PERTURB_TYPE_UPDATE_VIEW

  SUBROUTINE PERTURB_TYPE_FINAL(THIS, YDSTOPH)
    CLASS(PERTURB_TYPE) :: THIS
    TYPE(TSTOPH), TARGET, INTENT(INOUT) :: YDSTOPH

    ! bit of cleaning
    IF (YSPPT_CONFIG%LSPSDT) DEALLOCATE(THIS%PGP2DSDT)
    IF (YSPP_CONFIG%LSPP) DEALLOCATE(THIS%PGP2DSPP)
    IF(.NOT. YDSTOPH%LSTOPH_SPBS_T) DEALLOCATE(THIS%PTEMP)
  END SUBROUTINE PERTURB_TYPE_FINAL


  SUBROUTINE PERTURB_LOCAL_TYPE_INIT(SELF, REGISTRY, NLEV, PERSISTENT)
    CLASS(PERTURB_LOCAL_TYPE) :: SELF
    TYPE(FIELD_REGISTRY), INTENT(INOUT) :: REGISTRY
    INTEGER(KIND=JPIM), INTENT(IN) :: NLEV
    LOGICAL, OPTIONAL, INTENT(IN) :: PERSISTENT

    SELF%F_ZWMEAN => CREATE_TEMPORARY( GEOM=REGISTRY%GEOM, PERSISTENT=PERSISTENT)
    SELF%F_ZDISSCU => CREATE_TEMPORARY( GEOM=REGISTRY%GEOM, NLEV=NLEV, PERSISTENT=PERSISTENT)
    SELF%F_ZCUCONVCA => CREATE_TEMPORARY( GEOM=REGISTRY%GEOM, PERSISTENT=PERSISTENT)
    SELF%F_ZDISSGW => CREATE_TEMPORARY( GEOM=REGISTRY%GEOM, NLEV=NLEV, PERSISTENT=PERSISTENT)
  END SUBROUTINE PERTURB_LOCAL_TYPE_INIT

  SUBROUTINE PERTURB_LOCAL_TYPE_UPDATE_VIEW(SELF, BLOCK_INDEX)
    CLASS(PERTURB_LOCAL_TYPE) :: SELF
    INTEGER(KIND=JPIM), INTENT(IN) :: BLOCK_INDEX

    SELF%ZWMEAN => SELF%F_ZWMEAN%GET_VIEW(BLOCK_INDEX)
    SELF%ZDISSCU => SELF%F_ZDISSCU%GET_VIEW(BLOCK_INDEX)
    SELF%ZCUCONVCA => SELF%F_ZCUCONVCA%GET_VIEW(BLOCK_INDEX)
    SELF%ZDISSGW => SELF%F_ZDISSGW%GET_VIEW(BLOCK_INDEX, ZERO=.TRUE.)
  END SUBROUTINE PERTURB_LOCAL_TYPE_UPDATE_VIEW

  SUBROUTINE PERTURB_LOCAL_TYPE_FINAL(SELF)
    CLASS(PERTURB_LOCAL_TYPE) :: SELF

    CALL DELETE_TEMPORARY(SELF%F_ZWMEAN)
    CALL DELETE_TEMPORARY(SELF%F_ZDISSCU)
    CALL DELETE_TEMPORARY(SELF%F_ZCUCONVCA)
    CALL DELETE_TEMPORARY(SELF%F_ZDISSGW)
  END SUBROUTINE PERTURB_LOCAL_TYPE_FINAL

END MODULE ECPHYS_PERTURB_TYPE_MOD
