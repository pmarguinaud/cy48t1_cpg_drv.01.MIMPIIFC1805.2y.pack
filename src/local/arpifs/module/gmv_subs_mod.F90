MODULE GMV_SUBS_MOD

! Modifications
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   K. Yessad (Sep 2008): remove lpc_xidt.
!   K. Yessad (Nov 2009): prune lpc_old.
!   K. Yessad (Jan 2011): add attributes MCSPDPT and MCSVDPT.
!   K. Yessad (Jan 2011): new architecture for LBC modules and set-up.
!   P. Marguinaud (Mar 2011) : Reduce the number of allocated fields in SETUP_T9
!   N. Wedi  01-11-2011 : remove LVOR + add MSGRTL,M for LGRADSP
!   T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!   K. Yessad (July 2014): Move some variables, rename some variables.
!   R. El Khatib 06-Mar-2015 optimization : SETUP_PH9 prints only if nprintlev >=2
!   O. Marsden   sept 2016 added call to setup_t0(yt5), to allow cleaning up use of YT5 
!   K. Yessad (Dec 2016): Prune obsolete options.
!   K. Yessad (June 2017): Introduce NHQE model.
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!   O. Jaron (Sep 2018): remove LUVDER condition for MUL/MUV pointers.
!   R. El Khatib 01-Sep-2020 Memory saving for fullpos (conf 903)
!-------------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM, JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK
USE TYPE_GMVS , ONLY : TYPE_T0, TYPE_T1, TYPE_T9, TYPE_PH9, TYPE_GP
USE YOMCT0    , ONLY : LTWOTL, LNHDYN, NCONF, LSLAG, LRSHW, NUNDEFLD, LNHEE, LNHQE, &
 &                     LALLOPR
USE YOMMP0    , ONLY : NPRINTLEV
USE YOMDYNA   , ONLY : YRDYNA
USE YOMGMV    , ONLY : TGMV
USE YOMLUN    , ONLY : NULOUT
USE YEMLBC_INIT, ONLY : LTENC

IMPLICIT NONE

SAVE 

PRIVATE
PUBLIC SETUP_GMV,SETUP_GMV5, SETUP_PH9_PRED, SETUP_PH9_CORR, SETUP_GP

CONTAINS

!-------------------------------------------------------------------------

SUBROUTINE SETUP_GMV(YDGEOMETRY,YDGMV,YDEPHY,YDDIMF,YDPHY)
USE YOEPHY       , ONLY : TEPHY
USE YOMDIMF      , ONLY : TDIMF
USE YOMPHY       , ONLY : TPHY
USE GEOMETRY_MOD , ONLY : GEOMETRY
TYPE(GEOMETRY),INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)    ,INTENT(INOUT) :: YDGMV
TYPE(TDIMF)   ,INTENT(INOUT) :: YDDIMF
TYPE(TEPHY)   ,INTENT(INOUT) :: YDEPHY
TYPE(TPHY)    ,INTENT(INOUT) :: YDPHY
LOGICAL :: LLP
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GMV',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, &
 & YT0=>YDGMV%YT0, YT1=>YDGMV%YT1, YT5=>YDGMV%YT5, YT9=>YDGMV%YT9, YPH9=>YDGMV%YPH9, YGP=>YDGMV%YGP, &
 & NPROMA=>YDDIM%NPROMA, NGPBLKS=>YDDIM%NGPBLKS, &
 & NFLEVG=>YDDIMV%NFLEVG)

LLP = NPRINTLEV > 1.OR. LALLOPR
IF (LLP) WRITE(NULOUT,*)
IF (LLP) WRITE(NULOUT,*) 'SETUP_GMV PRINTS'
IF (LLP) WRITE(NULOUT,*)' * SETUP_T0 : time t POINTERS (YT0)'
CALL SETUP_T0(YDDIMF,YT0)
CALL SETUP_T0(YDDIMF,YT5)
CALL SETUP_T9(YDEPHY,YDDIMF,YDPHY,YT0,YT9)
CALL SETUP_T1(YDDIMF,YT1)
CALL SETUP_PH9(YT0,YT9,YPH9)
CALL SETUP_GP(YGP)
NDIMGMV  = YT0%NDIM+YT9%NDIM
NDIMGMVS = YT0%NDIMS+YT9%NDIMS
IF (LLP) WRITE(NULOUT,*)
IF (LLP) WRITE(NULOUT,*)'YT0%NDIM=',YT0%NDIM,' YT0%NDIMS=',YT0%NDIMS
IF (LLP) WRITE(NULOUT,*)'YT9%NDIM=',YT9%NDIM,' YT9%NDIMS=',YT9%NDIMS
IF (LLP) WRITE(NULOUT,*)'YT1%NDIM=',YT1%NDIM,' YT1%NDIMS=',YT1%NDIMS
IF (ALLOCATED(YDGMV%GMV))  DEALLOCATE(YDGMV%GMV)
IF (ALLOCATED(YDGMV%GMVS)) DEALLOCATE(YDGMV%GMVS)
ALLOCATE(YDGMV%GMV(NPROMA,NFLEVG,NDIMGMV,NGPBLKS))
IF (LLP) WRITE(NULOUT,*) 'GMV ALLOCATED  ',SIZE(YDGMV%GMV),SHAPE(YDGMV%GMV)
ALLOCATE(YDGMV%GMVS(NPROMA,NDIMGMVS,NGPBLKS))
IF (LLP) WRITE(NULOUT,*) 'GMVS ALLOCATED ',SIZE(YDGMV%GMVS),SHAPE(YDGMV%GMVS)
IF (LLP) WRITE(NULOUT,*)

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GMV',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_GMV

!-------------------------------------------------------------------------

SUBROUTINE SETUP_GMV5(YDGEOMETRY,YDGMV5,YDDIMF)
USE YOMDIMF      , ONLY : TDIMF
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMMP0    , ONLY : NPRINTLEV
TYPE(GEOMETRY),INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)    ,INTENT(INOUT) :: YDGMV5
TYPE(TDIMF)   ,INTENT(INOUT) :: YDDIMF
LOGICAL :: LLP
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GMV5',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, NGPBLKS=>YDDIM%NGPBLKS, &
 & YT5=>YDGMV5%YT5, &
 & NFLEVG=>YDDIMV%NFLEVG)

LLP = NPRINTLEV >= 1.OR. LALLOPR
IF (LLP) WRITE(NULOUT,*)
IF (LLP) WRITE(NULOUT,*) 'SETUP_GMV5 PRINTS'

IF (LLP) WRITE(NULOUT,*)' SETUP_T5 : TRAJECTORY POINTERS (YT5)'
CALL SETUP_T0(YDDIMF,YT5)
IF( YRDYNA%LGRADSP ) YT5%NDIM=YT5%NDIM-2
IF (LLP) WRITE(NULOUT,*)'YT5%NDIM=',YT5%NDIM,' YT5%NDIMS=',YT5%NDIMS
IF (ALLOCATED(YDGMV5%GMV5))  DEALLOCATE(YDGMV5%GMV5)
IF (ALLOCATED(YDGMV5%GMV5S)) DEALLOCATE(YDGMV5%GMV5S)
ALLOCATE(YDGMV5%GMV5(NPROMA,NFLEVG,YT5%NDIM,NGPBLKS))
IF (LLP) WRITE(NULOUT,*) 'GMV5 ALLOCATED  ',SIZE(YDGMV5%GMV5),SHAPE(YDGMV5%GMV5)
ALLOCATE(YDGMV5%GMV5S(NPROMA,YT5%NDIMS,NGPBLKS))
IF (LLP) WRITE(NULOUT,*) 'GMV5S ALLOCATED ',SIZE(YDGMV5%GMV5S),SHAPE(YDGMV5%GMV5S)
IF (LLP) WRITE(NULOUT,*)

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GMV5',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_GMV5

!-------------------------------------------------------------------------

SUBROUTINE SETUP_T0(YDDIMF,YDT0)

USE YOMDIMF , ONLY : TDIMF
TYPE(TDIMF)   ,INTENT(INOUT):: YDDIMF
TYPE(TYPE_T0), INTENT(OUT)  :: YDT0

INTEGER(KIND=JPIM) :: IPT,IPTS
LOGICAL :: LLP
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T0',0,ZHOOK_HANDLE)
ASSOCIATE(LADER=>YDDIMF%LADER, LUVDER=>YDDIMF%LUVDER)
LLP = NPRINTLEV > 1.OR. LALLOPR
YDT0%MU    = NUNDEFLD
YDT0%MV    = NUNDEFLD
YDT0%MT    = NUNDEFLD
YDT0%MTL   = NUNDEFLD
YDT0%MTM   = NUNDEFLD
YDT0%MDIV  = NUNDEFLD
YDT0%MVOR  = NUNDEFLD
YDT0%MUL   = NUNDEFLD
YDT0%MVL   = NUNDEFLD
YDT0%MSPD  = NUNDEFLD
YDT0%MSPDL = NUNDEFLD
YDT0%MSPDM = NUNDEFLD
YDT0%MSVD  = NUNDEFLD
YDT0%MSVDL = NUNDEFLD
YDT0%MSVDM = NUNDEFLD
YDT0%MNHX  = NUNDEFLD
YDT0%MNHXL = NUNDEFLD
YDT0%MNHXM = NUNDEFLD
YDT0%MEDOT = NUNDEFLD
YDT0%MSGRTL= NUNDEFLD
YDT0%MSGRTM= NUNDEFLD

! Field pointers in GMVS
YDT0%MSP   = NUNDEFLD
YDT0%MSPL  = NUNDEFLD
YDT0%MSPM  = NUNDEFLD

IPT  = 0
IPTS = 0
IF(LADER) THEN
  IPT = IPT+1
  YDT0%MVOR = IPT
  IPT = IPT+1
  YDT0%MDIV = IPT
ENDIF
IPT = IPT+1
YDT0%MU = IPT
IPT = IPT+1
YDT0%MV = IPT
IPT = IPT+1
YDT0%MUL = IPT
IPT = IPT+1
YDT0%MVL = IPT
YDT0%NDIMUV=IPT

IF (.NOT.LRSHW) THEN
  IPT = IPT+1
  YDT0%MT = IPT
  IF(LNHDYN) THEN
    IPT = IPT+1
    YDT0%MSPD = IPT
    IPT = IPT+1
    YDT0%MSVD = IPT
    IF(YRDYNA%LNHX) THEN
      IPT = IPT+1
      YDT0%MNHX = IPT
    ENDIF
  ENDIF
  IF (LADER) THEN
    IPT = IPT+1
    YDT0%MTM = IPT
    IF(LNHDYN) THEN
      IPT = IPT+1
      YDT0%MSPDM = IPT
      IPT = IPT+1
      YDT0%MSVDM = IPT
      IF(YRDYNA%LNHXDER) THEN
        IPT = IPT+1
        YDT0%MNHXM = IPT
      ENDIF
    ENDIF
  ENDIF
  IF (LADER) THEN
    IPT = IPT+1
    YDT0%MTL = IPT
    IF(LNHDYN) THEN
      IPT = IPT+1
      YDT0%MSPDL = IPT
      IPT = IPT+1
      YDT0%MSVDL = IPT
      IF(YRDYNA%LNHXDER) THEN
        IPT = IPT+1
        YDT0%MNHXL = IPT
      ENDIF
    ENDIF
  ENDIF
ENDIF

IF (YRDYNA%LRUBC) THEN
  IPT = IPT+1
  YDT0%MEDOT = IPT
ENDIF

IPTS    = IPTS+1
YDT0%MSP  = IPTS
IF (LADER) THEN
  IPTS    = IPTS+1
  YDT0%MSPM = IPTS
  IPTS    = IPTS+1
  YDT0%MSPL = IPTS
ENDIF

IF( YRDYNA%LGRADSP ) THEN
  IPT = IPT+1
  YDT0%MSGRTL = IPT
  IPT = IPT+1
  YDT0%MSGRTM = IPT
ENDIF

YDT0%NDIM  = IPT
YDT0%NDIMS = IPTS

IF (LLP) WRITE(NULOUT,*)' MU=',YDT0%MU,'MV=',YDT0%MV,&
 & ' MT=',YDT0%MT,' MTL=',YDT0%MTL,' MTM=',YDT0%MTM  
IF (LLP) WRITE(NULOUT,*)' MEDOT=',YDT0%MEDOT
IF (LLP) WRITE(NULOUT,*)' MDIV=',YDT0%MDIV,' MVOR=',YDT0%MVOR
IF (LLP) WRITE(NULOUT,*)' MUL=',YDT0%MUL,' MVL=',YDT0%MVL,' MSPD=',YDT0%MSPD,&
 & ' MSPDL=',YDT0%MSPDL,' MSPDM=',YDT0%MSPDM
IF (LLP) WRITE(NULOUT,*)' MSGRTL=',YDT0%MSGRTL,' MSGRTM=',YDT0%MSGRTM
IF (LLP) WRITE(NULOUT,*)' MSVD=',YDT0%MSVD,' MSVDL=',YDT0%MSVDL,' MSVDM=',YDT0%MSVDM
IF (LLP) WRITE(NULOUT,*)' MNHX=',YDT0%MNHX,' MNHXL=',YDT0%MNHXL,' MNHXM=',YDT0%MNHXM
IF (LLP) WRITE(NULOUT,*)' MSP=',YDT0%MSP,' MSPL=',YDT0%MSPL,' MSPM=',YDT0%MSPM  

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T0',1,ZHOOK_HANDLE)

END SUBROUTINE SETUP_T0

!-------------------------------------------------------------------------

SUBROUTINE SETUP_T9(YDEPHY,YDDIMF,YDPHY,YDT0,YDT9)

USE YOEPHY , ONLY : TEPHY
USE YOMDIMF , ONLY : TDIMF
USE YOMPHY , ONLY : TPHY
TYPE(TDIMF)   ,INTENT(INOUT):: YDDIMF
TYPE(TEPHY)   ,INTENT(INOUT):: YDEPHY
TYPE(TPHY)    ,INTENT(INOUT):: YDPHY
TYPE(TYPE_T0), INTENT(IN)  :: YDT0
TYPE(TYPE_T9), INTENT(OUT) :: YDT9

INTEGER(KIND=JPIM) :: IPT,IPTS
LOGICAL :: LLP
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T9',0,ZHOOK_HANDLE)
ASSOCIATE(NFTHER=>YDDIMF%NFTHER, &
 & LEPHYS=>YDEPHY%LEPHYS, &
 & LMPHYS=>YDPHY%LMPHYS)
LLP = NPRINTLEV > 1.OR. LALLOPR
YDT9%MU = NUNDEFLD
YDT9%MV = NUNDEFLD
YDT9%MT = NUNDEFLD
YDT9%MTL = NUNDEFLD
YDT9%MTM = NUNDEFLD
YDT9%MEDOT = NUNDEFLD
YDT9%MUNL = NUNDEFLD
YDT9%MUNL_SI = NUNDEFLD
YDT9%MVNL = NUNDEFLD
YDT9%MVNL_SI = NUNDEFLD
YDT9%MTNL = NUNDEFLD
YDT9%MTNL_SI = NUNDEFLD
YDT9%MSPNL = NUNDEFLD
YDT9%MSPNL_SI = NUNDEFLD
YDT9%MVWVNL = NUNDEFLD
YDT9%MSVDNL_SI = NUNDEFLD
YDT9%MSPDNL = NUNDEFLD
YDT9%MSPDNL_SI = NUNDEFLD
YDT9%MDIV = NUNDEFLD
YDT9%MVOR = NUNDEFLD
YDT9%MUL = NUNDEFLD
YDT9%MVL = NUNDEFLD
YDT9%MSPD = NUNDEFLD
YDT9%MSPDL = NUNDEFLD
YDT9%MSPDM = NUNDEFLD
YDT9%MSVD = NUNDEFLD
YDT9%MSVDL = NUNDEFLD
YDT9%MSVDM = NUNDEFLD
YDT9%MNHX = NUNDEFLD
YDT9%MGW   = NUNDEFLD
YDT9%MNHY = NUNDEFLD
YDT9%MCUNL = NUNDEFLD
YDT9%MCVNL = NUNDEFLD
YDT9%MCTNL = NUNDEFLD
YDT9%MCSPNL = NUNDEFLD
YDT9%MCVWVNL = NUNDEFLD
YDT9%MCSPDNL = NUNDEFLD
YDT9%MCUPT = NUNDEFLD
YDT9%MCVPT = NUNDEFLD
YDT9%MCTPT = NUNDEFLD
YDT9%MCSPDPT = NUNDEFLD
YDT9%MCSVDPT = NUNDEFLD
YDT9%MDPHI = NUNDEFLD

YDT9%MCURHS = NUNDEFLD
YDT9%MCVRHS = NUNDEFLD
YDT9%MCTRHS = NUNDEFLD
YDT9%MCSPDRHS = NUNDEFLD
YDT9%MCSVDRHS = NUNDEFLD
YDT9%MCSPRHS = NUNDEFLD
YDT9%MNHXNL = NUNDEFLD
YDT9%MCNHXNL = NUNDEFLD
YDT9%MSGRTL= NUNDEFLD
YDT9%MSGRTM= NUNDEFLD

! Field pointers in GMVS
YDT9%MSP = NUNDEFLD
YDT9%MSPL = NUNDEFLD
YDT9%MSPM = NUNDEFLD
YDT9%MCSPPT = NUNDEFLD
YDT9%MDBBC = NUNDEFLD
YDT9%MGWS = NUNDEFLD
YDT9%MSPNL2 = NUNDEFLD
YDT9%MCSPNL2 = NUNDEFLD
YDT9%MPREHYDS = NUNDEFLD

IPT  = YDT0%NDIM
IPTS = YDT0%NDIMS

IF (NCONF /= 701 .AND. NCONF /= 903) THEN

  IF (LTWOTL) THEN

    ! * GMV:
    IPT    = IPT+1
    YDT9%MU = IPT
    IPT    = IPT+1
    YDT9%MV = IPT
    IF (LRSHW) THEN
      IPT    = IPT+1
      YDT9%MSPNL = IPT
    ELSE
      IPT    = IPT+1
      YDT9%MEDOT = IPT
      IF( .NOT.YRDYNA%LNESC )THEN
        IPT = IPT+1
        YDT9%MUNL = IPT
        IPT = IPT+1
        YDT9%MVNL = IPT
        IPT = IPT+1
        YDT9%MTNL =IPT
        IF (YRDYNA%LSLINL) THEN
          IPT = IPT+1
          YDT9%MUNL_SI = IPT
          IPT = IPT+1
          YDT9%MVNL_SI = IPT
          IPT = IPT+1
          YDT9%MTNL_SI = IPT
        ENDIF
        IPT = IPT+1
        YDT9%MSPNL = IPT
        IF (YRDYNA%LSLINLC2) THEN
          IPT = IPT+1
          YDT9%MSPNL_SI = IPT
        ENDIF
      ENDIF
      IF(YRDYNA%LPC_FULL)THEN
        IPT = IPT+1
        YDT9%MT = IPT
        IPT = IPT+1
        YDT9%MTL = IPT
        IPT = IPT+1
        YDT9%MTM = IPT
        IF(LNHDYN) THEN
          IPT = IPT+1
          YDT9%MSPD=IPT
          IPT = IPT+1
          YDT9%MSVD=IPT
          IPT = IPT+1
          YDT9%MSPDL=IPT
          IPT = IPT+1
          YDT9%MSVDL=IPT
          IPT = IPT+1
          YDT9%MSPDM=IPT
          IPT = IPT+1
          YDT9%MSVDM=IPT
          IF(YRDYNA%LGWADV)THEN
            IPT    = IPT+1
            YDT9%MGW= IPT
            IF(YRDYNA%NVDVAR==5)THEN
              IPT = IPT+1
              YDT9%MNHY=IPT
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      IF( YRDYNA%LPC_FULL )THEN
        IPT = IPT+1
        YDT9%MCUNL=IPT
        IPT = IPT+1
        YDT9%MCVNL=IPT
        IPT = IPT+1
        YDT9%MCTNL=IPT
        IPT = IPT+1
        YDT9%MCSPNL=IPT
        IF(LNHDYN) THEN
          IPT = IPT+1
          YDT9%MCVWVNL=IPT
        ENDIF
        IF(LNHEE) THEN
          IPT = IPT+1
          YDT9%MCSPDNL=IPT
        ENDIF
      ENDIF
      IF(YRDYNA%LPC_FULL.AND.(LMPHYS.OR.LEPHYS))THEN  
        IPT = IPT+1
        YDT9%MCUPT = IPT
        IPT = IPT+1
        YDT9%MCVPT = IPT
        IPT = IPT+1
        YDT9%MCTPT = IPT
        IF(LNHEE) THEN
          IPT = IPT+1
          YDT9%MCSPDPT = IPT
        ENDIF
        IF(LNHDYN) THEN
          IPT = IPT+1
          YDT9%MCSVDPT = IPT
        ENDIF
      ENDIF
      IF(LNHDYN) THEN
        IF( .NOT.YRDYNA%LNESC )THEN
          IPT = IPT+1
          YDT9%MVWVNL=IPT
          IF (LNHEE) THEN
            IPT = IPT+1
            YDT9%MSPDNL=IPT
          ENDIF
          IF (YRDYNA%LSLINL) THEN
            IPT = IPT+1
            YDT9%MSVDNL_SI=IPT
          ENDIF
          IF (LNHEE.AND.YRDYNA%LSLINL) THEN
            IPT = IPT+1
            YDT9%MSPDNL_SI=IPT
          ENDIF
        ENDIF
        IF (YRDYNA%NVDVAR==4 .OR. YRDYNA%NVDVAR==5) THEN
          IPT = IPT+1
          YDT9%MNHX=IPT
        ENDIF
      ENDIF
      IF(LNHDYN.AND.YRDYNA%LRDBBC) THEN
        IPT = IPT+1
        YDT9%MDPHI = IPT
      ENDIF
    ENDIF

    ! * GMVS:
    IF(YRDYNA%LPC_FULL.OR.LTENC)THEN  
      IPTS    = IPTS+1
      YDT9%MSP  = IPTS
      IPTS    = IPTS+1
      YDT9%MSPM = IPTS
      IPTS    = IPTS+1
      YDT9%MSPL = IPTS
    ENDIF
    IF(YRDYNA%LPC_FULL.AND.LMPHYS)THEN  
      IPTS    = IPTS+1
      YDT9%MCSPPT = IPTS
    ENDIF
    IF(LNHDYN.AND.YRDYNA%LRDBBC) THEN
      IPTS    = IPTS+1
      YDT9%MDBBC = IPTS
      IF(YRDYNA%LPC_FULL) THEN
        IPTS    = IPTS+1
        YDT9%MGWS = IPTS
      ENDIF
    ENDIF
    IF(LNHQE.AND.(.NOT.YRDYNA%LNHQE_SIHYD)) THEN
      IPTS    = IPTS+1
      YDT9%MPREHYDS = IPTS
    ENDIF

  ELSE

    ! LPC_FULL pointers only valid for Eulerian code only.
    ! LPC_FULL not yet coded for SL3TL.

    ! * GMV:
    IPT   = IPT+1
    YDT9%MDIV = IPT
    IPT   = IPT+1
    YDT9%MU = IPT
    IPT   = IPT+1
    YDT9%MV = IPT
    IF(NFTHER > 0) THEN
      IPT   = IPT+1
      YDT9%MT = IPT
      IPT   = IPT+1
      YDT9%MTL = IPT
      IPT   = IPT+1
      YDT9%MTM = IPT
    ENDIF
    IF(LNHDYN) THEN
      IPT   = IPT+1
      YDT9%MSPD = IPT
      IPT   = IPT+1
      YDT9%MSVD = IPT
      IPT   = IPT+1
      YDT9%MSPDL = IPT
      IPT   = IPT+1
      YDT9%MSVDL = IPT
      IPT   = IPT+1
      YDT9%MSPDM = IPT
      IPT   = IPT+1
      YDT9%MSVDM = IPT
      IF (YRDYNA%NVDVAR == 4 .OR. YRDYNA%NVDVAR == 5) THEN
        IPT   = IPT+1
        YDT9%MNHX= IPT
        IF(YRDYNA%LPC_FULL)THEN
          IPT = IPT+1
          YDT9%MNHXNL=IPT
          IPT = IPT+1
          YDT9%MCNHXNL=IPT
        ENDIF
      ENDIF
    ENDIF
    IF( YRDYNA%LPC_FULL )THEN
      IPT = IPT+1
      YDT9%MUNL = IPT
      IPT = IPT+1
      YDT9%MVNL = IPT
      IPT = IPT+1
      YDT9%MTNL =IPT
      IF( LNHDYN )THEN
        IPT = IPT+1
        YDT9%MVWVNL=IPT
      ENDIF
      IF( LNHEE )THEN
        IPT = IPT+1
        YDT9%MSPDNL=IPT
      ENDIF
      IPT = IPT+1
      YDT9%MCUNL=IPT
      IPT = IPT+1
      YDT9%MCVNL=IPT
      IPT = IPT+1
      YDT9%MCTNL=IPT
      IF(LNHDYN) THEN
        IPT = IPT+1
        YDT9%MCVWVNL=IPT
      ENDIF
      IF(LNHEE) THEN
        IPT = IPT+1
        YDT9%MCSPDNL=IPT
      ENDIF
      IPT = IPT+1
      YDT9%MCURHS=IPT
      IPT = IPT+1
      YDT9%MCVRHS=IPT
      IPT = IPT+1
      YDT9%MCTRHS=IPT
      IF(LNHDYN) THEN
        IPT = IPT+1
        YDT9%MCSVDRHS=IPT
      ENDIF
      IF(LNHEE) THEN
        IPT = IPT+1
        YDT9%MCSPDRHS=IPT
      ENDIF
    ENDIF
  
    ! * GMVS:
    IPTS    = IPTS+1
    YDT9%MSP  = IPTS
    IPTS    = IPTS+1
    YDT9%MSPM = IPTS
    IPTS    = IPTS+1
    YDT9%MSPL = IPTS
    IF( YRDYNA%LPC_FULL )THEN
      ! ky: MSPNL,MSPNL_SI are in GMVS in EUL but in GMV in SL.
      ! mh : changed MSPNL and MCSPNL to MSPNL2 and MCSPNL2 to avoid confusion
      IPTS    = IPTS+1
      YDT9%MSPNL2  = IPTS  
      IPTS    = IPTS+1
      YDT9%MCSPNL2  = IPTS           
      IPTS    = IPTS+1
      YDT9%MCSPRHS  = IPTS           
    ENDIF  
    IF(LNHQE.AND.(.NOT.YRDYNA%LNHQE_SIHYD)) THEN
      IPTS    = IPTS+1
      YDT9%MPREHYDS = IPTS
    ENDIF

  ENDIF

ENDIF

IF( YRDYNA%LGRADSP ) THEN
  IPT = IPT+1
  YDT9%MSGRTL = IPT
  IPT = IPT+1
  YDT9%MSGRTM = IPT
ENDIF

YDT9%NDIM  = IPT -YDT0%NDIM
YDT9%NDIMS = IPTS-YDT0%NDIMS

IF (LLP) WRITE(NULOUT,*)' * SETUP_T9 : t-dt POINTERS (YT9)'
IF (LLP) WRITE(NULOUT,*)' MU=',YDT9%MU,'MV=',YDT9%MV,&
 & ' MT=',YDT9%MT,' MTL=',YDT9%MTL,' MTM=',YDT9%MTM  
IF (LLP) WRITE(NULOUT,*)' MEDOT=',YDT9%MEDOT,' MUNL=',YDT9%MUNL,&
 & ' MVNL=',YDT9%MVNL,' MTNL=',YDT9%MTNL  
IF (LLP) WRITE(NULOUT,*)' MSPNL=',YDT9%MSPNL,' MVWVNL=',YDT9%MVWVNL  
IF (LLP) WRITE(NULOUT,*)' MSPNL2=',YDT9%MSPNL2
IF (LLP) WRITE(NULOUT,*)' MSPDNL=',YDT9%MSPDNL,&
 & ' MDIV=',YDT9%MDIV,' MVOR=',YDT9%MVOR  
IF (LLP) WRITE(NULOUT,*)' MUL=',YDT9%MUL,' MVL=',YDT9%MVL,' MSPD=',YDT9%MSPD,&
 & ' MSPDL=',YDT9%MSPDL,' MSPDM=',YDT9%MSPDM  
IF (LLP) WRITE(NULOUT,*)' MSVD=',YDT9%MSVD,' MSVDL=',YDT9%MSVDL,' MSVDM=',YDT9%MSVDM,&
 & ' MNHX=',YDT9%MNHX,' MNHY=',YDT9%MNHY  
IF (LLP) WRITE(NULOUT,*)' MCUNL=',YDT9%MCUNL,' MCVNL=',YDT9%MCVNL,' MCTNL=',YDT9%MCTNL,&
 & ' MCSPNL=',YDT9%MCSPNL  
IF (LLP) WRITE(NULOUT,*)' MCSPNL2=',YDT9%MCSPNL2
IF (LLP) WRITE(NULOUT,*)' MCUPT=',YDT9%MCUPT,' MCVPT=',YDT9%MCVPT,' MCTPT=',YDT9%MCTPT
IF (LLP) WRITE(NULOUT,*)' MCSPDPT=',YDT9%MCSPDPT,' MCSVDPT=',YDT9%MCSVDPT
IF (LLP) WRITE(NULOUT,*)' MCVWVNL=',YDT9%MCVWVNL
IF (LLP) WRITE(NULOUT,*)' MCSPDNL=',YDT9%MCSPDNL
IF (LLP) WRITE(NULOUT,*)' MSP=',YDT9%MSP,' MSPL=',YDT9%MSPL,' MSPM=',YDT9%MSPM,&
 & ' MCSPPT=',YDT9%MCSPPT
IF (LLP) WRITE(NULOUT,*)' MDPHI=',YDT9%MDPHI
IF (LLP) WRITE(NULOUT,*)' MDBBC=',YDT9%MDBBC,' MGWS=',YDT9%MGWS
WRITE(NULOUT,*)' MPREHYDS=',YDT9%MPREHYDS

IF (LLP) WRITE(NULOUT,*)' MNHXNL=',YDT9%MNHXNL,' MCNHXNL=',YDT9%MCNHXNL

! JV 3TL ICI scheme
IF (LLP) WRITE(NULOUT,*)' ------ arrays for 3TL ICI scheme (iterative scheme) ----'
IF (LLP) WRITE(NULOUT,*)' MCURHS=',YDT9%MCURHS,' MCVRHS=',YDT9%MCVRHS
IF (LLP) WRITE(NULOUT,*)' MCTRHS =',YDT9%MCTRHS
IF (LLP) WRITE(NULOUT,*)' MCSPRHS =',YDT9%MCSPRHS
IF (LLP) WRITE(NULOUT,*)' MCSPDRHS =',YDT9%MCSPDRHS
IF (LLP) WRITE(NULOUT,*)' MCSVDRHS =',YDT9%MCSVDRHS
IF (LLP) WRITE(NULOUT,*)' ---------------------------------------------------------'
IF (LLP) WRITE(NULOUT,*)' ------ arrays for 2TL LGWADV PC_FULL scheme (iterative scheme) ----'
IF (LLP) WRITE(NULOUT,*)' MGW=',YDT9%MGW

IF (LLP) WRITE(NULOUT,*)' Pointers for separate linear terms '
IF (LLP) WRITE(NULOUT,*)' MUNL_SI   =',YDT9%MUNL_SI
IF (LLP) WRITE(NULOUT,*)' MVNL_SI   =',YDT9%MVNL_SI
IF (LLP) WRITE(NULOUT,*)' MTNL_SI   =',YDT9%MTNL_SI
IF (LLP) WRITE(NULOUT,*)' MSPNL_SI  =',YDT9%MSPNL_SI
IF (LLP) WRITE(NULOUT,*)' MSVDNL_SI =',YDT9%MSVDNL_SI
IF (LLP) WRITE(NULOUT,*)' MSPDNL_SI =',YDT9%MSPDNL_SI

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T9',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_T9

!-------------------------------------------------------------------------

SUBROUTINE SETUP_T1(YDDIMF,YDT1)

USE YOMDIMF , ONLY : TDIMF
TYPE(TDIMF)   ,INTENT(INOUT):: YDDIMF
TYPE(TYPE_T1), INTENT(OUT) :: YDT1

INTEGER(KIND=JPIM) :: IPT,IPTS
LOGICAL :: LLP
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

! Field pointers in GMVT1
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T1',0,ZHOOK_HANDLE)
ASSOCIATE(NFTHER=>YDDIMF%NFTHER)
LLP = NPRINTLEV > 1.OR. LALLOPR
YDT1%MU = NUNDEFLD
YDT1%MV = NUNDEFLD
YDT1%MT = NUNDEFLD
YDT1%MSPD = NUNDEFLD
YDT1%MSVD = NUNDEFLD
YDT1%MVOR = NUNDEFLD
YDT1%MDIV = NUNDEFLD
YDT1%MNHX = NUNDEFLD
! Field pointers in GMVT1S
YDT1%MSP = NUNDEFLD

IPT  = 0
IPTS = 0

! Field pointers in GMVT1
IPT = IPT+1
YDT1%MU = IPT
IPT = IPT+1
YDT1%MV = IPT
IF (NFTHER > 0) THEN
  IPT = IPT+1
  YDT1%MT = IPT
ENDIF
IF(LNHDYN) THEN
  IPT = IPT+1
  YDT1%MSPD = IPT
  IPT = IPT+1
  YDT1%MSVD = IPT
  IF(YRDYNA%LNHX) THEN
    IPT = IPT+1
    YDT1%MNHX = IPT
  ENDIF
ENDIF
! MVOR and MDIV are equivalanced to MU and MV for use in gridpoint Jb
! calculations and a special option of the Direct Transforms (Config G)
YDT1%MVOR = YDT1%MU
YDT1%MDIV = YDT1%MV
! Field pointers in GMVT1S
IPTS    = IPTS+1
YDT1%MSP  = IPTS
  
YDT1%NDIM  = IPT
YDT1%NDIMS = IPTS

IF (LLP) WRITE(NULOUT,*)' * SETUP_T1 : t+dt POINTERS (YT1)'
IF (LLP) WRITE(NULOUT,*)' MU=',YDT1%MU,'MV=',YDT1%MV,' MT=',YDT1%MT
IF (LLP) WRITE(NULOUT,*)' MSPD=',YDT1%MSPD,' MSVD=',YDT1%MSVD,' MSP=',YDT1%MSP
IF (LLP) WRITE(NULOUT,*)' MVOR=',YDT1%MVOR,' MDIV=',YDT1%MDIV,' MNHX=',YDT1%MNHX
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_T1',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_T1

!-------------------------------------------------------------------------

SUBROUTINE SETUP_PH9(YDT0,YDT9,YDPH9)

! Field pointers in GMV
TYPE(TYPE_T0) , INTENT(IN)  :: YDT0
TYPE(TYPE_T9) , INTENT(IN)  :: YDT9
TYPE(TYPE_PH9), INTENT(OUT) :: YDPH9

REAL(KIND=JPRB) :: ZHOOK_HANDLE
LOGICAL :: LLP
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9',0,ZHOOK_HANDLE)

LLP = NPRINTLEV > 1.OR. LALLOPR

YDPH9%MU = NUNDEFLD
YDPH9%MV = NUNDEFLD
YDPH9%MT = NUNDEFLD

! Field pointers in GMVS
YDPH9%MSP = NUNDEFLD

IF(LTWOTL) THEN
! Field pointers in GMV
  YDPH9%MU = YDT0%MU
  YDPH9%MV = YDT0%MV
  YDPH9%MT = YDT0%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT0%MSP
ELSE
! Field pointers in GMV
  YDPH9%MU = YDT9%MU
  YDPH9%MV = YDT9%MV
  YDPH9%MT = YDT9%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT9%MSP
ENDIF  

IF (LLP) WRITE(NULOUT,*)' SETUP_PH9 : PH9 POINTERS (YPH9)'
IF (LLP) WRITE(NULOUT,*)' MU=',YDPH9%MU,'MV=',YDPH9%MV,' MT=',YDPH9%MT,' MSP=',YDPH9%MSP

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_PH9

!-------------------------------------------------------------------------

SUBROUTINE SETUP_GP(YDGP)

! Field pointers in local SL buffer ZGMV9 in LAPINEB (SL)

TYPE(TYPE_GP) , INTENT(INOUT) :: YDGP

LOGICAL :: LLP
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GP',0,ZHOOK_HANDLE)

LLP = NPRINTLEV > 1.OR. LALLOPR
YDGP%MU=1
YDGP%MV=2
YDGP%MT=3
YDGP%NDIM=3
IF(LNHEE) THEN
  YDGP%MSPD=4
  YDGP%MSVD=5
  YDGP%NDIM=YDGP%NDIM+2
  IF(YRDYNA%LNHX) THEN
    YDGP%MNHX=6
    YDGP%NDIM=YDGP%NDIM+1
  ELSE
    YDGP%MNHX=1
  ENDIF
ELSEIF(LNHQE) THEN
  YDGP%MSVD=4
  YDGP%MSPD=1
  YDGP%NDIM=YDGP%NDIM+1
  IF(YRDYNA%LNHX) THEN
    YDGP%MNHX=5
    YDGP%NDIM=YDGP%NDIM+1
  ELSE
    YDGP%MNHX=1
  ENDIF
ELSE
  YDGP%MSPD=1
  YDGP%MSVD=1
  YDGP%MNHX=1
ENDIF

IF (LLP) WRITE(NULOUT,*)' SETUP_GP : ZGMV9 pointers for SL (LAPINEB)'
IF (LLP) WRITE(NULOUT,*)' MU=',YDGP%MU,' MV=',YDGP%MV,' MT=',YDGP%MT
IF (LLP) WRITE(NULOUT,*)' MSPD=',YDGP%MSPD,' MSVD=',YDGP%MSVD
IF (LLP) WRITE(NULOUT,*)' NDIM =',YDGP%NDIM

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_GP',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_GP

!-------------------------------------------------------------------------

SUBROUTINE SETUP_PH9_PRED(YDT0,YDT9,YDPH9)

! Field pointers in GMV
TYPE(TYPE_T0) , INTENT(IN)  :: YDT0
TYPE(TYPE_T9) , INTENT(IN)  :: YDT9
TYPE(TYPE_PH9), INTENT(OUT) :: YDPH9

REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9_PRED',0,ZHOOK_HANDLE)

YDPH9%MU = NUNDEFLD
YDPH9%MV = NUNDEFLD
YDPH9%MT = NUNDEFLD

! Field pointers in GMVS
YDPH9%MSP = NUNDEFLD

IF(LTWOTL) THEN
! Field pointers in GMV
  YDPH9%MU = YDT0%MU
  YDPH9%MV = YDT0%MV
  YDPH9%MT = YDT0%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT0%MSP
ELSE
! Field pointers in GMV
  YDPH9%MU = YDT9%MU
  YDPH9%MV = YDT9%MV
  YDPH9%MT = YDT9%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT9%MSP
ENDIF

IF (NPRINTLEV >=2) THEN
  WRITE(NULOUT,*)' MODIFICATION OF PH9 POINTERS FOR PREDICTOR STEP'
  WRITE(NULOUT,*)' MU=',YDPH9%MU,'MV=',YDPH9%MV,' MT=',YDPH9%MT,' MSP=',YDPH9%MSP
ENDIF

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9_PRED',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_PH9_PRED

!-------------------------------------------------------------------------

SUBROUTINE SETUP_PH9_CORR(YDT0,YDT9,YDPH9)

! Field pointers in GMV
TYPE(TYPE_T0) , INTENT(IN)  :: YDT0
TYPE(TYPE_T9) , INTENT(IN)  :: YDT9
TYPE(TYPE_PH9), INTENT(OUT) :: YDPH9

REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9_CORR',0,ZHOOK_HANDLE)

YDPH9%MU = NUNDEFLD
YDPH9%MV = NUNDEFLD
YDPH9%MT = NUNDEFLD

! Field pointers in GMVS
YDPH9%MSP = NUNDEFLD

IF(LTWOTL) THEN
! Field pointers in GMV
  YDPH9%MU = YDT9%MU
  YDPH9%MV = YDT9%MV
  YDPH9%MT = YDT9%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT9%MSP
ELSE
! These pointers used only for ECMWF Physics - to be done later for leap-frog scheme.
! Field pointers in GMV
  YDPH9%MU = YDT9%MU
  YDPH9%MV = YDT9%MV
  YDPH9%MT = YDT9%MT
! Field pointers in GMVS
  YDPH9%MSP = YDT9%MSP
ENDIF

IF (NPRINTLEV >=2) THEN
  WRITE(NULOUT,*)' MODIFICATION OF PH9 POINTERS FOR CORRECTOR STEP'
  WRITE(NULOUT,*)' MU=',YDPH9%MU,'MV=',YDPH9%MV,' MT=',YDPH9%MT,' MSP=',YDPH9%MSP
ENDIF

IF (LHOOK) CALL DR_HOOK('GMV_SUBS_MOD:SETUP_PH9_CORR',1,ZHOOK_HANDLE)
END SUBROUTINE SETUP_PH9_CORR

!-------------------------------------------------------------------------

END MODULE GMV_SUBS_MOD
