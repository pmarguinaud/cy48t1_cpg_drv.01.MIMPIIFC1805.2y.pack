MODULE YEMLBC_FIELDS 

! Purpose :
! -------
!    Forcing a LAM model by another model: part 0C
!    - forcing by lateral boundary conditions
!    - pressure tendency coupling
!    - spectral nudging

! Interface :
! ---------
!    Empty.

! External :
! --------
!    None.

! Method :
! ------
!    See Documentation.

! Reference :
! ---------

! Author :
! ------
!    Bogdan Bochenek - part from ELBC0B
! Original : Sep 2014

! Modifications :
! -------------
! H. Dhouioui (Sep 2017) Renamed from elbc0c_mod.F90
!-----------------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM, JPRB
USE YOMHOOK   , ONLY : LHOOK, DR_HOOK
USE YOMCT0    , ONLY : LNHDYN, LALLOPR, NCONF
USE YOMDYNA   , ONLY : YRDYNA
USE YOMLUN    , ONLY : NULOUT
USE YOMGMV    , ONLY : TGMV
USE YEMLBC_INIT, ONLY : LTENC, LALLTC, LQCPL, LCCPL, LESPCPL, LSPTENC, LUNBC
USE YEMLBC_GEO , ONLY : TELBC_GEO 

IMPLICIT NONE
SAVE

!=============================================================================

!      1.    DECLARATIONS
!            ------------

! Structure for GMVS coupled fields in LTENC option.
TYPE TGMVSTENC
INTEGER(KIND=JPIM) :: MSP        ! surface pressure variable
INTEGER(KIND=JPIM) :: MSPL       ! zonal component of grad(surface pressure variable)
INTEGER(KIND=JPIM) :: MSPM       ! meridian component of grad(surface pressure variable)
INTEGER(KIND=JPIM) :: NDIM       ! number of coupled fields (includes derivatives)
INTEGER(KIND=JPIM) :: NDIMT      ! number of temporally interpolated fields (includes derivatives)
END TYPE TGMVSTENC

TYPE TGMVCPL
! coupled GMV
INTEGER(KIND=JPIM) :: MU         ! U-wind
INTEGER(KIND=JPIM) :: MV         ! V-wind
INTEGER(KIND=JPIM) :: MT         ! temperature
INTEGER(KIND=JPIM) :: MSPD       ! pressure departure variable
INTEGER(KIND=JPIM) :: MSVD       ! vertical divergence variable
INTEGER(KIND=JPIM) :: MNHX       ! NHX term
! derivatives required for linear terms calculation (in ESEIMPLS)
INTEGER(KIND=JPIM) :: MDIV       ! horizontal divergence
INTEGER(KIND=JPIM) :: MTL        ! zonal component of grad(temperature)
INTEGER(KIND=JPIM) :: MTM        ! meridian component of grad(temperature)
INTEGER(KIND=JPIM) :: MSPDL      ! zonal component of grad(pressure departure variable)
INTEGER(KIND=JPIM) :: MSPDM      ! meridian component of grad(pressure departure variable)
INTEGER(KIND=JPIM) :: NDIM       ! number of coupled fields (does not include derivatives)
INTEGER(KIND=JPIM) :: NDIMT      ! number of temporally interpolated fields (includes derivatives)
END TYPE TGMVCPL

TYPE TGMVSCPL
INTEGER(KIND=JPIM) :: MSP        ! surface pressure variable
INTEGER(KIND=JPIM) :: MSPL       ! zonal component of grad(surface pressure variable)
INTEGER(KIND=JPIM) :: MSPM       ! meridian component of grad(surface pressure variable)
INTEGER(KIND=JPIM) :: NDIM       ! number of coupled fields (does not include derivatives)
INTEGER(KIND=JPIM) :: NDIMT      ! number of temporally interpolated fields (includes derivatives)
END TYPE TGMVSCPL

TYPE :: TELBC_FIELDS 

!      1.1   Number of coupled fields, structures for coupled fields.

! YYTGMVSTENC  : contains pointers and number of coupled fields for GMVS in LTENC option
! YYTGMVCPL    : contains pointers and number of coupled fields for GMV
! YYTGMVSCPL   : contains pointers and number of coupled fields for GMVS
! NDIMCPL      : number of GFL fields with true LCOUPLING attribute.
! NGALEF       : total number of coupled fields.

TYPE(TGMVSTENC) :: YYTGMVSTENC
TYPE(TGMVCPL) :: YYTGMVCPL
TYPE(TGMVSCPL) :: YYTGMVSCPL
INTEGER(KIND=JPIM) :: NDIMCPL
INTEGER(KIND=JPIM) :: NGALEF

!      1.2   Other variables for grid-point coupling.

! GMVCPL, GMVSCPL, GFLCPL: buffers containing the LBC for GMV, GMVS, GFL
! GMVUCPL, GMVSUCPL, GFLUCPL: buffers containing the UBC for GMV, GMVS, GFL
! GMVSTENC       : cf. GMVSCPL but for LTENC.
! MGMV0          : index for "GMV" memory transfers between GMV and coupling buffers.
! MGMV1          : index for "GMV" memory transfers between GMVT1 and coupling buffers.
! MGMVS0         : index for "GMVS" memory transfers between GMVS and coupling buffers.
! MGMVS1         : index for "GMVS" memory transfers between GMVT1S and coupling buffers.
! CCFIELD_GMV, CCFIELD_GMVS, CCFIELD_GFL: fields names for EWRLSGRAD.

REAL(KIND=JPRB),ALLOCATABLE:: GMVCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSCPL(:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GFLCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVUCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSUCPL(:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GFLUCPL(:,:,:,:)
REAL(KIND=JPRB),ALLOCATABLE:: GMVSTENC(:,:,:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMV0(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMV1(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMVS0(:)
INTEGER(KIND=JPIM),ALLOCATABLE:: MGMVS1(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GMV(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GMVS(:)
CHARACTER(LEN=12),ALLOCATABLE:: CCFIELD_GFL(:)

!      1.3   Other variables for spectral nudging.

! GT3SPBUF   : buffer for spectral boundary fields
! NOFFGT3BSP : Supposed to be half the size of GT3SPBUF (which contains 2 sets of fields)

REAL(KIND=JPRB),ALLOCATABLE :: GT3SPBUF(:)
INTEGER(KIND=JPIM) :: NOFFGT3BSP

END TYPE TELBC_FIELDS 


!=============================================================================

CONTAINS

!      2.    SET-UP

SUBROUTINE SUELBC_FIELDS(YDELBC_FIELDS,YDGEOMETRY,YDGMV,YGFL,KNFD2D,KNS3D)

!--------------------------------------------------------------------------
! Sets-up part 0C of forcing a LAM model by another model
!--------------------------------------------------------------------------

!--------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOM_YGFL     , ONLY : TYPE_GFLD
TYPE(TELBC_FIELDS)     , INTENT(INOUT), POINTER :: YDELBC_FIELDS 
TYPE(GEOMETRY)    , INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)        , INTENT(INOUT) :: YDGMV
TYPE(TYPE_GFLD)   , INTENT(INOUT) :: YGFL
INTEGER(KIND=JPIM), INTENT(IN)    :: KNFD2D
INTEGER(KIND=JPIM), INTENT(IN)    :: KNS3D
REAL(KIND=JPRB) :: ZHOOK_HANDLE
INTEGER(KIND=JPIM) :: IWS_TENC, ICPL, JGFL, IW, IW_TENC

!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:SUELBC_FIELDS',0,ZHOOK_HANDLE)
!--------------------------------------------------------------------------

!      Part A: calculations and allocations.

! * Calculation of YYGMVSTENC, YYTGMVCPL, YYGMVSCPL, NDIMCPL, NGALEF:

ALLOCATE(YDELBC_FIELDS)

! YYGMVSTENC:
IF (LTENC) THEN
  YDELBC_FIELDS%YYTGMVSTENC%MSP=1
  YDELBC_FIELDS%YYTGMVSTENC%MSPL=2
  YDELBC_FIELDS%YYTGMVSTENC%MSPM=3
  YDELBC_FIELDS%YYTGMVSTENC%NDIM=3
  YDELBC_FIELDS%YYTGMVSTENC%NDIMT=3
ELSE
  YDELBC_FIELDS%YYTGMVSTENC%MSP=1
  YDELBC_FIELDS%YYTGMVSTENC%MSPL=1
  YDELBC_FIELDS%YYTGMVSTENC%MSPM=1
  YDELBC_FIELDS%YYTGMVSTENC%NDIM=1
  YDELBC_FIELDS%YYTGMVSTENC%NDIMT=1
ENDIF

! YYTGMVCPL:
IF (NCONF == 701) THEN
  ! derivatives are useless because ESEIMPLS is not called.
  IF (LNHDYN.AND.YRDYNA%LNHX) THEN
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 4
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 5
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 6
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 6
    YDELBC_FIELDS%YYTGMVCPL%NDIMT= 6
  ELSEIF (LNHDYN.AND.(.NOT.YRDYNA%LNHX)) THEN
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 4
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 5
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 5
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 5
    YDELBC_FIELDS%YYTGMVCPL%NDIMT= 5
  ELSE
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 3
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 3
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 3
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 3
    YDELBC_FIELDS%YYTGMVCPL%NDIMT= 3
  ENDIF
ELSE
! derivatives are useful because ESEIMPLS is called.
  IF (LNHDYN.AND.YRDYNA%LNHX) THEN
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 4
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 5
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 6
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 6
    YDELBC_FIELDS%YYTGMVCPL%MDIV = 7
    YDELBC_FIELDS%YYTGMVCPL%MTL  = 8
    YDELBC_FIELDS%YYTGMVCPL%MTM  = 9
    YDELBC_FIELDS%YYTGMVCPL%MSPDL=10
    YDELBC_FIELDS%YYTGMVCPL%MSPDM=11
    YDELBC_FIELDS%YYTGMVCPL%NDIMT=11
  ELSEIF (LNHDYN.AND.(.NOT.YRDYNA%LNHX)) THEN
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 4
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 5
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 5
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 5
    YDELBC_FIELDS%YYTGMVCPL%MDIV = 6
    YDELBC_FIELDS%YYTGMVCPL%MTL  = 7
    YDELBC_FIELDS%YYTGMVCPL%MTM  = 8
    YDELBC_FIELDS%YYTGMVCPL%MSPDL= 9
    YDELBC_FIELDS%YYTGMVCPL%MSPDM=10
    YDELBC_FIELDS%YYTGMVCPL%NDIMT=10
  ELSE
    YDELBC_FIELDS%YYTGMVCPL%MU   = 1
    YDELBC_FIELDS%YYTGMVCPL%MV   = 2
    YDELBC_FIELDS%YYTGMVCPL%MT   = 3
    YDELBC_FIELDS%YYTGMVCPL%MSPD = 3
    YDELBC_FIELDS%YYTGMVCPL%MSVD = 3
    YDELBC_FIELDS%YYTGMVCPL%MNHX = 3
    YDELBC_FIELDS%YYTGMVCPL%NDIM = 3
    YDELBC_FIELDS%YYTGMVCPL%MDIV = 4
    YDELBC_FIELDS%YYTGMVCPL%MTL  = 5
    YDELBC_FIELDS%YYTGMVCPL%MTM  = 6
    YDELBC_FIELDS%YYTGMVCPL%MSPDL= 6
    YDELBC_FIELDS%YYTGMVCPL%MSPDM= 6
    YDELBC_FIELDS%YYTGMVCPL%NDIMT= 6
  ENDIF
ENDIF

! YYGMVSCPL:
IF (NCONF == 701) THEN
  ! derivatives are useless because ESEIMPLS is not called.
  YDELBC_FIELDS%YYTGMVSCPL%MSP=1
  YDELBC_FIELDS%YYTGMVSCPL%NDIM=1
  YDELBC_FIELDS%YYTGMVSCPL%NDIMT=1
ELSE
  ! derivatives are useful because ESEIMPLS is called.
  YDELBC_FIELDS%YYTGMVSCPL%MSP=1
  YDELBC_FIELDS%YYTGMVSCPL%MSPL=2
  YDELBC_FIELDS%YYTGMVSCPL%MSPM=3
  YDELBC_FIELDS%YYTGMVSCPL%NDIM=1
  YDELBC_FIELDS%YYTGMVSCPL%NDIMT=3
ENDIF

! YDELBC_FIELDS%NDIMCPL:
ICPL=0
DO JGFL=1,YGFL%NUMFLDS
  IF (YGFL%YCOMP(JGFL)%NCOUPLING /= 0) THEN
    ICPL=ICPL+1
  ENDIF
ENDDO
YDELBC_FIELDS%NDIMCPL=ICPL

! YDELBC_FIELDS%NGALEF:
YDELBC_FIELDS%NGALEF=YDELBC_FIELDS%YYTGMVCPL%NDIM+YDELBC_FIELDS%YYTGMVSCPL%NDIM+YDELBC_FIELDS%NDIMCPL

! * Allocation of GMVCPL, GMVSCPL, GFLCPL, GMVSTENC.

IF (LCCPL) THEN
  IW=4
  IW_TENC=4
ELSEIF (LQCPL) THEN
  IW=3
  IW_TENC=3
ELSEIF (LTENC.AND.LALLTC) THEN
  IW=3
  IW_TENC=3
ELSE
  IW=2
  IW_TENC=2
ENDIF

ALLOCATE (YDELBC_FIELDS%GMVCPL(YDGEOMETRY%YRELBC_GEO%NEDLST,YDGEOMETRY%YRDIMV%NFLEVG,YDELBC_FIELDS%YYTGMVCPL%NDIMT,IW))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GMVCPL   ',SIZE(YDELBC_FIELDS%GMVCPL   ),SHAPE(YDELBC_FIELDS%GMVCPL   )
ALLOCATE (YDELBC_FIELDS%GMVSCPL(YDGEOMETRY%YRELBC_GEO%NEDLST,YDELBC_FIELDS%YYTGMVSCPL%NDIMT,IW))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GMVSCPL  ',SIZE(YDELBC_FIELDS%GMVSCPL  ),SHAPE(YDELBC_FIELDS%GMVSCPL  )
ALLOCATE (YDELBC_FIELDS%GFLCPL(YDGEOMETRY%YRELBC_GEO%NEDLST,YDGEOMETRY%YRDIMV%NFLEVG,YDELBC_FIELDS%NDIMCPL,IW))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%GFLCPL   ',SIZE(YDELBC_FIELDS%GFLCPL   ),SHAPE(YDELBC_FIELDS%GFLCPL   )
IF(LUNBC) THEN
  ALLOCATE (YDELBC_FIELDS%GMVUCPL(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG,YDELBC_FIELDS%YYTGMVCPL%NDIMT,IW))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GMVUCPL   ',SIZE(YDELBC_FIELDS%GMVUCPL   ),SHAPE(YDELBC_FIELDS%GMVUCPL   )
  ALLOCATE (YDELBC_FIELDS%GMVSUCPL(YDGEOMETRY%YRGEM%NGPTOT,YDELBC_FIELDS%YYTGMVSCPL%NDIMT,IW))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GMVSUCPL  ',SIZE(YDELBC_FIELDS%GMVSUCPL  ),SHAPE(YDELBC_FIELDS%GMVSUCPL  )
  ALLOCATE (YDELBC_FIELDS%GFLUCPL(YDGEOMETRY%YRGEM%NGPTOT,YDGEOMETRY%YRDIMV%NFLEVG,YDELBC_FIELDS%NDIMCPL,IW))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
   & 'YDELBC_FIELDS%GFULCPL   ',SIZE(YDELBC_FIELDS%GFLUCPL   ),SHAPE(YDELBC_FIELDS%GFLUCPL   )
ENDIF

ALLOCATE (YDELBC_FIELDS%GMVSTENC(YDGEOMETRY%YRELBC_GEO%NEDLST,YDELBC_FIELDS%YYTGMVSTENC%NDIMT,IW_TENC))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
& 'YDELBC_FIELDS%GMVSTENC ',SIZE(YDELBC_FIELDS%GMVSTENC ),SHAPE(YDELBC_FIELDS%GMVSTENC )

! * Allocation and computation of MGMV0, MGMV1, MGMVS0, MGMVS1:

ALLOCATE (YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%NDIMT))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMV0    ',SIZE(YDELBC_FIELDS%MGMV0    ),SHAPE(YDELBC_FIELDS%MGMV0    )
YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MU)=YDGMV%YT0%MU
YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MV)=YDGMV%YT0%MV
YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MT)=YDGMV%YT0%MT
IF (LNHDYN) YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MSPD)=YDGMV%YT0%MSPD
IF (LNHDYN) YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MSVD)=YDGMV%YT0%MSVD
IF (LNHDYN.AND.YRDYNA%LNHX) YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MNHX)=YDGMV%YT0%MNHX
IF (NCONF /= 701) THEN
  YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MDIV)=YDGMV%YT0%MDIV
  YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MTL)=YDGMV%YT0%MTL
  YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MTM)=YDGMV%YT0%MTM
  IF (LNHDYN) YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MSPDL)=YDGMV%YT0%MSPDL
  IF (LNHDYN) YDELBC_FIELDS%MGMV0(YDELBC_FIELDS%YYTGMVCPL%MSPDM)=YDGMV%YT0%MSPDM
ENDIF

ALLOCATE (YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
& 'YDELBC_FIELDS%MGMV1    ',SIZE(YDELBC_FIELDS%MGMV1    ),SHAPE(YDELBC_FIELDS%MGMV1    )
YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MU)=YDGMV%YT1%MU
YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MV)=YDGMV%YT1%MV
YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MT)=YDGMV%YT1%MT
IF (LNHDYN) YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MSPD)=YDGMV%YT1%MSPD
IF (LNHDYN) YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MSVD)=YDGMV%YT1%MSVD
IF (LNHDYN.AND.YRDYNA%LNHX) YDELBC_FIELDS%MGMV1(YDELBC_FIELDS%YYTGMVCPL%MNHX)=YDGMV%YT1%MNHX

ALLOCATE (YDELBC_FIELDS%MGMVS0(YDELBC_FIELDS%YYTGMVSCPL%NDIMT))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMVS0   ',SIZE(YDELBC_FIELDS%MGMVS0   ),SHAPE(YDELBC_FIELDS%MGMVS0   )
YDELBC_FIELDS%MGMVS0(YDELBC_FIELDS%YYTGMVSCPL%MSP)=YDGMV%YT0%MSP
IF (NCONF /= 701) THEN
  YDELBC_FIELDS%MGMVS0(YDELBC_FIELDS%YYTGMVSCPL%MSPL)=YDGMV%YT0%MSPL
  YDELBC_FIELDS%MGMVS0(YDELBC_FIELDS%YYTGMVSCPL%MSPM)=YDGMV%YT0%MSPM
ENDIF

ALLOCATE (YDELBC_FIELDS%MGMVS1(YDELBC_FIELDS%YYTGMVSCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%MGMVS1   ',SIZE(YDELBC_FIELDS%MGMVS1   ),SHAPE(YDELBC_FIELDS%MGMVS1   )
YDELBC_FIELDS%MGMVS1(YDELBC_FIELDS%YYTGMVSCPL%MSP)=YDGMV%YT1%MSP

! * Allocation and computation of CCFIELD_GMV, CCFIELD_GMVS, CCFIELD_GFL:

ALLOCATE (YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GMV ',SIZE(YDELBC_FIELDS%CCFIELD_GMV),SHAPE(YDELBC_FIELDS%CCFIELD_GMV)
YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MU)='LSCGRAD_UUUU'
YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MV)='LSCGRAD_VVVV'
YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MT)='LSCGRAD_TEMP'
IF (LNHDYN) YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MSPD)='LSCGRAD_PDEP'
IF (LNHDYN) YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MSVD)='LSCGRAD_VDIV'
IF (LNHDYN.AND.YRDYNA%LNHX) YDELBC_FIELDS%CCFIELD_GMV(YDELBC_FIELDS%YYTGMVCPL%MNHX)='LSCGRAD_NHXX'

ALLOCATE (YDELBC_FIELDS%CCFIELD_GMVS(YDELBC_FIELDS%YYTGMVSCPL%NDIM))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GMVS',SIZE(YDELBC_FIELDS%CCFIELD_GMVS),SHAPE(YDELBC_FIELDS%CCFIELD_GMVS)
YDELBC_FIELDS%CCFIELD_GMVS(YDELBC_FIELDS%YYTGMVSCPL%MSP)='LSCGRAD_SURP'

ALLOCATE (YDELBC_FIELDS%CCFIELD_GFL(YDELBC_FIELDS%NDIMCPL))
IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A12,' ALLOCATED ',8I8)")&
 & 'YDELBC_FIELDS%CCFIELD_GFL ',SIZE(YDELBC_FIELDS%CCFIELD_GFL),SHAPE(YDELBC_FIELDS%CCFIELD_GFL)
YDELBC_FIELDS%CCFIELD_GFL(1:YDELBC_FIELDS%NDIMCPL)='LSCGRAD_HUMQ'

! * Other variables for spectral nudging: NOFFGT3BSP, allocate GT3SPBUF

IWS_TENC=2

IF (LESPCPL) THEN
  YDELBC_FIELDS%NOFFGT3BSP=YDGEOMETRY%YRDIM%NSPEC2*(KNS3D*YDGEOMETRY%YRDIMV%NFLEVL+KNFD2D)
ELSE
  YDELBC_FIELDS%NOFFGT3BSP=0
ENDIF

IF (LESPCPL) THEN
  IF (LSPTENC) IWS_TENC=3
  ALLOCATE(YDELBC_FIELDS%GT3SPBUF(IWS_TENC*YDELBC_FIELDS%NOFFGT3BSP))
  IF (LALLOPR) WRITE(NULOUT,"(1X,'ARRAY ',A10,' ALLOCATED ',8I8)") 'YDELBC_FIELDS%GT3SPBUF'&
& ,SIZE(YDELBC_FIELDS%GT3SPBUF ),SHAPE(YDELBC_FIELDS%GT3SPBUF )
ENDIF

!--------------------------------------------------------------------------

!      Part B: printings.

WRITE(NULOUT,*) ' --- PRINTINGS IN SUELBC_FIELDS --- '

! * B1: Number of coupled fields.
WRITE(UNIT=NULOUT,FMT='('' Grid-point coupling: '')')
WRITE(UNIT=NULOUT,FMT='(''  nb of GMV fields with temporal interpolation: YDELBC_FIELDS%YYTGMVCPL%NDIMT = '',I4)')&
  & YDELBC_FIELDS%YYTGMVCPL%NDIMT
WRITE(UNIT=NULOUT,FMT='(''  nb of coupled GMV fields: YDELBC_FIELDS%YYTGMVCPL%NDIM = '',I4)') YDELBC_FIELDS%YYTGMVCPL%NDIM
WRITE(UNIT=NULOUT,FMT='(''  nb of GMVS fields with temporal interpolation: YDELBC_FIELDS%YYTGMVSCPL%NDIMT = '',I4)')&
 & YDELBC_FIELDS%YYTGMVSCPL%NDIMT
WRITE(UNIT=NULOUT,FMT='(''  nb of coupled GMVS fields: YDELBC_FIELDS%YYTGMVSCPL%NDIM = '',I4)') YDELBC_FIELDS%YYTGMVSCPL%NDIM
WRITE(UNIT=NULOUT,FMT='(''  nb of coupled GFL fields: YDELBC_FIELDS%NDIMCPL = '',I4)') YDELBC_FIELDS%NDIMCPL
WRITE(UNIT=NULOUT,FMT='(''  total nb of coupled fields: YDELBC_FIELDS%NGALEF = '',I4)') YDELBC_FIELDS%NGALEF

! * B2: Other variables for grid-point coupling.
WRITE(UNIT=NULOUT,FMT='('' Other variables for grid-point coupling: '')')
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMV0 = '',20(1X,I2))') YDELBC_FIELDS%MGMV0(1:YDELBC_FIELDS%YYTGMVCPL%NDIMT)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMVS0 = '',20(1X,I2))') YDELBC_FIELDS%MGMVS0(1:YDELBC_FIELDS%YYTGMVSCPL%NDIMT)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMV1 = '',20(1X,I2))') YDELBC_FIELDS%MGMV1(1:YDELBC_FIELDS%YYTGMVCPL%NDIM)
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%MGMVS1 = '',20(1X,I2))') YDELBC_FIELDS%MGMVS1(1:YDELBC_FIELDS%YYTGMVSCPL%NDIM)

! * D3: Other variables for spectral nudging.
WRITE(UNIT=NULOUT,FMT='(''  YDELBC_FIELDS%NOFFGT3BSP = '',I8)') YDELBC_FIELDS%NOFFGT3BSP

WRITE(NULOUT,*) ' '

!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:SUELBC_FIELDS',1,ZHOOK_HANDLE)
END SUBROUTINE SUELBC_FIELDS 

SUBROUTINE DEALLOCATE_ELBC0C(YDELBC_FIELDS)

!--------------------------------------------------------------------------
! deallocates 'ELBC0C' arrays
!--------------------------------------------------------------------------

IMPLICIT NONE

TYPE(TELBC_FIELDS) , INTENT(INOUT) :: YDELBC_FIELDS
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:DEALLOCATE_ELBC0C',0,ZHOOK_HANDLE)
!     ------------------------------------------------------------------

IF (ALLOCATED(YDELBC_FIELDS%GMVCPL  )) DEALLOCATE(YDELBC_FIELDS%GMVCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSCPL )) DEALLOCATE(YDELBC_FIELDS%GMVSCPL)
IF (ALLOCATED(YDELBC_FIELDS%GFLCPL  )) DEALLOCATE(YDELBC_FIELDS%GFLCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSTENC)) DEALLOCATE(YDELBC_FIELDS%GMVSTENC)
IF (ALLOCATED(YDELBC_FIELDS%GT3SPBUF)) DEALLOCATE(YDELBC_FIELDS%GT3SPBUF)

IF (ALLOCATED(YDELBC_FIELDS%GMVUCPL )) DEALLOCATE(YDELBC_FIELDS%GMVUCPL)
IF (ALLOCATED(YDELBC_FIELDS%GMVSUCPL)) DEALLOCATE(YDELBC_FIELDS%GMVSUCPL)
IF (ALLOCATED(YDELBC_FIELDS%GFLUCPL )) DEALLOCATE(YDELBC_FIELDS%GFLUCPL)

IF (ALLOCATED(YDELBC_FIELDS%MGMV0  )) DEALLOCATE(YDELBC_FIELDS%MGMV0)
IF (ALLOCATED(YDELBC_FIELDS%MGMV1  )) DEALLOCATE(YDELBC_FIELDS%MGMV1)
IF (ALLOCATED(YDELBC_FIELDS%MGMVS0  )) DEALLOCATE(YDELBC_FIELDS%MGMVS0)
IF (ALLOCATED(YDELBC_FIELDS%MGMVS1  )) DEALLOCATE(YDELBC_FIELDS%MGMVS1)

IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GMV)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GMV)
IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GMVS)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GMVS)
IF (ALLOCATED(YDELBC_FIELDS%CCFIELD_GFL)) DEALLOCATE(YDELBC_FIELDS%CCFIELD_GFL)

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('YEMLBC_FIELDS:DEALLOCATE_ELBC0C',1,ZHOOK_HANDLE)
END SUBROUTINE DEALLOCATE_ELBC0C

!=============================================================================

END MODULE YEMLBC_FIELDS 
