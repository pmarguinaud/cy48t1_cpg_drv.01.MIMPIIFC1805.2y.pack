SUBROUTINE AER_LIDSIMTL &
!input
  &( KDLEN, KLEN , KLEV , KDFS, LDPHYLIN, &
  &  PLIDPRES , & 
  &  PCO25 , PNO25, PGEOP5, PTQO5  ,PAELIDPROF5,  &
  &  PCO2  , PNO2 , PGEOP , PTQO   ,PAELIDPROF,  &
  &  YDGP5, &
! output - perturbations
  &  PLISIS, PLISIT, PULISIS,PULISIT,PEXTT, PEXTS &
  & )



!**** *AER_LIDSIMTL* - TANGENT LINEAR VERSION: COMPUTES PERTURBATIONS ON THE LIDAR SIGNAL OUT OF THE ATMOSPHERE
!                    ASSUMED CLOUDLESS BUT INCLUDING THE GEMS-AEROSOLS.

!     PURPOSE.
!     --------

!          THIS ROUTINE COMPUTES THE TOTAL BACK-SCATTERING COEFFICIENT 
!          ACCOUNTING RAYLEIGH SCATTERING, AEROSOL SCATTERING AND GASEOUS 
!          ABSORPTION FOR THE THREE USUAL WAVELENGTHS (355, 532 AND 1064 NM)
!          FROM THE STANDARD "LIDAR EQUATION".

!**   INTERFACE.
!     ----------

!          *AER_LIDSIMTL* IS CALLED FROM *HOPTL*

!        IMPLICIT ARGUMENTS :
!        --------------------

!     ==== INPUTS ===
! PLIDPRES (KDLEN,KLEV) : LIDAR LEVEL PRESSURE (Pa)
! PCO25   (KDLEN,KLEV)   : CO2 CONCENTRATION (kg/kg) from traj
! PNO25   (KDLEN,KLEV)   : NO2 CONCENTRATION (kg/kg) from traj
! PTQO5   (KDLEN,KLEV)   : TEMPERATURE (K), SPECIFIC HUMIDITY (kg/kg) and OZONE from traj
! PGEOP5  (KDLEN,KLEV)   : GEOPOTENTIAL from traj
! PCO2    (KDLEN,KLEV)   : Perturbation on CO2 CONCENTRATION (kg/kg) 
! PNO2    (KDLEN,KLEV)   : Perturbation on NO2 CONCENTRATION (kg/kg) 
! PTQO    (KDLEN,KLEV,KDFS)   : Perturbation on TEMPERATURE (K), SPECIFIC HUMIDITY (kg/kg) and OZONE 
! PGEOP   (KDLEN,KLEV)   : Perturbation on GEOPOTENTIAL 

!     ==== OUTPUTS ===
! PLISIS (KDLEN,NWLID,0:KLEV) : Perturbation on BACK-SCATTERING SIGNAL FOR SURFACE LIDAR
! PLISIT (KDLEN,NWLID,0:KLEV) : Perturbation on BACK-SCATTERING SIGNAL FOR SATELLITE LIDAR
! PULISIS (KDLEN,NWLID,0:KLEV) : Perturbation on UNATTENUATED BACK-SCATTERING SIGNAL FOR SURFACE LIDAR
! PULISIT (KDLEN,NWLID,0:KLEV) : Perturbation on UNATTENUATED BACK-SCATTERING SIGNAL FOR SATELLITE LIDAR
! PEXTT (KDLEN,NWLID,0:KLEV) : Perturbation on EXTINCTION FOR SATELLITE LIDAR
! PEXTS (KDLEN,NWLID,0:KLEV) : Perturbation on EXTINCTION FOR SURFACE LIDAR


!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------

!     AUTHOR.
!     -------
!        JEAN-JACQUES MORCRETTE  *ECMWF*

!     MODIFICATIONS.
!     --------------
!        ORIGINAL : 20090702 

!     Angela Benedetti: 24-Aug-2010 Modifications to call it from hoptl.F90
!     A. Geer           29-Dec-2015 OOPS cleaning (GOM_PLUS added; GEMSPROF removed)
!        S. Remy     16-Sep-2016    Add nitrate and ammonium
!     A Benedetti    05-Mar-2018    Add unattenuated backscatter
!     ---------------------------------------------------------------------

USE PARKIND1      , ONLY : JPIM, JPRB
USE YOMHOOK       , ONLY : LHOOK, DR_HOOK
USE YOEAERLID     , ONLY : YREAERLID
USE YOEAEROP      , ONLY : ALF_SU, OMG_SU, ALF_OM, OMG_OM, ALF_DD, OMG_DD,ALF_SS,OMG_SS, &
 &                         ALF_BC, OMG_BC,  RALI_BC,RALI_DD,RALI_OM,RALI_SU,RALI_SS, ALF_NI, OMG_NI, &
 &                         RALI_NI, ALF_AM, OMG_AM, RALI_AM, ALF_SOA, OMG_SOA,RALI_SOA
USE YOEAERATM     , ONLY : YREAERATM
USE YOEAERSNK     , ONLY : YREAERSNK
USE YOEAERVOL     , ONLY : YREAERVOL
USE YOMCST        , ONLY : RG, RMD, RMCO2, RMNO2, RMO3, RNAVO, RPI , YRCST
USE YOETHF        , ONLY : YRTHF
USE GOM_PLUS      , ONLY : TYPE_GOM_PLUS
USE YOM_GRIB_CODES, ONLY : NGRBAERMR01,NGRBAERMR02, NGRBAERMR03, NGRBAERMR04, NGRBAERMR05, &
 & NGRBAERMR06, NGRBAERMR07, NGRBAERMR08, NGRBAERMR09, NGRBAERMR10, NGRBAERMR11, NGRBAERMR12, &
 & NGRBAERMR13, NGRBAERMR14, NGRBAERMR15, NGRBAERMR16, NGRBAERMR17, NGRBAERMR18, &
 & NGRBAERMR19, NGRBAERMR20, NGRBAERMR21, NGRBAERLG

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN) :: KDLEN  ! Total number of observations
INTEGER(KIND=JPIM),INTENT(IN) :: KLEN   ! Current number of observations
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV   ! Number of lidar vertical levels = YREAERLID%NAELID
INTEGER(KIND=JPIM),INTENT(IN) :: KDFS   ! Size of T q O3 array from interpolation 
LOGICAL           ,INTENT(IN) :: LDPHYLIN

TYPE(TYPE_GOM_PLUS), INTENT(IN) :: YDGP5 ! Model variables at observation locations

REAL(KIND=JPRB)   ,INTENT(IN) :: PLIDPRES(KDLEN,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN) :: PCO25(KDLEN,KLEV) ! CO2 profile from traj
REAL(KIND=JPRB)   ,INTENT(IN) :: PCO2(KDLEN,KLEV)  ! Perturbations on CO2
REAL(KIND=JPRB)   ,INTENT(IN) :: PNO25(KDLEN,KLEV) ! NO2 from traj
REAL(KIND=JPRB)   ,INTENT(IN) :: PNO2(KDLEN,KLEV)  ! Perturbation on NO2
REAL(KIND=JPRB)   ,INTENT(IN) :: PGEOP5(KDLEN,KLEV) ! Geopotential
REAL(KIND=JPRB)   ,INTENT(IN) :: PGEOP(KDLEN,KLEV)  ! Perturbations on geopotential 
REAL(KIND=JPRB)   ,INTENT(IN) :: PTQO5(KDLEN ,KLEV,KDFS) ! Interpolated profiles of T, q, O3
REAL(KIND=JPRB)   ,INTENT(IN) :: PTQO(KDLEN ,KLEV,KDFS) ! Perturbations on T, q, O3
REAL(KIND=JPRB)   ,INTENT(IN) :: PAELIDPROF5(KDLEN,KLEV,YDGP5%NGEMS) !GEMS profiles including aerosols from traj
REAL(KIND=JPRB)   ,INTENT(IN) :: PAELIDPROF(KDLEN,KLEV,YDGP5%NGEMS) !Perturbations on GEMS profiles including aerosols

REAL(KIND=JPRB)   ,INTENT(OUT) :: PLISIS(KDLEN,YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT) :: PLISIT(KDLEN,YREAERLID%NWLID,0:KLEV) ! Perturbations on lidar backscattering
REAL(KIND=JPRB)   ,INTENT(OUT) :: PULISIS(KDLEN,YREAERLID%NWLID,0:KLEV), PULISIT(KDLEN,YREAERLID%NWLID,0:KLEV), &
     &                            PEXTS(KDLEN,YREAERLID%NWLID,0:KLEV), PEXTT(KDLEN,YREAERLID%NWLID,0:KLEV) ! Perturbations on lidar variables 

!     ------------------------------------------------------------------

!*       0.1   LOCAL ARRAYS
!              ------------

INTEGER(KIND=JPIM) :: IAER, IBIN, IK, ITYP, IWAVL, IEFRH
INTEGER(KIND=JPIM) :: JAER, JL, JK, JTAB, JWL, JLEV, JP, JF
INTEGER(KIND=JPIM) :: IFLAG, IRH(KDLEN,KLEV)
INTEGER(KIND=JPIM), PARAMETER  :: JTABMX=12
INTEGER(KIND=JPIM) :: INACTAERO

LOGICAL :: LLPRINT

REAL(KIND=JPRB) :: ZAERF5(KDLEN,KLEV,YDGP5%NGEMS)
REAL(KIND=JPRB) :: ZAERF(KDLEN,KLEV,YDGP5%NGEMS)
REAL(KIND=JPRB) :: ZLISIS5(KDLEN,YREAERLID%NWLID,0:KLEV), ZLISIT5(KDLEN,YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZOTRAY5(KDLEN,YREAERLID%NWLID,1:KLEV), ZOTCO25(KDLEN,YREAERLID%NWLID,1:KLEV)
REAL(KIND=JPRB) :: ZOTO25(KDLEN,YREAERLID%NWLID,1:KLEV)
REAL(KIND=JPRB) :: ZOTRAY (KDLEN,YREAERLID%NWLID,1:KLEV), ZOTCO2 (KDLEN,YREAERLID%NWLID,1:KLEV)
REAL(KIND=JPRB) :: ZOTO2 (KDLEN,YREAERLID%NWLID,1:KLEV)
REAL(KIND=JPRB) :: ZOTNO25(KDLEN,YREAERLID%NWLID,1:KLEV), ZOTO35(KDLEN,YREAERLID%NWLID,1:KLEV)
REAL(KIND=JPRB) :: ZPATHT5(KDLEN,YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZOTNO2 (KDLEN,YREAERLID%NWLID,1:KLEV), ZOTO3 (KDLEN,YREAERLID%NWLID,1:KLEV) 
REAL(KIND=JPRB) :: ZPATHT (KDLEN,YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZPATHS5(KDLEN,YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZPATHS (KDLEN,YREAERLID%NWLID,0:KLEV)


REAL(KIND=JPRB) :: ZAERMSS5(KDLEN,KLEV,YDGP5%NGEMS)
REAL(KIND=JPRB) :: ZAERMSS (KDLEN,KLEV,YDGP5%NGEMS), ZALF(YDGP5%NGEMS), ZOMG(YDGP5%NGEMS), ZLIR(YDGP5%NGEMS)
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV) :: ZAERFT5 ! sum of single contributions of aer mixing ratio
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV) :: ZAERTMR5 ! total aer mixing ratios from traj
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV) :: ZAERTMR ! total aer mixing ratios pert
REAL(KIND=JPRB) :: ZFRACAER5(KLEN,KLEV,YDGP5%NGEMS) ! fraction of mixing ratios wrt total mass
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV)   :: ZQSAT5, ZTF5, ZPF5 ! Temperature, pressure, q sat from traj
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV)   :: ZTF , ZPF ! Perturbations on temperature, pressure, q sat 
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV)   :: ZO3F5, ZQF5 ! O3 and q from traj
REAL(KIND=JPRB), DIMENSION(KLEN,KLEV)   :: ZO3F, ZQF ! Perturbations on O3 and q from traj
REAL(KIND=JPRB) :: ZRHCL5 


REAL(KIND=JPRB) :: ZBSC5(KDLEN,YREAERLID%NWLID,KLEV)      , ZAEROD5(KDLEN,YREAERLID%NWLID,KLEV)
REAL(KIND=JPRB) :: ZBSC (KDLEN,YREAERLID%NWLID,KLEV)      , ZAEROD (KDLEN,YREAERLID%NWLID,KLEV)
REAL(KIND=JPRB) :: ZBSCMOL5(KDLEN,YREAERLID%NWLID,KLEV)   , ZBSCMOL(KDLEN,YREAERLID%NWLID,KLEV) 
REAL(KIND=JPRB) :: ZBOLTZ

REAL(KIND=JPRB) :: ZDP5(KDLEN, KLEV), ZDZ5(KDLEN,KLEV)
REAL(KIND=JPRB) :: ZDP(KDLEN, KLEV), ZDZ(KDLEN,KLEV)
REAL(KIND=JPRB) :: ZGEOMH5(KDLEN,0:KLEV), ZGEOMH(KDLEN,0:KLEV)

REAL(KIND=JPRB) :: ZDPG5,          ZFACT5
REAL(KIND=JPRB) :: ZA1, ZA2, ZA3, ZA4, ZAA, ZAN, ZAN2, ZDPG , ZEPSAER, ZFAC, ZFACT , ZMD, ZLIDRAT

REAL(KIND=JPRB) :: ZNO2MOL5, ZNO2TAU5, ZNO2V5, ZO3MOL5, ZO3TAU5, ZO3V5
REAL(KIND=JPRB) :: ZNO2MOL , ZNO2TAU , ZNO2V , ZO3MOL , ZO3TAU , ZO3V ,  ZREF, ZSIGMA

REAL(KIND=JPRB) ::                             ZTAUR5, ZTE5, ZTE25
REAL(KIND=JPRB) :: ZTAUR , ZTE , ZTE2 

REAL(KIND=JPRB) :: ZTRSOT5, ZTRTOT5, ZUSOT5, ZUTOT5
REAL(KIND=JPRB) :: ZTRSOT , ZTRTOT , ZUSOT , ZUTOT , ZWLUM, ZWLCM, ZWL2CM, ZWL4CM, ZWN, ZWN2

REAL(KIND=JPRB) :: ZGCO25,                       ZPHIC5, ZPSIC5, ZUCO25, ZUPCO25, ZVCO25
REAL(KIND=JPRB) :: ZGCO2 , ZMCO2,ZPHIC , ZPSIC , ZUCO2 , ZUPCO2 , ZVCO2

REAL(KIND=JPRB) ::       ZGO25,                        ZPHIO5, ZPSIO5, ZUO25, ZUPO25
REAL(KIND=JPRB) :: ZO2 , ZGO2 , ZMO2 , ZPHIO , ZPSIO , ZUO2 , ZUPO2

REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "satur.intfb.h"

!*************************************************************************************
IF (LHOOK) CALL DR_HOOK('AER_LIDSIMTL',0,ZHOOK_HANDLE)
ASSOCIATE( &
 & RSS_RH80_MASSFAC=>YREAERATM%RSS_RH80_MASSFAC, &
 & NVOLOPTP=>YREAERVOL%NVOLOPTP, &
 & JWLID=>YREAERLID%JWLID, NAELID=>YREAERLID%NAELID, NWLID=>YREAERLID%NWLID, &
 & RLICLS=>YREAERLID%RLICLS, RLICO2=>YREAERLID%RLICO2, &
 & RLIDELT=>YREAERLID%RLIDELT, RLINO2=>YREAERLID%RLINO2, RLINS=>YREAERLID%RLINS, &
 & RLIO2=>YREAERLID%RLIO2, RLIO3=>YREAERLID%RLIO3, RLIPREF=>YREAERLID%RLIPREF, &
 & RLIT0=>YREAERLID%RLIT0, RLITREF=>YREAERLID%RLITREF, RWLID=>YREAERLID%RWLID, &
 & RRHTAB=>YREAERSNK%RRHTAB, &
 & YAERO_DESC=>YREAERATM%YAERO_DESC)

!*         0.     SPECTRAL INFORMATION OF RELEVANCE
!                 ---------------------------------

! Rayleigh |  355nm / 28169cm-1 |  532nm / 18797cm-1 | 1064nm / 9398.5cm-1 |
! Index    |          2         |          8         |         16          ! 
! CO2      |         no         |         no         |        yes          |
! O2       |         no         |         no         |        yes          |
! O3       |        yes         |        yes         |        yes          |
! NO2      |        yes         |        yes         |      yes          |



!*         1.     INITIALIZATION AND PREPARATORY WORK
!                 -----------------------------------

ZREF=RLITREF/RLIPREF
ZO2=RMO3*2._JPRB/3._JPRB
ZEPSAER=1.E-15_JPRB
LLPRINT=.FALSE.
!-- the effective relative hunidity is the low value (20%) assumed for
!hydrophobic component of OM
IEFRH=3


! Extract aerosol mixing ratios from GEMS profiles structure
ZAERF5(:,:,:) = 0.0_JPRB
ZAERF(:,:,:)  = 0.0_JPRB

DO JL=1,KLEN
  IAER=0
  DO JF=1,YDGP5%NGEMS
    JP=0
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR01 .OR.  &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR02 .OR.  &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR03) JP=1 !SS 3 bins
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR04 .OR.  &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR05 .OR.  &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR06 ) JP=2 !DD 3 bins
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR07 .OR. &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR08) JP=3 ! ORGANIC MATTER - 2 bins
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR09 .OR. &
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR10) JP=4 ! Black Carbon - 2 bins
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR11 .OR. & 
     &   YDGP5%GEMS_IGRIB(JF) == NGRBAERMR12)  JP=5 ! SO4 & SO2
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR16 .OR. &
     & YDGP5%GEMS_IGRIB(JF) == NGRBAERMR17) JP=6 ! NO3
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR18) JP=7 ! NH4
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR19 .OR. &
     & YDGP5%GEMS_IGRIB(JF) == NGRBAERMR20 .OR. &
     & YDGP5%GEMS_IGRIB(JF) == NGRBAERMR21) JP=8 ! SOA
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR13) JP=9 ! Volcanic ashes 
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERMR14 .OR. &
     & YDGP5%GEMS_IGRIB(JF) == NGRBAERMR15)  JP=10 ! Volcanic SO4 and SO2

    IF(JP > 0) THEN
      IAER=IAER+1
      ZAERF5(JL,:,IAER)=PAELIDPROF5(JL,1:KLEV,JF)
    ENDIF
    IF(YDGP5%GEMS_IGRIB(JF) == NGRBAERLG) THEN ! Total aerosol mixing ratio
      ZAERTMR5(JL,:)= PAELIDPROF5(JL,1:KLEV,JF)
      ZAERTMR(JL,:) = PAELIDPROF(JL,1:KLEV,JF)
    ENDIF
  ENDDO
ENDDO
INACTAERO=IAER

! Compute total mixing ratio by summing single contributions
    ZAERFT5(:,:) = 0.0_JPRB
    DO JAER=1,INACTAERO 
      IF ((YAERO_DESC(JAER)%NTYP /= 5 .AND. YAERO_DESC(JAER)%NTYP /= 9) .OR. YAERO_DESC(JAER)%NBIN /= 2) THEN
        ZAERFT5(:,:)=ZAERFT5(:,:)+ ZAERF5(:,:,JAER)
      ENDIF
    ENDDO

! BEN the perturbations on the various species are computed from the perturbation
! on the total mass mixing ratio using the mass fraction assumed constant

ZFRACAER5(:,:,:) = 0.0_JPRB
DO JL=1,KLEN
  DO JAER=1,INACTAERO 
    IF ((YAERO_DESC(JAER)%NTYP /= 5 .AND. YAERO_DESC(JAER)%NTYP /= 9) .OR. YAERO_DESC(JAER)%NBIN /= 2) THEN
      DO JLEV=1,KLEV  
        IF(ZAERFT5(JL,JLEV) /= 0.0_JPRB) THEN
          ZFRACAER5(JL,JLEV,JAER) = ZAERF5(JL,JLEV,JAER)/ZAERFT5(JL,JLEV)
        ENDIF
        ZAERF(JL,JLEV,JAER)=ZAERTMR(JL,JLEV) * ZFRACAER5(JL,JLEV,JAER)
      ENDDO
    ENDIF
  ENDDO
ENDDO

! Save T, Q, and O3 profiles in separate arrays
ZTF5 (1:KLEN,:) = PTQO5(1:KLEN,:,1)
ZQF5 (1:KLEN,:) = PTQO5(1:KLEN,:,2)
ZO3F5(1:KLEN,:) = PTQO5(1:KLEN,:,3)

! Save T, Q, and O3 profiles in separate arrays
ZTF (1:KLEN,:) = PTQO(1:KLEN,:,1)
ZQF (1:KLEN,:) = PTQO(1:KLEN,:,2)
ZO3F(1:KLEN,:) = PTQO(1:KLEN,:,3)

ZPF5(1:KLEN,:) = PLIDPRES(1:KLEN,:)
ZPF (1:KLEN,:) = 0.0_JPRB ! no variations of pressure

! COMPUTE THICKNESS

ZGEOMH5(:,0) = PGEOP5(:,1)  ! Top layer is assumed to have zero thickness...
ZGEOMH5(:,1:KLEV) = PGEOP5(:,1:KLEV)

ZGEOMH(:,0) = PGEOP(:,1)  ! Top layer is assumed to have zero thickness...
ZGEOMH(:,1:KLEV) = PGEOP(:,1:KLEV)

DO JL=1,KLEN
 DO JK=1,KLEV
    ZDZ5(JL,JK) = (ZGEOMH5(JL,JK-1)-ZGEOMH5(JL,JK)) /RG
    ZDZ (JL,JK) = (ZGEOMH (JL,JK-1)-ZGEOMH (JL,JK)) /RG
  ENDDO
    ZDZ5(JL,1) = 60.0_JPRB ! set top layer equal to 60 meters
    ZDZ(JL,1) =  0.0_JPRB ! perturbation is zero
ENDDO

!     INTEGRATE HYDROSTATIC EQUATION TO COMPUTE ZDP from ZDZ

DO JK=1,KLEV
  DO JL=1,KLEN
     ZDP5(JL,JK) = RG * ZDZ5(JL,JK) / ( 287.0_JPRB*ZTF5(JL,JK)* &
     & (1.0_JPRB+0.608_JPRB*ZQF5(JL,JK)) ) * ZPF5(JL,JK)


     ZDP(JL,JK) =  RG *  ZPF5(JL,JK) * &
     &            ( ZDZ(JL,JK) / ( 287.0 *ZTF5(JL,JK)* &
     &                         (1.0 +0.608 *ZQF5(JL,JK)) )  &
     &           - ZTF(JL,JK) * ZDZ5(JL,JK)/ ( 287.0 *ZTF5(JL,JK)**2 * &
     &                         (1.0 +0.608 *ZQF5(JL,JK)) )  &
     &           - ZQF(JL,JK) * ZDZ5(JL,JK) * 0.608 / ( 287.0 *ZTF5(JL,JK)* &
     &                            (1.+ 0.608 *ZQF5(JL,JK) )**2  )      )


  ENDDO
ENDDO

IFLAG=2
CALL SATUR (YRTHF, YRCST, 1 , KLEN , KLEN  , 1 , KLEV, LDPHYLIN, &
  & ZPF5, ZTF5 , ZQSAT5 , IFLAG)
! BEN - initially do not include perturbations in T,Q deriving from call to SATUR
!CALL SATURTL (1 , KLEN , KLEN  , 1 , KLEV  ,&
!  & ZPF5, ZTF5 , ZQSAT5,&
!  & ZPF , ZTF , ZQSAT )

!-- define RH index from "clear-sky" relative humidity
IRH(:,:)=1
ZRHCL5= 0.0_JPRB
DO JK=1,KLEV
  DO JL=1,KLEN
    ZRHCL5=(ZQF5(JL,JK)/ZQSAT5(JL,JK))*100._JPRB
    DO JTAB=1,JTABMX
      IF (ZRHCL5 > RRHTAB(JTAB)) THEN
        IRH(JL,JK)=JTAB
      ENDIF
    ENDDO
  ENDDO
ENDDO

ZBSC5(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZAEROD5(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZBSC (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZAEROD (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZBSCMOL5(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZBSCMOL (1:KLEN,1:NWLID,1:KLEV)=0._JPRB

ZOTRAY5(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTNO25(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO35 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO25 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTCO25(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZPATHT5(1:KLEN,1:NWLID,0:KLEV)=0._JPRB
ZOTRAY (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTNO2 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO3  (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO2  (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTCO2 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZPATHT (1:KLEN,1:NWLID,0:KLEV)=0._JPRB

ZPATHS5(1:KLEN,1:NWLID,0:KLEV)=0._JPRB
ZPATHS (1:KLEN,1:NWLID,0:KLEV)=0._JPRB



!*         2.      RAYLEIGH AND GASEOUS OPTICAL THICKNESSES
!                  ----------------------------------------

DO JWL=1,NWLID
!-- RWLID in m, ZWLUM in um, ZWLCM in cm!!!!
  ZWLUM=RWLID(JWL)*1.0E+06_JPRB
  ZWLCM=RWLID(JWL)*1.0E+02_JPRB
  ZWL2CM=ZWLCM*ZWLCM
  ZWL4CM=ZWL2CM*ZWL2CM

  ZWN=1._JPRB/ZWLUM
  ZWN2=ZWN*ZWN

!* Air Refractive Index: Edlen, 1966, Metrologia, 2, 71-80 w pw=0
  ZA1=130._JPRB-ZWN2
  ZA2=38.9_JPRB-ZWN2
  ZA3=2406030._JPRB/ZA1
  ZA4=15997._JPRB/ZA2
  ZAN=1._JPRB+(8342.13_JPRB+ZA3+ZA4)*1.E-08_JPRB
  ZAN2=ZAN*ZAN
  ZAA=(24._JPRB*RPI**3) * ((ZAN2-1._JPRB)/(ZAN2+2._JPRB))**2 * (6._JPRB+3._JPRB*RLIDELT)/(6._JPRB-7._JPRB*RLIDELT)

!-- According to Bohdaine et al. (1999), T and p dependence of the molecular density Ns and of the function of 
!   the refractive index (ns**2-1)/(ns**2+2) cancels out, and Rayleigh scattering coefficient ZSIGMA is independent 
!   of T and p; wavelength MUST be in cm to get ZSIGMA in cm2. 
!   ZTAUR for the entire atmosphere should be between 0.59 at 355 nm and < 0.0007 at 1064 nm.

  ZSIGMA=ZAA/(ZWL4CM*RLINS*RLINS)
  ZTAUR =ZSIGMA*101325._JPRB*RNAVO/(RMD*RG)/10._JPRB

  ZOTRAY5(1:KLEN,JWL,1:KLEV)=0._JPRB
  ZOTNO25(1:KLEN,JWL,1:KLEV)=0._JPRB
  ZOTO35 (1:KLEN,JWL,1:KLEV)=0._JPRB
  ZOTRAY (1:KLEN,JWL,1:KLEV)=0._JPRB
  ZOTNO2 (1:KLEN,JWL,1:KLEV)=0._JPRB
  ZOTO3  (1:KLEN,JWL,1:KLEV)=0._JPRB

  DO JK=1,KLEV
    DO JL=1,KLEN

!-- ZFACT is the number of gas molecules in a layer of pressure ZDP at temperature T
      ZFACT5=RNAVO*(ZDP5(JL,JK)*RLITREF)/(ZTF5(JL,JK)*RLIPREF)
      ZFACT =ZFACT5*(ZDP(JL,JK)/ZDP5(JL,JK)-ZTF(JL,JK)/ZTF5(JL,JK))

!-- Rayleigh: Bodhaine et al., 1999: JAOT, 16, 1854-1861.   tau= sigma*DelP*Avo / (mmw_air * g)
      ZVCO25=PCO25(JL,JK)*RMD/RMCO2
      ZVCO2 =PCO2 (JL,JK)*RMD/RMCO2
      ZMD = 28.9595_JPRB + 15.0556_JPRB * ZVCO2 !??????????????????
      ZMD = RMD
      ZTAUR5=ZSIGMA*ZDP5(JL,JK)*RNAVO/(ZMD*RG)/10._JPRB
      ZTAUR =ZSIGMA*ZDP (JL,JK)*RNAVO/(ZMD*RG)/10._JPRB
      ZOTRAY5(JL,JWL,JK)= ZTAUR5
      ZOTRAY (JL,JWL,JK)= ZTAUR

!-- O3 
      ZO3V5=ZO3F5(JL,JK)*RMD/RMO3                              ! volume mixing ratio in layer
      ZO3V =ZO3F (JL,JK)*RMD/RMO3
      ZO3MOL5=ZO3V5*ZFACT5                                     ! number of O3 molecules in layer (pressure unit: kg m-1 s-2)
      ZO3MOL =ZO3V5*ZFACT +ZO3V*ZFACT5
      ZO3TAU5=1.E-04_JPRB*RLIO3(3,JWL)*ZO3MOL5/(RMO3*RG)    ! cross-section from cm2 molec-1 to m-2 molec-1
      ZO3TAU =1.E-04_JPRB*RLIO3(3,JWL)*ZO3MOL /(RMO3*RG)
      ZOTO35(JL,JWL,JK)=ZO3TAU5
      ZOTO3(JL,JWL,JK) =ZO3TAU

!-- NO2
      ZNO2V5=PNO25(JL,JK)*RMD/RMNO2                           ! volume mixing ratio in layer
      ZNO2V =PNO2 (JL,JK)*RMD/RMNO2                           ! volume mixing ratio in layer
      ZNO2MOL5=ZNO2V5*ZFACT5                                   ! number of NO2 molecules in layer (pressure unit: kg m-1 s-2)
      ZNO2MOL =ZNO2V5*ZFACT +ZNO2V*ZFACT5
      ZNO2TAU5=1.E-04_JPRB*RLINO2(3,JWL)*ZNO2MOL5/(RMNO2*RG)  ! cross-section from cm2 molec-1 to m-2 molec-1
      ZNO2TAU =1.E-04_JPRB*RLINO2(3,JWL)*ZNO2MOL /(RMNO2*RG) 
      ZOTNO25(JL,JWL,JK)=ZNO2TAU5
      ZOTNO2 (JL,JWL,JK)=ZNO2TAU
    ENDDO
  ENDDO
ENDDO


!-- to start with (and to be replaced by more proper quantities once debugged!)
ZMCO2= 357.E-06_JPRB*RMCO2/RMD
ZMO2 = 0.20947_JPRB *ZO2  /RMD

ZOTCO25(1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO25 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTCO2 (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
ZOTO2  (1:KLEN,1:NWLID,1:KLEV)=0._JPRB
DO JWL=1,NWLID
DO JK=1,KLEV
  DO JL=1,KLEN
    ZTE5=ZTF5(JL,JK)-RLIT0
    ZTE25=ZTE5*ZTE5
    ZDPG5=ZDP5(JL,JK)/RG
    ZTE=ZTF(JL,JK)
    ZTE2=2._JPRB*ZTE*ZTE5
    ZDPG =ZDP (JL,JK)/RG


!-- CO2 -------------------------------
    ZPHIC5=EXP(RLICO2(3)*ZTE5+RLICO2(4)*ZTE25)
    ZPSIC5=EXP(RLICO2(5)*ZTE5+RLICO2(6)*ZTE25)
    ZPHIC =ZPHIC5*(RLICO2(3)*ZTE+RLICO2(4)*ZTE2)
    ZPSIC =ZPSIC5*(RLICO2(5)*ZTE+RLICO2(6)*ZTE2)

    ZUCO25 =ZPHIC5*ZMCO2*ZDPG5
    ZUPCO25=ZPSIC5*ZMCO2*ZDPG5*ZPF5(JL,JK)/RLIPREF
    ZUCO2  =ZUCO25* (ZPHIC/ZPHIC5+ZDPG/ZDPG5)
    ZUPCO2 =ZUPCO25*(ZPSIC/ZPSIC5+ZDPG/ZDPG5+ZPF(JL,JK)/ZPF5(JL,JK))

!-- Goody model for CO2
    ZGCO25=RLICO2(1)*ZUCO25/SQRT(1._JPRB+RLICO2(1)*ZUCO25*ZUCO25/(RLICO2(2)*ZUPCO25))
    ZGCO2=ZGCO25*(ZUCO2/ZUCO25-0.5_JPRB*(2._JPRB*ZUCO2/ZUCO25-ZUPCO2/ZUPCO25)/& 
     &              ((RLICO2(2)*ZUPCO25)/(RLICO2(1)*ZUCO25*ZUCO25)+1._JPRB))


    ZOTCO25(JL,JWL,JK)=ZGCO25
    ZOTCO2 (JL,JWL,JK)=ZGCO2

!-- O2 --------------------------------
    ZPHIO5=EXP(RLIO2(3) *ZTE5+RLIO2(4) *ZTE25)
    ZPSIO5=EXP(RLIO2(5) *ZTE5+RLIO2(6) *ZTE25)
    ZPHIO =ZPHIO5*(RLIO2(3) *ZTE+RLIO2(4) *ZTE2)
    ZPSIO =ZPSIO5*(RLIO2(5) *ZTE+RLIO2(6) *ZTE2)
    ZUO25 =ZPHIO5*ZMO2*ZDPG5
    ZUPO25=ZPSIO5*ZMO2*ZDPG5*ZPF5(JL,JK)/RLIPREF
    ZUO2  =ZUO25 *(ZPHIO/ZPHIO5+ZDPG/ZDPG5)
    ZUPO2 =ZUPO25*(ZPSIO/ZPSIO5+ZDPG/ZDPG5+ZPF(JL,JK)/ZPF5(JL,JK))
!-- Goody model for O2
    ZGO25=RLIO2(1)*ZUO25/SQRT(1._JPRB+RLIO2(1)*ZUO25*ZUO25/(RLIO2(2)*ZUPO25))
    ZGO2=ZGO25*(ZUO2/ZUO25-0.5_JPRB*(2._JPRB*ZUO2/ZUO25-ZUPO2/ZUPO25)/((RLIO2(2)*ZUPO25)/(RLIO2(1)*ZUO25*ZUO25)+1._JPRB))


    ZOTO25(JL,JWL,JK)=ZGO25
    ZOTO2 (JL,JWL,JK)=ZGO2

  ENDDO
 ENDDO
ENDDO



!*         3.      AEROSOL-RELATED QUANTITIES
!                  --------------------------
ZAERMSS5(:,:,:)=0._JPRB
ZAERMSS(:,:,:)=0._JPRB

DO JAER=1,INACTAERO
  DO JK=1,KLEV
    DO JL=1,KLEN
      IF (ZAERF5(JL,JK,JAER) > ZEPSAER) THEN 
        ZAERMSS5(JL,JK,JAER)= ZAERF5(JL,JK,JAER)* ZDP5(JL,JK) /RG
        ZAERMSS (JL,JK,JAER)= ZAERF5(JL,JK,JAER)* ZDP (JL,JK) /RG &
       &                     +ZAERF (JL,JK,JAER)* ZDP5(JL,JK) /RG
      ENDIF
    ENDDO
  ENDDO
ENDDO

!-- path from TOA
ZPATHS5(1:KLEN,1:NWLID,0:KLEV)=0._JPRB
ZPATHT5(1:KLEN,1:NWLID,0:KLEV)=0._JPRB
ZPATHS (1:KLEN,1:NWLID,0:KLEV)=0._JPRB
ZPATHT (1:KLEN,1:NWLID,0:KLEV)=0._JPRB

PLISIT(:,:,:)=0._JPRB
PLISIS(:,:,:)=0._JPRB
PULISIT(:,:,:)=0._JPRB
PULISIS(:,:,:)=0._JPRB
PEXTT(:,:,:)=0._JPRB
PEXTS(:,:,:)=0._JPRB

DO JWL=1,NWLID
  IWAVL=JWLID(JWL)

  DO JAER=1,INACTAERO
    ITYP=YAERO_DESC(JAER)%NTYP
    IBIN=YAERO_DESC(JAER)%NBIN

!-- ITYP is the aerosol type 1:SS,   2:DD,   3:OM    4:BC,   5:SU,   6:FA,   7:BS,   8=VS,
!   IBIN is the bin index: 1-3:SS, 1-3:DD,   2:OM,   2:BC,   2:SU,   1:FA,   1:SB    1=VS,
!   N.B.: extinction coefficients are in m2 g-1

    DO JK=1,KLEV
      DO JL=1,KLEN
        ZFAC = 1.0_JPRB
        IF (ITYP == 1) THEN
          ZALF(JAER)=ALF_SS(IRH(JL,JK),IWAVL,IBIN)
          ZOMG(JAER)=OMG_SS(IRH(JL,JK),IWAVL,IBIN)
          ZLIR(JAER)=RALI_SS(IRH(JL,JK),IWAVL,IBIN)
          ZFAC = RSS_RH80_MASSFAC
        ELSEIF (ITYP == 2) THEN
          ZALF(JAER)=ALF_DD(IBIN,IWAVL)
          ZOMG(JAER)=OMG_DD(IBIN,IWAVL)
          ZLIR(JAER)=RALI_DD(IBIN,IWAVL)
        ELSEIF (ITYP == 3) THEN
          ZALF(JAER)=ALF_OM(IRH(JL,JK),IWAVL)
          ZOMG(JAER)=OMG_OM(IRH(JL,JK),IWAVL)
          ZLIR(JAER)=RALI_OM(IRH(JL,JK),IWAVL)
        ELSEIF (ITYP == 4) THEN
          ZALF(JAER)=ALF_BC(IWAVL)
          ZOMG(JAER)=OMG_BC(IWAVL)
          ZLIR(JAER)=RALI_BC(IWAVL)
        ELSEIF (ITYP == 5 .OR. ITYP == 10) THEN
          ZALF(JAER)=ALF_SU(IRH(JL,JK),IWAVL)
          ZOMG(JAER)=OMG_SU(IRH(JL,JK),IWAVL)
          ZLIR(JAER)=RALI_SU(IRH(JL,JK),IWAVL)
!-- SO2 does not contribute to optical depth, only SO4 does.
          IF (IBIN == 2) THEN
            ZALF(JAER)=0._JPRB
            ZOMG(JAER)=0._JPRB
            ZLIR(JAER)=0._JPRB
          ENDIF
        ELSEIF (ITYP == 6) THEN
          ZALF(JAER)=ALF_NI(IRH(JL,JK),IWAVL,IBIN)
          ZOMG(JAER)=OMG_NI(IRH(JL,JK),IWAVL,IBIN)
          ZLIR(JAER)=RALI_NI(IRH(JL,JK),IWAVL,IBIN)
        ELSEIF (ITYP == 7) THEN
          ZALF(JAER)=ALF_AM(IRH(JL,JK),IWAVL)
          ZOMG(JAER)=OMG_AM(IRH(JL,JK),IWAVL)
          ZLIR(JAER)=RALI_AM(IRH(JL,JK),IWAVL)
        ELSEIF (ITYP == 8) THEN
          ZALF(JAER)=ALF_SOA(IRH(JL,JK),IWAVL,IBIN)
          ZOMG(JAER)=OMG_SOA(IRH(JL,JK),IWAVL,IBIN)
          ZLIR(JAER)=RALI_SOA(IRH(JL,JK),IWAVL,IBIN)
        ELSEIF (ITYP ==9) THEN
          IF (NVOLOPTP == 1) THEN
            ZALF(JAER)= ALF_SU(IEFRH, IWAVL)
            ZOMG(JAER)= OMG_SU(IEFRH, IWAVL)
            ZLIR(JAER)=RALI_SU(IEFRH, IWAVL)
          ELSEIF (NVOLOPTP == 2) THEN
            ZALF(JAER)= ALF_BC(IWAVL)
            ZOMG(JAER)= OMG_BC(IWAVL)
            ZLIR(JAER)=RALI_BC(IWAVL)
          ELSEIF (NVOLOPTP == 3) THEN
            ZALF(JAER)= ALF_DD(3,IWAVL)
            ZOMG(JAER)= OMG_DD(3,IWAVL)
            ZLIR(JAER)=RALI_DD(3,IWAVL)
          ENDIF
        ENDIF
        IF (ZLIR(JAER) /= 0._JPRB) THEN
          ZLIDRAT=1._JPRB/ZLIR(JAER)
        ELSE
          ZLIDRAT=ZLIR(JAER)
        ENDIF

!-- extinction within a layer (sum on aerosol contribution)
        ZAEROD5(JL,JWL,JK) = ZAEROD5(JL,JWL,JK) + ZAERMSS5(JL,JK,JAER) * ZFAC * 1.E+03_JPRB * ZALF(JAER)
        ZAEROD (JL,JWL,JK) = ZAEROD (JL,JWL,JK) + ZAERMSS (JL,JK,JAER) * ZFAC * 1.E+03_JPRB * ZALF(JAER)

!-- back-scatter within a layer
        ZBSC5(JL,JWL,JK) = ZBSC5(JL,JWL,JK) + ZAERMSS5(JL,JK,JAER) * ZFAC * 1.E+03_JPRB * ZALF(JAER) * ZLIDRAT
        ZBSC (JL,JWL,JK) = ZBSC (JL,JWL,JK) + ZAERMSS (JL,JK,JAER) * ZFAC * 1.E+03_JPRB * ZALF(JAER) * ZLIDRAT

      ENDDO
    ENDDO
  ENDDO

ZBOLTZ = 1.38E-23_JPRB ! (J/K, Boltzman constant)
! Calculation of molecular backscatter
  ZWLUM=RWLID(JWL)*1.0E+06_JPRB ! wavelenght in micrometers
    DO JK=1,KLEV
      DO JL=1,KLEN
! Molecular backscatter from Collis and Russel (1976)
! Trajectory
        ZBSCMOL5(JL,JWL,JK) = 5.45E-32_JPRB * ZPF5(JL,JK)/(ZBOLTZ * ZTF5(JL,JK)) * (ZWLUM/0.55_JPRB)**(-4.09_JPRB)
!Perturbation
        ZBSCMOL(JL,JWL,JK)  = 5.45E-32_JPRB * (1._JPRB/ZBOLTZ) * (ZPF(JL,JK)/ZTF5(JL,JK) - &
        &       ZPF5(JL,JK) * ZTF(JL,JK)/ ZTF5(JL,JK)**2) * (ZWLUM/0.55_JPRB)**(-4.09_JPRB)
      ENDDO
    ENDDO

  ZPATHS5(1:KLEN,JWL,KLEV)=0._JPRB
  ZPATHT5(1:KLEN,JWL,0   )=0._JPRB
  ZPATHS(1:KLEN,JWL,KLEV)=0._JPRB
  ZPATHT(1:KLEN,JWL,0   )=0._JPRB

  DO JK=1,KLEV
    IK=KLEV-JK
    DO JL=1,KLEN
!-- lidar signal seen from TOA
      ZPATHT5(JL,JWL,JK)=ZPATHT5(JL,JWL,JK-1)+2._JPRB*(ZAEROD5(JL,JWL,JK) &
      & +ZOTRAY5(JL,JWL,JK)+ZOTO35(JL,JWL,JK)+ZOTNO25(JL,JWL,JK)+ZOTO25(JL,JWL,JK)+ZOTCO25(JL,JWL,JK))
      ZPATHT(JL,JWL,JK) =ZPATHT (JL,JWL,JK-1)+2._JPRB*(ZAEROD (JL,JWL,JK) &
      & +ZOTRAY (JL,JWL,JK)+ZOTO3 (JL,JWL,JK)+ZOTNO2 (JL,JWL,JK)+ZOTO2 (JL,JWL,JK)+ZOTCO2 (JL,JWL,JK))

      ZUTOT5=200._JPRB
      ZUTOT =0._JPRB
      IF(ZPATHT5(JL,JWL,JK) < 200._JPRB) THEN
        ZUTOT5=ZPATHT5(JL,JWL,JK)
        ZUTOT=ZPATHT(JL,JWL,JK)
      ENDIF
      ZTRTOT5=EXP(-ZUTOT5)
      ZTRTOT =-ZUTOT*ZTRTOT5

      ZLISIT5(JL,JWL,JK)= RLICLS* (ZBSC5(JL,JWL,JK)/ZDZ5(JL,JK) + ZBSCMOL5(JL,JWL,JK) ) * ZTRTOT5

      PLISIT(JL,JWL,JK) = RLICLS* ( (ZBSC5(JL,JWL,JK)/ZDZ5(JL,JK) + ZBSCMOL5(JL,JWL,JK) ) * ZTRTOT + &
     &          ZBSC(JL,JWL,JK)/ZDZ5(JL,JK) * ZTRTOT5  - ZBSC5(JL,JWL,JK) * ZDZ(JL,JK)/ZDZ5(JL,JK)**2 * ZTRTOT5 )
! No perturbations in molecular backscatter
!   + &
!     &                               ZBSCMOL(JL,JWL,JK) * ZTRTOT5 ) 
! Perturbations in unattenuated backscatter from TOA (no molecolar contribution)
      PULISIT(JL,JWL,JK) = RLICLS* ( ZBSC(JL,JWL,JK)/ZDZ5(JL,JK) - ZBSC5(JL,JWL,JK) * ZDZ(JL,JK)/ZDZ5(JL,JK)**2 )
! Perturbations in extinction  from TOA
      PEXTT(JL,JWL,JK) = ZTRTOT


!-- lidar signal seen from the surface
      ZPATHS5(JL,JWL,IK)=ZPATHS5(JL,JWL,IK+1)+2._JPRB*(ZAEROD5(JL,JWL,IK+1) &
      & +ZOTRAY5(JL,JWL,IK+1)+ZOTO35(JL,JWL,IK+1)+ZOTNO25(JL,JWL,IK+1)+ZOTO25(JL,JWL,IK+1)+ZOTCO25(JL,JWL,IK+1))
      ZPATHS (JL,JWL,IK)=ZPATHS (JL,JWL,IK+1)+2._JPRB*(ZAEROD (JL,JWL,IK+1) &
      & +ZOTRAY (JL,JWL,IK+1)+ZOTO3 (JL,JWL,IK+1)+ZOTNO2 (JL,JWL,IK+1)+ZOTO2 (JL,JWL,IK+1)+ZOTCO2 (JL,JWL,IK+1))

      ZUSOT5=200._JPRB
      ZUSOT =0._JPRB
      IF(ZPATHS5(JL,JWL,IK) < 200._JPRB) THEN
        ZUSOT5=ZPATHS5(JL,JWL,IK)
        ZUSOT =ZPATHS (JL,JWL,IK)
      ENDIF
      ZTRSOT5=EXP(-ZUSOT5)
      ZTRSOT =-ZUSOT*ZTRSOT5

      ZLISIS5(JL,JWL,IK)=RLICLS* (ZBSC5(JL,JWL,IK+1)/ZDZ5(JL,IK+1) + ZBSCMOL5(JL,JWL,IK+1) ) * ZTRSOT5

      PLISIS(JL,JWL,IK) = RLICLS* ( (ZBSC5(JL,JWL,IK+1)/ZDZ5(JL,IK+1) + ZBSCMOL5(JL,JWL,IK+1) ) * ZTRTOT + &
     &                 ZBSC(JL,JWL,IK+1)/ZDZ5(JL,IK+1) * ZTRSOT5  - ZBSC5(JL,JWL,IK+1) * ZDZ(JL,IK+1)/ZDZ5(JL,IK+1)**2 * ZTRSOT5 )
! No perturbations in molecular backscatter
! + &  
!   &                 ZBSCMOL(JL,JWL,IK+1) * ZTRSOT5 )
! Perturbations in unattenuated backscatter from ground (no molecolar contribution)
      PULISIS(JL,JWL,IK) = RLICLS* ( ZBSC(JL,JWL,IK+1)/ZDZ5(JL,IK+1) - ZBSC5(JL,JWL,IK+1) * ZDZ(JL,IK+1)/ZDZ5(JL,IK+1)**2 )
! Perturbations in extinction  from ground 
      PEXTS(JL,JWL,IK) = ZTRSOT

    ENDDO
  ENDDO
ENDDO

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('AER_LIDSIMTL',1,ZHOOK_HANDLE)
END SUBROUTINE AER_LIDSIMTL
