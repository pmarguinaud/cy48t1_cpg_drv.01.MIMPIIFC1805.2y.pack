!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACACON ( YDML_PHY_MF,KIDIA,KFDIA,KLON,&
 !-----------------------------------------------------------------------
 ! - INPUT LOGICAL
 & LDSIMP,&
 ! - INPUT 1D .
 & PEXPA,PEXPN,PLSCP,PPART,PLSM,PNEIJ,PTS,&
 ! - INPUT/OUTPUT 1D .
& PQIST,PQLST,PQNST,PQRST,PQGST,PTST,&
 ! - OUTPUT 1D .
 & PACONI,PACONL,PACORL,PACOGI,PACOGL)

!**** *ACACON* - CALCULS D'AUTO-CONVERSION.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF (SUR UN NIVEAU).
!       CALCUL DE TROIS INCREMENTS D'AUTO-CONVERSION.
!     - COMPUTATION OF THREE AUTO-CONVERSION INCREMENTS.

!**   Interface.
!     ----------
!        *CALL* *ACACON*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLMPHYS" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.

! - ARGUMENT DE CONTROLE D'UNE VERSION SIMPLIFIEE (TOTALE AUTO-CONVERSION).

! LDSIMP     : IF .TRUE., CAS SIMPLIFIE.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D .

! PEXPA      : FONCTION DE DEPENDANCE EN TEMPERATURE SEUIL AUTOCONV < 0C.
! PEXPN      : FONCTION DE DEPENDANCE EN TEMPERATURE DES PROCESSUS < 0C.
! PLSCP      : CHALEUR DE FUSION SUR CHALEUR MASSIQUE A PRESSION CONSTANTE.
! PPART      : PROPORTION DE LA MAILLE CONCERNEE.
! PLSM       : LAND/SEA MASK.
! PNEIJ      : PROPORTION OF SNOW-COVERED LAND.
! PTS        : SURFACE TEMPERATURE.

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTRÉE-SORTIE.
!     --------------------------

! - 1D .

! PQIST      : QUANTITE COURANTE D'EAU SOLIDE NUAGEUSE.
! PQLST      : QUANTITE COURANTE D'EAU LIQUIDE NUAGEUSE.
! PQNST      : QUANTITE COURANTE D'EAU SOLIDE PRECIPITANTE.
! PQRST      : QUANTITE COURANTE D'EAU LIQUIDE PRECIPITANTE.
! PQGST      : QUANTITE COURANTE GRAUPEL
! PTST       : VALEUR COURANTE DE LA TEMPERATURE A L'ECHELLE DE LA MAILLE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 1D .

! PACONI     : INCREMENT D'AUTO-CONVERSION GLACE => NEIGE.
! PACONL     : INCREMENT D'AUTO-CONVERSION EAU LIQUIDE => NEIGE (W-B-F).
! PACORL     : INCREMENT D'AUTO-CONVERSION EAU LIQUIDE => PLUIE.
! PACOGI     : INCREMENT D'AUTO-CONVERSION GRAUPEL => NEIGE.
! PACOGL     : INCREMENT D'AUTO-CONVERSION GRAUPEL => PLUIE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY1/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        07-02, J.-F. Geleyn (extraction from APLMPHYS).

!     Modifications.
!     --------------
!        07-03, Option of the ARPEGE scheme - J.-F. Geleyn & J.-M. Piriou
!        07-12, Full auto-conversion simplification - R. Brozkova
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        J. Van den Bergh, adapting for prognostic graupel
!        Sep-2019, L. Gerard: Fix of temperature dependency of autoconversion.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RPI      ,RTT

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
LOGICAL           ,INTENT(IN)    :: LDSIMP 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEXPA(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEXPN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCP(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPART(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQIST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQLST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQNST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQRST(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQGST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTST(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PACONI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PACONL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PACORL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PACOGI(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PACOGL(KLON)

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZEPS1, ZHSEFN, ZHSEFL, ZALF, ZQLCR, ZQICR, ZPOISS,& 
 & ZAUTEFR, ZAUTEFS, ZARG1, ZARG2, ZALPH, ZBETA, ZDELT, ZQCR, ZFACICE,&
 & ZHSEFG, ZAUTEFG, ZWBFEFN, ZWBFEFG

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACACON',0,ZHOOK_HANDLE)
ASSOCIATE(RAUTEFR=>YDML_PHY_MF%YRPHY0%RAUTEFR, RAUTEFS=>YDML_PHY_MF%YRPHY0%RAUTEFS, &
 & RAUTSBET=>YDML_PHY_MF%YRPHY0%RAUTSBET, RQICRMIN=>YDML_PHY_MF%YRPHY0%RQICRMIN, RWBF2=>YDML_PHY_MF%YRPHY0%RWBF2, &
 & RWBF1=>YDML_PHY_MF%YRPHY0%RWBF1, RQICRT1=>YDML_PHY_MF%YRPHY0%RQICRT1, RQICRT2=>YDML_PHY_MF%YRPHY0%RQICRT2, &
 & RQICRSN=>YDML_PHY_MF%YRPHY0%RQICRSN, RQLCR=>YDML_PHY_MF%YRPHY0%RQLCR, RQICRMAX=>YDML_PHY_MF%YRPHY0%RQICRMAX, &
 & TMERGL=>YDML_PHY_MF%YRPHY1%TMERGL, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LARPMPS=>YDML_PHY_MF%YRPHY%LARPMPS, LA0MPS=>YDML_PHY_MF%YRPHY%LA0MPS, &
 & LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
!-----------------------------------------------------------------------

ZEPS1=1.E-10_JPRB

IF (LA0MPS) THEN

  ZPOISS=2.0_JPRB/SQRT(RPI)

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    IF (.NOT.LDSIMP) THEN
      ZAUTEFR=RAUTEFR
      ZAUTEFS=RAUTEFS*PEXPN(JLON)
      ZQLCR=RQLCR*ZPOISS
      ZQICR=(RQICRMIN+(RQICRMAX-RQICRMIN)*PEXPA(JLON))*ZPOISS
      ZALF=PQIST(JLON)/MAX(ZEPS1,PQLST(JLON)+PQIST(JLON))
      ZHSEFN=ZAUTEFS*TSPHY*(1.0_JPRB-EXP(-(PQIST(JLON)/ZQICR)**2))
      ZHSEFL=ZAUTEFR*TSPHY*(1.0_JPRB-EXP(-(PQLST(JLON)/ZQLCR)**2))
      IF (LGRAPRO) THEN
        ZAUTEFG=0.0_JPRB
        ZHSEFG=ZAUTEFG*TSPHY*(1.0_JPRB-EXP(-(PQIST(JLON)/ZQICR)**2))
        PACONI(JLON)=ZHSEFN*PQIST(JLON)/(1.0_JPRB+ZHSEFN+ZHSEFG)
        PACOGI(JLON)=ZHSEFG*PQIST(JLON)/(1.0_JPRB+ZHSEFN+ZHSEFG)
      ! WBF PROCESS, CONTRIBUTES TO GRAUPEL ONLY, NOT TO SNOW -> PACONL=0
        ZWBFEFG=ZAUTEFR*TSPHY*RWBF1*(1.0_JPRB-EXP(-(PQLST(JLON)*PQIST(JLON))&
         &/(ZQLCR*ZQICR*RWBF2**2)))*ZALF*(1.0_JPRB-ZALF)
        ZWBFEFN=0.0_JPRB
        PACORL(JLON)=ZHSEFL*(PQLST(JLON)/(1.0_JPRB+ZHSEFL+ZWBFEFN+ZWBFEFG))
        PACONL(JLON)=ZWBFEFN*(PQLST(JLON)/(1.0_JPRB+ZHSEFL+ZWBFEFN+ZWBFEFG))
        PACOGL(JLON)=ZWBFEFG*(PQLST(JLON)/(1.0_JPRB+ZHSEFL+ZWBFEFN+ZWBFEFG))
        PQGST(JLON)=PQGST(JLON)+PACOGL(JLON)+PACOGI(JLON)
        PQLST(JLON)=PQLST(JLON)-PACORL(JLON)-PACONL(JLON)-PACOGL(JLON)
        PQIST(JLON)=PQIST(JLON)-PACONI(JLON)-PACOGI(JLON)
        PTST(JLON)=PTST(JLON)+(PACONL(JLON)+PACOGL(JLON))*PLSCP(JLON)*PPART(JLON)
      ELSE
        PACONI(JLON)=ZHSEFN*PQIST(JLON)/(1.0_JPRB+ZHSEFN)
        ZWBFEFN=ZAUTEFR*TSPHY*RWBF1*(1.0_JPRB-EXP(-(PQLST(JLON)*PQIST(JLON))&
         &/(ZQLCR*ZQICR*RWBF2**2)))*ZALF*(1.0_JPRB-ZALF)
        PACORL(JLON)=ZHSEFL*(PQLST(JLON)/(1.0_JPRB+ZHSEFL+ZWBFEFN))
        PACONL(JLON)=ZWBFEFN*(PQLST(JLON)/(1.0_JPRB+ZHSEFL+ZWBFEFN))
        PQLST(JLON)=PQLST(JLON)-PACORL(JLON)-PACONL(JLON)
        PQIST(JLON)=PQIST(JLON)-PACONI(JLON)
        PTST(JLON)=PTST(JLON)+PACONL(JLON)*PLSCP(JLON)*PPART(JLON)
      ENDIF
!      PQLST(JLON)=PQLST(JLON)-PACORL(JLON)-PACONL(JLON)-PACOGL(JLON)
!      PQIST(JLON)=PQIST(JLON)-PACONI(JLON)-PACOGI(JLON)
      PQRST(JLON)=PQRST(JLON)+PACORL(JLON)
      PQNST(JLON)=PQNST(JLON)+PACONL(JLON)+PACONI(JLON)
    ELSE
      ZQLCR=RQLCR*ZPOISS
      ZQICR=(RQICRMIN+(RQICRMAX-RQICRMIN)*PEXPA(JLON))*ZPOISS
      ZALF=PQIST(JLON)/MAX(ZEPS1,PQLST(JLON)+PQIST(JLON))
      PACONI(JLON)=PQIST(JLON)
      ZHSEFL=(1.0_JPRB-EXP(-(PQLST(JLON)/ZQLCR)**2))
      IF (LGRAPRO) THEN
        PACOGI(JLON)=0.0_JPRB
        ZWBFEFG=RWBF1*(1.0_JPRB-EXP(-(PQLST(JLON)*PQIST(JLON))&
         &/(ZQLCR*ZQICR*RWBF2**2)))*ZALF*(1.0_JPRB-ZALF)
        ZWBFEFN=0.0_JPRB
        PACONL(JLON)=ZWBFEFN*(PQLST(JLON)/MAX(ZEPS1,ZHSEFL+ZWBFEFN+ZWBFEFG))
        PACOGL(JLON)=ZWBFEFG*(PQLST(JLON)/MAX(ZEPS1,ZHSEFL+ZWBFEFN+ZWBFEFG))
        PACORL(JLON)=PQLST(JLON)-PACONL(JLON)-PACOGL(JLON)
        PQLST(JLON)=PQLST(JLON)-PACORL(JLON)-PACONL(JLON)-PACOGL(JLON)
        PQIST(JLON)=PQIST(JLON)-PACONI(JLON)-PACOGI(JLON)
        PTST(JLON)=PTST(JLON)+(PACONL(JLON)+PACOGL(JLON))*PLSCP(JLON)*PPART(JLON)
      ELSE
        ZWBFEFN=RWBF1*(1.0_JPRB-EXP(-(PQLST(JLON)*PQIST(JLON))&
         &/(ZQLCR*ZQICR*RWBF2**2)))*ZALF*(1.0_JPRB-ZALF)
        PACONL(JLON)=ZWBFEFN*(PQLST(JLON)/MAX(ZEPS1,ZHSEFL+ZWBFEFN))
        PACORL(JLON)=PQLST(JLON)-PACONL(JLON)
        PQLST(JLON)=PQLST(JLON)-PACORL(JLON)-PACONL(JLON)
        PQIST(JLON)=PQIST(JLON)-PACONI(JLON)
        PTST(JLON)=PTST(JLON)+PACONL(JLON)*PLSCP(JLON)*PPART(JLON)
      ENDIF
      PQRST(JLON)=PQRST(JLON)+PACORL(JLON)
      PQNST(JLON)=PQNST(JLON)+PACONL(JLON)+PACONI(JLON)
    ENDIF
  ENDDO

ENDIF

IF (LARPMPS) THEN

  ZARG1=2.0_JPRB*RQICRMAX*(1.0_JPRB-0.999_JPRB)/(RQICRMAX-RQICRMIN)-1.0_JPRB
  ZARG2=2.0_JPRB*(RQICRMAX-1.5_JPRB*RQICRMIN)/(RQICRMAX-RQICRMIN)-1.0_JPRB
  ZARG1=0.5_JPRB*LOG(ABS((1.0_JPRB+ZARG1)/(1.0_JPRB-ZARG1)))
  ZARG2=0.5_JPRB*LOG(ABS((1.0_JPRB+ZARG2)/(1.0_JPRB-ZARG2)))
  ZALPH=(ZARG1-ZARG2)/(RQICRT2-RQICRT1)
  ZBETA=ZARG1-RQICRT2*ZALPH

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    IF (.NOT.LDSIMP) THEN
      ZAUTEFR=RAUTEFR
      ZDELT=PTST(JLON)-RTT
      ZAUTEFS=RAUTEFS*EXP(RAUTSBET*ZDELT)
      ZQCR=RQICRMAX-(RQICRMAX-RQICRMIN)*0.5_JPRB*(1.0_JPRB+TANH(ZALPH*ZDELT&
       &+ZBETA))
      ZFACICE=PLSM(JLON)*PNEIJ(JLON)+(1.0_JPRB-PLSM(JLON))&
       &*MAX(0.0_JPRB,SIGN(1.0_JPRB,TMERGL-PTS(JLON)))
      ZQCR=ZQCR*(1.0_JPRB-ZFACICE*(1.0_JPRB-RQICRSN))
      PACONI(JLON)=MAX(0.0_JPRB,(1.0_JPRB-EXP(-ZAUTEFS*TSPHY))*(PQIST(JLON)&
       &-ZQCR))
      PACORL(JLON)=MAX(0.0_JPRB,(1.0_JPRB-EXP(-ZAUTEFR*TSPHY))*(PQLST(JLON)&
       &-RQLCR))
      IF (LGRAPRO) THEN
        PACONL(JLON)=0.0_JPRB
      ELSE
        PACONL(JLON)=PACORL(JLON)*MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZDELT))
      ENDIF
      PACORL(JLON)=PACORL(JLON)-PACONL(JLON)
      PQLST(JLON)=PQLST(JLON)-PACORL(JLON)
      PQIST(JLON)=PQIST(JLON)-PACONI(JLON)
      PQRST(JLON)=PQRST(JLON)+PACORL(JLON)
      PQNST(JLON)=PQNST(JLON)+PACONI(JLON)
    ELSE
      ZDELT=PTST(JLON)-RTT
      PACONI(JLON)=PQIST(JLON)
      PACORL(JLON)=PQLST(JLON)
      IF (LGRAPRO) THEN
        PACONL(JLON)=0.0_JPRB
      ELSE
        PACONL(JLON)=PACORL(JLON)*MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZDELT))
      ENDIF
      PACORL(JLON)=PACORL(JLON)-PACONL(JLON)
      PQLST(JLON)=PQLST(JLON)-PACORL(JLON)
      PQIST(JLON)=PQIST(JLON)-PACONI(JLON)
      PQRST(JLON)=PQRST(JLON)+PACORL(JLON)
      PQNST(JLON)=PQNST(JLON)+PACONI(JLON)
    ENDIF
  ENDDO

ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACACON',1,ZHOOK_HANDLE)
END SUBROUTINE ACACON

