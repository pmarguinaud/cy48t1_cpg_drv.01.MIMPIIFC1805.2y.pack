!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCDEV (YDCST, YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  1D VERTICAL.
 & PVETAF,&
 ! - INPUT  2D .
 & PAPHI,PAPRS,PAPRSF,PCP,PDELP,PQ,PQI,PQL,PQN,PQR,PQG,PQW,PR,PT,&
 & PTW,PNEBCOND,PHCRICS,PRMF,PQSATS,PNCV,PRHDFDA,PRKTTEND,&
 & PRKQVTEND, PRKQCTEND,PQC_CVPP,PNEB_CVPP,&
 ! - INPUT  1D .
 & PLSM,PNEIJ,PGM,PTS,PDECRD,&
 ! - OUTPUT 2D .
 & PFASL,PFASN,PFASG,PFCSQL,PFCSQN,PFESL,PFESN,PFESG,PFPLSL,PFPLSN,PFPLSG,PSEDIQL,PSEDIQI,&
 ! - OUTPUT 1D .
 & PGRVINT)

!**** *ACCDEV* - CALCUL DES FLUX DE CONDENSATION ET/OU PRECIPITATION
!                STRATIFORME.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX DE CONDENSATION ET/OU PRECIPITATION STRATIFORME
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF STRATIFORM CONDENSATION AND/OR PRECIPITATION FLUXES
!              (WATER AND SNOW) .

!     - CALCUL DES PSEUDO-FLUX LIES AUX PRECIPITATIONS STRATIFORMES
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF PSEUDO-FLUXES LINKED TO STRATIF. PRECIPITATIONS
!              (WATER AND SNOW) .

!**   Interface.
!     ----------
!        *CALL* *ACCDEV*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D (1:KLEV) .

! PVETAF     : LA COORDONNEE VERTICALE AUX NIVEAUX PLEIN.

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPRSF     : PRESSION AUX NIVEAUX PLEIN.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PTW        : WET-BULB TEMPERATURE
! PNEBCOND   : NEBULOSITE DIAGNOSTIQUE POUR CONDENSATION/EVAPORATION.
! PHCRICS    : HUMIDITE RELATIVE CRITIQUE POUR CONDENSATION/EVAPORATION.
! PRMF       : PROPORTION DE LA GLACE.
! PQSATS     : HUMIDITE SPECIFIQUE DE SATURATION (SCHEMA COND./EVAP.).
! PNCV       : CONVECTIVE CLOUDINESS (HISTORIC).
! PRHDFDA    : CHANGE IN RH OVER THE CLOUD
! PQC_CVPP   : CONDENSAT DUE A LA CONEVCTION PEU PROFONDE.
! PNEB_CVPP  : NEBULOSITE DUE A LA CONEVCTION PEU PROFONDE.

! - 1D 

! PLSM       : INDICE TERRE/MER.
! PNEIJ      : PROPORTION DE SOL ENNEIGE.
! PGM        : FACTEUR D'ECHELLE.
! PTS        : TEMPERATURE DE SURFACE.
! PDECRD     : DECORRELATION DEPTH FOR CLOUD OVERLAPS [Pa].

! ADDITIONAL PROGNOSTIC VARIABLES

! PQI        : RATIO OF SUSPENDED ICE
! PQL        : RATIO OF SUSPENDED LIQUID WATER
! PQN        : RATIO OF SNOW
! PQR        : RATIO OF RAIN WATER

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PFPLSL     : PRECIPITATIONS STRATIFORMES SOUS FORME LIQUIDE.
! PFPLSN     : PRECIPITATIONS STRATIFORMES SOUS FORME NEIGE.
! PSEDIQL    : SEDIMENTATION FLUX OF LIQUID CLOUD WATER.
! PSEDIQI    : SEDIMENTATION FLUX OF SOLID CLOUD WATER.

! - 1D .

! PGRVINT    : VERTICAL INTEGRAL OF GRAUPEL.

! PROGNOSTIC PSEUDO FLUXES (WITH RESPECT TO VAPOUR)

! PFASL      : STRATIFORM AUTOCONVERSION (LIQUID)
! PFASN      : STRATIFORM AUTOCONVERSION (SOLID)
! PFCSQL     : STRATIFORM CONDENSATION (LIQUID)
! PFCSQN     : STRATIFORM CONDENSATION (SOLID)
! PFESL      : STRATIFORM EVAPORATION OF RAIN
! PFESN      : STRATIFORM EVAPORATION OF SNOW 

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        07-02, R. Brozkova.

!     Modifications.
!     --------------
!     S. Ivatek-Sahdan 29.03.2007. : passive bug reported by R. Brozkova
!     K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!     L. Bengtsson, K-I. Ivarsson Nov 2011: RK-scheme bug-fix, introduce 
!                   call to HLEVAPPREC and HLSNOWMELT
!     R. Brozkova (Feb 2012): introduction of adjustment after Smith 1990 paper;
!                     sedimentation of cloud water, treatment of SC condensates.
!     J. Masek (Apr 2016): Exponential-random cloud overlap with variable
!                          decorrelation depth.
!     L. Gerard (Sep 2019): Fix of cloud water under LXRCDEV.
!-------------------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST    ,ONLY : TCST
!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVETAF(KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQN(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQG(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEBCOND(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PHCRICS(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRHDFDA(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRMF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQSATS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNCV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQC_CVPP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEB_CVPP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDECRD(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRKTTEND(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRKQVTEND(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRKQCTEND(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFASL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFASN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFASG(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFESL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFESN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFESG(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSG(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSEDIQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSEDIQI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGRVINT(KLON)


!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZPOID(KLON,KLEV),ZIPOI(KLON,KLEV),ZDQ(KLON,KLEV)&
 & ,ZFCSQL(KLON,0:KLEV),ZFCSQN(KLON,0:KLEV),ZQLTMP(KLON,KLEV),ZQITMP(KLON,KLEV)&
 & ,ZQTMP(KLON,KLEV),ZLHV(KLON,KLEV),ZLHS(KLON,KLEV),ZDQM(KLON,KLEV)&
 & ,ZFALLR(KLON,KLEV),ZFALLS(KLON,KLEV),ZFALLG(KLON,KLEV),ZRCVOTT(KLON,0:KLEV)&
 & ,ZTCORR(KLON,KLEV),ZMELNET(KLON,KLEV),ZCME(KLON,0:KLEV)&
 & ,ZCWI(KLON,KLEV),ZQCS(KLON,KLEV),ZNEBCOND(KLON,KLEV)&
  !TEMPORARY GRAUPEL SOLUTION
 & ,ZMELGET(KLON,KLEV)

REAL(KIND=JPRB) :: ZCALPHA(KLON), ZCBETA(KLON), ZCBETAH(KLON), ZCGAMMA(KLON), &
 & ZCGAMAH(KLON), ZRCGAMA(KLON), ZCLDM(KLON), ZCLDMAX(KLON),ZRELHUM(KLON), &
 & ZICWC(KLON), ZCSIGMA(KLON), ZCMEC1(KLON), ZCMEC2(KLON), ZCMEC3(KLON), &
 & ZCMEC4(KLON)

INTEGER(KIND=JPIM) :: JLEV, JLON

REAL(KIND=JPRB) :: ZEPS1, ZGDTI, ZRMLOC, ZCONL, ZCONI, ZTESTB, ZALF, &
 & ZQ, ZQL, ZQI, ZQVN, ZNEBM1, ZDQVN, ZTEST, ZRNEB, ZQVCS, &
 & ZQT, ZQC9, ZICE, ZEPQC, ZNEIGE, ZSQRT6, ZRDSRV, ZDELTA, ZRHC, &
 & ZDQI, ZDQL, ZFRACON, ZLEFF, ZTLIQ, ZSIGS, ZRATQ, ZRAT2, ZQC, &
 & ZQSAT, ZNEBT, ZZ, ZZRHC, ZOMSM, ZOMEPS, ZMINCLD,ZCMEI,ZCMEL, &
 & ZESN, ZDESDTM, ZDENOM, ZDQSDT, ZQSN, ZQTEMP, ZTTEMP, &
 & ZTEMPCME, ZNEBTOT, ZQSATS, ZEPSILO, ZWEIGHT, ZFAC, ZADJL, ZADJI, &
 & ZQCST,ZEVAPS,ZEVAP,ZCEVAP,ZRAINRES,ZSNOWRES,ZDZ,ZNEWRAIN,&
 & ZMELTHEAT

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "aplmphys.intfb.h"
#include "hlevapprec.intfb.h"
#include "hlsnowmelt.intfb.h"
#include "acnebsm.intfb.h"
#include "fctdoi.ycst.h"
#include "fcttrm.ycst.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCDEV',0,ZHOOK_HANDLE)
ASSOCIATE(RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, RSMDTX=>YDML_PHY_MF%YRPHY0%RSMDTX, ADJTAU=>YDML_PHY_MF%YRPHY0%ADJTAU, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LSMGCDEV=>YDML_PHY_MF%YRPHY%LSMGCDEV, LSMNIMBT=>YDML_PHY_MF%YRPHY%LSMNIMBT, LXRCDEV=>YDML_PHY_MF%YRPHY%LXRCDEV, &
 & LRKCDEV=>YDML_PHY_MF%YRPHY%LRKCDEV, LSMITH_CDEV=>YDML_PHY_MF%YRPHY%LSMITH_CDEV, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, &
 & LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CALCUL DE PARAMETRES DERIVES ET CONSTANTES DE SECURITE.

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS.

ZGDTI=1.0_JPRB/(YDCST%RG*TSPHY)
ZEPS1=1.E-10_JPRB

ZEPQC=1.E-14_JPRB
ZSQRT6=SQRT(6._JPRB)
ZRDSRV=YDCST%RD/YDCST%RV

IF (LNEIGE) THEN
  ZNEIGE=1.0_JPRB
ELSE
  ZNEIGE=0.0_JPRB
ENDIF

ZWEIGHT=1.0_JPRB-EXP(-TSPHY/ADJTAU)
!*
!     ------------------------------------------------------------------
!     II - CALCULS PRELIMINAIRES DE L'EPAISSEUR EN PRESSION DES COUCHES
!     DIVISEE PAR G*DT POUR CHAQUE NIVEAU. INVERSE DE CETTE QUANTITE.

!          PRELIMINARY COMPUTATIONS OF THE LAYER'S PRESSURE THICKNESS
!     DIVIDED BY G*DT FOR ALL LEVELS. INVERSE OF THIS QUANTITY.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZPOID     : DP/(RG*DT) POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!           : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
! ZIPOI     : INVERSE DE ZPOID.
!           : INVERSE OF ZPOID.

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=1.0_JPRB/ZPOID(JLON,JLEV)
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     III - CALCULS DE CONDENSATION-EVAPORATION.

!           CONDENSATION-EVAPORATION COMPUTATIONS.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFCSQL    : PSEUDO-FLUX TEMPORAIRE POUR CONDENSATION MOINS EVAPORATION.
!           : TEMPORARY PSEUDO-FLUX FOR CONDENSATION MINUS EVAPORATION.
! ZFCSQN    : PSEUDO-FLUX TEMPORAIRE POUR CONDENSATION SOLIDE MOINS SUBLIMATION.
!           : TEMPORARY PSEUDO-FLUX FOR SOLID CONDENSATION MINUS SUBLIMATION.
! ZLHS      : CHALEUR LATENTE DE SUBLIMATION.
!           : SUBLIMATION LATENT HEAT.
! ZLHV      : CHALEUR LATENTE DE VAPORISATION.
!           : VAPORISATION LATENT HEAT.
! ZQTMP     : PQ EVOLUTIF PROTEGE CONTRE LES VALEURS NEGATIVES.
!           : NEGATIVE-PROTECTED EVOLUTIVE VALUE OF PQ.
! ZQITMP    : PQI EVOLUTIF PROTEGE CONTRE LES VALEURS NEGATIVES.
!           : NEGATIVE-PROTECTED EVOLUTIVE VALUE OF PQI.
! ZQLTMP    : PQL EVOLUTIF PROTEGE CONTRE LES VALEURS NEGATIVES.
!           : NEGATIVE-PROTECTED EVOLUTIVE VALUE OF PQL.
! ZRCVOTT   : DEGRE D'INHOMOGENEITE SUPPOSE DES PRECIPITATIONS.
!           : INHOMOGENEITY DEGREE OF PRECIPITATIONS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZDQM      : DEFICIT MAXIMUM DE SATURATION.
!           : MAXIMUM OF SATURATION DEFICIT.

DO JLON=KIDIA,KFDIA
  ZFCSQL(JLON,KTDIA-1)=0.0_JPRB
  ZFCSQN(JLON,KTDIA-1)=0.0_JPRB
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZLHV(JLON,JLEV)=FOLH(PT(JLON,JLEV),0.0_JPRB)
    ZLHS(JLON,JLEV)=FOLH(PT(JLON,JLEV),1.0_JPRB)
  ENDDO
ENDDO

ZRCVOTT(:,:)=0.0_JPRB
ZTCORR(:,:)=0.0_JPRB
ZDQM(:,:)=PQW(:,:)

IF(LXRCDEV) THEN

!     ------------------------------------------------------------------
!     IIIa - SCHEMA D'APRES ACPLUIE_PROG. 

!            SCHEME AFTER ACPLUIE_PROG.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZDQ       : CORRECTION DE Q TOTAL POUR PASSER A L'EQUILIBRE SATURE.
!           : CORRECTION OF TOTAL Q TO REACH THE SATURATION BALANCE.

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! ZRNEB=1/Nt for vapour
      ZNEBT=PNEBCOND(JLON,JLEV)&
       &+PNCV(JLON,JLEV)-PNEBCOND(JLON,JLEV)*PNCV(JLON,JLEV)
      ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB, ZNEBT-ZEPS1))
      ZRNEB=ZZ*PNEBCOND(JLON,JLEV)/(ZNEBT+1._JPRB-ZZ)
      ZFAC=ZRNEB+(1._JPRB-ZZ) 
      ZQVCS=MAX(0.0_JPRB,PQ(JLON,JLEV)-PQW(JLON,JLEV)*ZNEBT)
      ZQ=(PQ(JLON,JLEV)-ZQVCS)*ZRNEB+ZQVCS/MAX(ZEPS1,1.0_JPRB-PNCV(JLON,JLEV))
      ZADJL=ZWEIGHT*((1.0_JPRB-PRMF(JLON,JLEV))*(PQL(JLON,JLEV)+PQI(JLON,JLEV))&
       &-PQL(JLON,JLEV))
      ZADJI=ZWEIGHT*(PRMF(JLON,JLEV)*(PQL(JLON,JLEV)+PQI(JLON,JLEV))&
       &-PQI(JLON,JLEV))
      ZQL=(PQL(JLON,JLEV)+ZADJL)*ZFAC
      ZQI=(PQI(JLON,JLEV)+ZADJI)*ZFAC
      ZALF=ZQI/MAX(ZEPS1,ZQL+ZQI)

      ZNEBM1=1.0_JPRB-PNEBCOND(JLON,JLEV)
      ZQVN=PQW(JLON,JLEV)*(1.0_JPRB+(PHCRICS(JLON,JLEV)-1.0_JPRB)*ZNEBM1)

      ZDQVN=ZQ-ZQVN
      ZTEST=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZQVN-(ZQ+ZQL+ZQI)))
      ZTESTB=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDQVN))
      ZRMLOC=(1.0_JPRB-ZTESTB)*ZALF+ZTESTB*PRMF(JLON,JLEV)
      ZCONL=(1.0_JPRB-ZRMLOC)*(1.0_JPRB-ZTEST)*ZDQVN-ZTEST*ZQL
      ZCONI=ZRMLOC*(1.0_JPRB-ZTEST)*ZDQVN-ZTEST*ZQI
      ZDQ(JLON,JLEV)=PQW(JLON,JLEV)-MIN(ZQVN,ZQ+ZQL+ZQI)

      ZFCSQL(JLON,JLEV)=ZFCSQL(JLON,JLEV-1)+ZPOID(JLON,JLEV)*(1.0_JPRB&
       &-PNCV(JLON,JLEV))*(ZCONL+ZADJL)
      ZFCSQN(JLON,JLEV)=ZFCSQN(JLON,JLEV-1)+ZPOID(JLON,JLEV)*(1.0_JPRB&
       &-PNCV(JLON,JLEV))*(ZCONI+ZADJI)

      ZQLTMP(JLON,JLEV)=MAX(0.0_JPRB,ZQL+ZCONL)
      ZQITMP(JLON,JLEV)=MAX(0.0_JPRB,ZQI+ZCONI)
      ZQTMP(JLON,JLEV)=MAX(0.0_JPRB,ZQ-ZCONL-ZCONI)
    ENDDO
  ENDDO

ENDIF ! LXRCDEV

IF(LSMGCDEV) THEN

!     ------------------------------------------------------------------
!     IIIb - SCHEMA SMITH-GERARD.

!            SMITH-GERARD SCHEME.

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

! Total cloud fraction

      ZNEBT=PNEBCOND(JLON,JLEV)&
       &+PNCV(JLON,JLEV)-PNEBCOND(JLON,JLEV)*PNCV(JLON,JLEV)
      ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB, ZNEBT-ZEPS1))

! Convective proportion

      ZFRACON=ZZ*PNCV(JLON,JLEV)/(ZNEBT+(1.0_JPRB-ZZ))
      ZQC9=PQI(JLON,JLEV)+PQL(JLON,JLEV)
      ZQT=ZQC9+PQ(JLON,JLEV)
      ZRHC=PHCRICS(JLON,JLEV)
      ZICE=PRMF(JLON,JLEV)

!-----------------
! TOTAL CONDENSATE
!-----------------

      ZQSAT=PQSATS(JLON,JLEV)

      ZLEFF=ZLHV(JLON,JLEV)*(1.0_JPRB-ZICE)+ZLHS(JLON,JLEV)*ZICE
      ZTLIQ=PT(JLON,JLEV)-&
     & (ZLHV(JLON,JLEV)*PQL(JLON,JLEV)+ZLHS(JLON,JLEV)*PQI(JLON,JLEV))/&
     & PCP(JLON,JLEV)

      ZSIGS=(1.0_JPRB-ZRHC) *ZQSAT/&
     &      (ZSQRT6 * (1.0_JPRB + (ZLEFF * ZLEFF * ZQSAT * ZRDSRV)/&
     &      (PR(JLON,JLEV) * ZTLIQ * ZTLIQ * PCP(JLON,JLEV))) )

      ZRATQ=ZQT/ZQSAT
      ZZRHC=MIN(ZRHC,1.0_JPRB-ZEPQC)
      ZRATQ=MAX(ZZRHC,MIN(2.0_JPRB-ZZRHC,ZRATQ))
      ZRAT2=(ZRATQ-1.0_JPRB)/(1.0_JPRB-ZZRHC)

      ZTEST=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRATQ-1.0_JPRB))
      ZQC=(ZTEST*(6.0_JPRB*ZRAT2+(1.0_JPRB-ZRAT2)**3)+&
        &(1.0_JPRB-ZTEST)*(1.0_JPRB+ZRAT2)**3)/ZSQRT6
      ZQC=ZQC*ZSIGS

! Control of warming.

      ZQC=MIN(ZQC, MAX(0.0_JPRB,ZQC9)+RSMDTX*PCP(JLON,JLEV)/ZLEFF)

! Direct protection  of convective condensate against evaporation:

      ZQC=MAX(ZQC, ZQC9*ZFRACON)
      
! Repartition by FONICE.

      ZQL=(1.0_JPRB-ZICE)*ZQC
      ZQI=ZQC-ZQL

!-----------------------------------------
! PREVENT MELTING AT NEGATIVE TEMPERATURE.
!-----------------------------------------

      IF (LSMNIMBT) THEN

! Prevent melting of existing ice below RTT.

        ZDELTA=ZNEIGE*MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-PT(JLON,JLEV)))
        ZZ=MAX(0.0_JPRB, SIGN(1.0_JPRB,PQI(JLON,JLEV)-ZQI))*ZDELTA

        ZQI=ZQI*(1.0_JPRB-ZZ) + &
          & ZZ* PQI(JLON,JLEV)*MIN(1.0_JPRB,ZQC/MAX(ZEPQC,ZQC9))

      ENDIF

! Additional security required to avoid rounding error problems.

      ZQI=MIN(ZQC,ZQI)
      ZQL=MAX(0.0_JPRB,ZQC-ZQI)

!---------------------------------------------------
! COMPUTE LARGE-SCALE CONDENSATION/EVAPORATION FLUX.
!---------------------------------------------------

      ZDQL=ZQL - PQL(JLON,JLEV)
      ZDQI=ZQI - PQI(JLON,JLEV)

! Stratiform fluxes:

      ZFCSQL(JLON,JLEV) = ZFCSQL(JLON,JLEV-1)+ ZPOID(JLON,JLEV)*ZDQL
      ZFCSQN(JLON,JLEV) = ZFCSQN(JLON,JLEV-1)+ ZPOID(JLON,JLEV)*ZDQI

! Update in case we call microphysics (LSTRAPRO):

      ZQLTMP(JLON,JLEV)=MAX(0.0_JPRB,PQL(JLON,JLEV)+ZDQL)
      ZQITMP(JLON,JLEV)=MAX(0.0_JPRB,PQI(JLON,JLEV)+ZDQI)
      ZQTMP(JLON,JLEV)=MAX(0.0_JPRB,PQ(JLON,JLEV)-ZDQL-ZDQI)
      ZDQ(JLON,JLEV)=MAX(0.0_JPRB,PQW(JLON,JLEV)-ZQTMP(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF ! LSMGCDEV

IF(LRKCDEV) THEN

!     ------------------------------------------------------------------
!     IIIc - SCHEMA D'APRES RASCH-KRISTJANSSON.

!            SCHEME AFTER RASCH-KRISTJANSSON. (AND ZHANG ET. AL 2003)

! Constants for this scheme
  ZMINCLD = 1.E-4_JPRB
  ZOMSM=1._JPRB - 1.E-10_JPRB
  ZOMEPS=1._JPRB - 1.E-10_JPRB
  ZEPSILO=YDCST%RMV/YDCST%RMD
 
! Initialize:
  ZCLDMAX=0._JPRB
  ZICE=0._JPRB
  ZTEMPCME=0._JPRB
 
 
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

! Define the coefficients for Condensation - Evaporation calculations

      ZICE=PRMF(JLON,JLEV)
      ZQSATS=PQSATS(JLON,JLEV)
      ZNEBTOT=PNEBCOND(JLON,JLEV)
         
      ZQTMP(JLON,JLEV)=MAX(0._JPRB,PQ(JLON,JLEV))
      ZQLTMP(JLON,JLEV)=MAX(0._JPRB,PQL(JLON,JLEV))
      ZQITMP(JLON,JLEV)=MAX(0._JPRB,PQI(JLON,JLEV))        
      ZCWI(JLON,JLEV)=MAX(0._JPRB,PQL(JLON,JLEV)+PQI(JLON,JLEV))
 
      ZESN=FOEW(PT(JLON,JLEV),1._JPRB)*ZICE+&
     & FOEW(PT(JLON,JLEV),0._JPRB)*(1._JPRB-ZICE)
      ZDESDTM=FOEW(PT(JLON,JLEV),1._JPRB)*FODLEW(PT(JLON,JLEV),1._JPRB)*ZICE+&
     & FOEW(PT(JLON,JLEV),0._JPRB)*FODLEW(PT(JLON,JLEV),0._JPRB)*(1._JPRB-ZICE)
      ZDENOM=PAPRSF(JLON,JLEV)-(1._JPRB-ZEPSILO)*ZESN
      ZDQSDT=PAPRSF(JLON,JLEV)*ZEPSILO*ZDESDTM/ZDENOM**2
       
      ZRELHUM(JLON) = MAX(0._JPRB,MIN(1._JPRB,ZQTMP(JLON,JLEV)/ZQSATS))

      ZCLDM(JLON)=MAX(ZNEBTOT,ZMINCLD)
      ZCLDMAX(JLON)=MAX(ZCLDMAX(JLON),ZCLDM(JLON))
      
      CALL HLEVAPPREC(YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,JLEV,PQN(JLON,JLEV),PQR(JLON,JLEV), &
     & PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQSATS(JLON,JLEV), &
     & PDELP(JLON,JLEV),PQ(JLON,JLEV),ZCLDMAX(JLON),      &
     & 1.25_JPRB,0.8_JPRB,                                &
     & ZSNOWRES,ZRAINRES,ZDZ,                             &
     & ZEVAPS,ZEVAP,ZCEVAP)

      CALL HLSNOWMELT(YDML_PHY_MF%YRPHY2,ZSNOWRES,ZRAINRES, &
     & PT(JLON,JLEV),PTW(JLON,JLEV),PAPRSF(JLON,JLEV),ZDZ,  &
     & PDELP(JLON,JLEV),PQ(JLON,JLEV),ZCLDMAX(JLON),1._JPRB,&
     & ZNEWRAIN)

      ZMELTHEAT = -(YDCST%RLSTT-YDCST%RLVTT) * ZNEWRAIN * YDCST%RG/YDCST%RCPD/PDELP(JLON,JLEV)
     
!PART 2: Compute and define the coefficients for C-E calculations

      ZCALPHA(JLON)=1._JPRB/ZQSATS
      ZCBETA(JLON)=ZQTMP(JLON,JLEV)/ZQSATS**2*ZDQSDT
      ZCBETAH(JLON)=1._JPRB/ZQSATS*ZDQSDT
      ZCGAMMA(JLON)=ZCALPHA(JLON)+YDCST%RLVTT*ZCBETA(JLON)/YDCST%RCPD
      ZCGAMAH(JLON)=ZCALPHA(JLON)+YDCST%RLVTT*ZCBETAH(JLON)/YDCST%RCPD
      ZRCGAMA(JLON)=ZCGAMMA(JLON)/ZCGAMAH(JLON)

      IF (ZCLDM(JLON)>ZMINCLD) THEN
        ZICWC(JLON)=MAX(0._JPRB,ZCWI(JLON,JLEV)/ZCLDM(JLON))
      ELSE
        ZICWC(JLON)=0._JPRB
      ENDIF
         
      IF (ZICWC(JLON) < 1.0E-15_JPRB) ZICWC(JLON)=0._JPRB

!PART 4:Compute the tendency of cloud condensate CME (ice + water)  
  
      IF(ZRELHUM(JLON)>PHCRICS(JLON,JLEV)) THEN

!  1. Whole grid saturation
!  ========================

        IF(ZRELHUM(JLON)>=0.999.OR.ZCLDM(JLON)>=0.999) THEN

          ZCME(JLON,JLEV)=(ZCALPHA(JLON)*PRKQVTEND(JLON,JLEV)-&
         & ZCBETAH(JLON)*PRKTTEND(JLON,JLEV))/ZCGAMAH(JLON)

! 2. Fractional saturation
! ========================
        ELSE
          IF(ZICWC(JLON)>0)THEN
            ZCSIGMA(JLON)=1._JPRB/(PRHDFDA(JLON,JLEV)+ZCGAMMA(JLON)*&
           & ZICWC(JLON))
            ZCMEC1(JLON)=(1._JPRB-ZCLDM(JLON))*ZCSIGMA(JLON)*&
           & PRHDFDA(JLON,JLEV)
            ZCMEC2(JLON)=ZCLDM(JLON)*ZCALPHA(JLON)/ZCGAMAH(JLON)+&
           & (1._JPRB-ZRCGAMA(JLON)*ZCLDM(JLON))*ZCSIGMA(JLON)*&
           & ZCALPHA(JLON)*ZICWC(JLON)
            ZCMEC3(JLON)=ZCLDM(JLON)*ZCBETAH(JLON)/ZCGAMAH(JLON)+&
           & (ZCBETA(JLON)-ZRCGAMA(JLON)*ZCLDM(JLON)*&
           & ZCBETAH(JLON))*ZCSIGMA(JLON)*ZICWC(JLON)
            ZCMEC4(JLON)=ZCSIGMA(JLON)*ZCGAMMA(JLON)*ZICWC(JLON)

            ZCME(JLON,JLEV)=-ZCMEC1(JLON)*PRKQCTEND(JLON,JLEV)+ZCMEC2(JLON)*&
           & PRKQVTEND(JLON,JLEV)-ZCMEC3(JLON)*(ZMELTHEAT+PRKTTEND(JLON,JLEV))+&
           & ZCMEC4(JLON)*(ZEVAPS+ZEVAP)+ZCEVAP*ZCLDM(JLON)*ZCSIGMA(JLON)*&
           & PRHDFDA(JLON,JLEV)

          ELSE

            ZCMEC1(JLON)=(1._JPRB-ZCLDM(JLON))
            ZCMEC2(JLON)=ZCLDM(JLON)*ZCALPHA(JLON)/ZCGAMAH(JLON)
            ZCMEC3(JLON)=ZCLDM(JLON)*ZCBETAH(JLON)/ZCGAMAH(JLON)

            ZCME(JLON,JLEV)=-ZCMEC1(JLON)*PRKQCTEND(JLON,JLEV)+&
           & ZCMEC2(JLON)*PRKQVTEND(JLON,JLEV)-&
           & ZCMEC3(JLON)*(ZMELTHEAT+PRKTTEND(JLON,JLEV))

          ENDIF
        ENDIF

! 3. When rh < critical rh, evaporate existing cloud water
! ========================================================
      ELSEIF(ZCWI(JLON,JLEV)>0._JPRB)THEN
        ZCME(JLON,JLEV)=-MIN(MAX(0._JPRB,PQW(JLON,JLEV)-ZQTMP(JLON,JLEV)),&
       & ZCWI(JLON,JLEV))/TSPHY


! 4. No condensation nor evaporation
! =================================        
      ELSE
        ZCME(JLON,JLEV)=0._JPRB
      ENDIF

! 5. Final Checks
! ===============  
! Because of the finite time step,
! place a bound here not to exceed wet bulb point
! and not to evaporate more than available water

      ZQTEMP=ZQTMP(JLON,JLEV)-ZCME(JLON,JLEV)*TSPHY

      IF(ZQTEMP > PQW(JLON,JLEV))THEN
        ZCME(JLON,JLEV)=ZCME(JLON,JLEV)+(ZQTEMP-PQW(JLON,JLEV))/TSPHY
      ENDIF

      IF(ZCME(JLON,JLEV)<-ZCWI(JLON,JLEV)/TSPHY)THEN
        ZCME(JLON,JLEV)=-ZCWI(JLON,JLEV)/TSPHY
      ENDIF

      ZCME(JLON,JLEV)=ZCME(JLON,JLEV)*ZOMSM 
         
      ZQTEMP=ZQTMP(JLON,JLEV) + (PRKQVTEND(JLON,JLEV))*TSPHY 

!First iteration
      ZTTEMP=PT(JLON,JLEV)+TSPHY*(PRKTTEND(JLON,JLEV)+(1._JPRB/YDCST%RCPD)*&
     & ((YDCST%RLVTT+(YDCST%RLSTT-YDCST%RLVTT)*ZICE)*ZCME(JLON,JLEV))) 


! <-- ZCME first guess
           
      ZESN = FOEW(ZTTEMP,1._JPRB)*ZICE + &
     & FOEW(ZTTEMP,0._JPRB)*(1._JPRB-ZICE)

    
      ZQSN = MIN(ZEPSILO*ZESN/(PAPRSF(JLON,JLEV)- ZOMEPS*ZESN),1._JPRB)

      ZTEMPCME = ZCME(JLON,JLEV)

      IF(ZCME(JLON,JLEV) > 0._JPRB  ) THEN
        ZCME(JLON,JLEV) = MIN(ZCME(JLON,JLEV),(ZQTEMP-ZQSN*&
       & PHCRICS(JLON,JLEV))/TSPHY)
      ENDIF

   
!Second iteration
      ZTTEMP=PT(JLON,JLEV)+TSPHY*(PRKTTEND(JLON,JLEV)+(1._JPRB/YDCST%RCPD)*&
     & ((YDCST%RLVTT+(YDCST%RLSTT-YDCST%RLVTT)*ZICE)*ZCME(JLON,JLEV))) 


!      ! <-- ZCME second guess

      ZESN = FOEW(ZTTEMP,1._JPRB)*ZICE + &
     & FOEW(ZTTEMP,0._JPRB)*(1._JPRB-ZICE)

      ZQSN = MIN(ZEPSILO*ZESN/(PAPRSF(JLON,JLEV) - ZOMEPS*ZESN),1._JPRB)
          
      IF(ZTEMPCME > 0._JPRB  ) THEN
        ZCME(JLON,JLEV) = MIN( ZTEMPCME*0.25_JPRB+&
       & ZCME(JLON,JLEV)*0.75_JPRB, (ZQTEMP-ZQSN*&
       & PHCRICS(JLON,JLEV))/TSPHY)
      ENDIF

!     !Original check
!     IF(ZCME(JLON,JLEV)>0._JPRB .AND. ZRELHUM(JLON)>PHCRICS(JLON,JLEV)) THEN
!       ZCME(JLON,JLEV)=MIN(ZCME(JLON,JLEV),(ZQTMP(JLON,JLEV)-ZQSATS*&
!      & PHCRICS(JLON,JLEV))/TSPHY)
!     ENDIF

!     The test above may cause negative cwi so once again:                               
!     if net evaporation, it should not exceed available cloud condensate                

      IF(ZCME(JLON,JLEV)<-ZCWI(JLON,JLEV)/TSPHY)THEN
        ZCME(JLON,JLEV)=-ZCWI(JLON,JLEV)/TSPHY
      ENDIF
       
! Reduce condensation activity in convective area:
      ZCME(JLON,JLEV)=ZCME(JLON,JLEV)*(1._JPRB-PNCV(JLON,JLEV))

! Split between water and ice                  
      ZCMEL=ZCME(JLON,JLEV)*(1._JPRB-ZICE)
      ZCMEI=ZCME(JLON,JLEV)*ZICE
        
! Convert from tendencies to fluxe divergence:
      ZFCSQL(JLON,JLEV)=ZFCSQL(JLON,JLEV-1)+(1._JPRB/YDCST%RG)*PDELP(JLON,JLEV)*ZCMEL
      ZFCSQN(JLON,JLEV)=ZFCSQN(JLON,JLEV-1)+(1._JPRB/YDCST%RG)*PDELP(JLON,JLEV)*ZCMEI
        
!! Need to update in case LSTRAPRO=TRUE (Dont know how for the moment)

      ZQLTMP(JLON,JLEV)=MAX(0.0_JPRB,ZQLTMP(JLON,JLEV)+ZCMEL)
      ZQITMP(JLON,JLEV)=MAX(0.0_JPRB,ZQITMP(JLON,JLEV)+ZCMEI)
      ZQTMP(JLON,JLEV)=MAX(0.0_JPRB,ZQTMP(JLON,JLEV)-ZCMEL-ZCMEI)
      ZDQ(JLON,JLEV)=MAX(0.0_JPRB,PQW(JLON,JLEV)-ZQTMP(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF ! LRKCDEV

IF (LSMITH_CDEV) THEN

  CALL ACNEBSM (YDCST, YDML_PHY_MF%YRPHY0, &
   & KIDIA, KFDIA, KLON, KTDIA, KLEV, &
   & PT, PQ, PQL, PQI, &
   & PAPHI, PAPRSF, PCP, PR, &
   & PGM, PVETAF, &
   & ZQCS, ZNEBCOND, PHCRICS, PRMF ,PQSATS )

 ! protection of convective condensate, using changed distribution
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

      ZQCST=PQI(JLON,JLEV)+PQL(JLON,JLEV)
      ZNEBT=PNEBCOND(JLON,JLEV)&
       &+PNCV(JLON,JLEV)-PNEBCOND(JLON,JLEV)*PNCV(JLON,JLEV)
      ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB, ZNEBT-ZEPS1))
      ZFRACON=ZZ*PNCV(JLON,JLEV)/(ZNEBT+(1.0_JPRB-ZZ))
      ZQCS(JLON,JLEV)=(ZQCS(JLON,JLEV)**2+(ZQCST*ZFRACON)**2)/&
       & MAX(ZEPS1,(ZQCS(JLON,JLEV)+ZQCST*ZFRACON))

! contribution of shallow convection

      ZQCS(JLON,JLEV)=ZQCS(JLON,JLEV)+PQC_CVPP(JLON,JLEV)
      PNEBCOND(JLON,JLEV)=MAX(PNEBCOND(JLON,JLEV),PNEB_CVPP(JLON,JLEV))

! repartition by FONICE

      ZQL=(1.0_JPRB-PRMF(JLON,JLEV))*ZQCS(JLON,JLEV)
      ZQI=ZQCS(JLON,JLEV)-ZQL
!---------------------------------------------------
! COMPUTE LARGE-SCALE CONDENSATION/EVAPORATION FLUX.
!---------------------------------------------------

      ZDQL=ZQL - PQL(JLON,JLEV)
      ZDQI=ZQI - PQI(JLON,JLEV)

! Stratiform fluxes:

      ZFCSQL(JLON,JLEV) = ZFCSQL(JLON,JLEV-1)+ ZPOID(JLON,JLEV)*ZDQL
      ZFCSQN(JLON,JLEV) = ZFCSQN(JLON,JLEV-1)+ ZPOID(JLON,JLEV)*ZDQI

! Update in case we call microphysics (LSTRAPRO):

      ZQLTMP(JLON,JLEV)=MAX(0.0_JPRB,PQL(JLON,JLEV)+ZDQL)
      ZQITMP(JLON,JLEV)=MAX(0.0_JPRB,PQI(JLON,JLEV)+ZDQI)
      ZQTMP(JLON,JLEV)=MAX(0.0_JPRB,PQ(JLON,JLEV)-ZDQL-ZDQI)
      ZDQ(JLON,JLEV)=MAX(0.0_JPRB,PQW(JLON,JLEV)-ZQTMP(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF

IF (LSTRAPRO) THEN
!*
!     ------------------------------------------------------------------
!     IV  - CALCULS DE LA MICROPHYSIQUE (HORS 3MT).

!           MICROPHYSIC COMPUTATIONS (OUTSIDE 3MT).

  CALL APLMPHYS (YDML_PHY_MF, &
              & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
              & PAPRS,PAPRSF,PCP,ZQTMP,ZQITMP,ZQLTMP,PQN,PQR,PQG,PR,PT,&
              & ZIPOI,ZDQ,ZDQM,ZLHS,ZLHV,PNEBCOND,ZPOID,ZRCVOTT,&
              & ZTCORR,PLSM,PNEIJ,PTS,PDECRD,&
              & ZFCSQL,ZFCSQN,&
              & ZFALLR,ZFALLS,ZFALLG,&
              & PFASL,PFASN,PFASG,PFESL,PFESN,PFESG,PFPLSL,PFPLSN,PFPLSG,&
              & PSEDIQL,PSEDIQI,ZMELNET,ZMELGET,PGRVINT)

ENDIF ! LSTRAPRO
 
!*
!     ------------------------------------------------------------------
!     V   - FLUX DE CONDENSATION-EVAPORATION.

!           CONDENSATION-EVAPORATION FLUX.

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    PFCSQL(JLON,JLEV)=ZFCSQL(JLON,JLEV)
    PFCSQN(JLON,JLEV)=ZFCSQN(JLON,JLEV)
  ENDDO
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCDEV',1,ZHOOK_HANDLE)
END SUBROUTINE ACCDEV
