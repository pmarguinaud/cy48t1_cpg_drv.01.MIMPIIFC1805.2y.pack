!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCLPH ( YDPHY0,YDPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PTHETAV,PAPHI,PAPHIF,PU,PV,&
 ! - INPUT  1D .
 & PTHETAVS,&
 ! - OUTPUT 1D .
 & KCLPH,PCLPH,PVEIN,PUGST,PVGST)

!**** *ACCLPH * - PHI-PHIS,U,V AU SOMMET DIAGNOSTIQUE DE LA CLP.

!     Sujet.
!     ------

!     - ROUTINE DE CALCUL ACTIF .
!          SOMMET DE LA CLA DIAGNOSTIQUE D'APRES AYOTTE BLM 1996.
!          VENTS AU SOMMET DE LA COUCHE LIMITE PLANETAIRE,

!          TOP OF PBL DIAGNOSED VIA FOLLOWING AYOTTE BLM 1996.
!          WIND AT THE TOP OF THE PLANETARY BOUNDARY LAYER

!**   Interface.
!     ----------
!        *CALL* *ACCLPH*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX.
! PTHETAV    : THETAV.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (DIAGNOSTIQUE) .

! PTHETAVS   : THETAV EN SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D (DIAGNOSTIQUE) .

! KCLPH      : INDICE DU NIVEAU MODELE CONTENANT LE SOMMET DE LA CLP.
! PCLPH      : ALTITUDE EN METRES (PAR RAPPORT A LA SURFACE) AU SOMMET DE LA CLP.
! PVEIN      : INDEX DE VENTILATION DANS LA CLP.
! PUGST      : U COMPONENT OF GUSTS (DIAGNOSTIC)
! PVGST      : V COMPONENT OF GUSTS (DIAGNOSTIC)

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMCST /
! COMMON/YOMPHY0 /

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteurs.
!     -------
!        2002-08, J.M. Piriou et Jean-Francois Geleyn.

!     Modifications.
!     --------------
!     2003-04. M. Bellus: wind gusts in case of LRAFTUR=.F.
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!     03-03-2006  R.Brozkova Shear linked convection after M. Tudor
!                          and harmonisation with Troen-Mahrt method
!     K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG
USE YOMPHY0  , ONLY : TPHY0
USE YOMPHY2  , ONLY : TPHY2

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
TYPE(TPHY2)       ,INTENT(IN)    :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTHETAV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTHETAVS(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KCLPH(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCLPH(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVEIN(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUGST(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVGST(KLON) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZINT(KLON),ZX_PREV(KLON),ZTHETAV(KLON,KLEV+1),ZPHI(KLON,KLEV+1) &
 & ,ZLCH_PREV(KLON)  ,ZU(KLON,KLEV+1),ZV(KLON,KLEV+1),ZTHETAVS(KLON,KLEV+1)

! ZCLPU :: VENT "U" AU SOMMET DE LA CLP
! ZCLPV :: VENT "V" AU SOMMET DE LA CLP
REAL(KIND=JPRB) :: ZCLPU(KLON),ZCLPV(KLON)

! ZVITM   :: VITESSE DU VENT MOYEN VERTICAL DANS LA CLP
REAL(KIND=JPRB) :: ZVITM(KLON)

INTEGER(KIND=JPIM) :: JLON, JLEV, IBIN, IC(KLON), INDI

REAL(KIND=JPRB) :: ZPBLHK1,ZX,ZPSI,ZEPS,ZBIG,ZDELTA,ZARG1,ZARG2,ZBIN,ZLCH,ZVAL_LCH,ZLOG2
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCLPH',0,ZHOOK_HANDLE)
ASSOCIATE(GPBLHRA=>YDPHY0%GPBLHRA, GPBLHK0=>YDPHY0%GPBLHK0, &
 & LRAFTUR=>YDPHY2%LRAFTUR)
!-----------------------------------------------------------------------

!     ------------------------------------------------------------------
!     PREPARATION.

!         INITIAL SETTING.

ZPBLHK1=GPBLHK0/GPBLHRA
ZEPS=0.001_JPRB
ZBIG=30._JPRB
ZLOG2=LOG(2.0_JPRB)

ZARG1=-GPBLHK0/ZPBLHK1
ZVAL_LCH=MAX(LOG(COSH(MIN(ABS(ZARG1),ZBIG))),ABS(ZARG1)-ZLOG2)
DO JLON=KIDIA,KFDIA
  PCLPH(JLON)=0.0_JPRB
  PVEIN(JLON)=0.0_JPRB
  ZCLPU(JLON)=0.0_JPRB
  ZCLPV(JLON)=0.0_JPRB
  ZVITM(JLON)=SQRT(PU(JLON,KLEV)**2 + PV(JLON,KLEV)**2)
  KCLPH(JLON)=KLEV
  ZINT(JLON)=0.0_JPRB
  ZX_PREV(JLON)=0.0_JPRB
  ZLCH_PREV(JLON)=ZVAL_LCH
  ZTHETAV(JLON,KLEV+1)=MAX(PTHETAVS(JLON),PTHETAV(JLON,KLEV))
  ZTHETAVS(JLON,KLEV+1)=ZTHETAV(JLON,KLEV+1)
  ZPHI(JLON,KLEV+1)=PAPHI(JLON,KLEV)
  ZU(JLON,KLEV+1)=0.0_JPRB
  ZV(JLON,KLEV+1)=0.0_JPRB
ENDDO
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZTHETAV(JLON,JLEV)=PTHETAV(JLON,JLEV)
    ZPHI(JLON,JLEV)=PAPHIF(JLON,JLEV)
    ZU(JLON,JLEV)=PU(JLON,JLEV)
    ZV(JLON,JLEV)=PV(JLON,JLEV)
  ENDDO
ENDDO
!MT shear linked convection modification
!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZTHETAVS(JLON,JLEV)=ZTHETAVS(JLON,JLEV+1)&
     &+ZTHETAV(JLON,JLEV)-ZTHETAV(JLON,JLEV+1)&
     &-0.5_JPRB*(ZTHETAV(JLON,JLEV)+ZTHETAV(JLON,JLEV+1))&
     &*((ZU(JLON,JLEV)-ZU(JLON,JLEV+1))**2+(ZV(JLON,JLEV)-ZV(JLON,JLEV+1))**2)&
     &/(ZPHI(JLON,JLEV)-ZPHI(JLON,JLEV+1))
  ENDDO
ENDDO

!-------------------------------------------------
! INTEGRALE ASCENDANTE.
! UPWARD INTEGRATIONS.
!-------------------------------------------------

DO JLEV=KLEV,KTDIA,-1
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    !
    !-------------------------------------------------
    ! INTEGRALE DE THETAV.
    ! THETAV INTEGRAL.
    !-------------------------------------------------
    !
    ZINT(JLON)=ZINT(JLON)+(ZPHI(JLON,JLEV)-ZPHI(JLON,JLEV+1))&
     & *0.5_JPRB*(ZTHETAVS(JLON,JLEV)+ZTHETAVS(JLON,JLEV+1))  
    !
    !-------------------------------------------------
    ! ECART THETAV' ENTRE THETAV DU NIVEAU COURANT
    ! ET LA MOYENNE DE THETAV ENTRE LA SURFACE ET LE NIVEAU COURANT.
    ! COMPUTE THETAV', DIFFERENCE BETWEEN CURRENT THETAV VALUE
    ! AND ITS INTEGRAL BETWEEN SURFACE AND CURRENT LEVEL.
    !-------------------------------------------------
    !
    ZX=ZTHETAVS(JLON,JLEV)-ZINT(JLON)/(ZPHI(JLON,JLEV)-ZPHI(JLON,KLEV+1))
    !
    !-------------------------------------------------
    ! DIFFERENCE ENTRE THETAV' DU NIVEAU COURANT ET CELUI DU NIVEAU DU DESSOUS.
    ! DIFFERENCE BETWEEN CURRENT LEVEL THETAV' AND LEVEL BELOW.
    !-------------------------------------------------
    !
    ZDELTA=ZX-ZX_PREV(JLON)
    !
    !-------------------------------------------------
    ! SECURITE EN DIVISION: ZDELTA VA SERVIR AU DENOMINATEUR.
    ! ON LE BORNE POUR EVITER LA DIVISION PAR ZERO, EN CONSERVANT SON SIGNE.
    ! SECURE DIVISION: ZDELTA IS AT THE DENOMINATOR.
    ! ONE PREVENTS A ZERO VALUE, WHILE KEEPING THE SAME SIGN.
    !-------------------------------------------------
    !
    ZBIN=MAX(0.0_JPRB,SIGN(1.0_JPRB,ABS(ZDELTA)-ZEPS))
    ZDELTA=ZBIN*ZDELTA+(1.0_JPRB-ZBIN)*ZEPS*SIGN(1.0_JPRB,ZDELTA)
    ZX=ZX_PREV(JLON)+ZDELTA
    !
    !-------------------------------------------------
    ! SECURITE EN DEBORDEMENT DE L'EXPONENTIELLE: ON UTILISE
    ! LA FONCTION LOG(COSH) POUR LES ARGUMENTS INFERIEURS A ZBIG, ET SON ASYMPTOTE AU-DELA.
    ! SECURE EXPONENTIAL OVERFLOWS: ONE USES
    ! THE LOG(COSH) FUNCTION FOR ARGUMENTS BELOW ZBIG, AND ITS ASYMPTOT ELSE CASE.
    !-------------------------------------------------
    !
    ZARG2=(ZX-GPBLHK0)/ZPBLHK1
    ZLCH=MAX(LOG(COSH(MIN(ABS(ZARG2),ZBIG))),ABS(ZARG2)-ZLOG2)
    ZPSI=0.5_JPRB*(1.0_JPRB-ZPBLHK1/ZDELTA*(ZLCH-ZLCH_PREV(JLON)))
    !
    !-------------------------------------------------
    ! INTEGRALE DE L'EPAISSEUR GEOPOTENTIELLE, AVEC POUR POIDS PSI.
    ! INTEGRATE GEOPOTENTIAL DEPTH, WITH PSI WEIGHTING FUNCTION.
    !-------------------------------------------------
    !
    PCLPH(JLON)=PCLPH(JLON)+(ZPHI(JLON,JLEV)-ZPHI(JLON,JLEV+1))*ZPSI
    !
    !-------------------------------------------------
    ! ON REMPLACE LES VALEURS DU NOUVEAU INFERIEUR PAR CELLES DU NIVEAU COURANT.
    ! ONE REPLACES VALUES FROM LEVEL BELOW BY THOSE OF CURRENT LEVEL.
    !-------------------------------------------------
    !
    ZX_PREV(JLON)=ZX
    ZLCH_PREV(JLON)=ZLCH
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  PCLPH(JLON)=MAX(PCLPH(JLON),PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV))
ENDDO

!-------------------------------------------------
! SECONDE BOUCLE VERTICALE: VENT AU SOMMET DE LA CLA.
! SECOND VERTICAL LOOP: WIND AT THE TOP OF PBL.
!-------------------------------------------------

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZPSI=MAX(0.0_JPRB,MIN(1.0_JPRB,(PCLPH(JLON)+PAPHI(JLON,KLEV)-ZPHI(JLON,JLEV+1))&
     & /(ZPHI(JLON,JLEV)-ZPHI(JLON,JLEV+1))))  
    ZCLPU(JLON)=ZPSI*PU(JLON,JLEV)+(1.0_JPRB-ZPSI)*ZCLPU(JLON)
    ZCLPV(JLON)=ZPSI*PV(JLON,JLEV)+(1.0_JPRB-ZPSI)*ZCLPV(JLON)
    IBIN=NINT(ZPSI)
    KCLPH(JLON)=IBIN*JLEV+(1-IBIN)*KCLPH(JLON)
  ENDDO
ENDDO

!cdir unroll=8
IC(KIDIA:KFDIA)=1
DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    INDI=INT(MAX(0.0_JPRB,SIGN(1.0_JPRB,PCLPH(JLON)+PAPHI(JLON,KLEV)-ZPHI(JLON,JLEV+1))) )
    ZVITM(JLON)=ZVITM(JLON)+INDI*SQRT(PU(JLON,JLEV)**2 + PV(JLON,JLEV)**2) 
    IC(JLON)=IC(JLON) + INDI
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  PVEIN(JLON)=PCLPH(JLON)*(ZVITM(JLON)/FLOAT(IC(JLON)))/RG
ENDDO
!-------------------------------------------------
! WIND GUSTS IN CASE OF LRAFTUR=.F.
!-------------------------------------------------

IF (.NOT.LRAFTUR) THEN
  PUGST(KIDIA:KFDIA)=ZCLPU(KIDIA:KFDIA)
  PVGST(KIDIA:KFDIA)=ZCLPV(KIDIA:KFDIA)
ENDIF

!-------------------------------------------------
! GEOPOTENTIELS > ALTITUDES.
! GEOPOTENTIALS > ALTITUDES.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
  PCLPH(JLON)=PCLPH(JLON)/RG
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCLPH',1,ZHOOK_HANDLE)
END SUBROUTINE ACCLPH
