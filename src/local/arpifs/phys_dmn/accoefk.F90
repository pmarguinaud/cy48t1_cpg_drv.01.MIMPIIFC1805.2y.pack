!OPTIONS XOPT(NOEVAL)
!OPTION! -O nochg
SUBROUTINE ACCOEFK ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PDELP,PLSCPE,PQ,PQL,PQI,PQSAT,&
 & PR,PT,PU,PV,PFPLSH,PFPLCH,PLMU,PLMT,&
 ! - INPUT  1D .
 & PGZ0, PGZ0H, PBLH, &
 ! - OUTPUT 2D .
 & PKTROV,PKUROV,PKNROV,PNBVNO,PXTROV,PXUROV,PXPTKEROV)

!**** *ACCOEFK* - COEFFICIENTS D'ECHANGE VERTICAL TURBULENT.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES COEFFICIENTS D'ECHANGE VERTICAL TURBULENT (DIMENSION
!       (DP/(G*DT)) ET DE LA STABILITE STATIQUE (DIMENSION (U/DP)**2),
!       AINSI QUE DES FACTEURS D'AMPLIFICATION "ANTI-FIBRILLATION" DES
!       PREMIERS .
!     - COMPUTATION OF VERTICAL TURBULENT EXCHANGE COEFFICIENTS
!       (DIMENSION (DP/(G*DT)) AND OF STATIC STABILITY (DIMENSION
!       (U/DP)**2), AS WELL AS OF THE "ANTI-FIBRILLATION" AMPLIFICATION
!       FACTORS OF THE FORMERS .

!**   Interface.
!     ----------
!        *CALL* *ACCOEFK*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PFPLSH     : CHAMP "HISTORIQUE" DES PRECIPITATIONS STRATIFORMES.
! PFPLCH     : CHAMP "HISTORIQUE" DES PRECIPITATIONS CONVECTIVES.
! PLMT       : LONGUEUR DE MELANGE POUR L'ENERGIE.
! PLMU       : LONGUEUR DE MELANGE POUR LA QUANTITE DE MOUVEMENT.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PLSCPE     : RAPPORT EFECTIF DES L ET CP EN CONDENSATION/EVAPORATION.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQL        : EAU LIQUIDE
! PQI        : GLACE
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PGZ0H      : G*LONGUEUR DE RUGOSITE THERMIQUE COURANTE (SI KVCLIV >= 8)
! PBLH       : HAUTEUR DU SOMMET DE COUCHE LIMITE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PKTROV     : COEFFICIENT D'ECHANGE VERTICAL DE T ET Q EN KG/(M*M*S).
! PKUROV     : COEFFICIENT D'ECHANGE VERTICAL DE U ET V EN KG/(M*M*S).
! PKNROV     : COEFFICIENT D'ECHANGE VERTICAL NEUTRE EN KG/(M*M*S).
! PNBVNO     : CARRE DE FR. BRUNT-VAISALA DIVISEE PAR G FOIS LA DENSITE.
! PXTROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKTROV.
! PXUROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKUROV.
! PXPTKEROV  : MULTIPLICATEUR "ANTI-FIBRILLATION" POUR "PKEROV".

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        89-12, J.F. Geleyn.

!     Modifications.
!     --------------
!      2001-08, New definition and use of the thermal mixing length - J.M. Piriou / J.F Geleyn.
!      2001-08, Consistency between USURID and antifibrillation - J.M. Piriou / J.F Geleyn.
!      2002-06, Temporal smoothing of shallow convection activity - F. Bouyssel.
!      2002-06, Gustiness term in case of precipitations - F. Bouyssel.
!      2002-12, Increasing the accuracy of the anti-fibrilation computation
!               in the case of shallow convection - J.-F.Geleyn / D. Banciu 
!      2002-12, Cleaning of LCVPPLIS - F. Bouyssel.
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      2003-11, Externalize mixing lengths - J.M. Piriou.
!      2003-12, Minimum value for wind shear in turbulent computations - J.M. Piriou.
!      2004-02, Introduces EDK for Fm and Fh in stable case and
!               small correction for Lm*Lu in the instable case-(ZCK) E. Bazile.
!      2005-11, Duplication of ACCOEFK for pTKE - J. Cedilnik
!      2006-01, K_n computation for pTKE - F. Vana / J.F. Geleyn.
!      2006-03-03 R.Brozkova pre-ALARO 0 modset: preparation for the pseudo-TKE scheme
!      26-Sep-2006, M. Bellus - ALARO-0 phasing (ZQTOT=PQ+PQL+PQI for L3MT or LSTRAPRO;
!                   moist gustiness parameterization was moved after the antifib. part)
!      R. El Khatib 28-Jul-2006 Porting to NEC
!      2007-05, New formulation for XMULAF>0 - F. Bouyssel / J.F. Geleyn.
!      2007-05, Bugs correction in ZUSURIC,ZUSURID - F. Bouyssel / J.F. Geleyn.
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RKAPPA   ,&
 & RV       ,RCPD     ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD  

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI    (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF   (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS    (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF   (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP      (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCPE   (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ       (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL      (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI      (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR       (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT       (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU       (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV       (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSH   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCH   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMU     (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMT     (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0     (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0H    (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBLH     (KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKTROV   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKUROV   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKNROV   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNBVNO   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXTROV   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXUROV   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXPTKEROV(KLON,0:KLEV) 

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV, JLON

LOGICAL :: LLAFGD, LLAFAD

REAL(KIND=JPRB) :: Z2B, Z3B, Z3BCF, ZA1, ZAPRIM, ZAT, ZAU, ZAUX,&
 & ZB1, ZBE, ZBETA, ZBETAP, ZBPRIM, ZC1, ZCIS, &
 & ZCKU, ZCKT, ZCPRIM, ZCRIT1, ZCRIT2, &
 & ZDELTA1, ZDENUM, ZDI, ZDIFT, ZDIFV, ZDPHI, ZDS, ZEDIFV, &
 & ZDTETA, ZGA, ZGAMDZ, ZGKT, ZGKU, ZGLMT2, ZDELTA, &
 & ZGLMU2, ZGLT, ZGLU, ZIS, ZISD, ZIT, ZIU, &
 & ZLOI, ZLOS, ZMULA2, ZPFH, ZRCVPP, ZRHOH, &
 & ZRI, ZRLTLU, ZRTI, ZSDPRIM, ZST, ZSTA, ZSTAD, &
 & ZSU, ZTAU, ZU, ZZ, ZZT, ZUSURIC, ZSTABV, ZIXP, ZSTAH, ZHS, &
 & ZUSURID, ZDERH, ZSTABI, ZIXPD, ZSTADH, ZRIH, ZRIOLD, ZIN, &
 & ZFP, ZRRCOR  
REAL(KIND=JPRB) :: ZDI0, ZDS0, ZGDT, ZHS0
REAL(KIND=JPRB) :: ZCPVMD, ZDAUX, ZDCP, ZDELBI, ZDELT1, ZDELT2, ZDSEPRIM, &
 & ZEW, ZEPS, ZEPS2, ZKTROV0, ZKUROV0, ZMLSCPE, ZQPRIM, ZQSAT1, ZQSAT2, &
 & ZTPRIM1, ZTPRIM2, ZTC, ZQTOT, ZQTOTI   
REAL(KIND=JPRB) :: ZBETA1(KLON,0:KLEV), ZDSE(KLON,1:KLEV), ZQSAT(KLON,1:KLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCOEFK',0,ZHOOK_HANDLE)
ASSOCIATE(VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, USURICL=>YDML_PHY_MF%YRPHY0%USURICL, GCCSV=>YDML_PHY_MF%YRPHY0%GCCSV, &
 & USURIDE=>YDML_PHY_MF%YRPHY0%USURIDE, ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, BEDIFV=>YDML_PHY_MF%YRPHY0%BEDIFV, &
 & RRSCALE=>YDML_PHY_MF%YRPHY0%RRSCALE, GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, UTILGUST=>YDML_PHY_MF%YRPHY0%UTILGUST, &
 & UHDIFV=>YDML_PHY_MF%YRPHY0%UHDIFV, GRCVPP=>YDML_PHY_MF%YRPHY0%GRCVPP, EDB=>YDML_PHY_MF%YRPHY0%EDB, &
 & EDC=>YDML_PHY_MF%YRPHY0%EDC, EDD=>YDML_PHY_MF%YRPHY0%EDD, EDK=>YDML_PHY_MF%YRPHY0%EDK, &
 & USURICE=>YDML_PHY_MF%YRPHY0%USURICE, &
 & RRGAMMA=>YDML_PHY_MF%YRPHY0%RRGAMMA, USURIC=>YDML_PHY_MF%YRPHY0%USURIC, USURID=>YDML_PHY_MF%YRPHY0%USURID, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, XDAMP=>YDML_PHY_MF%YRPHY2%XDAMP, XMULAF=>YDML_PHY_MF%YRPHY2%XMULAF, &
 & XMUCVPP=>YDML_PHY_MF%YRPHY2%XMUCVPP, &
 & L3MT=>YDML_PHY_MF%YRPHY%L3MT, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO, &
 & LRRGUST=>YDML_PHY_MF%YRPHY%LRRGUST, LCVPP=>YDML_PHY_MF%YRPHY%LCVPP)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     LE CARRE DU CISAILLEMENT DE VENT).

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS (FOR
!     THE SQUARE OF THE WIND SHEAR).

ZCPVMD=RCPV-RCPD
ZGDT=RG*TSPHY

Z2B=2.0_JPRB*EDB
Z3B=3._JPRB*EDB
Z3BCF=EDB*EDC*VKARMN**2/SQRT(3._JPRB)
ZEDIFV=BEDIFV/SQRT(3._JPRB)
ZRLTLU=1.5_JPRB*EDD
ZDIFV=UHDIFV/(RG*VKARMN)
ZDIFT=ZDIFV/SQRT(3._JPRB)
ZRCVPP=1.0_JPRB/GRCVPP

LLAFGD=XMULAF /= 0.0_JPRB

IF(USURID == 0.0_JPRB) THEN
  ZEDIFV=BEDIFV
  ZIN=0.0_JPRB
ELSE
  ZIN=1.0_JPRB
ENDIF

ZEPS2=1.E-10_JPRB

LLAFAD=XDAMP /=  0.0_JPRB
ZBETA1(:,:)=0.0_JPRB
ZQTOT=0.0_JPRB
ZQTOTI=0.0_JPRB

IF (L3MT.OR.LSTRAPRO) THEN
  DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
       ZTC=PT(JLON,JLEV)-(FOLH(PT(JLON,JLEV),0.0_JPRB)*PQL(JLON,JLEV)&
        & + FOLH(PT(JLON,JLEV),1.0_JPRB)*PQI(JLON,JLEV))/PCP(JLON,JLEV)
       ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*ZTC+PAPHIF(JLON,JLEV)
       IF(LNEIGE)THEN
         ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTC))
       ELSE
         ZDELTA=0.0_JPRB
       ENDIF
       ZEW=FOEW(ZTC,ZDELTA)
       ZEPS=ZEW/PAPRSF(JLON,JLEV)
       ZQSAT(JLON,JLEV)=FOQS(ZEPS)
     ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZQSAT(JLON,JLEV)=PQSAT(JLON,JLEV)
      ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     II - PASSAGE EN GEOPOTENTIEL POUR LES LONGUEURS DE MELANGE
!     ASYMPTOTIQUES.

!          GOING TO GEOPOTENTIAL FOR ASYMPTOTIC MIXING LENGTHS.

ZGLU=RG*ALMAV
ZGLT=ZGLU*ZRLTLU

!*
!     ------------------------------------------------------------------
!     III - BOUCLE PASSIVE SUR LES NIVEAUX VERTICAUX.

!           PASSIVE LOOP ON VERTICAL LEVELS.

!cdir outerunroll=4
DO JLEV=KTDIA,KLEV-1

!*
!     ------------------------------------------------------------------
!     IV - CALCULS PROPREMENT DITS.

!          EFFECTIVE CALCULATIONS.

  DO JLON=KIDIA,KFDIA

    ZZ=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))&
     & -PAPHI(JLON,KLEV)+PGZ0(JLON))  
    ZZT=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))&
     & -PAPHI(JLON,KLEV)+PGZ0H(JLON))  
    ZPFH=MAX(0.0_JPRB,ZEDIFV+(1.0_JPRB-ZEDIFV)/(1.0_JPRB+(ZDIFT*ZZT)**2))
    ZGLMU2=(PLMU(JLON,JLEV)*RG)**2
    ZGLMT2=PLMT(JLON,JLEV)*PLMU(JLON,JLEV)*RG**2
    ZCKT=Z3BCF*ZGLMT2/(ZZT*ZZ)
    ZCKU=Z3BCF*ZGLMU2/(ZZ*ZZ)
    ZDPHI=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)

!     CISAILLEMENT DE VENT.
!     WIND SHEAR.

    ZCIS=(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2+(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2&
     & +(GCISMIN*ZDPHI/(RG*(1.0_JPRB+ZZ/ZGLU)))**2  

    ZU=SQRT(ZCIS)

!     PRECALCUL DE STABILITE.
!     PRELIMINARY STABILITY COMPUTATION.

    ZDTETA=PR(JLON,JLEV)*PT(JLON,JLEV)-PR(JLON,JLEV+1)*&
     & PT(JLON,JLEV+1)+RKAPPA*ZDPHI  

!     CORRECTION DE CONVECTION PEU PROFONDE (OPTIONNELLE).
!     CORRECTION FOR SHALLOW CONVECTION (OPTIONAL).

! ZDELBI     : SATURATION DEPARTURE OF THE INITIAL STATE
! ZQTOT      : TOTAL WATER CONTENT AT LEVEL JLEV
! ZQTOTI     : TOTAL WATER CONTENT AT LEVEL JLEV+1

    IF(L3MT.OR.LSTRAPRO)THEN
      ZQTOT=PQ(JLON,JLEV)+PQL(JLON,JLEV)+PQI(JLON,JLEV)
      ZQTOTI=PQ(JLON,JLEV+1)+PQL(JLON,JLEV+1)+PQI(JLON,JLEV+1)
    ELSE
      ZQTOT=PQ(JLON,JLEV)
      ZQTOTI=PQ(JLON,JLEV+1)
    ENDIF

    IF (LCVPP) THEN
      ZDELBI=ZQSAT(JLON,JLEV)-ZQTOT&
       & -ZQSAT(JLON,JLEV+1)+ZQTOTI
      ZDELBI=SIGN(1.0_JPRB,ZDELBI)*MAX(ZEPS2,ABS(ZDELBI))
      ZMLSCPE=0.5_JPRB*(PLSCPE(JLON,JLEV)+PLSCPE(JLON,JLEV+1))
      ZDAUX=MAX(0.0_JPRB,SIGN(1.0_JPRB,RD&
       & *(ZQTOTI-ZQTOT&
       & +GCCSV*(ZQSAT(JLON,JLEV)-ZQSAT(JLON,JLEV+1))&
       & *PAPRS(JLON,JLEV)/PAPRS(JLON,KLEV))&
       & *ZMLSCPE-ZDTETA))  
      ZAUX=ZRCVPP *&
       & MIN(0.0_JPRB,-ZDELBI)*ZMLSCPE*ZDAUX  
    ELSE
      ZDELBI=ZEPS2
      ZDAUX=0.0_JPRB
      ZAUX=0.0_JPRB
    ENDIF

!     CALCUL DE STABILITE.
!     STABILITY COMPUTATION.

    ZRTI=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
    ZSTA=ZDPHI*(ZDTETA+RD*ZAUX)*ZRTI
    ZSTAD=ZDPHI*(ZDTETA+XMUCVPP*RD*ZAUX)*ZRTI
    ZSTABV=ZSTA
    ZSTABI=ZSTAD
    ZUSURIC=USURIC*(1.0_JPRB+(USURICL-1.0_JPRB)*(RG*PLMT(JLON,JLEV)/ZZT)&
     & **USURICE)
    ZUSURID=USURID*(1.0_JPRB+(UHDIFV*PBLH(JLON))**2&
     & /((ZZ*ZDIFV)*(ZZT*ZDIFT)))**USURIDE
    ZIXP=(1.0_JPRB+3._JPRB*ZUSURID*MAX(0.0_JPRB,ZSTA)/ZCIS)&
     & /(1.0_JPRB+ZUSURID*MAX(0.0_JPRB,ZSTA)/ZCIS)  
    ZIXPD=(1.0_JPRB+3._JPRB*ZUSURID*MAX(0.0_JPRB,ZSTAD)/ZCIS)&
     & /(1.0_JPRB+ZUSURID*MAX(0.0_JPRB,ZSTAD)/ZCIS)  
    ZSTAH=ZSTA/(1.0_JPRB+ZIXP*ZUSURIC*MAX(0.0_JPRB,ZSTA)/ZCIS)**(1.0_JPRB/ZIXP)
    ZSTA=ZSTA/(1.0_JPRB+ZUSURIC*MAX(0.0_JPRB,ZSTA)/ZCIS)
    ZSTADH=ZSTAD/(1.0_JPRB+ZIXPD*ZUSURIC*MAX(0.0_JPRB,ZSTAD)/ZCIS)**(1.0_JPRB/ZIXPD)
    ZSTAD=ZSTAD/(1.0_JPRB+ZUSURIC*MAX(0.0_JPRB,ZSTAD)/ZCIS)
    ZIS=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZSTA))
    ZISD=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZSTAD))

!     CALCULS COMMUNS POUR QUANTITE DE MOUVEMENT ET ENERGIE.
!     COMMON COMPUTATIONS FOR MOMENTUM AND ENERGY.

    ZDS=SQRT(ZCIS+EDD/EDK*ABS(ZSTA))
    ZDS0=SQRT(ZCIS+EDD/EDK*ABS(ZSTAD))
    ZHS=SQRT(ZCIS+EDD*EDK*ABS(ZSTAH))
    ZHS0=SQRT(ZCIS+EDD*EDK*ABS(ZSTADH))

!     CALCULS POUR LES COMPOSANTES DU VENT.
!     COMPUTATIONS FOR THE WIND COMPONENTS.

! ZKTROV0    : COEFFICIENT D'ECHANGE VERTICAL DE T ET Q EN KG/(M*M*S),
!              SANS CVPP.
! ZKUROV0    : COEFFICIENT D'ECHANGE VERTICAL DE U ET V EN KG/(M*M*S),
!              SANS CVPP.

    ZDI=1.0_JPRB/(ZU+ZCKU*SQRT(ABS(ZSTA)))
    ZDI0=1.0_JPRB/(ZU+ZCKU*SQRT(ABS(ZSTAD)))
    ZLOS=ZCIS*ZDS/(ZU*ZDS+Z2B*ABS(ZSTA))
    ZLOI=ZU-Z2B*ZSTA*ZDI
    PKUROV(JLON,JLEV)=(ZLOI+ZIS*(ZLOS-ZLOI))*ZGLMU2*&
     & PAPRS(JLON,JLEV)*ZRTI/ZDPHI**2  
    PKNROV(JLON,JLEV)=ZU*ZGLMU2*&
   &  PAPRS(JLON,JLEV)*ZRTI/ZDPHI**2

    ZLOS=ZCIS*ZDS0/(ZU*ZDS0+Z2B*ABS(ZSTAD))
    ZLOI=ZU-Z2B*ZSTAD*ZDI0
    ZKUROV0=(ZLOI+ZISD*(ZLOS-ZLOI))*ZGLMU2*&
     & PAPRS(JLON,JLEV)*ZRTI/ZDPHI**2  

!     CALCULS POUR LA TEMPERATURE ET L'HUMIDITE.
!     COMPUTATIONS FOR TEMPERATURE AND HUMIDITY.

    ZDI=1.0_JPRB/(ZU+ZCKT*SQRT(ABS(ZSTA)))
    ZDI0=1.0_JPRB/(ZU+ZCKT*SQRT(ABS(ZSTAD)))
    ZLOS=ZCIS**2/(ZU*ZCIS+Z3B*ABS(ZSTAH)*ZHS)
    ZLOI=ZU-Z3B*ZSTA*ZDI
    PKTROV(JLON,JLEV)=(ZLOI+ZIS*(ZLOS-ZLOI))*ZGLMT2*&
     & PAPRS(JLON,JLEV)*ZRTI/ZDPHI**2  

    ZLOS=ZCIS**2/(ZU*ZCIS+Z3B*ABS(ZSTADH)*ZHS0)
    ZLOI=ZU-Z3B*ZSTAD*ZDI0
    ZKTROV0=(ZLOI+ZISD*(ZLOS-ZLOI))*ZGLMT2*&
     & PAPRS(JLON,JLEV)*ZRTI/ZDPHI**2  

!     CALCUL DE UN SUR G FOIS LA FREQUENCE DE BRUNT-VAISALA DIVISEE PAR
!     LA DENSITE LE TOUT AU CARRE.
!     COMPUTATION OF ONE OVER G TIME THE BRUNT-VAISALA FREQUENCY DIVIDED
!     BY DENSITY THE WHOLE BEING SQUARED.

    PNBVNO(JLON,JLEV)=ZSTABV/(PAPRS(JLON,JLEV)*ZRTI*ZDPHI)**2

!     CORRECTION DES COEFFICIENTS TURBULENTS EN PRESENCE DE PRECIPITATIONS.
!     CORRECTION OF TURBULENT COEFFICIENTS IN CASE OF PRECIPITATIONS.

    PXUROV(JLON,JLEV)=1.0_JPRB
    PXTROV(JLON,JLEV)=1.0_JPRB
    PXPTKEROV(JLON,JLEV)=1.0_JPRB

!     CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
!     ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.

    IF (LLAFGD) THEN
      ZRI=ABS(ZSTAD)/ZCIS
      ZRIH=ABS(ZSTADH)/ZCIS
      ZRIOLD=ABS(ZSTABI)/ZCIS
      ZDERH=(1.0_JPRB+(ZIXPD-1.0_JPRB)*ZUSURIC*ZRIOLD)/(1.0_JPRB+ZIXPD*ZUSURIC*ZRIOLD) 
      ZDERH=ZDERH+ZIN*(2.0_JPRB*ZUSURID*ZRIOLD/(ZIXPD*(1.0_JPRB+ZUSURID*ZRIOLD)**2))&
       & *(LOG(1.0_JPRB+ZIXPD*ZUSURIC*ZRIOLD)/ZIXPD-1.0_JPRB+ZDERH)  
      ZST=-3._JPRB*EDB*ZRIH*(1.0_JPRB+1.5_JPRB*EDD*EDK*ZRIH)/&
       & (SQRT(1.0_JPRB+EDD*EDK*ZRIH)+&
       & 3._JPRB*EDB*ZRIH *(1.0_JPRB+EDD*EDK*ZRIH))*ZDERH  
      ZSU=-2.0_JPRB*EDB*ZRI*(1.0_JPRB+0.5_JPRB*EDD*ZRI/EDK)/&
       & ((SQRT(1.0_JPRB+EDD*ZRI/EDK)+2.0_JPRB*&
       & EDB*ZRI)*(1.0_JPRB+EDD*ZRI/EDK))*(1.0_JPRB-ZRI*ZUSURIC)  
      ZIT=3._JPRB*EDB*ZRI*(1.0_JPRB+0.5_JPRB*ZCKT*SQRT(ZRI))/((1.0_JPRB+ZCKT*&
       & SQRT(ZRI))*(1.0_JPRB+ZCKT*SQRT(ZRI)+3._JPRB*EDB*ZRI))  
      ZIU=2.0_JPRB*EDB*ZRI*(1.0_JPRB+0.5_JPRB*ZCKU*SQRT(ZRI))/((1.0_JPRB+ZCKU*&
       & SQRT(ZRI))*(1.0_JPRB+ZCKU*SQRT(ZRI)+2.0_JPRB*EDB*ZRI))  
      ZAT=ZIT+ZISD*(ZST-ZIT)
      ZAU=ZIU+ZISD*(ZSU-ZIU)
      ZBE=3._JPRB+ZAT-2.0_JPRB*ZAU
      ZGA=2.0_JPRB+2.0_JPRB*ZAT-3._JPRB*ZAU
      IF(XMULAF > 0.0_JPRB) THEN
        PXUROV(JLON,JLEV)=MAX(PXUROV(JLON,JLEV),XMULAF)
        PXTROV(JLON,JLEV)=MAX(PXTROV(JLON,JLEV),XMULAF)
        PXPTKEROV(JLON,JLEV)=PXUROV(JLON,JLEV)
      ELSE
        ZGAMDZ=4._JPRB*TSPHY*(RG/ZDPHI)
        ZRHOH=PAPRS(JLON,JLEV)*ZRTI
        ZGKU=ZGAMDZ*PKUROV(JLON,JLEV)/ZRHOH
        ZGKT=ZGAMDZ*PKTROV(JLON,JLEV)/ZRHOH
        ZMULA2=XMULAF**2
        ZDENUM=ZGKU*ZGKT
        ZA1=(1.0_JPRB+ZGKU)*(1.0_JPRB+ZGKT)
        ZB1=(1.0_JPRB+ZGKU)*ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*(1.0_JPRB+ZGKT)&
         & *ZGKU*(1.0_JPRB-ZAU)
        ZC1=ZDENUM*ZGA
        ZDELTA1=ZB1**2-4._JPRB*ZA1*ZC1
        ZCRIT1=MAX(SIGN(1.0_JPRB,ZDELTA1),0.0_JPRB)
        ZTAU=-0.5_JPRB * ZCRIT1*(ZB1+SQRT(ABS(ZDELTA1)))/ZA1
        ZCRIT2=MAX(SIGN(1.0_JPRB,XMULAF-ZTAU),0.0_JPRB)
        ZAPRIM=ZMULA2*ZDENUM
        ZBPRIM=ZMULA2*(ZGKU+ZGKT)+XMULAF*ZDENUM*ZBE
        ZCPRIM=ZMULA2+XMULAF*(ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*ZGKU*(1.0_JPRB-ZAU))&
         & +ZDENUM*ZGA  
        ZSDPRIM=SQRT(ABS(ZBPRIM**2-4._JPRB*ZAPRIM*ZCPRIM))
        ZBETAP= 0.5_JPRB*(-ZBPRIM +ZSDPRIM)/MAX(ZAPRIM,1.0_JPRB-ZCRIT2)
        ZBETA=1.0_JPRB+ZCRIT2*(ZBETAP-1.0_JPRB)
        PXUROV(JLON,JLEV)=MAX(PXUROV(JLON,JLEV),ZBETA)
        PXTROV(JLON,JLEV)=MAX(PXTROV(JLON,JLEV),ZBETA)
        PXPTKEROV(JLON,JLEV)=PXUROV(JLON,JLEV)
      ENDIF
    ENDIF

! ZQPRIM     : SPECIFIC HUMIDITY AFTER MIXING.
! ZDSEPRIM   : DRY STATIC ENERGY AFTER MIXING.
! ZBETA1     : BETA COEFFICIENT -1, MULTIPLIED BY THE DAMPING FACTOR,
!              USED FOR INCREASING THE ACCURACY OF THE ANTIFIBRILLATION
!              COMPUTATION IN THE CASE OF SHALLOW CONVECTION  

    IF (LLAFAD) THEN
      ZQPRIM=(ZQTOT*PDELP(JLON,JLEV)&
       & +ZQTOTI*PDELP(JLON,JLEV+1))&
       & /(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1))  
      ZDSEPRIM=(ZDSE(JLON,JLEV)*PDELP(JLON,JLEV)&
       & +ZDSE(JLON,JLEV+1)*PDELP(JLON,JLEV+1))&
       & /(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1))  
      ZDCP=RCPD+ZCPVMD*ZQPRIM
      ZTPRIM1=(ZDSEPRIM-PAPHIF(JLON,JLEV))/ZDCP
      ZTPRIM2=(ZDSEPRIM-PAPHIF(JLON,JLEV+1))/ZDCP
      IF (LNEIGE) THEN
        ZDELT1=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTPRIM1))
        ZDELT2=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTPRIM2))
      ELSE
        ZDELT1=0.0_JPRB
        ZDELT2=0.0_JPRB
      ENDIF
      ZEW=FOEW(ZTPRIM1,ZDELT1)
      ZEPS=ZEW/PAPRSF(JLON,JLEV)
      ZQSAT1=FOQS(ZEPS)
      ZEW=FOEW(ZTPRIM2,ZDELT2)
      ZEPS=ZEW/PAPRSF(JLON,JLEV+1)
      ZQSAT2=FOQS (ZEPS)
      ZBETA1(JLON,JLEV)=MAX(0.0_JPRB,(ZQSAT2-ZQSAT1)/ZDELBI)*ZDAUX/XDAMP
      ZBETA1(JLON,JLEV)=MAX(ZBETA1(JLON,JLEV),ZBETA1(JLON,JLEV-1))
      PKTROV(JLON,JLEV)=ZKTROV0+(PKTROV(JLON,JLEV)-ZKTROV0)&
       & /(1.0_JPRB+ZBETA1(JLON,JLEV)*(PKTROV(JLON,JLEV)-ZKTROV0)&
       & *ZGDT*2.0_JPRB/(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1)))  
      PKUROV(JLON,JLEV)=ZKUROV0+(PKUROV(JLON,JLEV)-ZKUROV0)&
       & /(1.0_JPRB+ZBETA1(JLON,JLEV)*(PKUROV(JLON,JLEV)-ZKUROV0)&
       & *ZGDT*2.0_JPRB/(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1)))  
    ENDIF
    IF (LRRGUST) THEN
      ZFP=MAX(0.0_JPRB,PFPLSH(JLON,JLEV)+PFPLCH(JLON,JLEV))
      ZRRCOR=SQRT(1.0_JPRB+((((ZFP/(ZFP+RRSCALE))**RRGAMMA)*UTILGUST)**2)&
       & *PAPRS(JLON,JLEV)*ZRTI/(ZU*PKUROV(JLON,JLEV)))  
      PKUROV(JLON,JLEV)=PKUROV(JLON,JLEV)*ZRRCOR
      PKTROV(JLON,JLEV)=PKTROV(JLON,JLEV)*ZRRCOR
      PKNROV(JLON,JLEV)=PKNROV(JLON,JLEV)*ZRRCOR
    ENDIF

  ENDDO

!*
!     ------------------------------------------------------------------
!     V - FIN DE LA BOUCLE VERTICALE.

!         END OF THE VERTICAL LOOP.

ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCOEFK',1,ZHOOK_HANDLE)
END SUBROUTINE ACCOEFK
