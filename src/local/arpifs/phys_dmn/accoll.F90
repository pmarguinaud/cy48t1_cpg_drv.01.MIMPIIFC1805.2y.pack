!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCOLL ( YDML_PHY_MF,KIDIA,KFDIA,KLON,&
 !-----------------------------------------------------------------------
 ! - INPUT 1D .
 & PEXPN,PHPLSL,PHPLSN,PHPLSG,PLPLSL,PLPLSN,PLPLSG,PLSCP,PPART,PRCOLL,PRHOAIR,&
 ! - INPUT/OUTPUT 1D .
 & PQIST,PQLST,PQNST,PQRST,PQGST,PTST,&
 ! - OUTPUT 1D .
 & PCOLNI,PCOLNL,PCOLRI,PCOLRL,PCOLGI,PCOLGL)

!**** *ACCOLL* - CALCULS DE COLLECTION.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF (SUR UN NIVEAU).
!       CALCUL DE QUATRE INCREMENTS DE COLLECTION.
!     - COMPUTATION OF FOUR COLLECTION INCREMENTS.

!**   Interface.
!     ----------
!        *CALL* *ACCOLL*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLMPHYS" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D .

! PEXPN      : FONCTION DE DEPENDANCE EN TEMPERATURE DES PROCESSUS < 0°C.
! PHPLSL     : FLUX DE PRECIPITATION LIQUIDE AU SOMMET DE LA COUCHE.
! PHPLSN     : FLUX DE PRECIPITATION NEIGEUSE AU SOMMET DE LA COUCHE.
! PHPLSG     : FLUX DE PRECIPITATION GRAUPEL AU SOMMET DE LA COUCHE.
! PLPLSL     : FLUX DE PRECIPITATION LIQUIDE AU BAS DE LA COUCHE.
! PLPLSN     : FLUX DE PRECIPITATION NEIGEUSE AU BAS DE LA COUCHE.
! PLPLSG     : FLUX DE PRECIPITATION GRAUPEL AU BAS DE LA COUCHE.
! PLSCP      : CHALEUR DE FUSION SUR CHALEUR MASSIQUE A PRESSION CONSTANTE.
! PPART      : PROPORTION DE LA MAILLE CONCERNEE.
! PRCOLL     : RAPPORT DES EFFICACITES DE COLLECTION NEIGE/PLUIE.
! PRHOAIR    : DENSITE DE L'AIR.

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTRÉE-SORTIE.
!     --------------------------

! - 1D .

! PQIST      : QUANTITE COURANTE D'EAU SOLIDE NUAGEUSE.
! PQLST      : QUANTITE COURANTE D'EAU LIQUIDE NUAGEUSE.
! PQNST      : QUANTITE COURANTE D'EAU SOLIDE PRECIPITANTE.
! PQRST      : QUANTITE COURANTE D'EAU LIQUIDE PRECIPITANTE.
! PQGST      : QUANTITE COURANTE GRAUPEL.
! PTST       : VALEUR COURANTE DE LA TEMPERATURE A L'ECHELLE DE LA MAILLE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 1D .

! PCOLNI     : INCREMENT DE COLLECTION GLACE => NEIGE.
! PCOLNL     : INCREMENT DE COLLECTION EAU LIQUIDE => NEIGE.
! PCOLRI     : INCREMENT DE COLLECTION GLACE => PLUIE.
! PCOLRL     : INCREMENT DE COLLECTION EAU LIQUIDE => PLUIE.
! PCOLGI     : INCREMENT DE COLLECTION GLACE => GRAUPEL.
! PCOLGL     : INCREMENT DE COLLECTION EAU LIQUIDE => GRAUPEL.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST/
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        07-02, J.-F. Geleyn (extraction from APLMPHYS).

!     Modifications.
!     --------------
!        07-03, Option of the ARPEGE scheme - J.-F. Geleyn & J.-M. Piriou
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        J. Van den Bergh: adapting for prognostic graupel
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RTT

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEXPN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSG(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCP(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPART(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCOLL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRHOAIR(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQIST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQLST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQNST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQRST(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQGST(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTST(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLNI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLNL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLRI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLRL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLGI(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOLGL(KLON)

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZEXCOLL, ZCOLLN, ZCOLLI, ZFPLSL, ZFPLSN, ZPOWFL, ZPOWFN, &
 & ZEPS1, ZEXRHO, ZCEXPN, ZCOEFFA, ZCOEFFB, ZFACRHO, ZEXPN, ZCOLNL, ZCOLRL, &
 & ZFAC, ZFPLSG, ZPOWFG, ZCOLLG
REAL(KIND=JPRB) :: ZEXCOLLR, ZMULCOL, ZMULCOLR


REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCOLL',0,ZHOOK_HANDLE)
ASSOCIATE(RACCEF=>YDML_PHY_MF%YRPHY0%RACCEF, RRIMEF=>YDML_PHY_MF%YRPHY0%RRIMEF, RAGGEF=>YDML_PHY_MF%YRPHY0%RAGGEF, &
 & EFFCOLL=>YDML_PHY_MF%YRPHY0%EFFCOLL, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LA0MPS=>YDML_PHY_MF%YRPHY%LA0MPS, LARPMPS=>YDML_PHY_MF%YRPHY%LARPMPS, LAB12=>YDML_PHY_MF%YRPHY%LAB12, &
 & LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
!-----------------------------------------------------------------------

IF (LA0MPS) THEN

  ZEXCOLL=0.8_JPRB
  ZEXCOLLR=ZEXCOLL
  ZMULCOL=1.0_JPRB
  ZMULCOLR=ZMULCOL

  IF (LAB12) THEN
    ZEXCOLLR=0.6_JPRB
    ZMULCOL=0.960_JPRB
    ZMULCOLR=0.278_JPRB
  ENDIF

  IF (LGRAPRO) THEN
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZCOLLN=EFFCOLL*PRCOLL(JLON)
      ! GRAUPEL : COLLECTION EFFICIENCY OF RAIN  
      ZCOLLG=EFFCOLL
      ZCOLLI=PEXPN(JLON)
      ZFPLSL=MAX(0.0_JPRB,0.5_JPRB*(PHPLSL(JLON)+PLPLSL(JLON)))

      IF (LAB12) THEN
        ZFPLSL=ZFPLSL/SQRT(PRHOAIR(JLON))
      ENDIF

      ZPOWFL=ZMULCOLR*EFFCOLL*TSPHY*ZFPLSL**ZEXCOLLR
      ZFPLSG=MAX(0.0_JPRB,0.5_JPRB*(PHPLSG(JLON)+PLPLSG(JLON)))

      IF (LAB12) THEN
        ZFPLSG=ZFPLSG/SQRT(PRHOAIR(JLON))
      ENDIF

      ZPOWFG=ZMULCOLR*ZCOLLG*TSPHY*ZFPLSG**ZEXCOLLR
      ZFPLSN=MAX(0.0_JPRB,0.5_JPRB*(PHPLSN(JLON)+PLPLSN(JLON)))

      IF (LAB12) THEN
        ZFPLSN=ZFPLSN/SQRT(SQRT(PRHOAIR(JLON)))
      ENDIF

      ZPOWFN=ZMULCOL*ZCOLLN*TSPHY*ZFPLSN**ZEXCOLL
      PCOLRL(JLON)=PQLST(JLON)*ZPOWFL/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      PCOLNL(JLON)=PQLST(JLON)*ZPOWFN/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      PCOLGL(JLON)=PQLST(JLON)*ZPOWFG/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      ZPOWFL=ZPOWFL*ZCOLLI
      ZPOWFN=ZPOWFN*ZCOLLI
      ZPOWFG=ZPOWFG*ZCOLLI
      PCOLRI(JLON)=PQIST(JLON)*ZPOWFL/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      PCOLNI(JLON)=PQIST(JLON)*ZPOWFN/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      PCOLGI(JLON)=PQIST(JLON)*ZPOWFG/(1.0_JPRB+ZPOWFL+ZPOWFN+ZPOWFG)
      PQLST(JLON)=PQLST(JLON)-PCOLRL(JLON)-PCOLNL(JLON)-PCOLGL(JLON)
      PQIST(JLON)=PQIST(JLON)-PCOLNI(JLON)-PCOLRI(JLON)-PCOLGI(JLON)
      PQRST(JLON)=PQRST(JLON)+PCOLRL(JLON)+PCOLRI(JLON)
      PQNST(JLON)=PQNST(JLON)+PCOLNL(JLON)+PCOLNI(JLON)
      PQGST(JLON)=PQGST(JLON)+PCOLGL(JLON)+PCOLGI(JLON)
      PTST(JLON)=PTST(JLON)+(PCOLNL(JLON)+PCOLGL(JLON)-PCOLRI(JLON))&
       &*PLSCP(JLON)*PPART(JLON)
    ENDDO
  ELSE
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZCOLLN=EFFCOLL*PRCOLL(JLON)
      ZCOLLI=PEXPN(JLON)
      ZFPLSL=MAX(0.0_JPRB,0.5_JPRB*(PHPLSL(JLON)+PLPLSL(JLON)))

      IF (LAB12) THEN
        ZFPLSL=ZFPLSL/SQRT(PRHOAIR(JLON))
      ENDIF

      ZPOWFL=ZMULCOLR*EFFCOLL*TSPHY*ZFPLSL**ZEXCOLLR
      ZFPLSN=MAX(0.0_JPRB,0.5_JPRB*(PHPLSN(JLON)+PLPLSN(JLON)))

      IF (LAB12) THEN
        ZFPLSN=ZFPLSN/SQRT(SQRT(PRHOAIR(JLON)))
      ENDIF

      ZPOWFN=ZMULCOL*ZCOLLN*TSPHY*ZFPLSN**ZEXCOLL
      PCOLRL(JLON)=PQLST(JLON)*ZPOWFL/(1.0_JPRB+ZPOWFL+ZPOWFN)
      PCOLNL(JLON)=PQLST(JLON)*ZPOWFN/(1.0_JPRB+ZPOWFL+ZPOWFN)
      ZPOWFL=ZPOWFL*ZCOLLI
      ZPOWFN=ZPOWFN*ZCOLLI
      PCOLRI(JLON)=PQIST(JLON)*ZPOWFL/(1.0_JPRB+ZPOWFL+ZPOWFN)
      PCOLNI(JLON)=PQIST(JLON)*ZPOWFN/(1.0_JPRB+ZPOWFL+ZPOWFN)
      PQLST(JLON)=PQLST(JLON)-PCOLRL(JLON)-PCOLNL(JLON)
      PQIST(JLON)=PQIST(JLON)-PCOLNI(JLON)-PCOLRI(JLON)
      PQRST(JLON)=PQRST(JLON)+PCOLRL(JLON)+PCOLRI(JLON)
      PQNST(JLON)=PQNST(JLON)+PCOLNL(JLON)+PCOLNI(JLON)
      PTST(JLON)=PTST(JLON)+(PCOLNL(JLON)-PCOLRI(JLON))*PLSCP(JLON)*PPART(JLON)
    ENDDO
  ENDIF

ENDIF

IF (LARPMPS) THEN

  ZEPS1=1.E-20_JPRB

  ZEXRHO=0.6_JPRB
  ZCEXPN=0.025_JPRB
  ZCOEFFA=5.1748_JPRB
  ZCOEFFB=18.4607_JPRB

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    ZFACRHO=PRHOAIR(JLON)**ZEXRHO
    ZEXPN=EXP(ZCEXPN*(PTST(JLON)-RTT))
    PCOLNI(JLON)=PQIST(JLON)*(1.0_JPRB-EXP(-RAGGEF*ZEXPN*ZFACRHO*TSPHY*ZCOEFFB&
     &*PQNST(JLON)))
    ZCOLNL=PQLST(JLON)*(1.0_JPRB-EXP(-RRIMEF*ZFACRHO*TSPHY*ZCOEFFB*PQNST(JLON))) 
    ZCOLRL=PQLST(JLON)*(1.0_JPRB-EXP(-RACCEF*ZFACRHO*TSPHY*ZCOEFFA*PQRST(JLON)))
    ZFAC=MIN(1.0_JPRB, PQLST(JLON)/MAX(ZEPS1,ZCOLNL+ZCOLRL))
    PCOLNL(JLON)=ZFAC*ZCOLNL
    PCOLRL(JLON)=ZFAC*ZCOLRL
    PCOLRI(JLON)=0.0_JPRB
    PQLST(JLON)=PQLST(JLON)-PCOLRL(JLON)-PCOLNL(JLON)
    PQIST(JLON)=PQIST(JLON)-PCOLNI(JLON)
    PQRST(JLON)=PQRST(JLON)+PCOLRL(JLON)
    PQNST(JLON)=PQNST(JLON)+PCOLNL(JLON)+PCOLNI(JLON)
    PTST(JLON)=PTST(JLON)+PCOLNL(JLON)*PLSCP(JLON)*PPART(JLON)
  ENDDO

ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCOLL',1,ZHOOK_HANDLE)
END SUBROUTINE ACCOLL
