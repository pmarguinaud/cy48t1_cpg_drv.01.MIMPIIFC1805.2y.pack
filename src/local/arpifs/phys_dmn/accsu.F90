SUBROUTINE ACCSU ( YDECUCONVCA,YDECUMF,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
!-----------------------------------------------------------------------
! - INPUT  2D .
                    &PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PR,&
                    &PCVGQ, PDELP,PLNPR,&
                    &PQ, PQCS, PQI, PQL, PQSAT,PQW,&
                    &PRDELP,PT,PTW, PU,PV,PVORT0, &
                    &PVERVEL,&
                    &PFCSQL, PFCSQN, &
! - INPUT 1D .
                    &PTS,PTAUX,PRCORI, PDECRD, &
! - INPUT/OUTPUT 1D .
                    &PUDGRO, &
                    &PCUCONVCA,PNLCONVCA,&
! - OUTPUT 2D .
                    &PDIFCQ,PDIFCQL,PDIFCQI, PDIFCS,&
                    &PFCCQL,PFCCQI,PSTRCU,PSTRCV,PZATSLC,&
                    &KNACT,PFRDE,PDETFI,&
                    &PTU, PQU, PUU, PVU,&
! - OUTPUT 1D .
                    &KNND,PCAPE,&
! - INPUT/OUTPUT 2D .
                    &PGEOSLC,PENTCH,PUDAL,PUDOM)

!**** *ACCSU * - COMPLEMENTARY SUBGRID UPDRAFT
!     DEEP CONVECTIVE UPDRAFT PROGNOSTIC PARAMETERIZATION
!     IN A FRAME OF PROGNOSTIC CONDENSED PHASES MICROPHYSICS

!     Sujet.
!     ------
!     - MASS-FLUX DEEP CONVECTION SCHEME, COMPUTING CONDENSATE AND
!       "SUB-GRID SCALE" ASSOCIATED FLUXES .

!**   Interface.
!     ----------
!        *CALL* *ACCSU*
!-----------------------------------------------------------------------
! -   INPUT ARGUMENTS.
!     ----------------

! - PHYSICS DIMENSIONING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - PHYSICS VARIABLES (ALPHABETICALLY IN EACH CATEGORY)

! - 2D (0:KLEV) .

! PAPHI      : HALF LEVEL GEOPOTENTIAL
! PAPRS      : HALF LEVEL PRESSURE

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : FULL LEVEL GEOPOTENTIAL
! PAPRSF     : FULL LEVEL PRESSURE
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PR         : CONSTANTE DE L'AIR HUMIDE.
! PCVGQ      : WATER VAPOUR MOISTURE CONVERGENCE (RESOLVED+SUBGRID)
! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : MICROPHYSICAL WATER VAPOUR SPECIFIC HUMIDITY
! PQCS       : CONDENSATE INCREMENT FROM CLOUD SCHEME 
! PQI        : T9 RESOLVED CLOUD ICE SPECIFIC CONTENTS
! PQL        : T9 RESOLVED CLOUD LIQUID SPECIFIC CONTENTS
! PQW        : WET BULB SPECIFIC MOISTURE
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : WET BULB TEMPERATURE.
! PU         : U WIND COMPONENT 
! PV         : V WIND COMPONENT 
! PVERVEL    : (OMEGA/P) MEAN GRID BOX VERT. VEL. DIVIDED BY PRESSURE.
! PFCSQL     : RESOLVED SCHEME LIQUID CONDENSATION FLUX
! PFCSQN     : RESOLVED SCHEME ICE CONDENSATION FLUX

! - 2D (0:KLEV) .

! - 1D (PROGNOSTIC) .

! PTS        : SURFACE TEMPERATURE.
! PDECRD     : DECORRELATION DEPTH FOR CLOUD OVERLAPS [Pa].

! - 1D (DIAGNOSTIC) .

! PTAUX      : CHARACTERISTIC TIME FOR CAPE CONSUMPTION.

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PDIFCQ     : WATER VAPOUR CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQI    : ICE WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQL    : LIQUID WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCS     : DRY STATIC ENERGY CONVECTIVE SPECIFIC MOISTURE FLUX
! PFCCQL     : LIQUID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PFCCQI     : SOLID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PSTRCU     : U-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCV     : V-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PZATSLC    : ATTENUATION FACTOR FOR SLANTWISE CONVECTION.

! - 2D (1:KLEV) .

! PFRDE  : MESH FRACTION OCCUPIED BY DETRAINED AIR
! PDETFI : MESH FRACTION OCCUPIED BY INSTANTANEOUS DETRAINED AIR
! KNACT  : CONVECTIVE ACTIVITY INDEX (ACTIVE IF EQUAL TO ONE).
! PTU    : TEMPERATURE PERTURBATION OF THE CLOUDY ASCENT.
! PQU    : WATER VAPOUR SPECIFIC HUMIDITY PERTURBATION OF THE ASCENT.
! PUU    : ZONAL VELOCITY PERTURBATION IN UPDRAFT
! PVU    : MERIDIONAL VELOCITY PERTURBATION IN UPDRAFT

! - 1D (DIAGNOSTIQUE) .

! KNND   : ZERO WHENEVER A NON PHYSICAL SITUATION OCCURS 
!          OR IF NO TRIGGERING, ONE OTHERWISE 
! PCAPE  : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).

!-----------------------------------------------------------------------

! -   INPUT/OUTPUT ARGUMENTS 
!     ----------------------

! - 2D (0:KLEV) .

! - 2D (1:KLEV) .

! PGEOSLC  : EQUIVALENT GEOPOTENTIEL POUR CALCUL DE CONVECTION EN PENTE.
! PUDAL    : PROGNOSTIC UPDRAFT MESH FRACTION
! PUDOM    : PROGNOSTIC UPDRAFT PERTURB. VELOCITY 

! -1D
! PUDGRO    : Fractional localisation of updraft top between model levels
! PCUCONVCA : CA array for interaction with convection
! PNLCONVCA : CA array for interaction with convection
!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS 
!     ------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     *ACTRGRC*
!     *ACTRGKF*




!     *APLMINI*

!     Methode.
!     --------
!     Base: ACCVUD, ALARO-0 AL35 2010, ADAPTATIONS FOR ALARO-1

!     Auteur.
!     -------
!        05/2011 - L. Gerard.

!     Modifications.
!     --------------
!-----------------------------------------------------------------------


USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1    , ONLY: JPIM, JPRB
USE YOMHOOK     , ONLY : LHOOK,   DR_HOOK

USE YOMCST      , ONLY : RG       ,RD       ,RV       ,RCPD     ,RDT     ,&
            &            RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT   ,&
            &            RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
            &            RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
            &            RGAMD    
USE YOE_CUCONVCA, ONLY : TECUCONVCA
USE YOECUMF     , ONLY : TECUMF

IMPLICIT NONE

!     DUMMY INTEGER SCALARS
TYPE(TECUCONVCA)  ,INTENT(IN) :: YDECUCONVCA
TYPE(TECUMF)      ,INTENT(IN) :: YDECUMF
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV
INTEGER(KIND=JPIM),INTENT(IN) :: KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA

REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQCS(KLON,KLEV)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVORT0(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTAUX(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORI(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDECRD(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUDGRO(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCUCONVCA(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNLCONVCA(KLON)

INTEGER(KIND=JPIM),INTENT(OUT)   :: KNACT(KLON,KLEV) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNND(KLON)

REAL(KIND=JPRB)   ,INTENT(OUT)     :: PCAPE(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PDIFCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PDIFCQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PFCCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PFCCQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PFRDE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)     :: PDETFI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PSTRCV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)     :: PZATSLC(KLON,0:KLEV) 

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOSLC(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUDAL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUDOM(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PENTCH(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVU(KLON,KLEV)

! 2D LOCALS

REAL(KIND=JPRB) :: ZA10(KLON,KLEV),ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
    &,ZA15(KLON,KLEV)&
    &,ZBET(KLON,KLEV),ZUDAL9(KLON,KLEV),ZB0(KLON,KLEV),ZB1(KLON,KLEV)&
    &,ZDSE(KLON,KLEV),ZENT(KLON,KLEV),ZFSIG(KLON,KLEV),ZHSE(KLON,KLEV)&
    &,ZIFRAC(KLON,KLEV),ZIPOI(KLON,KLEV)&
    &,ZLDN(KLON,KLEV),ZLHE(KLON,KLEV),ZLHTBW(KLON,KLEV)&
    &,ZPOID(KLON,KLEV)&
    &,ZQC(KLON,KLEV),ZRHO(KLON,KLEV)&
    &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV)&
    &,ZTVE(KLON,KLEV),ZTVN(KLON,KLEV),ZUDOM(KLON,KLEV)&
    &,ZUM(KLON,KLEV),ZVM(KLON,KLEV)&
    &,ZFORA(KLON,0:KLEV)&
    &,ZFORM(KLON,0:KLEV),ZDQCA(KLON,0:KLEV),ZNEBC(KLON,KLEV)&
    &,ZQLC(KLON,KLEV),ZQIC(KLON,KLEV),ZLHS(KLON,KLEV),ZLHV(KLON,KLEV)&
    &,ZMELNET(KLON,KLEV),ZMELGET(KLON,KLEV),ZOME(KLON,KLEV), ZTEST(KLON,KLEV)&
    &,ZLHCORR(KLON,KLEV)

REAL(KIND=JPRB) :: ZICVG(KLON,KLEV),ZTACT(KLON,KLEV)

INTEGER(KIND=JPIM) :: INBAS(KLON,KLEV),INAC9(KLON,KLEV),INASC(KLON,KLEV),&
                    & INSCA(KLON,KLEV)

! 1D LOCALS

REAL(KIND=JPRB) :: ZALR1(KLON),ZCP(KLON),ZCR(KLON)&
    &,ZDLNSIG(KLON),ZDELP(KLON), ZDPHIF(KLON)&
    &,ZENEND(KLON),ZENDYN(KLON),ZENTRMN(KLON), ZENTRMX(KLON), ZENTUPH(KLON)&
    &,ZFMDQC(KLON)&
    &,ZHCMHEB(KLON),ZHU(KLON) &
    &,ZLCL(KLON),ZLA(KLON), ZLB(KLON), ZLH(KLON),ZLHSB(KLON)&
    &,ZOMB(KLON),ZIDPAD(KLON)&
    &,ZQA(KLON)&
    &,ZQB(KLON),ZTB(KLON),ZDELNL(KLON)&
    &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
    &,ZS3(KLON),ZS4(KLON),ZS5(KLON),ZS6(KLON)&
    &,ZS10(KLON)&
    &,ZS14(KLON)&
    &,ZS17(KLON),ZS18(KLON)& 
    &,ZRHLH(KLON),ZENTCH(KLON),ZSIG9(KLON)&
    &,ZSBAS(KLON),ZSDELQ(KLON), ZSINASC(KLON),ZSMIX0(KLON)&
    &,ZSIGS(KLON),ZSIGB(KLON),ZSIGE(KLON),ZSIGM(KLON)&
    &,ZSUM(KLON),ZSUM1(KLON),ZSUM2(KLON)&
    &,ZTA(KLON),ZPLCL(KLON),ZQUSL(KLON),ZTUSL(KLON),ZTLCL(KLON)
REAL(KIND=JPRB) :: ZUDGKX(KLON), ZUDGKY(KLON)
INTEGER (KIND=JPIM) :: JS3(KLON),JLOBA(KLON), JUSLACT(KLON), JUSLT(KLON),&
    &                  JLFCT(KLON),JNNS(KLON)

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: II, ILEV, ISUM, ITOP, &
    &    JH, JIT, JLEV, JLON, &
    &    JUSLMAX, JKEEPNBU , JDPBU

!     LOCAL REAL SCALARS
REAL(KIND=JPRB) :: ZA, ZABSUD,ZAT,ZALR9, ZAUX, &
  &ZB, ZBAS, ZBLUE, ZBUO, ZBU, &
  &ZC,ZC1, ZC2, ZC3, ZCOEF,ZCORIO,ZCPB,ZCPH, ZCPSMD, &
  &ZCPVMD,&
  &ZCPVMS, ZCPVMW, ZCPWMD, ZD, ZDAL, ZDCP, ZDEDYN, ZDELQ, ZDELT, &
  &ZDELTA, ZDEQC, ZDLNRHO, ZDM, &
  &ZDP, ZDPSGDT, &
  &ZDQW, ZDSCA, ZDTOTAUS, &
  &ZECMNP, ZENTR, ZENTRN, &
  &ZESP, ZEW, ZEXP, ZEXPND, ZEXPNS, &
  &ZFAC1, ZFAC2, ZFETA, ZFFAND, ZFMCORR, ZFMDQCL, ZFRAA, ZGDT, ZGDTI, &
  &ZHCMHE, ZHS_REE, ZHWE, &
  &ZIDP, ZIDT, ZINDX, ZIP,&
  &ZK, ZKQ, ZKS, ZKUO2, ZL, ZLIQ, ZLHB, ZLHH, ZLU, &
  &ZMH, ZMIX, ZMIX0, &
  &ZNBA, ZO, ZOD, ZO2, ZO9, ZOMEH, ZPATH, &
  &ZQU, ZQVU, &
  &ZQW, ZRVMD, ZSA, ZSB, ZSCO, ZSIGSS, ZSU, ZTH,&
  &ZTPHY, ZTU, ZTVUH, ZUP, ZUSIGE, &
  &ZVORT0, ZZ, ZZ2, ZZDQI, ZZDQL, ZZVAL
REAL(KIND=JPRB) :: ZAUTOADV, & 
  & ZBUFAC, ZCVENDYMAX, ZDPMIX2, ZDTMEAN, ZDYNENT,& 
  & ZENVSRH, ZJPRODFX, ZMIXVSRH, ZRHLH1,&
  & ZUDFR, ZKSKV, ZDPSCMN,ZFOMUSS,ZOMUN
! CONVERSION OF INTEGER INDEXES TO REAL 
!  (REQUIRED BY ARITHMETIC PRECISION PORTABILITY):
REAL(KIND=JPRB) :: ZINAC9, ZINAC, ZINASC,ZINSCA, ZRITO
! Prognostic entrainment:
REAL(KIND=JPRB) :: ZFD, &
                 & ZENTUR, ZOE
REAL(KIND=JPRB) :: ZFRAAD, ZENTRD, ZALFA
REAL(KIND=JPRB) :: ZSIGRA
! Cellular Automaton
REAL(KIND=JPRB) :: ZTSCATAU, ZLIVES

!-----------------------------------------------
!      ANCILLARY CONSTANTS.
!-----------------------------------------------
REAL (KIND=JPRB), PARAMETER :: ZSIGEMN=0._JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS1=-1.E-3_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS2=1.E-8_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS3=1.E-12_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS30=1.1E-12_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS4=1.E-5_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS5=1.E-11_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEP5C=1._JPRB-ZEPS5
REAL (KIND=JPRB), PARAMETER :: ZEPS6=1.E-03_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS7=1.E-11_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPS8=1.E-5_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPSALN=1.E-8_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPSDOM1=1.E-2_JPRB ! omega thrshld (Pa/s)
REAL (KIND=JPRB), PARAMETER :: ZEPSDOM3=1._JPRB    ! omega thrshld (Pa/s)
REAL (KIND=JPRB), PARAMETER :: ZEPSAC=1.E-12_JPRB
! Minimum value of ZLDN to start detraining
REAL (KIND=JPRB), PARAMETER :: ZEPSL=1.E-9_JPRB
REAL (KIND=JPRB), PARAMETER :: ZARLII=0.004_JPRB
REAL (KIND=JPRB), PARAMETER :: ZEPSIMP=1.E-12_JPRB

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     LOCAL LOGICAL SCALARS
LOGICAL :: LLBLTR,LLETRCLO, LLDEQC, LLLCL, LLMIXCLO, LLPROSIG,&
    &   LLRELSIG, LLBO, LLKUO2, LLRITO 

!-----------------------------------------------------
#include "actrgrc.intfb.h"
#include "actrgkf.intfb.h"
#include "aplmini.intfb.h"
!-----------------------------------------------------
! STATEMENT FUNCTIONS
!-----------------------------------------------------
#include "fcttrm.func.h"
#include "fctdoi.func.h"
 
REAL (KIND=JPRB) :: FUSIG,PX,PS
FUSIG(PX,PS)=1._JPRB-(MAX(0._JPRB,2._JPRB*PX-1._JPRB))**2&
             & *(1._JPRB-MIN(1._JPRB,4._JPRB*PS*PS))
!-------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ACCSU',0,ZHOOK_HANDLE)
ASSOCIATE(CGTRG=>YDML_PHY_MF%YRPHY%CGTRG,&
 & LCAPE=>YDML_PHY_MF%YRPHY%LCAPE, LCVGQ=>YDML_PHY_MF%YRPHY%LCVGQ, LCVGQM=>YDML_PHY_MF%YRPHY%LCVGQM, &
 & LCVSHCU=>YDML_PHY_MF%YRPHY%LCVSHCU, LENTCH=>YDML_PHY_MF%YRPHY%LENTCH, LNEBINS=>YDML_PHY_MF%YRPHY%LNEBINS,&
 & LPEC=>YDML_PHY_MF%YRPHY%LPEC,&
 & LSLC=>YDML_PHY_MF%YRPHY%LSLC, LUDEVOL=>YDML_PHY_MF%YRPHY%LUDEVOL, LUSL=>YDML_PHY_MF%YRPHY%LUSL, &
 & NBITER=>YDML_PHY_MF%YRPHY%NBITER, NCLOMIX=>YDML_PHY_MF%YRPHY%NCLOMIX, NFSIG=>YDML_PHY_MF%YRPHY%NFSIG,&
 & NIMELIT=>YDML_PHY_MF%YRPHY%NIMELIT, NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, &
 & ECMNP=>YDML_PHY_MF%YRPHY0%ECMNP, ECMNPI=>YDML_PHY_MF%YRPHY0%ECMNPI, ETACUT=>YDML_PHY_MF%YRPHY0%ETACUT, &
 & GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, GCVALBU=>YDML_PHY_MF%YRPHY0%GCVALBU, GCVALFA=>YDML_PHY_MF%YRPHY0%GCVALFA,&
 & GCVALMX=>YDML_PHY_MF%YRPHY0%GCVALMX, &
 & GCVENDY1=>YDML_PHY_MF%YRPHY0%GCVENDY1, GCVENDY2=>YDML_PHY_MF%YRPHY0%GCVENDY2, &
 & GCVENDYMX=>YDML_PHY_MF%YRPHY0%GCVENDYMX, GCVIDPBAS=>YDML_PHY_MF%YRPHY0%GCVIDPBAS,&
 & GCVKSKV=>YDML_PHY_MF%YRPHY0%GCVKSKV, GCVNU=>YDML_PHY_MF%YRPHY0%GCVNU, GCVTAUDE=>YDML_PHY_MF%YRPHY0%GCVTAUDE,&
 & GCVTAUSIG=>YDML_PHY_MF%YRPHY0%GCVTAUSIG, GENVSRH=>YDML_PHY_MF%YRPHY0%GENVSRH,&
 & GTRGDPMIX=>YDML_PHY_MF%YRPHY0%GTRGDPMIX, GTRGDPHIMN=>YDML_PHY_MF%YRPHY0%GTRGDPHIMN,&
 & RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, RMULACVG=>YDML_PHY_MF%YRPHY0%RMULACVG, RTCAPE=>YDML_PHY_MF%YRPHY0%RTCAPE,&
 & SCO=>YDML_PHY_MF%YRPHY0%SCO, TENTR=>YDML_PHY_MF%YRPHY0%TENTR, TENTRX=>YDML_PHY_MF%YRPHY0%TENTRX,&
 & TUDFR=>YDML_PHY_MF%YRPHY0%TUDFR, TUDGP=>YDML_PHY_MF%YRPHY0%TUDGP,&
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY,&
 & GCATHRCAP=>YDML_PHY_MF%YRPHY0%GCATHRCAP, GCATHRMOC=>YDML_PHY_MF%YRPHY0%GCATHRMOC, &
 & GCATHRCND=>YDML_PHY_MF%YRPHY0%GCATHRCND, GCAOMCND=>YDML_PHY_MF%YRPHY0%GCAOMCND, &
 & GCVCATAU=>YDML_PHY_MF%YRPHY0%GCVCATAU, &
 & LCUCONV_CA=>YDECUCONVCA%LCUCONV_CA,NLIVES=>YDECUCONVCA%NLIVES, &
 & NJKT4=>YDECUMF%NJKT4, NJKT5=>YDECUMF%NJKT5, YDPHY0=>YDML_PHY_MF%YRPHY0)
!*
!     -------------------
!     I - INITIALISATIONS
!     -------------------

!     ---------------------------------
!     Ia - TEST PARAMETERS AND SWITCHES
!     ---------------------------------
! HARD CODED TEST PARAMETERS
LLBLTR=LUSL
LLRITO=LUDEVOL
LLBO=.FALSE.                    ! T for Open loop
ZJPRODFX=1._JPRB                ! 1 for zform /= zfora
ZSIGRA=1.E-3_JPRB
JKEEPNBU=1_JPIM
JDPBU=1_JPIM                   ! Allow hole (Pa) in net buoyancy
ZDPSCMN=28000._JPRB            ! min dp of scaling levels
ZDTMEAN=0.5_JPRB
ZFOMUSS=1.5_JPRB               ! Max allowed ratio of omu/omuss
ZOMUN=-180._JPRB               ! Most negative allowed omega_u
!-----------------
!     ------------------------------------
!     Ib - DERIVED PARAMETERS AND SWITCHES
!     ------------------------------------
ZTPHY=TSPHY
ZLIVES=REAL(NLIVES,JPRB)
ZTSCATAU=ZTPHY/GCVCATAU
ZAUTOADV=0.5_JPRB*TSPHY  ! Inertial term in motion eqn (zero to drop it)
LLDEQC=GCVTAUDE<2000._JPRB           ! probably should use a separate key
ZUDFR=TUDFR/RG
II=NINT(GCVTAUSIG)
LLRELSIG=II>1
LLPROSIG=II<0
ZDYNENT=1._JPRB
ZENVSRH=MAX(0._JPRB,GENVSRH)
ZMIXVSRH=-MIN(0._JPRB, GENVSRH)
IF (GCVENDYMX<=ZEPS2) ZDYNENT=0._JPRB
ZCVENDYMAX=GCVENDYMX*ZDYNENT

ZEXPND=EXP(-TSPHY/GCVTAUDE)
ZDTOTAUS=-TSPHY/MAX(GCVTAUSIG,1.E-3_JPRB) ! 
ZEXPNS=EXP(ZDTOTAUS)                      ! for llrelsig
ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD
ZDPMIX2=GTRGDPMIX*0.5_JPRB
ZBUFAC=GCVALBU*RG*RG/RD

ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDTI=1.0_JPRB/ZGDT
ZKSKV=GCVKSKV/(2._JPRB*RG*RG)

ZENTRN=TENTR*SQRT(SQRT(TENTR/TENTRX))

!need to calculate the CVGQ closure
LLKUO2=LCVGQ

!------------------------------------

!*
!     ----------------------------------------------
!     IIa - COMPUTATION OF ANCILLARY VARIABLES
!     ----------------------------------------------
! ZOME      : RESOLVED OMEGA VERICAL VELOCITY
! ZQC       : TOTAL CONDENSATE OF ENVIRONMENT, GUARANTEED >=0 FROM APLPAR.
! ZTVE      : VIRTUAL ENVIRONMENT TEMPERATURE 
! ZRHO      : MEAN GRID-BOX DENSITY
! ZLHV      : LIQUID TO VAPOUR LATENT HEAT
! ZLHS      : ICE TO VAPOUR LATENT HEAT
! ZLHE      : ARRAY FOR ENVIRONMENT LOCAL LATENT HEAT.
! ZDSE      : LOCAL ARRAY FOR DRY STATIC ENERGIES.
! ZIFRAC    : ICE FRACTION IN THE UD, INITIALISE HERE WHERE NO ASCENT
ZOME(KIDIA:KFDIA,:)=PVERVEL(KIDIA:KFDIA,:)*PAPRSF(KIDIA:KFDIA,:)
ZQC=PQI+PQL
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZTVE(JLON,JLEV)=(1.0_JPRB-ZQC(JLON,JLEV)&
                 & +RETV*PQ(JLON,JLEV))* PT(JLON,JLEV)
    ZRHO(JLON,JLEV)=PAPRSF(JLON,JLEV)/(RD*ZTVE(JLON,JLEV))
    ZLHV(JLON,JLEV)=FOLH(PT(JLON,JLEV),0.0_JPRB)
    ZLHS(JLON,JLEV)=FOLH(PT(JLON,JLEV),1.0_JPRB)
    ZDELTA=FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC) 
    ZIFRAC(JLON,JLEV)=ZDELTA ! default for places with no ud
    ZLHE(JLON,JLEV)=FOLH(PT(JLON,JLEV),ZDELTA)
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
  ENDDO
ENDDO

! 1D ARRAYS:
!    ZLHSB    : BUDGET LATENT HEAT AS COMPUTED AT THE SURFACE.
!    ZHCMHEB  : EXCESS MOIST STATIC ENERGY OF THE SATURATED ADIABAT
!    ZS17     : INTEGRATED BUOYANCY OF THE NON DILUTED ADIABAT
!    ZS18     : INTEGRATED RELATIVE HUMIDITY TO COMPUTE BUOYANCY WEIGHTED
!               AVERAGE IF (LENTCH.AND.LCDDPRO)

! 2D ARRAYS:
!    ZA10     : FIRST GUESS OF SATURATED ADIABAT (ZHSEAD)
!    ZHSE     : ENVIRONMENT MOIST STATIC ENERGY

DO JLON=KIDIA,KFDIA
  ZDELTA=FONICE(PTS(JLON),YDPHY0%RDTFAC)
  ZLHSB(JLON)=FOLH(PTS(JLON),ZDELTA)-ZCPVMD*PTS(JLON)
  ZA10(JLON,KLEV)=ZDSE(JLON,KLEV)+ZLHSB(JLON)*PQ(JLON,KLEV)
  ZHCMHEB(JLON)=0.0_JPRB
  ZRHLH(JLON)=0.5_JPRB*PQ(JLON,KLEV)/PQSAT(JLON,KLEV)
  ZHSE(JLON,KLEV)=ZA10(JLON,KLEV)
  ZS17(JLON)=0._JPRB
  ZS18(JLON)=0._JPRB
ENDDO
DO JLEV=KLEV-1,KTDIA,-1
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PGEOSLC(JLON,JLEV)=MIN(PGEOSLC(JLON,JLEV),PGEOSLC(JLON,JLEV+1)&
     & *PTW(JLON,JLEV)/PTW(JLON,JLEV+1))
    IF (LSLC) THEN
      ZINDX=MAX(0.0_JPRB,SIGN(1.0_JPRB,ABS(PRCORI(JLON))-ZEPS5))
      ZCORIO=ZEPS5+ZINDX*(PRCORI(JLON)-ZEPS5)
      ZVORT0=0.5_JPRB*(PVORT0(JLON,JLEV)+PVORT0(JLON,JLEV+1))
      ZFETA=ZINDX/(MAX(ETACUT,1.0_JPRB+ZVORT0/ZCORIO))
      PZATSLC(JLON,JLEV)=ZFETA*0.5_JPRB*(PGEOSLC(JLON,JLEV)+&
       & PGEOSLC(JLON,JLEV+1))&
       & *((PU(JLON,JLEV+1)-PU(JLON,JLEV))**2+(PV(JLON,JLEV+1)&
       & -PV(JLON,JLEV))**2)/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))**2
    ELSE
      PZATSLC(JLON,JLEV)=0.0_JPRB
    ENDIF
    ZHSE(JLON,JLEV)=ZDSE(JLON,JLEV)+ZLHE(JLON,JLEV)*PQ(JLON,JLEV)
    ZA10(JLON,JLEV)=MAX(ZA10(JLON,JLEV+1)+(PAPHIF(JLON,JLEV)&
      &-PAPHIF(JLON,JLEV+1))*PZATSLC(JLON,JLEV)/(1.0_JPRB&
      &+PZATSLC(JLON,JLEV)),ZHSE(JLON,JLEV))
    ZHCMHE=0.5_JPRB*(ZA10(JLON,JLEV)-ZHSE(JLON,JLEV))
    ZRHLH1=0.5_JPRB*PQ(JLON,JLEV)/PQSAT(JLON,JLEV)
    ZS17(JLON)=ZS17(JLON)+(ZHCMHEB(JLON)+ZHCMHE)*&
     &(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZS18(JLON)=ZS18(JLON)+(ZRHLH1+ZRHLH(JLON))*(ZHCMHEB(JLON)+ZHCMHE)*&
     &(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZHCMHEB(JLON)=ZHCMHE
    ZRHLH(JLON)=ZRHLH1
  ENDDO
ENDDO

!*
!     ----------------------------------------------------
!     IIb - PRELIMINARY TREATMENT OF PROGNOSTIC VARIABLES 
!           AND CLOUD-HISTORY ANCILLARY VARIABLES
!     ----------------------------------------------------
! PREPARE THE PROGNOSTIC VARIABLES AND INDEXES
!  2-D INPUT VALUES:
!     PUDAL=prognostic mesh fraction (cf original 3MT)
!           has positive values up to FINAL detrainment level.
!     ZUDOM= instantaneous UPDRAFT perturbation velocity: returns 
!           to zero above CURRENT POSITION OF THE CLOUD TOP
!  DERIVED VALUES:
!  2-D:
!     INAC9 : 1 WHERE T9 ZUDOM <0 AND (MIN(0,ZUDOM)+ZOME)<0
!     ZUDAL9: T9 MESH FRACTION (RESET TO ZERO BELOW A THRESHOLD)
!  1-D:
!     ZSIG9 : REFERENCE (BASE OR MEAN) MESH FRACTION FROM T9
!----------------------------------------------------------------------
ZSIG9=MIN(GCVALMX, MAX(0._JPRB,MAXVAL(PUDAL,DIM=2)) )
! ZSUM: THRESHOLD OF PUDAL TO DECREE ZUDAL9/=0
DO JLON=KIDIA,KFDIA
   ZSUM(JLON)=MAX(ZEPSALN,ZSIG9(JLON))*ZSIGRA
ENDDO
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
! T9 MESH FRACTION VALUE                    (UP TO FINAL DETRAINMENT LEVEL)
     ZINAC9=MAX(0.0_JPRB,&
        & SIGN(1.0_JPRB, PUDAL(JLON,JLEV)-ZSUM(JLON)))
     INASC(JLON,JLEV)=NINT(ZINAC9)
     ZUDAL9(JLON,JLEV)=ZINAC9*MIN(PUDAL(JLON,JLEV),GCVALMX)
  ENDDO
ENDDO
!-------------------------

ZUDOM(KIDIA:KFDIA,KTDIA:KLEV)=PUDOM(KIDIA:KFDIA,KTDIA:KLEV)
! FILTER OUT POSITIVE VALUES OF ZUDOM
!  and limit values giving a too negative absolute velocity
DO JLEV=KLEV,KTDIA,-1
      DO JLON=KIDIA,KFDIA
        ZUDOM(JLON,JLEV)=MAX((MIN(0.0_JPRB,ZUDOM(JLON,JLEV))),&
                   & ZOMUN*(1._JPRB-ZUDAL9(JLON,JLEV)),&
                   & MIN(0._JPRB,ZOMUN-MIN(0._JPRB,ZOME(JLON,JLEV))))
      ENDDO
ENDDO ! jlev

! COMPLETE EARLIER ACTIVITY INDICATOR (UP TO CURRENT RISING TOP)

DO JLEV=KLEV,KTDIA,-1
   DO JLON=KIDIA,KFDIA
! INASC CONTAINS SIGMA CRITERION OR 1, MULTIPLY NOW BY OMEGA CRITERION
! BOTH FOR RESOLVED AND SUBGRID PART
          ZZ=MAX(0.0_JPRB,SIGN(1._JPRB,-ZUDOM(JLON,JLEV)-ZEPSDOM1)&
            &)*MAX(0._JPRB,SIGN(1._JPRB, -ZOME(JLON,JLEV)-ZEPSDOM1))
          INAC9(JLON,JLEV)=NINT(ZZ)*INASC(JLON,JLEV) 
   ENDDO
ENDDO

!*
!  -------------------------------------------------------------------
!  III - INITIAL CONDITIONS AT THE BOTTOM LEVEL TO START THE CLOUD PROFILE 
!           (USL OR UNTRIGGERED FOLLOWING LUSL) 
!  -------------------------------------------------------------------
! ACTIVITY INDEXES:
!  KNND     : PHYSICAL SOLUTION FLAG OR INDICATOR OF TRIGGERING
!  JNNS     : INDICATOR OF SHALLOW ACTIVITY WHEN KNND=0 FROM TRIGGERING
!  INSCA    : LAYER TAKES PART TO CLOSURE (buoyancy and cvgq for instance)
!  INASC    : LAYER PART OF A CONTINUED ASCENT
!------------------------------------
! CLOUD PROPERTIES:
!  PTU       : TEMPERATURE PERTURBATION OF THE CLOUDY ASCENT.
!  PQU       : WATER VAPOUR SPECIFIC HUMIDITY PERTURB OF THE CLOUDY ASCENT.
!  ZSDN      : DRY STATIC ENERGY PERTURBATION OF THE CLOUDY ASCENT
!  ZLDN      : CONDENSED SPECIFIC HUMIDITY PERTURBATION OF THE CLDY ASCENT.
!  ZTVN      : UPDRAFT VIRTUAL TEMPERATURE PERTURBATION
! ZMELNET    : INITIAL VERTICAL VELOCITY PERTURBATION FROM TRIGGERING
!------------------------------------
PTU(KIDIA:KFDIA,:)=0._JPRB
PQU(KIDIA:KFDIA,:)=0._JPRB
ZSDN(:,:)=0._JPRB
ZTVN(:,:)=0._JPRB
ZLDN(:,:)=0._JPRB ! ZQC
ZFSIG(:,:)=1._JPRB
ZMELNET(:,:)=0._JPRB
ZTEST(:,:)=1._JPRB
INSCA=0
INBAS=0
!--------------------------------------------------------------------------
IF (LUSL) THEN  ! CALL USL & TRIGGERING ROUTINE
! too shallow ascent must fulfill same trigng criteria as new-born ones
! (for deeper ones, inac9 prevents cutting by insufficient LCL buoyancy)

! MIN VERTICAL EXTENSION CRITERION to force trigggering
! USE GTRGDPHIMN THE MIN DEPTH FOR _DEEP_ CONVECTION.
   ZSUM2=0._JPRB
   DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
       ZSUM2(JLON)=ZSUM2(JLON)+INAC9(JLON,JLEV)*&
                  &(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV))
      ENDDO
   ENDDO
   DO JLON=KIDIA,KFDIA
      II=MAX(0,NINT(SIGN(1._JPRB,ZSUM2(JLON)-GTRGDPHIMN)))
      INASC(JLON,KTDIA:KLEV)=INAC9(JLON,KTDIA:KLEV)*II
   ENDDO


      ZA15(KIDIA:KFDIA,:)=ZLHE(KIDIA:KFDIA,:)/PCP(KIDIA:KFDIA,:)
      ZFORA(KIDIA:KFDIA,:)=PFCSQL(KIDIA:KFDIA,:)+PFCSQN(KIDIA:KFDIA,:)
      DO JLEV=KLEV,KTDIA,-1
        DO JLON=KIDIA,KFDIA
           ZBET(JLON,JLEV)=(ZFORA(JLON,JLEV)-ZFORA(JLON,JLEV-1))&
               &*ZA15(JLON,JLEV)*ZGDT
        ENDDO
      ENDDO
IF (CGTRG=='RC') THEN
      CALL ACTRGRC(YDECUMF,YDML_PHY_MF%YRPHY0, &
        & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
        & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PDELP,&
        & PQ, PQCS, PQSAT,PQW, PT,ZTVE,PTW, &
        & ZBET, ZS17, &
        & INASC, PQU, ZSDN, PTU, ZTVN, &
        & KNND, JNNS, JUSLT, JLFCT, ZPLCL, ZQUSL, ZTLCL, ZTUSL)
ELSE IF (CGTRG=='KF') THEN
      CALL ACTRGKF(YDECUMF,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
        & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PDELP,&
        & ZOME, PQ, PQCS, PQSAT,PQW, PT,ZTVE,PTW, &
        & ZBET, ZS17, &
        & INASC, PQU, ZSDN, PTU, ZTVN, &
        & KNND, JNNS, JUSLT, JLFCT, ZPLCL, ZQUSL, ZTLCL, ZTUSL)
ENDIF

! INBAS=1 AT THE LCL (CANNOT BE PUT IN NEXT LOOP THAT ADAPTS INASC)
   DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
         INBAS(JLON,JLEV)=INASC(JLON,JLEV)*(1-INASC(JLON,JLEV+1))
      ENDDO
   ENDDO
! UNDILUTE ASCENT LIMITS FROM H_U AT LCL 
!   (PROTECT UP TO LFC AND UP TO PREVIOUS HEIGHT)
! ALSO RESET ZA10 TO HU, FROM LCL TO TOP OF UNDILUTE AND 0 ELSEWHERE
   ZHU=0._JPRB
   ZIDPAD=0._JPRB 
   DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
         ZHU(JLON)=(1-INBAS(JLON,JLEV))*ZHU(JLON) +&
             & INBAS(JLON,JLEV)* ((RCPD+ZCPVMD*ZQUSL(JLON))*ZTLCL(JLON) &
             &  + PAPHIF(JLON,JLEV) + ZLHE(JLON,JLEV)*ZQUSL(JLON))
         INASC(JLON,JLEV)= INASC(JLON,JLEV)*MAX(& 
             & INAC9(JLON,JLEV),INBAS(JLON,JLEV), &
             & SIGN(1,JLEV-JLFCT(JLON)),&
             & MIN(NINT(SIGN(1._JPRB,ZHU(JLON)-ZHSE(JLON,JLEV))), &
             & INASC(JLON,JLEV+1)))
         ITOP=INASC(JLON,JLEV+1)*(1-INASC(JLON,JLEV))
         ZIDPAD(JLON)=REAL(ITOP,JPRB)/MAX(1._JPRB,&
                  & (ZPLCL(JLON)-PAPRS(JLON,JLEV)))&
                  & +REAL((1-ITOP),JPRB)*ZIDPAD(JLON)
         ZA10(JLON,JLEV)=ZHU(JLON)*INASC(JLON,JLEV)
      ENDDO
   ENDDO

! EXTEND INAC9 =1 FROM OLD LCL TO NEW LCL IN ALL CASES
! Initialize zmelnet between lcl and lfc
   JS3=0
   DO JLEV=KTDIA+1, KLEV
      DO JLON=KIDIA,KFDIA
         JS3(JLON)=MAX(JS3(JLON),INAC9(JLON,JLEV))
         INAC9(JLON,JLEV)=JS3(JLON)*INASC(JLON,JLEV)
         ZZ=ZUDOM(JLON,JLEV)*REAL(INAC9(JLON,JLEV), JPRB)
         ZMELNET(JLON,JLEV)=MIN(ZZ,ZMELNET(JLON,JLEV),0._JPRB)
      ENDDO
   ENDDO
ELSE ! not LUSL
! UNTRIGGERED (MULTPLE) ASCENT: can start from any level
! ZLCL USED TO ESTIMATE ABILITY TO REACH LCL FOR UNTRIGGERED ASCENT
   INASC=1
   KNND=1
   JNNS=0
   JUSLT=1
   DO JLON=KIDIA,KFDIA
    ZTU=PTW(JLON,KLEV)
    ZQU=PQW(JLON,KLEV)
    PTU(JLON,KLEV)=ZTU-PT(JLON,KLEV)
    PQU(JLON,KLEV)=ZQU-PQ(JLON,KLEV)
!---------------------
!! ceci est trop sec => perturbation <0 par rapport Ã  la moyenne !
    ZLDN(JLON,KLEV)=MAX(0.0_JPRB,ZQC(JLON,KLEV)-&
      & (PQW(JLON,KLEV)-PQ(JLON,KLEV)) )-ZQC(JLON,KLEV)
!---------------------
    ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*ZQU)*ZTU+PAPHIF(JLON,KLEV)-ZDSE(JLON,KLEV)
    ZTVN(JLON,KLEV)=PTU(JLON,KLEV)*(1._JPRB+RETV*ZQU) &
         & + RETV*PT(JLON,KLEV)*PQU(JLON,KLEV)&
         & -ZLDN(JLON,KLEV)*ZTU-ZQC(JLON,KLEV)*PTU(JLON,KLEV)
    ZLCL(JLON)=PQSAT(JLON,KLEV)-PQW(JLON,KLEV)
    ZTEST(JLON,KLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZLCL(JLON)))
   ENDDO
ENDIF ! not LUSL
IF (.NOT.LCVSHCU) THEN
! SUPPRESS SHALLOW CLOUDS DIAGNOSED BY actrg*
   JNNS(KIDIA:KFDIA)=0
ENDIF
!------------------------------------------------------------------------
!*
!     ----------------------------------------------------------------
!     IIIb - GUESS MESH FRACTION
!     ----------------------------------------------------------------
! ZSIG9: MESH FRACTION FROM T9  (OBTAINED ABOVE)
! ZSIGB: MIN VALUE FROM INERTIAL CRITERION (AFTER TRIGGERING)
! AFTER THIS, THESE VALUES WILL BE RE-COMBINED INTO ZSIGB, ZSIGS

ZSIGS=ZSIG9 ! already guaranteed in [0, GCVALMX]

!-------------------------------------------------------------------------
!     -------------------------------------------
!     IIIc - VERTICAL VARIATION OF MESH FRACTION
!     -------------------------------------------
IF (NFSIG==2) THEN ! THIS IMPLIES LUSL=T
! GUESS OF ZFSIG (KNOWING LCL) WITH A FUNCTION OF REDUCED HEIGHT AND ZSIGS
! NO ENSEMBLE EFFECT WHERE KNND=0 (E.G. SHALLOW CLOUDS)
    DO JLEV=KTDIA,KLEV
       DO JLON=KIDIA,KFDIA
           ZINASC=REAL(INASC(JLON,JLEV),JPRB)
           ZFRAA=MIN(1._JPRB,MAX(0._JPRB,(ZPLCL(JLON)-PAPRSF(JLON,JLEV))&
             & *ZINASC*ZIDPAD(JLON)))*KNND(JLON)
           ZFSIG(JLON,JLEV)=FUSIG(ZFRAA,ZSIGS(JLON))*ZINASC
       ENDDO
    ENDDO
ENDIF
!-------------------------------------------------------------------------

DO JLON=KIDIA,KFDIA 
! For computation of zendyn:
   ZOMB(JLON)=0._JPRB         ! SS VEL FROM BELOW
   ZRHLH(JLON)=MAX(ZEPS5,ZS18(JLON))/MAX(ZEPS5,ZS17(JLON))
!----------------------------------------------------------
! lentch-type nu
! pentch: t9 prognostic mixing variable
!------------------------------------------------------
   IF(LENTCH) THEN
      ZALFA=GCVALFA*EXP(ZENVSRH*LOG(ZRHLH(JLON)))
      ZENTCH(JLON)=PENTCH(JLON,KLEV)*ZALFA/&
                  & MAX(ZEPSIMP,PENTCH(JLON,KLEV)+ZALFA)
   ELSE
      ZENTCH(JLON)=GCVNU
   ENDIF
   ZA15(JLON,KLEV)=0._JPRB
ENDDO
!--------------------------------------------------------------------------
! ZIFRAC    : ICE FRACTION IN THE UPDRAFT
DO JLON=KIDIA,KFDIA
  ZDELTA=FONICE(PTW(JLON,KLEV),YDPHY0%RDTFAC)
  ZIFRAC(JLON,KLEV)=ZDELTA
  ZTU=PTU(JLON,KLEV)+PT(JLON,KLEV)
  ZQU=PQU(JLON,KLEV)+PQ(JLON,KLEV)
  ZHU(JLON)= (RCPD+ZCPVMD*ZQU)*ZTU+ FOLH(ZTU,ZDELTA)*ZQU
ENDDO
! INITIALISE CURRENT-LEVEL REAL MESH FRACTION TO BASE GUESS 
ZSIGB=ZSIGS
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
      ZUDAL9(JLON,JLEV)=ZSIGB(JLON)*ZFSIG(JLON,JLEV)*INAC9(JLON,JLEV)
   ENDDO
ENDDO
! NOW FORCE zsigb/=0 for division in vertical loop (esp nfsig/=2)
IF (NFSIG/=2) ZSIGB=MAX(ZSIGB,ZEPSALN)

IF (LLKUO2) THEN
 DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZICVG(JLON,JLEV)=ZLHE(JLON,JLEV)*PCVGQ(JLON,JLEV)*PDELP(JLON,JLEV)
  ENDDO
 ENDDO
ENDIF

!*
!     ----------------------------------------------------------
!     IV - UPWARDS VERTICAL LOOP COMPUTING BASE ASCENT
!     STEADY-STATE PROPERTIES AND ENTRAINMENT/MIXING COEFFICIENTS
!     ----------------------------------------------------------
! ITOP     : MAY HELP SKIPPING UNNECESSARY COMPUTATIONS IN THE UPPER 
!            PART OF THE ATMOSPHERE 
! 2D ARRAYS:
ZRMIX=0._JPRB   ! LAMBDA
ZPOID=0._JPRB   ! LAMBDA_W_TUR * DPHI;  AND AFTER,  THE SAME/DP
ZIPOI=0._JPRB   ! LAMBDA_DYN * DPHI;  AND AFTER,  THE SAME/DP
ZDQCA=0._JPRB   ! CONDENSATION INCREMENT (at interface)
ZLHCORR=0._JPRB ! L/cp*dq_ca at interface for closure
ZVM=0._JPRB     ! SS VELOCITY PERTURBATION (OMEGA_U-OMEGA)_ss
ZA13=0._JPRB    ! zvm*(q_u-q_e) for closure
ZA14=0._JPRB    ! zvm*(s_u-s_e) for closure
ZQLC=0.0_JPRB  ! d(RHO)/RHO/(-DP) <0

! 1D ARRAYS:
ZENDYN=0.0_JPRB
ZS10=0.0_JPRB  ! MAX BUOYANCY ENCOUNTERED BELOW
JUSLACT=1      ! HANDLING OF BUOYANCY INTERRUPTION:
JS3=0          ! ACCUMULATED DP OF CONSECUTIVE NON-BUOYANT LEVELS
ZS4=0._JPRB    ! CVGQ CRITERION FOR BOUVEAULT ASCENT
ZS5=0.0_JPRB   ! DPHI ABOVE UPDRAFT BASE
ZB0(:,KLEV)=0._JPRB
ZB1(:,KLEV)=0._JPRB

! SCALAR:
ITOP=KLEV+1

! TURBULENT MIXING PARAMETERS
!       IF ZDYNENT=1, DIRECTLY USE CONSTANT TENTR FOR DEEP AND CONSTANT TENTRX FOR SHALLOW
!                     IN THIS CASE ZENVSRH DOES NOT PLAY (AND SHOULD BE ZERO)
IF (ZDYNENT>0.5_JPRB) THEN
   DO JLON=KIDIA,KFDIA
! Give deep value everywhere except where shallow (cannot be zero)
      ZENTRMN(JLON)=TENTR*(1-JNNS(JLON))+TENTRX*JNNS(JLON)
      ZENTRMX(JLON)=ZENTRMN(JLON)
      ZENEND(JLON)=ZENTRMN(JLON)
   ENDDO
ELSE ! No Organized entrainment
   DO JLON=KIDIA,KFDIA
      ZZ=GCVALFA*ZS17(JLON)*EXP(ZENVSRH*LOG(ZRHLH(JLON)))
      ZCOEF=ZZ*TENTR/(TENTR+TENTRX/(1.0_JPRB+ZZ*TENTRX))
      ZENTRMX(JLON)=ZENTRN+(TENTRX-ZENTRN)/(1.0_JPRB+(TENTRX-ZENTRN)*ZCOEF)
      ZENTRMN(JLON)=ZENTRN+(TENTR-ZENTRN)/(1.0_JPRB+(TENTR-ZENTRN)*ZCOEF)
      ZENEND(JLON)=ZENTRMX(JLON)*SQRT(SQRT(ZENTRMN(JLON)/ZENTRMX(JLON)))
   ENDDO
ENDIF
! MAIN VERTICAL LOOP:
DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
!==========================================================================
! ---------------------------------------------
! IVa - COMPUTE ENTRAINMENT COEFFICIENTS
!     ENTRAINMENT at Starting full level JLEV+1
!     OUTPUT: ZENTR, ZENTUR and ZENDYN
! ---------------------------------------------

! ZFRAA    : UNCOMPLETED FRACTION OF THE ASCENT (1 at LCL, approaches 0 at TOP)
! ZENDYN   : ORGANIZED (DYNAMICAL) ENTRAINMENT PART
! ZENTUR   : TURBULENT ENTRAINMENT PART
    ZDPHIF(JLON)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
    ZDELP(JLON)=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)

! IVa.1 TURBULENT ENTRAINMENT PART
    IF (LUSL) THEN
       ZFRAAD=1._JPRB-MIN(1._JPRB,&
            &MAX(0._JPRB,(ZPLCL(JLON)-PAPRSF(JLON,JLEV))*ZIDPAD(JLON)))
    ELSE ! old ud limits, if not lusl
! diagnostic entrainment ZENTRD
       ZFRAAD=EXP(-ZENEND(JLON)*ZS5(JLON))
    ENDIF
    ZENTRD=ZENTRMN(JLON)+(ZENTRMX(JLON)-ZENTRMN(JLON))*ZFRAAD
!------------------
! DIAGNOSTIC MIXING
!------------------
    ZFRAA=ZFRAAD
    ZENTUR=ZENTRD
    ZZ=ZDPHIF(JLON)/(1._JPRB-ZSIGB(JLON))
    ZENTUPH(JLON)=ZENTUR*ZZ
    ZSMIX0(JLON)=(ZENTUR+ZUDFR)*ZZ
! MIXING DEPENDENCY ON LOCAL RELATIVE HUMIDITY  (if genvsrh<0)
! APPLY IT ONLY FOR DEEP CLOUDS (KNND=1)
    ZS18(JLON)=1._JPRB+ZMIXVSRH*KNND(JLON)*&
              & (.3_JPRB-MIN(1._JPRB,(PQ(JLON,JLEV+1)+PQ(JLON,JLEV))&
              & /(PQSAT(JLON,JLEV+1)+PQSAT(JLON,JLEV))))
! MEMORY OF LAMBDA_TURB*DPHI FOR FINAL PROGNOSTIC EQUATION 
! WILL BE DIVIDED BY DP BELOW
    ZPOID(JLON,JLEV)=(ZENTUR +ZUDFR)*ZDPHIF(JLON)
! TURB MIXING FOR DETRAINMENT FRACTION CALCULATION
    ZENT(JLON,JLEV+1)=ZENTUR*ZS18(JLON)*&
                     & (PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1))

! IVa.2 ORGANIZED ENTRAINMENT PART AND ESTIMATION OF STEADY STATE OMEGA_U

! GUESS OF ZENDYN FROM PREVIOUS ZENDYN
    ZINASC= REAL(INASC(JLON,JLEV+1),JPRB)
    ZENDYN(JLON)=ZDYNENT*MAX(0._JPRB,(ZENDYN(JLON)&
           & -ZINASC*GCVENDY1*EXP(-MAX(0._JPRB,GCVENDY2*ZS5(JLON)))))
!==========================================================================
!-----------------------------
! IVb -  STARTING LEVEL JLEV+1
!-----------------------------
! ZTA      :  Evolving ascending parcel properties
! ZQA      : 
! ZLA      : 
! ZQB, ZTB : memory of values at the bottom of the slab.
! ZHS_REE  : MOIST STATIC ENERGY AT THE BASE OF THE LAYER after mixing

    ZTA(JLON)=PTU(JLON,JLEV+1)+PT(JLON,JLEV+1)
    ZQA(JLON)=PQU(JLON,JLEV+1)+PQ(JLON,JLEV+1)
    ZLA(JLON)=ZLDN(JLON,JLEV+1)+ZQC(JLON,JLEV+1)



    ZTB(JLON)=ZTA(JLON)
    ZQB(JLON)=ZQA(JLON)
    ZLB(JLON)=ZLA(JLON)
    ZLHTBW(JLON,JLEV+1)=ZLHE(JLON,JLEV+1)

    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ZDELNL(JLON)=ZDELTA
! Use an accurate local estimation of zhs_ree
    ZHS_REE=(RCPD+ZCPVMD*ZQB(JLON))*ZTB(JLON)+PAPHIF(JLON,JLEV+1)&
          &+FOLH(ZTB(JLON),ZDELTA)*ZQB(JLON)
!-----------------------------------------------------
! IVc - HYDROSTATIC COEFFICIENTS FOR THE ASCENT SEGMENT
!-----------------------------------------------------

! - TEMPORAIRE(S) 1D .

! ZRBB      : WEIGHT OF "Tu" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH      : WEIGHT OF "Tu" AT "qu=ZQB" IN THE SAME THICKNESS.
! ZRVH      : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB &
     &-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

!     TO COMPUTE SATURATED ADIABATIC PROFILE, GCVADS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*ZDPHIF(JLON)/ZTB(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

! ZFFAND    : FRACTION OF BUOYANCY-EXCESS OVER AN UNDILUTE PLUME.

    ZFFAND=1.0_JPRB/(1.0_JPRB+ZENTCH(JLON)*(1.0_JPRB-ZFRAA) &
      & *MAX(0.0_JPRB,ZA10(JLON,JLEV)-ZHS_REE))
    ZFFAND=ZFFAND/(1.0_JPRB+PZATSLC(JLON,JLEV))
    ZRBB(JLON)=ZRBB(JLON)*ZFFAND
    ZRBH(JLON)=ZRBH(JLON)*ZFFAND
    ZRVH(JLON)=ZRVH(JLON)*ZFFAND

! ZLH       : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP       : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)=FOLH(ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
     &ZCPWMD))*ZLB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     FIRST GUESS AS STARTING POINT FOR THE NEWTON LOOP
!     OBTAINED BY CHANGE OF LEVEL 
!     ZTA, ZQA will store the ascent segment T and Q

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTA(JLON)=ZTB(JLON)+ZDELT
    ZQA(JLON)=ZQB(JLON)+ZDELQ
  ENDDO
!-----------------------------------------------------
! IVd - COMPUTE MOIST ADIABAT SEGMENT WITH NEWTON LOOP
!-----------------------------------------------------

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA
!     SNOW OPTION DEPENDENT COMPUTATIONS.
!        Diagnostic ice fraction
      ZDELTA=ZDELNL(JLON)

!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (ZTA(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTA(JLON),ZDELTA))

!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQA(JLON))&
        &*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQA(JLON)=ZQA(JLON)+ZDELQ
      ZTA(JLON)=ZTA(JLON)+ZDELT
    ENDDO
  ENDDO
!----------------------------------------------------- 
! IVe to g - TREAT ARRIVAL POINT OF THE ASCENT SEGMENT
!----------------------------------------------------- 

  DO JLON=KIDIA,KFDIA
   IF (LUSL) THEN
    ZSINASC(JLON)= REAL(INASC(JLON,JLEV+1),JPRB)
    ZSBAS(JLON)=REAL(INBAS(JLON,JLEV),JPRB)
    IF (NFSIG==2) THEN! ZFSIG computed in advance and ZSIGS>=zepsaln
       ZZ=MAX(ZFSIG(JLON,JLEV)*ZSIGS(JLON),ZEPSALN)
       ZDLNSIG(JLON)=LOG(ZZ/MAX(ZSIGB(JLON),ZZ))
       ZSIGB(JLON)=ZZ
    ELSE ! neglect mesh fraction variation 
       ZDLNSIG(JLON)=0._JPRB
    ENDIF
   ELSE ! not lusl
    ZSINASC(JLON)=1._JPRB
    ZSBAS(JLON)=0._JPRB
    ZDLNSIG(JLON)=0._JPRB
   ENDIF ! LUSL
! NOW divide ZPOID by DP 
   ZPOID(JLON,JLEV)=ZPOID(JLON,JLEV)/ZDELP(JLON)
   ZSIGE(JLON)=MAX(1._JPRB-ZSIGB(JLON),ZSIGEMN)  ! zsigb<gcvalmx<1
  ENDDO 



  DO JLON=KIDIA,KFDIA
!------------------------------------------
! IVe - CONDENSATION AND ARRIVAL PROPERTIES
!------------------------------------------
!   POSITIVE CONDENSATION ASSOCIATED TO THE ASCENT SEGMENT l+1->l
! IVe1: FIRST GUESS 
    ZZ=MAX(0._JPRB, SIGN(1._JPRB,-ZEPSDOM1-ZOME(JLON,JLEV)))

!  REMOVE ILLICIT NEGATIVE CONDENSATION PRODUCED BY NEWTON LOOP
!  AND INACTIVATE WHEN RESOLVED *EVAPORATION* > SG CONDENSATION
!              or WHEN RESOLVED SUBSIDENCE
    ZZ2=ZQB(JLON)-ZQA(JLON)
    ZINASC=ZSINASC(JLON)*ZZ*MAX(0._JPRB,SIGN(1._JPRB,ZZ2-ZEPS3))
    ZDQCA(JLON,JLEV)=ZINASC*ZZ2
! real dry static energy difference including ensemblist effect
    ZCPH=RCPD+ZCPVMD*ZQA(JLON)
    ZLHH=FOLH(ZTA(JLON),ZDELNL(JLON))
    ZCPB=RCPD+ZCPVMD*ZQB(JLON)
    ZLHB=FOLH(ZTB(JLON),ZDELNL(JLON))
    ZSA=ZCPH*ZTA(JLON)+PAPHIF(JLON,JLEV)
    ZSB=ZCPB*ZTB(JLON)+PAPHIF(JLON,JLEV+1)
    ZDSCA=ZINASC*(ZSA-ZSB)
! HERE zendyn is Lambda'_dyn, zentr is Lambda'*|dp|
    ZENTR=ZENTUPH(JLON)*ZS18(JLON)&
        & +ZENDYN(JLON)*ZDELP(JLON)/ZSIGE(JLON)
    ZEXP=EXP(-ZENTR)
    ZB=(1._JPRB-ZEXP)/ZENTR
! ZAT CANNOT BE OBTAINED FROM ds/(cpdq) because of ensemblist effect
!    => use  L(T_h)/cp(qh)>zat>L(Tb)/cp(qb)
    ZAT=(ZLHH+ZLHB)*0.5_JPRB/ZCPH
! VERIFY THAT NET CONDENSATION OCCURS
    ZQU=PQ(JLON,JLEV)+ZEXP*PQU(JLON,JLEV+1)&
      & -ZB*(PQ(JLON,JLEV)-PQ(JLON,JLEV+1)+ZDQCA(JLON,JLEV))
    ZSU=ZDSE(JLON,JLEV)+ZEXP*ZSDN(JLON,JLEV+1)&
      & -ZB*(ZDSE(JLON,JLEV)-ZDSE(JLON,JLEV+1)-ZDSCA)
    ZTU=(ZSU-PAPHIF(JLON,JLEV))/ZCPH
! IVe2:  CORRECT FIRST GUESS TO OBTAIN SATURATION
    ZDELT=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTU))
    ZEW= FOEW (ZTU,ZDELT)
    ZESP=ZEW/PAPRSF(JLON,JLEV)
    ZQW= FOQS (ZESP)
    ZDQW=FODQS (ZQW,ZESP, FODLEW (ZTU,ZDELT))
    ZC=(ZQU-ZQW)/(ZB*(1._JPRB+ZAT*ZDQW))
! SECURITY: NOT MORE THAN INITIAL VAPOUR CAN BE CONDENSED 
!   NOT ALL INITIAL CONDENSATION CAN BE CANCELED: keep at least zeps30
!   and TAKE ZEPS30>ZEPS3 PREVENTING FURTHER RETURN TO BLUE POINT
    ZC=MAX(MIN(ZC,ZQB(JLON)-ZDQCA(JLON,JLEV)),-ZDQCA(JLON,JLEV)+ZEPS30)
    ZDQCA(JLON,JLEV)=ZDQCA(JLON,JLEV)+ZC
    ZSDELQ(JLON)=-ZDQCA(JLON,JLEV)
    ZQU=ZQU-ZB*ZC
! Beware, this way q_u can become < mean q when over-saturated
!    IN THIS CASE :
!              - GO BACK TO BLUE POINT ?
!              - OR RESET PTU=0=PQU ?
    ZINASC=ZINASC*MAX(0.0_JPRB,SIGN(1._JPRB,ZQU))
    ZTU=ZTU+ZB*ZAT*ZC 
! DO NOT OVERSTRIKE VALUES SET BY actrg* WHERE INBAS=1
    PQU(JLON,JLEV)=ZSBAS(JLON)*PQU(JLON,JLEV)+ZINASC*(ZQU-PQ(JLON,JLEV))
    PTU(JLON,JLEV)=ZSBAS(JLON)*PTU(JLON,JLEV)+ZINASC*(ZTU-PT(JLON,JLEV))
! UPDATE ZTU AND ZQU  FOLLOWING ZINASC EFFECT:
    ZTU=PTU(JLON,JLEV)+PT(JLON,JLEV)
    ZQU=PQU(JLON,JLEV)+PQ(JLON,JLEV)
    ZDQCA(JLON,JLEV)=ZDQCA(JLON,JLEV)*ZINASC
! STORE L/CP *dqca at interface for closure
    ZLHCORR(JLON,JLEV)=(ZLHH/ZCPH+ZLHB/ZCPB)*0.5_JPRB*ZDQCA(JLON,JLEV)
    ZTA(JLON)=ZTU
    ZQA(JLON)=ZQU
    ZSINASC(JLON)=ZINASC
  ENDDO

DO JLON=KIDIA,KFDIA
    ZDELTA=FONICE(ZTA(JLON),YDPHY0%RDTFAC)
    ZIFRAC(JLON,JLEV)=ZDELTA
!   ESTIMATE FRACTION OF CONDENSATE CONTINUING IN THE UPDRAFT
    ZECMNP=ZDELTA*ECMNPI+(1.0_JPRB-ZDELTA)*ECMNP
    
    ZLIQ=ZECMNP/(ZRBB(JLON)*ZTB(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
      & *ZSDELQ(JLON))*ZTA(JLON))
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLU=MAX(0.0_JPRB,ZLB(JLON)*ZEXP&
      & +ZSDELQ(JLON)*ZLIQ *(ZEXP-1.0_JPRB))
! Storage for vectorization:
    ZDELNL(JLON)=ZDELTA
    ZLA(JLON)=ZLU
    IF (LUSL) THEN
! rem: if not lusl, ZSBAS(JLON)=0 and zsinasc=1
       ZLDN(JLON,JLEV)=ZSBAS(JLON)*ZLDN(JLON,JLEV)&
                    & +ZSINASC(JLON)*(ZLA(JLON)-ZQC(JLON,JLEV))
    ELSE ! not lusl (then zsbas=0, zsinasc=1)
!   ZLCL is diagnosed Before "cold cloud" adaptation 
       ZLCL(JLON)=MIN(ZLCL(JLON),PQSAT(JLON,JLEV)-ZQA(JLON))
       ZTEST(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,-ZLCL(JLON)))
       ZLU=ZLA(JLON)*ZTEST(JLON,JLEV)
       ZLDN(JLON,JLEV)=ZLU-ZQC(JLON,JLEV)
       ZLA(JLON)=ZLU
    ENDIF
ENDDO
!--------------------------------------------------------------------
!-------------------------------------------
! IVf - COMPARISON TO ENVIRONMENT BLUE POINT
!       FINAL PTU, PQU, ZTVN, HU-HE
!-------------------------------------------
 DO JLON=KIDIA,KFDIA
! IF ENVIRONMENT BLUE POINT WOULD BE MORE BUOYANT, THEN
! REPLACE UPDRAFT VAPOUR AND TEMPERATURE (NOT CONDENSATE) BY IT
! AND RESET ZLU TO MEAN GRID BOX VALUE
! ALSO IF FINAL ZDQCA IS TOO SMALL 
      ZTU=ZTA(JLON)
      ZQU=ZQA(JLON)
      ZLU=ZLA(JLON)
      ZHWE=(RCPD+ZCPVMD*PQW(JLON,JLEV))*PTW(JLON,JLEV)+&
          & FOLH(PTW(JLON,JLEV),ZDELNL(JLON))*PQW(JLON,JLEV)
      ZHU(JLON)= (RCPD+ZCPVMD*ZQU)*ZTU+ FOLH(ZTU,ZDELNL(JLON))*ZQU
      ZBLUE=MAX(0._JPRB,SIGN(1._JPRB,ZHWE-ZHU(JLON)),&
           &    SIGN(1._JPRB,ZEPS3-ZDQCA(JLON,JLEV)))
      ZB=1._JPRB-ZBLUE
      ZQVU=ZQU
      ZTU=ZTU*ZB+PTW(JLON,JLEV)*ZBLUE
      ZQU=ZQU*ZB+PQW(JLON,JLEV)*ZBLUE
      ZLU=ZLU*ZB+ZQC(JLON,JLEV)*ZBLUE
      PTU(JLON,JLEV)=ZSBAS(JLON)*PTU(JLON,JLEV)+&
                   & ZSINASC(JLON)*(ZTU-PT(JLON,JLEV))
      PQU(JLON,JLEV)=ZSBAS(JLON)*PQU(JLON,JLEV)+&
                   & ZSINASC(JLON)*(ZQU-PQ(JLON,JLEV))
      ZLDN(JLON,JLEV)=ZSBAS(JLON)*ZLDN(JLON,JLEV)+&
                   & ZSINASC(JLON)*(ZLU-ZQC(JLON,JLEV))
      ZTVN(JLON,JLEV)=ZSBAS(JLON)*ZTVN(JLON,JLEV)+ZSINASC(JLON)*(&
                 & PTU(JLON,JLEV)*(1._JPRB+RETV*ZQU)&
                 & + RETV*PT(JLON,JLEV)*PQU(JLON,JLEV)&
                 &  -ZLDN(JLON,JLEV)*ZTU-ZQC(JLON,JLEV)*PTU(JLON,JLEV))
! Re-adapt the computed ZHU-ZHWE:
     ZHU(JLON)=ZHU(JLON)*ZB+ZHWE*ZBLUE
     ZA15(JLON,JLEV)=(ZHU(JLON)-ZHWE)! /ZSIGE(JLON)
! REM: DO NOT CHANGE CONDENSATION INCREMENT DEPENDING ON ZBLUE.
! (PQW>ZQA => would give negative condensation)
     ZTA(JLON)=ZTU
     ZQA(JLON)=ZQU
     ZLA(JLON)=ZLU
 ENDDO
!---------------------------------------------------------------

!---------------------------------------------------------------
! FINAL VALUE OF THE UPDRAFT DRY STATIC ENERGY PERTURBATION
 DO JLON=KIDIA,KFDIA
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQA(JLON))*ZTA(JLON)+PAPHIF(JLON,JLEV)&
                  & -ZDSE(JLON,JLEV)
 ENDDO
!==========================================================================
!-------------------------------------------------
! IVg - COMPUTE VELOCITY AT ARRIVAL LEVEL JLEV 
!-------------------------------------------------

   DO JLON=KIDIA,KFDIA
!-------------------------------------------------------------------------
!       USE ACTIVITY INDEX ZINSCA=1 IF NEGATIVE SS-VELOCITY PERTURBATION
!       ZINSCA CONSIDERED IN ENTRAINMENT COEFFICIENTS
!-------------------------------------------------------------------------
! THE (-DELTA P), ZBUO at INTERFACE LEVEL (L+1 -> L)
      ZIDP=1._JPRB/ZDELP(JLON)
      ZUSIGE=1._JPRB/ZSIGE(JLON)
      ZOMEH=(ZOME(JLON,JLEV)+ZOME(JLON,JLEV+1))*0.5_JPRB
      ZZ=(ZTVE(JLON,JLEV)+ZTVE(JLON,JLEV+1))*0.5_JPRB
! zbu is alpha_b rho g^2/T_v at interface
      ZBU=ZBUFAC*PAPRS(JLON,JLEV)/(ZZ*ZZ)
      ZTVUH=(0.5_JPRB*(ZTVN(JLON,JLEV)+ZTVN(JLON,JLEV+1)))
      ZBUO=ZBU*ZTVUH
! ZC=1 WHILE UNDER INITIAL CIN BARRIER (ZS10=MAX BUOYANCY BELOW < ZEPS7)
!      AND ALSO AT THE LCL
      ZS10(JLON)=MAX(ZS10(JLON),ZBUO)
      ZC=MAX(ZSBAS(JLON),SIGN(1._JPRB,ZEPS7-ZS10(JLON)))
! zoe=delta_oe/(1-sigma_u)
      ZOE=MAX(ZC,SIGN(1._JPRB,ZENDYN(JLON)*ZBUO))*ZDYNENT*ZUSIGE
      ZK=2._JPRB/(1._JPRB+ZOE)
      ZDLNRHO=LOG(ZRHO(JLON,JLEV))-LOG(ZRHO(JLON,JLEV+1))
      ZMIX0=-ZK*MAX(0._JPRB, ZSMIX0(JLON)*ZUSIGE-ZDLNRHO) ! <0
      ZC=-ZK*ZBUO*ZDELP(JLON) ! <0 if zbuo>0
      ZB=ZK*( (ZOME(JLON,JLEV)-ZOME(JLON,JLEV+1))-ZOMEH*ZDLNRHO)
      ZZ2=MAX(0._JPRB, SIGN(1._JPRB,-ZEPS8-ZMIX0))
      ZEXP=EXP(-ZDELP(JLON)*GCVIDPBAS)
      ZZ=MAX(0._JPRB, ZB*ZB+4._JPRB*ZMIX0*ZC)
      ZOD=ZZ2*MIN(0._JPRB,(ZB+SQRT(ZZ))/(2._JPRB*ZMIX0+1._JPRB-ZZ2))
      ZO9=ZOMB(JLON)*(1._JPRB-ZSBAS(JLON))-ZOD*ZEXP*ZSBAS(JLON)
! ZOD = SS VELOCITY AT ARRIVAL LEVEL JLEV
! BELOW THE LFC (MAY BE ABOVE LCL), KEEP (AT LEAST) VALUE IN ZMELNET.
! UPDATE ACTIVITY INDEX IF CANNOT ASCENT 
      ZO2=ZO9*ZO9
      ZA=1._JPRB-ZMIX0 ! >0
      ZC=ZC-ZO2 ! <0
      ZZ=ZB*ZB-4._JPRB*ZA*ZC
      ZZ2=MAX(0._JPRB, SIGN(1._JPRB,ZZ))
      ZOD=ZZ2*(-ZB-SQRT(ZZ2*ZZ))/(2._JPRB*ZA)
!-----------------------------------------
      ZOD=MIN(ZOD,0._JPRB)
!-----------------------------------------
! ZINSCA GOES TO ZERO WHEN TOTAL UPDRAFT VELOCITY NOT UPWARDS
      ZINSCA=MAX(0._JPRB, SIGN(1._JPRB,-ZOD-ZOME(JLON,JLEV)))
      ZINSCA=REAL(INASC(JLON,JLEV),JPRB)*ZINSCA
      IF (LLKUO2) THEN ! CASE OF REQUIRED-CVGQ CRITERION:
       ZKUO2=ZS4(JLON)+ZICVG(JLON,JLEV)+ZICVG(JLON,JLEV+1)
       ZINSCA=ZINSCA*MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZKUO2))
       ZS4(JLON)=ZS4(JLON)+REAL(INSCA(JLON,JLEV+1),JPRB)*ZICVG(JLON,JLEV+1)
      ENDIF
! UPDATE ZS5=DPHI ABOVE THE UPDRAFT BASE
      ZS5(JLON)=ZS5(JLON)+ZINSCA*ZDPHIF(JLON)
!----------------------
! STORE FINAL VALUES
!      ZVM    = FIRST GUESS SS-VELOCITY PERTURBATION (ABOVE LCL)
!      ZENDYN = MAX[0,(DM/M)]/DP
!      ZIPOI=ZENDYN
      ZOMB(JLON)=ZOD
      ZVM(JLON,JLEV)=ZOMB(JLON) *ZINSCA
      ZZ=2._JPRB*(ZOD-ZO9) /MIN(ZEPS1,ZOD+ZO9)+ZDLNSIG(JLON)
      ZOE=MAX(0._JPRB,SIGN(1._JPRB,ZZ))
! reset zendyn to 0 below the base and where no buoyancy:
      ZENDYN(JLON)=MAX(0._JPRB, MIN(ZCVENDYMAX,ZZ*ZOE*ZIDP))*ZINSCA
! MEMORIZE VALUES FOR PROGNOSTIC EQUATION AND FOR MOMENTUM TRANSPORT
      ZDEDYN=-MIN(0._JPRB,ZZ)*ZINSCA
      ZIPOI(JLON,JLEV)=ZENDYN(JLON)
      ZQLC(JLON,JLEV)=ZDLNRHO*ZIDP
      ZENTR=ZENTUPH(JLON)*ZS18(JLON)
      ZRMIX(JLON,JLEV)=ZENTR +ZENDYN(JLON)*ZDELP(JLON)
!---------------------------------------------------------------------
!  UPDATE ACTIVITY INDEXES AT ARRIVAL LEVEL FOR CSA ASCENT
!     If absolute ss velocity not upwards (e.g. not buoyant),
!     set inactive except if USL base (inbas=0 if lusl=F)
      INSCA(JLON,JLEV)=MAX(INBAS(JLON,JLEV), NINT(ZINSCA))
   ENDDO
! STORE OMEGA_USS*(q_u-q_e) AND *(s_u-s_e) FOR CLOSURE
   DO JLON=KIDIA,KFDIA
      ZOD=ZVM(JLON,JLEV)/ZSIGE(JLON)
      ZA13(JLON,JLEV)=ZOD*PQU(JLON,JLEV)
      ZA14(JLON,JLEV)=ZOD*ZSDN(JLON,JLEV)
   ENDDO
!----------------------------------------------------------
! IVh UPDATE INASC
!     SMOOTH ACTIVITY INDEXES AT ARRIVAL LEVEL FOR CSA+USL ASCENT
!----------------------------------------------------------
   IF (LUSL) THEN
      DO JLON=KIDIA,KFDIA
!-------------------------------------
! increment js3, the accumulated dp of non buoyant consecutive levels: 
! if it reaches jdpbu (e.g. 50 hpa), we cancel above.
! test: but do not cut if inac9=1 (by setting jkeepnbu=1)
         JS3(JLON)=(1-INSCA(JLON,JLEV))*&
               & (JS3(JLON)+INASC(JLON,JLEV)*NINT(PDELP(JLON,JLEV)))
         II=MAX(JKEEPNBU*INAC9(JLON,JLEV),MIN(1,JDPBU-JS3(JLON)))
         JUSLACT(JLON)=JUSLACT(JLON)*II
         INASC(JLON,JLEV)=INASC(JLON,JLEV)*JUSLACT(JLON)
         INSCA(JLON,JLEV)=INSCA(JLON,JLEV)*JUSLACT(JLON)
!-------------------------------------
      ENDDO ! end of horizontal loop
   ELSE
      DO JLON=KIDIA,KFDIA
         INASC(JLON,JLEV)=INSCA(JLON,JLEV) ! USED FOR ZENDYN GUESS
      ENDDO
   ENDIF
! ------------------------
! IVi - GLOBAL DIAGNOSTICS
! ------------------------
!   * VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.
   ISUM=0
   DO JLON=KIDIA,KFDIA
      ISUM=ISUM+INSCA(JLON,JLEV+1)
   ENDDO
   IF (ISUM /= 0) THEN
       ITOP=JLEV+1
   ENDIF
ENDDO ! END OF VERTICAL LOOP

!     QUITTING IN CASE OF NO CONVECTION EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA+1 ! ALLOW USING ITOP-1

IF (ITOP < KLEV+1) THEN ! START PSEUDO-RETURN BLOCK

! ----------------------
! IVj - POLISH THE WORK
! ----------------------
DO JLON=KIDIA,KFDIA
    ZLHTBW(JLON,KTDIA)=ZLHE(JLON,KTDIA)
    ZENT(JLON,KTDIA)=0._JPRB
ENDDO
! *  Limit INASC to the highest and lowest limit of INSCA
JS3=0
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
      JS3(JLON)=MAX(JS3(JLON),INSCA(JLON,JLEV))
      INASC(JLON,JLEV)=INASC(JLON,JLEV)*JS3(JLON)
! SUPPRESS NEGATIVE VALUES OF UPDRAFT PERTURBATIONS:
      ZSDN(JLON,JLEV)=MAX(0._JPRB,ZSDN(JLON,JLEV))
      PTU(JLON,JLEV)=MAX(0._JPRB,PTU(JLON,JLEV))
      PQU(JLON,JLEV)=MAX(0._JPRB,PQU(JLON,JLEV))
   ENDDO
ENDDO
JS3=0
ZRBB=0._JPRB
DO JLEV=KLEV,ITOP,-1
   DO JLON=KIDIA,KFDIA
      JS3(JLON)=MAX(JS3(JLON),INSCA(JLON,JLEV))
      INASC(JLON,JLEV)=INASC(JLON,JLEV)*JS3(JLON)
! INDICATOR FOR SS CLOSURE:
! CHECK accumulated height of actual scaling levels:
      ZA=-ZLHCORR(JLON,JLEV)*REAL(INSCA(JLON,JLEV),JPRB)*ZVM(JLON,JLEV)
      ZRBB(JLON)=ZRBB(JLON)&
            & +MAX(0._JPRB, SIGN(1._JPRB, ZA-1.E-4_JPRB))*PDELP(JLON,JLEV)
   ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
! ACTIVITY INDEX FOR STEADY-STATE MESH FRACTION: ALSO ALLOW SHALLOW CLOUDS
   ZRBB(JLON)=MAX(0._JPRB, SIGN(1._JPRB, ZRBB(JLON)-ZDPSCMN))*MAX(KNND(JLON),JNNS(JLON))
ENDDO

IF (.NOT.LUSL) THEN ! No triggering: set inasc to zero below base
   JS3=0
   DO JLEV=KLEV,KTDIA+1,-1
   DO JLON=KIDIA,KFDIA
      JS3(JLON)=MAX(JS3(JLON),INSCA(JLON,JLEV))
      INASC(JLON,JLEV)=INASC(JLON,JLEV)*MAX(INSCA(JLON,JLEV),JS3(JLON))
   ENDDO
   ENDDO
ENDIF

! RE-COMPUTE INBAS=1 AT THE LOWEST TRANSITION OF INSCA (upwards velocity)
JS3=1
! jloba initialised in the loop while js3=1
INBAS(:,KLEV)=INSCA(:,KLEV)
DO JLEV=KLEV,KTDIA+1,-1
   DO JLON=KIDIA,KFDIA
    INBAS(JLON,JLEV-1)=INSCA(JLON,JLEV-1)*(1-INASC(JLON,JLEV))*JS3(JLON)
    JLOBA(JLON)=JS3(JLON)*(JLEV-1)+(1-JS3(JLON))*JLOBA(JLON)
    JS3(JLON)=JS3(JLON)*(1-INBAS(JLON,JLEV-1))
   ENDDO
ENDDO




 DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
     ZFSIG(JLON,JLEV)=ZFSIG(JLON,JLEV)*REAL(INASC(JLON,JLEV),JPRB)
   ENDDO
 ENDDO




!------------------
! IVk - DIAGNOSTICS
!------------------

! * DIAGNOSTIC OF CAPE (FORECASTER NEED) WHATEVER CLOSURE
PCAPE(KIDIA:KFDIA)=0._JPRB
DO JLEV=ITOP,KLEV
  DO JLON=KIDIA,KFDIA
    PCAPE(JLON)=PCAPE(JLON) +REAL(INSCA(JLON,JLEV),JPRB)*RD&
      &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV) *ZTVN(JLON,JLEV)
  ENDDO
ENDDO
!    --------------------------------------------------------------
!    V - RECOMPUTE STEADY-STATE VELOCITY ACCOUNTING FOR ENVIRONMENT
!        STORE ZB0, ZB1 FOR PROGNOSTIC EQUATION AT THE END
!    --------------------------------------------------------------
! PUT BUOYANCY IN ZB0, 0 in ZB1
DO JLEV=KLEV-1,KTDIA,-1
   DO JLON=KIDIA,KFDIA
      ZUSIGE=1._JPRB/(1._JPRB-MAX(ZEPSALN,ZSIGS(JLON)*ZFSIG(JLON,JLEV)))
      ZZ=(ZTVE(JLON,JLEV)+ZTVE(JLON,JLEV+1))*0.5_JPRB
! zbu=alpha_b rho g^2/T_v at interface
      ZTVUH=(0.5_JPRB*(ZTVN(JLON,JLEV)+ZTVN(JLON,JLEV+1)))*ZUSIGE
      ZBU=ZBUFAC*PAPRS(JLON,JLEV)/(ZZ*ZZ)
      ZB0(JLON,JLEV)=ZTVUH*ZBU
   ENDDO
ENDDO
ZB1=0._JPRB

!    ------------------------------
!    VI   MESH-FRACTION AND CLOSURE
!    ------------------------------
!    ------------------------------------------------------------
!    VI.a - EDDY TRANSPORT REDUCTION CLOSURE: Estimation of ZSIGM
!    ------------------------------------------------------------
ZSIGM=0._JPRB
ZS6=0._JPRB
DO JLEV=KLEV,ITOP,-1
   DO JLON=KIDIA,KFDIA
      ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZDPSCMN-ZS6(JLON)))* &
      & MAX(0._JPRB,SIGN(1._JPRB,-ZEPSDOM1-ZVM(JLON,JLEV)))
      ZDP=INSCA(JLON,JLEV)*PDELP(JLON,JLEV)*ZZ
      ZOD=MIN(-ZEPSDOM1,ZVM(JLON,JLEV))
      ZO=MIN(0._JPRB,ZOME(JLON,JLEV))
      ZA=(1._JPRB-SQRT(ZOD/(ZOD+ZO)))
      ZSIGM(JLON)=ZSIGM(JLON)+ZDP*ZA
      ZS6(JLON)=ZS6(JLON)+ZDP
   ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
   ZSIGM(JLON)=ZSIGM(JLON)/MAX(1._JPRB,ZS6(JLON))
ENDDO
!    ------------------------
!    VI.b - ANCILLARY ARRAYS
!    ------------------------
ZUM(:,KTDIA:ITOP)=0._JPRB
ZBET(:,KTDIA:ITOP)=0._JPRB
DO JLEV=KLEV,ITOP,-1
 DO JLON=KIDIA,KFDIA
! Positive ds and negative dq associated with resolved condensation:
    ZA=(PFCSQL(JLON,JLEV)-PFCSQL(JLON,JLEV-1))
    ZB=(PFCSQN(JLON,JLEV)-PFCSQN(JLON,JLEV-1))
    ZBET(JLON,JLEV)=RG*(&
     & ZA*FOLH(PT(JLON,JLEV),0._JPRB)+ZB*FOLH(PT(JLON,JLEV),1._JPRB))
    ZUM(JLON,JLEV)=-RG*(ZA+ZB)
 ENDDO
ENDDO
IF (RTCAPE<0._JPRB) THEN ! RE-COMPUTE PTAUX HERE
! Tau cape=dp/<omega_u> = (sum dp)^2/sum(omega_u*dp)
! Prevent being shorter than 12 minutes.
! RTCAPE<0 and OMEGA<0 while PDELP>0
   ZTA(:)=0._JPRB
   ZTB(:)=0._JPRB
   DO JLEV=KLEV,ITOP,-1
    DO JLON=KIDIA,KFDIA
    ZINSCA=REAL(INSCA(JLON,JLEV),JPRB)
    ZTA(JLON)=ZTA(JLON)+(ZVM(JLON,JLEV)+ZOME(JLON,JLEV))&
             & *PDELP(JLON,JLEV)*ZINSCA
    ZTB(JLON)=ZTB(JLON)+PDELP(JLON,JLEV)*ZINSCA
    ENDDO
   ENDDO
   DO JLON=KIDIA,KFDIA
      PTAUX(JLON)=MAX(720._JPRB,&
               & RTCAPE*ZTB(JLON)*ZTB(JLON)/MIN(-1._JPRB, ZTA(JLON)))
   ENDDO
ENDIF
! PEC scaling array, full levels
ZA10(:,KTDIA:KLEV)=1._JPRB
IF (LPEC) THEN
   DO JLEV=KLEV,ITOP,-1
    DO JLON=KIDIA,KFDIA
       ZA10(JLON,JLEV)=-(ZVM(JLON,JLEV)+ZOME(JLON,JLEV))
    ENDDO
   ENDDO
ENDIF
!    ---------------------------------------------
!    VI.c - ACCUMULATIONS FOR GENERALIZED CLOSURE
!    ---------------------------------------------
!-------------------------------------------------
! we have:
!: ZVM = (omega_uss-omega) at full levels
!: ZFSIG=nu
!: INSCA=1 at (steady-state) buoyant or active levels
!: za15=h_u-h_we
!: zdqca= dq_ca at interface levels
!: ZBET= g*L*d(Fcs)
!: ZUM= g*d(Fcs)
!: ZA10= PEC coeffcient m at full levels
!-------------------------------------------------
ZS4=0._JPRB
ZS5=0._JPRB
ZS6=0._JPRB
ZS10=0._JPRB
ZS14=0._JPRB
ZS17=0._JPRB
ZS18=0._JPRB
ZLB(KIDIA:KFDIA)=ZA10(KIDIA:KFDIA,KLEV)
DO JLEV=KLEV,ITOP,-1
   DO JLON=KIDIA,KFDIA
! FULL LEVELS ACCUMULATIONS
! Integrant factor: insca*m*|dp|/p
      ZIP=REAL(INSCA(JLON,JLEV),JPRB)*ZA10(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDP=PDELP(JLON,JLEV)*ZIP
! ks, kq and (L*ks-kq)
      ZKS=(1._JPRB+RETV*PQ(JLON,JLEV))/PCP(JLON,JLEV)
      ZKQ=(RETV-ZCPVMD*ZKS)*PT(JLON,JLEV)
      ZL=ZLHE(JLON,JLEV)*ZKS-ZKQ
! m at INTERFACE LEVEL JLEV-1 (JLEV kept in ZLB)
      ZMH=(ZA10(JLON,JLEV)+ZA10(JLON,JLEV-1))*0.5_JPRB
! MOCON
      ZS4(JLON)=ZS4(JLON)+PCVGQ(JLON,JLEV)*ZL*ZDP
! RES COND
! ----> rather keep all (also evap, dd) for correct budget !
      ZS10(JLON)=ZS10(JLON)+ & !MAX(0._JPRB,&
    &  (-ZL*ZUM(JLON,JLEV))*ZIP
! UPDRAFT TRANSPORT
      ZFAC1=1._JPRB/PAPRSF(JLON,JLEV)&
    &       -(ZLB(JLON)-ZMH)*PRDELP(JLON,JLEV)/ZA10(JLON,JLEV)
      ZS5(JLON)=ZS5(JLON)- ZFSIG(JLON,JLEV)*ZFAC1*ZDP*&
    &                (ZA14(JLON,JLEV)*ZKS+ZA13(JLON,JLEV)*ZKQ)
! MOCON UD TRANSPORT CORRECTION 1
      ZS17(JLON)=ZS17(JLON)- ZFSIG(JLON,JLEV)*ZDP*&
    &      ZFAC1*ZKS*(ZA14(JLON,JLEV)+ZA13(JLON,JLEV)*ZLHE(JLON,JLEV))
! CAPE
      ZS14(JLON)=ZS14(JLON)+ZTVN(JLON,JLEV)*ZDP

! HALF LEVELS ACCUMULATIONS AT JLEV-1
! DELTA ZL AT JLEV-1 INTERFACE
      ZKS=(1._JPRB+RETV*PQ(JLON,JLEV-1))/PCP(JLON,JLEV-1)
      ZKQ=(RETV-ZCPVMD*ZKS)*PT(JLON,JLEV-1)
      ZFAC2=(ZLHE(JLON,JLEV-1)*ZKS-ZKQ)-ZL
! ANCILLARIES AT INTERFACE JLEV-1
      ZTH=(PT(JLON,JLEV)+PT(JLON,JLEV-1))*0.5_JPRB
      ZKS=(1._JPRB+RETV*(PQ(JLON,JLEV)+PQ(JLON,JLEV-1))*0.5_JPRB)
      ZKQ=(RETV-ZCPVMD*ZKS*2._JPRB/(PCP(JLON,JLEV)+PCP(JLON,JLEV-1)))*ZTH
      ZINSCA=REAL(MAX(INSCA(JLON,JLEV),INSCA(JLON,JLEV-1)),JPRB)&
     &       * (ZFSIG(JLON,JLEV)+ZFSIG(JLON,JLEV-1))*0.5_JPRB &
     &       *ZMH/PAPRS(JLON,JLEV-1)
      ZOD=(ZVM(JLON,JLEV)+ZVM(JLON,JLEV-1))*0.5_JPRB
! UPDRAFT CONDENSATION
      ZS6(JLON)=ZS6(JLON)-ZINSCA*ZOD*&
     &    (ZLHCORR(JLON,JLEV-1)*ZKS-ZDQCA(JLON,JLEV-1)*ZKQ)
! MOCON UD TRANSPORT CORRECTION 2
      ZS18(JLON)=ZS18(JLON)-ZINSCA*ZFAC2 &
     &   * (ZA13(JLON,JLEV)+ZA13(JLON,JLEV-1))*0.5_JPRB
! UPDATE MEMORY
      ZLB(JLON)=ZMH
   ENDDO
ENDDO
!    ------------------------------
!    VI.d - SELECT THE CLOSURE MIX
!    ------------------------------
LLETRCLO=.NOT.(LCAPE.OR.LCVGQ)! ETR CLOSURE: NO FURTHER COMPUTATION
LLMIXCLO=(LCAPE.AND.LCVGQ)
IF (NCLOMIX==1) THEN ! sigb2 on gc-CAPE and MOC 
   ZC=1._JPRB
ELSE ! eCAPE and MOC (etc.)
   ZC=ZEPSALN
ENDIF
IF (NCLOMIX==3) THEN ! sigB3 everywhere
   ZS3(:)=1._JPRB
ELSEIF (NCLOMIX==4) THEN ! sigB3 at deep, sigB2 at shallow
   DO JLON=KIDIA,KFDIA
      ZS3(JLON)=REAL(1-JNNS(JLON),JPRB)
   ENDDO
ELSE
   ZS3(:)=0._JPRB
ENDIF
DO JLON=KIDIA,KFDIA
   ZSIGE(JLON)=MAX(ZC,1._JPRB-ZSIGM(JLON))
   ZLB(JLON)=ZS14(JLON)/PTAUX(JLON)
ENDDO
ZLA(:)=0._JPRB ! DEFAULT FOR CAPE CLOSURE
IF (LLMIXCLO) THEN ! MIXED CLOSURE: COMPUTE ZLA
   DO JLON=KIDIA,KFDIA
      ZZ=ZS4(JLON)+ZLB(JLON)/ZSIGE(JLON)
      ZLA(JLON)=MIN(1._JPRB,MAX(0._JPRB,ZS4(JLON)/MAX(ZEPS4,ZZ)) )
   ENDDO
ELSEIF (LCVGQ) THEN  ! MOCON CLOSURE
   ZLA(:)=1._JPRB
ENDIF
! IMPOSE CVGQ CLOSURE FOR SHALLOW CLOUDS
DO JLON=KIDIA,KFDIA
   ZLA(JLON)=MAX(ZLA(JLON),REAL(JNNS(JLON),JPRB))
ENDDO
!    ---------------------------------------------
!    VI.e - FINAL STEADY-STATE MESH FRACTION 
!    ---------------------------------------------
DO JLON=KIDIA,KFDIA
!(alpha M-R)
    ZDM=ZLA(JLON)*ZS4(JLON)-ZS10(JLON) 
! (U-alphaN): MUST BE POSITIVE AS WELL AS ZS6 OR NOT PHYSICAL
    ZB=(ZS5(JLON)+ZS6(JLON)-ZLA(JLON)*(ZS17(JLON)+ZS18(JLON)))
    ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZS6(JLON)-ZEPSAC))*&
      &MAX(0._JPRB,SIGN(1._JPRB,ZB-ZEPSAC))
! (1-alpha) B/tau
    ZBU=(1._JPRB-ZLA(JLON))*ZLB(JLON)
! ZC2=1 for sigB2, 0 for sigB3
    ZC3=ZS3(JLON)
    ZC2=1._JPRB-ZC3
    ZSIGB(JLON)=ZZ*(ZDM+ZBU/MAX(ZC3,ZSIGE(JLON))) /&
                & MAX(ZEPS4,(ZB*MAX(ZC2,ZSIGE(JLON))+ZC3*ZDM))
    II=NINT(ZZ)
    KNND(JLON)=KNND(JLON)*II
    JNNS(JLON)=JNNS(JLON)*II
ENDDO
IF (LLETRCLO) THEN ! EDDY TRANSPORT REDUCTION CLOSURE
   DO JLON=KIDIA,KFDIA
      ZSIGB(JLON)=ZSIGM(JLON)*KNND(JLON)+ZSIGB(JLON)*JNNS(JLON)
   ENDDO
ENDIF ! lletrclo
DO JLON=KIDIA,KFDIA
   ZSIGB(JLON)=MAX(0._JPRB,MIN(GCVALMX, ZSIGB(JLON)))
ENDDO
!    -------------------------------------------------------
!    VI.f - COMPUTE PROGNOSTIC MESH FRACTION BASED ON OMU_SS
!    -------------------------------------------------------
IF (LLPROSIG) THEN ! COMPUTE sigma+ with sigss, before omu+
! we have:
!: ZVM = (omega_uss-omega) at full levels
!: ZFSIG=nu
!: INSCA=1 at (steady-state) buoyant or active levels
! we compute:
!:  ZS10= -sum[nu * L_u*zdqca * (omega_uss+omega)] at scaling levels
   ZS10=0._JPRB
   DO JLEV=KLEV-1,MAX(KTDIA,ITOP-1),-1
       DO JLON=KIDIA,KFDIA
! HALF LEVEL SUMMATION
         ZINSCA=REAL(MAX(INSCA(JLON,JLEV),INSCA(JLON,JLEV+1)),JPRB)
         ZTU=0.5_JPRB*&
           &(PT(JLON,JLEV)+PTU(JLON,JLEV)+PT(JLON,JLEV+1)+PTU(JLON,JLEV+1))
         ZOD=0.5_JPRB* (ZFSIG(JLON,JLEV)*(ZVM(JLON,JLEV)+ZOME(JLON,JLEV))&
               & +ZFSIG(JLON,JLEV+1)*(ZVM(JLON,JLEV+1)+ZOME(JLON,JLEV+1)))
         ZDELTA=FONICE(ZTU,YDPHY0%RDTFAC)
         ZA=-FOLH(ZTU,ZDELTA)*ZDQCA(JLON,JLEV)*ZINSCA
         ZS10(JLON)=ZS10(JLON)+ZA*ZOD
       ENDDO
   ENDDO
   ZS4=0._JPRB
   ZS6=0._JPRB
   DO JLEV=KLEV,ITOP,-1
     DO JLON=KIDIA,KFDIA
        ZINSCA=REAL(INSCA(JLON,JLEV),JPRB)
        ZZ=ZFSIG(JLON,JLEV)*PDELP(JLON,JLEV)
        ZS6(JLON)=ZS6(JLON)+ZZ*ZA15(JLON,JLEV)*ZINSCA
        ZA=ZKSKV /(ZRHO(JLON,JLEV)*ZRHO(JLON,JLEV))
        ZOD=ZVM(JLON,JLEV)+ZOME(JLON,JLEV)
        ZK=ZA*ZOD*ZOD
        ZS4(JLON)=ZS4(JLON)+ZZ*ZK*ZINSCA
     ENDDO
   ENDDO
   DO JLON=KIDIA,KFDIA
      ZS10(JLON)=ZS10(JLON)*ZTPHY
! Use zsigm to compare to absolute ud condensation in zs10
      ZSUM1(JLON)=MAX(0._JPRB,ZS6(JLON)/ZSIGE(JLON)+ZS4(JLON))
   ENDDO
   IF (LCUCONV_CA)THEN
      ZSUM(KIDIA:KFDIA)=PCUCONVCA(KIDIA:KFDIA)*ZTSCATAU/ZLIVES
      ZSUM2=1._JPRB+ZTSCATAU
   ELSE
      ZSUM=0._JPRB
      ZSUM2=1._JPRB
   ENDIF
!----------------------------------------------------------
! Final prognostic closure expression
! based on ZS10, ZSUM, ZSUM1, ZSUM2 and ZSIG9, ZSIGB
!----------------------------------------------------------
   DO JLON=KIDIA,KFDIA
       ZSIGSS=ZSIGB(JLON)
       ZC=ZS10(JLON)
       ZA=(ZSIG9(JLON)+ZSUM(JLON))*ZSUM1(JLON)
       ZD=ZSUM1(JLON)*ZSUM2(JLON)+ZS10(JLON)
       ZZ2=MAX(0._JPRB, SIGN(1._JPRB, ZD-ZEPS7))
       IF (LCVGQM) THEN ! multiply zc by a reduction coefficient alpha
          ZC1=MAX(0.01_JPRB, MIN(GCVALMX, &
            & ZZ2*(ZA+ZC*ZSIGB(JLON))/MAX(ZD,ZEPS7) ))
          ZC=ZC*MIN(1.0_JPRB,ZC1*RMULACVG)
       ENDIF
       ZSIGB(JLON)=MAX(0._JPRB, MIN(GCVALMX, ZZ2*(ZA+ZC*ZSIGSS)&
            &  /MAX(ZD,ZEPS7) ))
   ENDDO
ELSE IF (LLRELSIG) THEN
! RELAXATION TIME
   DO JLON=KIDIA,KFDIA
      ZEXP=1._JPRB-ZEXPNS
      ZSIGSS=ZSIGB(JLON)
      IF (LCVGQM) &
        & ZSIGSS=ZSIGSS*MAX(0._JPRB,MIN(1.0_JPRB,ZSIGSS*RMULACVG))
      ZSIGB(JLON)=MIN(ZSIGB(JLON), ZSIGSS*ZEXP+ZSIG9(JLON)*ZEXPNS)
   ENDDO
ENDIF ! Prognostic closure based on omu_ss or relaxation expression
!    ------------------------------------------------------
!    VI.c - PUT FINAL MESH FRACTION AT ALL LEVELS into ZA10
!    ------------------------------------------------------
ZA10(KIDIA:KFDIA,KTDIA:ITOP)=0._JPRB
DO JLEV=ITOP, KLEV
   DO JLON=KIDIA,KFDIA
      ZA10(JLON,JLEV)=REAL(INASC(JLON,JLEV),JPRB)*&
                      & ZFSIG(JLON,JLEV)*ZSIGB(JLON)
   ENDDO
ENDDO

!    ----------------------------------------------
!    VII - PROGNOSTIC UPDRAFT VELOCITY EQUATION AND
!         CLOUD TOP EVOLUTION
!         INSCA: 1 at levels with upwards ss velocity
!         KNACT: ACTIVITY INDEX
!         ZTACT: ACTIVITY DURATION
!         PUDGRO: FRACTION OF PATH COMPLETED ABOVE LAST ACTIVE LEVEL AT T9
!         ZALR1: FRACTION OF PATH COMPLETED ABOVE LAST ACTIVE LEVEL 
!         ZUDOM: omega_u perturbation at T9
!         ZMELNET: omega_u kick from actrg* + perturbation at T9 
!         ZUM:   omega_u perturbation at T1
!    -----------------------------------------------

IF (LUDEVOL) THEN
   ZUM=0._JPRB
   ZTACT=0._JPRB
   ZALR1=0._JPRB
   ZFORM=0._JPRB
   ZRITO=0._JPRB
! ZMELNET CONTAINS COMPLETE ADAPTED INITIAL VELOCITY PROFILE
! (BELOW and ABOVE CLOUD BASE)
! PRE_COMPUTE AUTO-ADVECTION TERM FOR PROGNOSTIC EQUATION
   ZS3=0._JPRB
   DO JLEV=KLEV-1,KTDIA,-1
     DO JLON=KIDIA,KFDIA
!---------------------------------------
! LIMIT OMEGA_U- to ZFOMUSS*OMEGA_USS
       ZMELNET(JLON,JLEV)=MAX(ZMELNET(JLON,JLEV),&
                        & MIN(ZVM(JLON,JLEV),-1._JPRB)*ZFOMUSS)
!---------------------------------------
       ZZ=ZMELNET(JLON,JLEV)&
         & *REAL(INAC9(JLON,JLEV),JPRB)
       ZFORM(JLON,JLEV)=(ZZ+ZS3(JLON))*ZAUTOADV
       ZS3(JLON)=ZZ
     ENDDO
   ENDDO
   DO JLON=KIDIA,KFDIA
      KNACT(JLON,KLEV)=MAX(INBAS(JLON,KLEV),INAC9(JLON,KLEV))
      ZTACT(JLON,KLEV)= REAL(KNACT(JLON,KLEV),JPRB)
      KNACT(JLON,KTDIA)=0
   ENDDO
   DO JLEV=KLEV-1, MAX(ITOP,KTDIA+1), -1
      DO JLON=KIDIA,KFDIA
       ZDP=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)
       ZIDP=1._JPRB/ZDP
       ZSIGE(JLON)=1._JPRB-ZA10(JLON,JLEV) !>0 bcse ZA10 <1
       ZUSIGE=1._JPRB/ZSIGE(JLON)
! Test: * inasc
       ZINAC9=REAL(INAC9(JLON,JLEV)*INASC(JLON,JLEV), JPRB)
       ZBAS=REAL(INBAS(JLON, JLEV), JPRB)
       IF (LLRITO) THEN ! RISING TOP
          ZOMB(JLON)=ZUM(JLON,JLEV+1)+ZOME(JLON,JLEV+1)
! Here we consider the transient zudom (ss in insca)
          ZOD=ZOMB(JLON)*ZTACT(JLON,JLEV+1)
          ZUP=MAX(0._JPRB,SIGN(1._JPRB,-ZOD-ZEPSDOM1))
          ZALR9=PUDGRO(JLON)*REAL(INAC9(JLON,JLEV+1),JPRB)*(1._JPRB-ZINAC9)
          ZPATH=ZDP*(1._JPRB-ZALR9)
          ZTACT(JLON,JLEV)=MIN(1._JPRB,MAX(0._JPRB, &
          & ZUP*(ZTACT(JLON,JLEV+1)*ZIDT+ZPATH/MIN(ZOD,-ZEPSDOM1)),&
          & ZINAC9, ZBAS))*ZTPHY
          ZINAC=MAX(ZINAC9,SIGN(1._JPRB,-ZOD-ZPATH),&
                  & ZBAS)
          KNACT(JLON,JLEV)=NINT(ZINAC)
! COMPUTE ZALR1 for output
          ZZ=(1._JPRB-ZINAC)*REAL(KNACT(JLON,JLEV+1),JPRB)
          ZALR1(JLON)=(1._JPRB-ZZ)*ZALR1(JLON) + ZZ*&
              & MIN(1._JPRB,ZALR9-ZUP*ZOD*ZIDP)
! RISING TOP INDICATOR
          ZRITO=KNACT(JLON,JLEV)*(1-INAC9(JLON,JLEV-1))*&
              & MAX(0._JPRB,SIGN(1._JPRB,-ZVM(JLON,JLEV-1) -ZEPSDOM3))
       ELSE ! NO RISING TOP
          KNACT(JLON,JLEV)=INASC(JLON,JLEV)
          ZTACT(JLON,JLEV)=ZTPHY*REAL(KNACT(JLON,JLEV),JPRB)
       ENDIF
! rem:  Keep the buoyancy at the half level
       ZO=(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1))*0.5_JPRB
       ZZ=MAX(0._JPRB, SIGN(1._JPRB, -ZO-ZEPSDOM1))
! IF ZZ IS ZERO , TAKE THE UNTOUCHED ENVIRONMENT BUOYANCY
       ZBU=ZBUFAC*PAPRSF(JLON,JLEV)/(ZTVE(JLON,JLEV)*ZTVE(JLON,JLEV))
       ZBUO=ZBU*ZTVN(JLON,JLEV) *(1._JPRB-ZZ) &
       &+ZZ* ZSIGE(JLON)*(ZB0(JLON,JLEV)+ZB1(JLON,JLEV)/(ZO+(1._JPRB-ZZ)))
       ZMIX=( ( (ZPOID(JLON,JLEV)+ZPOID(JLON,JLEV+1))*ZUSIGE &
           & + ZIPOI(JLON,JLEV)+ZIPOI(JLON,JLEV+1))*ZUSIGE&
           & -(ZQLC(JLON,JLEV)+ZQLC(JLON,JLEV+1)) )*0.5_JPRB
! ZQLC stored zdlnrho/(-dp) <0
       ZA=ZTACT(JLON,JLEV)*ZMIX
       ZZ2=1._JPRB-ZRITO
       ZB=1._JPRB-ZINAC9*ZFORM(JLON,JLEV)*PRDELP(JLON,JLEV)*ZZ2
       ZD=ZTACT(JLON,JLEV)*ZZ2*(ZOME(JLON,JLEV)*ZQLC(JLON,JLEV)&
         & +(ZOME(JLON,JLEV+1)-ZOME(JLON,JLEV))/ZDP ) 
       ZB=ZB+ZD
! first part of zc1:
       ZC1=ZMELNET(JLON,JLEV)*(ZINAC9+(1._JPRB-ZINAC9)&
        & *ZTACT(JLON,JLEV)*ZIDT)
       ZC2=-ZTACT(JLON,JLEV)*ZBUO 
       ZC=ZC1+ZC2
       ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ZA-ZEPS7))
! Free Base velocity: to estimate velocity below base (and auto advection)
       ZZ=SQRT(MAX(0._JPRB,ZB*ZB-4._JPRB*ZA*ZC))
       ZEXP=EXP(-ZDP*GCVIDPBAS)
       ZO9=MIN(0._JPRB,ZZ2*(ZB-ZZ)/(2._JPRB*MAX(ZA,ZEPS7)))*ZEXP*ZBAS &
              & + ZMELNET(JLON,JLEV+1)*(1._JPRB-ZBAS)
       ZFD=ZFORM(JLON,JLEV)+ZBAS*(ZO9-ZMELNET(JLON,JLEV+1))*0.5_JPRB
! Final calculation
       ZC1=ZC1+ZRITO*ZUM(JLON,JLEV+1)
       ZC3=-ZINAC9*(1._JPRB-ZRITO)*ZSIGE(JLON)&
          & *PRDELP(JLON,JLEV)&
          & *(ZFD*(ZUM(JLON,JLEV+1)-0.5_JPRB*(ZO9-ZMELNET(JLON,JLEV)))&
          & +ZFORM(JLON,JLEV-1)*0.5_JPRB*&
          &  (ZMELNET(JLON,JLEV)-ZMELNET(JLON,JLEV-1)))
       ZC=ZC1+ZC2+ZC3
       ZZ=ZB*ZB-4._JPRB*ZA*ZC
       ZZ2=MAX(0._JPRB,SIGN(1._JPRB, ZZ))*ZZ2
       ZOD=ZZ2*(ZB-SQRT(ZZ2*ZZ))/(2._JPRB*ZA+(ZZ2-1._JPRB))
! PREVENT omu+ FROM BEING more negative than omu_ss*ZFOMUSS
! by construction zvm is <=0 but avoid forcing zum to zero when zvm=0 !
! => This limitation should apply only when ZVM<-1 Pa/s
       ZUM(JLON,JLEV)=MAX(MIN(0._JPRB,ZOD),&
                         & MIN(ZVM(JLON,JLEV),-1._JPRB)*ZFOMUSS)
      ENDDO
   ENDDO
! NOW UPDATE PUDGRO
   IF (LLRITO) PUDGRO(KIDIA:KFDIA)=ZALR1(KIDIA:KFDIA)
ELSE ! not LUDEVOL
   ZUM=ZVM
   KNACT(KIDIA:KFDIA,:)=INASC(KIDIA:KFDIA,:)
   IF (LLRITO) PUDGRO(KIDIA:KFDIA)=0._JPRB
   ZTACT(KIDIA:KFDIA,:)=REAL(KNACT(KIDIA:KFDIA,:),JPRB)*ZTPHY
ENDIF

!------------------------------------------------------------------------
! VIII - FINAL UPDRAFT MASS FLUX AND VELOCITY PERTURBATION
!------------------------------------------------------------------------
!------------------------------------------------------------
! VIIIb  - COMPUTE THE TIME-STEP-AVERAGED MASS FLUX 
!                       at the interface levels
!     STORE TRANSIENT PROGNOSTIC UD PERTURBATION VELOCITY PROFILE
!------------------------------------------------------------
!* EFFECTIVE MASS FLUX AT THE INTERFACE
!       ZA10 CONTAINS PUDAL+, ZUDAL9 CONTAINS PUDAL-
!       ZFORA: PRODUCTION FLUX -sigma_u (omega_u-omega)*(chi*dt) >=0
!       ZFORM: TRANSPORT FLUX= ZFORA/(1-pudal) >=0
!       ZA15: ABSOLUTE UPDRAFT FLUX EXCLUDING ZTACT
! REM: ZFOMUSS APPLIED ABOVE TO ZUM, MUST ALSO BE APPLIED TO ZUDOM9

!  FLUXES ARE AVERAGES OVER TIME STEP

  ZFORA=0.0_JPRB
  ZFORM=0.0_JPRB
  ZA15=0._JPRB
! Use ZUDOM info for averaging
  ZZ2=1._JPRB-ZDTMEAN

  DO JLON=KIDIA,KFDIA
! LIMIT VALUES OF OMEGA_U AT LARGE MESH FRACTIONS
! TO PREVENT UNREALISTIC TRANSPORT FLUXES - zfsig=inasc at klev
    ZSIGE(JLON)=1._JPRB-ZJPRODFX*ZA10(JLON,KLEV)
    ZUM(JLON,KLEV)=MAX(ZUM(JLON,KLEV),ZOMUN*ZSIGE(JLON))
! Time-step averaged velocities AND MESH FRACTION
      ZINAC9=REAL(INAC9(JLON,KLEV),JPRB)
      ZO9=MAX(ZUDOM(JLON,KLEV),ZVM(JLON,KLEV)*ZFOMUSS)
      ZA=ZUDAL9(JLON,KLEV)*ZINAC9*ZDTMEAN
      ZC1=-(ZA*ZO9 +ZA10(JLON,KLEV)*ZUM(JLON,KLEV)*ZZ2)
      ZC2=ZC1-(ZA+ZA10(JLON,KLEV)*ZZ2)*ZOME(JLON,KLEV)
      ZD=ZTACT(JLON,KLEV)*ZC1
! FLUXES ARE ALREADY ZERO AT SURFACE
      ZC=ZD/ZSIGE(JLON)
      ZS3(JLON)=ZD
      ZS4(JLON)=ZC
      ZS5(JLON)=ZC2
  ENDDO
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
! LIMIT VALUES OF OMEGA_U AT LARGE MESH FRACTIONS
! TO PREVENT UNREALISTIC TRANSPORT FLUXES
! Beware: Mf=sigma_u_scaling*omega_uper
!    but  Mt=Mf/(1-sigma_u_real)
      ZSIGE(JLON)=1._JPRB-ZJPRODFX*ZA10(JLON,JLEV)
      ZUM(JLON,JLEV)=MAX(ZUM(JLON,JLEV), ZOMUN*ZSIGE(JLON))
! Time-step averaged velocities and mesh fraction
! Rem: inac9=1 => ztact=ztphy else ztact <ztphy
      ZINAC9=REAL(INAC9(JLON,JLEV),JPRB)
      ZO9=MAX(ZUDOM(JLON,JLEV),ZVM(JLON,JLEV)*ZFOMUSS)
      ZA=ZUDAL9(JLON,JLEV)*ZINAC9*ZDTMEAN
      ZC1=-(ZA*ZO9 +ZA10(JLON,JLEV)*ZUM(JLON,JLEV)*ZZ2)




      ZC2=-(ZA+ZA10(JLON,JLEV)*ZZ2)*ZOME(JLON,JLEV)
      ZD=ZTACT(JLON,JLEV)*ZC1

      ZC=ZTACT(JLON,JLEV)*ZC1/ZSIGE(JLON)
      ZC3=ZC1+ZC2
! PRODUCTION FLUX (HALF LEVELS)
      ZFORA(JLON,JLEV)= MAX(0._JPRB,(ZD+ZS3(JLON))*0.5_JPRB)
! TRANSPORT MASS FLUX (NO TIME-INTERPOLATION FOR THE DIVISION)
      ZFORM(JLON,JLEV)= MAX(0._JPRB,(ZC+ZS4(JLON))*0.5_JPRB)
! ABSOLUTE MASS FLUX/ZTACT (HALF LEVELS)
      ZA15(JLON,JLEV)=MAX(0._JPRB,(ZC3+ZS5(JLON))*0.5_JPRB)
      ZS3(JLON)=ZD
      ZS4(JLON)=ZC
      ZS5(JLON)=ZC3
    ENDDO
  ENDDO
! NOW UPDATE ZUDOM VALUE (already identical under llomustar) 
  ZUDOM(KIDIA:KFDIA,KTDIA:KLEV)=ZUM(KIDIA:KFDIA,KTDIA:KLEV)
!------------------------------------------------------------
! VIIIc - PROTECTION AGAINST NL INSTABILITY
!  and compute the environment values affected y pseudo-subsidence
!------------------------------------------------------------
!     ADITIONAL VERTICAL LOOP (UPWARDS) TO MODIFY THE PREVIOUSLY
!     COMPUTED MASS FLUX.
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
 
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV+1)+(ZFORM(JLON,JLEV)&
       &-ZFORM(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
       &*MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV+1))))
      ZFORA(JLON,JLEV)=MAX(0.0_JPRB,ZFORA(JLON,JLEV+1)+(ZFORA(JLON,JLEV)&
       &-ZFORA(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
       &*MAX(0.0_JPRB,ZFORA(JLON,JLEV)-ZFORA(JLON,JLEV+1))))
! BEWARE, ZA15*dt is a pressure
      ZA15(JLON,JLEV)=MAX(0.0_JPRB,ZA15(JLON,JLEV+1)+(ZA15(JLON,JLEV)&
       &-ZA15(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
       &*MAX(0.0_JPRB,ZA15(JLON,JLEV)-ZA15(JLON,JLEV+1))*ZTPHY))
    ENDDO
  ENDDO

!*    -----------------------------------------------------------------
!     IX - WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERM (KERSHAW & GREGORY SCHEME), CONSIDERING 
!     THE ACTIVE LAYERS of the TRANSIENT CLOUD 
!     (RISING CLOUD: THE FLUX WILL BE ZERO AT LEVELS KNACT=0, 
!               HENCE WE CAN USE DIRECTLY KNACT HERE)
!*    -----------------------------------------------------------------
! WORK ARRAYS
! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.
! ZA13      : UC0.
! ZA14      : VC0.
! ZUM       : AVERAGE U VALUE FROM CLOUD BASE.
! ZVM       : AVERAGE V VALUE FROM CLOUD BASE.

  ZUM=0.0_JPRB
  ZVM=0.0_JPRB
  ZBET=0.0_JPRB 
  ZA13=0.0_JPRB
  ZA14=0.0_JPRB

  DO JLON=KIDIA,KFDIA

! AT BASE: ZBE=1, ZUM=PU, ZVM=PV, ZA13=PU, ZA14=PV
! Inactive layers and base => ZUM=PU, ZVM=PV

    ZBET(JLON,KLEV)=REAL(INBAS(JLON,KLEV),JPRB)
    ZUM(JLON,KLEV)=PU(JLON,KLEV)
    ZVM(JLON,KLEV)=PV(JLON,KLEV)

! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

    ZA13(JLON,KLEV)=PU(JLON,KLEV)
    ZA14(JLON,KLEV)=PV(JLON,KLEV)
  ENDDO

! THESE ARRAYS COULD BE SET TO FIELDS IN THE FUTURE - CURRENTLY CONSTANT VALUE
  ZUDGKX(KIDIA:KFDIA)=TUDGP
  ZUDGKY(KIDIA:KFDIA)=TUDGP
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA

! ZNBA=1 WHERE ACTIVE AND NOT BASE
! ZBAS=1  AT BASE (AND THEN ACTIVE)
! BOTH ARE 0 IN INACTIVE LAYERS
      ZBAS=REAL(INBAS(JLON,JLEV),JPRB)
      ZNBA=REAL((1-INBAS(JLON,JLEV))*KNACT(JLON,JLEV),JPRB)

!     ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
!     ZBET IS 0 IN INACTIVE LAYERS
!             1 IN THE BASE LAYERS
!             DECREASE UPWARDS IN THE ACTIVE LAYERS
!     ZUM = PU IN INACTIVE LAYERS
!     ZVM = PV IN INACTIVE LAYERS
!     AND BOTH ARE ACCUMULATED IN ACTIVE LAYERS
!     ZRMIX CONTAINS FINAL LAMBDA*DP AT INTERFACE

      ZMIX=ZRMIX(JLON,JLEV)/(1._JPRB+ZRMIX(JLON,JLEV))
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX) +ZBAS 

! ZUM BACK TO PU IF  KNACT=0 OR INBAS=1
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
       &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
       &+ZUDGKX(JLON)*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
       &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
       &+ZUDGKY(JLON)*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)


! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY
! ZA13,ZA14 : WIND AT BASE OF ACTIVE SEGMENT
! SAME VALUE UP TO NEXT BASE
! (IN INACTIVE LAYERS, MULTIPLIED BY ZBET=0)

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
    ENDDO
  ENDDO

! STORE THE FINAL VALUES OF CLOUD MOMENTUM PERTURBATION (PUU,PVU)
  PUU(KIDIA:KFDIA,:)=0
  PVU(KIDIA:KFDIA,:)=0
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      ZZ=1.0_JPRB-ZBET(JLON,JLEV)
      PUU(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)&
                    & -PU(JLON,JLEV)
      PVU(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)&
                    & -PV(JLON,JLEV)
    ENDDO
  ENDDO


!*
!     ------------------------------------------------------------------
!     X - FLUXES COMPUTATION AND MELTING/FREEZING CORRECTION

  ZMELNET=0.0_JPRB
  ZBET=0._JPRB
!    ZPOID    : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
!    ZIPOI    : INVERSE OF ZPOID.
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
      ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
      ZLHCORR(JLON,JLEV)=ZPOID(JLON,JLEV)&
        & *(ZLHS(JLON,JLEV)-ZLHV(JLON,JLEV))/ZLHTBW(JLON,JLEV)
    ENDDO
  ENDDO
 

! ITERATE CORRECTION DUE TO FREEZING/MELTING
! STORE T9 VALUE OF PFRDE IN ZUM BEFORE ITERATION
  ZUM(KIDIA:KFDIA,:)=PFRDE(KIDIA:KFDIA,:)
! STORE TOTAL UPDRAFT CONDENSATE IN ZA13
  ZA13(KIDIA:KFDIA,:)=ZLDN(KIDIA:KFDIA,:)+ZQC(KIDIA:KFDIA,:)
  DO JIT=1,NIMELIT

! CONVECTIVE CONDENSATION FLUXES

! TOP
    DO JLON=KIDIA,KFDIA
      ZFMDQC(JLON)=ZFORA(JLON,ITOP)*MAX(0.0_JPRB,ZDQCA(JLON,ITOP))&
     & *ZGDTI*0.5_JPRB
      ZFMCORR=ZFMDQC(JLON)+ZLHCORR(JLON,ITOP)&
     & *ZMELNET(JLON,ITOP)*REAL(KNACT(JLON,ITOP),JPRB)
      ZCR(JLON)=ZFMCORR*PRDELP(JLON,ITOP)
! CONTRIBUTION TO CONDENSATE BUDGET:
      ZBET(JLON,ITOP)=ZCR(JLON)*ZGDT
! PARTITION OF THE CONDENSATE RATE BETWEEN ICE AND LIQUID
      ZZDQI=ZTEST(JLON,ITOP)*ZCR(JLON)*ZIFRAC(JLON,ITOP)
      ZZDQL=ZTEST(JLON,ITOP)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,ITOP))
      PFCCQL(JLON,ITOP)=ZZDQL*PDELP(JLON,ITOP)
      PFCCQI(JLON,ITOP)=ZZDQI*PDELP(JLON,ITOP) 
    ENDDO
! MID LEVELS
    DO JLEV=ITOP+1,KLEV-1 
      DO JLON=KIDIA,KFDIA
        ZFMDQCL=ZFORA(JLON,JLEV)*MAX(0.0_JPRB,ZDQCA(JLON,JLEV))&
       & *ZGDTI*0.5_JPRB
        ZFMCORR=ZFMDQC(JLON)+ZFMDQCL+ZLHCORR(JLON,JLEV)&
       & *ZMELNET(JLON,JLEV)*REAL(KNACT(JLON,JLEV),JPRB)
        ZCR(JLON)=ZFMCORR*PRDELP(JLON,JLEV)
! CONTRIBUTION TO CONDENSATE BUDGET:
        ZBET(JLON,JLEV)=ZCR(JLON)*ZGDT
! PARTITION OF THE CONDENSATE RATE BETWEEN ICE AND LIQUID 
        ZZDQI=ZTEST(JLON,JLEV)*ZCR(JLON)*ZIFRAC(JLON,JLEV) 
        ZZDQL=ZTEST(JLON,JLEV)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,JLEV))
        ZFMDQC(JLON)=ZFMDQCL
        PFCCQL(JLON,JLEV)=PFCCQL(JLON,JLEV-1) + ZZDQL*PDELP(JLON,JLEV)  
        PFCCQI(JLON,JLEV)=PFCCQI(JLON,JLEV-1) + ZZDQI*PDELP(JLON,JLEV)
      ENDDO
    ENDDO 
!BOTTOM
    DO JLON=KIDIA,KFDIA
      ZFMCORR=ZFMDQC(JLON)+ZLHCORR(JLON,KLEV)&
     & *ZMELNET(JLON,KLEV)*REAL(KNACT(JLON,KLEV),JPRB)
      ZCR(JLON)=ZFMCORR*PRDELP(JLON,KLEV)
! CONTRIBUTION TO CONDENSATE BUDGET:
      ZBET(JLON,KLEV)=ZCR(JLON)*ZGDT
! PARTITION OF THE CONDENSATE RATE BETWEEN ICE AND LIQUID   
      ZZDQI=ZTEST(JLON,KLEV)*ZCR(JLON)*ZIFRAC(JLON,KLEV) 
      ZZDQL=ZTEST(JLON,KLEV)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,KLEV))
      PFCCQL(JLON,KLEV)=PFCCQL(JLON,KLEV-1) + ZZDQL*PDELP(JLON,KLEV)  
      PFCCQI(JLON,KLEV)=PFCCQI(JLON,KLEV-1) + ZZDQI*PDELP(JLON,KLEV)
    ENDDO

! DETRAINMENT AREA
! ZA15 is the (positive) absolute mass flux/ztact
  IF (LLDEQC) THEN
! Contribution of divergence of mass flux to updraught condensate budget.
    ZFMDQC(:)=0._JPRB
    ZCR(:)=0._JPRB
!cdir unroll=8
    DO JLEV=KLEV, MAX(ITOP,KTDIA+1), -1 ! ZA15 has no 0-level
      DO JLON=KIDIA,KFDIA
        ZAUX=ZTACT(JLON,JLEV)/&
         & (PDELP(JLON,JLEV)+ZTACT(JLON,JLEV)*ZA15(JLON,JLEV-1))
        ZFMDQCL=ZA15(JLON,JLEV-1) *(ZA13(JLON,JLEV-1)+ZA13(JLON,JLEV)&
       & )*0.5_JPRB
        ZCR(JLON)=ZAUX*(ZFMDQC(JLON)-ZFMDQCL+ZCR(JLON)*ZA15(JLON,JLEV))
        ZFMDQC(JLON)=ZFMDQCL
        ZBET(JLON,JLEV)=ZBET(JLON,JLEV)+ZCR(JLON)
      ENDDO
    ENDDO
!cdir unroll=8
! zent(jlev) is zentur(jlev)*(paphi(jlev-1)-paphi(jlev))
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        ZINAC=REAL(KNACT(JLON,JLEV),JPRB)
        ZINAC9=REAL(INAC9(JLON,JLEV),JPRB)
        ZDM=(ZFORM(JLON,JLEV)+ZFORM(JLON,JLEV-1))*0.5_JPRB*ZINAC
        ZENTR=ZENT(JLON,JLEV)*ZDM*PRDELP(JLON,JLEV)
! Use tendency wrt the vertical average (accvud): no rising top
! ZINAC9 allows to account for newly reached levels:
        ZDAL=ZINAC*(ZSIGB(JLON)-ZSIG9(JLON)*ZINAC9)*ZFSIG(JLON,JLEV)
        ZDEQC=MAX(0.0_JPRB, ZBET(JLON,JLEV)-(ZENTR+ZDAL)*ZLDN(JLON,JLEV))
! security
        ZZ=ZINAC*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZA13(JLON,JLEV)-ZEPSL))
        PDETFI(JLON,JLEV)=ZZ* ZDEQC/(ZA13(JLON,JLEV)+(1.0_JPRB-ZZ))&
                       & *KNND(JLON)
!  LIMIT 0< sigma_D< 1-ZA10
        ZZ=ZUM(JLON,JLEV)*ZEXPND+ PDETFI(JLON,JLEV)
        PFRDE(JLON,JLEV)=MIN(1.0_JPRB-ZA10(JLON,JLEV),ZZ)
        PDETFI(JLON,JLEV)=MAX(0.0_JPRB,PDETFI(JLON,JLEV)&
                              & -(ZZ-PFRDE(JLON,JLEV)))
      ENDDO
    ENDDO
  ELSE  ! Detrainement BY MASS BUDGET
! COMPUTE THE INCREMENT OF SIGMA_D IN PDETFI, TOTAL VALUE IN PFRDE
    ZS10=0._JPRB
    DO JLEV=MAX(ITOP,KTDIA+1),KLEV
       DO JLON=KIDIA,KFDIA
        ZINAC=REAL(KNACT(JLON,JLEV),JPRB)
        ZINAC9=REAL(INAC9(JLON,JLEV),JPRB)
        ZDM=(ZA15(JLON,JLEV)-ZA15(JLON,JLEV-1))*0.5_JPRB*ZTACT(JLON,JLEV)&
           & *PRDELP(JLON,JLEV)
        ZDAL=ZINAC*(ZSIGB(JLON)-ZSIG9(JLON)*ZINAC9)*ZFSIG(JLON,JLEV)
        PDETFI(JLON,JLEV)=MAX(0._JPRB,ZDM-ZDAL)
!  LIMIT 0< sigma_D< 1-ZA10
        ZZ=ZUM(JLON,JLEV)*ZEXPND+ PDETFI(JLON,JLEV)
        PFRDE(JLON,JLEV)=MIN(1.0_JPRB-ZA10(JLON,JLEV),ZZ)
        PDETFI(JLON,JLEV)=MAX(0.0_JPRB,PDETFI(JLON,JLEV)&
                              & -(ZZ-PFRDE(JLON,JLEV)))
       ENDDO
    ENDDO
  ENDIF ! detrainement computation

! Test APLMINI

! Compute convective condensates of the time step:
! ZABSUD=1 if we must consider that resolved scheme contribution 
!          IF POSITIVE is also convective

!-  ZABSUD=0._JPRB
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
!-      IF (NMELTYP==1) THEN
         ZABSUD=2._JPRB*SQRT(ZA10(JLON,JLEV)*(1._JPRB-ZA10(JLON,JLEV)))
!-      ENDIF
      ZQLC(JLON,JLEV)=ZIPOI(JLON,JLEV)* &
          & (PFCCQL(JLON,JLEV)-PFCCQL(JLON,JLEV-1)&
          &+ZABSUD*MAX(0._JPRB,PFCSQL(JLON,JLEV)-PFCSQL(JLON,JLEV-1)))
      ZQIC(JLON,JLEV)=ZIPOI(JLON,JLEV)* &
          & (PFCCQI(JLON,JLEV)-PFCCQI(JLON,JLEV-1)&
          &+ZABSUD*MAX(0._JPRB,PFCSQN(JLON,JLEV)-PFCSQN(JLON,JLEV-1)))
      IF(LNEBINS) THEN
        ZNEBC(JLON,JLEV)=MIN(ZEP5C,PDETFI(JLON,JLEV)+ZA10(JLON,JLEV))
      ELSE
        ZNEBC(JLON,JLEV)=MIN(ZEP5C,PFRDE(JLON,JLEV)+ZA10(JLON,JLEV))
      ENDIF
    ENDDO
  ENDDO

  IF(JIT < NIMELIT) THEN
! Call APLMINI
    CALL APLMINI( YDML_PHY_MF, KIDIA,KFDIA,KLON,KTDIA,KLEV,&
     & PAPRS, PAPRSF, PCP, ZQIC, ZQLC, PR, PT,&
     & ZIPOI, ZLHS, ZLHV, ZNEBC, ZPOID,PTU,&
     & PDECRD, ZMELNET,ZMELGET)

!-   IF (NMELTYP==0) THEN
!-    ZSUM1(:)=0.0_JPRB
!-    ZSUM2(:)=0.0_JPRB
!-    DO JLEV=KTDIA,KLEV
!-      DO JLON=KIDIA,KFDIA
!-        ZINAC=REAL(KNACT(JLON,JLEV),JPRB)
!-        ZSUM1(JLON)=ZSUM1(JLON)+ZMELNET(JLON,JLEV)*(ZLHCORR(JLON,JLEV)&
!-         &*ZINAC)
!-        ZSUM2(JLON)=ZSUM2(JLON)+(ZLHCORR(JLON,JLEV)*ZINAC)
!-      ENDDO
!-    ENDDO
!-
!-    DO JLON=KIDIA,KFDIA
!-      ZSUM1(JLON)=ZSUM1(JLON)/MAX(ZSUM2(JLON),ZEPS5)
!-    ENDDO
!-
!-    DO JLEV=KTDIA,KLEV
!-      DO JLON=KIDIA,KFDIA
!-        ZMELNET(JLON,JLEV)=ZMELNET(JLON,JLEV)-ZSUM1(JLON)
!-      ENDDO
!-    ENDDO
!-   ENDIF ! NMELTYP==0

  ENDIF

ENDDO ! END OF ITERATION NIMELIT

!*   ---------------------------
!    CONVECTIVE TRANSPORT FLUXES
!*   ---------------------------
! Implicit calculation

! THE ICE FRACTION CAN DIFFER BETWEEN UD AND MEAN GRID BOX
! => RE_COMPUTE THE PERTURBATIONS
! za13=lu, zldn=lu-l zsdn=su-s pqu=qu-q, (opposite signs from accvud)
ZQIC(KIDIA:KFDIA,:)=ZIFRAC(KIDIA:KFDIA,:)*ZA13(KIDIA:KFDIA,:)&
                 &  -PQI(KIDIA:KFDIA,:)
ZQLC(KIDIA:KFDIA,:)=(1.0_JPRB-ZIFRAC(KIDIA:KFDIA,:))*ZA13(KIDIA:KFDIA,:)& 
                 &-PQL(KIDIA:KFDIA,:)

! Considering FULL flux affects resolved tendency:
! Above itop: all fluxes are zero
! Level klev: ZFORM=0 there, hence all the fluxes are zero, too.
! Elsewhere we need ZFORM >=0 - AVOID the rounding errors
! Require at least that pdifcq<0
DO JLEV=ITOP,KLEV-1
   DO JLON=KIDIA,KFDIA
      ZAUX=ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV)+ZFORM(JLON,JLEV))
      ZDPSGDT=ZPOID(JLON,JLEV)*0.5_JPRB
      PDIFCQ(JLON,JLEV)=ZAUX*( PDIFCQ(JLON,JLEV-1)- &
         & ZDPSGDT*(PQU(JLON,JLEV)+PQU(JLON,JLEV+1)) )
      PDIFCQL(JLON,JLEV)=ZAUX*( PDIFCQL(JLON,JLEV-1)- &
         & ZDPSGDT*(ZQLC(JLON,JLEV)+ZQLC(JLON,JLEV+1)) )
      PDIFCQI(JLON,JLEV)=ZAUX*( PDIFCQI(JLON,JLEV-1)- &
         & ZDPSGDT*(ZQIC(JLON,JLEV)+ZQIC(JLON,JLEV+1)) )
      PDIFCS(JLON,JLEV)=ZAUX*( PDIFCS(JLON,JLEV-1)- &
         & ZDPSGDT*(ZSDN(JLON,JLEV)+ZSDN(JLON,JLEV+1)) )
      PSTRCU(JLON,JLEV)=ZAUX*( PSTRCU(JLON,JLEV-1)- &
         & ZDPSGDT*(PUU(JLON,JLEV)+PUU(JLON,JLEV+1)) )
      PSTRCV(JLON,JLEV)=ZAUX*( PSTRCV(JLON,JLEV-1)- &
         & ZDPSGDT*(PVU(JLON,JLEV)+PVU(JLON,JLEV+1)) )
   ENDDO
ENDDO

IF (LLBLTR) THEN
! SET GRADUAL RETURN OF THE FLUXES TO ZERO AT USL TOP
! (FOR VAPOUR AND HEAT ONLY)
! JLOBA : LOWEST ENTRAINING LEVEL ABOVE BASE
! ZSUM  : PRESSURE AT USL TOP
  JH=MINVAL(JLOBA(KIDIA:KFDIA))
  JUSLMAX=MAXVAL(JUSLT(KIDIA:KFDIA))
  DO JLON=KIDIA,KFDIA
     ZSUM(JLON)=PAPRSF(JLON,JUSLT(JLON))-ZDPMIX2
  ENDDO
  DO JLEV=JH,JUSLMAX
!DEC$ IVDEP
     DO JLON=KIDIA,KFDIA
! level below 1st entraining one, the latter being above the base)
        ILEV=JLOBA(JLON)+1 
! zbas is 1 from ILEV downwards
        ZBAS=MAX(0._JPRB,SIGN(1._JPRB,REAL(JLEV-ILEV,JPRB)+0.5_JPRB))
        ZNBA=1._JPRB-ZBAS
! ZC is 1 above usl top, then decreases to zero over GTRGDPMIX
        ZDP=PAPRSF(JLON,JLEV)-ZSUM(JLON)
        ZC=MAX(0._JPRB, 1._JPRB-MAX(0._JPRB,ZDP/GTRGDPMIX))
        PDIFCQ(JLON,JLEV)=ZNBA*PDIFCQ(JLON,JLEV)+ZBAS*PDIFCQ(JLON,ILEV)*ZC
        PDIFCS(JLON,JLEV)=ZNBA*PDIFCS(JLON,JLEV)+ZBAS*PDIFCS(JLON,ILEV)*ZC
     ENDDO
  ENDDO
ENDIF !LLBLTR

!*
!     ------------------------------------------------------------------
!     XI - FINAL ADAPTATIONS, RESET WHERE (KNND|&JNNS)=0

IF (.NOT.LUSL) THEN ! lcl treatment for untriggered ascent
!     setting all results to zero in case of negative precipitations at
!     the surface or of "knnd" already zero.
!     if sco > 0,          setting to zero if precip. < sco.
!     if -1-eps < sco < 0, no setting to zero.
!     if sco < -1-eps,     setting to zero if precip. < -sco*min(qsat-qn).

  LLLCL=SCO < -1.0_JPRB-ZEPS6
  DO JLON=KIDIA,KFDIA
    IF(LLLCL) THEN
      ZSCO=-SCO*ZLCL(JLON)
    ELSE
      ZSCO=SCO
    ENDIF
    ZZVAL=PFCCQL(JLON,KLEV)+PFCCQI(JLON,KLEV)-ZSCO
    KNND(JLON)=KNND(JLON)*MAX(0,NINT(-SIGN(1.0_JPRB,0.0_JPRB-ZZVAL)))
  ENDDO
ENDIF ! not lusl
! OPEN-LOOP TEST
IF (LLBO) THEN
   KNND(KIDIA:KFDIA)=0
   PFRDE(KIDIA:KFDIA,:)=0.0_JPRB
ENDIF

!------------------------------------------------------------------
!      - RESET ALL FLUXES TO ZERO WHERE KNND=0=JNNS 
!        AND ALSO RESET ZUDOM AND ZA10
! Detrained condensates will NOT be reset to zero if knnd=0
!------------------------------------------------------------------
DO JLON=KIDIA,KFDIA
 JNNS(JLON)=MAX(JNNS(JLON),KNND(JLON))
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
     ZA10(JLON,JLEV)=JNNS(JLON)*ZA10(JLON,JLEV)
     ZUDOM(JLON,JLEV)=JNNS(JLON)*ZUDOM(JLON,JLEV)
     ZSIGB(JLON)=JNNS(JLON)*ZSIGB(JLON)
  ENDDO
ENDDO

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      PDIFCQ(JLON,JLEV)=JNNS(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCQI(JLON,JLEV)=JNNS(JLON)*PDIFCQI(JLON,JLEV)
      PDIFCQL(JLON,JLEV)=JNNS(JLON)*PDIFCQL(JLON,JLEV)
      PDIFCS(JLON,JLEV)=JNNS(JLON)*PDIFCS(JLON,JLEV)
      PFCCQL(JLON,JLEV)=JNNS(JLON)*PFCCQL(JLON,JLEV)
      PFCCQI(JLON,JLEV)=JNNS(JLON)*PFCCQI(JLON,JLEV)
      PSTRCU(JLON,JLEV)=JNNS(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=JNNS(JLON)*PSTRCV(JLON,JLEV)
    ENDDO
  ENDDO
ELSE ! PSEUDO-RETURN
! In case of pseudo-return with NO convection, reset the prognostic fields:
! Alike the case KNND=0 anyway
  ZUDOM=0.0_JPRB
  ZA10=0.0_JPRB
  ZSIGB=0.0_JPRB 
  PFRDE(KIDIA:KFDIA,KTDIA:KLEV)=0.0_JPRB
ENDIF ! ITOP < KLEV+1
PUDAL(KIDIA:KFDIA,KTDIA:KLEV)=ZA10(KIDIA:KFDIA,KTDIA:KLEV)
PUDOM(KIDIA:KFDIA,KTDIA:KLEV)=ZUDOM(KIDIA:KFDIA,KTDIA:KLEV)
IF (LCUCONV_CA) THEN
! OUTPUT CA-FIELDS
! USE LOCAL ARRAY IN 'WHERE' STATEMENTS TO PROVIDE  FULL CAPACITY KLON
    ZSUM=0._JPRB
    IF (GCATHRCAP>0._JPRB) WHERE (PCAPE>GCATHRCAP) ZSUM=1._JPRB
    IF (GCATHRCND>0._JPRB) THEN
       ZS4=0._JPRB
       ZDELP=0._JPRB
       ZZ=1._JPRB/GCAOMCND
       DO JLEV=NJKT4, NJKT5,-1
        DO JLON=KIDIA,KFDIA
           ZA=(PFCSQL(JLON,JLEV)-PFCSQL(JLON,JLEV-1))+&
             &(PFCSQN(JLON,JLEV)-PFCSQN(JLON,JLEV-1))
           ZB=(PFCCQL(JLON,JLEV)-PFCCQL(JLON,JLEV-1))+&
             &(PFCCQI(JLON,JLEV)-PFCCQI(JLON,JLEV-1))
           ZS4(JLON)=ZS4(JLON)+ZA*MAX(0._JPRB,-ZOME(JLON,JLEV))*ZZ &
             & + ZB
          ZDELP(JLON)=ZDELP(JLON)+PDELP(JLON,JLEV)
        ENDDO
       ENDDO
       ZS4(KIDIA:KFDIA)=ZS4(KIDIA:KFDIA)/MAX(1._JPRB,ZDELP(KIDIA:KFDIA))
       WHERE (ZS4>GCATHRCND) ZSUM=1._JPRB
    ENDIF
    IF (GCATHRMOC>0._JPRB) THEN
       ZS4=0._JPRB
       ZDELP=0._JPRB
       DO JLEV=NJKT4, NJKT5,-1
        DO JLON=KIDIA,KFDIA
           ZS4(JLON)=ZS4(JLON)+PCVGQ(JLON,JLEV)*PDELP(JLON,JLEV)
          ZDELP(JLON)=ZDELP(JLON)+PDELP(JLON,JLEV)
        ENDDO
       ENDDO
       ZS4(KIDIA:KFDIA)=ZS4(KIDIA:KFDIA)/MAX(1._JPRB,ZDELP(KIDIA:KFDIA))
       WHERE (ZS4>GCATHRMOC) ZSUM=1._JPRB
    ENDIF
    PCUCONVCA(KIDIA:KFDIA)=ZSUM(KIDIA:KFDIA)
    PNLCONVCA(KIDIA:KFDIA)=ZLIVES*ZSUM(KIDIA:KFDIA)
ENDIF

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCSU',1,ZHOOK_HANDLE)
END SUBROUTINE ACCSU
