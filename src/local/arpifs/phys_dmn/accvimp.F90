!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCVIMP ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PCVGQ,&
 & PDELP,PLNPR,PQ,PQSAT,PQW,PR,PRDELP,PT,PTW,PU,PV,&
 & PVORT0,PVERVEL,&
 ! - INPUT  1D .
 & PGM,PTS,PTAUX,PRCORI,&
 ! - OUTPUT 2D .
 & PDIFCQ,PDIFCS,PFCCQL,PFCCQN,PFPLCL,PFPLCN,PFPFPCL,PFPFPCN,&
 & PFPEVPCL,PFPEVPCN,PSTRCU,PSTRCV,PFPCOR,&
 & KNLAB,&
 ! - OUTPUT 1D .
 & PCAPE,KNND,&
 ! - INPUT/OUTPUT 2D .
 & PDIFTQ,PDIFTS,PGEOSLC,PGEMU)

!**** *ACCVIMP * - SCHEMA DE CONVECTION PROFONDE A FLUX DE MASSE.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX DE PRECIPITATION CONVECTIVE ET DES FLUX DE
!       TRANSPORT "SUB-GRID SCALE" ASSOCIES, PAR UN SCHEMA DE CONVECTION
!       PROFONDE A FLUX DE MASSE .
!     - MASS-FLUX DEEP CONVECTION SCHEME, COMPUTING CONVECTIVE
!       PRECIPITATIONS AND "SUB-GRID SCALE" ASSOCIATED FLUXES .

!     - CALCUL DES FLUX D'ENTHALPIE LIES AUX PRECIPITATIONS CONVECTIVES
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF ENTHALPY FLUXES LINKED TO CONVECT. PRECIPITATIONS
!              (WATER AND SNOW) .

!**   Interface.
!     ----------
!        *CALL* *ACCVIMP*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PCVGQ      : CONVERGENCE D'HUMIDITE (CONDITION DE "KUO").
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PVORT0     : RELATIVE VORTICITY.
! PVERVEL    : (OMEGA/P) VITESSE VERTICALE DIVISEE PAR LA PRESSION.

! - 1D (PROGNOSTIQUE) .

! PGM        : FACTEUR D'ECHELLE.
! PTS        : TEMPERATURE DE SURFACE.

! - 1D (DIAGNOSTIQUE) .

! PTAUX      : TEMPS CARACTERISTIQUE DE LA CAPE.
! PRCORI     : PARAMETRE DE CORIOLIS.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PDIFCQ     : FLUX CONVECTIF D'HUMIDITE SPECIFIQUE (HORS PLUIE/NEIGE).
! PDIFCS     : FLUX CONVECTIF D'ENTHALPIE (HORS PLUIE/NEIGE).
! PFCCQL     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES LIQUIDES.
! PFCCQN     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES NEIGEUSES.
! PFPFPCL    : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFPFPCN    : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFPEVPCL   : FLUX DE PRECIPITATIONS DU A L'EVAPORATION CONVECTIVE.
! PFPEVPCN   : FLUX DE PRECIPITATIONS DU A LA SUBLIMATION CONVECTIVE.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PSTRCU     : FLUX CONVECTIF DE QUANTITE DE MOUVEMENT "U".
! PSTRCV     : FLUX CONVECTIF DE QUANTITE DE MOUVEMENT "V".
! PFPCOR     : PRECIPITATIONS PLUS LISSE POUR LE CALCUL DE NEBUL.

! - 2D (1:KLEV) .

! KNLAB      : INDICE D'ACTIVITE CONVECTIVE (NUAGE SI EGAL A UN).

! - 1D (DIAGNOSTIQUE) .

! PCAPE      : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).
! KNND       : ZERO SI LA SOLUTION EST NON-PHYSIQUE, UN SINON.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).

! - 2D (1:KLEV) .

! PGEOSLC    : EQUIVALENT GEOPOTENTIEL POUR CALCUL DE CONVECTION EN PENTE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     *ACCVIMPD*

!     Methode.
!     --------

!     Auteur.
!     -------
!      89-12, J.F. Geleyn.

!     Modifications.
!     --------------
!      2001-02, Hysteresis of precipitations melting/freezing (uses GCVMLT). - J.M. Piriou.
!      2001-11, Security for forcing KNND=0 with ZALF=0. - E. Bazile.
!      2002-04, Security ZEPS1 increased. - E. Bazile / J.M. Piriou.
!      2002-06  LCVEVAP becomes RCVEVAP - Y. Bouteloup
!               RCVEVAP=0 <=> LCVEVAP=.F and RCVEVAP=1 <==> LCVEVAP=.T.
!      2002-11, Corrections - J.-F. Geleyn/D. Banciu
!               - to assure the stability of the the implicit algorithm for
!                 compesanting subsidence computation
!               - remove the condensed water from the convective turbulent flux
!      2002-12, Introduction of a multiplication factor (pressure ** GCVPSIE)
!               of pqsat in the normalization integral for the smoothing of Jq
!               term (under LCVLIS=.T.) - J.-F. Geleyn/D. Banciu
!      2003-02, Output of a smooth flux of precip. for the computation of the
!               cloud cover - E. BAZILE.
!      2003-02, Stop convection in the case of absolut dry instability (LNOIAS)
!               - E. BAZILE.
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      2005-05, Cleaning of delta_m=1 related calculations, suppression
!               of ACCLFP at the same occasion.
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      2010-11, Modification and PCVGQ/PDIFTQ in case of resolved
!               condensation (GCVQCCR>0) - F. Bouyssel
!      2011-01, Intro. of GCVOMGE, GCVOMGQ - F. Bouyssel
!      2011-03, Intro. of GCVOMGS, GCVOMGP - F. Bouyssel
!      2011-06, Security forcing KNND=0 with unrealistic tendency - F. Bouyssel
!      2011-06: M. Jerczynski - some cleaning to meet norms
!      2011-12, Adding qsat condition with GCVOMGQ - F. Bouyssel
!      2012-12, Call to ACCVIMPGPS - F. Bouyssel
!      2019-09, J.M. Piriou: Kessler evaporation (LKESC) test to suppress rain under deep dry PBLs.
!      2018-04, Add PGEMU for ACCVIMPGPS - P. Marquet/L. Descamps
!      2019-10, Phasing call to accvimpgps in cy47t0 - Y. Bouteloup
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
 & RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 & RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 & RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 & RGAMD    ,RLVZER   ,RLSZER
USE YOMLSFORC, ONLY : LMUSCLFA,NMUSCLFA

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVORT0(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTAUX(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORI(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCQ(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPCL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPCN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPCN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCU(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPCOR(KLON,0:KLEV)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNLAB(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCAPE(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNND(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOSLC(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(KLON)

!-----------------------------------------------------------------------
! 2D LOCALS

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV),ZALFPB(KLON,KLEV)&
 & ,ZBET(KLON,KLEV),ZDSE(KLON,KLEV),ZATSLC(KLON,0:KLEV)&
 & ,ZFORM(KLON,0:KLEV),ZFQSC(KLON,KLEV)&
 & ,ZFSSC(KLON,KLEV),ZFUSC(KLON,KLEV),ZFVSC(KLON,KLEV)&
 & ,ZIPOI(KLON,KLEV),ZLHE(KLON,KLEV),ZLHL(KLON,0:KLEV)&
 & ,ZLHN(KLON,0:KLEV),ZPOID(KLON,KLEV),ZQDN(KLON,KLEV)&
 & ,ZQVDN(KLON,KLEV)&
 & ,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV),ZSNP(KLON,0:KLEV)&
 & ,ZTRAN(KLON,KLEV),ZUM(KLON,KLEV),ZVM(KLON,KLEV)&
 & ,ZZLHP(KLON,0:KLEV),ZHSEAD(KLON,KLEV),ZLNN(KLON,KLEV)&
 & ,ZDIFTS(KLON,0:KLEV),ZDIFTQ(KLON,0:KLEV),ZTENTRX(KLON,KLEV)

! 1D LOCALS

REAL(KIND=JPRB) :: ZALF(KLON),ZALFP(KLON),ZCP(KLON)&
 & ,ZENTRD(KLON),ZFMDQ(KLON),ZFMDS(KLON)&
 & ,ZFMDU(KLON),ZFMDV(KLON),ZHCMH(KLON),ZHCMHEB(KLON)&
 & ,ZICVG(KLON)&
 & ,ZLB(KLON),ZLH(KLON),ZLHSB(KLON),ZLN(KLON),ZLNL(KLON)&
 & ,ZQB(KLON),ZQN(KLON),ZQNL(KLON)&
 & ,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
 & ,ZS1(KLON),ZS2(KLON),ZS3(KLON),ZS4(KLON),ZS5(KLON),ZS5S(KLON)&
 & ,ZS6(KLON),ZS7(KLON),ZS8(KLON),ZS8S(KLON),ZS9(KLON),ZS10(KLON)&
 & ,ZS11(KLON),ZS12(KLON),ZS13(KLON),ZS14(KLON)&
 & ,ZS15(KLON),ZS16(KLON),ZS17(KLON),ZS18(KLON)&
 & ,ZTB(KLON),ZTN(KLON),ZTNL(KLON),ZLCL(KLON),ZFPCORS(KLON)&
 & ,ZFPCORK(KLON),ZCVTOP(KLON),ZNND(KLON)

INTEGER(KIND=JPIM) :: IOLD, ISUM, ITOP, ITRAN, JIT, JLEV, JLON

LOGICAL :: LLLCL, LLNEWM

REAL(KIND=JPRB) :: ZMAXRR(KLON),ZRACR(KLON)
REAL(KIND=JPRB) :: ZBIN,ZEPSRR
INTEGER(KIND=JPIM) :: ILEVX(KLON)

REAL(KIND=JPRB) :: ZALFS, ZARLII, ZAUX, ZCOEF, ZCPSMD, ZCPVMD,&
 & ZCPVMS, ZCPVMW, ZCPWMD, ZCVPSI, ZDCP, ZDELQ, &
 & ZDELT, ZDELTA, ZDPLAB, ZDQW, ZECORQ, ZENEN, &
 & ZENTR, ZENTRMN, ZENTRMX, ZEPS1, ZEPS2, ZEPS3, &
 & ZEPS4, ZEPS5, ZEPS6, ZESP, ZEW, ZEXP, ZFCORQ, &
 & ZFMDQL, ZFMDSL, ZFMDUL, ZFMDVL, ZFTOTQ, ZFTOTS, &
 & ZGDT, ZGDTI, ZHCMHE, ZHCMHL, ZHSE, ZICVGL, &
 & ZIND, ZKUO1, ZKUO2, ZLIQ, ZMIX, ZQW, &
 & ZRVMD, ZTMEAN, ZZ, ZZ2, ZZALFP, ZZLAB, ZZVAL, ZENTRN, &
 & ZFFAND, ZFRAA, ZSCO, ZHS_REE, ZSMOOTH, &
 & ZINDX, ZCORIO, ZVORT0, ZFETA, ZMIXCIN
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "accvimpgps.intfb.h"
#include "accvimpd.intfb.h"
#include "wrscmr.intfb.h"

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCVIMP',0,ZHOOK_HANDLE)
ASSOCIATE(RCIN=>YDML_PHY_MF%YRPHY0%RCIN, GCVPSI=>YDML_PHY_MF%YRPHY0%GCVPSI, GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, &
 & SCO=>YDML_PHY_MF%YRPHY0%SCO, GCVALFA=>YDML_PHY_MF%YRPHY0%GCVALFA, GCVHMIN=>YDML_PHY_MF%YRPHY0%GCVHMIN, &
 & ETACUT=>YDML_PHY_MF%YRPHY0%ETACUT, TENTRX=>YDML_PHY_MF%YRPHY0%TENTRX, RCVEVAP=>YDML_PHY_MF%YRPHY0%RCVEVAP, &
 & TUDGP=>YDML_PHY_MF%YRPHY0%TUDGP, GCVBETA=>YDML_PHY_MF%YRPHY0%GCVBETA, GCVNU=>YDML_PHY_MF%YRPHY0%GCVNU, &
 & ECMNP=>YDML_PHY_MF%YRPHY0%ECMNP, GCVMLT=>YDML_PHY_MF%YRPHY0%GCVMLT, USDMLT=>YDML_PHY_MF%YRPHY0%USDMLT, &
 & TENTR=>YDML_PHY_MF%YRPHY0%TENTR, GCVPSIE=>YDML_PHY_MF%YRPHY0%GCVPSIE, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, LCAPE=>YDML_PHY_MF%YRPHY%LCAPE, LCVDD=>YDML_PHY_MF%YRPHY%LCVDD, &
 & LCVLIS=>YDML_PHY_MF%YRPHY%LCVLIS, LCVCAS=>YDML_PHY_MF%YRPHY%LCVCAS, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, &
 & LNOIAS=>YDML_PHY_MF%YRPHY%LNOIAS, NBITER=>YDML_PHY_MF%YRPHY%NBITER, LSLC=>YDML_PHY_MF%YRPHY%LSLC, EVAP=>YDML_PHY_MF%YRPHY0%EVAP, &
 & LKESC=>YDML_PHY_MF%YRPHY0%LKESC,GCVGAMMA=>YDML_PHY_MF%YRPHY0%GCVGAMMA)
!-----------------------------------------------------------------------

!*
!     -------------------
!     0 - INITIALISATIONS

!     MISE A ZERO DE TOUS LES FLUX (POUR LE CAS SANS CONVECTION)
!     SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)

PFPLCL=0.0_JPRB
PFPLCN=0.0_JPRB
PDIFCQ=0.0_JPRB
PDIFCS=0.0_JPRB
PSTRCU=0.0_JPRB
PSTRCV=0.0_JPRB
ZFORM=0.0_JPRB

!     INTEGRALES INITIALISEES.
!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D .

! ZS1       : INTEGRALE PONDEREE DE L FOIS LA CONVERGENCE D'HUMIDITE.
!            : WEIGHTED INTEGRAL OF L TIME THE HUMIDITY CONVERGENCE.
! ZS2       : INTEGRALE PONDEREE DE L'EXCES NUAGEUX D'ENTHALPIE HUMIDE.
!            : WEIGHTED INTEGRAL OF THE CLOUD EXCESS IN MOIST ENTHALPY.
! ZS3       : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "U".
!            : WEIGHTED INTEGRAL OF THE "U" WIND TENDENCY.
! ZS4       : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "V".
!            : WEIGHTED INTEGRAL OF THE "V" WIND TENDENCY.
! ZS5       : INTEGRALE PONDEREE DE "1" (ENTR. PUIS DENOMINATEUR).
!            : WEIGHTED INTEGRAL OF "1" (ENTR. THEN DENOMINATOR).
! ZS6       : INTEGRALE PONDEREE DE L FOIS LA TENDANCE DE "Q".
!            : WEIGHTED INTEGRAL OF L TIME THE "Q" TENDENCY.
! ZS7       : INTEGRALE PONDEREE DE LA TENDANCE DE "S=CP*T+PHI".
!            : WEIGHTED INTEGRAL OF THE "S=CP*T+PHI" TENDENCY.
! ZS8       : INTEGRALE PONDEREE DE L FOIS LA TENDANCE "PBL" DE "Q".
!            : WEIGHTED INTEGRAL OF L TIME THE "PBL" "Q" TENDENCY.
! ZS9       : INTEGRALE PONDEREE DE LA TENDANCE "PBL" DE "S=CP*T+PHI".
!            : WEIGHTED INTEGRAL OF THE "PBL" "S=CP*T+PHI" TENDENCY.
! ZS10      : INTEGRALE PONDEREE DU VENT "U" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "U" WIND.
! ZS11      : INTEGRALE PONDEREE DU VENT "V" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "V" WIND.
! ZS12      : INTEGRALE PONDEREE DE BETA*K
!            : WEIGHTED INTEGRAL OF BETA*K
! ZS13      : INTEGRALE PONDEREE DE (1-BETA)*K*U_MOYENNE
!            : WEIGHTED INTEGRAL OF (1-BETA)*K*U_AVERAGE
! ZS14      : INTEGRALE PONDEREE DE (1-BETA)*K*V_MOYENNE
!            : WEIGHTED INTEGRAL OF (1-BETA)*K*U_AVERAGE
! ZS15      : CAPE/Ra.
!            : CONDITIONAL AVAILABLE POTENTIAL ENERGY/Ra
! ZS16      : TRANSPORT VERTICAL DE "S" DANS LA PARTIE CLAIRE
!            CREE PAR LE NUAGE (LCAPE)
!             : VERTICAL TRANSPORT OF "S" IN THE ENVIRONMENT CREATED
!            BY THE CLOUD (LCAPE)
! ZS17      : INTEGRALE DE L'EXCES NUAGEUX D'ENTHALPIE HUMIDE, ESTIME
!             POUR L'ENTRAINEMENT.
!            : THE ESTIMATED CLOUD EXCESS IN MOIST ENTHALPY FOR ENTRAINMENT.
! ZS18      : INTEGRALE PONDEREE DE LA TENDANCE DE L'ENTHALPIE HUMIDE
!             FOIS GCVBETA*dMc/dp
!            : WEIGHTED INTEGRAL OF MOIST ENTHALPY TENDENCY
!              TIME  GCVBETA*dMc/dp

ZS1 =0.0_JPRB
ZS2 =0.0_JPRB
ZS3 =0.0_JPRB
ZS4 =0.0_JPRB
ZS5 =0.0_JPRB
ZS5S =0.0_JPRB
ZS6 =0.0_JPRB
ZS7 =0.0_JPRB
ZS8 =0.0_JPRB
ZS8S =0.0_JPRB
ZS9 =0.0_JPRB
ZS10=0.0_JPRB
ZS11=0.0_JPRB
ZS12=0.0_JPRB
ZS13=0.0_JPRB
ZS14=0.0_JPRB
ZS15=0.0_JPRB
ZS16=0.0_JPRB
ZS17=0.0_JPRB
ZS18=0.0_JPRB
!*
!     ---------------------------
!     I - CONSTANTES AUXILIAIRES.

!         AUXILIARY CONSTANTS.

! POUR RETROUVER L'ANCIEN SCHEMA SANS SEGMENTS ACTIFS :
! TO REOBTAIN OLD SCHEME WITHOUT ACTIVE SEGMENTS.:
IOLD=0
IF (LCVCAS) IOLD=1

!     POUR RETROUVER L'ANCIEN SCHEMA SANS LISSAGE DES FLUX TURBULENTS :
!     TO REOBTAIN OLD SCHEME WITHOUT SMOOTHING OF TURBULENT FLUXES :
ZIND=1.0_JPRB
IF (LCVLIS) ZIND=0.0_JPRB
ZCVPSI=MAX( GCVPSI,ZIND )

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD

!*
!     ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES, CONSTANTES DE SECURITE (POUR
!     TROIS DES INTEGRALES VERTICALES DU SCHEMA) ET INVERSE D'UN
!     ARGUMENT LIMITE (POUR EVITER "L'UNDERFLOW" DANS LES CALCULS D'EAU
!     LIQUIDE (OU GLACE) EN SUSPENSION DANS L'ASCENDANCE NUAGEUSE).

!          COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME) AND INVERSE OF A
!     CRITICAL ARGUMENT (TO AVOID "UNDERFLOW" IN LIQUID (OR ICE) WATER
!     CALCULATIONS FOR SUSTENTATION IN CLOUDY ASCENTS).

ZGDT=RG*TSPHY
ZGDTI=1.0_JPRB/ZGDT

ZEPS1=1.E+03_JPRB
ZEPS2=1.E+01_JPRB
ZEPS3=1.E-01_JPRB
ZEPS4=1.E-06_JPRB
ZEPS5=1.E-11_JPRB
ZEPS6=1.E-03_JPRB

ZARLII=0.004_JPRB
ZLNN(:,:)=0.0_JPRB
PFPCOR(:,:)=0.0_JPRB
ZFPCORS(:)=0.0_JPRB
ZTENTRX(:,:)=TENTRX
ZDIFTQ(:,:)=PDIFTQ(:,:)
ZDIFTS(:,:)=PDIFTS(:,:)

!     GRID POINT STORM SCHEME

CALL ACCVIMPGPS(YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 &               PDELP,PAPRSF,PVERVEL,PT,PQ,PQSAT,&
 &               ZTENTRX,PCVGQ,PDIFTQ,PDIFTS,PGEMU)

!*
!     ------------------------------------------------------------------
!     III - CALCULS PRELIMINAIRES D'ENERGIE STATIQUE SECHE, DE CHALEUR
!     LATENTE EFFECTIVE (DEPENDANTE DE L'OPTION PRESSION DE SURFACE
!     VARIANT OU NON AVEC LES FLUX D'HUMIDITE EN SURFACE), D'EPAISSEUR
!     EN PRESSION DES COUCHES DIVISEE PAR G*DT ET DE SON INVERSE POUR
!     CHAQUE NIVEAU AINSI QUE DES PROPORTIONS IMPOSEES DE PRECIPITATIONS
!     NEIGEUSE EN FONCTION DE LA TEMPERATURE ET DE LA PRESSION.

!           PRELIMINARY CALCULATIONS OF DRY STATIC ENERGY, OF EFFECTIVE
!     LATENT HEAT (DEPENDING UPON THE OPTION WHETHER SURFACE PRESSURE IS
!     VARYING OR NOT WITH THE SURFACE WATER FLUXES), OF LEVEL PRESSURE
!     THICKNESSES DIVIDED BY G*DT AND OF THEIR INVERSES AT ALL LEVELS AS
!     WELL AS OF IMPOSED PROPORTIONS OF SNOW PRECIPITATION IN FUNCTION
!     OF TEMPERATURE AND PRESSURE.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSNP      : PROPORTION DE PRECIPITATIONS NEIGEUSE.
!            : PROPORTION OF SNOW PRECIPITATION.
! ZLHL      : MOINS LA CHALEUR LATENTE DE CONSERV. D'ENTHALPIE LIQUIDE.
!            : MINUS THE LATENT HEAT OF CONSERVATION OF LIQUID ENTHALPY.
! ZLHN      : MOINS LA CHALEUR LATENTE DE CONSERV. D'ENTHALPIE SOLIDE.
!            : MINUS THE LATENT HEAT OF CONSERVATION OF SOLID ENTHALPY.
! ZPOID     : DP/(RG*DT) POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!            : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
! ZIPOI     : INVERSE DE ZPOID.
!            : INVERSE OF ZPOID.
! ZDSE      : TABLEAU LOCAL DES ENERGIES STATIQUE SECHES.
!            : LOCAL ARRAY FOR DRY STATIC ENERGIES.
! ZLHE      : TABLEAU LOCAL DES CHALEURS LATENTES EFFECTIVES.
!            : LOCAL ARRAY FOR EFFECTIVE LATENT HEATS.

! TEMPORARY SWITCH LLNEWM: TRUE IF THE NEW APPROACH FOR THE MELTING/FREEZING
! OF CONVECTIVE PRECIPITATIONS IS USED.
LLNEWM=GCVMLT > 0.0_JPRB

IF(LNEIGE .AND. LLNEWM) THEN
  !
  ! THE NEW APPROACH FOR THE MELTING/FREEZING OF CONVECTIVE PRECIPITATIONS IS USED:
  ! THE SNOW FRACTION FROM A GIVEN LEVEL DEPENDS BOTH ON THE SNOW FRACTION FROM ABOVE
  ! AND ON THE LOCAL TEMPERATURE.
  !
  DO JLON=KIDIA,KFDIA
    ZSNP(JLON,0)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PT(JLON,1)))
    ZSNP(JLON,1)=ZSNP(JLON,0)
  ENDDO
  DO JLEV=2,KLEV
    DO JLON=KIDIA,KFDIA
      ZSNP(JLON,JLEV)=MAX(0.0_JPRB,MIN(1.0_JPRB,ZSNP(JLON,JLEV-1)+GCVMLT&
       & *(RTT-PT(JLON,JLEV))*(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV))))
    ENDDO
  ENDDO
ELSEIF(LNEIGE) THEN
  !
  ! OLD APPROACH FOR THE MELTING/FREEZING OF CONVECTIVE PRECIPITATIONS IS USED:
  ! THE SNOW FRACTION DEPENDS ON LOCAL TEMPERATURE.
  !
  DO JLON=KIDIA,KFDIA
    ZSNP(JLON,0)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,PT(JLON,1)&
     & -RTT)**2/MAX(ZEPS6,PAPRS(JLON,0)))
  ENDDO
  DO JLEV=1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZSNP(JLON,JLEV)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,0.5_JPRB*(PT(JLON,JLEV)&
       & +PT(JLON,JLEV+1))-RTT)**2/PAPRS(JLON,JLEV))
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZSNP(JLON,KLEV)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,PTS(JLON)-RTT)**2&
     & /PAPRS(JLON,KLEV))
  ENDDO
ELSE
  !
  ! LNEIGE IS FALSE ==> ZSNP IS EQUAL TO ZERO.
  !
  DO JLEV=0,KLEV
    DO JLON=KIDIA,KFDIA
      ZSNP(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!     INITIALISATION DES TABLEAUX DE CHALEUR LATENTE DE FLUX.
!     "FLUX LATENT HEAT" ARRAYS INITIALIZATION.

DO JLON=KIDIA,KFDIA
  ZLHL(JLON,0)= -(FOLH(PT(JLON,1),0.0_JPRB)-ZCPVMD*PT(JLON,1))
  ZLHN(JLON,0)= -(FOLH(PT(JLON,1),1.0_JPRB)-ZCPVMD*PT(JLON,1))
ENDDO

DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZTMEAN=0.5_JPRB*(PT(JLON,JLEV)+PT(JLON,JLEV+1))
    ZLHL(JLON,JLEV)= -(FOLH(ZTMEAN,0.0_JPRB)-ZCPVMD*ZTMEAN)
    ZLHN(JLON,JLEV)= -(FOLH(ZTMEAN,1.0_JPRB)-ZCPVMD*ZTMEAN)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZLHL(JLON,KLEV)= -(FOLH(PTS(JLON),0.0_JPRB)-ZCPVMD*PTS(JLON))
  ZLHN(JLON,KLEV)= -(FOLH(PTS(JLON),1.0_JPRB)-ZCPVMD*PTS(JLON))
ENDDO

DO JLON=KIDIA,KFDIA

!     APPEL A LA FONCTION DEFINIE POUR CALCULER LA REFERENCE DE CHALEUR
!     LATENTE EFFECTIVE AU SOL.
!     CALL TO THE DEFINED FUNCTION TO COMPUTE THE REFERENCE OF EFFECTIVE
!     LATENT HEAT AT THE SURFACE.

! - TEMPORAIRE(S) 1D .

! ZLHSB     : CHALEUR LATENTE DE BILAN CALCULEE EN SURFACE.
!            : BUDGET LATENT HEAT AS COMPUTED AT THE SURFACE.
! ZHSEAD    : L'ADIABATIQUE SATUREE, ESTIMEE POUR l'ENTRAINEMENT
!            : FIRST GUESS OF SATURATED ADIABAT
! ZHCMHEB   : L'EXCES NUAGEUX DE L'ENERGIE STATIQUE HUMIDE
!            : ESTIMATED CLOUD EXCESS IN MOIST STATIC ENERGY

  ZLHSB(JLON)=-ZLHL(JLON,KLEV)-ZSNP(JLON,KLEV)*(ZLHN(JLON,KLEV)&
   & -ZLHL(JLON,KLEV))

  ZPOID(JLON,KLEV)=PDELP(JLON,KLEV)*ZGDTI
  ZIPOI(JLON,KLEV)=PRDELP(JLON,KLEV)*ZGDT
  ZDSE(JLON,KLEV)=PCP(JLON,KLEV)*PT(JLON,KLEV)+PAPHIF(JLON,KLEV)
  ZDELTA=0.5_JPRB*(ZSNP(JLON,KLEV-1)+ZSNP(JLON,KLEV))
  ZLHE(JLON,KLEV)= FOLH (PT(JLON,KLEV),ZDELTA)+(ZCPWMD+ZDELTA&
   & *(ZCPSMD-ZCPWMD))*(PT(JLON,KLEV)-PTS(JLON))
  ZHSEAD(JLON,KLEV)=ZDSE(JLON,KLEV)+ZLHSB(JLON)*PQ(JLON,KLEV)
  ZHCMHEB(JLON)=0.0_JPRB
ENDDO

!     CALCULS A TOUS LES NIVEAUX. ATTENTION LA FONCTION DEFINIE FOLH EST
!     APPELEE AVEC DES VALEURS NON NECESSAIREMENT 0 OU 1 DE SON SECOND
!     ARGUMENT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!     COMPUTATIONS AT ALL LEVELS. BEWARE, THE DEFINED FUNCTION FOLH WILL
!     BE CALLED WITH VALUES THAT ARE NOT NECESSARILY 0 OR 1 FOR ITS
!     SECOND ARGUMENT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

DO JLEV=KLEV-1,KTDIA,-1
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    IF (LSLC) THEN
      ZINDX=MAX(0.0_JPRB,SIGN(1.0_JPRB,ABS(PRCORI(JLON))-ZEPS5))
      ZCORIO=ZEPS5+ZINDX*(PRCORI(JLON)-ZEPS5)
      ZVORT0=0.5_JPRB*(PVORT0(JLON,JLEV)+PVORT0(JLON,JLEV+1))
      ZFETA=ZINDX/(MAX(ETACUT,1.0_JPRB+ZVORT0/ZCORIO))
      PGEOSLC(JLON,JLEV)=MIN(PGEOSLC(JLON,JLEV),PGEOSLC(JLON,JLEV+1)&
       & *PTW(JLON,JLEV)/PTW(JLON,JLEV+1))
      ZATSLC(JLON,JLEV)=ZFETA*0.5_JPRB*(PGEOSLC(JLON,JLEV)+PGEOSLC(JLON,JLEV+1))&
       & *((PU(JLON,JLEV+1)-PU(JLON,JLEV))**2+(PV(JLON,JLEV+1)&
       & -PV(JLON,JLEV))**2)/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))**2
    ELSE
      ZATSLC(JLON,JLEV)=0.0_JPRB
    ENDIF
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    ZDELTA=0.5_JPRB*(ZSNP(JLON,JLEV-1)+ZSNP(JLON,JLEV))
    ZLHE(JLON,JLEV)= FOLH (PT(JLON,JLEV),ZDELTA)+(ZCPWMD+ZDELTA&
     & *(ZCPSMD-ZCPWMD))*(PT(JLON,JLEV)-PTS(JLON))
    ZHSE=ZDSE(JLON,JLEV)+ZLHSB(JLON)*PQ(JLON,JLEV)
    ZHSEAD(JLON,JLEV)=MAX(ZHSEAD(JLON,JLEV+1)+(PAPHIF(JLON,JLEV)&
     & -PAPHIF(JLON,JLEV+1))*ZATSLC(JLON,JLEV)/(1.0_JPRB&
     & +ZATSLC(JLON,JLEV)),ZHSE)
    ZHCMHE=0.5_JPRB*(ZHSEAD(JLON,JLEV)-ZHSE)
    ZS17(JLON)=ZS17(JLON)+(ZHCMHEB(JLON)+ZHCMHE)*&
     & (PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZHCMHEB(JLON)=ZHCMHE
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     IV - INITIALISATION A LA BASE. LE PROFIL NUAGEUX SERA FINALEMENT
!     CARACTERISE PAR SON HUMIDITE TOTALE ET SON ENERGIE STATIQUE SECHE
!     DE DETRAINEMENT AINSI QUE PAR UN INDICE DE STABILITE (UN AUX DEUX
!     BOUTS DE TOUTE COUCHE CONVECTIVE (DE COUCHE DU MODELE A COUCHE DU
!     MODELE ADJACENTE) ET ZERO AILLEURS) ET UN PROFIL NON ENCORE NORME
!     DU FLUX DE MASSE, TABLEAU DANS LEQUEL SERA ENSUITE RANGE LE
!     PRODUIT DU FLUX DE MASSE PAR LE PAS DE TEMPS (HOMOGENE A UNE
!     PRESSION).

!          BOTTOM INITIALISATION. THE CLOUD PROFILE WILL EVENTUALY BE
!     CHARACTERISED BY ITS TOTAL HUMIDITY AND ITS DETRAINING DRY STATIC
!     ENERGY AS WELL AS BY A STABILITY INDEX (ONE AT BOTH ENDS OF ANY
!     CONVECTIVE SLAB (FROM MODEL LAYER TO THE ADJACENT ONE) AND ZERO
!     ELSEWHERE) AND A YET UNNORMALIZED MASS FLUX PROFILE, ARRAY IN
!     WHICH THE PRODUCT OF THE MASS FLUX BY THE TIME STEP (QUANTITY
!     HOMOGENEOUS TO A PRESSURE) WILL AFTERWARDS BE WRITEN.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM     : PROFIL DE FLUX DE MASSE PUIS FLUX DE MASSE FOIS DT.
!            : PROFILE OF MASS FLUX AND LATER MASS FLUX TIME DT.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSDN      : ENERGIE STATIQUE SECHE DE DETRAINEMENT DU NUAGE.
!            : DETRAINING DRY STATIC ENERGY OF THE CLOUD.
! ZQDN      : HUMIDITE TOTALE DE DETRAINEMENT DU NUAGE.
!            : DETRAINING TOTAL CLOUD HUMIDITY.

! ZQVDN     : HUMIDITE (q_v) DE DETRAINEMENT DU NUAGE.
!            : DETRAINING (q_v) CLOUD HUMIDITY.

DO JLON=KIDIA,KFDIA

!     CARACTERISTIQUES THERMODYNAMIQUES DU NUAGE.
!     THERMODYNAMIC CHARACTERISTICS OF THE CLOUD.

! - TEMPORAIRE(S) 1D .

! ZTN       : TEMPERATURE DE L'ASCENDANCE NUAGEUSE.
!            : TEMPERATURE OF THE CLOUDY ASCENT.
! ZQN       : HUMIDITE SPECIFIQUE VAPEUR DE L'ASCENDANCE NUAGEUSE.
!            : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! ZLN       : HUMIDITE SPECIFIQUE CONDENSEE DE L'ASCENDANCE NUAGEUSE.
!            : CONDENSED SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.

  ZTN(JLON)=MIN(PT(JLON,KLEV),PTW(JLON,KLEV))
  ZQN(JLON)=MAX(PQ(JLON,KLEV),PQW(JLON,KLEV))
  ZLCL(JLON)=PQSAT(JLON,KLEV)-PQW(JLON,KLEV)
  ZLN(JLON)=0.0_JPRB
  ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*ZQN(JLON))*ZTN(JLON)+PAPHIF(JLON,KLEV)
  ZQDN(JLON,KLEV)=ZQN(JLON)
  ZQVDN(JLON,KLEV)=ZQN(JLON)

!     CARACTERISTIQUES ACTIVES DU NUAGE.
!     ACTIVE CHARACTERISTICS OF THE CLOUD.

! - TEMPORAIRE(S) 1D .

! ZHCMH     : DIFFERENCE D'ENERGIE STATIQUE HUMIDE NUAGE/ENVIRONEMENT.
!            : DIFFERENCE IN MOIST STATIC ENERGY CLOUD/ENVIRONMENT.
! ZFMDQ     : "0.5*ZFORML" FOIS LE DELTA VERTICAL D'HUMIDITE SPECIFIQUE.
!            : "0.5*ZFORML" TIME THE VERTICAL SPECIFIC HUMIDITY JUMP.
! ZICVG     : L FOIS DELP FOIS LA CONVERGENCE D'HUMIDITE.
!            : L TIME DELP TIME THE HUMIDITY CONVERGENCE.

  KNLAB(JLON,KLEV)=0
  KNND(JLON)=0
  ZFORM(JLON,KLEV)=0.0_JPRB
  ZHCMH(JLON)=0.0_JPRB
  ZFMDQ(JLON)=0.0_JPRB
  ZFMDS(JLON)=0.0_JPRB
  ZICVG(JLON)=PDELP(JLON,KLEV)*PCVGQ(JLON,KLEV)*ZLHE(JLON,KLEV)

ENDDO

!*
!     ------------------------------------------------------------------
!     V - PREMIERE BOUCLE VERTICALE (VERS LE HAUT) INCLUANT LE CALCUL
!     "EN ADIABATIQUE SATUREE" DU PROFIL NUAGEUX.

!         FIRST VERTICAL LOOP (UPWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     INITIALISATION DU PARAMETRE QUI SERVIRA EVENTUELLEMENT A EVITER
!     DES CALCULS INUTILES EN HAUT DE L'ATMOSPHERE ET DEBUT DE LA
!     BOUCLE.
!     INITIALIZATION OF THE PARAMETER THAT SHALL EVENTUALLY HELP
!     AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE AND START OF THE VERTICAL LOOP.

ITOP=KLEV+1
DO JLEV=KLEV-1,KTDIA,-1

!     ADIABATIQUE SATUREE AVEC ENTRAINEMENT ET SUSTENTATION D'EAU
!     LIQUIDE (OU GLACE).
!     SATURATED ADIABAT WITH ENTRAINMENT AND LIQUID (OR ICE) WATER
!     SUSTENTATION.

  DO JLON=KIDIA,KFDIA

!     ENTRAINEMENT.
!     ENTRAINEMENT.

! - TEMPORAIRE(S) 1D .

! ZTB       : "ZTN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB       : "ZQN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZLB       : "ZLN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZLN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZFRAA     : FRACTION DE CHEMIN D'ASCENDANCE PARCOURU.
!            : ASCENT FRACTION DONE.

    ZZ=GCVALFA*ZS17(JLON)
    ZCOEF=ZZ*TENTR/(TENTR+ZTENTRX(JLON,JLEV)/(1.0_JPRB+ZZ*ZTENTRX(JLON,JLEV)))
    ZENTRN=TENTR*SQRT(SQRT(TENTR/ZTENTRX(JLON,JLEV)))
    ZENTRMX=ZENTRN+(ZTENTRX(JLON,JLEV)-ZENTRN)/(1._JPRB+(ZTENTRX(JLON,JLEV)-ZENTRN)*ZCOEF)
    ZENTRMN=ZENTRN+(TENTR-ZENTRN)/(1.0_JPRB+(TENTR-ZENTRN)*ZCOEF)
    ZENTRD(JLON)=ZENTRMN
    ZENEN=ZENTRMX*SQRT(SQRT(ZENTRMN/ZENTRMX))
    ZFRAA=EXP(-ZENEN*ZS5(JLON))
    ZENTR=ZENTRMN+(ZENTRMX-ZENTRMN)*ZFRAA
    ZMIX=ZENTR*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
    ZRMIX(JLON,JLEV)=ZMIX
    ZTB(JLON)=ZTN(JLON)+ZMIX*(PT(JLON,JLEV+1)-ZTN(JLON))
    ZQB(JLON)=ZQN(JLON)+ZMIX*(PQ(JLON,JLEV+1)-ZQN(JLON))
    ZLB(JLON)=ZLN(JLON)*(1.0_JPRB-ZMIX)
    ZHS_REE=PCP(JLON,JLEV+1)*ZTB(JLON)+PAPHIF(JLON,JLEV+1)&
     & +ZLHSB(JLON)*ZQB(JLON)

! VALEUR DU NIVEAU PRECEDENT (FERMETURE EN CAPE):
! STORE PREVIOUS LEVEL VALUES (CAPE CLOSURE):

    ZTNL(JLON)=ZTN(JLON)
    ZQNL(JLON)=ZQN(JLON)
    ZLNL(JLON)=ZLN(JLON)

!     COEFFICIENTS DE L'HYDROSTATIQUE DU PROFIL NUAGEUX.
!     HYDROSTATIC COEFFICIENTS FOR THE CLOUD PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB      : POIDS DE "ZTB" DANS L'EPAISSEUR EN GEOPOTENTIEL.
!            : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH      : POIDS DE "ZTN" A "ZQN=ZQB" DANS LA MEME EPAISSEUR.
!            : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH      : POUR LA CORRECTION DE VARIATION DE Q A ZRBH.
!            : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB&
     & -ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

!     DANS LE CALCUL DE L'ADIABATIQUE SATUREE GCVADS PERRMET UNE
!     TRANSITION CONTINUE ENTRE LE TRAITEMENT EQUIPRESSION ET
!     EQUIGEOPOTENTIEL.

!     TO COMPUTATE ADIABATIC PROFILE GCVADS ALLOWS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
     & -PAPHIF(JLON,JLEV+1))/ZTB(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

! ZFFAND    : FRACTION DE FLOTTABILITE PAR RAPPORT A UNE ADIABATIQUE
!             NON DILUEE.
!           : FRACTION OF THE BUOYANCY-EXCESS OF AN UNDILUTE PLUME.

    ZFFAND=1.0_JPRB/(1.0_JPRB+GCVNU*(1.0_JPRB-ZFRAA)&
     & *MAX(0.0_JPRB,ZHSEAD(JLON,JLEV)-ZHS_REE))
    ZFFAND=ZFFAND/(1.0_JPRB+ZATSLC(JLON,JLEV))
    ZRBB(JLON)=ZRBB(JLON)*ZFFAND
    ZRBH(JLON)=ZRBH(JLON)*ZFFAND
    ZRVH(JLON)=ZRVH(JLON)*ZFFAND

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     APPEL A LA FONCTION DEFINIE POUR LE CALCUL DE LA PSEUDO CHALEUR
!     LATENTE A LA BASE (PARMI TROIS PARAMETRES CARACTERISTIQUES).
!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH       : VALEUR COURANTE DU PSEUDO LH DURANT LA BOUCLE DE NEWTON.
!            : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP       : COMME ZLH MAIS POUR LA CHALEUR MASSIQUE CP.
!            : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)= FOLH (ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
     & ZCPWMD))&
     & *ZLB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     CHANGEMENT DE NIVEAU POUR OBTENIR LA PREMIERE EBAUCHE, POINT DE
!     DEPART DE LA BOUCLE DE NEWTON.
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ENDDO

!     BOUCLE DE NEWTON.
!     NEWTON'S LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

      IF (LNEIGE) THEN
        ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
      ELSE
        ZDELTA=0.0_JPRB
      ENDIF

!     CALCULS DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (ZTN(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON),ZDELTA))

!     INCREMENTATIONS.
!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQN(JLON)=ZQN(JLON)+ZDELQ
      ZTN(JLON)=ZTN(JLON)+ZDELT
    ENDDO
  ENDDO

!     SUSTENTATION DE L'EAU LIQUIDE (OU GLACE) NUAGEUSE.
!     SUSTENTATION OF LIQUID (OR ICE) WATER.

  DO JLON=KIDIA,KFDIA
    ZLIQ=ECMNP/(ZRBB(JLON)*ZTB(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
     & *(ZQN(JLON)-ZQB(JLON)))*ZTN(JLON))
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLN(JLON)=MAX(0.0_JPRB,ZLB(JLON)*ZEXP+(ZQN(JLON)-ZQB(JLON))*ZLIQ&
     & *(ZEXP-1.0_JPRB))
  ENDDO

!     TEST DE STABILITE, RECTIFICATION EVENTUELLE DU NIVEAU INFERIEUR,
!     CALCUL DU PARAMETRE DE PROFIL DU FLUX DE MASSE ET DES DEUX
!     INTEGRALES INTERVENANT DANS LA CONDITION DE FERMETURE "TYPE KUO".
!     STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
!     COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
!     INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

!     TEST DOUBLE DE KUO (FLOTABILITE ET CONVERGENCE D'HUMIDITE).
!     DOUBLE KUO-TYPE TEST (BUOYANCY AND HUMIDITY CONVERGENCE).

    ZKUO1=(RD*(1.0_JPRB-ZLN(JLON))+ZRVMD*ZQN(JLON))*ZTN(JLON)&
     & -PR(JLON,JLEV)*PT(JLON,JLEV)
    ZICVGL=PDELP(JLON,JLEV)*PCVGQ(JLON,JLEV)*ZLHE(JLON,JLEV)
    ZKUO2=ZS1(JLON)+ZICVGL+ZICVG(JLON)
    KNLAB(JLON,JLEV)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZKUO1)))
    IF (.NOT.LCAPE) THEN
      KNLAB(JLON,JLEV) = KNLAB(JLON,JLEV) *&
       & NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZKUO2)))
    ENDIF
    IF (LNOIAS) THEN
      KNLAB(JLON,JLEV)=KNLAB(JLON,JLEV)*MAX(0.0_JPRB,SIGN(1.0_JPRB,&
       & ZDSE(JLON,JLEV)-ZDSE(JLON,JLEV+1)))
    ENDIF
    KNLAB(JLON,JLEV+1)=MAX(KNLAB(JLON,JLEV+1),KNLAB(JLON,JLEV))

!     EN CAS DE NUAGE PLUS "FROID" QUE L'ENVIRONEMENT ON REEGALISE
!     TEMPERATURE ET HUMIDITE SPECIFIQUES DU NUAGE A CELLES DU
!     THERMOMETRE MOUILLE DE L'ENVIRONEMENT.
!     IN CASE OF A "COLDER" CLOUD THAN THE ENVIRONMENT ONE SETS BACK
!     TEMPERATURE AND SPECIFIC HUMIDITY OF THE CLOUD TO THAT OF THE WET
!     BULB CHARACTERISTICS OF THE ENVIRONMENT.

    ZLCL(JLON)=MIN(ZLCL(JLON),PQSAT(JLON,JLEV)-ZQN(JLON))
    ZMIXCIN=ZRMIX(JLON,JLEV)*RCIN
    ZLN(JLON)=MAX(0.0_JPRB,ZLN(JLON)+ZQN(JLON)-MAX(ZQN(JLON),&
    &MAX(PQ(JLON,JLEV),PQW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)+ZMIXCIN*ZQN(JLON)))
    ZLNN(JLON,JLEV)=ZLN(JLON)
    ZQN(JLON)=MAX(ZQN(JLON),MAX(PQ(JLON,JLEV),PQW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)+ZMIXCIN*ZQN(JLON))
    ZTN(JLON)=MAX(ZTN(JLON),MIN(PT(JLON,JLEV),PTW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)+ZMIXCIN*ZTN(JLON))

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     VALEURS FINALES DE DETRAINEMENT AVEC APPEL A LA FONCTION DEFINIE
!     POUR REVENIR A LA VERITABLE CHALEUR LATENTE.
!     FINAL DETRAINMENT VALUES WITH A CALL TO THE DEFINED FUNCTION TO
!     COME BACK TO THE TRUE LATENT HEAT.

    ZQDN(JLON,JLEV)=ZQN(JLON)+ZLN(JLON)
    ZQVDN(JLON,JLEV)=ZQN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQDN(JLON,JLEV))*ZTN(JLON)&
     & - FOLH (ZTN(JLON),ZDELTA)*ZLN(JLON)&
     & +PAPHIF(JLON,JLEV)

!     PROFIL DE FLUX DE MASSE.
!     MASS FLUX PROFILE.

    ZHCMHL=PCP(JLON,JLEV)*(ZTN(JLON)-PTW(JLON,JLEV))+ZLHE(JLON,&
     & JLEV)&
     & *(ZQN(JLON)-PQW(JLON,JLEV))
    ZFORM(JLON,JLEV)=KNLAB(JLON,JLEV)&
     & *SQRT(MAX(0.0_JPRB,ZHCMHL+ZHCMH(JLON))&
     & *PAPRS(JLON,JLEV)/(PT(JLON,JLEV)&
     & +PT(JLON,JLEV+1)))
    ZHCMH(JLON)=ZHCMHL

!     SOMMATIONS.
!     SUMMATIONS.
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,JLEV+1)&
     & *PDELP(JLON,JLEV+1)/PAPRSF(JLON,JLEV+1)&
     & *( ZTNL(JLON)*(1.0_JPRB-ZLNL(JLON)+RETV*ZQNL(JLON))&
     & -  PT(JLON,JLEV+1)*(1.0_JPRB+RETV*PQ(JLON,JLEV+1)) )
    ZFMDQL=0.5_JPRB*ZFORM(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQ(JLON,JLEV))
    ZFMDSL=0.5_JPRB*ZFORM(JLON,JLEV)*(ZDSE(JLON,JLEV+1)-ZDSE(JLON,JLEV))
    ZS6(JLON)=ZS6(JLON)+KNLAB(JLON,JLEV+1)*(ZFMDQL+ZFMDQ(JLON))&
     & *ZLHE(JLON,JLEV+1)
    ZS16(JLON)= ZS16(JLON) - KNLAB(JLON,JLEV+1) * (&
     & (1.0_JPRB+RETV*PQ(JLON,JLEV+1))/PCP(JLON,JLEV+1)*&
     & (ZFMDSL+ZFMDS(JLON)) + RETV * PT(JLON,JLEV+1) *&
     & (ZFMDQL+ZFMDQ(JLON)) )&
     & /PAPRSF(JLON,JLEV+1)
    ZFMDQ(JLON)=ZFMDQL
    ZFMDS(JLON)=ZFMDSL
    ZS1(JLON)=ZS1(JLON)+KNLAB(JLON,JLEV+1)*ZICVG(JLON)
    ZICVG(JLON)=ZICVGL
    ZS5(JLON)=ZS5(JLON)+KNLAB(JLON,JLEV)*(PAPHIF(JLON,JLEV)&
     & -PAPHIF(JLON,JLEV+1))
  ENDDO

!     VERIFICATION DE LA PRESENCE D'AU MOINS UN NUAGE LE LONG DU VECTEUR
!     "JLON" ET MODIFICATION SI NECESSAIRE DE ITOP.
!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+KNLAB(JLON,JLEV)
  ENDDO

  IF (ISUM /= 0) THEN
    ITOP=JLEV
  ENDIF

ENDDO

!     ABANDON DU CALCUL EN CAS D'ABSENCE DE CONVECTION. LA SEQUENCE SOUS
!     "IF THEN / ENDIF" NE SERA PAS INDENTEE VU SA LONGUEUR ET SON
!     CARACTERE PARTICULIER (PSEUDO "RETURN").
!     QUITTING IN CASE OF NO CONVECTION EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA

IF (ITOP < KLEV+1) THEN

!     SOMMATION AU DERNIER NIVEAU (SOMMET).
!     LAST LEVEL SUMMATION (TOP).
  DO JLON=KIDIA,KFDIA
    ZS6(JLON)=ZS6(JLON)+KNLAB(JLON,KTDIA)*ZFMDQ(JLON)*ZLHE(JLON,KTDIA)
    ZS1(JLON)=ZS1(JLON)+KNLAB(JLON,KTDIA)*ZICVG(JLON)
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,KTDIA)&
     & *PDELP(JLON,KTDIA)/PAPRSF(JLON,KTDIA)&
     & *( ZTNL(JLON)*(1.0_JPRB-ZLNL(JLON)+RETV*ZQNL(JLON))&
     & -  PT(JLON,KTDIA)*(1.0_JPRB+RETV*PQ(JLON,KTDIA)) )
    ZS16(JLON)= ZS16(JLON) - KNLAB(JLON,KTDIA) * (&
     & (1.0_JPRB+RETV*PQ(JLON,KTDIA))/PCP(JLON,KTDIA)*&
     & ZFMDS(JLON) + RETV * PT(JLON,KTDIA) *&
     & ZFMDQ(JLON) ) /PAPRSF(JLON,KTDIA)
  ENDDO

!*
!     ------------------------------------------------------------------
!     VI - CONDITION DE FERMETURE ET REMISE A ZERO DE L'INTEGRALE ZS6L.

!          CLOSURE ASSUMPTION AND SET BACK TO ZERO OF THE ZS6L INTEGRAL.

! - TEMPORAIRE(S) 1D .

! ZALF      : FACTEUR DE NORMALISATION POUR "ZFORML" (FERMETURE DE KUO).
!            : NORMALIZATION FACTOR FOR "ZFORML" (KUO CLOSURE).

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PCAPE(JLON)=ZS15(JLON)*RD
    IF (LCAPE) THEN
      ZALF(JLON)=TSPHY*MAX(0.0_JPRB,ZS15(JLON))/MAX(ZEPS4,ZS16(JLON))&
       & *MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS16(JLON)-ZEPS4))&
       & /MAX(PTAUX(JLON),ZEPS5)
    ELSE
      ZALF(JLON)=(TSPHY*MAX(0.0_JPRB,ZS1(JLON))/MAX(ZEPS1,ZS6(JLON)))&
       & *MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS6(JLON)))
    ENDIF
    ZS6(JLON)=0.0_JPRB
    ZS5(JLON)=0.0_JPRB
  ENDDO

!*
!     ------------------------------------------------------------------
!     VII - DEUXIEME BOUCLE VERTICALE (VERS LE BAS) SERVANT A RESOUDRE
!     L'ALGORITHME IMPLICITE POUR L'ADVECTION DE SUBSIDENCE
!     COMPENSATOIRE ET A EFFECTUER TOUTES LES SOMMATIONS PONDEREES SUR
!     L'EPAISSEUR DU NUAGE. ON STOCKERA EGALEMENT AU PASSAGE LES VALEURS
!     FINALES PROVISOIRES DE L'HUMIDITE, DE L'ENERGIE STATIQUE SECHE ET
!     DES DEUX COMPOSANTES DU VENT.

!           SECOND VERTICAL LOOP (DOWNWARDS) TO SOLVE THE IMPLICIT
!     ALGORITHM FOR COMPENSATING SUBSIDENCE ADVECTION AND TO DO ALL
!     WEIGHTED SUMS ACROSS THE CLOUD DEPTH. ONE WILL AT THE SAME TIME
!     STORE PROVISIONAL FINAL VALUES FOR HUMIDITY, DRY STATIC ENERGY AND
!     FOR THE TWO WIND COMPONENTS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZFQSC     : VALEUR FINALE DE Q (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL Q VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFSSC     : VALEUR FINALE DE S (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL S VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFUSC     : VALEUR FINALE DE U (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL U VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFVSC     : VALEUR FINALE DE V (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL V VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZALFPB    : DIVERGENCE DE FLUX DE MASSE
!            : MASS FLUX DIVERCENCY

!     ADITIONAL VERTICAL LOOP (UPWARDS) TO MODIFY THE PREVIOUSLY
!     COMPUTED MASS FLUX.

  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA

!     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
!     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.

      ZFORM(JLON,JLEV)=ZALF(JLON)*ZFORM(JLON,JLEV)

!     PROTECTION CONTRE L'INSTABILITE NON-LINEAIRE QUI PEUT SE
!     SE DECLENCHER OCCASIONELLEMENT MALGRE L'ALGORITHME IMPLICITE.
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.

      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV+1)+(ZFORM(JLON,JLEV)&
       & -ZFORM(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
       & *MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV+1))))
    ENDDO
  ENDDO

!     CAS PARTICULIER DU PREMIER NIVEAU.
!     SPECIAL CASE OF THE FIRST LEVEL.

  DO JLON=KIDIA,KFDIA

!     DIVERGENCE DE FLUX DE MASSE

    ZALFPB(JLON,ITOP)=GCVBETA*PRDELP(JLON,ITOP)*ZFORM(JLON,ITOP)

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

! - TEMPORAIRE(S) 1D .

! ZFMDS     : DEMI FLUX DE MASSE FOIS DELTA DE S.
!            : HALF THE MASS FLUX TIME THE JUMP IN S.
! ZFMDU     : DEMI FLUX DE MASSE FOIS DELTA DE U.
!            : HALF THE MASS FLUX TIME THE JUMP IN U.
! ZFMDV     : DEMI FLUX DE MASSE FOIS DELTA DE V.
!            : HALF THE MASS FLUX TIME THE JUMP IN V.

    ZFMDQ(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PQ(JLON,ITOP+1)-PQ(JLON,ITOP))
    ZFMDS(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(ZDSE(JLON,ITOP+1)-ZDSE(JLON,ITOP))
    ZFMDU(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PU(JLON,ITOP+1)-PU(JLON,ITOP))
    ZFMDV(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PV(JLON,ITOP+1)-PV(JLON,ITOP))
    ZFQSC(JLON,ITOP)=PQ(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDQ(JLON)
    ZFSSC(JLON,ITOP)=ZDSE(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDS(JLON)
    ZFUSC(JLON,ITOP)=PU(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDU(JLON)
    ZFVSC(JLON,ITOP)=PV(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDV(JLON)

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=KNLAB(JLON,ITOP)*PDELP(JLON,ITOP)
    ZZ2=ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,ITOP) -ZDSE(JLON,ITOP))&
     & +ZLHSB(JLON) *MAX(0.0_JPRB,ZQDN(JLON,ITOP)-PQ(JLON,ITOP)))
    ZS2(JLON)=ZS2(JLON)+ZZ2
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZSMOOTH=PQSAT(JLON,ITOP)*PAPRSF(JLON,ITOP)**GCVPSIE
    ZS5S(JLON)=ZS5S(JLON)+ZDPLAB*ZSMOOTH
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,ITOP)-PQ(JLON,ITOP))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,ITOP)-ZDSE(JLON,ITOP))
    ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,ITOP)&
     & *(PDIFTQ(JLON,ITOP-1)-PDIFTQ(JLON,ITOP))
    ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,ITOP)&
     & *(PDIFTS(JLON,ITOP-1)-PDIFTS(JLON,ITOP))
    ZS18(JLON)=ZS18(JLON)+ZALFPB(JLON,ITOP)*ZZ2
  ENDDO

!     BOUCLE VERTICALE.
!     VERTICAL LOOP.

  DO JLEV=ITOP+1,KLEV-1
    DO JLON=KIDIA,KFDIA

!     DIVERGENCE DE FLUX DE MASSE

      ZALFPB(JLON,JLEV)=GCVBETA*PRDELP(JLON,JLEV)&
       & *MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

      ZFMDQL=0.5_JPRB*ZFORM(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQ(JLON,JLEV))
      ZFMDSL=0.5_JPRB*ZFORM(JLON,JLEV)*(ZDSE(JLON,JLEV+1)-ZDSE(JLON,JLEV))
      ZFMDUL=0.5_JPRB*ZFORM(JLON,JLEV)*(PU(JLON,JLEV+1)-PU(JLON,JLEV))
      ZFMDVL=0.5_JPRB*ZFORM(JLON,JLEV)*(PV(JLON,JLEV+1)-PV(JLON,JLEV))
      ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,JLEV)*ZFORM(JLON,JLEV-1))
      ZFQSC(JLON,JLEV)=ZAUX*(PQ(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDQ(JLON)-ZFMDQL+ZFORM(JLON,JLEV-1)&
       & *ZFQSC(JLON,JLEV-1)))
      ZFSSC(JLON,JLEV)=ZAUX*(ZDSE(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDS(JLON)-ZFMDSL+ZFORM(JLON,JLEV-1)&
       & *ZFSSC(JLON,JLEV-1)))
      ZFUSC(JLON,JLEV)=ZAUX*(PU(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDU(JLON)-ZFMDUL+ZFORM(JLON,JLEV-1)&
       & *ZFUSC(JLON,JLEV-1)))
      ZFVSC(JLON,JLEV)=ZAUX*(PV(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDV(JLON)-ZFMDVL+ZFORM(JLON,JLEV-1)&
       & *ZFVSC(JLON,JLEV-1)))
      ZFMDQ(JLON)=ZFMDQL
      ZFMDS(JLON)=ZFMDSL
      ZFMDU(JLON)=ZFMDUL
      ZFMDV(JLON)=ZFMDVL

!     SOMMATIONS.
!     SUMMATIONS.

      ZDPLAB=KNLAB(JLON,JLEV)*PDELP(JLON,JLEV)
      ZZ2=ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,JLEV)-ZDSE(JLON,JLEV))&
       & +ZLHSB(JLON)*MAX(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV)))
      ZS2(JLON)=ZS2(JLON)+ZZ2
      ZS5(JLON)=ZS5(JLON)+ZDPLAB
      ZSMOOTH=PQSAT(JLON,JLEV)*PAPRSF(JLON,JLEV)**GCVPSIE
      ZS5S(JLON)=ZS5S(JLON)+ZDPLAB*ZSMOOTH
      ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,JLEV)-PQ(JLON,JLEV))
      ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,JLEV)-ZDSE(JLON,JLEV))
      ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,JLEV)&
       & *(PDIFTQ(JLON,JLEV-1)-PDIFTQ(JLON,JLEV))
      ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,JLEV)&
       & *(PDIFTS(JLON,JLEV-1)-PDIFTS(JLON,JLEV))
      ZS18(JLON)=ZS18(JLON)+ZALFPB(JLON,JLEV)*ZZ2
    ENDDO
  ENDDO

!     CAS PARTICULIER DU DERNIER NIVEAU.
!     SPECIAL CASE OF THE LAST LEVEL.

  DO JLON=KIDIA,KFDIA

!     DIVERGENCE DU FLUX DE MASSE

    ZALFPB(JLON,KLEV)=0.0_JPRB

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

    ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,KLEV)*ZFORM(JLON,KLEV-1))
    ZFQSC(JLON,KLEV)=ZAUX*(PQ(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDQ(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFQSC(JLON,KLEV-1)))
    ZFSSC(JLON,KLEV)=ZAUX*(ZDSE(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDS(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFSSC(JLON,KLEV-1)))
    ZFUSC(JLON,KLEV)=ZAUX*(PU(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDU(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFUSC(JLON,KLEV-1)))
    ZFVSC(JLON,KLEV)=ZAUX*(PV(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDV(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFVSC(JLON,KLEV-1)))

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=KNLAB(JLON,KLEV)*PDELP(JLON,KLEV)
    ZZ2=ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,KLEV) -ZDSE(JLON,KLEV))&
     & +ZLHSB(JLON)*MAX(0.0_JPRB,ZQDN(JLON,KLEV)-PQ(JLON,KLEV)))
    ZS2(JLON)=ZS2(JLON)+ZZ2
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZSMOOTH=PQSAT(JLON,KLEV)*PAPRSF(JLON,KLEV)**GCVPSIE
    ZS5S(JLON)=ZS5S(JLON)+ZDPLAB*ZSMOOTH
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,KLEV)-PQ(JLON,KLEV))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,KLEV)-ZDSE(JLON,KLEV))
    ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,KLEV)&
     & *(PDIFTQ(JLON,KLEV-1)-PDIFTQ(JLON,KLEV))
    ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,KLEV)&
     & *(PDIFTS(JLON,KLEV-1)-PDIFTS(JLON,KLEV))
  ENDDO

!*
!     ------------------------------------------------------------------
!     VIII - CALCUL DES COEFFICIENTS DU SCHEMA ET NORMALISATION DES
!     INTEGRALES, AVEC DE PLUS DANS LES CAS D'ALIMENTATION OU DE
!     FLOTABILITE INSUFISANTE UNE MISE A ZERO DES EFFETS CONVECTIFS
!     (SEULEMENT EFFECTIVE EN FIN DE ROUTINE).

!            COMPUTATION OF THE COEFFICIENTS OF THE SCHEME AND
!     NORMALIZATION OF THE INTEGRALS, WITH, IN THE CASE OF INSUFFICIENT
!     MOISTURE FEEDING OR BUOYANCY, A RESET TO ZERO OF CONVECTIVE
!     EFECTS (ONLY EFFECTIVE IN FACT AT THE END OF THE SUBROUTINE).

! - TEMPORAIRE(S) 1D .

! ZALFP     : PROPORTION CONSTANTE DETRAINEE EN UN PAS DE TEMPS (K0 DE
!             K=K0+D(MC)/DP).
!            : CONSTANT DETRAINED FRACTION IN ONE TIME-STEP (K0 FROM
!              K=K0+D(MC)/DP).

  DO JLON=KIDIA,KFDIA
    ZALFP(JLON)=MAX(0.0_JPRB,ZS8(JLON)+ZS9(JLON)-ZS6(JLON)-ZS7(JLON)&
     & -ZS18(JLON))/MAX(ZEPS2,ZS2(JLON))
    KNND(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZALFP(JLON)))&
     & *MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS2-ZS2(JLON))))
    KNND(JLON)=KNND(JLON)*NINT(1.0_JPRB-MAX(0.0_JPRB,SIGN(1.0_JPRB,0.0_JPRB-ZALF(JLON))))
    ZS8S(JLON)=ZS8(JLON)/(ZLHSB(JLON)*MAX(ZEPS4,ZS5S(JLON)))
    ZS8(JLON)=ZS8(JLON)/(ZLHSB(JLON)*MAX(ZEPS3,ZS5(JLON)))
    ZS9(JLON)=ZS9(JLON)/MAX(ZEPS3,ZS5(JLON))
  ENDDO
!*
!     -------------------------------------------------------------------
!     IX - CALCULS CONCERNANT LE VENT INCLUANT L'ENTRAINEMENT ET LE TERME
!     DU GRADIENT DE PRESSION (SCHEMA KERSSHAW&GREGORY), CONSIDERANT LES
!     SEGMENTS  ACTIFS

!          WIND CONCERNING COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERME (KERSSHAW&GREGORY SCHEME), CONSIDERING THE
!     ACTIVE LAYERS
!*
!     --------------------------------
!     IX A -
!            RECOGNIZE ACTIVE LAYERS. COMPUTE THE FINAL VALUE OF
!     THE DETRAINED FRACTION IN ONE TIME-STEP.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZTRAN    : TABLEAU DES TRANSITIONS:
!           1. AU NIVEAU LE PLUS BAS DE CHAQUE SEGMENT ACTIF RECONNU,
!           0. PARTOUT AILLEURS.
!          : TRANSITION ARRAY:
!           1. IF LOWEST LEVEL OF AN ACTIVE ZONE,
!           0. ELSEWHERE.

!     SI IOLD VAUT 0 ON ACCEPTE UNE SEULE ZONE ACTIVE;
!     EN CE CAS, ZTRAN VAUT 1 SEULEMENT AU NIVEAU KLEV.
!     SI KNLAB=0 SUR TOUTE LA HAUTEUR, LES INTEGRALES RESTERONT NULLES.
!     IF IOLD IS 0 A SINGLE ACTIVE ZONE IS ADMITTED;
!     IN THAT CASE, ZTRAN IS 1 ONLY AT LEVEL KLEV.
!     IF KNLAB=0 FOR THE WHOLE VERTICAL PROFILE, THE INTEGRALS WILL REMAIN 0.

  DO JLON=KIDIA,KFDIA

!     REMISE A ZERO DE KNLAB DES QUE SOLUTION CONVECTIVE PAS PHYSIQUE:
!     RESET KNLAB TO ZERO AS SOON AS CONVECTIVE SOLUTION NO LONGER PHYSICAL

    KNLAB(JLON,KLEV)=KNLAB(JLON,KLEV)*KNND(JLON)
    ZTRAN(JLON,KLEV)=MAX(KNLAB(JLON,KLEV),1-IOLD)

  ENDDO

  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA

!    REMISE A ZERO DE KNLAB DES QUE SOLUTION CONVECTIVE PAS PHYSIQUE:
!    RESET KNLAB TO ZERO AS SOON AS CONVECTIVE SOLUTION NO LONGER PHYSICAL

      KNLAB(JLON,JLEV)=KNLAB(JLON,JLEV)*KNND(JLON)
      ITRAN=MAX((KNLAB(JLON,JLEV)-KNLAB(JLON,JLEV+1)),0)
      ZTRAN(JLON,JLEV)=REAL(ITRAN*IOLD,JPRB)
    ENDDO
  ENDDO
!*
!    -------------------------------------------------------------------
!     IX B - BOUCLE ASCENDANTE POUR LES TABLEAUX DE QUANTITE DE MOUVEMENT
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

!           UPWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

! TEMPORAIRES 1D.

! ZBET      : COEFFICIENT BETA POUR LE PROFIL DE QUANTITE DE MOUVEMENT.
!            : BETA COEFFICIENT FOR MOMENTUM PROFILE.

! TEMPORAIRE 0D.

! ZALFS     : "ZALFP" BORNE A UN (CALCUL PSEUDO-IMPLICITE EN U ET V).
!            : "ZALFP" LIMITED TO ONE (PSEUDO IMPLICIT U/V TREATMENT).
! TEMPORAIRES 2D.

! ZA13      : UC0.
! ZA14      : VC0.
! ZUM       : U MOYEN DEPUIS LA BASE DU NUAGE.
!            : AVERAGE U VALUE FROM CLOUD BASE.
! ZVM       : V MOYEN DEPUIS LA BASE DU NUAGE.
!            : AVERAGE V VALUE FROM CLOUD BASE.

!     VARIABLE ZZLAB SERVANT A LA REINITIALISATION DES INTEGRALES:
!     ON OUBLIE L'HISTOIRE (REMISE A ZERO) AU PREMIER NIVEAU INACTIF,
!     AINSI ON NE S'ANNULE JAMAIS SUR UN NIVEAU ACTIF.

!     ZZLAB VARIABLE FOR INTEGRAL REINITIALIZING:
!     ONE FORGETS ABOUT HISTORY (PUT TO ZERO) AT FIRST INACTIVE LEVEL,
!     THUS ONE NEVERS GETS ZERO AT AN ACTIVE LEVEL.

  DO JLON=KIDIA,KFDIA

    ZDPLAB=PDELP(JLON,KLEV)*KNLAB(JLON,KLEV)
    ZZLAB=(1.0_JPRB-ZTRAN(JLON,KLEV))
    ZS3(JLON)=ZDPLAB*(ZFUSC(JLON,KLEV)-PU(JLON,KLEV))
    ZS4(JLON)=ZDPLAB*(ZFVSC(JLON,KLEV)-PV(JLON,KLEV))

!     THE PROBABLE CASE WHERE ZTRAN(KLEV)=1 IS THE OLD SCHEME (IOLD=0)
!     WE SHOULD TAKE THEN
!     ZBET(KLEV)=ZTRAN AND ZUM(KLEV-1)=PU(KLEV), ZVM=PV(KLEV)
!     AS IN THIS CASE TUDGP=0, THE LOOP BELOW PRODUCES
!     ZUM(KLEV-1)= ZUM(KLEV)+ZMIX*0 = ZUM(KLEV) =PU(KLEV) AND IT IS OK.

    ZZALFP=ZALFP(JLON)
    ZALFS=ZZALFP/(1.0_JPRB+ZZALFP)

    ZBET(JLON,KLEV)=1.0_JPRB
    ZUM(JLON,KLEV)=PU(JLON,KLEV)
    ZVM(JLON,KLEV)=PV(JLON,KLEV)

    ZS10(JLON)=ZDPLAB*PU(JLON,KLEV)*ZALFS
    ZS11(JLON)=ZDPLAB*PV(JLON,KLEV)*ZALFS
    ZS12(JLON)=ZDPLAB*ZALFS
    ZS13(JLON)=0.0_JPRB
    ZS14(JLON)=0.0_JPRB
    ZA13(JLON,KLEV)=(-ZS3(JLON)+ZS10(JLON)-ZS13(JLON))&
     & /MAX(ZEPS3, ZS12(JLON))
    ZA14(JLON,KLEV)=(-ZS4(JLON)+ZS11(JLON)-ZS14(JLON))&
     & /MAX(ZEPS3, ZS12(JLON))
  ENDDO

  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA

      ZDPLAB=PDELP(JLON,JLEV)*KNLAB(JLON,JLEV)
      ZZLAB=(1.0_JPRB-ZTRAN(JLON,JLEV))
      ZS3(JLON)=ZS3(JLON)*ZZLAB+ZDPLAB*(ZFUSC(JLON,JLEV)-PU(JLON,JLEV))
      ZS4(JLON)=ZS4(JLON)*ZZLAB+ZDPLAB*(ZFVSC(JLON,JLEV)-PV(JLON,JLEV))

!     SI GCVBETA > 0, IL Y A UN DETRAINEMENT SUPPLEMENTAIRE DE VAPEUR
!     D'EAU PROPORTIONEL AVEC D(MC)/DP.
!     IF GCVBETA > 0, THERE IS AN ADDITIONAL WATER VAPOUR DETRAINMENT
!     PROPORTIONAL TO D(MC)/DP.

      ZZALFP=ZALFP(JLON)+ZALFPB(JLON,JLEV)
      ZALFS=ZZALFP/(1.0_JPRB+ZZALFP)

!     ZBET, ZUM, ZVM SONT UTILISES SEULEMENT DANS LES COUCHES ACTIVES.
!     ZBE EST REMIS A 1 JUSTE SOUS LE PREMIER NIVEAU ACTIF OFFICIEL
!     (SI RENCONTRE DANS CETTE BOUCLE), ET AINSI SA VALEUR
!     ACTIVE DE DEPART EST 1.-ZMIX.

!     ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
!     ZBET RESET TO 1 JUST BELOW THE FIRST OFFICIAL ACTIVE LAYER
!     (IF MET IN THIS LOOP), SO ITS STARTING ACTIVE VALUE IS 1.-ZMIX.

      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= (ZBET(JLON,JLEV+1)*ZZLAB+(1.0_JPRB-ZZLAB))*(1.0_JPRB-ZMIX)

!     ZUM AND ZVM RESET TO PU(JLEV+1), PV AT FIRST ACTIVE LAYER (SO CALLED B-1),
!     I.E. VALUE OF ZUM IN THE LAYER ABOVE LEVEL WHERE ZBET=1 IS PU AT THE
!     LEVEL ZBET=1.
!     ZBET IN THIS LOOP SHOULD NEVER REACH 1, MAX VALUE IS 1-ZMIX(JLEV) <1.

      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
       & +(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
       & +TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
       & / (1.0_JPRB-ZBET(JLON,JLEV)))*ZZLAB&
       & + (1.0_JPRB-ZZLAB)*PU(JLON,JLEV)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
       & +(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
       & +TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
       & / (1.0_JPRB-ZBET(JLON,JLEV)))*ZZLAB&
       & + (1.0_JPRB-ZZLAB)*PV(JLON,JLEV)
      ZBET(JLON,JLEV)=ZBET(JLON,JLEV)*ZZLAB+(1.0_JPRB-ZZLAB)

      ZS10(JLON)=ZS10(JLON)*ZZLAB+ZDPLAB*PU(JLON,JLEV)*ZALFS
      ZS11(JLON)=ZS11(JLON)*ZZLAB+ZDPLAB*PV(JLON,JLEV)*ZALFS
      ZS12(JLON)=ZS12(JLON)*ZZLAB+ZDPLAB*ZBET(JLON,JLEV)*ZALFS
      ZS13(JLON)=ZS13(JLON)*ZZLAB+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))&
       & *ZUM(JLON,JLEV)*ZALFS
      ZS14(JLON)=ZS14(JLON)*ZZLAB+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))&
       & *ZVM(JLON,JLEV)*ZALFS

!     ZA13=UC0 ET ZA14=VC0 EN HAUT,
!     ET CETTE VALEUR DOIT ETRE TRANSMISE VERS LE BAS, VOIR PLUS LOIN.

!     ZA13=UC0 AND ZA14=VC0 AT THE UPPER SIDE,
!     AND HAS TO BE PROPAGATED DOWNWARDS, SEE FARTHER.

      ZA13(JLON,JLEV)=(-ZS3(JLON)+ZS10(JLON)-ZS13(JLON))&
       & /MAX(ZEPS3, ZS12(JLON))
      ZA14(JLON,JLEV)=(-ZS4(JLON)+ZS11(JLON)-ZS14(JLON))&
       & /MAX(ZEPS3, ZS12(JLON))
    ENDDO
  ENDDO
!*
!     ---------------------------------------------------------
!     IX C -  TRANSMISSION VERS LE BAS DE ZA13, ZA14
!     ON GARDE LA VALEUR DE LA PREMIERE COUCHE ACTIVE DU DESSUS.

!             NOW PROPAGATE DOWNWARDS ZA13, ZA14
!     KEEP THE VALUE OF THE FIRST ACTIVE LAYER ABOVE.

  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA

      ZA13(JLON,JLEV)=(1.0_JPRB-ZTRAN(JLON,JLEV-1))*ZA13(JLON,JLEV-1)&
       & + ZTRAN(JLON,JLEV-1)*ZA13(JLON,JLEV)
      ZA14(JLON,JLEV)=(1.0_JPRB-ZTRAN(JLON,JLEV-1))*ZA14(JLON,JLEV-1)&
       & + ZTRAN(JLON,JLEV-1)*ZA14(JLON,JLEV)
    ENDDO
  ENDDO

!*
!     ------------------------------------------------------------------
!     X - CALCUL DES FLUX (ON MODELISE LE FLUX D'ENTHALPIE ASSOCIE AUX
!     PRECIPITATIONS DE MANIERE COHERENTE AVEC L'OPTION CHOISIE POUR LA
!     CHALEUR LATENTE EFFECTIVE -BOUCLES 310 ET 320-).

!          FLUX COMPUTATION (ONE MODELIZES THE ENTHALPY FLUX ASSOCIATED
!     TO FALLING WATER IN A COHERENT WAY WITH THE OPTION CHOSEN FOR THE
!     EFFECTIVE LATENT HEAT -LOOPS 310 AND 320-).

!     FLUX DIFFUSIF D'HUMIDITE ET CHALEUR LATENTE DE FLUX INTERPOLEE.
!     DIFFUSIVE HUMIDITY FLUX AND INTERPOLATED LATENT HEAT OF FLUXES.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZZLHP     : CHALEUR LATENTE FICTIVE DU FLUX DE PRECIPITATION.
!            : FICTITIOUS PRECIPITATION FLUX LATENT HEAT.

  DO JLON=KIDIA,KFDIA
    ZZLHP(JLON,ITOP-1)=-ZLHL(JLON,ITOP-1)-ZSNP(JLON,ITOP-1)&
     & *(ZLHN(JLON,ITOP-1)-ZLHL(JLON,ITOP-1))
  ENDDO
  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
      PDIFCQ(JLON,JLEV)=ZGDTI*ZFORM(JLON,JLEV)*0.5_JPRB&
       & *(ZFQSC(JLON,JLEV)+ZFQSC(JLON,JLEV+1)&
       & -ZQVDN(JLON,JLEV)-ZQVDN(JLON,JLEV+1))
      ZZLHP(JLON,JLEV)=-ZLHL(JLON,JLEV)-ZSNP(JLON,JLEV)&
       & *(ZLHN(JLON,JLEV)-ZLHL(JLON,JLEV))
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZZLHP(JLON,KLEV)=ZLHSB(JLON)
  ENDDO

!     CALCUL FINAL DES FLUX AVEC SECURITE CONTRE LES FLUX DE
!     PRECIPITATION NEGATIFS.
!     FINAL COMPUTATION OF FLUXES WITH SECURITY AGAINST NEGATIVE
!     PRECIPITATION.

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA

      ZZALFP=ZALFP(JLON)+ZALFPB(JLON,JLEV)
      ZALFS=ZZALFP/(1.0_JPRB+ZZALFP)

!     FLUX DIFFUSIFS DE QUANTITE DE MOUVEMENT.
!     DIFFUSIVE MOMENTUM FLUXES.

      PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV-1)&
       & -KNLAB(JLON,JLEV)*ZPOID(JLON,JLEV)&
       & *( ZFUSC(JLON,JLEV)-PU(JLON,JLEV)*(1.0_JPRB+ZALFS)&
       & +ZALFS*( (1.0_JPRB-ZBET(JLON,JLEV))*ZUM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZA13(JLON,JLEV) ) )
      PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV-1)&
       & -KNLAB(JLON,JLEV)*ZPOID(JLON,JLEV)&
       & *( ZFVSC(JLON,JLEV)-PV(JLON,JLEV)*(1.0_JPRB+ZALFS)&
       & +ZALFS*( (1.0_JPRB-ZBET(JLON,JLEV))*ZVM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZA14(JLON,JLEV) ) )
    ENDDO
  ENDDO

  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ZZALFP=ZALFP(JLON)+ZALFPB(JLON,JLEV)
      ZALFS=ZZALFP/(1.0_JPRB+ZZALFP)

!     FLUX D'HUMIDITE (TOTAL ET PRECIPITANTS).
!     HUMIDITY FLUXES (TOTAL AND PRECIPITATING).

      ZSMOOTH=PQSAT(JLON,JLEV)*PAPRSF(JLON,JLEV)**GCVPSIE
      ZFTOTQ=PDIFCQ(JLON,JLEV-1)&
       & +PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1)-KNLAB(JLON,JLEV)&
       & *(ZPOID(JLON,JLEV)*(ZFQSC(JLON,JLEV)-PQ(JLON,JLEV)&
       & +ZZALFP*MAX(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV))&
       & -ZS8S(JLON)*ZSMOOTH*(1.0_JPRB-ZCVPSI))&
       & +ZCVPSI*(PDIFTQ(JLON,JLEV)-PDIFTQ(JLON,JLEV-1)))
      ZFCORQ=MAX(0.0_JPRB,PDIFCQ(JLON,JLEV)-ZFTOTQ)
      PFPLCL(JLON,JLEV)=(ZFCORQ-(PDIFCQ(JLON,JLEV)-ZFTOTQ))*(1.0_JPRB&
       & -ZSNP(JLON,JLEV))
      PFPLCN(JLON,JLEV)=(ZFCORQ-(PDIFCQ(JLON,JLEV)-ZFTOTQ))*ZSNP(JLON,JLEV)

!     FLUX D'ENTHALPIE (TOTAL ET DIFFUSIF).
!     ENTHALPY FLUXES (TOTAL AND DIFFUSIVE).

      ZFTOTS=PDIFCS(JLON,JLEV-1)-ZZLHP(JLON,JLEV-1)&
       & *(PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1))-KNLAB(JLON,JLEV)&
       & *(ZPOID(JLON,JLEV)*(ZFSSC(JLON,JLEV)-ZDSE(JLON,JLEV)&
       & +ZZALFP*MAX(0.0_JPRB,ZSDN(JLON,JLEV)-ZDSE(JLON,JLEV))&
       & -ZS9(JLON)*(1.0_JPRB-GCVPSI))&
       & +GCVPSI*(PDIFTS(JLON,JLEV)-PDIFTS(JLON,JLEV-1)))
      PDIFCS(JLON,JLEV)=ZFTOTS-ZLHSB(JLON)*ZFCORQ+ZZLHP(JLON,JLEV)&
       & *(PFPLCL(JLON,JLEV)+PFPLCN(JLON,JLEV))

!     EVAPORATION DES PRECIPITATIONS DANS LES COUCHES SOUS-NUAGEUSES.
!     EVAPORATION OF PRECIPITATIONS IN SUB-CLOUD LAYERS.

      ZECORQ=(1-KNLAB(JLON,JLEV))*ZALFS*RCVEVAP&
       & *MAX(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV))
      PFPLCL(JLON,JLEV)=MAX(0.0_JPRB,PFPLCL(JLON,JLEV)&
       & -ZPOID(JLON,JLEV)*ZECORQ*(1.0_JPRB-ZSNP(JLON,JLEV)))
      PFPLCN(JLON,JLEV)=MAX(0.0_JPRB,PFPLCN(JLON,JLEV)&
       & -ZPOID(JLON,JLEV)*ZECORQ*ZSNP(JLON,JLEV))
    ENDDO
  ENDDO
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPCOR(JLON,JLEV)=PFPCOR(JLON,JLEV-1)+ZLNN(JLON,JLEV)*&
       & ZPOID(JLON,JLEV)*KNLAB(JLON,JLEV)
      ZFPCORS(JLON)=ZFPCORS(JLON)+ KNLAB(JLON,JLEV) *&
       & (PFPLCL(JLON,JLEV)+PFPLCN(JLON,JLEV)-&
       & PFPLCL(JLON,JLEV-1)-PFPLCN(JLON,JLEV-1))
    ENDDO
  ENDDO

!*
!     ------------------------------------------------------------------
!     XI - APPEL DES DOWNDRAFTS.
!         CALL TO DOWNDRAFT COMPUTATION.

  IF(LCVDD) THEN
    CALL ACCVIMPD(YDML_PHY_MF,KIDIA,KFDIA,KLON,ITOP,KLEV,&
     & KNLAB,&
     & PALPH,PAPHIF,PAPRS,PAPRSF,PCP,&
     & PDELP,PLNPR,PQ,PQW,PR,PRDELP,PT,PTW,PU,PV,&
     & ZATSLC,ZALF,ZDSE,ZENTRD,ZLHE,ZPOID,ZSNP,ZZLHP,ZLHSB,&
     & PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV)
  ENDIF
  IF ( GCVHMIN > 0.0_JPRB ) THEN
     DO JLON=KIDIA,KFDIA
       ZNND  (JLON) = 1.0_JPRB
       ZCVTOP(JLON) = 0.0_JPRB
     ENDDO
     DO JLEV=ITOP,KLEV
       DO JLON=KIDIA,KFDIA
         ZCVTOP(JLON) = ZCVTOP(JLON)&
          & + (PAPHI(JLON,JLEV-1)-PAPHI(JLON,KLEV))*KNLAB(JLON,JLEV)*ZNND(JLON)
         ZNND(JLON) = MIN(ZNND(JLON),1.0_JPRB-KNLAB(JLON,JLEV))
       ENDDO
     ENDDO
     DO JLON=KIDIA,KFDIA
      KNND(JLON) = KNND(JLON)&
          & * MAX(0.0_JPRB,SIGN(1.0_JPRB,ZCVTOP(JLON)-GCVHMIN))
     ENDDO
  ENDIF


!     MISE A ZERO DE TOUS LES RESULTATS EN CAS DE PRECIPITATIONS NULLES
!     EN SURFACE OU DE "KNND" DEJA NUL.
!     SETTING ALL RESULTS TO ZERO IN CASE OF NEGATIVE PRECIPITATIONS AT
!     THE SURFACE OR OF "KNND" ALREADY ZERO.

!     SI SCO > 0,          ON MET A ZERO SI PRECIP. < SCO.
!     SI -1-EPS < SCO < 0, ON NE MET JAMAIS A ZERO.
!     SI SCO < -1-EPS,     ON MET A ZERO SI PRECIP. < -SCO*MIN(QSAT-QN).
!     IF SCO > 0,          SETTING TO ZERO IF PRECIP. < SCO.
!     IF -1-EPS < SCO < 0, NO SETTING TO ZERO.
!     IF SCO < -1-EPS,     SETTING TO ZERO IF PRECIP. < -SCO*MIN(QSAT-QN).

  LLLCL=SCO < -1.0_JPRB-ZEPS6

  DO JLON=KIDIA,KFDIA
    IF(LLLCL) THEN
      ZSCO=-SCO*ZLCL(JLON)
    ELSE
      ZSCO=SCO
    ENDIF
    ZZVAL=PFPLCL(JLON,KLEV)+PFPLCN(JLON,KLEV)-ZSCO
    KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZZVAL))
    ZFPCORK(JLON)=PFPCOR(JLON,KLEV)
  ENDDO

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      ZZVAL  = ABS(RG*PRDELP(JLON,JLEV)/PCP(JLON,JLEV)*&
       &  (PDIFCS (JLON,JLEV-1) - PDIFCS (JLON,JLEV)&
       &  -RLVZER*(PFPLCL (JLON,JLEV-1) - PFPLCL (JLON,JLEV))&
       &  -RLSZER*(PFPLCN (JLON,JLEV-1) - PFPLCN (JLON,JLEV))))
      KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,SIGN(1.0_JPRB,0.02_JPRB-ZZVAL))
    ENDDO
  ENDDO

  IF(LKESC) THEN
    ! KESSler Correction: if Kessler evaporation of actual precipitation flux
    ! generates a virga, and actual precipitation is non-zero at surface, KKND
    ! is set to zero.
    DO JLON=KIDIA,KFDIA
      ZMAXRR(JLON)=0._JPRB ! maximum actual precipitation value.
      ILEVX(JLON)=ITOP ! level at which actual precipitation reaches the maximum.
      ZRACR(JLON)=0._JPRB ! SQRT of precipitation, in the Kessler evaporation approach.
    ENDDO
    ! Determines maximum actual precipitation rate and Kessler evaporation.
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA

        ! ZBIN is 0. if maximum not reached, 1. else case.
        ZBIN=MAX(0._JPRB,SIGN(1._JPRB,PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1)-ZMAXRR(JLON)))
        ILEVX(JLON)=MAX(ILEVX(JLON),(JLEV-1)*NINT(ZBIN))
        ZMAXRR(JLON)=MAX(ZMAXRR(JLON),PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1))

        ! Initializes ZRACR to the SQRT of the precipitation maximum.
        ZRACR(JLON)=(1._JPRB-ZBIN)*ZRACR(JLON)+ZBIN*SQRT(MAX(0._JPRB,GCVGAMMA*ZMAXRR(JLON)))

        ! Applies Kessler evaporation to ZRACR. ZRACR may become negative.
        ZRACR(JLON)=ZRACR(JLON)-EVAP/PAPRSF(JLON,JLEV)**2 &
          & *MAX(0._JPRB,PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*PDELP(JLON,JLEV)
      ENDDO
    ENDDO
    ZEPSRR=2.8E-8_JPRB ! 1000 times less than light drizzle.
    DO JLON=KIDIA,KFDIA
      ! KNND is set to zero if ZRACR < 0. and PFPLCL+PFPLCN > ZEPSRR.
      KNND(JLON)=KNND(JLON)*NINT(1._JPRB-MAX(0._JPRB,SIGN(1._JPRB,-ZRACR(JLON))) &
        & *MAX(0._JPRB,SIGN(1._JPRB,PFPLCL(JLON,KLEV)+PFPLCN(JLON,KLEV)-ZEPSRR)))
    ENDDO
  ENDIF ! LKESC.

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=KNND(JLON)*PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=KNND(JLON)*PFPLCN(JLON,JLEV)
      PDIFCQ(JLON,JLEV)=KNND(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCS(JLON,JLEV)=KNND(JLON)*PDIFCS(JLON,JLEV)
      PSTRCU(JLON,JLEV)=KNND(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=KNND(JLON)*PSTRCV(JLON,JLEV)
      PFPCOR(JLON,JLEV)=KNND(JLON)*PFPCOR(JLON,JLEV)&
       & *ZFPCORS(JLON)/MAX(ZEPS5,ZFPCORK(JLON))
    ENDDO
  ENDDO

!     RETOUR DU PSEUDO-RETURN D'APRES LA BOUCLE 570.
!     RETURN FROM THE PSEUDO-RETURN SITUATED AFTER LOOP 570.

ENDIF

PDIFTQ(:,:)=ZDIFTQ(:,:)
PDIFTS(:,:)=ZDIFTS(:,:)
PFCCQL(:,:)=PFPLCL(:,:)
PFCCQN(:,:)=PFPLCN(:,:)
PFPFPCL(:,:)=PFPLCL(:,:)
PFPFPCN(:,:)=PFPLCN(:,:)
PFPEVPCL(:,:)=0._JPRB
PFPEVPCN(:,:)=0._JPRB

IF(LMUSCLFA) THEN
  ZFORM(:,:)=ZFORM(:,:)/TSPHY/RG
  CALL WRSCMR(NMUSCLFA,'FLUX_MASSE_CV_UD',ZFORM,KLON,KLEV+1)
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCVIMP',1,ZHOOK_HANDLE)
END SUBROUTINE ACCVIMP

