!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCVIMP_V3 ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PCVGQ,&
 & PDELP,PLNPR,PQ,PQW,PR,PRDELP,PT,PTW,PU,PV,&
 ! - INPUT  1D .
 & PTS,PPBLH,&
 ! - OUTPUT 2D .
 & PDIFCQ,PDIFCS,PFCCQL,PFCCQN,PFPLCL,PFPLCN,PFPFPCL,PFPFPCN,&
 & PFPEVPCL,PFPEVPCN,PSTRCU,PSTRCV,&
 & KNLAB,&
 ! - OUTPUT 1D .
 & KNND,&
 ! - INPUT/OUTPUT 2D .
 & PDIFTQ,PDIFTS)

!**** *ACCVIMP_V3 * - SCHEMA DE CONVECTION PROFONDE A FLUX DE MASSE.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX DE PRECIPITATION CONVECTIVE ET DES FLUX DE
!       TRANSPORT "SUB-GRID SCALE" ASSOCIES, PAR UN SCHEMA DE CONVECTION
!       PROFONDE A FLUX DE MASSE .
!     - MASS-FLUX DEEP CONVECTION SCHEME, COMPUTING CONVECTIVE
!       PRECIPITATIONS AND "SUB-GRID SCALE" ASSOCIATED FLUXES .

!     - CALCUL DES FLUX D'ENTHALPIE LIES AUX PRECIPITATIONS CONVECTIVES
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF ENTHALPY FLUXES LINKED TO CONVECT. PRECIPITATIONS
!              (WATER AND SNOW) .

!**   Interface.
!     ----------
!        *CALL* *ACCVIMP_V3*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PCVGQ      : CONVERGENCE D'HUMIDITE (CONDITION DE "KUO").
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (PROGNOSTIQUE) .

! PTS        : TEMPERATURE DE SURFACE.

! - 1D (DIAGNOSTIQUE) .

! PPBLH      : HAUTEUR DE LA CLP (CF ACPBLH).
! PTAUX      : TEMPS CARACTERISTIQUE DE LA CAPE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PDIFCQ     : FLUX CONVECTIF D'HUMIDITE SPECIFIQUE (HORS PLUIE/NEIGE).
! PDIFCS     : FLUX CONVECTIF D'ENTHALPIE (HORS PLUIE/NEIGE).
! PFCCQL     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES LIQUIDES.
! PFCCQN     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES NEIGEUSES.
! PFPFPCL    : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFPFPCN    : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFPEVPCL   : FLUX DE PRECIPITATIONS DU A L'EVAPORATION CONVECTIVE.
! PFPEVPCN   : FLUX DE PRECIPITATIONS DU A LA SUBLIMATION CONVECTIVE.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PSTRCU     : FLUX CONVECTIF DE QUANTITE DE MOUVEMENT "U".
! PSTRCV     : FLUX CONVECTIF DE QUANTITE DE MOUVEMENT "V".

! - 2D (1:KLEV) .

! KNLAB      : INDICE D'ACTIVITE CONVECTIVE (NUAGE SI EGAL A UN).

! - 1D (DIAGNOSTIQUE) .

! KNND       : ZERO SI LA SOLUTION EST NON-PHYSIQUE, UN SINON.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     *ACCLFP*

!     Methode.
!     --------

!     Auteur.
!     -------
!      89-12, J.F. Geleyn.

!     Modifications.
!     --------------
!      2002-01, Created from ACCVIMP (V3) for test in (V4) - P. Marquet
!      2002-02, No call to ACCVIMPD - P. Marquet
!      2002-11, correct use of SIGN for ZLTVL / TENTRVL - P. Marquet
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      05-09, Replace PLM by PPBLH - A. Alias
!      06-02, Call to ACCLP replaced by incore initialisation - A. Alias
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
 & RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 & RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 & RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 & RGAMD  

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVGQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPBLH(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPCL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPCN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPCN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCV(KLON,0:KLEV) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNLAB(KLON,KLEV) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNND(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTS(KLON,0:KLEV) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) ::Z_PTAUX(KLON)
REAL(KIND=JPRB) :: ZBET(KLON,KLEV)&
 & ,ZDSE(KLON,KLEV),ZFORM(KLON,0:KLEV),ZFQSC(KLON,KLEV)&
 & ,ZFSSC(KLON,KLEV),ZFUSC(KLON,KLEV),ZFVSC(KLON,KLEV)&
 & ,ZIPOI(KLON,KLEV),ZLHE(KLON,KLEV),ZLHL(KLON,0:KLEV)&
 & ,ZLHN(KLON,0:KLEV),ZPOID(KLON,KLEV),ZQDN(KLON,KLEV)&
 & ,ZSDN(KLON,KLEV),ZSNP(KLON,0:KLEV)&
 & ,ZUM(KLON,KLEV),ZVM(KLON,KLEV),ZZLHP(KLON,0:KLEV)&
 & ,ZALF(KLON),ZALFP(KLON),ZALFS(KLON),ZCP(KLON),ZFMDQ(KLON)&
 & ,ZFMDS(KLON),ZFMDU(KLON),ZFMDV(KLON),ZHCMH(KLON)&
 & ,ZICVG(KLON),ZLB(KLON),ZLH(KLON),ZLHSB(KLON),ZLN(KLON)&
 & ,ZQB(KLON),ZQN(KLON),ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
 & ,ZS1(KLON),ZS2(KLON),ZS3(KLON),ZS4(KLON),ZS5(KLON)&
 & ,ZS6(KLON),ZS7(KLON),ZS8(KLON),ZS9(KLON),ZS10(KLON)&
 & ,ZS11(KLON),ZS12(KLON),ZS13(KLON),ZS14(KLON)&
 & ,ZS15(KLON),ZS16(KLON)&
 & ,ZTB(KLON),ZTN(KLON)  

INTEGER(KIND=JPIM) :: ISUM, ITOP, JIT, JLEV, JLON

LOGICAL :: LL_LCAPE, LL_LCVEVAP

!LOGICAL :: LCVDD

REAL(KIND=JPRB) ::  ZARLII, ZAUX, ZCPSMD, ZCPVMD, ZCPVMS, ZCPVMW, &
 & ZCPWMD, ZDCP, ZDELQ, ZDELT, ZDELTA, ZDPLAB, &
 & ZDQW, ZECORQ, ZENEN, ZENTR, ZEPS1, ZEPS2, &
 & ZEPS3, ZEPS4, ZEPSO, ZESP, ZEW, ZEXP, ZFCORQ, &
 & ZFMDQL, ZFMDSL, ZFMDUL, ZFMDVL, ZFTOTQ, ZFTOTS, &
 & ZGDT, ZGDTI, ZGZ, ZHCMHL, ZICVGL, ZKUO1, &
 & ZKUO2, ZLIQ, ZLTVL, ZMIX, ZQMEAN, ZQW, ZRVMD, &
 & ZTMEAN, ZZVAL, ZTQ
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCVIMP_V3',0,ZHOOK_HANDLE)
ASSOCIATE(SCO=>YDML_PHY_MF%YRPHY0%SCO, TENTR=>YDML_PHY_MF%YRPHY0%TENTR, ECMNP=>YDML_PHY_MF%YRPHY0%ECMNP, &
 & USDMLT=>YDML_PHY_MF%YRPHY0%USDMLT, TENTRVL=>YDML_PHY_MF%YRPHY0%TENTRVL, TENTRX=>YDML_PHY_MF%YRPHY0%TENTRX, &
 & TRENTRV=>YDML_PHY_MF%YRPHY0%TRENTRV, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NBITER=>YDML_PHY_MF%YRPHY%NBITER, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.

!         AUXILIARY CONSTANTS.
! downdrafts (if .TRUE.)
!     LCVDD=.FALSE.
!     LCVDD=.TRUE.

! if downdraft is .T. LCVEVAP should be .F.
! otherwise it should be .T.
LL_LCVEVAP=.TRUE.
!     LCVEVAP=.FALSE.

! CAPE closure (if .TRUE.)
LL_LCAPE=.FALSE.
!     LCAPE=.TRUE.

DO JLON=KIDIA,KFDIA
  Z_PTAUX(JLON)=10800._JPRB
ENDDO

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD

!*
!     ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES, CONSTANTES DE SECURITE (POUR
!     TROIS DES INTEGRALES VERTICALES DU SCHEMA) ET INVERSE D'UN
!     ARGUMENT LIMITE (POUR EVITER "L'UNDERFLOW" DANS LES CALCULS D'EAU
!     LIQUIDE (OU GLACE) EN SUSPENSION DANS L'ASCENDANCE NUAGEUSE).

!          COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME) AND INVERSE OF A
!     CRITICAL ARGUMENT (TO AVOID "UNDERFLOW" IN LIQUID (OR ICE) WATER
!     CALCULATIONS FOR SUSTENTATION IN CLOUDY ASCENTS).

ZGDT=RG*TSPHY
ZGDTI=1.0_JPRB/ZGDT

ZEPS1=1.E-01_JPRB
ZEPS2=1.E+01_JPRB
ZEPS3=1.E-01_JPRB
ZEPS4=1.E-06_JPRB
ZEPSO=1.E-11_JPRB

ZARLII=0.004_JPRB

ZENEN = (TENTR**0.25_JPRB) * (TENTRX**0.75_JPRB)

!*
!     ------------------------------------------------------------------
!     III - MISE A ZERO DE TOUS LES FLUX (POUR LE CAS SANS CONVECTION)
!     ET CALCULS PRELIMINAIRES D'ENERGIE STATIQUE SECHE, DE CHALEUR
!     LATENTE EFFECTIVE (DEPENDANTE DE L'OPTION PRESSION DE SURFACE
!     VARIANT OU NON AVEC LES FLUX D'HUMIDITE EN SURFACE), D'EPAISSEUR
!     EN PRESSION DES COUCHES DIVISEE PAR G*DT ET DE SON INVERSE POUR
!     CHAQUE NIVEAU AINSI QUE DES PROPORTIONS IMPOSEES DE PRECIPITATIONS
!     NEIGEUSE EN FONCTION DE LA TEMPERATURE ET DE LA PRESSION.

!           SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)
!     AND PRELIMINARY CALCULATIONS OF DRY STATIC ENERGY, OF EFFECTIVE
!     LATENT HEAT (DEPENDING UPON THE OPTION WHETHER SURFACE PRESSURE IS
!     VARYING OR NOT WITH THE SURFACE WATER FLUXES), OF LEVEL PRESSURE
!     THICKNESSES DIVIDED BY G*DT AND OF THEIR INVERSES AT ALL LEVELS AS
!     WELL AS OF IMPOSED PROPORTIONS OF SNOW PRECIPITATION IN FUNCTION
!     OF TEMPERATURE AND PRESSURE.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSNP      : PROPORTION DE PRECIPITATIONS NEIGEUSE.
!            : PROPORTION OF SNOW PRECIPITATION.
! ZLHL      : MOINS LA CHALEUR LATENTE DE CONSERV. D'ENTHALPIE LIQUIDE.
!            : MINUS THE LATENT HEAT OF CONSERVATION OF LIQUID ENTHALPY.
! ZLHN      : MOINS LA CHALEUR LATENTE DE CONSERV. D'ENTHALPIE SOLIDE.
!            : MINUS THE LATENT HEAT OF CONSERVATION OF SOLID ENTHALPY.
! ZPOID     : DP/(RG*DT) POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!            : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
! ZIPOI     : INVERSE DE ZPOID.
!            : INVERSE OF ZPOID.
! ZDSE      : TABLEAU LOCAL DES ENERGIES STATIQUE SECHES.
!            : LOCAL ARRAY FOR DRY STATIC ENERGIES.
! ZLHE      : TABLEAU LOCAL DES CHALEURS LATENTES EFFECTIVES.
!            : LOCAL ARRAY FOR EFFECTIVE LATENT HEATS.

!     LES PROPORTIONS DE FLUX NEIGEUX SONT FIXEES. ATTENTION, UNE VALEUR
!     ZERO POUR LE COEFFICIENT USDMLT NE NOUS DONNE PLUS QUE DES FLUX
!     NEIGEUX QUELQUE SOIT T ; UNE TRANSITION BRUTALE AU POINT TRIPLE
!     SERA OBTENUE POUR DE TRES GRANDES VALEURS DU MEME COEFFICIENT !!!!
!     THE SNOW PARTS OF THE FLUXES ARE SET-UP. BEWARE, A ZERO VALUE FOR
!     THE COEFFICIENT USDMLT WILL RESULT IN ONLY SNOW FLUXES WHATEVER
!     VALUE T MAY TAKE ; A BRUTAL TRANSITION AT THE TREBLE POINT WILL BE
!     OBTAINED FOR VERY BIG VALUES OF THE SAME COEFFICIENT !!!!!!!!!!!!!

DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

  IF (LNEIGE) THEN
    ZSNP(JLON,KTDIA-1)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,PT(JLON,KTDIA)&
     & -RTT)**2/MAX(1.E-03_JPRB,PAPRS(JLON,KTDIA-1)))  
  ELSE
    ZSNP(JLON,KTDIA-1)=0.0_JPRB
  ENDIF

ENDDO
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (LNEIGE) THEN
      ZSNP(JLON,JLEV)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,0.5_JPRB*(PT(JLON,JLEV)&
       & +PT(JLON,JLEV+1))-RTT)**2/PAPRS(JLON,JLEV))  
    ELSE
      ZSNP(JLON,JLEV)=0.0_JPRB
    ENDIF

  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

  IF (LNEIGE) THEN
    ZSNP(JLON,KLEV)=1.0_JPRB-MIN(1.0_JPRB,USDMLT*MAX(0.0_JPRB,PTS(JLON)-RTT)**2&
     & /PAPRS(JLON,KLEV))  
  ELSE
    ZSNP(JLON,KLEV)=0.0_JPRB
  ENDIF

ENDDO

!     INITIALISATION DES TABLEAUX DE CHALEUR LATENTE DE FLUX.
!     "FLUX LATENT HEAT" ARRAYS INITIALIZATION.

DO JLON=KIDIA,KFDIA
  ZTQ = ZCPVMD * PT(JLON,1)
  ZLHL(JLON,0)= -(FOLH(PT(JLON,1),0.0_JPRB)-ZTQ)
  ZLHN(JLON,0)= -(FOLH(PT(JLON,1),1.0_JPRB)-ZTQ)
ENDDO

DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZTMEAN=0.5_JPRB*(PT(JLON,JLEV)+PT(JLON,JLEV+1))
    ZQMEAN=0.5_JPRB*(PQ(JLON,JLEV)+PQ(JLON,JLEV+1))
    ZTQ=ZCPVMD * ZTMEAN 
    ZLHL(JLON,JLEV)= -(FOLH(ZTMEAN,0.0_JPRB)-ZTQ)
    ZLHN(JLON,JLEV)= -(FOLH(ZTMEAN,1.0_JPRB)-ZTQ)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZTQ = ZCPVMD * PTS(JLON) 
  ZLHL(JLON,KLEV)= -(FOLH(PTS(JLON),0.0_JPRB)-ZTQ)
  ZLHN(JLON,KLEV)= -(FOLH(PTS(JLON),1.0_JPRB)-ZTQ)
ENDDO


DO JLON=KIDIA,KFDIA

!     APPEL A LA FONCTION DEFINIE POUR CALCULER LA REFERENCE DE CHALEUR
!     LATENTE EFFECTIVE AU SOL.
!     CALL TO THE DEFINED FUNCTION TO COMPUTE THE REFERENCE OF EFFECTIVE
!     LATENT HEAT AT THE SURFACE.

! - TEMPORAIRE(S) 1D .

! ZLHSB     : CHALEUR LATENTE DE BILAN CALCULEE EN SURFACE.
!            : BUDGET LATENT HEAT AS COMPUTED AT THE SURFACE.

  ZLHSB(JLON)=-ZLHL(JLON,KLEV)-ZSNP(JLON,KLEV)*(ZLHN(JLON,KLEV)&
   & -ZLHL(JLON,KLEV))  

!     MISE A ZERO DES FLUX AU SOMMET.
!     SETTING TO ZERO FLUXES AT THE TOP.

  PFPLCL(JLON,KTDIA-1)=0.0_JPRB
  PFPLCN(JLON,KTDIA-1)=0.0_JPRB
  PDIFCQ(JLON,KTDIA-1)=0.0_JPRB
  PDIFCS(JLON,KTDIA-1)=0.0_JPRB
  PSTRCU(JLON,KTDIA-1)=0.0_JPRB
  PSTRCV(JLON,KTDIA-1)=0.0_JPRB
ENDDO

!     CALCULS A TOUS LES NIVEAUX. ATTENTION LA FONCTION DEFINIE FOLH EST
!     APPELEE AVEC DES VALEURS NON NECESSAIREMENT 0 OU 1 DE SON SECOND
!     ARGUMENT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!     COMPUTATIONS AT ALL LEVELS. BEWARE, THE DEFINED FUNCTION FOLH WILL
!     BE CALLED WITH VALUES THAT ARE NOT NECESSARILY 0 OR 1 FOR ITS
!     SECOND ARGUMENT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    ZDELTA=0.5_JPRB*(ZSNP(JLON,JLEV-1)+ZSNP(JLON,JLEV))
    ZLHE(JLON,JLEV)= FOLH (PT(JLON,JLEV),ZDELTA)+(ZCPWMD+ZDELTA&
     & *(ZCPSMD-ZCPWMD))*(PT(JLON,JLEV)&
     & -PTS(JLON))

!     MISE A ZERO DES FLUX.
!     SETTING FLUXES TO ZERO.

    PFPLCL(JLON,JLEV)=0.0_JPRB
    PFPLCN(JLON,JLEV)=0.0_JPRB
    PDIFCQ(JLON,JLEV)=0.0_JPRB
    PDIFCS(JLON,JLEV)=0.0_JPRB
    PSTRCU(JLON,JLEV)=0.0_JPRB
    PSTRCV(JLON,JLEV)=0.0_JPRB
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     IV - INITIALISATION A LA BASE. LE PROFIL NUAGEUX SERA FINALEMENT
!     CARACTERISE PAR SON HUMIDITE TOTALE ET SON ENERGIE STATIQUE SECHE
!     DE DETRAINEMENT AINSI QUE PAR UN INDICE DE STABILITE (UN AUX DEUX
!     BOUTS DE TOUTE COUCHE CONVECTIVE (DE COUCHE DU MODELE A COUCHE DU
!     MODELE ADJACENTE) ET ZERO AILLEURS) ET UN PROFIL NON ENCORE NORME
!     DU FLUX DE MASSE, TABLEAU DANS LEQUEL SERA ENSUITE RANGE LE
!     PRODUIT DU FLUX DE MASSE PAR LE PAS DE TEMPS (HOMOGENE A UNE
!     PRESSION).

!          BOTTOM INITIALISATION. THE CLOUD PROFILE WILL EVENTUALY BE
!     CHARACTERISED BY ITS TOTAL HUMIDITY AND ITS DETRAINING DRY STATIC
!     ENERGY AS WELL AS BY A STABILITY INDEX (ONE AT BOTH ENDS OF ANY
!     CONVECTIVE SLAB (FROM MODEL LAYER TO THE ADJACENT ONE) AND ZERO
!     ELSEWHERE) AND A YET UNNORMALIZED MASS FLUX PROFILE, ARRAY IN
!     WHICH THE PRODUCT OF THE MASS FLUX BY THE TIME STEP (QUANTITY
!     HOMOGENEOUS TO A PRESSURE) WILL AFTERWARDS BE WRITEN.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM     : PROFIL DE FLUX DE MASSE PUIS FLUX DE MASSE FOIS DT.
!            : PROFILE OF MASS FLUX AND LATER MASS FLUX TIME DT.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSDN      : ENERGIE STATIQUE SECHE DE DETRAINEMENT DU NUAGE.
!            : DETRAINING DRY STATIC ENERGY OF THE CLOUD.
! ZQDN      : HUMIDITE TOTALE DE DETRAINEMENT DU NUAGE.
!            : DETRAINING TOTAL CLOUD HUMIDITY.

DO JLON=KIDIA,KFDIA

!     CARACTERISTIQUES THERMODYNAMIQUES DU NUAGE.
!     THERMODYNAMIC CHARACTERISTICS OF THE CLOUD.

! - TEMPORAIRE(S) 1D .

! ZTN       : TEMPERATURE DE L'ASCENDANCE NUAGEUSE.
!            : TEMPERATURE OF THE CLOUDY ASCENT.
! ZQN       : HUMIDITE SPECIFIQUE VAPEUR DE L'ASCENDANCE NUAGEUSE.
!            : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! ZLN       : HUMIDITE SPECIFIQUE CONDENSEE DE L'ASCENDANCE NUAGEUSE.
!            : CONDENSED SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.

  ZTN(JLON)=PTW(JLON,KLEV)
  ZQN(JLON)=PQW(JLON,KLEV)
  ZLN(JLON)=0.0_JPRB
  ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*ZQN(JLON))*ZTN(JLON)+PAPHIF(JLON,KLEV)
  ZQDN(JLON,KLEV)=ZQN(JLON)

!     CARACTERISTIQUES ACTIVES DU NUAGE.
!     ACTIVE CHARACTERISTICS OF THE CLOUD.

! - TEMPORAIRE(S) 1D .

! ZHCMH     : DIFFERENCE D'ENERGIE STATIQUE HUMIDE NUAGE/ENVIRONEMENT.
!            : DIFFERENCE IN MOIST STATIC ENERGY CLOUD/ENVIRONMENT.
! ZFMDQ     : "0.5*ZFORML" FOIS LE DELTA VERTICAL D'HUMIDITE SPECIFIQUE.
!            : "0.5*ZFORML" TIME THE VERTICAL SPECIFIC HUMIDITY JUMP.
! ZICVG     : L FOIS DELP FOIS LA CONVERGENCE D'HUMIDITE.
!            : L TIME DELP TIME THE HUMIDITY CONVERGENCE.

  KNLAB(JLON,KLEV)=0
  KNND(JLON)=0
  ZHCMH(JLON)=0.0_JPRB
  ZFORM(JLON,KLEV)=0.0_JPRB
  ZFMDQ(JLON)=0.0_JPRB
  ZFMDS(JLON)=0.0_JPRB
  ZICVG(JLON)=PDELP(JLON,KLEV)*PCVGQ(JLON,KLEV)*ZLHE(JLON,KLEV)

! -TEMPORAIRES 2D: VALEUR A LA SURFACE:
! ZBET: COEFFICIENT BETA DE LA VITESSE HORIZONTALE
! ZUM: MOYENNE PONDEREE DE LA VITESSE U DEPUIS LA SURFACE
! ZVM: MOYENNE PONDEREE DE LA VITESSE V DEPUIS LA SURFACE

  ZBET(JLON,KLEV)=1.0_JPRB
  ZUM(JLON,KLEV)=PU(JLON,KLEV)
  ZVM(JLON,KLEV)=PV(JLON,KLEV)

!     INTEGRALES INITIALISEES.
!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D .

! ZS1       : INTEGRALE PONDEREE DE L FOIS LA CONVERGENCE D'HUMIDITE.
!            : WEIGHTED INTEGRAL OF L TIME THE HUMIDITY CONVERGENCE.
! ZS2       : INTEGRALE PONDEREE DE L'EXCES NUAGEUX D'ENTHALPIE HUMIDE.
!            : WEIGHTED INTEGRAL OF THE CLOUD EXCESS IN MOIST ENTHALPY.
! ZS3       : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "U".
!            : WEIGHTED INTEGRAL OF THE "U" WIND TENDENCY.
! ZS4       : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "V".
!            : WEIGHTED INTEGRAL OF THE "V" WIND TENDENCY.
! ZS5       : INTEGRALE PONDEREE DE "1" (ENTR. PUIS DENOMINATEUR).
!            : WEIGHTED INTEGRAL OF "1" (ENTR. THEN DENOMINATOR).
! ZS6       : INTEGRALE PONDEREE DE L FOIS LA TENDANCE DE "Q".
!            : WEIGHTED INTEGRAL OF L TIME THE "Q" TENDENCY.
! ZS7       : INTEGRALE PONDEREE DE LA TENDANCE DE "S=CP*T+PHI".
!            : WEIGHTED INTEGRAL OF THE "S=CP*T+PHI" TENDENCY.
! ZS8       : INTEGRALE PONDEREE DE L FOIS LA TENDANCE "PBL" DE "Q".
!            : WEIGHTED INTEGRAL OF L TIME THE "PBL" "Q" TENDENCY.
! ZS9       : INTEGRALE PONDEREE DE LA TENDANCE "PBL" DE "S=CP*T+PHI".
!            : WEIGHTED INTEGRAL OF THE "PBL" "S=CP*T+PHI" TENDENCY.
! ZS10      : INTEGRALE PONDEREE DU VENT "U" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "U" WIND.
! ZS11      : INTEGRALE PONDEREE DU VENT "V" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "V" WIND.
! ZS12      : INTEGRALE PONDEREE DE BETA
!            : WEIGHTED INTEGRAL OF BETA
! ZS13      : INTEGRALE PONDEREE DE (1-BETA)*UMOYENNE puis K*UC0
!            : WEIGHTED INTEGRAL OF (1-BETA)*Uaverage then K*UC0
! ZS14      : INTEGRALE PONDEREE DE (1-BETA)*Vmoyenne puis K*VC0
!            : WEIGHTED INTEGRAL OF (1-BETA)*Vaverage then K*VC0
! ZS15      : LA CAPE.
!            : CONDITIONAL AVAILABLE POTENTIAL ENERGY
! ZS16      : TRANSPORT VERTICAL DE "S" DANS LA PARTIE CLAIRE
!            CREE PAR LE NUAGE (LCAPE)
!             : VERTICAL TRANSPORT OF "S" IN THE ENVIRONMENT CREATED
!            BY THE CLOUD (LCAPE)

  ZS1(JLON)=0.0_JPRB
  ZS2(JLON)=0.0_JPRB
  ZS3(JLON)=0.0_JPRB
  ZS4(JLON)=0.0_JPRB
  ZS5(JLON)=0.0_JPRB
  ZS6(JLON)=0.0_JPRB
  ZS7(JLON)=0.0_JPRB
  ZS8(JLON)=0.0_JPRB
  ZS9(JLON)=0.0_JPRB
  ZS10(JLON)=0.0_JPRB
  ZS11(JLON)=0.0_JPRB
  ZS12(JLON)=0.0_JPRB
  ZS13(JLON)=0.0_JPRB
  ZS14(JLON)=0.0_JPRB
  ZS15(JLON)=0.0_JPRB
  ZS16(JLON)=0.0_JPRB
ENDDO

!*
!     ------------------------------------------------------------------
!     V - PREMIERE BOUCLE VERTICALE (VERS LE HAUT) INCLUANT LE CALCUL
!     "EN ADIABATIQUE SATUREE" DU PROFIL NUAGEUX.

!         FIRST VERTICAL LOOP (UPWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     INITIALISATION DU PARAMETRE QUI SERVIRA EVENTUELLEMENT A EVITER
!     DES CALCULS INUTILES EN HAUT DE L'ATMOSPHERE ET DEBUT DE LA
!     BOUCLE.
!     INITIALIZATION OF THE PARAMETER THAT SHALL EVENTUALLY HELP
!     AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE AND START OF THE VERTICAL LOOP.

ZLTVL=0.5_JPRB-SIGN(0.5_JPRB,-TENTRVL)
ITOP=KLEV+1
DO JLEV=KLEV-1,KTDIA,-1

!     ADIABATIQUE SATUREE AVEC ENTRAINEMENT ET SUSTENTATION D'EAU
!     LIQUIDE (OU GLACE).
!     SATURATED ADIABAT WITH ENTRAINMENT AND LIQUID (OR ICE) WATER
!     SUSTENTATION.

  DO JLON=KIDIA,KFDIA

!     ENTRAINEMENT.
!     ENTRAINEMENT.

! - TEMPORAIRE(S) 1D .

! ZTB       : "ZTN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB       : "ZQN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZLB       : "ZLN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZLN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
    ZGZ=0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))-PAPHI(JLON,KLEV)
!           FORME LISIBLE ET INEFFICACE:
!         IF(TENTRVL.GT.0.) THEN
!           ZENTR=TENTRVL/MIN(ZGZ,RG*PPBLH(JLON))
!         ELSE
!           ZENTR=TENTR+(TENTRX-TENTR)*EXP(-ZENEN*ZS5(JLON))
!         ENDIF
!           FORME PLUS EFFICACE:
    ZENTR=ZLTVL*TENTRVL/MIN(ZGZ,RG*PPBLH(JLON))+(1-ZLTVL)*&
     & (TENTR+(TENTRX-TENTR)*EXP(-ZENEN*ZS5(JLON)))  
    ZMIX=ZENTR*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
    ZTB(JLON)=ZTN(JLON)+ZMIX*(PT(JLON,JLEV+1)-ZTN(JLON))
    ZQB(JLON)=ZQN(JLON)+ZMIX*(PQ(JLON,JLEV+1)-ZQN(JLON))
    ZLB(JLON)=ZLN(JLON)*(1.0_JPRB-ZMIX)

! ENTRAINEMENT DE LA VITESSE HORIZONTALE:
! NOUVELLE PARAMETRISATION (KERSHAW & GREGORY).
! MOMENTUM ENTRAINMENT:
! NEW PARAMETERIZATION (KERSHAW & GREGORY).

    ZBET(JLON,JLEV)=ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX)
    ZUM(JLON,JLEV)=ZUM(JLON,JLEV+1)&
     & +(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
     & +TRENTRV*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
     & / (1.0_JPRB-ZBET(JLON,JLEV))  
    ZVM(JLON,JLEV)=ZVM(JLON,JLEV+1)&
     & +(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
     & +TRENTRV*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
     & /(1.0_JPRB-ZBET(JLON,JLEV))  

!     COEFFICIENTS DE L'HYDROSTATIQUE DU PROFIL NUAGEUX.
!     HYDROSTATIC COEFFICIENTS FOR THE CLOUD PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB      : POIDS DE "ZTB" DANS L'EPAISSEUR EN GEOPOTENTIEL.
!            : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH      : POIDS DE "ZTN" A "ZQN=ZQB" DANS LA MEME EPAISSEUR.
!            : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH      : POUR LA CORRECTION DE VARIATION DE Q A ZRBH.
!            : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB&
     & -ZLB(JLON))+ZRVMD*ZQB(JLON))  
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     APPEL A LA FONCTION DEFINIE POUR LE CALCUL DE LA PSEUDO CHALEUR
!     LATENTE A LA BASE (PARMI TROIS PARAMETRES CARACTERISTIQUES).
!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH       : VALEUR COURANTE DU PSEUDO LH DURANT LA BOUCLE DE NEWTON.
!            : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP       : COMME ZLH MAIS POUR LA CHALEUR MASSIQUE CP.
!            : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)= FOLH (ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
     & ZCPWMD))&
     & *ZLB(JLON)+ZRBH(JLON)  
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     CHANGEMENT DE NIVEAU POUR OBTENIR LA PREMIERE EBAUCHE, POINT DE
!     DEPART DE LA BOUCLE DE NEWTON.
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ENDDO

!     BOUCLE DE NEWTON.
!     NEWTON'S LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

      IF (LNEIGE) THEN
        ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
      ELSE
        ZDELTA=0.0_JPRB
      ENDIF

!     CALCULS DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (ZTN(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON),ZDELTA))

!     INCREMENTATIONS.
!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQN(JLON)=ZQN(JLON)+ZDELQ
      ZTN(JLON)=ZTN(JLON)+ZDELT
    ENDDO
  ENDDO

!     SUSTENTATION DE L'EAU LIQUIDE (OU GLACE) NUAGEUSE.
!     SUSTENTATION OF LIQUID (OR ICE) WATER.

  DO JLON=KIDIA,KFDIA
    ZLIQ=ECMNP/(ZRBB(JLON)*ZTB(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
     & *(ZQN(JLON)-ZQB(JLON)))*ZTN(JLON))  
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLN(JLON)=MAX(0.0_JPRB,ZLB(JLON)*ZEXP+(ZQN(JLON)-ZQB(JLON))*ZLIQ&
     & *(ZEXP-1.0_JPRB))  
  ENDDO

!     TEST DE STABILITE, RECTIFICATION EVENTUELLE DU NIVEAU INFERIEUR,
!     CALCUL DU PARAMETRE DE PROFIL DU FLUX DE MASSE ET DES DEUX
!     INTEGRALES INTERVENANT DANS LA CONDITION DE FERMETURE "TYPE KUO".
!     STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
!     COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
!     INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

!     TEST DOUBLE DE KUO (FLOTABILITE ET CONVERGENCE D'HUMIDITE).
!     DOUBLE KUO-TYPE TEST (BUOYANCY AND HUMIDITY CONVERGENCE).

    ZKUO1=(RD*(1.0_JPRB-ZLN(JLON))+ZRVMD*ZQN(JLON))*ZTN(JLON)&
     & -PR(JLON,JLEV)*PT(JLON,JLEV)  
    ZICVGL=PDELP(JLON,JLEV)*PCVGQ(JLON,JLEV)*ZLHE(JLON,JLEV)
    ZKUO2=ZS1(JLON)+ZICVGL+ZICVG(JLON)
    KNLAB(JLON,JLEV)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZKUO1)))
    IF (.NOT.LL_LCAPE) THEN
      KNLAB(JLON,JLEV) = KNLAB(JLON,JLEV) *&
       & NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZKUO2)))  
    ENDIF
    KNLAB(JLON,JLEV+1)=MAX(KNLAB(JLON,JLEV+1),KNLAB(JLON,JLEV))

!     EN CAS DE NUAGE PLUS "FROID" QUE L'ENVIRONEMENT ON REEGALISE
!     TEMPERATURE ET HUMIDITE SPECIFIQUES DU NUAGE A CELLES DU
!     THERMOMETRE MOUILLE DE L'ENVIRONEMENT.
!     IN CASE OF A "COLDER" CLOUD THAN THE ENVIRONMENT ONE SETS BACK
!     TEMPERATURE AND SPECIFIC HUMIDITY OF THE CLOUD TO THAT OF THE WET
!     BULB CHARACTERISTICS OF THE ENVIRONMENT.

    ZLN(JLON)=MAX(0.0_JPRB,ZLN(JLON)+ZQN(JLON)-MAX(ZQN(JLON),PQW(JLON,JLEV)))
    ZQN(JLON)=MAX(ZQN(JLON),PQW(JLON,JLEV))
    ZTN(JLON)=MAX(ZTN(JLON),PTW(JLON,JLEV))

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     VALEURS FINALES DE DETRAINEMENT AVEC APPEL A LA FONCTION DEFINIE
!     POUR REVENIR A LA VERITABLE CHALEUR LATENTE.
!     FINAL DETRAINMENT VALUES WITH A CALL TO THE DEFINED FUNCTION TO
!     COME BACK TO THE TRUE LATENT HEAT.

    ZQDN(JLON,JLEV)=ZQN(JLON)+ZLN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQDN(JLON,JLEV))*ZTN(JLON)&
     & - FOLH (ZTN(JLON),ZDELTA)*ZLN(JLON)&
     & +PAPHIF(JLON,JLEV)  

!     PROFIL DE FLUX DE MASSE.
!     MASS FLUX PROFILE.

    ZHCMHL=PCP(JLON,JLEV)*(ZTN(JLON)-PTW(JLON,JLEV))+ZLHE(JLON,&
     & JLEV)&
     & *(ZQN(JLON)-PQW(JLON,JLEV))  
    ZFORM(JLON,JLEV)=KNLAB(JLON,JLEV)&
     & *SQRT(MAX(0.0_JPRB,ZHCMHL+ZHCMH(JLON))&
     & *PAPRS(JLON,JLEV)/(PT(JLON,JLEV)&
     & +PT(JLON,JLEV+1)))  
    ZHCMH(JLON)=ZHCMHL

!     SOMMATIONS.
!     SUMMATIONS.

    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,JLEV+1)*PLNPR(JLON,JLEV+1)&
     & *( ZTB(JLON)*(1.0_JPRB-ZLB(JLON)+RETV*ZQB(JLON))&
     & -  PT(JLON,JLEV+1)*(1.0_JPRB+RETV*PQ(JLON,JLEV+1)) )  
    ZFMDQL=0.5_JPRB*ZFORM(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQ(JLON,JLEV))
    ZFMDSL=0.5_JPRB*ZFORM(JLON,JLEV)*(ZDSE(JLON,JLEV+1)-ZDSE(JLON,JLEV))
    ZS6(JLON)=ZS6(JLON)+KNLAB(JLON,JLEV+1)*(ZFMDQL+ZFMDQ(JLON))&
     & *ZLHE(JLON,JLEV+1)  
    ZS16(JLON)= ZS16(JLON) - KNLAB(JLON,JLEV+1) * (&
     & (1.0_JPRB+RETV*PQ(JLON,JLEV+1))/PCP(JLON,JLEV+1)*&
     & (ZFMDSL+ZFMDS(JLON)) + RETV * PT(JLON,JLEV+1) *&
     & (ZFMDQL+ZFMDQ(JLON)) ) *&
     & PRDELP(JLON,JLEV+1) * PLNPR(JLON,JLEV+1)  
    ZFMDQ(JLON)=ZFMDQL
    ZFMDS(JLON)=ZFMDSL
    ZS1(JLON)=ZS1(JLON)+KNLAB(JLON,JLEV+1)*ZICVG(JLON)
    ZICVG(JLON)=ZICVGL
    ZS5(JLON)=ZS5(JLON)+KNLAB(JLON,JLEV)*(PAPHIF(JLON,JLEV)&
     & -PAPHIF(JLON,JLEV+1))  
  ENDDO

!     VERIFICATION DE LA PRESENCE D'AU MOINS UN NUAGE LE LONG DU VECTEUR
!     "JLON" ET MODIFICATION SI NECESSAIRE DE ITOP.
!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+KNLAB(JLON,JLEV)
  ENDDO

  IF (ISUM /= 0) THEN
    ITOP=JLEV
  ENDIF

ENDDO

!     ABANDON DU CALCUL EN CAS D'ABSENCE DE CONVECTION. LA SEQUENCE SOUS
!     "IF THEN / ENDIF" NE SERA PAS INDENTEE VU SA LONGUEUR ET SON
!     CARACTERE PARTICULIER (PSEUDO "RETURN").
!     QUITTING IN CASE OF NO CONVECTION EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF (ITOP < KLEV+1) THEN

!     SOMMATION AU DERNIER NIVEAU (SOMMET).
!     LAST LEVEL SUMMATION (TOP).

  DO JLON=KIDIA,KFDIA
    ZS6(JLON)=ZS6(JLON)+KNLAB(JLON,KTDIA)*ZFMDQ(JLON)*ZLHE(JLON,KTDIA)
    ZS1(JLON)=ZS1(JLON)+KNLAB(JLON,KTDIA)*ZICVG(JLON)
  ENDDO

!*
!     ------------------------------------------------------------------
!     VI - CONDITION DE FERMETURE ET REMISE A ZERO DE L'INTEGRALE ZS6L.

!          CLOSURE ASSUMPTION AND SET BACK TO ZERO OF THE ZS6L INTEGRAL.

! - TEMPORAIRE(S) 1D .

! ZALF      : FACTEUR DE NORMALISATION POUR "ZFORML" (FERMETURE DE KUO).
!            : NORMALIZATION FACTOR FOR "ZFORML" (KUO CLOSURE).

  DO JLON=KIDIA,KFDIA
    IF (LL_LCAPE) THEN
      ZALF(JLON)=TSPHY*MAX(0.0_JPRB,ZS15(JLON))/MAX(ZEPS4,ZS16(JLON))&
       & *MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS16(JLON)-ZEPS4))&
       & /MAX(Z_PTAUX(JLON),ZEPSO)  
    ELSE
      ZALF(JLON)=(TSPHY*MAX(0.0_JPRB,ZS1(JLON))/MAX(ZEPS1,ZS6(JLON)))&
       & *MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS6(JLON)-ZEPS1))  
    ENDIF
    ZS6(JLON)=0.0_JPRB
    ZS5(JLON)=0.0_JPRB
    ZS15(JLON)=0.0_JPRB
    ZS16(JLON)=0.0_JPRB
  ENDDO

!*
!     ------------------------------------------------------------------
!     VII - DEUXIEME BOUCLE VERTICALE (VERS LE BAS) SERVANT A RESOUDRE
!     L'ALGORITHME IMPLICITE POUR L'ADVECTION DE SUBSIDENCE
!     COMPENSATOIRE ET A EFFECTUER TOUTES LES SOMMATIONS PONDEREES SUR
!     L'EPAISSEUR DU NUAGE. ON STOCKERA EGALEMENT AU PASSAGE LES VALEURS
!     FINALES PROVISOIRES DE L'HUMIDITE, DE L'ENERGIE STATIQUE SECHE ET
!     DES DEUX COMPOSANTES DU VENT.

!           SECOND VERTICAL LOOP (DOWNWARDS) TO SOLVE THE IMPLICIT
!     ALGORITHM FOR COMPENSATING SUBSIDENCE ADVECTION AND TO DO ALL
!     WEIGHTED SUMS ACROSS THE CLOUD DEPTH. ONE WILL AT THE SAME TIME
!     STORE PROVISIONAL FINAL VALUES FOR HUMIDITY, DRY STATIC ENERGY AND
!     FOR THE TWO WIND COMPONENTS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZFQSC     : VALEUR FINALE DE Q (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL Q VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFSSC     : VALEUR FINALE DE S (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL S VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFUSC     : VALEUR FINALE DE U (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL U VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFVSC     : VALEUR FINALE DE V (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL V VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).

!     CAS PARTICULIER DU PREMIER NIVEAU.
!     SPECIAL CASE OF THE FIRST LEVEL.

  DO JLON=KIDIA,KFDIA

!     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
!     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.

    ZFORM(JLON,ITOP)=ZALF(JLON)*ZFORM(JLON,ITOP)

!     PROTECTION CONTRE L'INSTABILITE NON-LINEAIRE QUI PEUT SE
!     SE DECLENCHER OCCASIONELLEMENT MALGRE L'ALGORITHME IMPLICITE.
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.

    ZFORM(JLON,ITOP)=ZFORM(JLON,ITOP)/(1.0_JPRB+PRDELP(JLON,ITOP)&
     & *ZFORM(JLON,ITOP))  

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

! - TEMPORAIRE(S) 1D .

! ZFMDS     : DEMI FLUX DE MASSE FOIS DELTA DE S.
!            : HALF THE MASS FLUX TIME THE JUMP IN S.
! ZFMDU     : DEMI FLUX DE MASSE FOIS DELTA DE U.
!            : HALF THE MASS FLUX TIME THE JUMP IN U.
! ZFMDV     : DEMI FLUX DE MASSE FOIS DELTA DE V.
!            : HALF THE MASS FLUX TIME THE JUMP IN V.

    ZFMDQ(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PQ(JLON,ITOP+1)-PQ(JLON,ITOP))
    ZFMDS(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(ZDSE(JLON,ITOP+1)-ZDSE(JLON,ITOP))
    ZFMDU(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PU(JLON,ITOP+1)-PU(JLON,ITOP))
    ZFMDV(JLON)=0.5_JPRB*ZFORM(JLON,ITOP)*(PV(JLON,ITOP+1)-PV(JLON,ITOP))
    ZFQSC(JLON,ITOP)=PQ(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDQ(JLON)
    ZFSSC(JLON,ITOP)=ZDSE(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDS(JLON)
    ZFUSC(JLON,ITOP)=PU(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDU(JLON)
    ZFVSC(JLON,ITOP)=PV(JLON,ITOP)-PRDELP(JLON,ITOP)*ZFMDV(JLON)

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=KNLAB(JLON,ITOP)*PDELP(JLON,ITOP)
    ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,ITOP)&
     & -ZDSE(JLON,ITOP))+ZLHSB(JLON)&
     & *MAX(0.0_JPRB,ZQDN(JLON,ITOP)-PQ(JLON,ITOP)))  
    ZS3(JLON)=ZS3(JLON)+ZDPLAB*(ZFUSC(JLON,ITOP)-PU(JLON,ITOP))
    ZS4(JLON)=ZS4(JLON)+ZDPLAB*(ZFVSC(JLON,ITOP)-PV(JLON,ITOP))
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,ITOP)-PQ(JLON,ITOP))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,ITOP)-ZDSE(JLON,ITOP))
    ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,ITOP)&
     & *(PDIFTQ(JLON,ITOP-1)-PDIFTQ(JLON,ITOP))  
    ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,ITOP)&
     & *(PDIFTS(JLON,ITOP-1)-PDIFTS(JLON,ITOP))  
    ZS10(JLON)=ZS10(JLON)+ZDPLAB*PU(JLON,ITOP)
    ZS11(JLON)=ZS11(JLON)+ZDPLAB*PV(JLON,ITOP)
! INTEGRALES SUPPLEMENTAIRES POUR UC, VC VARIABLES:
    ZS12(JLON)=ZS12(JLON)+ZDPLAB*ZBET(JLON,ITOP)
    ZS13(JLON)=ZS13(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,ITOP))*ZUM(JLON,ITOP)
    ZS14(JLON)=ZS14(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,ITOP))*ZVM(JLON,ITOP)
  ENDDO

!     BOUCLE VERTICALE.
!     VERTICAL LOOP.

  DO JLEV=ITOP+1,KLEV-1
    DO JLON=KIDIA,KFDIA

!     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
!     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.

      ZFORM(JLON,JLEV)=ZALF(JLON)*ZFORM(JLON,JLEV)

!     PROTECTION CONTRE L'INSTABILITE NON-LINEAIRE QUI PEUT SE
!     SE DECLENCHER OCCASIONELLEMENT MALGRE L'ALGORITHME IMPLICITE.
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.

      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)+(ZFORM(JLON,JLEV)&
       & -ZFORM(JLON,JLEV-1))*(1.0_JPRB+PRDELP(JLON,JLEV)&
       & *ZFORM(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
       & *ABS(ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))))  

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

      ZFMDQL=0.5_JPRB*ZFORM(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQ(JLON,JLEV))
      ZFMDSL=0.5_JPRB*ZFORM(JLON,JLEV)*(ZDSE(JLON,JLEV+1)-ZDSE(JLON,JLEV))
      ZFMDUL=0.5_JPRB*ZFORM(JLON,JLEV)*(PU(JLON,JLEV+1)-PU(JLON,JLEV))
      ZFMDVL=0.5_JPRB*ZFORM(JLON,JLEV)*(PV(JLON,JLEV+1)-PV(JLON,JLEV))
      ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,JLEV)*ZFORM(JLON,JLEV-1))
      ZFQSC(JLON,JLEV)=ZAUX*(PQ(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDQ(JLON)-ZFMDQL+ZFORM(JLON,JLEV-1)&
       & *ZFQSC(JLON,JLEV-1)))  
      ZFSSC(JLON,JLEV)=ZAUX*(ZDSE(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDS(JLON)-ZFMDSL+ZFORM(JLON,JLEV-1)&
       & *ZFSSC(JLON,JLEV-1)))  
      ZFUSC(JLON,JLEV)=ZAUX*(PU(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDU(JLON)-ZFMDUL+ZFORM(JLON,JLEV-1)&
       & *ZFUSC(JLON,JLEV-1)))  
      ZFVSC(JLON,JLEV)=ZAUX*(PV(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDV(JLON)-ZFMDVL+ZFORM(JLON,JLEV-1)&
       & *ZFVSC(JLON,JLEV-1)))  
      ZFMDQ(JLON)=ZFMDQL
      ZFMDS(JLON)=ZFMDSL
      ZFMDU(JLON)=ZFMDUL
      ZFMDV(JLON)=ZFMDVL

!     SOMMATIONS.
!     SUMMATIONS.

      ZDPLAB=KNLAB(JLON,JLEV)*PDELP(JLON,JLEV)
      ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,JLEV)&
       & -ZDSE(JLON,JLEV))+ZLHSB(JLON)&
       & *MAX(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV)))  
      ZS3(JLON)=ZS3(JLON)+ZDPLAB*(ZFUSC(JLON,JLEV)-PU(JLON,JLEV))
      ZS4(JLON)=ZS4(JLON)+ZDPLAB*(ZFVSC(JLON,JLEV)-PV(JLON,JLEV))
      ZS5(JLON)=ZS5(JLON)+ZDPLAB
      ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,JLEV)-PQ(JLON,JLEV))
      ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,JLEV)-ZDSE(JLON,JLEV))
      ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,JLEV)&
       & *(PDIFTQ(JLON,JLEV-1)-PDIFTQ(JLON,JLEV))  
      ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,JLEV)&
       & *(PDIFTS(JLON,JLEV-1)-PDIFTS(JLON,JLEV))  
      ZS10(JLON)=ZS10(JLON)+ZDPLAB*PU(JLON,JLEV)
      ZS11(JLON)=ZS11(JLON)+ZDPLAB*PV(JLON,JLEV)
! INTEGRALES SUPPLEMENTAIRES POUR UC, VC VARIABLES:
      ZS12(JLON)=ZS12(JLON)+ZDPLAB*ZBET(JLON,JLEV)
      ZS13(JLON)=ZS13(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))*ZUM(JLON,JLEV)
      ZS14(JLON)=ZS14(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))*ZVM(JLON,JLEV)
    ENDDO
  ENDDO

!     CAS PARTICULIER DU DERNIER NIVEAU.
!     SPECIAL CASE OF THE LAST LEVEL.

  DO JLON=KIDIA,KFDIA

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

    ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,KLEV)*ZFORM(JLON,KLEV-1))
    ZFQSC(JLON,KLEV)=ZAUX*(PQ(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDQ(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFQSC(JLON,KLEV-1)))  
    ZFSSC(JLON,KLEV)=ZAUX*(ZDSE(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDS(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFSSC(JLON,KLEV-1)))  
    ZFUSC(JLON,KLEV)=ZAUX*(PU(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDU(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFUSC(JLON,KLEV-1)))  
    ZFVSC(JLON,KLEV)=ZAUX*(PV(JLON,KLEV)+PRDELP(JLON,KLEV)&
     & *(ZFMDV(JLON)+ZFORM(JLON,KLEV-1)&
     & *ZFVSC(JLON,KLEV-1)))  

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=KNLAB(JLON,KLEV)*PDELP(JLON,KLEV)
    ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MAX(0.0_JPRB,ZSDN(JLON,KLEV)&
     & -ZDSE(JLON,KLEV))+ZLHSB(JLON)&
     & *MAX(0.0_JPRB,ZQDN(JLON,KLEV)-PQ(JLON,KLEV)))  
    ZS3(JLON)=ZS3(JLON)+ZDPLAB*(ZFUSC(JLON,KLEV)-PU(JLON,KLEV))
    ZS4(JLON)=ZS4(JLON)+ZDPLAB*(ZFVSC(JLON,KLEV)-PV(JLON,KLEV))
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*ZLHSB(JLON)*(ZFQSC(JLON,KLEV)-PQ(JLON,KLEV))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC(JLON,KLEV)-ZDSE(JLON,KLEV))
    ZS8(JLON)=ZS8(JLON)+ZDPLAB*ZLHSB(JLON)*ZIPOI(JLON,KLEV)&
     & *(PDIFTQ(JLON,KLEV-1)-PDIFTQ(JLON,KLEV))  
    ZS9(JLON)=ZS9(JLON)+ZDPLAB*ZIPOI(JLON,KLEV)&
     & *(PDIFTS(JLON,KLEV-1)-PDIFTS(JLON,KLEV))  
    ZS10(JLON)=ZS10(JLON)+ZDPLAB*PU(JLON,KLEV)
    ZS11(JLON)=ZS11(JLON)+ZDPLAB*PV(JLON,KLEV)
! INTEGRALES SUPPLEMENTAIRES POUR UC, VC VARIABLES:
    ZS12(JLON)=ZS12(JLON)+ZDPLAB*ZBET(JLON,KLEV)
    ZS13(JLON)=ZS13(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,KLEV))*ZUM(JLON,KLEV)
    ZS14(JLON)=ZS14(JLON)+ZDPLAB*(1.0_JPRB-ZBET(JLON,KLEV))*ZVM(JLON,KLEV)
  ENDDO

!*
!     ------------------------------------------------------------------
!     VIII - CALCUL DES COEFFICIENTS DU SCHEMA ET NORMALISATION DES
!     INTEGRALES, AVEC DE PLUS DANS LES CAS D'ALIMENTATION OU DE
!     FLOTABILITE INSUFISANTE UNE MISE A ZERO DES EFFETS CONVECTIFS
!     (SEULEMENT EFFECTIVE EN FIN DE ROUTINE).

!            COMPUTATION OF THE COEFFICIENTS OF THE SCHEME AND
!     NORMALIZATION OF THE INTEGRALS, WITH, IN THE CASE OF INSUFFICIENT
!     MOISTURE FEEDING OR BUOYANCY, A RESET TO ZERO OF CONVECTIVE
!     EFECTS (ONLY EFFECTIVE IN FACT AT THE END OF THE SUBROUTINE).

! - TEMPORAIRE(S) 1D .

! ZALFP     : PROPORTION DETRAINEE EN UN PAS DE TEMPS.
!            : DETRAINED FRACTION IN ONE TIME-STEP.
! ZALFS     : "ZALFP" BORNE A UN (CALCUL PSEUDO-IMPLICITE EN U ET V).
!            : "ZALFP" LIMITED TO ONE (PSEUDO IMPLICIT U/V TREATMENT).

  DO JLON=KIDIA,KFDIA
    ZALFP(JLON)=MAX(0.0_JPRB,ZS8(JLON)+ZS9(JLON)-ZS6(JLON)-ZS7(JLON))&
     & /MAX(ZEPS2,ZS2(JLON))  
    ZALFS(JLON)=ZALFP(JLON)/(1.0_JPRB+ZALFP(JLON))
    KNND(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZALFP(JLON)))&
     & *MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS2-ZS2(JLON))))  
    ZS9(JLON)=ZS9(JLON)/MAX(ZEPS3,ZS5(JLON))

! ZS13 CONTIENT ZALFS*UC0, ZS14 CONTIENT ZALFS*VC0:

    ZS13(JLON)=(-ZS3(JLON)+ZALFS(JLON)*(ZS10(JLON)-ZS13(JLON)))&
     & /MAX(ZEPS3, ZS12(JLON))  
    ZS14(JLON)=(-ZS4(JLON)+ZALFS(JLON)*(ZS11(JLON)-ZS14(JLON)))&
     & /MAX(ZEPS3, ZS12(JLON))  
  ENDDO

!*
!     ------------------------------------------------------------------
!     IX - CALCUL DES FLUX (ON MODELISE LE FLUX D'ENTHALPIE ASSOCIE AUX
!     PRECIPITATIONS DE MANIERE COHERENTE AVEC L'OPTION CHOISIE POUR LA
!     CHALEUR LATENTE EFFECTIVE -BOUCLES 310 ET 320-).

!          FLUX COMPUTATION (ONE MODELIZES THE ENTHALPY FLUX ASSOCIATED
!     TO FALLING WATER IN A COHERENT WAY WITH THE OPTION CHOSEN FOR THE
!     EFFECTIVE LATENT HEAT -LOOPS 310 AND 320-).

!     FLUX DIFFUSIF D'HUMIDITE ET CHALEUR LATENTE DE FLUX INTERPOLEE.
!     DIFFUSIVE HUMIDITY FLUX AND INTERPOLATED LATENT HEAT OF FLUXES.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZZLHP     : CHALEUR LATENTE FICTIVE DU FLUX DE PRECIPITATION.
!            : FICTITIOUS PRECIPITATION FLUX LATENT HEAT.

  DO JLON=KIDIA,KFDIA
    ZZLHP(JLON,ITOP-1)=-ZLHL(JLON,ITOP-1)-ZSNP(JLON,ITOP-1)&
     & *(ZLHN(JLON,ITOP-1)-ZLHL(JLON,ITOP-1))  
  ENDDO
  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
      PDIFCQ(JLON,JLEV)=ZGDTI*ZFORM(JLON,JLEV)*0.5_JPRB*(ZFQSC(JLON,&
       & JLEV)&
       & +ZFQSC(JLON,JLEV+1)-ZQDN(JLON,JLEV)&
       & -ZQDN(JLON,JLEV+1))  
      ZZLHP(JLON,JLEV)=-ZLHL(JLON,JLEV)-ZSNP(JLON,JLEV)&
       & *(ZLHN(JLON,JLEV)-ZLHL(JLON,JLEV))  
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZZLHP(JLON,KLEV)=ZLHSB(JLON)
  ENDDO

!     CALCUL FINAL DES FLUX AVEC SECURITE CONTRE LES FLUX DE
!     PRECIPITATION NEGATIFS.
!     FINAL COMPUTATION OF FLUXES WITH SECURITY AGAINST NEGATIVE
!     PRECIPITATION.

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA

!     FLUX DIFFUSIFS DE QUANTITE DE MOUVEMENT.
!     DIFFUSIVE MOMENTUM FLUXES.

      PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV-1)&
       & -KNLAB(JLON,JLEV)*ZPOID(JLON,JLEV)&
       & *( ZFUSC(JLON,JLEV)-PU(JLON,JLEV)*(1.0_JPRB+ZALFS(JLON))&
       & +ZALFS(JLON)*(1.0_JPRB-ZBET(JLON,JLEV))*ZUM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZS13(JLON) )  
      PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV-1)&
       & -KNLAB(JLON,JLEV)*ZPOID(JLON,JLEV)&
       & *( ZFVSC(JLON,JLEV)-PV(JLON,JLEV)*(1.0_JPRB+ZALFS(JLON))&
       & +ZALFS(JLON)*(1.0_JPRB-ZBET(JLON,JLEV))*ZVM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZS14(JLON) )  

!     FLUX D'HUMIDITE (TOTAL ET PRECIPITANTS).
!     HUMIDITY FLUXES (TOTAL AND PRECIPITATING).

      ZFTOTQ=PDIFCQ(JLON,JLEV-1)+PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,&
       & JLEV-1)&
       & -KNLAB(JLON,JLEV)*(ZPOID(JLON,JLEV)*(ZFQSC(JLON,JLEV)&
       & -PQ(JLON,JLEV)+ZALFP(JLON)*MAX(0.0_JPRB,ZQDN(JLON,JLEV)&
       & -PQ(JLON,JLEV)))+PDIFTQ(JLON,JLEV)-PDIFTQ(JLON,JLEV-1))  
      ZFCORQ=MAX(0.0_JPRB,PDIFCQ(JLON,JLEV)-ZFTOTQ)
      PFPLCL(JLON,JLEV)=(ZFCORQ-(PDIFCQ(JLON,JLEV)-ZFTOTQ))*(1.0_JPRB&
       & -ZSNP(JLON,JLEV))  
      PFPLCN(JLON,JLEV)=(ZFCORQ-(PDIFCQ(JLON,JLEV)-ZFTOTQ))*ZSNP(JLON,JLEV)

!     FLUX D'ENTHALPIE (TOTAL ET DIFFUSIF).
!     ENTHALPY FLUXES (TOTAL AND DIFFUSIVE).

      ZFTOTS=PDIFCS(JLON,JLEV-1)-ZZLHP(JLON,JLEV-1)&
       & *(PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1))-KNLAB(JLON,JLEV)&
       & *ZPOID(JLON,JLEV)*(ZFSSC(JLON,JLEV)-ZDSE(JLON,JLEV)&
       & +ZALFP(JLON)*MAX(0.0_JPRB,ZSDN(JLON,JLEV)-ZDSE(JLON,JLEV))&
       & -ZS9(JLON))  
      PDIFCS(JLON,JLEV)=ZFTOTS-ZLHSB(JLON)*ZFCORQ+ZZLHP(JLON,JLEV)&
       & *(PFPLCL(JLON,JLEV)+PFPLCN(JLON,JLEV))  

!     EVAPORATION DES PRECIPITATIONS DANS LES COUCHES SOUS-NUAGEUSES.
!     EVAPORATION OF PRECIPITATIONS IN SUB-CLOUD LAYERS.

      IF(LL_LCVEVAP) THEN
        ZECORQ=(1-KNLAB(JLON,JLEV))*ZALFS(JLON)&
         & *MAX(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV))  
        PFPLCL(JLON,JLEV)=MAX(0.0_JPRB,PFPLCL(JLON,JLEV)&
         & -ZPOID(JLON,JLEV)*ZECORQ*(1.0_JPRB-ZSNP(JLON,JLEV)))  
        PFPLCN(JLON,JLEV)=MAX(0.0_JPRB,PFPLCN(JLON,JLEV)&
         & -ZPOID(JLON,JLEV)*ZECORQ*ZSNP(JLON,JLEV))  
      ENDIF
    ENDDO
  ENDDO

!     MISE A ZERO DE TOUS LES RESULTATS EN CAS DE PRECIPITATIONS NULLES
!     EN SURFACE OU DE "KNND" DEJA NUL (VOIR BOUCLE 810).
!     SETTING ALL RESULTS TO ZERO IN CASE OF NEGATIVE PRECIPITATIONS AT
!     THE SURFACE OR OF "KNND" ALREADY ZERO (SEE LOOP 810).

  DO JLON=KIDIA,KFDIA
    ZZVAL=PFPLCL(JLON,KLEV)+PFPLCN(JLON,KLEV)-SCO
    KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,-SIGN(1.0_JPRB,-(ZZVAL)))
  ENDDO
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=KNND(JLON)*PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=KNND(JLON)*PFPLCN(JLON,JLEV)
      PDIFCQ(JLON,JLEV)=KNND(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCS(JLON,JLEV)=KNND(JLON)*PDIFCS(JLON,JLEV)
      PSTRCU(JLON,JLEV)=KNND(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=KNND(JLON)*PSTRCV(JLON,JLEV)
    ENDDO
  ENDDO

!*
!     ------------------------------------------------------------------
!     X - APPEL DES DOWNDRAFTS.
!         CALL TO DOWNDRAFT COMPUTATION.

! IF(LCVDD) THEN
!   CALL ACCVIMPD_V3( KIDIA,KFDIA,KLON,ITOP,KLEV,&
!    &KNLAB,&
!    &PALPH,PAPHIF,PAPRS,PAPRSF,PCP,&
!    &PDELP,PLNPR,PQ,PQW,PR,PRDELP,PT,PTW,PU,PV,&
!    &ZDSE,ZLHE,ZPOID,ZSNP,ZZLHP,ZLHSB,&
!    &PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV,&
!    &'ACCVIMPD')
! ENDIF

!*

!     *** ATTENTION : CE GROUPE "X" PEUT ETRE CONSIDERE COMME UNE SORTE
!     DE ROUTINE INDEPENDANTE ET N'EST INCLUS DANS ACCVIMP QUE POUR DES
!     RAISONS DE FACILITE.

!     *** WARNING : THIS "X" GROUP CAN BE CONSIDERED AS A KIND OF
!     INDEPENDENT SUBROUTINE AND IS ONLY INCLUDED IN ACCVIMP FOR
!     CONVENIENCY.

!     RETOUR DU PSEUDO-RETURN D'APRES LA BOUCLE 570.
!     RETURN FROM THE PSEUDO-RETURN SITUATED AFTER LOOP 570.

ENDIF

PFCCQL(:,:)=PFPLCL(:,:)
PFCCQN(:,:)=PFPLCN(:,:)
PFPFPCL(:,:)=PFPLCL(:,:)
PFPFPCN(:,:)=PFPLCN(:,:)
PFPEVPCL(:,:)=0._JPRB
PFPEVPCN(:,:)=0._JPRB

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCVIMP_V3',1,ZHOOK_HANDLE)
END SUBROUTINE ACCVIMP_V3
