!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCVIMPD( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 ! -----------------------------------------------------------------------
 ! - INPUT  2D .
 & KNLAB,&
 & PALPH,PAPHIF,PAPRS,PAPRSF,PCP,&
 & PDELP,PLNPR,PQ,PQW,PR,PRDELP,PT,PTW,PU,PV,&
 & PZATSLC,PALF,PZDSE,PZENTR,PZLHE,PZPOID,PZSNP,PZZLHP,&
 & PZLHSB,&
 ! - INPUT/OUTPUT 2D .
 & PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV)

! **** *ACCVIMPD * - SCHEMA DE DE COURENTS DESCENDANTS EXTERNS ASSOCIES
! A LA CONVECTION PROFONDE.

! Sujet.
! ------
!     - ROUTINE DE CALCUL ACTIF SUIVANT LA MEME ORGANIZATION DE LA
!       ROUTINE DE CONVECTION (C'EST UN "MIROIR" D'ACCVIMP)
!       CALCUL DE CHANGEMENT DES FLUX DE PRECIPITATION CONVECTIVE ET DES
!       FLUX DE TRANSPORT "SUB-GRID SCALE" ASSOCIES, PAR UN SCHEMA DE COURANTS
!       DESCENDANTS A FLUX DE MASSE .
!     - ACTIVE SUBROUTINE  FOLLOWING THE SAME ORGANIZATION AS ACCVIMP.
!       (THE ROUTINE IS A "MIRROR" OF ACCVIMP)
!       MASS-FLUX  EXTERNAL CONVECTIVE DOWNDDRAFT SCHEME, COMPUTING THE CHANGING
!       OF CONVECTIVE PRECIPITATIONS AND "SUB-GRID SCALE" ASSOCIATED FLUXES .

!     - CALCUL DES FLUX D'ENTHALPIE LIES A L'EVAPORATION DES PRECIPITATIONS
!       CONVECTIVES  (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF ENTHALPY FLUXES LINKED TO THE EVAPORATION OF CONVECTIVE
!       PRECIPITATIONS (WATER AND SNOW) .

! **   Interface.
! ----------
! *CALL* *ACCVIMPD*

! -----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
! "APLPAR" CODE.
! -----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
! -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (NIVEL DU SOMMET
!            : DU NUAGE).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
! CATEGORIE).

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PZSNP      : PROPORTION DE PRECIPITATIONS NEIGEUSE.
!            : PROPORTION OF SNOW PRECIPITATION.
! PZZLHP     : CHALEUR LATENTE FICTIVE DU FLUX DE PRECIPITATION.
!            : FICTITIOUS PRECIPITATION FLUX LATENT HEAT.

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PZATSLC    : FACTEUR D'ATTENUATION POUR LA CONVECTION EN PENTE.
!            : ATTENUATION FACTOR FOR SLANTWISE CONVECTION.
! PALF       : FACTEUR DE NORMALISATION.
! PZDSE      : TABLEAU DES ENERGIES STATIQUE SECHES.
!            : ARRAY FOR DRY STATIC ENERGIES.
! PZLHE      : TABLEAU DES CHALEURS LATENTES EFFECTIVES.
!            : ARRAY FOR EFFECTIVE LATENT HEATS.
! PZPOID     : DP/(RG*DT) POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!            : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.

! - 1D

! PZENTR     : TABLEAU DES TAUX D'ENTRAINEMENT.
!            : ARRAY FOR ENTRAINMENT RATES.
! PZLHSB     : CHALEUR LATENTE DE BILAN CALCULEE EN SURFACE.
!            : BUDGET LATENT HEAT AS COMPUTED AT THE SURFACE.

! -----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
! ---------------------------

! - 2D (0:KLEV) .

! PDIFCQ     : FLUX CONVECTIF FINAL D'HUMIDITE SPECIFIQUE (HORS PLUIE/NEIGE).
!            : (D'ABORD FLUX DU AU COURANT ASCENDENT)
! PDIFCS     : FLUX CONVECTIF FINAL D'ENTHALPIE (HORS PLUIE/NEIGE).
!            : (D'ABORD FLUX DU AU COURANT ASCENDENT)
! PFPLCL     : PRECIPITATIONS CONVECTIVES FINALLE SOUS FORME LIQUIDE.
!            : (D'ABORD  PRECIPITATIONS DUES  AU COURANT ASCENDENT)
! PFPLCN     : PRECIPITATIONS CONVECTIVES FINALLES SOUS FORME NEIGE.
!            : (D'ABORD  PRECIPITATIONS DUES  AU COURANT ASCENDENT)
! PSTRCU     : FLUX CONVECTIF FINAL DE QUANTITE DE MOUVEMENT "U".
!            : (D'ABORD FLUX DU AU COURANT ASCENDENT)
! PSTRCV     : FLUX CONVECTIF FINAL DE QUANTITE DE MOUVEMENT "V".
!            : (D'ABORD FLUX DU AU COURANT ASCENDENT)

! - 2D (1:KLEV) .

! KNLAB      : INDICE D'ACTIVITE CONVECTIVE (NUAGE SI EGAL A UN).

! -----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
! ---------------------

! -----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!     97-05, D. Banciu, J.-F. Geleyn

!     Modifications.
!     --------------
!     2001-11, INLAB correction in the case of clouds without RR 
!            (ZNLAB) - E. Bazile.
!     2002-04, Security ZEPS1 increased. - E. Bazile / J.M. Piriou.
!     2002-04, More security on INND, for symmetry with that of ACCVIMP - E. Bazile.
!     2002-04, Security to avoid the increase of RR without clouds in DD (ZFPD) - E. Bazile.
!     2002-05, Downdraft intensity limited to that of the updraft. - J.M. Piriou.
!     2002-11, Correction to assure the stability of the the implicit algorithm
!              for compesanting subsidence computation. -J.-F. Geleyn/D. Banciu
!     M.Hamrud      01-Oct-2003 CY28 Cleaning
!     K. Yessad (Jul 2009): remove CDLOCK + some cleanings
! -----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
 & RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 & RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 & RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 & RGAMD  
USE YOMLSFORC, ONLY : LMUSCLFA,NMUSCLFA

! -----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(INOUT) :: KNLAB(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZATSLC(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZDSE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZENTR(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZLHE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZPOID(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZSNP(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZZLHP(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PZLHSB(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCV(KLON,0:KLEV) 

! -----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: INLAB(KLON,KLEV),INND(KLON)

! 2D LOCALS

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV),ZBET(KLON,KLEV)&
 & ,ZDIFCQD(KLON,0:KLEV),ZDIFCSD(KLON,0:KLEV)&
 & ,ZFMOD(KLON,0:KLEV),ZFORM(KLON,0:KLEV)&
 & ,ZFPLCLD(KLON,0:KLEV),ZFPLCND(KLON,0:KLEV)&
 & ,ZFQSC1(KLON,KLEV),ZFSSC1(KLON,KLEV),ZFUSC1(KLON,KLEV)&
 & ,ZFVSC1(KLON,KLEV),ZQDN(KLON,KLEV)&
 & ,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV)&
 & ,ZSTRCUD(KLON,0:KLEV),ZSTRCVD(KLON,0:KLEV)&
 & ,ZTRAN(KLON,KLEV),ZUM(KLON,KLEV),ZVM(KLON,KLEV)&
 & ,ZNLAB(KLON,KLEV),ZFPD(KLON,0:KLEV)  

! 1D LOCALS

REAL(KIND=JPRB) :: ZALF(KLON),ZALFP(KLON),ZALFS(KLON),ZCP(KLON)&
 & ,ZDVGP1(KLON),ZFMDQ1(KLON),ZFMDS1(KLON),ZFMDU1(KLON)&
 & ,ZFMDV1(KLON),ZHCMH1(KLON)&
 & ,ZLH(KLON),ZQB(KLON),ZQN(KLON)&
 & ,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
 & ,ZS1(KLON),ZS2(KLON),ZS3(KLON),ZS4(KLON),ZS5(KLON)&
 & ,ZS6(KLON),ZS7(KLON),ZS10(KLON),ZS11(KLON)&
 & ,ZS12(KLON),ZS13(KLON),ZS14(KLON)&
 & ,ZTB(KLON),ZTN(KLON)  

INTEGER(KIND=JPIM) :: IOLD, ISUM, ITOP, ITRAN, JIT, JLEV, JLON

REAL(KIND=JPRB) :: ZAUX, ZCPVMD, ZCPVMS, ZCPVMW, &
 & ZDCP, ZDELQ, ZDELT, ZDELTA, ZDPLAB, ZDQW, &
 & ZDVGP, ZEPS1, ZEPS2, ZEPS3, ZESP, ZEW, ZFCORQ, &
 & ZFCORQ1, ZFCORQ2, ZFCORQ3, ZFMDQ, ZFMDS, ZFMDU, ZFMDV, ZFTOTQ, &
 & ZFTOTS, ZGDT, ZGDTI, ZHCMH, ZKUO1, ZKUO2, &
 & ZLAB, ZMIX, ZQW, ZRVMD, ZTEST, ZZLAB, ZZFPD, ZFFSLC, ZMIXCIN
REAL(KIND=JPRB) :: ZHOOK_HANDLE

! -----------------------------------------------------------------------

#include "wrscmr.intfb.h"

#include "fcttrm.func.h"

! -----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCVIMPD',0,ZHOOK_HANDLE)
ASSOCIATE(GDDEVA=>YDML_PHY_MF%YRPHY0%GDDEVA, RCIN=>YDML_PHY_MF%YRPHY0%RCIN, GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, &
 & GDDSDE=>YDML_PHY_MF%YRPHY0%GDDSDE, TDDGP=>YDML_PHY_MF%YRPHY0%TDDGP, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, NBITER=>YDML_PHY_MF%YRPHY%NBITER, LCVCAS=>YDML_PHY_MF%YRPHY%LCVCAS, &
 & LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE)
! -----------------------------------------------------------------------

! *
! ------------------------------------------------------------------

!     I - CONSTANTES AUXILIAIRES.

!         AUXILIARY CONSTANTS.

! POUR RETROUVER L'ANCIEN SCHEMA SANS SEGMENTS ACTIFS :
! TO REOBTAIN OLD SCHEME WITHOUT ACTIVE SEGMENTS :
IOLD=0
IF (LCVCAS) IOLD=1

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS

ZFORM(:,:)=0.

! *
! ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES, CONSTANTES DE SECURITE (POUR
!     TROIS DES INTEGRALES VERTICALES DU SCHEMA).

!     COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME).

ZGDT=RG*TSPHY
ZGDTI=1.0_JPRB/ZGDT

ZEPS1=1.E+03_JPRB
ZEPS2=1.E+01_JPRB
ZEPS3=1.E-01_JPRB

ZFPD(:,:)=0.0_JPRB
ZNLAB(:,:) = 0.0_JPRB

! *
! ------------------------------------------------------------------
!     III - MISE A ZERO DE TOUS LES FLUX (POUR LE CAS SANS CONVECTION)

!           SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZDIFCQD    : FLUX D'HUMIDITE SPECIFIQUE DU AU COURANT DESCENDANT
!            : (HORS D'EVAPORATION PLUIE/NEIGE).
! ZDIFCSD    : FLUX D'ENTHALPIE DU AU COURANT DESCENDANT
!            : (HORS D'EVAPORATION PLUIE/NEIGE).
! ZFPLCLD    : MOINS FLUX D'EVAPORATIONS DES PRECIPITATIONS CONVECTIVES
!            : SOUS FORME LIQUIDE.
! ZFPLCND    : MOINS FLUX D'EVAPORATIONS DES PRECIPITATIONS CONVECTIVES
!            : SOUS FORME NEIGE.
! ZSTRCUD    : FLUX DE QUANTITE DE MOUVEMENT "U" DU AU COURANT DESCENDANT.
! ZSTRCVD    : FLUX DE QUANTITE DE MOUVEMENT "V" DU AU COURANT DESCENDANT.

! ZFMOD      : FOCTION DE LA VITESE DU COURANT DESCENDANT.

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA

!      MISE A ZERO DES FLUX.
!      SETTING FLUXES TO ZERO.

    ZFPLCLD(JLON,JLEV)=0.0_JPRB
    ZFPLCND(JLON,JLEV)=0.0_JPRB
    ZDIFCQD(JLON,JLEV)=0.0_JPRB
    ZDIFCSD(JLON,JLEV)=0.0_JPRB
    ZSTRCUD(JLON,JLEV)=0.0_JPRB
    ZSTRCVD(JLON,JLEV)=0.0_JPRB

!      MISE A UN DE LA FONCTION DE MODULATION.
!      SETTING MODULATION FUNCTION TO ONE.

    ZFMOD(JLON,JLEV)=1.0_JPRB

  ENDDO
ENDDO

! *
! ------------------------------------------------------------------
!     IV - INITIALISATION EN HAUT. LE PROFIL DU COURANT DESCENDANT
!     SERA FINALEMENT CARACTERISE PAR SON HUMIDITE TOTALE ET SON ENERGIE
!     STATIQUE SECHE DE DETRAINEMENT AINSI QUE PAR UN INDICE DE STABILITE
!     (UN AUX DEUX BOUTS DE TOUTE COUCHE (DE COUCHE DU MODELE A COUCHE DU
!     MODELE ADJACENTE) ET ZERO AILLEURS) ET UN PROFIL NON ENCORE NORME
!     DU FLUX DE MASSE, TABLEAU DANS LEQUEL SERA ENSUITE RANGE LE
!     PRODUIT DU FLUX DE MASSE PAR LE PAS DE TEMPS (HOMOGENE A UNE
!     PRESSION).

!     TOP INITIALISATION. THE DOWNDRAFT PROFILE WILL EVENTUALY BE
!     CHARACTERISED BY ITS TOTAL HUMIDITY AND ITS DETRAINING DRY STATIC
!     ENERGY AS WELL AS BY A STABILITY INDEX (ONE AT BOTH ENDS OF ANY
!     SLAB (FROM MODEL LAYER TO THE ADJACENT ONE) AND ZERO
!     ELSEWHERE) AND A YET UNNORMALIZED MASS FLUX PROFILE, ARRAY IN
!     WHICH THE PRODUCT OF THE MASS FLUX BY THE TIME STEP (QUANTITY
!     HOMOGENEOUS TO A PRESSURE) WILL AFTERWARDS BE WRITEN.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM     : MOINS PROFIL DE FLUX DE MASSE PUIS MOINS FLUX DE MASSE FOIS DT.
!           : MINUS PROFILE OF MASS FLUX AND LATER MINUS MASS FLUX TIME DT.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSDN       : ENERGIE STATIQUE SECHE DE DETRAINEMENT DU COURANT DESCENDANT.
!            : DETRAINING DRY STATIC ENERGY OF THE DOWNDRAFT.
! ZQDN       : HUMIDITE DE DETRAINEMENT DU COURANT DESCENDANT.
!            : DETRAINING DOWNDRAFT HUMIDITY.

! - TEMPORAIRE  1D

! INND       : ZERO SI LA SOLUTION EST NON-PHYSIQUE, UN SINON.

DO JLON=KIDIA,KFDIA

!     CARACTERISTIQUES THERMODYNAMIQUES DU COURANT DESCENDANT.
!     THERMODYNAMIC CHARACTERISTICS OF THE DOWNDRAFT.

! - TEMPORAIRE(S) 1D .

! ZTN        : TEMPERATURE DU COURANT DESCENDANT.
!            : TEMPERATURE OF THE DOWNDRAFT.
! ZQN        : HUMIDITE SPECIFIQUE VAPEUR DU COURANT DESCENDANT.
!            : WATER VAPOUR SPECIFIC HUMIDITY OF THE DOWNDRAFT.

  ZTN(JLON)= MIN(PT(JLON,KTDIA),PTW(JLON,KTDIA))
  ZQN(JLON)= MAX(PQ(JLON,KTDIA),PQW(JLON,KTDIA))
  ZSDN(JLON,KTDIA)=(RCPD+ZCPVMD*ZQN(JLON))*ZTN(JLON)+PAPHIF(JLON,KTDIA)
  ZQDN(JLON,KTDIA)=ZQN(JLON)

!      CARACTERISTIQUES ACTIVES DU COURANT DESCENDANT.
! ACTIVE CHARACTERISTICS OF THE DOWNDRAFT.

! - TEMPORAIRE(S) 1D .

! ZHCMH1     : DIFFERENCE D'ENERGIE STATIQUE HUMIDE COURANT
!            : DESCENDANT/ENVIRONEMENT.
!            : DIFFERENCE IN MOIST STATIC ENERGY DOWNDRAFT/ENVIRONMENT.
! ZFMDQ1     : "0.5*ZFORM" FOIS LE DELTA VERTICAL D'HUMIDITE SPECIFIQUE.
!            : "0.5*ZFORM" TIME THE VERTICAL SPECIFIC HUMIDITY JUMP.
! ZDVGP1     : L FOIS RG FOIS LA DIVERGENCE DU FLUX DE PRECIPITATION
!            : L TIME RG TIME THE PRECIPITATION FLUX DIVERGENCE

  INLAB(JLON,KTDIA)=0
  INND(JLON)=0
  ZHCMH1(JLON)=0.0_JPRB
  ZFORM(JLON,KTDIA-1)=0.0_JPRB
  ZFMDQ1(JLON)=0.0_JPRB

  ZFORM(JLON,KLEV)=0.0_JPRB

  ZDVGP1(JLON)=RG*(PFPLCL(JLON,KTDIA-1)+PFPLCN(JLON,KTDIA-1)-&
   & PFPLCL(JLON,KTDIA)-PFPLCN(JLON,KTDIA))*PZLHE(JLON,KTDIA)  

!     INTEGRALES INITIALISEES.
!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D .

! ZS1        : INTEGRALE PONDEREE DE L FOIS LA DIVERGENCE DE FLUX DE
!            : PRECIPITATION CONVECTIVE.
!            : WEIGHTED INTEGRAL OF L TIME THE CONVECTIVE PRECIPITATION
!            : FLUX DIVERGENCE
! ZS2        : INTEGRALE PONDEREE DE L'EXCES D'ENTHALPIE HUMIDE DU
!            : COURANT DESCENDANT.
!            : WEIGHTED INTEGRAL OF THE DOWNDRAFT EXCESS IN MOIST ENTHALPY.
! ZS3        : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "U".
!            : WEIGHTED INTEGRAL OF THE "U" WIND TENDENCY.
! ZS4        : INTEGRALE PONDEREE DE LA TENDANCE EN VENT "V".
!            : WEIGHTED INTEGRAL OF THE "V" WIND TENDENCY.
! ZS5        : INTEGRALE PONDEREE DE "1" (ENTR. PUIS DENOMINATEUR).
!            : WEIGHTED INTEGRAL OF "1" (ENTR. THEN DENOMINATOR).
! ZS6        : INTEGRALE PONDEREE DE L FOIS LA TENDANCE DE "Q".
!            : WEIGHTED INTEGRAL OF L TIME THE "Q" TENDENCY.
! ZS7        : INTEGRALE PONDEREE DE LA TENDANCE DE "S=CP*T+PHI".
!            : WEIGHTED INTEGRAL OF THE "S=CP*T+PHI" TENDENCY.
! ZS10       : INTEGRALE PONDEREE DU VENT "U" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "U" WIND.
! ZS11       : INTEGRALE PONDEREE DU VENT "V" DE L'ENVIRONEMENT.
!            : WEIGHTED INTEGRAL OF THE ENVIRONMENT "V" WIND.
! ZS12       : INTEGRALE PONDEREE DE BETA
!            : WEIGHTED INTEGRAL OF BETA
! ZS13       : INTEGRALE PONDEREE DE (1-BETA)*UMOYENNE
!            : WEIGHTED INTEGRAL OF (1-BETA)*UAVERAGE
! ZS14       : INTEGRALE PONDEREE DE (1-BETA)*VMOYENNE
!            : WEIGHTED INTEGRAL OF (1-BETA)*VAVERAGE

  ZS1(JLON)=0.0_JPRB
  ZS2(JLON)=0.0_JPRB
  ZS3(JLON)=0.0_JPRB
  ZS4(JLON)=0.0_JPRB
  ZS5(JLON)=0.0_JPRB
  ZS6(JLON)=0.0_JPRB
  ZS7(JLON)=0.0_JPRB
  ZS10(JLON)=0.0_JPRB
  ZS11(JLON)=0.0_JPRB
  ZS12(JLON)=0.0_JPRB
  ZS13(JLON)=0.0_JPRB
  ZS14(JLON)=0.0_JPRB
ENDDO

! *
! ------------------------------------------------------------------
!     V - PREMIERE BOUCLE VERTICALE (VERS LE BAS) INCLUANT LE CALCUL
!     "EN ADIABATIQUE SATUREE" DU PROFIL DU COURANT DESCENDANT.

!     FIRST VERTICAL LOOP (DOWNWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE DOWNDRAFT PROFILE.

!     INITIALISATION DU PARAMETRE QUI SERVIRA EVENTUELLEMENT A EVITER
!     DES CALCULS INUTILES EN HAUT DE L'ATMOSPHERE ET DEBUT DE LA
!     BOUCLE.
!     INITIALIZATION OF THE PARAMETER THAT SHALL EVENTUALLY HELP
!     AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE AND START OF THE VERTICAL LOOP.

ITOP=KTDIA
DO JLEV=KTDIA+1,KLEV

!     ADIABATIQUE SATUREE AVEC ENTRAINEMENT.
!     SATURATED ADIABAT WITH ENTRAINMENT.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    ZNLAB(JLON,JLEV)=KNLAB(JLON,JLEV)
    KNLAB(JLON,JLEV)=MAX(KNLAB(JLON,JLEV),KNLAB(JLON,JLEV-1))

!     ENTRAINEMENT.
!     ENTRAINEMENT.

! - TEMPORAIRE(S) 1D .

! ZTB        : "ZTN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB        : "ZQN" A LA BASE DE LA COUCHE EN INCLUANT L'ENTRAINEMENT.
!            : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".

    ZMIX=PZENTR(JLON)*(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
    ZTB(JLON)=ZTN(JLON)+ZMIX*(PT(JLON,JLEV-1)-ZTN(JLON))
    ZQB(JLON)=ZQN(JLON)+ZMIX*(PQ(JLON,JLEV-1)-ZQN(JLON))

!     STORE ZMIX FOR FURTHER USE

    ZRMIX(JLON,JLEV)=ZMIX

!     COEFFICIENTS DE L'HYDROSTATIQUE DU PROFIL COURANT DESCENDANT.
!     HYDROSTATIC COEFFICIENTS FOR THE DOWNDRAFT PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB       : POIDS DE "ZTB" DANS L'EPAISSEUR EN GEOPOTENTIEL.
!            : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH       : POIDS DE "ZTN" A "ZQN=ZQB" DANS LA MEME EPAISSEUR.
!            : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH       : POUR LA CORRECTION DE VARIATION DE Q A ZRBH.
!            : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=-PALPH(JLON,JLEV-1)*(RD+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*(RD+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*RV

!     DANS LE CALCUL DE L'ADIABATIQUE SATUREE GCVADS PERRMET UNE
!     TRANSITION CONTINUE ENTRE LE TRAITEMENT EQUIPRESSION ET
!     EQUIGEOPOTENTIEL.

!     TO COMPUTATE ADIABATIC PROFILE GCVADS ALLOWS TO GO
!     CONTINUOSLY FROM EQUIORESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
     & -PAPHIF(JLON,JLEV-1))/ZTB(JLON)  
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

    ZFFSLC=1.0_JPRB/(1.0_JPRB+PZATSLC(JLON,JLEV-1))
    ZRBB(JLON)=ZRBB(JLON)*ZFFSLC
    ZRBH(JLON)=ZRBH(JLON)*ZFFSLC
    ZRVH(JLON)=ZRVH(JLON)*ZFFSLC

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     APPEL A LA FONCTION DEFINIE POUR LE CALCUL DE LA PSEUDO CHALEUR
!     LATENTE A LA BASE (PARMI TROIS PARAMETRES CARACTERISTIQUES).
!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH        : VALEUR COURANTE DU PSEUDO LH DURANT LA BOUCLE DE NEWTON.
!            : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP        : COMME PZLH MAIS POUR LA CHALEUR MASSIQUE CP.
!            : AS PZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)= FOLH (ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     CHANGEMENT DE NIVEAU POUR OBTENIR LA PREMIERE EBAUCHE, POINT DE
!     DEPART DE LA BOUCLE DE NEWTON.
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV-1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ENDDO

!     BOUCLE DE NEWTON.
!     NEWTON'S LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

      IF (LNEIGE) THEN
        ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
      ELSE
        ZDELTA=0.0_JPRB
      ENDIF

!     CALCULS DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (ZTN(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON),ZDELTA))

!     INCREMENTATIONS.
!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQN(JLON)=ZQN(JLON)+ZDELQ
      ZTN(JLON)=ZTN(JLON)+ZDELT
    ENDDO
  ENDDO

!     TEST DE STABILITE, RECTIFICATION EVENTUELLE DU NIVEAU SUPERIEUR,
!     CALCUL DU PARAMETRE DE PROFIL DU FLUX DE MASSE ET DES DEUX
!     INTEGRALES INTERVENANT DANS LA CONDITION DE FERMETURE "TYPE KUO".
!     STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
!     COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
!     INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

  DO JLON=KIDIA,KFDIA

!    TEST DOUBLE DE KUO (FLOTABILITE ET DIVERGENCE DE FLUX DE PRECIPITATIONS).
!    DOUBLE KUO-TYPE TEST (BUOYANCY AND PRECIPIATION DIVERGENCE).

    ZKUO1=(RD+ZRVMD*ZQN(JLON))*ZTN(JLON)-PR(JLON,JLEV)*PT(JLON,JLEV)
    ZDVGP=RG*(PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1)-&
     & PFPLCL(JLON,JLEV)-PFPLCN(JLON,JLEV))*PZLHE(JLON,JLEV)  
    ZKUO2=ZS1(JLON)+ZDVGP+ZDVGP1(JLON)

    INLAB(JLON,JLEV)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZKUO1))&
     & *MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZKUO2),ZNLAB(JLON,JLEV)))  

    INLAB(JLON,JLEV-1)=MAX(INLAB(JLON,JLEV-1),INLAB(JLON,JLEV)&
     & *KNLAB(JLON,JLEV-1))  

!     EN CAS DE COURANT DESCENDANT PLUS "CHAUD" QUE L'ENVIRONEMENT ON
!     REEGALISE  TEMPERATURE ET HUMIDITE SPECIFIQUES DU COURANT
!     DESCENDANT A CELLES DU THERMOMETRE MOUILLE DE L'ENVIRONEMENT.
!     IN CASE OF A "WARMER" DOWNDRAFT THAN THE ENVIRONMENT ONE SETS BACK
!     TEMPERATURE AND SPECIFIC HUMIDITY OF THE DOWNDRAFT TO THAT OF THE WET
!     BULB CHARACTERISTICS OF THE ENVIRONMENT.

    ZMIXCIN=ZRMIX(JLON,JLEV)*RCIN
    ZQN(JLON)=MIN(ZQN(JLON), MAX(PQ(JLON,JLEV),PQW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)+ZMIXCIN*ZQN(JLON))
    ZTN(JLON)=MIN(ZTN(JLON), MIN(PT(JLON,JLEV),PTW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)+ZMIXCIN*ZTN(JLON))

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT COMPUTATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTN(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

!     VALEURS FINALES DE DETRAINEMENT AVEC APPEL A LA FONCTION DEFINIE
!     POUR REVENIR A LA VERITABLE CHALEUR LATENTE.
!     FINAL DETRAINMENT VALUES WITH A CALL TO THE DEFINED FUNCTION TO
!     COME BACK TO THE TRUE LATENT HEAT.

    ZQDN(JLON,JLEV)=ZQN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQDN(JLON,JLEV))*ZTN(JLON)+PAPHIF(JLON,JLEV)

!     PROFIL DE FLUX DE MASSE.
!     MASS FLUX PROFILE.

    ZHCMH=PCP(JLON,JLEV)*(ZTN(JLON)-PTW(JLON,JLEV))+PZLHE(JLON,JLEV)&
     & *(ZQN(JLON)-PQW(JLON,JLEV))  
    ZFORM(JLON,JLEV-1)=INLAB(JLON,JLEV)&
     & *SQRT(MAX(0.0_JPRB,-(ZHCMH+ZHCMH1(JLON)))&
     & *PAPRS(JLON,JLEV-1)/(PT(JLON,JLEV)&
     & +PT(JLON,JLEV-1)))  
    ZHCMH1(JLON)=ZHCMH

    ZTEST=MAX(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZFORM(JLON,JLEV-1)))&
     & ,MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZFMOD(JLON,JLEV-2)-1.0_JPRB)))  
    ZFMOD(JLON,JLEV-1)=(1.0_JPRB-ZTEST)+ZTEST*ZFMOD(JLON,JLEV-2)&
     & *((PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV-1))&
     & /(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV-2)))&
     & **GDDSDE  
    ZFORM(JLON,JLEV-1)=ZFORM(JLON,JLEV-1)*ZFMOD(JLON,JLEV-1)

!     SOMMATIONS.
!     SUMMATIONS.

    ZFMDQ=0.5_JPRB*ZFORM(JLON,JLEV-1)*(PQ(JLON,JLEV-1)-PQ(JLON,JLEV))
    ZS6(JLON)=ZS6(JLON)+INLAB(JLON,JLEV-1)*(ZFMDQ+ZFMDQ1(JLON))&
     & *PZLHE(JLON,JLEV-1)  
    ZFMDQ1(JLON)=ZFMDQ
    ZS1(JLON)=ZS1(JLON)+INLAB(JLON,JLEV-1)*ZDVGP1(JLON)
    ZDVGP1(JLON)=ZDVGP
  ENDDO

!     REINITALISATION A LA BASE POUR LES COURANTS DESCENDANTS AVEC
!     LA BASE AU DESSOUS DU DEBUT  DE LA BOUCLE VERTICALE (ITOP).

!     BOTTOM REINITIALIZATION FOR THE DOWNDRAFTS HAVING THE BASIS
!     UNDER THE START OF THE VERTICAL LOOP (ITOP).

  DO JLON=KIDIA,KFDIA

    ZLAB=1.0_JPRB-KNLAB(JLON,JLEV-1)

    ZTN(JLON)=ZTN(JLON)+ZLAB*(PTW(JLON,JLEV)-ZTN(JLON))
    ZQN(JLON)=ZQN(JLON)+ZLAB*(PQW(JLON,JLEV)-ZQN(JLON))
    ZSDN(JLON,JLEV)=ZSDN(JLON,JLEV)+ZLAB&
     & *((RCPD+ZCPVMD*ZQN(JLON))*ZTN(JLON)&
     & +PAPHIF(JLON,JLEV)-ZSDN(JLON,JLEV))  
    ZQDN(JLON,JLEV)=ZQDN(JLON,JLEV)+ZLAB*(ZQN(JLON)-ZQDN(JLON,JLEV))

    INLAB(JLON,JLEV)=INLAB(JLON,JLEV)*KNLAB(JLON,JLEV-1)
    ZHCMH1(JLON)=ZHCMH1(JLON)*KNLAB(JLON,JLEV-1)
    ZFORM(JLON,JLEV-1)=ZFORM(JLON,JLEV-1)*KNLAB(JLON,JLEV-1)
    ZFMOD(JLON,JLEV-1)=ZFMOD(JLON,JLEV-1)+ZLAB*(1.0_JPRB-ZFMOD(JLON,JLEV-1))
    ZFMDQ1(JLON)=ZFMDQ1(JLON)*KNLAB(JLON,JLEV-1)
    ZDVGP1(JLON)=ZDVGP1(JLON)+ZLAB&
     & *(RG*(PFPLCL(JLON,JLEV-1)+PFPLCN(JLON,JLEV-1)&
     & -PFPLCL(JLON,JLEV)-PFPLCN(JLON,JLEV))&
     & *PZLHE(JLON,JLEV)-ZDVGP1(JLON))  

    ZS1(JLON)=ZS1(JLON)*KNLAB(JLON,JLEV-1)
    ZS6(JLON)=ZS6(JLON)*KNLAB(JLON,JLEV-1)
  ENDDO

!     VERIFICATION DE LA PRESENCE D'AU MOINS UN COURANT DESCENDANT LE
!     LONG DU VECTEUR "JLON" ET MODIFICATION SI NECESSAIRE DE ITOP.
!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE DOWNDRAFT ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+INLAB(JLON,JLEV)
  ENDDO

  IF ((ISUM == 0).AND.(ITOP == JLEV-1)) THEN
    ITOP=JLEV
  ENDIF

ENDDO

!     ABANDON DU CALCUL EN CAS D'ABSENCE DES COURANTS DESCENDANTS.
!     LA SEQUENCE SOUS "IF THEN / ENDIF" NE SERA PAS INDENTEE VU SA
!     LONGUEUR ET SON CARACTERE PARTICULIER (PSEUDO "RETURN").
!     QUITTING IN CASE OF NO DOWNDRAFT EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA

IF(ITOP < KLEV) THEN

!     SOMMATION AU DERNIER NIVEAU.
!     LAST LEVEL SUMMATION.

  DO JLON=KIDIA,KFDIA
    ZS6(JLON)=ZS6(JLON)+INLAB(JLON,KLEV)*ZFMDQ1(JLON)*PZLHE(JLON,KLEV)
    ZS1(JLON)=ZS1(JLON)+INLAB(JLON,KLEV)*ZDVGP1(JLON)
  ENDDO

! *
! ------------------------------------------------------------------
!     VI - CONDITION DE FERMETURE ET REMISE A ZERO DE L'INTEGRALE ZS6.

!          CLOSURE ASSUMPTION AND SET BACK TO ZERO OF THE ZS6 INTEGRAL.

! - TEMPORAIRE(S) 1D .

! ZALF       : FACTEUR DE NORMALISATION POUR "ZFORM" (FERMETURE DE KUO).
!            : NORMALIZATION FACTOR FOR "ZFORM" (KUO CLOSURE).

  DO JLON=KIDIA,KFDIA
    ZALF(JLON)=MIN(PALF(JLON),(TSPHY*MIN(0.0_JPRB,ZS1(JLON)*GDDEVA)&
     & /MAX(ZEPS1,-ZS6(JLON)))&
     & *MIN(0.0_JPRB,SIGN(1.0_JPRB,ZS6(JLON))))  
    ZS6(JLON)=0.0_JPRB
  ENDDO

! *
! ------------------------------------------------------------------
!     VII - DEUXIEME BOUCLE VERTICALE (VERS LE HAUT) SERVANT A RESOUDRE
!     L'ALGORITHME IMPLICITE POUR L'ADVECTION DE SUBSIDENCE
!     COMPENSATOIRE ET A EFFECTUER TOUTES LES SOMMATIONS PONDEREES SUR
!     L'EPAISSEUR DU COURANT DESCENDANT. ON STOCKERA EGALEMENT AU PASSAGE
!     LES VALEURS FINALES PROVISOIRES DE L'HUMIDITE, DE L'ENERGIE STATIQUE
!     SECHE ET DES DEUX COMPOSANTES DU VENT.

!     SECOND VERTICAL LOOP (UPWARDS) TO SOLVE THE IMPLICIT
!     ALGORITHM FOR COMPENSATING SUBSIDENCE ADVECTION AND TO DO ALL
!     WEIGHTED SUMS ACROSS THE DOWNDRAFT DEPTH. ONE WILL AT THE SAME TIME
!     STORE PROVISIONAL FINAL VALUES FOR HUMIDITY, DRY STATIC ENERGY AND
!     FOR THE TWO WIND COMPONENTS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZFQSC1     : VALEUR FINALE DE Q (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL Q VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFSSC1     : VALEUR FINALE DE S (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL S VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFUSC1     : VALEUR FINALE DE U (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL U VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).
! ZFVSC1     : VALEUR FINALE DE V (D'ABORD SUBSIDENCE COMPENSATOIRE).
!            : FINAL V VALUE (AT FIRST ONLY COMPENSATING SUBSIDENCE).

!     ADITIONAL VERTICAL LOOP (DOWNWARDS) TO MODIFY THE PREVIOUSLY
!     COMPUTED MASS FLUX.

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

!     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
!     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.

      ZFORM(JLON,JLEV)=ZALF(JLON)*ZFORM(JLON,JLEV)

!     PROTECTION CONTRE L'INSTABILITE NON-LINEAIRE QUI PEUT SE
!     SE DECLENCHER OCCASIONELLEMENT MALGRE L'ALGORITHME IMPLICITE.
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.

      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)+(ZFORM(JLON,JLEV)&
       & -ZFORM(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
       & *MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))))  
    ENDDO
  ENDDO

!     CAS PARTICULIER DU PREMIER NIVEAU (EN BAS).
!     SPECIAL  CASE OF THE FIRST LEVEL (BOTTOM).

  DO JLON=KIDIA,KFDIA

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

! TEMPORAIRE(S) 1D .

! ZFMDS1     : DEMI FLUX DE MASSE FOIS DELTA DE S.
!            : HALF THE MASS FLUX TIME THE JUMP IN S.
! ZFMDU1     : DEMI FLUX DE MASSE FOIS DELTA DE U.
!            : HALF THE MASS FLUX TIME THE JUMP IN U.
! ZFMDV1     : DEMI FLUX DE MASSE FOIS DELTA DE V.
!            : HALF THE MASS FLUX TIME THE JUMP IN V.

    ZFMDQ1(JLON)=0.5_JPRB*ZFORM(JLON,KLEV-1)*(PQ(JLON,KLEV-1)-PQ(JLON,KLEV))
    ZFMDS1(JLON)=0.5_JPRB*ZFORM(JLON,KLEV-1)*(PZDSE(JLON,KLEV-1)&
     & -PZDSE(JLON,KLEV))  
    ZFMDU1(JLON)=0.5_JPRB*ZFORM(JLON,KLEV-1)*(PU(JLON,KLEV-1)-PU(JLON,KLEV))
    ZFMDV1(JLON)=0.5_JPRB*ZFORM(JLON,KLEV-1)*(PV(JLON,KLEV-1)-PV(JLON,KLEV))
    ZFQSC1(JLON,KLEV)=PQ(JLON,KLEV)-PRDELP(JLON,KLEV)*ZFMDQ1(JLON)
    ZFSSC1(JLON,KLEV)=PZDSE(JLON,KLEV)-PRDELP(JLON,KLEV)*ZFMDS1(JLON)
    ZFUSC1(JLON,KLEV)=PU(JLON,KLEV)-PRDELP(JLON,KLEV)*ZFMDU1(JLON)
    ZFVSC1(JLON,KLEV)=PV(JLON,KLEV)-PRDELP(JLON,KLEV)*ZFMDV1(JLON)

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=INLAB(JLON,KLEV)*PDELP(JLON,KLEV)
    ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MIN(0.0_JPRB,ZSDN(JLON,KLEV)&
     & -PZDSE(JLON,KLEV))+PZLHSB(JLON)&
     & *MIN(0.0_JPRB,ZQDN(JLON,KLEV)-PQ(JLON,KLEV)))  
    ZS3(JLON)=ZS3(JLON)+ZDPLAB*(ZFUSC1(JLON,KLEV)-PU(JLON,KLEV))
    ZS4(JLON)=ZS4(JLON)+ZDPLAB*(ZFVSC1(JLON,KLEV)-PV(JLON,KLEV))
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*PZLHSB(JLON)*(ZFQSC1(JLON,KLEV)-PQ(JLON,KLEV))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC1(JLON,KLEV)-PZDSE(JLON,KLEV))
  ENDDO

!     BOUCLE VERTICALE.
!     VERTICAL LOOP.

  DO JLEV=KLEV-1,ITOP+1,-1
    DO JLON=KIDIA,KFDIA

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

      ZFMDQ=0.5_JPRB*ZFORM(JLON,JLEV-1)*(PQ(JLON,JLEV-1)-PQ(JLON,JLEV))
      ZFMDS=0.5_JPRB*ZFORM(JLON,JLEV-1)*(PZDSE(JLON,JLEV-1)-PZDSE(JLON,JLEV))
      ZFMDU=0.5_JPRB*ZFORM(JLON,JLEV-1)*(PU(JLON,JLEV-1)-PU(JLON,JLEV))
      ZFMDV=0.5_JPRB*ZFORM(JLON,JLEV-1)*(PV(JLON,JLEV-1)-PV(JLON,JLEV))
      ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,JLEV)*ZFORM(JLON,JLEV))
      ZFQSC1(JLON,JLEV)=ZAUX*(PQ(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDQ1(JLON)-ZFMDQ+ZFORM(JLON,JLEV)&
       & *ZFQSC1(JLON,JLEV+1)))  
      ZFSSC1(JLON,JLEV)=ZAUX*(PZDSE(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDS1(JLON)-ZFMDS+ZFORM(JLON,JLEV)&
       & *ZFSSC1(JLON,JLEV+1)))  
      ZFUSC1(JLON,JLEV)=ZAUX*(PU(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDU1(JLON)-ZFMDU+ZFORM(JLON,JLEV)&
       & *ZFUSC1(JLON,JLEV+1)))  
      ZFVSC1(JLON,JLEV)=ZAUX*(PV(JLON,JLEV)+PRDELP(JLON,JLEV)&
       & *(ZFMDV1(JLON)-ZFMDV+ZFORM(JLON,JLEV)&
       & *ZFVSC1(JLON,JLEV+1)))  
      ZFMDQ1(JLON)=ZFMDQ
      ZFMDS1(JLON)=ZFMDS
      ZFMDU1(JLON)=ZFMDU
      ZFMDV1(JLON)=ZFMDV

!     SOMMATIONS.
!     SUMMATIONS.

      ZDPLAB=INLAB(JLON,JLEV)*PDELP(JLON,JLEV)
      ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MIN(0.0_JPRB,ZSDN(JLON,JLEV)&
       & -PZDSE(JLON,JLEV))+PZLHSB(JLON)&
       & *MIN(0.0_JPRB,ZQDN(JLON,JLEV)-PQ(JLON,JLEV)))  
      ZS5(JLON)=ZS5(JLON)+ZDPLAB
      ZS6(JLON)=ZS6(JLON)+ZDPLAB*PZLHSB(JLON)*(ZFQSC1(JLON,JLEV)-PQ(JLON,&
       & JLEV))  
      ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC1(JLON,JLEV)-PZDSE(JLON,JLEV))

    ENDDO
  ENDDO

!     CAS PARTICULIER DU DERNIER NIVEAU.
!     SPECIAL CASE OF THE LAST LEVEL.

  DO JLON=KIDIA,KFDIA

!     SOLUTION DU SYSTEME LINEAIRE IMPLICITE PAR SIMPLE ELIMINATION
!     (MATRICE BI-DIAGONALE).
!     STRAIGHTFORWARD ELIMINATION-TYPE SOLUTION OF THE LINEAR SYSTEM
!     (BI-DIAGONAL MATRIX).

    ZAUX=1.0_JPRB/(1.0_JPRB+PRDELP(JLON,ITOP)*ZFORM(JLON,ITOP))
    ZFQSC1(JLON,ITOP)=ZAUX*(PQ(JLON,ITOP)+PRDELP(JLON,ITOP)&
     & *(ZFMDQ1(JLON)+ZFORM(JLON,ITOP)&
     & *ZFQSC1(JLON,ITOP+1)))  
    ZFSSC1(JLON,ITOP)=ZAUX*(PZDSE(JLON,ITOP)+PRDELP(JLON,ITOP)&
     & *(ZFMDS1(JLON)+ZFORM(JLON,ITOP)&
     & *ZFSSC1(JLON,ITOP+1)))  
    ZFUSC1(JLON,ITOP)=ZAUX*(PU(JLON,ITOP)+PRDELP(JLON,ITOP)&
     & *(ZFMDU1(JLON)+ZFORM(JLON,ITOP)&
     & *ZFUSC1(JLON,ITOP+1)))  
    ZFVSC1(JLON,ITOP)=ZAUX*(PV(JLON,ITOP)+PRDELP(JLON,ITOP)&
     & *(ZFMDV1(JLON)+ZFORM(JLON,ITOP)&
     & *ZFVSC1(JLON,ITOP+1)))  

!     SOMMATIONS.
!     SUMMATIONS.

    ZDPLAB=INLAB(JLON,ITOP)*PDELP(JLON,ITOP)
    ZS2(JLON)=ZS2(JLON)+ZDPLAB*(MIN(0.0_JPRB,ZSDN(JLON,ITOP)&
     & -PZDSE(JLON,ITOP))+PZLHSB(JLON)&
     & *MIN(0.0_JPRB,ZQDN(JLON,ITOP)-PQ(JLON,ITOP)))  
    ZS5(JLON)=ZS5(JLON)+ZDPLAB
    ZS6(JLON)=ZS6(JLON)+ZDPLAB*PZLHSB(JLON)*(ZFQSC1(JLON,ITOP)-PQ(JLON,ITOP))
    ZS7(JLON)=ZS7(JLON)+ZDPLAB*(ZFSSC1(JLON,ITOP)-PZDSE(JLON,ITOP))

  ENDDO

! *
! ------------------------------------------------------------------
!     VIII - CALCUL DES COEFFICIENTS DU SCHEMA ET NORMALISATION DES
!     INTEGRALES, AVEC DE PLUS DANS LES CAS D'ALIMENTATION OU DE
!     FLOTABILITE INSUFISANTE UNE MISE A ZERO DES EFFETS CONVECTIFS
!     (SEULEMENT EFFECTIVE EN FIN DE ROUTINE).

!            COMPUTATION OF THE COEFFICIENTS OF THE SCHEME AND
!     NORMALIZATION OF THE INTEGRALS, WITH, IN THE CASE OF INSUFFICIENT
!     MOISTURE FEEDING OR BUOYANCY, A RESET TO ZERO OF CONVECTIVE
!     EFECTS (ONLY EFFECTIVE IN FACT AT THE END OF THE SUBROUTINE).

! - TEMPORAIRE(S) 1D .

! ZALFP      : PROPORTION DETRAINEE EN UN PAS DE TEMPS.
!            : DETRAINED FRACTION IN ONE TIME-STEP.
! ZALFS      : "ZALFP" BORNE A UN (CALCUL PSEUDO-IMPLICITE EN U ET V).
!            : "ZALFP" LIMITED TO ONE (PSEUDO IMPLICIT U/V TREATMENT).

  DO JLON=KIDIA,KFDIA
    ZALFP(JLON)=MAX(0.0_JPRB,ZS6(JLON)+ZS7(JLON))/MAX(ZEPS2,-ZS2(JLON))
    ZALFS(JLON)=ZALFP(JLON) /(1.0_JPRB+ZALFP(JLON))
    INND(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZALFP(JLON)))&
     & *MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS2+ZS2(JLON))))  
    INND(JLON)=INND(JLON)*NINT(1.0_JPRB-MAX(0.0_JPRB,SIGN(1.0_JPRB,0.0_JPRB-ZALF(JLON))))
  ENDDO

!     -------------------------------------------------------------------
!     IX - CALCULS CONCERNANT LE VENT INCLUANT L'ENTRAINEMENT ET LE TERME
!     DU GRADIENT DE PRESSION (SCHEMA KERSSHAW&GREGORY), CONSIDERANT LES
!     SEGMENTS  ACTIFS

!          WIND CONCERNING COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERME (KERSSHAW&GREGORY SCHEME), CONSIDERING THE
!     ACTIVE LAYERS
!*
!     --------------------------------
!     IX A -
!            RECOGNIZE ACTIVE LAYERS. COMPUTE THE FINAL VALUE OF
!     THE DETRAINED FRACTION IN ONE TIME-STEP.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZTRAN    : TABLEAU DES TRANSITIONS:
!           1. AU NIVEAU LE PLUS BAS DE CHAQUE SEGMENT ACTIF RECONNU,
!           0. PARTOUT AILLEURS.
!          : TRANSITION ARRAY:
!           1. IF LOWEST LEVEL OF AN ACTIVE ZONE,
!           0. ELSEWHERE.
! ----------------------------------------------------------------------
! SI IOLD VAUT 0 ON ACCEPTE UNE SEULE ZONE ACTIVE;
! EN CE CAS, ZTRAN VAUT 1 SEULEMENT AU NIVEAU ITOP.
! SI INLAB=0 SUR TOUTE LA HAUTEUR, LES INTEGRALES RESTERONT NULLES.
! IF IOLD IS 0 A SINGLE ACTIVE ZONE IS ADMITTED;
! IN THAT CASE, ZTRAN IS 1 ONLY AT LEVEL ITOP.
! IF INLAB=0 FOR THE WHOLE VERTICAL PROFILE, THE INTEGRALS WILL REMAIN 0.
! ----------------------------------------------------------------------

  DO JLON=KIDIA,KFDIA

! REMISE A ZERO DE INLAB DES QUE PAS PHYSIQUE:
! RESET INLAB TO ZERO AS SOON AS NOT PHYSICAL:

    INLAB(JLON,ITOP)=INLAB(JLON,ITOP)*INND(JLON)
    ZTRAN(JLON,ITOP)=MAX(INLAB(JLON,ITOP),1-IOLD)
  ENDDO

  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA

! REMISE A ZERO DE INLAB DES QUE PAS PHYSIQUE:
! RESET INLAB TO ZERO AS SOON AS NOT PHYSICAL:

      INLAB(JLON,JLEV)=INLAB(JLON,JLEV)*INND(JLON)
      ITRAN=MAX((INLAB(JLON,JLEV)-INLAB(JLON,JLEV-1)),0)
      ZTRAN(JLON,JLEV)=REAL(ITRAN*IOLD,JPRB)
    ENDDO
  ENDDO
!*
!    -------------------------------------------------------------------
!     IX B - BOUCLE ASCENDANTE POUR LES TABLEAUX DE QUANTITE DE MOUVEMENT
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

!           UPWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

! TEMPORAIRES 1D.

! ZBET      : COEFFICIENT BETA POUR LE PROFIL DE QUANTITE DE MOUVEMENT.
!            : BETA COEFFICIENT FOR MOMENTUM PROFILE.

! TEMPORAIRES 2D.
! ZA13      : K*UC0.
! ZA14      : K*VC0.
! ZBET      : COEFFICIENT BETA POUR LE PROFIL DE QUANTITE DE MOUVEMENT.
!           : BETA COEFFICIENT FOR MOMENTUM PROFILE.
! ZRMIX     : COEFFICIENT D'ENTRAINEMENT.
!           : ENTRAINMENT COEFFICIENT.
! ZUM       : U MOYEN DEPUIS LA BASE DU COURANT DESCENDANT.
!           : AVERAGE U VALUE FROM THE DOWNDRAFT BASE.
! ZVM       : V MOYEN DEPUIS LA BASE DU COURANT DESCENDANT.
!           : AVERAGE V VALUE FROM THE DOWNDRAFT BASE.

  DO JLON=KIDIA,KFDIA
    ZDPLAB=PDELP(JLON,ITOP)*INLAB(JLON,ITOP)
    ZS3(JLON)=ZDPLAB*(ZFUSC1(JLON,ITOP)-PU(JLON,ITOP))
    ZS4(JLON)=ZDPLAB*(ZFVSC1(JLON,ITOP)-PV(JLON,ITOP))
    ZS10(JLON)=ZDPLAB*PU(JLON,ITOP)
    ZS11(JLON)=ZDPLAB*PV(JLON,ITOP)

    ZBET(JLON,ITOP)=1.0_JPRB
    ZUM(JLON,ITOP)=PU(JLON,ITOP)
    ZVM(JLON,ITOP)=PV(JLON,ITOP)

    ZS12(JLON)=ZDPLAB
    ZS13(JLON)=0.0_JPRB
    ZS14(JLON)=0.0_JPRB
    ZZLAB=1.0_JPRB-ZTRAN(JLON,ITOP)
    ZA13(JLON,ITOP)=(-ZS3(JLON)+ZALFS(JLON)*(ZS10(JLON)-ZS13(JLON)))&
     & /MAX(ZEPS3, ZS12(JLON))  
    ZA14(JLON,ITOP)=(-ZS4(JLON)+ZALFS(JLON)*(ZS11(JLON)-ZS14(JLON)))&
     & /MAX(ZEPS3, ZS12(JLON))  
  ENDDO

!     TOUTES LES INTEGRALES ZS SONT MISES A ZERO EN ENTRANT
!     DANS UNE COUCHE INACTIVE, TANDIS QUE LES TABLEAUX DE SORTIE
!     CONSERVENT, EUX, LEUR VALEUR JUSQU'AU DEBUT D'UN NOUVEAU SEGMENT.

!     ALL VERTICAL INTEGRALS ZS ARE RESET TO ZERO WHEN PASSING INTO
!     AN INACTIVE LAYER, WHILE THE OUTPUT ARRAYS KEEP THEIR VALUE
!     UNTIL THE BEGINNING OF A NEW SEGMENT.

  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA

      ZDPLAB=PDELP(JLON,JLEV)*INLAB(JLON,JLEV)
      ZZLAB=1.0_JPRB-ZTRAN(JLON,JLEV)
      ZS3(JLON)=ZS3(JLON)*ZZLAB+ZDPLAB*(ZFUSC1(JLON,JLEV)-PU(JLON,JLEV))
      ZS4(JLON)=ZS4(JLON)*ZZLAB+ZDPLAB*(ZFVSC1(JLON,JLEV)-PV(JLON,JLEV))
      ZS10(JLON)=ZS10(JLON)*ZZLAB+ZDPLAB*PU(JLON,JLEV)
      ZS11(JLON)=ZS11(JLON)*ZZLAB+ZDPLAB*PV(JLON,JLEV)

!     ZBET, ZUM, ZVM SONT UTILISES SEULEMENT DANS LES COUCHES ACTIVES.
!     ZBE EST REMIS A 1 JUSTE AU DESUS LE PREMIER NIVEAU ACTIF OFFICIEL
!     (SI RENCONTRE DANS CETTE BOUCLE), ET AINSI SA VALEUR
!     ACTIVE DE DEPART EST 1.-ZMIX.

!     ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
!     ZBET RESET TO 1 JUST ABOVE THE FIRST OFFICIAL ACTIVE LAYER
!     (IF MET IN THIS LOOP), SO ITS STARTING ACTIVE VALUE IS 1.-ZMIX.

      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)=(ZBET(JLON,JLEV-1)*ZZLAB+(1.0_JPRB-ZZLAB))*(1.0_JPRB-ZMIX)

!     ZUM AND ZVM RESET TO PU(JLEV-1), PV AT FIRST ACTIVE LAYER
!     (SO CALLED B-1),I.E. VALUE OF ZUM IN THE LAYER BELOW LEVEL
!     WHERE ZBET=1 IS PU AT THE LEVEL ZBET=1.
!     ZBET IN THIS LOOP SHOULD NEVER REACH 1, MAX VALUE IS 1-ZMIX(JLEV) <1.

      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV-1)&
       & +(ZMIX*(PU(JLON,JLEV-1)-ZUM(JLON,JLEV-1))&
       & +TDDGP*(PU(JLON,JLEV)-PU(JLON,JLEV-1)))&
       & / (1.0_JPRB-ZBET(JLON,JLEV)))*ZZLAB&
       & + (1.0_JPRB-ZZLAB)*PU(JLON,JLEV)  
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV-1)&
       & +(ZMIX*(PV(JLON,JLEV-1)-ZVM(JLON,JLEV-1))&
       & +TDDGP*(PV(JLON,JLEV)-PV(JLON,JLEV-1)))&
       & / (1.0_JPRB-ZBET(JLON,JLEV)))*ZZLAB&
       & + (1.0_JPRB-ZZLAB)*PV(JLON,JLEV)  
      ZBET(JLON,JLEV)=ZBET(JLON,JLEV)*ZZLAB+(1.0_JPRB-ZZLAB)

      ZS12(JLON)=ZS12(JLON)*ZZLAB+ZDPLAB*ZBET(JLON,JLEV)
      ZS13(JLON)=ZS13(JLON)*ZZLAB+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))&
       & *ZUM(JLON,JLEV)  
      ZS14(JLON)=ZS14(JLON)*ZZLAB+ZDPLAB*(1.0_JPRB-ZBET(JLON,JLEV))&
       & *ZVM(JLON,JLEV)  

!      ZA13=ZALFS*UC0 ET ZA14=ZALFS*VC0 EN BAS,
!      ET CETTE VALEUR DOIT ETRE TRANSMISE VERS LE HAUT, VOIR PLUS LOIN.

!      ZA13=ZALFS*UC0 AND ZA14=ZALFS*VC0 AT THE LOWER SIDE,
!      AND HAS TO BE PROPAGATED UPWARDS, SEE FARTHER.

      ZA13(JLON,JLEV)=(-ZS3(JLON)+ZALFS(JLON)*(ZS10(JLON)-ZS13(JLON)))&
       & /MAX(ZEPS3, ZS12(JLON))  
      ZA14(JLON,JLEV)=(-ZS4(JLON)+ZALFS(JLON)*(ZS11(JLON)-ZS14(JLON)))&
       & /MAX(ZEPS3, ZS12(JLON))  
    ENDDO
  ENDDO
!*
!----------------------------------------------------------------
!     IX C -  TRANSMISSION VERS LE HAUT DE ZA13, ZA14

!             NOW PROPAGATE UPWARDS ZA13, ZA14

  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
      ZA13(JLON,JLEV)=(1.0_JPRB-ZTRAN(JLON,JLEV+1))*ZA13(JLON,JLEV+1)&
       & + ZTRAN(JLON,JLEV+1)*ZA13(JLON,JLEV)  
      ZA14(JLON,JLEV)=(1.0_JPRB-ZTRAN(JLON,JLEV+1))*ZA14(JLON,JLEV+1)&
       & + ZTRAN(JLON,JLEV+1)*ZA14(JLON,JLEV)  
    ENDDO
  ENDDO

! *
! ------------------------------------------------------------------
!     X - CALCUL DES FLUX (ON MODELISE LE FLUX D'ENTHALPIE ASSOCIE A
!     L'EVAPORATION DE MANIERE COHERENTE AVEC L'OPTION CHOISIE POUR LA
!     CHALEUR LATENTE EFFECTIVE -BOUCLES 310 ET 320- DE L'ACCVIMP).

!     FLUX COMPUTATION (ONE MODELIZES THE ENTHALPY FLUX ASSOCIATED
!     TO EVAPORATING WATER IN A COHERENT WAY WITH THE OPTION CHOSEN FOR THE
!     EFFECTIVE LATENT HEAT -LOOPS 310 AND 320-).

!     FLUX DIFFUSIF D'HUMIDITE.
!     DIFFUSIVE HUMIDITY FLUX.

  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDIFCQD(JLON,JLEV)=-ZGDTI*ZFORM(JLON,JLEV)*&
       & 0.5_JPRB*(ZFQSC1(JLON,JLEV)&
       & +ZFQSC1(JLON,JLEV+1)-ZQDN(JLON,JLEV)&
       & -ZQDN(JLON,JLEV+1))  
    ENDDO
  ENDDO

!     CALCUL FINAL DES FLUX AVEC SECURITE CONTRE LES FLUX DE
!     PRECIPITATION NEGATIFS.
!     FINAL COMPUTATION OF FLUXES WITH SECURITY AGAINST NEGATIVE
!     PRECIPITATION.

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA

!     FLUX DIFFUSIFS DE QUANTITE DE MOUVEMENT.
!     DIFFUSIVE MOMENTUM FLUXES.

      ZSTRCUD(JLON,JLEV)=ZSTRCUD(JLON,JLEV-1)&
       & -INLAB(JLON,JLEV)*PZPOID(JLON,JLEV)&
       & *( ZFUSC1(JLON,JLEV)-PU(JLON,JLEV)*(1.0_JPRB+ZALFS(JLON))&
       & +ZALFS(JLON)*(1.0_JPRB-ZBET(JLON,JLEV))*ZUM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZA13(JLON,JLEV) )  
      ZSTRCVD(JLON,JLEV)=ZSTRCVD(JLON,JLEV-1)&
       & -INLAB(JLON,JLEV)*PZPOID(JLON,JLEV)&
       & *( ZFVSC1(JLON,JLEV)-PV(JLON,JLEV)*(1.0_JPRB+ZALFS(JLON))&
       & +ZALFS(JLON)*(1.0_JPRB-ZBET(JLON,JLEV))*ZVM(JLON,JLEV)&
       & +ZBET(JLON,JLEV)*ZA14(JLON,JLEV) )  

! FLUX D'HUMIDITE (TOTAL ET D'EVAPORATION).
! HUMIDITY FLUXES (TOTAL AND EVAPORATING).

      ZFTOTQ=ZDIFCQD(JLON,JLEV-1)+ZFPLCLD(JLON,JLEV-1)&
       & +ZFPLCND(JLON,JLEV-1)&
       & -INLAB(JLON,JLEV)*(PZPOID(JLON,JLEV)*(ZFQSC1(JLON,JLEV)&
       & -PQ(JLON,JLEV)+ZALFP(JLON)*MIN(0.0_JPRB,ZQDN(JLON,JLEV)&
       & -PQ(JLON,JLEV))))  
      ZFCORQ=MIN(0.0_JPRB,ZDIFCQD(JLON,JLEV)-ZFTOTQ)
      ZFPLCLD(JLON,JLEV)=(ZFCORQ-(ZDIFCQD(JLON,JLEV)-ZFTOTQ))*(1.0_JPRB&
       & -PZSNP(JLON,JLEV))  
      ZFPLCND(JLON,JLEV)=(ZFCORQ-(ZDIFCQD(JLON,JLEV)-ZFTOTQ))*PZSNP(JLON,JLEV)
      ZFCORQ1=MAX(0.0_JPRB,-PFPLCL(JLON,JLEV)-PFPLCN(JLON,JLEV)&
       & -ZFPLCLD(JLON,JLEV)-ZFPLCND(JLON,JLEV))  
      ZFPLCLD(JLON,JLEV)=ZFPLCLD(JLON,JLEV)+ZFCORQ1*(1.0_JPRB-PZSNP(JLON,JLEV))
      ZFPLCND(JLON,JLEV)=ZFPLCND(JLON,JLEV)+ZFCORQ1*PZSNP(JLON,JLEV)
      ZZFPD=ZFPLCLD(JLON,JLEV)+ZFPLCND(JLON,JLEV)
      ZFPD(JLON,JLEV)=(1.0_JPRB-ZNLAB(JLON,JLEV))&
       & *MIN(ZZFPD,ZFPD(JLON,JLEV-1))&
       & + ZNLAB(JLON,JLEV)*ZZFPD  
      ZFCORQ2=ZFPD(JLON,JLEV)-ZZFPD
      ZFPLCLD(JLON,JLEV)=ZFPD(JLON,JLEV)*(1.0_JPRB-PZSNP(JLON,JLEV))
      ZFPLCND(JLON,JLEV)=ZFPD(JLON,JLEV)*PZSNP(JLON,JLEV)
      ZFCORQ3=MAX(0.0_JPRB,-PFPLCL(JLON,JLEV)-PFPLCN(JLON,JLEV)&
       & -ZFPLCLD(JLON,JLEV)-ZFPLCND(JLON,JLEV))  
      ZFPLCLD(JLON,JLEV)=ZFPLCLD(JLON,JLEV)+ZFCORQ3*(1.0_JPRB-PZSNP(JLON,JLEV))
      ZFPLCND(JLON,JLEV)=ZFPLCND(JLON,JLEV)+ZFCORQ3*PZSNP(JLON,JLEV)

!     FLUX D'ENTHALPIE (TOTAL ET DIFFUSIF).
!     ENTHALPY FLUXES (TOTAL AND DIFFUSIVE).

      ZFTOTS=ZDIFCSD(JLON,JLEV-1)-PZZLHP(JLON,JLEV-1)&
       & *(ZFPLCLD(JLON,JLEV-1)+ZFPLCND(JLON,JLEV-1))&
       & -INLAB(JLON,JLEV)&
       & *PZPOID(JLON,JLEV)*(ZFSSC1(JLON,JLEV)-PZDSE(JLON,JLEV)&
       & +ZALFP(JLON)*MIN(0.0_JPRB,ZSDN(JLON,JLEV)-PZDSE(JLON,JLEV)))  
      ZDIFCSD(JLON,JLEV)=ZFTOTS-PZLHSB(JLON)*(ZFCORQ+ZFCORQ1+ZFCORQ2+ZFCORQ3)&
       & +PZZLHP(JLON,JLEV)&
       & *(ZFPLCLD(JLON,JLEV)+ZFPLCND(JLON,JLEV))  
    ENDDO
  ENDDO

!     MISE A ZERO DE TOUS LES RESULTATS EN CAS DE PRECIPITATIONS NULLES
!     EN SURFACE OU DE "INND" DEJA NUL.
!     SETTING ALL RESULTS TO ZERO IN CASE OF NEGATIVE PRECIPITATIONS AT
!     THE SURFACE OR OF "INND" ALREADY ZERO.

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=ZFPLCLD(JLON,JLEV)*INND(JLON)+PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=ZFPLCND(JLON,JLEV)*INND(JLON)+PFPLCN(JLON,JLEV)
      PDIFCQ(JLON,JLEV)=ZDIFCQD(JLON,JLEV)*INND(JLON)+PDIFCQ(JLON,JLEV)
      PDIFCS(JLON,JLEV)=ZDIFCSD(JLON,JLEV)*INND(JLON)+PDIFCS(JLON,JLEV)
      PSTRCU(JLON,JLEV)=ZSTRCUD(JLON,JLEV)*INND(JLON)+PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=ZSTRCVD(JLON,JLEV)*INND(JLON)+PSTRCV(JLON,JLEV)
    ENDDO
  ENDDO
! RETOUR DU PSEUDO-RETURN D'APRES LA BOUCLE 580.
! RETURN FROM THE PSEUDO-RETURN SITUATED AFTER LOOP 580.

ENDIF

IF(LMUSCLFA) THEN
  ZFORM(:,:)=ZFORM(:,:)/TSPHY/RG
  CALL WRSCMR(NMUSCLFA,'FLUX_MASSE_CV_DD',ZFORM,KLON,KLEV+1)
ENDIF

! -----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCVIMPD',1,ZHOOK_HANDLE)
END SUBROUTINE ACCVIMPD
