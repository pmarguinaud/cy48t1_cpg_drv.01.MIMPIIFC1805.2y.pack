!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACCVIMPGPS ( YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PDELP,PAPRSF,PVERVEL,PT,PQ,PQSAT,&
 ! - INPUT/OUTPUT 2D .
 & PTENTRX,PCVGQ,PDIFTQ,PDIFTS,PGEMU)

!**** *ACCVIMPGPS * - SCHEMA ANTI GRID-POINT STORM

!     Sujet.
!     ------

!**   Interface.
!     ----------
!        *CALL* *ACCVIMPGPS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE.

! - 2D (1:KLEV) .

! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PVERVEL    : (OMEGA/P) VITESSE VERTICALE DIVISEE PAR LA PRESSION.
! PT         : TEMPERATURE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------

! - 2D (1:KLEV) .

! PTENTRX    : ENTRAINMENT MAXIMUM
! PCVGQ      : CONVERGENCE D'HUMIDITE (CONDITION DE "KUO").

! - 2D (0:KLEV) .

! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).

!-----------------------------------------------------------------------

!     Auteur.
!     -------
!      2012-10, F. Bouyssel

!     Modifications.
!     --------------
!      2017-12, P. Marquet : GCVOMDPS and GCVOMGSX in NAMELIST
!      2018-04, P. Marquet / L. Descamps : PGEMU as argument and
!                            GCVRHMIN GCVRHMAX GCVOMCA GCVOMCC
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RV       ,&
 & RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 & RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 & RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 & RGAMD
USE YOMPHY0  , ONLY : TPHY0

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENTRX(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCVGQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(KLON)

!-----------------------------------------------------------------------
! 2D LOCALS

REAL(KIND=JPRB) :: ZVERVEL(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIFTQ(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDIFTS(KLON,0:KLEV)

! 1D LOCALS

REAL(KIND=JPRB) :: ZHOOK_HANDLE
REAL(KIND=JPRB) :: ZDPL, ZDELT, ZA, ZB, ZCVOMGQ, ZOMGQ, ZDPS, ZOMGSX
REAL(KIND=JPRB) :: ZOMGS(KLON),ZDP(KLON)
REAL(KIND=JPRB) :: ZEPS,ZCVOMGS,ZCOMGS,ZRHMIN,ZRHMAX
REAL(KIND=JPRB) :: ZCD,ZRAP,ZCOND,ZFACT,ZCA,ZCC
INTEGER(KIND=JPIM) :: JLEV, JLON

!-----------------------------------------------------------------------

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCVIMPGPS',0,ZHOOK_HANDLE)
ASSOCIATE(GCVOMGS=>YDPHY0%GCVOMGS, TENTR=>YDPHY0%TENTR, GCVOMGQ=>YDPHY0%GCVOMGQ, &
 & GCVOMDPS=>YDPHY0%GCVOMDPS, GCVOMGSX=>YDPHY0%GCVOMGSX, &
 & GCVRHMIN=>YDPHY0%GCVRHMIN, GCVRHMAX=>YDPHY0%GCVRHMAX, &
 & GCVOMCA=>YDPHY0%GCVOMCA, GCVOMCC=>YDPHY0%GCVOMCC, &
 & GCVOMGE=>YDPHY0%GCVOMGE, TENTRX=>YDPHY0%TENTRX)
!-----------------------------------------------------------------------

!    - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     MODIFICATION OF OMEGA IN CASE OF OROGRAPHIC FORCING
!    - - - - - - - - - - - - - - - - - - - - - - - - - - -
!
ZEPS=1.E-8_JPRB
!
ZDPS=GCVOMDPS
ZOMGSX=GCVOMGSX
!
ZCVOMGQ=ABS(GCVOMGQ)
ZOMGQ=0.0_JPRB
IF (GCVOMGQ>0.0_JPRB) ZOMGQ=1.0_JPRB
!
ZCVOMGS=ABS(GCVOMGS)
ZCOMGS=0.0_JPRB
IF (GCVOMGS>0.0_JPRB) ZCOMGS=1.0_JPRB
!
ZRHMIN=GCVRHMIN
ZRHMAX=GCVRHMAX
!
ZCA=GCVOMCA
ZCC=GCVOMCC
!
IF ((ZCVOMGQ>0.0_JPRB).OR.(ZCVOMGS>0.0_JPRB)) THEN
  DO JLON=KIDIA,KFDIA
    ZDP(JLON)=0.0_JPRB
    ZOMGS(JLON)=0.0_JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZDPL=PDELP(JLON,JLEV)*MAX(0.0_JPRB,SIGN(1.0_JPRB, &
       & PAPRSF(JLON,JLEV)-PAPRSF(JLON,KLEV)+ZDPS))
      ZDP(JLON)=ZDP(JLON)+ZDPL
      ZOMGS(JLON)=ZOMGS(JLON)+ZDPL*PVERVEL(JLON,JLEV)*PAPRSF(JLON,JLEV)
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZOMGS(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZOMGSX-ABS(ZOMGS(JLON)/ZDP(JLON))))
  ENDDO
ELSE
  DO JLON=KIDIA,KFDIA
    ZOMGS(JLON)=1.0_JPRB
  ENDDO
ENDIF

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZVERVEL(JLON,JLEV)=PVERVEL(JLON,JLEV)*PAPRSF(JLON,JLEV)*ZOMGS(JLON)
  ENDDO
ENDDO

!    - - - - - - - - - - - - - - - - - - - - - - - -
!     MAXIMUM ENTRAINMENT COMPUTATION IF GCVOMGE>0
!    - - - - - - - - - - - - - - - - - - - - - - - -

IF (GCVOMGE>0.0_JPRB) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZA=-ZVERVEL(JLON,JLEV)/GCVOMGE
      PTENTRX(JLON,JLEV)=MAX(TENTR,MIN(2.0_JPRB*TENTRX,TENTRX*(1.0_JPRB-ZA)))
    ENDDO
  ENDDO
ENDIF

!    - - - - - - - - - - - - - - - - - - -
!     MODIFICATION OF PDIFTQ IF ZCVOMGQ>0
!    - - - - - - - - - - - - - - - - - - -

IF (ZCVOMGQ>0.0_JPRB) THEN
  ZDIFTQ(:,:)=PDIFTQ(:,:)
  PDIFTQ(:,:)=0.0_JPRB
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZA=MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZVERVEL(JLON,JLEV)-ZCVOMGQ)) &
       & *MAX(ZOMGQ,SIGN(1.0_JPRB,PQ(JLON,JLEV)-PQSAT(JLON,JLEV)))
      ZB=PCVGQ(JLON,JLEV)*PDELP(JLON,JLEV)/RG &
       & +ZOMGQ*MAX(0.0_JPRB,ZDIFTQ(JLON,JLEV)-ZDIFTQ(JLON,JLEV-1))
      PDIFTQ(JLON,JLEV) = PDIFTQ(JLON,JLEV-1) &
       & +(1.0_JPRB-ZA)*(ZDIFTQ(JLON,JLEV)-ZDIFTQ(JLON,JLEV-1))-ZA*ZB
    ENDDO
  ENDDO
ENDIF

!    - - - - - - - - - - - - - - - - - - -
!     MODIFICATION OF PDIFTS IF GCVOMGS>0
!    - - - - - - - - - - - - - - - - - - -

IF (ZCVOMGS>0.0_JPRB) THEN
  ZFACT=0.0_JPRB
  ZDIFTS(:,:)=PDIFTS(:,:)
  PDIFTS(:,:)=0.0_JPRB
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZDELT=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PT(JLON,JLEV)))
      ZCD=ABS(PGEMU(JLON))
      ZCOND=MAX(ZRHMIN,MIN((ZCA*ZCD*ZCD)+ZCC,ZRHMAX))
      ZRAP=ABS(PQ(JLON,JLEV)/MAX(PQSAT(JLON,JLEV),ZEPS))
      ZFACT=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRAP-ZCOND))
      ZA=MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZVERVEL(JLON,JLEV)-ZCVOMGS))*ZFACT
      ZB=FOLH(PT(JLON,JLEV),ZDELT)*PCVGQ(JLON,JLEV)*PDELP(JLON,JLEV)/RG
      PDIFTS(JLON,JLEV)=PDIFTS(JLON,JLEV-1) &
       & +ZDIFTS(JLON,JLEV)-ZDIFTS(JLON,JLEV-1)-ZA*ZB
    ENDDO
  ENDDO
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCVIMPGPS',1,ZHOOK_HANDLE)
END SUBROUTINE ACCVIMPGPS
