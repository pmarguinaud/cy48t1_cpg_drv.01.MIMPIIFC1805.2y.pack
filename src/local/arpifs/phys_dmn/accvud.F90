SUBROUTINE ACCVUD ( YDECUCONVCA,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
!-----------------------------------------------------------------------
! - INPUT  2D .
                    &PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PR,&
                    &PCVGQ,PDELP,PLNPR,&
                    &PQ,PQI,PQL,PQSAT,PQW,&
                    &PRDELP,PT,PTW,PU,PV,PVORT0, &
                    &PDDAL,&
! - INPUT 1D .
                    &PTS,PTAUX,PRCORI,PGM,PDECRD, &
! - OUTPUT 2D .
                    &PDIFCQ,PDIFCQL,PDIFCQI, PDIFCS,&
                    &PFCCQL,PFCCQI,PSTRCU,PSTRCV,PZATSLC,&
                    &KNACT,PFRDE,PDETFI,&
                    &PTU, PQU, PUU, PVU,&
! - OUTPUT 1D .
                    &KNND,PCAPE,PCUCONVCA,PNLCONVCA,&
! - INPUT/OUTPUT 2D .
                    &PGEOSLC,PENTCH,PUDAL,PUDOM)

!**** *ACCVUD * - MASS FLUX PROGNOSTIC UPDRAUGHT /CONVECTIVE SCHEME 
!     IN A FRAME OF PROGNOSTIC SUSPENDED CONDENSED PHASES MICROPHYSICS

!     Sujet.
!     ------
!     - MASS-FLUX DEEP CONVECTION SCHEME, COMPUTING CONDENSATE AND
!       "SUB-GRID SCALE" ASSOCIATED FLUXES .

!**   Interface.
!     ----------
!        *CALL* *ACCVUD*
!-----------------------------------------------------------------------
! -   INPUT ARGUMENTS.
!     ----------------

! - PHYSICS DIMENSIONNING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - PHYSICS VARIABLES (ALPHABETICALLY IN EACH CATEGORY)

! - 2D (0:KLEV) .

! PAPHI      : HALF LEVEL GEOPOTENTIAL
! PAPRS      : HALF LEVEL PRESSURE

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : FULL LEVEL GEOPOTENTIAL
! PAPRSF     : FULL LEVEL PRESSURE
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PR         : CONSTANTE DE L'AIR HUMIDE.
! PCVGQ      : WATER VAPOUR MOISTURE CONVERGENCE (KUO-TYPE CLOSURE)
! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : MICROPHYSICAL WATER VAPOUR SPECIFIC HUMIDITY
! PQI        : MICROPHYSICAL RESOLVED CLOUD ICE SPECIFIC CONTENTS
! PQL        : MICROPHYSICAL RESOLVED CLOUD LIQUID SPECIFIC CONTENTS
! PQW        : WET BULB SPECIFIC MOISTURE
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : WET BULB TEMPERATURE.
! PU         : U WIND COMPONENT 
! PV         : V WIND COMPONENT 
! PDDAL      : Prognostic downdraught mesh fraction (case LENTCH)

! - 2D (0:KLEV) .

! - 1D (PROGNOSTIC) .

! PTS        : SURFACE TEMPERATURE.

! PDECRD     : DECORRELATION DEPTH FOR CLOUD OVERLAPS [Pa].

! - 1D (DIAGNOSTIC) .

! PTAUX      : CHARACTERISTIC TIME FOR CAPE CONSUMPTION.

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PDIFCQ     : WATER VAPOUR CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQI    : ICE WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQL    : LIQUID WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCS     : DRY STATIC ENERGY CONVECTIVE SPECIFIC MOISTURE FLUX
! PFCCQL     : LIQUID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAUGHT
! PFCCQI     : SOLID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAUGHT
! PSTRCU     : U-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCV     : V-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PZATSLC    : ATTENUATION FACTOR FOR SLANTWISE CONVECTION.

! - 2D (1:KLEV) .

! PFRDE  : MESH FRACTION OCCUPIED BY DETRAINED AIR
! PDETFI : MESH FRACTION OCCUPIED BY INSTANTANEOUS DETRAINED AIR
! KNACT  : CONVECTIVE ACTIVITY INDEX (ACTIVE IF EQUAL TO ONE).
! PTU    : TEMPERATURE OF THE CLOUDY ASCENT.
! PQU    : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! PUU    : ZONAL VELOCITY IN UPDRAUGHT
! PVU    : MERIDIONAL VELOCITY IN UPDRAUGHT

! - 1D (DIAGNOSTIQUE) .

! KNND   : ZERO WHENEVER A NON PHYSICAL SITUATION OCCURS, ONE OTHERWISE 
! PCAPE  : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).

!-----------------------------------------------------------------------

! -   INPUT/OUTPUT ARGUMENTS 
!     ----------------------

! - 2D (0:KLEV) .

! - 2D (1:KLEV) .

! PGEOSLC  : EQUIVALENT GEOPOTENTIEL POUR CALCUL DE CONVECTION EN PENTE.
! PENTCH   : PROGNOSTIC UPDRAUGHT ENTRAINMENT
! PUDAL    : PROGNOSTIC UPDRAUGHT MESH FRACTION
! PUDOM    : PROGNOSTIC UPDRAUGHT RELATIVE VELOCITY
! PCUCONVCA : CA array for interaction with convection
! PNLCONVCA : CA array for interaction with convection

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS 
!     ------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     *ACCLFP*

!     Methode.
!     --------
!     Base: ACCVIMP, version AL15 2002, acvuvimp, accvmt 2005.

!     Auteur.
!     -------
!        03/2005 - L. Gerard.

!     Modifications.
!     --------------
!       01/2007 Alaro-0 version - L. Gerard
!       A.Bogatchev : 26-Oct-2007 removing of severe breaks of coding rules
!       22-Oct-2008 R. Brozkova   correction of sleeping bug + additional output
!       D. Banciu   : 06-Oct-2008 modifications to the prognostic entrainment
!       K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!       R. Brozkova (Feb 2011): correction of latent heat and transport
!                               of cloud condensates.
!       L. Bengtsson & F. Vana 18-Feb-2011 : CA scheme
!       R. Brozkova (Oct 2012): correction of mass-flux and introduction of
!                               organized detrainment.
!       R. Brozkova (Jan 2013): correction of activity and bouyancy indices.
!       R. Brozkova (Nov 2013): closure modulation dependency on resolution.
!                               detrainment computation correction.
!       L. Bengtsson (Aug 2014): Updates to CA scheme
!       J. Masek (Apr 2016): Exponential-random cloud overlap with variable
!                            decorrelation depth.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1             , ONLY : JPIM, JPRB
USE YOMHOOK              , ONLY : LHOOK,   DR_HOOK

USE YOMCST               , ONLY : RG       ,RD       ,RV       ,RCPD     ,RDT     ,&
 &                                RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 &                                RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 &                                RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 &                                RGAMD
USE YOMCT3               , ONLY : NSTEP
USE YOE_CUCONVCA         , ONLY : TECUCONVCA

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TECUCONVCA)           ,INTENT(IN) :: YDECUCONVCA
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN) :: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVORT0(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTAUX(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORI(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDECRD(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNACT(KLON,KLEV) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KNND(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCAPE(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCUCONVCA(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNLCONVCA(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFRDE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDETFI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PZATSLC(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOSLC(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PENTCH(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUDAL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUDOM(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVU(KLON,KLEV)

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
    &,ZBET(KLON,KLEV),ZDAL(KLON,KLEV)&
    &,ZDEQCA(KLON,KLEV)&
    &,ZDSE(KLON,KLEV)&
    &,ZENT(KLON,KLEV),ZTEST(KLON,KLEV)&
    &,ZHSE(KLON,KLEV),ZHSEAD(KLON,KLEV)&
    &,ZIFRAC(KLON,KLEV),ZIPOI(KLON,KLEV)&
    &,ZLDN(KLON,KLEV),ZLHE(KLON,KLEV),ZPOID(KLON,KLEV)&
    &,ZQC(KLON,KLEV),ZFRDE(KLON,KLEV)&
    &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV)&
    &,ZTVE(KLON,KLEV),ZTVN(KLON,KLEV)&
    &,ZUM(KLON,KLEV),ZVM(KLON,KLEV),ZQBW(KLON),ZTBW(KLON),ZDELNL(KLON)&
    &,ZFORM(KLON,0:KLEV),ZDQCA(KLON,0:KLEV),ZNEBC(KLON,KLEV)&
    &,ZQLC(KLON,KLEV),ZQIC(KLON,KLEV),ZLHS(KLON,KLEV),ZLHV(KLON,KLEV)&
    &,ZTCORR(KLON,KLEV),ZMELNET(KLON,KLEV),ZMELGET(KLON,KLEV),ZLHTBW(KLON,KLEV)& 
    &,ZLHCORR(KLON,KLEV)

REAL(KIND=JPRB) :: ZTS(KLON)

INTEGER(KIND=JPIM) :: INBAS(KLON,KLEV),INDET(KLON,KLEV),INBU(KLON,KLEV)

REAL(KIND=JPRB) :: ZBL(KLON),ZCP(KLON)&
    &,ZFDQA(KLON)&
    &,ZFDSA(KLON)&
    &,ZHCMHEB(KLON)&
    &,ZICVG(KLON)&
    &,ZLB(KLON),ZLH(KLON),ZLHSB(KLON)&
    &,ZQB(KLON)&
    &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
    &,ZS3(KLON),ZS4(KLON),ZS5(KLON)&
    &,ZS10(KLON)&
    &,ZS13(KLON),ZS14(KLON)&
    &,ZS15(KLON),ZS16(KLON),ZS17(KLON),ZS18(KLON)&
    &,ZRHLH(KLON),ZENTCH(KLON),ZSIG9(KLON),ZSIGB(KLON)&
    &,ZSUM(KLON),ZTB(KLON), ZLCL(KLON), ZMULACVG(KLON)&
    &,ZFMDQC(KLON),ZCR(KLON),ZSUM1(KLON),ZSUM2(KLON)&
    !**CA scheme
    &,ZNORMCAPE(KLON),ZNORMCONV(KLON),ZCAPECONV(KLON)

INTEGER(KIND=JPIM) :: IOLD, ISUM, ITOP, ITRAN, JIT, JLEV, JLON, II, ILEV, IZNNN

REAL(KIND=JPRB) :: ZARLII,ZBAS,ZBLUE, &
  &ZCOEF, ZCPSMD, ZCPVMD,&
  &ZCPVMS, ZCPVMW, ZCPWMD, ZDCP, ZDELQ, &
  &ZDELT, ZDELTA, ZDPHI, ZDPSGDT, ZDQW,  ZECMNP, ZENEN, &
  &ZENTR, ZENTRMN, ZENTRMX, ZEPSIMP, ZEPS16, &
  &ZEPS5, ZEPS6, ZEPS7, ZEPS9, ZEPSALDE, ZESP, ZEW, ZEXP, ZEXPND, &
  &ZAUX, ZFDQB, ZFDSB, ZGDT, ZGDTI, ZHCMHE, ZICVGL, &
  &ZKUO1, ZKUO2, ZLIQ, ZMIX, ZMIXCIN, ZNBA, ZQVU, ZQW, &
  &ZRVMD, ZFF,ZZ, ZZ2, ZZ3, ZZVAL, ZENTRN, ZALFA, &
  &ZINDX, ZCORIO, ZVORT0, ZFETA, ZLWE, ZQWE, ZTWE, &
  !**Prognostic Convection**
  &ZA,ZB,ZBU,ZC,ZC1, ZC2, ZC3,ZD,ZEPSALN,Z1KAPPAP,&
  &ZEPSDOM,ZEPSDOM0,ZCVG,ZSIG2,ZRHLH1,&
  &ZIDT, ZKSG, ZTPHY, ZDEQC, ZEPSL,ZMESH,ZGREYL,ZFAC,&
  &ZFFAND, ZFRAA, ZHS_REE, ZOD, ZSCO,&
  &ZZDQI, ZZDQL, ZFMDQCL,ZINCRQ,ZDELTBW,ZCPBW,ZFMCORR

!**CA scheme parameters**
REAL(KIND=JPRB) :: ZWEIGHT,ZCATAU,ZCAPEMAX,ZCVGMAX,ZLIVES

! Prognostic entrainment:
REAL(KIND=JPRB) :: ZFA, ZENTROM

REAL(KIND=JPRB) :: ZHOOK_HANDLE

LOGICAL :: LLLCL, LLDIVENT, LLDEQC

!-----------------------------------------------------------------------

#include "aplmini.intfb.h"

#include "fcttrm.func.h"
#include "fctdoi.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACCVUD',0,ZHOOK_HANDLE)
ASSOCIATE(REFLRHC=>YDML_PHY_MF%YRPHY0%REFLRHC, RCIN=>YDML_PHY_MF%YRPHY0%RCIN, GENVSRH=>YDML_PHY_MF%YRPHY0%GENVSRH, &
 & GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, NCVGMAX=>YDML_PHY_MF%YRPHY0%NCVGMAX, SCO=>YDML_PHY_MF%YRPHY0%SCO, &
 & GCVALFA=>YDML_PHY_MF%YRPHY0%GCVALFA, GCVNU=>YDML_PHY_MF%YRPHY0%GCVNU, RMULACVG=>YDML_PHY_MF%YRPHY0%RMULACVG, &
 & GCVACHI=>YDML_PHY_MF%YRPHY0%GCVACHI, ETACUT=>YDML_PHY_MF%YRPHY0%ETACUT, TENTRX=>YDML_PHY_MF%YRPHY0%TENTRX, &
 & TUDGP=>YDML_PHY_MF%YRPHY0%TUDGP, NWEIGHT=>YDML_PHY_MF%YRPHY0%NWEIGHT, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, &
 & ECMNP=>YDML_PHY_MF%YRPHY0%ECMNP, GCVTAUDE=>YDML_PHY_MF%YRPHY0%GCVTAUDE, ECMNPI=>YDML_PHY_MF%YRPHY0%ECMNPI, &
 & NCAPEMAX=>YDML_PHY_MF%YRPHY0%NCAPEMAX, NCATAU=>YDML_PHY_MF%YRPHY0%NCATAU, TEQH=>YDML_PHY_MF%YRPHY0%TEQH, &
 & TUDFR=>YDML_PHY_MF%YRPHY0%TUDFR, TENTR=>YDML_PHY_MF%YRPHY0%TENTR, GCVALMX=>YDML_PHY_MF%YRPHY0%GCVALMX, &
 & TUDBU=>YDML_PHY_MF%YRPHY0%TUDBU, GCVBEE=>YDML_PHY_MF%YRPHY0%GCVBEE, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LENTCH=>YDML_PHY_MF%YRPHY%LENTCH, NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, LCAPE=>YDML_PHY_MF%YRPHY%LCAPE, &
 & LNEBINS=>YDML_PHY_MF%YRPHY%LNEBINS, NIMELIT=>YDML_PHY_MF%YRPHY%NIMELIT, LNOIAS=>YDML_PHY_MF%YRPHY%LNOIAS, &
 & NBITER=>YDML_PHY_MF%YRPHY%NBITER, LCVGQM=>YDML_PHY_MF%YRPHY%LCVGQM, LSLC=>YDML_PHY_MF%YRPHY%LSLC, &
 & LSCMF=>YDML_PHY_MF%YRPHY%LSCMF, LCUCONV_CA=>YDECUCONVCA%LCUCONV_CA, &
 & NLIVES=>YDECUCONVCA%NLIVES, YDPHY0=>YDML_PHY_MF%YRPHY0)
!-----------------------------------------------------------------------

!*
!     -------------------
!     0 - INITIALISATIONS
ZTPHY=TSPHY                     

! FOR TESTS ONLY:
IZNNN=1 ! allows to suppress auto-advection of pudom by setting to zero.
LLDIVENT=.TRUE.
LLDEQC=.TRUE.
ZEXPND=EXP(-TSPHY/GCVTAUDE)

! Closure modulation
ZGREYL=1200._JPRB
IF(RMULACVG > 0._JPRB) THEN
  ZMULACVG(KIDIA:KFDIA)=RMULACVG
ELSE
  DO JLON=KIDIA,KFDIA
    ZMESH=REFLRHC/(TEQH*PGM(JLON))
    ZMULACVG(JLON)=MIN(RMULACVG*(-1._JPRB),MAX(1._JPRB,(ZMESH/ZGREYL)**2)) 
  ENDDO
ENDIF
!**CA scheme parameters:
ZWEIGHT=NWEIGHT 
ZCATAU=NCATAU
ZCAPEMAX=NCAPEMAX
ZCVGMAX=NCVGMAX
ZLIVES=REAL(NLIVES,JPRB)

!--------------------------------------------------

!     SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)
!            this is done now by initaplpar

!     INTEGRALES INITIALISEES.
!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D .

ZS3 =0.0_JPRB
ZS4 =0.0_JPRB
ZS5 =0.0_JPRB
ZS10=0.0_JPRB
ZS13=0.0_JPRB
ZS14=0.0_JPRB
ZS17=0.0_JPRB
ZS18=0.0_JPRB

ZNORMCAPE=0.0_JPRB
ZNORMCONV=0.0_JPRB
ZCAPECONV=0.0_JPRB

!*
!     ---------------------------
!     I - AUXILIARY CONSTANTS.

ZEPSIMP=1.E-12_JPRB
ZEPS16=1.E-06_JPRB
ZEPS5=1.E-11_JPRB
ZEPS6=1.E-03_JPRB
ZEPS7=1.E-11_JPRB
ZEPS9=1.E-01_JPRB

ZEPSALN=1.E-11_JPRB! ignore advected fractions smaller than this
ZEPSDOM0=-1.E-12! min vert vel. allowing to have detrainment
! Minimum value of ZLDN to start detraining
ZEPSL=1.E-9_JPRB
ZEPSALDE=1.E-3_JPRB

ZARLII=0.004_JPRB

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD

ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDTI=1.0_JPRB/ZGDT

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA

!     CALCULATION OF LATENT HEATS

    ZLHV(JLON,JLEV)=FOLH(PT(JLON,JLEV),0.0_JPRB)
    ZLHS(JLON,JLEV)=FOLH(PT(JLON,JLEV),1.0_JPRB)

    ENDDO
  ENDDO
!*
!     ------------------------------------------------------------------
!     II - COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS 
!        - OTHER PRELIMINARY CALCULATION

!----------------------------------------------------------

! Prognostic Entrainment

!----------------------------------------------------------

IOLD=1

ZENTRN=TENTR*SQRT(SQRT(TENTR/TENTRX))

! prognostic omega_u equation:
ZKSG=TUDFR/RG
ZBU= ZGDT*ZGDT/(RD*(1.0_JPRB+TUDBU))
! Activity Hist.: advected omega_u<-GCVACHI Pa/s enforces layer activity
  ZEPSDOM=-ZTPHY*GCVACHI
!*
!     ------------------------------------------------------------------
!     III - CALCULS PRELIMINAIRES
!           PRELIMINARY CALCULATIONS
!     ------------------------------------------------------------------

!  ZQC guaranteed >=0 from APLPAR.
   ZQC(KIDIA:KFDIA,:)=PQI(KIDIA:KFDIA,:)+PQL(KIDIA:KFDIA,:)

!     "LATENT HEAT" ARRAYS INITIALIZATION.
! ZLHE      : ARRAY FOR ENVIRONMENT LOCAL LATENT HEAT.
! ZDSE      : LOCAL ARRAY FOR DRY STATIC ENERGIES.

DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZDELTA=FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC) 
    ZLHE(JLON,JLEV)=FOLH(PT(JLON,JLEV),ZDELTA)
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA

! - TEMPORAIRE(S) 2D (1:KLEV) .
!    ZPOID     : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
!    ZIPOI     : INVERSE OF ZPOID.
! - TEMPORAIRE(S) 1D .
!    ZLHSB    : BUDGET LATENT HEAT AS COMPUTED AT THE SURFACE.
!    ZHSEAD    : FIRST GUESS OF SATURATED ADIABAT
!    ZHCMHEB   : ESTIMATED CLOUD EXCESS IN MOIST STATIC ENERGY
 
  ZDELTA=FONICE(PTS(JLON),YDPHY0%RDTFAC)
  ZLHSB(JLON)=FOLH(PTS(JLON),ZDELTA)-ZCPVMD*PTS(JLON)
  ZPOID(JLON,KLEV)=PDELP(JLON,KLEV)*ZGDTI
  ZIPOI(JLON,KLEV)=PRDELP(JLON,KLEV)*ZGDT
  ZHSEAD(JLON,KLEV)=ZDSE(JLON,KLEV)+ZLHSB(JLON)*PQ(JLON,KLEV)
  ZHCMHEB(JLON)=0.0_JPRB
  ZRHLH(JLON)=0.5_JPRB*PQ(JLON,KLEV)/PQSAT(JLON,KLEV)
ENDDO

!     COMPUTATIONS AT ALL LEVELS.

! ZHSE          : ENVIRONMENT MOIST STATIC ENERGY
! ZHCMHE        : EXCESS MOIST STATIC ENERGY OF THE SATURATED ADIABAT
! ZS17          : INTEGRATED BUOYANCY OF THE NON DILUTED ADIABAT
! ZS18          : INTEGRATED RELATIVE HUMIDITY TO COMPUTE BUOYANCY WEIGHTED
!                 AVERAGE 

!cdir unroll=4
DO JLEV=KLEV-1,KTDIA,-1
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PGEOSLC(JLON,JLEV)=MIN(PGEOSLC(JLON,JLEV),PGEOSLC(JLON,JLEV+1)&
     & *PTW(JLON,JLEV)/PTW(JLON,JLEV+1))
    IF (LSLC) THEN
      ZINDX=MAX(0.0_JPRB,SIGN(1.0_JPRB,ABS(PRCORI(JLON))-ZEPS5))
      ZCORIO=ZEPS5+ZINDX*(PRCORI(JLON)-ZEPS5)
      ZVORT0=0.5_JPRB*(PVORT0(JLON,JLEV)+PVORT0(JLON,JLEV+1))
      ZFETA=ZINDX/(MAX(ETACUT,1.0_JPRB+ZVORT0/ZCORIO))
      PZATSLC(JLON,JLEV)=ZFETA*0.5_JPRB*(PGEOSLC(JLON,JLEV)+&
       & PGEOSLC(JLON,JLEV+1))&
       & *((PU(JLON,JLEV+1)-PU(JLON,JLEV))**2+(PV(JLON,JLEV+1)&
       & -PV(JLON,JLEV))**2)/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))**2
    ELSE
      PZATSLC(JLON,JLEV)=0.0_JPRB
    ENDIF
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
    ZHSE(JLON,JLEV)=ZDSE(JLON,JLEV)+ZLHSB(JLON)*PQ(JLON,JLEV)
    ZHSEAD(JLON,JLEV)=MAX(ZHSEAD(JLON,JLEV+1)+(PAPHIF(JLON,JLEV)&
      &-PAPHIF(JLON,JLEV+1))*PZATSLC(JLON,JLEV)/(1.0_JPRB&
      &+PZATSLC(JLON,JLEV)),ZHSE(JLON,JLEV))
    ZHCMHE=0.5_JPRB*(ZHSEAD(JLON,JLEV)-ZHSE(JLON,JLEV))
    ZRHLH1=0.5_JPRB*PQ(JLON,JLEV)/PQSAT(JLON,JLEV)
    ZS17(JLON)=ZS17(JLON)+(ZHCMHEB(JLON)+ZHCMHE)*&
     &(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZS18(JLON)=ZS18(JLON)+(ZRHLH1+ZRHLH(JLON))*(ZHCMHEB(JLON)+ZHCMHE)*&
     &(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZHCMHEB(JLON)=ZHCMHE
    ZRHLH(JLON)=ZRHLH1
  ENDDO
ENDDO


! Cleaning up the prognostic variables 
!  coming back from their advection by the resolved flow

! PUDAL : (ADVECTED MESH FRACTION) SET TO 0 BELOW A THRESHOLD
! PUDOM : ADVECTED RELATIVE UD VELOCITY SET TO RELATIVE UD VELOCITY*DT
! INDET : 1 WHERE ADVECTED MESH FRACTION > 0, 0 ELSEWHERE.
! ZSIG9 : MEAN UPDRAUGHT ADVECTED MESH FRACTION 

DO JLON=KIDIA,KFDIA
  ZSIG9(JLON)=0.0_JPRB
ENDDO

!cdir unroll=8
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    PUDOM(JLON,JLEV)=(MIN(0.0_JPRB,PUDOM(JLON,JLEV))*ZTPHY)
    INDET(JLON,JLEV)=NINT(MAX(0.0_JPRB,&
      & SIGN(1.0_JPRB, PUDAL(JLON,JLEV)-ZEPSALN)))
    PUDAL(JLON,JLEV)=INDET(JLON,JLEV)*PUDAL(JLON,JLEV)
    ZSIG9(JLON)=ZSIG9(JLON)+PUDAL(JLON,JLEV)
    ZS3(JLON)=ZS3(JLON)+INDET(JLON,JLEV)
  ENDDO
ENDDO

! NOW LEAVE  THE MEAN ADVECTED FRACTION IN ZSIG9

DO JLON=KIDIA,KFDIA
  ZSIG9(JLON)=MIN(GCVALMX, ZSIG9(JLON)/MAX(ZEPS9,ZS3(JLON)))
ENDDO

! Put the average ZSIG9 at all active PUDAL / or everywhere in PUDAL
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    PUDAL(JLON,JLEV)=ZSIG9(JLON) *INDET(JLON,JLEV) 
  ENDDO
ENDDO

! Compute flux coefficients for prognostic equation


! ZFORM: AUTO-ADVECTION COEFFICIENT  IN PROGNOSTIC EQUATION
DO JLON=KIDIA,KFDIA
  ZS3(JLON)= (1.0_JPRB-PUDAL(JLON,KLEV))*PUDOM(JLON,KLEV)
  ZFORM(JLON,KLEV)=0.0_JPRB
  ZFORM(JLON,KTDIA-1)=0.0_JPRB
ENDDO
!cdir unroll=8
DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZZ=(1.0_JPRB-PUDAL(JLON,JLEV))*PUDOM(JLON,JLEV)
    ZFORM(JLON,JLEV)=(ZZ+ZS3(JLON))*0.5_JPRB 
    ZS3(JLON)=ZZ
  ENDDO   
ENDDO

! now can re-use ZS3

ZS3=0.0_JPRB


!*
!     ------------------------------------------------------------------
!     IV - INITIALISATION AT THE BOTTOM LAYER TO START THE CLOUD PROFILE 

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM    : PROFILE OF MASS FLUX AND LATER MASS FLUX TIME DT.
! KNACT    : FINAL LAYER ACTIVITY INDEX
! KNND     : PHYSICAL SOLUTION FLAG

KNACT(KIDIA:KFDIA,:)=0
ZDQCA(:,:)=0._JPRB
INBU=0
INBAS=0

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA

!     THERMODYNAMIC CHARACTERISTICS OF THE CLOUD.

! ZTDN       : TEMPERATURE OF THE CLOUDY ASCENT.
! ZQDN       : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! ZLDN       : CONDENSED SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! ZIFRAC     : SOLID FRACTION OF THE UPDRAUGHT CONDENSATE
! ZTVN      : VIRTUAL CLOUD TEMPERATURE 
! ZTVE      : VIRTUAL ENVIRONMENT TEMPERATURE 
! ZBL       : BUOYANCY TERM IN PUDOM EQUATION AT LEVEL JLEV+1

  PTU(JLON,KLEV)=PTW(JLON,KLEV)
  PQU(JLON,KLEV)=PQW(JLON,KLEV)
  ZLCL(JLON)=PQSAT(JLON,KLEV)-PQW(JLON,KLEV)
  ZTEST(JLON,KLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,-ZLCL(JLON)))
  ZLDN(JLON,KLEV)=MAX(0.0_JPRB,ZQC(JLON,KLEV)-&
    & (PQW(JLON,KLEV)-PQ(JLON,KLEV)) )
  ZDELTA=FONICE(PTW(JLON,KLEV),YDPHY0%RDTFAC)
  ZIFRAC(JLON,KLEV)=ZDELTA
  ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*PQU(JLON,KLEV))*PTU(JLON,KLEV)&
    & +PAPHIF(JLON,KLEV)
  ZTVN(JLON,KLEV)=(1.0_JPRB-ZLDN(JLON,KLEV)+RETV*PQU(JLON,KLEV))&
    & *PTU(JLON,KLEV)
  ZTVE(JLON,KLEV)=(1.0_JPRB-ZQC(JLON,KLEV)+RETV*PQ(JLON,KLEV))*&
    & PT(JLON,KLEV)

! INITIALISATION for VERTICAL MOTION EQUATION:
!   Initial values of  ZBL(JLON), ZS13, ZS14 

! ZA13      : DIFFERENCE IN MOIST STATIC ENERGY CLOUD/ENVIRONMENT.
! ZFDQA     : "0.5*ZFORML" TIME THE VERTICAL SPECIFIC HUMIDITY JUMP.
! ZICVG     : L TIME DELP TIME THE HUMIDITY CONVERGENCE.

  ZA13(JLON,KLEV)=0.0_JPRB            ! ZHCMH in accvimp
  ZRMIX(JLON,KLEV)=0.0_JPRB
  ZICVG(JLON)=PDELP(JLON,KLEV)*PCVGQ(JLON,KLEV)*ZLHE(JLON,KLEV)

! Other additions at KLEV:

  ZKUO1=ZTVN(JLON,KLEV)-ZTVE(JLON,KLEV)
  ZBL(JLON)= - PAPRSF(JLON,KLEV)*ZBU*ZKUO1/&
    & (ZTVN(JLON,KLEV)*ZTVE(JLON,KLEV)) 
  ZS13(JLON)=(PAPRS(JLON,KLEV)-PAPRSF(JLON,KLEV))/&
    & (1.0_JPRB-PUDAL(JLON,KLEV))
  ZS14(JLON)=0.0_JPRB
  ZHSE(JLON,KLEV)=ZDSE(JLON,KLEV)+ZLHSB(JLON)*PQ(JLON,KLEV)
! lentch-type nu
  IF(LENTCH) THEN
    ZALFA=GCVALFA*EXP(GENVSRH*LOG(MAX(ZEPS5,ZS18(JLON))/MAX(ZEPS5,ZS17(JLON))))
    ZENTCH(JLON)=PENTCH(JLON,KLEV)*ZALFA/MAX(ZEPSIMP,PENTCH(JLON,KLEV)+ZALFA)
  ELSE
    ZENTCH(JLON)=GCVNU
  ENDIF

ENDDO

!*
!     ------------------------------------------------------------------
!     V - FIRST VERTICAL LOOP (UPWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     ITOP MAY HELP AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE 

ITOP=KLEV+1
DO JLEV=KLEV-1,KTDIA,-1

!     SATURATED ADIABAT WITH ENTRAINMENT AND LIQUID (OR ICE) WATER
!     SUSTENTATION.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

!     ENTRAINMENT at Starting level JLEV+1

! ZTB       : "PTU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB       : "PQU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZLB       : "ZLDN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZFRAA     : ASCENT FRACTION DONE.
! ZENTROM   : TURBULENT ENTRAINMENT (WITHOUT THE MORE ORGANIZED PART -
!             BETA TERM) TO BE USED IN OMEGA EQUATION

    ZDPHI=PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1)
    ZFA=GCVBEE/ZDPHI *MAX(0.0_JPRB, ZS10(JLON))
!   ZZ=GCVALFA*ZS17(JLON)
    ZZ=GCVALFA*ZS17(JLON)*EXP(GENVSRH*LOG(MAX(ZEPS5,ZS18(JLON)) &
     & /MAX(ZEPS5,ZS17(JLON))))
    ZCOEF=ZZ*TENTR/(TENTR+TENTRX/(1.0_JPRB+ZZ*TENTRX))
    ZENTRMX=ZENTRN+(TENTRX-ZENTRN)/(1.0_JPRB+(TENTRX-ZENTRN)*ZCOEF)
    ZENTRMN=ZENTRN+(TENTR-ZENTRN)/(1.0_JPRB+(TENTR-ZENTRN)*ZCOEF)
    ZENEN=ZENTRMX*SQRT(SQRT(ZENTRMN/ZENTRMX))
    ZFRAA=EXP(-ZENEN*ZS5(JLON))
    ZENTROM=ZENTRMN+(ZENTRMX-ZENTRMN)*ZFRAA
    ZENTR=ZENTROM+ZFA

    IF(LLDIVENT.AND.LSCMF) THEN
! To do as in accvimp, one divides by 1-sigma
      ZZ=1.0_JPRB/(1.0_JPRB-ZSIG9(JLON))
      ZS3(JLON)=ZENTROM*ZZ
    ELSE
!**: store ZENTR in ZS3 (does not include Dphi !)
      ZS3(JLON)=ZENTROM
    ENDIF

!  ZENT         : ACTUAL ENTRAINMENT at FULL LEVEL JLEV+1
!  ZRMIX        : TO APPLY WHEN COMPUTING PROFILE AT JLEV

    ZENT(JLON,JLEV+1)=ZENTR*(PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1))
    ZMIX=ZENTR*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    IF (LSCMF) ZMIX=ZMIX/(1.0_JPRB-ZSIG9(JLON))
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
    ZRMIX(JLON,JLEV)=ZMIX
    ZLWE=MAX(0.0_JPRB,ZQC(JLON,JLEV+1)-MAX(0.0_JPRB,PQW(JLON,JLEV+1)&
     & -PQ(JLON,JLEV+1)))
    ZQWE=ZQC(JLON,JLEV+1)+PQ(JLON,JLEV+1)-ZLWE
    ZZ2=MAX(0.0_JPRB,SIGN(1.0_JPRB,PQW(JLON,JLEV+1)-PQ(JLON,JLEV+1)-ZEPSL))
    ZTWE=PT(JLON,JLEV+1) + ZZ2*(PTW(JLON,JLEV+1)-PT(JLON,JLEV+1)) *&
     & (ZQWE-PQ(JLON,JLEV+1))/(PQW(JLON,JLEV+1)-PQ(JLON,JLEV+1)+(1.0_JPRB-ZZ2))
    ZTB(JLON)=PTU(JLON,JLEV+1)+ZMIX*(ZTWE-PTU(JLON,JLEV+1))
    ZQB(JLON)=PQU(JLON,JLEV+1)+ZMIX*(ZQWE-PQU(JLON,JLEV+1))
    ZTBW(JLON)=PTU(JLON,JLEV+1)+ZMIX*(PTW(JLON,JLEV+1)-PTU(JLON,JLEV+1))
    ZQBW(JLON)=PQU(JLON,JLEV+1)+ZMIX*(PQW(JLON,JLEV+1)-PQU(JLON,JLEV+1))
    ZLB(JLON)=ZLDN(JLON,JLEV+1)+ZMIX*(ZLWE-ZLDN(JLON,JLEV+1)) 

! RE-ESTIMATION DE LA SATURATION APRES MELANGE
 
!   ice fraction and latent heat for mixed air temperature 

!   ZDELTBW=FONICE(ZTBW(JLON))
    ZDELTBW=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTBW(JLON)))
    ZLHTBW(JLON,JLEV+1)=FOLH(ZTBW(JLON),ZDELTBW)
    ZCPBW=RCPD+ZCPVMD*ZQBW(JLON)

!   computation of new equilibrium of mixed air

    ZEW= FOEW (ZTBW(JLON),ZDELTBW)
    ZESP=ZEW/PAPRSF(JLON,JLEV+1)
    ZQW= FOQS (ZESP)
    ZDQW=FODQS (ZQW,ZESP, FODLEW (ZTBW(JLON),ZDELTBW))
    ZINCRQ=(ZDQW*(ZCPBW*(ZTB(JLON)-ZTBW(JLON))+ZLHTBW(JLON,JLEV+1)&
     &*(ZQB(JLON)-ZQBW(JLON)))&
     &+(ZQW-ZQBW(JLON))*ZCPBW)/(ZCPBW+ZLHTBW(JLON,JLEV+1)*ZDQW)
    ZQBW(JLON)=ZQBW(JLON)+ZINCRQ
    ZQBW(JLON)=MIN(ZQBW(JLON),ZQB(JLON)+ZLB(JLON))


! MOIST STATIC ENERGY AT THE BASE OF THE LAYER AFTER MIXING
! remark the use of the budget latent heat in this case 

    ZHS_REE=PCP(JLON,JLEV+1)*ZTB(JLON)+PAPHIF(JLON,JLEV+1) &
     & +ZLHSB(JLON)*ZQB(JLON) 

!     HYDROSTATIC COEFFICIENTS FOR THE CLOUD PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB      : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH      : WEIGHT OF "PTU" AT "PQU=ZQB" IN THE SAME THICKNESS.
! ZRVH      : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB &
     &-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

!     TO COMPUTE SATURATED ADIABATIC PROFILE, GCVADS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
     &-PAPHIF(JLON,JLEV+1))/ZTB(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

! ZFFAND    : FRACTION OF BUOYANCY-EXCESS OVER AN UNDILUTE PLUME.

!   ZFFAND=1.0_JPRB/(1.0_JPRB+GCVNU*(1.0_JPRB-ZFRAA) &
    ZFFAND=1.0_JPRB/(1.0_JPRB+ZENTCH(JLON)*(1.0_JPRB-ZFRAA) &
      & *MAX(0.0_JPRB,ZHSEAD(JLON,JLEV)-ZHS_REE))
    ZFFAND=ZFFAND/(1.0_JPRB+PZATSLC(JLON,JLEV))
    ZRBB(JLON)=ZRBB(JLON)*ZFFAND
    ZRBH(JLON)=ZRBH(JLON)*ZFFAND
    ZRVH(JLON)=ZRVH(JLON)*ZFFAND

!     SNOW OPTION DEPENDENT CALCULATIONS.
! Diagnostic ice fraction 
!   ZDELTA=FONICE(ZTB(JLON))
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ZDELNL(JLON)=ZDELTA

!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! ZLH       : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP       : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)=FOLH(ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
     &ZCPWMD))*ZLB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!               CHANGE OF LEVEL TO OBTAIN A 
!     FIRST GUESS AS STARTING POINT FOR THE NEWTON LOOP.

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    PTU(JLON,JLEV)=ZTB(JLON)+ZDELT
    PQU(JLON,JLEV)=ZQB(JLON)+ZDELQ
  ENDDO


!     NEWTON LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA

!     SNOW OPTION DEPENDENT COMPUTATIONS.
!        Diagnostic ice fraction
      ZDELTA=ZDELNL(JLON)

!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW= FOEW (PTU(JLON,JLEV),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (PTU(JLON,JLEV),ZDELTA))

!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-PQU(JLON,JLEV))&
        &*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      PQU(JLON,JLEV)=PQU(JLON,JLEV)+ZDELQ
      PTU(JLON,JLEV)=PTU(JLON,JLEV)+ZDELT
    ENDDO
  ENDDO


!     STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
!     COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
!     INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

  DO JLON=KIDIA,KFDIA

!   PHASE PARTITION IN THE UPDRAUGHT
!   SIMPLY STORE THE UPDRAUGHT ICE FRACTION in ZIFRAC

    ZDELTA=FONICE(PTU(JLON,JLEV),YDPHY0%RDTFAC)
    ZIFRAC(JLON,JLEV)=ZDELTA
    ZECMNP=ZDELTA*ECMNPI+(1.0_JPRB-ZDELTA)*ECMNP

    ZLIQ=ZECMNP/(ZRBB(JLON)*ZTB(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
      & *(PQU(JLON,JLEV)-ZQB(JLON)))*PTU(JLON,JLEV))
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLDN(JLON,JLEV)=MAX(0.0_JPRB,ZLB(JLON)*ZEXP&
      & +(PQU(JLON,JLEV)-ZQB(JLON))*ZLIQ *(ZEXP-1.0_JPRB))

    ZDQCA(JLON,JLEV)=ZQBW(JLON)-PQU(JLON,JLEV)

!     IN CASE OF A "COOLER" CLOUD THAN THE ENVIRONMENT ONE SETS BACK
!     TEMPERATURE AND SPECIFIC HUMIDITY OF THE CLOUD TO THAT OF THE WET
!     BULB CHARACTERISTICS OF THE ENVIRONMENT.

!   ZLCL is diagnosed Before "cold cloud" adaptation 
    ZLCL(JLON)=MIN(ZLCL(JLON),PQSAT(JLON,JLEV)-PQU(JLON,JLEV))
    ZTEST(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,-ZLCL(JLON)))
    ZLDN(JLON,JLEV)=ZLDN(JLON,JLEV)*ZTEST(JLON,JLEV)

! ZBLUE=1 if back to blue point
! Then q_u -> q_we and q_c -> q_ce simply

    ZBLUE=MAX(0.0_JPRB,SIGN(1.0_JPRB,(PTW(JLON,JLEV)-PTU(JLON,JLEV))))
!---------------------
! RCIN business
    ZMIXCIN=ZRMIX(JLON,JLEV)*RCIN
    ZQVU=MAX(PQU(JLON,JLEV),&
         & MAX(PQ(JLON,JLEV),PQW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)&
         & +PQU(JLON,JLEV)*ZMIXCIN)
    ZDELQ=ZQVU-PQ(JLON,JLEV)
    ZLDN(JLON,JLEV)=ZBLUE* MAX(0.0_JPRB,(ZQC(JLON,JLEV)-ZDELQ))&
               & + (1.0_JPRB-ZBLUE)*ZLDN(JLON,JLEV)
    PQU(JLON,JLEV) = PQU(JLON,JLEV)* (1.0_JPRB-ZBLUE) + ZQVU*ZBLUE
    PTU(JLON,JLEV)=PTU(JLON,JLEV)*(1.0_JPRB-ZBLUE)&
         & + ZBLUE* MAX(PTU(JLON,JLEV),&
         & MIN(PT(JLON,JLEV),PTW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)&
         & +PTU(JLON,JLEV)*ZMIXCIN)
!---------------------
    
!     DOUBLE ACTIVITY TEST (BUOYANCY OR INERTIAL MOTION).
!     **Prognostic Convection** use Tv, Tvn
    ZTVN(JLON,JLEV)=(1.0_JPRB-ZLDN(JLON,JLEV)+RETV*PQU(JLON,JLEV))&
      & *PTU(JLON,JLEV)
! As the computation of PR ignored the condensate, use rather:
    ZTVE(JLON,JLEV)=(1.0_JPRB-ZQC(JLON,JLEV) &
      & +RETV*PQ(JLON,JLEV))*PT(JLON,JLEV)
    ZKUO1=ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV)


! First guess of INBU:

    INBU(JLON,JLEV)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZKUO1)))

    IF (.NOT.LCAPE) THEN
      ZICVGL=PDELP(JLON,JLEV)*PCVGQ(JLON,JLEV)*ZLHE(JLON,JLEV)
      ZKUO2=ZS4(JLON)+ZICVGL+ZICVG(JLON)
      INBU(JLON,JLEV)=INBU(JLON,JLEV)* &
        &NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZKUO2)))
    ENDIF
    IF (LNOIAS) THEN 
      INBU(JLON,JLEV)=INBU(JLON,JLEV)*MAX(0.0_JPRB,SIGN(1.0_JPRB,&
       & ZDSE(JLON,JLEV)-ZDSE(JLON,JLEV+1)))  
    ENDIF


!   accvimp's alternative ways to activity

    IF (GCVACHI > 0.0_JPRB) THEN
!   ALLOW INERTIAL EFFECTS TO PASS COUNTER GRADIENTS:
!   A NEGATIVE PUDOM IS ENOUGH TO DECREE ACTIVITY

      INBU(JLON,JLEV)=MAX(INBU(JLON,JLEV),&
        & NINT(SIGN(1.0_JPRB,ZEPSDOM-PUDOM(JLON,JLEV))))
    ENDIF

! The following does not work because PUDOM would be there =0. ???
    INBU(JLON,JLEV+1)=MAX(INBU(JLON,JLEV+1),INBU(JLON,JLEV))


!    FINAL VALUE OF THE UPDRAUGHT DRY STATIC ENERGY

! now we keep the separation between the
!      vapour PQU and condensate ZLDN

     ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*PQU(JLON,JLEV))*PTU(JLON,JLEV)&
       &+PAPHIF(JLON,JLEV)

! ZA13          : STORE hc-h, EXCESS MOIST STATIC ENERGY OF THE CLOUD
!                 OVER THE ENVIRONMENT

     ZA13(JLON,JLEV)=PCP(JLON,JLEV)*(PTU(JLON,JLEV)-PTW(JLON,JLEV))&
       &+ZLHE(JLON,JLEV)*(PQU(JLON,JLEV)-PQW(JLON,JLEV))
! New Storage of ZS10 to compute the entrainment in GCVBEE
     IF (GCVBEE/=0) THEN
       ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZEPSDOM-PUDOM(JLON,JLEV)))
       ILEV=MAX(KTDIA,JLEV-1)
       ZS10(JLON)=ZZ* (PUDOM(JLON,ILEV)-PUDOM(JLON,JLEV+1)) /&
         & ((1.0_JPRB-ZZ)+PUDOM(JLON,JLEV))*0.5_JPRB
     ENDIF

!    PROGNOSTIC VERTICAL MOTION EQUATION
!    -----------------------------------
!    COMPUTATION OF PUDOM at LEVEL JLEV+1 BECAUSE
!    WE DO NOT YET KNOW THE ENTRAINMENT HIGHER
!          ZS3(JLON)   is ZENTR AT JLEV+1
!          ZBL(JLON) COMPUTED IN PREVIOUS ITERATION i.e. JLEV+1

! ZA and ZC are the coefficients in the algebraic second order equation

!    SCOPE OF the COEFFICIENTS:
!          ZA IS ALWAYS >0
!          WE NEED PUDOM < 0 (BUT INDEPENDENT OF INBU)
!          THIS REQUIRES ZC<0

    ILEV=JLEV+1
! A >0 by construction
    ZZ=RD*ZTVN(JLON,ILEV)
    ZD=ZZ*(ZS3(JLON) + ZKSG)
    Z1KAPPAP=1._JPRB-PR(JLON,ILEV)*PTW(JLON,ILEV)/PGEOSLC(JLON,ILEV)
    ZA=(ZD+(1.0_JPRB-PUDAL(JLON,ILEV))*Z1KAPPAP)/PAPRSF(JLON,ILEV)
! B
    ZB=1.0_JPRB-IZNNN* ZFORM(JLON,ILEV)*PRDELP(JLON,ILEV)
! C
    ZC1=ZBL(JLON)+PUDOM(JLON,ILEV)

    ZD=PUDOM(JLON,ILEV)-PUDOM(JLON,JLEV)
    II=MIN(1,KLEV-ILEV)
    ZC2=ZFORM(JLON,ILEV)*(PUDOM(JLON,ILEV+II)-0.5_JPRB*ZS14(JLON))
    ZC3=ZFORM(JLON,JLEV)*0.5_JPRB*ZD
    ZS14(JLON)=ZD
    ZC=ZC1-IZNNN* II*PRDELP(JLON,ILEV)*(ZC2+ZC3)

! A x^2 -B x +C = 0
! ZC must be <0 and ZA must be >0
!-And reset to zero as soon as negative buoyancy:
    ZZ2=MAX(0.0_JPRB,SIGN(1.0_JPRB,0.0_JPRB-ZC)) 
    ZZ=ZB*ZB-4.0_JPRB*ZA*ZC 
    ZZ=SQRT(ZZ2*ZZ)

    ZOD=ZZ2*(ZB-ZZ)/(2.0_JPRB*ZA)
    PUDOM(JLON,ILEV)=ZOD
    ZBL(JLON)=- PAPRSF(JLON,JLEV)*ZBU*ZKUO1/&
      & (ZTVN(JLON,JLEV)*ZTVE(JLON,JLEV))

!     SUMMATIONS.


    ZS5(JLON)=ZS5(JLON)+INBU(JLON,JLEV)*(PAPHIF(JLON,JLEV)&
      &-PAPHIF(JLON,JLEV+1))
    IF (.NOT.LCAPE) THEN
! For ZKUO2-test
      ZS4(JLON)=ZS4(JLON)+INBU(JLON,JLEV+1)*ZICVG(JLON)
      ZICVG(JLON)=ZICVGL
    ENDIF
  ENDDO

!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+INBU(JLON,JLEV)
  ENDDO

  IF (ISUM /= 0) THEN
    ITOP=JLEV
  ENDIF
ENDDO

!     QUITTING IN CASE OF NO CONVECTION EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).


IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA

IF (ITOP < KLEV+1) THEN


!     LAST LEVEL SUMMATION (TOP).

  DO JLON=KIDIA,KFDIA
    IF (.NOT.LCAPE) THEN
      ZS4(JLON)=ZS4(JLON)+INBU(JLON,KTDIA)*ZICVG(JLON)
    ENDIF

!  PUDOM has to be zero i.e. the large scale vert vel. at level KTDIA

    PUDOM(JLON,KTDIA)=0.0_JPRB
! ZENT also used at level KTDIA:
    ZENT(JLON,KTDIA)=0.0_JPRB
    ZLHTBW(JLON,KTDIA)=ZLHE(JLON,KTDIA)

  ENDDO


! ADAPT THE DIFFERENT INDEXES:
!       INBU: BUOYANT LAYER
!       INDET: DETRAINING LAYER
!       INBAS: CLOUD BASE LAYER
!       KNACT: ACTIVE LAYER: BUOYANT and 
!               either DETRAINING or CLOUD BASE or REACHABLE

! AXILIARY ZSUM : TIME NEEDED FOR THE TOP OF A CLOUD TO 
!                  REACH CURRENT LAYER
  INBAS=0

  DO JLON=KIDIA,KFDIA
    INBAS(JLON,KLEV)=MAX(INBU(JLON,KLEV),1-IOLD)
    INDET(JLON,KLEV)=INDET(JLON,KLEV)*INBU(JLON,KLEV)
    KNACT(JLON,KLEV)=INBU(JLON,KLEV)
  ENDDO
  ZSUM=0.0_JPRB
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
      INBAS(JLON,JLEV)=IOLD*INBU(JLON,JLEV)*(1-INBU(JLON,JLEV+1))
      INDET(JLON,JLEV)=INDET(JLON,JLEV)*INBU(JLON,JLEV)
      ITRAN=1-INDET(JLON,JLEV)
      ZSUM(JLON)=INBU(JLON,JLEV)*(ZSUM(JLON)-ITRAN*PDELP(JLON,JLEV)&
        & /MIN(ZEPSDOM0,PUDOM(JLON,JLEV)))
      ITRAN=ITRAN*(1-INBAS(JLON,JLEV))* &
        & MAX(0.0_JPRB,SIGN(1.0_JPRB,ZSUM(JLON)-1.0_JPRB))
      KNACT(JLON,JLEV)=INBU(JLON,JLEV)*(1-ITRAN)

! NOW STORE minus THE UPDRAUGHT VELOCITY AT THE HALF LEVEL IN ZFORM

      ZFORM(JLON,JLEV)=-INBU(JLON,JLEV)*0.5_JPRB*&
        & (PUDOM(JLON,JLEV)+PUDOM(JLON,JLEV+1))
    ENDDO
  ENDDO

! ------------------------------------------------------------------
!    VI -   MESH FRACTION, VERTICAL ENTHALPY BUDGET and 
!                      DETRAINMENT 
! ------------------------------------------------------------------

!------------------------------------------------------------
! VIa - Compute the constant mesh fraction :
!------------------------------------------------------------

! ACCUMULATION OF INTEGRAL QUANTITIES
!     ZS15, ZS16, different for CAPE or NOT CAPE
!     ZFORM CONTAINS -PUDOM interpolated at half levels
  ZS15=0.0_JPRB
  ZS16=0.0_JPRB
  ZS17=0.0_JPRB
  ZFDQA=0.0_JPRB
  ZFDSA=0.0_JPRB
  PCAPE(KIDIA:KFDIA)=0.0_JPRB

! Diagnostic of CAPE (forecaster need) whatever closure

  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PCAPE(JLON)=PCAPE(JLON) +INBU(JLON,JLEV)*RD&
        &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
        &*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
    ENDDO
  ENDDO

  IF (LCAPE) THEN

!       CAPE closure

! Top level:
    DO JLON=KIDIA,KFDIA
      ZS15(JLON)=INBU(JLON,ITOP)&
        &*PDELP(JLON,ITOP)/PAPRSF(JLON,ITOP)&
        &* (ZTVN(JLON,ITOP)-ZTVE(JLON,ITOP))
      ZFDQB=0.5_JPRB*ZFORM(JLON,ITOP)*(PQ(JLON,ITOP+1)-PQ(JLON,ITOP))
      ZFDSB=0.5_JPRB*ZFORM(JLON,ITOP)*&
        &(ZDSE(JLON,ITOP+1)-ZDSE(JLON,ITOP))
      ZS16(JLON)=  INBU(JLON,ITOP) * (&
        &(1.0_JPRB+RETV*PQ(JLON,ITOP))/PCP(JLON,ITOP)*&
        &(ZFDSB) + RETV * PT(JLON,ITOP) *&
        &(ZFDQB) )&
        &/PAPRSF(JLON,ITOP)
      ZFDQA(JLON)=ZFDQB
      ZFDSA(JLON)=ZFDSB
    ENDDO
    DO JLEV=ITOP+1,KLEV-1
      DO JLON=KIDIA,KFDIA
! Cape in ZS15, now
! back to zero immediately after lowest active level
        ZS15(JLON)=ZS15(JLON) +INBU(JLON,JLEV)&
          &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
          &*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))

        ZFDQB=0.5_JPRB*ZFORM(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQ(JLON,JLEV))
        ZFDSB=0.5_JPRB*ZFORM(JLON,JLEV)*&
          & (ZDSE(JLON,JLEV+1)-ZDSE(JLON,JLEV))
        ZS16(JLON)= ZS16(JLON)  + INBU(JLON,JLEV) * (&
          &(1.0_JPRB+RETV*PQ(JLON,JLEV))/PCP(JLON,JLEV)*&
          &(ZFDSB+ZFDSA(JLON)) + RETV * PT(JLON,JLEV) *&
          &(ZFDQB+ZFDQA(JLON)) )&
          &/PAPRSF(JLON,JLEV)
        ZFDQA(JLON)=ZFDQB
        ZFDSA(JLON)=ZFDSB
      ENDDO
    ENDDO
    DO JLON=KIDIA,KFDIA
! Complete at KLEV:
      ZS15(JLON)=ZS15(JLON)+INBU(JLON,KLEV)&
        &*PDELP(JLON,KLEV)/PAPRSF(JLON,KLEV)&
        &*(ZTVN(JLON,KLEV)-ZTVE(JLON,KLEV))
 
      ZS16(JLON)= ZS16(JLON) + INBU(JLON,KLEV) * (&
        &(1.0_JPRB+RETV*PQ(JLON,KLEV))/PCP(JLON,KLEV)*&
        &(ZFDSA(JLON)) + RETV * PT(JLON,KLEV) *&
        &(ZFDQA(JLON)) )&
        &/PAPRSF(JLON,KLEV)
! Bottom
      ZSIGB(JLON)= ZTPHY*MAX(0.0_JPRB,ZS15(JLON))&
        & /(MAX(PTAUX(JLON),ZEPS7) * MAX(ZEPS16,ZS16(JLON)) )&
        & * MAX(0.0_JPRB,SIGN(1.0_JPRB, (ZS16(JLON)-ZEPS16)))
! Limitation between 0 and GCVALMX: done when copying into pudal
    ENDDO
!^^ end of cape closure ^^------------------------------------
! also when NCVCLO < 5
  ELSE ! NOT LCAPE

! GLOBAL CVGQ CLOSURE

!     WATER VAPOUR MOISTURE CONVERGENCE PROGNOSTIC CLOSURE
!     Temporary integrals:
!     ZS16: sum (hu-he) dp (ZS2 dans accvimp)
!     sum L TMC dp: already in ZS4
!     ZS17: sum L (omega_u dt dq_ca/dp dp) (cf zs6 in accvimp with dq)
!           ZDQCA is positive decrement, at interface (=0 at klev)

! Sigma_u constant 

!cdir unroll=8
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        ZS15(JLON)=ZS15(JLON)+ &
          & ZA13(JLON,JLEV)*PDELP(JLON,JLEV)*PUDAL(JLON,JLEV)
        ZS16(JLON)=ZS16(JLON)+INBU(JLON,JLEV)* &
          & ZA13(JLON,JLEV)*PDELP(JLON,JLEV)
        ZFDQB=0.5_JPRB*ZFORM(JLON,JLEV)*ZDQCA(JLON,JLEV)
        ZS17(JLON)=ZS17(JLON)+INBU(JLON,JLEV)*&
          &(ZFDQB+ZFDQA(JLON))*ZLHE(JLON,JLEV)
        ZFDQA(JLON)=ZFDQB
      ENDDO
    ENDDO

    IF(.NOT.LCUCONV_CA)THEN

      DO JLON=KIDIA,KFDIA

        ! All ZS15, ZS16, ZS17 and ZS4 must be >0:
        ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS15(JLON)))&
         &*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS16(JLON)))&
         &*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS17(JLON)-ZEPS7))

        ZCVG=ZTPHY*MAX(0.0_JPRB,ZS4(JLON)) 
        IF(LCVGQM) THEN

          ZSIG2=ZZ*(ZS15(JLON)+ZCVG)/&
           & ( ZS16(JLON)+ZS17(JLON)+(1.0_JPRB-ZZ))
          ZCVG=ZCVG*MAX(0.0_JPRB,MIN(1.0_JPRB,ZSIG2*ZMULACVG(JLON)))
        ENDIF

        ZSIGB(JLON)=ZZ*(ZS15(JLON)+ZCVG)/&
         & ( ZS16(JLON)+ZS17(JLON)+(1.0_JPRB-ZZ))

      ENDDO

    ELSE !**Use CA scheme
 
      DO JLON=KIDIA,KFDIA

        ! All ZS15, ZS16, ZS17 and ZS4 must be >0:
        ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS15(JLON)))&
         &*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS16(JLON)))&
         &*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS17(JLON)-ZEPS7))

        ZCVG=ZTPHY*MAX(0.0_JPRB,ZS4(JLON))
        IF(LCVGQM) THEN
          ZSIG2=ZZ*(ZS15(JLON)+ZCVG)/&
           & ( ZS16(JLON)+ZS17(JLON)+(1.0_JPRB-ZZ))
          ZCVG=ZCVG*MAX(0.0_JPRB,MIN(1.0_JPRB,ZSIG2*ZMULACVG(JLON)))
        ENDIF

        IF(NSTEP==0)THEN
          ZSIGB(JLON)=ZZ*(ZS15(JLON)+ZCVG)/&
           & ( ZS16(JLON)+ZS17(JLON)+(1.0_JPRB-ZZ))
        ELSE
          ZSIGB(JLON)=ZZ*(ZS15(JLON)+ZCVG+((PCUCONVCA(JLON)/ZLIVES)/ZCATAU)*&
           & ZTPHY*ZS16(JLON))/(ZS16(JLON)+ZS17(JLON)+(1.0_JPRB-ZZ)+&
           & (ZS16(JLON)*ZTPHY)/ZCATAU)
        ENDIF

        !Update CA field with information about CAPE+MOISTURE CONVERGENCE:

        IF(PCAPE(JLON)>ZCAPEMAX)ZNORMCAPE(JLON)=1
        IF(PCVGQ(JLON,KLEV)>ZCVGMAX)ZNORMCONV(JLON)=1

        ZCAPECONV(JLON)=(ZWEIGHT *ZNORMCAPE(JLON) &
          &           +(1.0_JPRB-ZWEIGHT)*ZNORMCONV(JLON))

IF(ZCAPECONV(JLON)>0)ZCAPECONV(JLON)=MIN((PCAPE(JLON)/10.),ZLIVES)

      ENDDO 

      PNLCONVCA(KIDIA:KFDIA)=ZCAPECONV(KIDIA:KFDIA)
      WHERE (PNLCONVCA(KIDIA:KFDIA) > 1._JPRB)
        PCUCONVCA(KIDIA:KFDIA)=1._JPRB
      ELSEWHERE
        PCUCONVCA(KIDIA:KFDIA)=0._JPRB
      ENDWHERE

    ENDIF

  ENDIF ! global cvgq closure

! FIRST TEST OF PHYSICAL VALIDITY OF THE OBTAINED ZSIGB

  DO JLON=KIDIA,KFDIA
    KNND(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZSIGB(JLON))))
  ENDDO
  DO JLEV=KLEV,KTDIA,-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      KNACT(JLON,JLEV)=KNND(JLON)*KNACT(JLON,JLEV)
      INBU(JLON,JLEV)=KNND(JLON)*INBU(JLON,JLEV)
      PUDAL(JLON,JLEV)=MAX(0.0_JPRB,MIN(GCVALMX, &
                & KNACT(JLON,JLEV)*ZSIGB(JLON) ))
! TENDANCE de sigma_u.  PLUTOT UTILISER LA TENDANCE DE LA MOYENNE:
      ZDAL(JLON,JLEV)=KNACT(JLON,JLEV)*(PUDAL(JLON,JLEV)-ZSIG9(JLON))
    ENDDO
  ENDDO

!------------------------------------------------------------
! VIb - Adapt the mesh fraction profile and tendency
!       and Compute the MASS FLUX at the interface levels
!------------------------------------------------------------

  DO JLEV=KLEV,ITOP,-1
    DO JLON=KIDIA,KFDIA

!* EFFECTIVE MASS FLUX AT THE INTERFACE
!  ZFORM already contains -PUDOM at interface *inbu
!        and is zero at KLEV
      ZFORM(JLON,JLEV)=PUDAL(JLON,JLEV)*ZFORM(JLON,JLEV)
    ENDDO
  ENDDO

!------------------------------------------------------------
! VIc - PROTECTION AGAINST NL INSTABILITY
!  and compute the environment values affected y pseudo-subsidence
!------------------------------------------------------------
!     ADITIONAL VERTICAL LOOP (UPWARDS) TO MODIFY THE PREVIOUSLY
!     COMPUTED MASS FLUX.


! ZFORM has been completely initialized to zero earlier, so it is zero 
!       above ITOP (and normally also at KLEV - but see VIb...)

!cdir unroll=8
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
 
!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV+1)+(ZFORM(JLON,JLEV)&
       &-ZFORM(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
       &*MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV+1))))
      
    ENDDO
  ENDDO


!*    -----------------------------------------------------------------
!     IX - WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERM (KERSSHAW & GREGORY SCHEME), CONSIDERING 
!     THE ACTIVE LAYERS
!*    -----------------------------------------------------------------

! INITIALIZE WORK ARRAYS

  ZUM=0.0_JPRB
  ZVM=0.0_JPRB
  ZBET=0.0_JPRB 
  ZA13=0.0_JPRB
  ZA14=0.0_JPRB

!*
!     --------------------------------
!     IX A - RECOGNIZE ACTIVE LAYERS. 

!  RESET KNACT TO ZERO AS SOON AS CONVECTIVE SOLUTION NO LONGER PHYSICAL
!  INBAS IS 1 AT THE BASE OF EACH ACTIVE SEGMENT

  DO JLEV=KLEV,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      KNACT(JLON,JLEV)=KNACT(JLON,JLEV)*KNND(JLON)
      INBU(JLON,JLEV)=INBU(JLON,JLEV)*KNND(JLON)
      INBAS(JLON,JLEV)=INBAS(JLON,JLEV)*KNND(JLON)
      PUDAL(JLON,JLEV)=PUDAL(JLON,JLEV)*KNACT(JLON,JLEV)
    ENDDO
  ENDDO
!*
!    -------------------------------------------------------------------
!     IX B - UPWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

! TEMPORAIRES 1D.

! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.

! TEMPORAIRE 0D.

! TEMPORAIRES 2D.

! ZA13      : UC0.
! ZA14      : VC0.
! ZUM       : AVERAGE U VALUE FROM CLOUD BASE.
! ZVM       : AVERAGE V VALUE FROM CLOUD BASE.



  DO JLON=KIDIA,KFDIA

!     THE PROBABLE CASE WHERE INBAS(KLEV)=1 IS THE OLD SCHEME (IOLD=0)
!     WE SHOULD TAKE THEN
!     ZBET(KLEV)=INBAS AND ZUM(KLEV-1)=PU(KLEV), ZVM=PV(KLEV)
!     AS IN THIS CASE TUDGP=0, THE LOOP BELOW PRODUCES
!     ZUM(KLEV-1)= ZUM(KLEV)+ZMIX*0 = ZUM(KLEV) =PU(KLEV) AND IT IS OK.

! AT BASE: ZBE=1, ZUM=PU, ZVM=PV, ZA13=PU, ZA14=PV
! Inactive layers and base => ZUM=PU, ZVM=PV

    ZBET(JLON,KLEV)=INBAS(JLON,KLEV)
    ZUM(JLON,KLEV)=PU(JLON,KLEV)
    ZVM(JLON,KLEV)=PV(JLON,KLEV)

! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

    ZA13(JLON,KLEV)=PU(JLON,KLEV)
    ZA14(JLON,KLEV)=PV(JLON,KLEV)
  ENDDO

!cdir unroll=4
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA

! ZNBA=1 where ACTIVE and not BASE
! ZBAS=1  at BASE (and then active)
! BOTH are 0 in INACTIVE layers
      ZBAS=REAL(INBAS(JLON,JLEV),JPRB)
      ZNBA=REAL(1-INBAS(JLON,JLEV),JPRB)*KNACT(JLON,JLEV)

!     ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
!     ZBET IS 0 IN INACTIVE LAYERS
!             1 IN THE BASE LAYERS
!             DECREASE UPWARDS IN THE ACTIVE LAYERS
!     ZUM = PU IN INACTIVE LAYERS
!     ZVM = PV IN INACTIVE LAYERS
!     AND BOTH ARE ACCUMULATED IN ACTIVE LAYERS


      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX) +ZBAS 

! ZUM back to PU if  KNACT=0 or INBAS=1
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
       &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
       &+TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
       &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
       &+TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA &
       &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)


! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

! ZA13,ZA14 : wind at base of active segment
! Same value up to next base
! (In inactive layers, multiplied by ZBET=0)

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
    ENDDO
  ENDDO


! STORE THE FINAL VALUES OF CLOUD MOMENTUM PROFILE INTO PUU AND PVU
! KEEP ALSO THE PRESSURE GRADIENT ACCELERATION INTO ZA13, ZA14
  DO JLEV=KTDIA,ITOP-1
    DO JLON=KIDIA,KFDIA
      ZUM(JLON,JLEV)=PU(JLON,JLEV)
      ZVM(JLON,JLEV)=PV(JLON,JLEV)
    ENDDO
  ENDDO
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      ZZ=1.0_JPRB-ZBET(JLON,JLEV)
      ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
      ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
    ENDDO
  ENDDO
  PUU(KIDIA:KFDIA,:)=ZUM(KIDIA:KFDIA,:)
  PVU(KIDIA:KFDIA,:)=ZVM(KIDIA:KFDIA,:)


!*
!     ------------------------------------------------------------------
!     X - FLUXES COMPUTATION 

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZLHCORR(JLON,JLEV)=ZPOID(JLON,JLEV)&
        & *(ZLHS(JLON,JLEV)-ZLHV(JLON,JLEV))/ZLHTBW(JLON,JLEV)
      ZTCORR(JLON,JLEV)=PTU(JLON,JLEV)-PT(JLON,JLEV)
    ENDDO
  ENDDO
 
  DO JLON=KIDIA,KFDIA
    ZTS(JLON)=PT(JLON,KLEV)
  ENDDO
  ZMELNET=0.0_JPRB
  ZFRDE(:,:)=PFRDE(:,:)

! iterate correction due to freezing/melting
  DO JIT=1,NIMELIT
    DO JLON=KIDIA,KFDIA
      ZFMDQC(JLON)=ZFORM(JLON,ITOP)*MAX(0.0_JPRB,ZDQCA(JLON,ITOP))&
     & *ZGDTI*0.5_JPRB
      ZFMCORR=ZFMDQC(JLON)+ZLHCORR(JLON,ITOP)&
     & *ZMELNET(JLON,ITOP)*KNACT(JLON,ITOP)
      ZCR(JLON)=ZFMCORR*PRDELP(JLON,ITOP)
! Contribution to condensate budget:
      ZDEQCA(JLON,ITOP)=ZCR(JLON)*ZGDT
! Partition of the condensate rate between ice and liquid
      ZZDQI=ZTEST(JLON,ITOP)*ZCR(JLON)*ZIFRAC(JLON,ITOP)
      ZZDQL=ZTEST(JLON,ITOP)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,ITOP))
      PFCCQL(JLON,ITOP)=ZZDQL*PDELP(JLON,ITOP)
      PFCCQI(JLON,ITOP)=ZZDQI*PDELP(JLON,ITOP) 
    ENDDO

!cdir unroll=8
    DO JLEV=ITOP+1,KLEV-1 
      DO JLON=KIDIA,KFDIA
        ZFMDQCL=ZFORM(JLON,JLEV)*MAX(0.0_JPRB,ZDQCA(JLON,JLEV))&
       & *ZGDTI*0.5_JPRB
        ZFMCORR=ZFMDQC(JLON)+ZFMDQCL+ZLHCORR(JLON,JLEV)&
       & *ZMELNET(JLON,JLEV)*KNACT(JLON,JLEV)
        ZCR(JLON)=ZFMCORR*PRDELP(JLON,JLEV)
        ZFMDQC(JLON)=ZFMDQCL
! Contribution to condensate budget:
        ZDEQCA(JLON,JLEV)=ZCR(JLON)*ZGDT
! Partition of the condensate rate between ice and liquid 
        ZZDQI=ZTEST(JLON,JLEV)*ZCR(JLON)*ZIFRAC(JLON,JLEV)
        ZZDQL=ZTEST(JLON,JLEV)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,JLEV))
        PFCCQL(JLON,JLEV)=PFCCQL(JLON,JLEV-1) + ZZDQL*PDELP(JLON,JLEV)  
        PFCCQI(JLON,JLEV)=PFCCQI(JLON,JLEV-1) + ZZDQI*PDELP(JLON,JLEV)
      ENDDO
    ENDDO 

    DO JLON=KIDIA,KFDIA
      ZFMCORR=ZFMDQC(JLON)+ZLHCORR(JLON,KLEV)&
     & *ZMELNET(JLON,KLEV)*KNACT(JLON,KLEV)
      ZCR(JLON)=ZFMCORR*PRDELP(JLON,KLEV) 
! Contribution to condensate budget:
      ZDEQCA(JLON,KLEV)=ZCR(JLON)*ZGDT
! Partition of the condensate rate between ice and liquid   
      ZZDQI=ZTEST(JLON,KLEV)*ZCR(JLON)*ZIFRAC(JLON,KLEV)
      ZZDQL=ZTEST(JLON,KLEV)*ZCR(JLON)*(1.0_JPRB-ZIFRAC(JLON,KLEV))
      PFCCQL(JLON,KLEV)=PFCCQL(JLON,KLEV-1) + ZZDQL*PDELP(JLON,KLEV)  
      PFCCQI(JLON,KLEV)=PFCCQI(JLON,KLEV-1) + ZZDQI*PDELP(JLON,KLEV)
    ENDDO

  IF (LLDEQC) THEN ! PFRDE BY CONDENSATE BUDGET

    ZFMDQC=0._JPRB
    ZCR=0._JPRB

!cdir unroll=8
    DO JLEV=KLEV, MAX(ITOP,KTDIA+1), -1 ! ZA15 has no 0-level
      DO JLON=KIDIA,KFDIA
        ZAUX=1.0_JPRB/(PDELP(JLON,JLEV)+ZFORM(JLON,JLEV-1))
        ZFMDQCL=ZFORM(JLON,JLEV-1)*&
               & (ZLDN(JLON,JLEV-1)+ZLDN(JLON,JLEV))*0.5_JPRB
        ZCR(JLON)=ZAUX*(ZFMDQC(JLON)-ZFMDQCL+ZCR(JLON)*ZFORM(JLON,JLEV))
        ZFMDQC(JLON)=ZFMDQCL
        ZDEQCA(JLON,JLEV)=ZDEQCA(JLON,JLEV)+ZCR(JLON)
      ENDDO
    ENDDO

!cdir unroll=8
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
 
        ZFF=(ZFORM(JLON,JLEV)+ZFORM(JLON,JLEV-1))*0.5_JPRB*KNACT(JLON,JLEV)
        ZENTR=ZENT(JLON,JLEV)*ZFF*PRDELP(JLON,JLEV)
        ZDEQC=MAX(0.0_JPRB, ZDEQCA(JLON,JLEV)-(ZENTR+ZDAL(JLON,JLEV))&
             & *(ZLDN(JLON,JLEV)-ZQC(JLON,JLEV)))
! security
        ZZ3=KNACT(JLON,JLEV)*&
               & MAX(0.0_JPRB,SIGN(1.0_JPRB,ZLDN(JLON,JLEV)-ZEPSL))
        PFRDE(JLON,JLEV)=ZFRDE(JLON,JLEV)*ZEXPND+&
               & ZZ3* ZDEQC/(ZLDN(JLON,JLEV)+(1.0_JPRB-ZZ3))*KNND(JLON)
        PDETFI(JLON,JLEV)=ZZ3* ZDEQC/(ZLDN(JLON,JLEV)&
               &+(1.0_JPRB-ZZ3))*KNND(JLON)
!  LIMIT 0< sigma_D< 1-PUDAL
        ZZ=ZFRDE(JLON,JLEV)*ZEXPND+PDETFI(JLON,JLEV)
        PFRDE(JLON,JLEV)=MIN(1.0_JPRB-PUDAL(JLON,JLEV),ZZ)
        PDETFI(JLON,JLEV)=MAX(0.0_JPRB,PDETFI(JLON,JLEV)&
                             & -(ZZ-PFRDE(JLON,JLEV)))
      ENDDO
    ENDDO

  ELSE  ! Detrainement BY MASS BUDGET

    ZCR(:)=0.0_JPRB
 
!cdir unroll=8
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
 
        ZFF=(ZFORM(JLON,JLEV)+ZFORM(JLON,JLEV-1))*0.5_JPRB*KNACT(JLON,JLEV)
        ZAUX=(ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))*PRDELP(JLON,JLEV)
! Mass entrainment
        ZENTR=ZENT(JLON,JLEV)*ZFF*PRDELP(JLON,JLEV)
! Detrainment area increment  dsigma_D
! obtained by local budget
        ZDEQC=MAX(0.0_JPRB, ZAUX+ZENTR-ZDAL(JLON,JLEV))
        PFRDE(JLON,JLEV)=ZFRDE(JLON,JLEV)*ZEXPND+ZDEQC*KNND(JLON)
        PDETFI(JLON,JLEV)=ZDEQC*KNND(JLON)
        ZCR(JLON)=ZFF

!  LIMIT 0< sigma_D< 1-PUDAL
        ZZ2=MAX(0.0_JPRB, SIGN(1.0_JPRB,PFRDE(JLON,JLEV)-ZEPSALDE))
        PFRDE(JLON,JLEV)=MIN(1.0_JPRB-PUDAL(JLON,JLEV),&
                        &PFRDE(JLON,JLEV)*ZZ2)
        ZZ2=MAX(0.0_JPRB, SIGN(1.0_JPRB,PDETFI(JLON,JLEV)-ZEPSALDE))
        PDETFI(JLON,JLEV)=MIN(1.0_JPRB-PUDAL(JLON,JLEV),&
                        &PDETFI(JLON,JLEV)*ZZ2)


      ENDDO
    ENDDO

  ENDIF  ! detrainement computation

! Test APLMINI

! Compute convective condensates of the time step:

!cdir unroll=8
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZQLC(JLON,JLEV)=ZIPOI(JLON,JLEV)* &
          & (PFCCQL(JLON,JLEV)-PFCCQL(JLON,JLEV-1))
      ZQIC(JLON,JLEV)=ZIPOI(JLON,JLEV)* &
          & (PFCCQI(JLON,JLEV)-PFCCQI(JLON,JLEV-1))
      IF(LNEBINS) THEN
        ZNEBC(JLON,JLEV)=MIN(1.0_JPRB-ZEPS5,PDETFI(JLON,JLEV)+&
            & MAX(0._JPRB,PUDAL(JLON,JLEV)))
      ELSE
        ZNEBC(JLON,JLEV)=MIN(1.0_JPRB-ZEPS5,PFRDE(JLON,JLEV)+&
            & MAX(0._JPRB,PUDAL(JLON,JLEV)))
      ENDIF
    ENDDO
  ENDDO

  IF(JIT < NIMELIT) THEN
! Call APLMINI
    CALL APLMINI(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV, &
     & PAPRS, PAPRSF, PCP, ZQIC, ZQLC, PR, PT, &
     & ZIPOI, ZLHS, ZLHV, ZNEBC, ZPOID, ZTCORR, PDECRD, &
     & ZMELNET,ZMELGET)

    DO JLON=KIDIA,KFDIA
      ZSUM1(JLON)=0.0_JPRB
      ZSUM2(JLON)=0.0_JPRB
    ENDDO

!cdir unroll=8
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ZSUM1(JLON)=ZSUM1(JLON)+ZMELNET(JLON,JLEV)*(ZLHCORR(JLON,JLEV)&
         &*KNACT(JLON,JLEV))
        ZSUM2(JLON)=ZSUM2(JLON)+(ZLHCORR(JLON,JLEV)*KNACT(JLON,JLEV))
      ENDDO
    ENDDO

    DO JLON=KIDIA,KFDIA
      ZSUM1(JLON)=ZSUM1(JLON)/MAX(ZSUM2(JLON),ZEPS5)
    ENDDO

    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ZMELNET(JLON,JLEV)=ZMELNET(JLON,JLEV)-ZSUM1(JLON)
      ENDDO
    ENDDO

  ENDIF

! end of iteration nimelit
ENDDO

!*   ---------------------------
!    CONVECTIVE TRANSPORT FLUXES
!*   ---------------------------
! Implicit calculation

! replace the updraught values by the difference (psi-psi_u)
  ZA13(KIDIA:KFDIA,:)=PQ(KIDIA:KFDIA,:)-PQU(KIDIA:KFDIA,:)
  ZSDN(KIDIA:KFDIA,:)=ZDSE(KIDIA:KFDIA,:)-ZSDN(KIDIA:KFDIA,:)
  ZQC(KIDIA:KFDIA,:)=PQI(KIDIA:KFDIA,:)&
     &-ZIFRAC(KIDIA:KFDIA,:)*ZLDN(KIDIA:KFDIA,:)*(1._JPRB-PUDAL(KIDIA:KFDIA,:)) 
  ZLDN(KIDIA:KFDIA,:)=PQL(KIDIA:KFDIA,:)&
     & -(1.0_JPRB-ZIFRAC(KIDIA:KFDIA,:))*ZLDN(KIDIA:KFDIA,:)&
     & *(1._JPRB-PUDAL(KIDIA:KFDIA,:))
  ZUM(KIDIA:KFDIA,:)=PU(KIDIA:KFDIA,:)-ZUM(KIDIA:KFDIA,:)
  ZVM(KIDIA:KFDIA,:)=PV(KIDIA:KFDIA,:)-ZVM(KIDIA:KFDIA,:)

! Above itop: all fluxes are zero
! Level klev: ZFORM=0 there, hence all the fluxes are zero, too.
! Elsewhere we need ZFORM >=0 - AVOID the rounding errors
! Require at least that pdifcq<0
!cdir unroll=8
  DO JLEV=ITOP,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZAUX=ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV)+ZFORM(JLON,JLEV))
        ZDPSGDT=PDELP(JLON,JLEV)*ZGDTI*0.5_JPRB
        PDIFCQ(JLON,JLEV)=ZAUX*( PDIFCQ(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZA13(JLON,JLEV)+ZA13(JLON,JLEV+1)) )
        PDIFCQL(JLON,JLEV)=ZAUX*( PDIFCQL(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZLDN(JLON,JLEV)+ZLDN(JLON,JLEV+1)) )
        PDIFCQI(JLON,JLEV)=ZAUX*( PDIFCQI(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZQC(JLON,JLEV)+ZQC(JLON,JLEV+1)) )
        PDIFCS(JLON,JLEV)=ZAUX*( PDIFCS(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZSDN(JLON,JLEV)+ZSDN(JLON,JLEV+1)) )
        PSTRCU(JLON,JLEV)=ZAUX*( PSTRCU(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
        PSTRCV(JLON,JLEV)=ZAUX*( PSTRCV(JLON,JLEV-1)+ &
         & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
      ENDDO
  ENDDO
!*          -------------
!           LCL TREATMENT
!*          -------------

!     SETTING ALL RESULTS TO ZERO IN CASE OF NEGATIVE PRECIPITATIONS AT
!     THE SURFACE OR OF "KNND" ALREADY ZERO.

!     SI SCO > 0,          ON MET A ZERO SI PRECIP. < SCO.
!     SI -1-EPS < SCO < 0, ON NE MET JAMAIS A ZERO.
!     SI SCO < -1-EPS,     ON MET A ZERO SI PRECIP. < -SCO*MIN(QSAT-QN).
!     IF SCO > 0,          SETTING TO ZERO IF PRECIP. < SCO.
!     IF -1-EPS < SCO < 0, NO SETTING TO ZERO.
!     IF SCO < -1-EPS,     SETTING TO ZERO IF PRECIP. < -SCO*MIN(QSAT-QN).

  LLLCL=SCO < -1.0_JPRB-ZEPS6
  DO JLON=KIDIA,KFDIA
    IF(LLLCL) THEN
      ZSCO=-SCO*ZLCL(JLON)
    ELSE
      ZSCO=SCO
    ENDIF
    ZZVAL=PFCCQL(JLON,KLEV)+PFCCQI(JLON,KLEV)-ZSCO
    KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB-ZZVAL))
  ENDDO

!------------------------------------------------------------------
!      BEFORE LEAVING 
!      - LEAVE IN PUDOM THE RELATIVE UPDRAUGHT VELOCITY FOR ADVECTION
!      - RESET ALL FLUXES TO ZERO WHERE KNND IS !
!        AND ALSO RESET PUDOM AND PUDAL
!------------------------------------------------------------------

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

! PUDOM is now omega_u(t+)*dt, outside, pudom is omega_u(t-)
! Detrained condensates will be reset to zero if knnd=0
!     PFRDE(JLON,JLEV)=PFRDE(JLON,JLEV)*KNND(JLON)
      PUDAL(JLON,JLEV)=KNND(JLON)*PUDAL(JLON,JLEV)
      PUDOM(JLON,JLEV)=KNND(JLON)*PUDOM(JLON,JLEV)*ZIDT
    ENDDO
  ENDDO

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      PDIFCQ(JLON,JLEV)=KNND(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCQI(JLON,JLEV)=KNND(JLON)*PDIFCQI(JLON,JLEV)
      PDIFCQL(JLON,JLEV)=KNND(JLON)*PDIFCQL(JLON,JLEV)
      PDIFCS(JLON,JLEV)=KNND(JLON)*PDIFCS(JLON,JLEV)
      PFCCQL(JLON,JLEV)=KNND(JLON)*PFCCQL(JLON,JLEV)
      PFCCQI(JLON,JLEV)=KNND(JLON)*PFCCQI(JLON,JLEV)
      PSTRCU(JLON,JLEV)=KNND(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=KNND(JLON)*PSTRCV(JLON,JLEV)
    ENDDO
  ENDDO

ELSE
! Do not forget to re-divide PUDOM by dt
  PUDOM=PUDOM*ZIDT

ENDIF ! ITOP < KLEV+1

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACCVUD',1,ZHOOK_HANDLE)
END SUBROUTINE ACCVUD

