!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACDIFUS ( YDMCC,YGFL,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,KCSS,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PCP,PDELP,PFRSO,PKTROV,PKUROV,PKNROV,&
 & PQ,PRDELP,PT,PU,PV,PXTROV,PXUROV,PXPTKEROV,PALPH,&
 & PAPRS,PR,PQL,PQI,PCOEFN,PTKE,PLMU,PLNPR,&
 ! - INPUT  1D .
 & PCD,PCDN,PCDROV,PCHROV,PCEROV,PCPS,PCT,PDQSTS,PEMIS,PFHPS,&
 & PHQ,PHTR,PHU,PHV,PLSM,PIVEG,PNEIJ,PQS,PQSATS,&
 & PTP,PTS,PVEG,PXDROV,PXHROV,PWS,PWSI,&
 ! - OUTPUT 2D .
 & PDIFTQ,PDIFTS,PFCQNG,PSTRTU,PSTRTV,&
 & PDIFTQL,PDIFTQI,PTENDPTKE,&
 ! - OUTPUT 1D .
 & PFCHSP,PFCLL,PFCLN,PFCS,PFEVI,PFEVL,PFEVN,&
 & PFEVV,PFTR,PLHS,&
 ! - INPUT/OUTPUT 2D .
 & PFRTH,&
 ! - INPUT/OUTPUT 1D .
 & PGZ0F,PGZ0HF)

!**** *ACDIFUS * - DIFFUSION VERTICALE TURBULENTE.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCULS DE DIFFUSION VERTICALE TURBULENTE INCLUANT LA CORRECTION
!       DES HUMIDITES NEGATIVES : FLUX TURBULENTS VERTICAUX, APPEL A LA
!       FORMULE DE CHARNOCK, CORRECTION DES FLUX DE RAYONNEMENT
!       THERMIQUE ET TOUS LES FLUX AU ET DANS LE SOL CORRESPONDANTS .
!     - COMPUTATION OF VERTICAL TURBULENT DIFFUSION INCLUDING A NEGATIVE
!       HUMIDITY CORRECTION : VERTICAL TURBULENT FLUXES, CALL TO THE
!       CHARNOCK FORMULA, THERMAL RADIATION FLUXES' CORRECTION AND ALL
!       CORRESPONDING FLUXES AT THE SURFACE OR IN THE SOIL .

!**   Interface.
!     ----------
!        *CALL* *ACDIFUS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KCSS       : NOMBRE DE NIVEAUX PROFONDS DANS LE SOL (1 SI LSOLV=.F.).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PFRSO      : FLUX DE RAYONNEMENT SOLAIRE.
! PKTROV     : COEFFICIENT D'ECHANGE VERTICAL DE T ET Q EN KG/(M*M*S).
! PKUROV     : COEFFICIENT D'ECHANGE VERTICAL DE U ET V EN KG/(M*M*S).
! PKNROV     : COEFFICIENT D'ECHANGE VERTICAL NEUTRE (U,V) EN KG/(M*M*S).
! PXTROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKTROV.
! PXUROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKUROV.
! PXPTKEROV  : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PTKEROV

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PQL        : HUMIDITE SPECIFIQUE DE L'EAU LIQUIDE.
! PQI        : HUMIDITE SPECIFIQUE DE L'EAU GLACE.
! PCOEFN     : COEFFICIENT STATISTIQUE POUR LES FLUX D'EAUX CONDENSEES.
! PTKE       : TURBULENT KINETIC ENERGY
! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATICS).
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (FOR HYDROSTATICS).
! PTENDPTKE  : TKE INCREMENT (USED BY PSEUDOPROGNOSTIC TKE SCHEME)

! - 1D (PROGNOSTIQUE) .

! PTP        : TEMPERATURE EN PROFONDEUR.
!     (KCSS)
! PTS        : TEMPERATURE DE SURFACE.
! PWS        : RESERVOIR D'EAU SUPERFICIEL.
! PWSI       : RESERVOIR D'EAU GELEE SUPERFICIEL.

! - 1D (GEOGRAPHIQUE) .

! PLSM       : INDICE TERRE/MER.
! PIVEG      : TABLEAU PERMETTANT D'IDENTIFIER LE TYPE DE VEGETATION
!              DANS CHAQUE MAILLE DU DOMAINE SIMULE.
! PVEG       : PROPORTION DE SOL COUVERT PAR LA VEGETATION.

! - 1D (DIAGNOSTIQUE) .

! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PCDROV     : PCD RENORME EN DENSITE FOIS VITESSE.
! PCHROV     : PCH RENORME EN DENSITE FOIS VITESSE.
! PCEROV     : PCE RENORME EN DENSITE FOIS VITESSE.
! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.
! PCT        : COEFFICIENT THERMIQUE DU MILIEU SOL-VEGETATION.
! PDQSTS     : DERIVEE DE PQSATS PAR RAPPORT A LA TEMPERATURE.
! PEMIS      : EMISSIVITE DE SURFACE COURANTE.
! PFHPS      : FLUX DE CHALEUR VERS LE SOL ASSOCIE AUX PRECIPITATIONS.
! PHQ        : POIDS DE L'HUMIDITE DE L'AIR DANS L'HUMIDITE DE SURFACE.
! PHU        : POIDS DE L'HUMIDITE SATURANTE DANS L'HUMIDITE DE SURFACE.
! PHTR       : RESISTANCE A LA TRANSPIRATION DU COUVERT VEGETAL.
! PHV        : RESISTANCE A L'EVAPOTRANSPIRATION DU COUVERT VEGETAL.
! PNEIJ      : PROPORTION DE SOL ENNEIGE.
! PQS        : HUMIDITE SPECIFIQUE DE SURFACE.
! PQSATS     : HUMIDITE SPECIFIQUE DE SATURATION EN SURFACE.
! PXDROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PCDROV.
! PXHROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PCHROV.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).
! PFCQNG     : FLUX FICTIF D'EAU POUR CORRIGER LES Q<0.
! PDIFTQL    : FLUX TURBULENT D'EAU LIQUIDE.
! PDIFTQI    : FLUX TURBULENT D'EAU SOLIDE.
! PSTRTU     : FLUX TURBULENT DE QUANTITE DE MOUVEMENT "U".
! PSTRTV     : FLUX TURBULENT DE QUANTITE DE MOUVEMENT "V".

! - 2D (1:KLEV) .

! PTENDPTKE  : TENDENCE D'ENERGIE CINETIQUE TURBULENTE.

! - 1D (DIAGNOSTIQUE) .

! PFCHSP     : FLUX DE CHALEUR DE LA SURFACE VERS LE SOL PROFOND.
!     (KCSS)
! PFCLL      : FLUX DE CHALEUR LATENTE SUR EAU LIQUIDE (OU SOL HUMIDE).
! PFCLN      : FLUX DE CHALEUR LATENTE SUR NEIGE (OU GLACE).
! PFCS       : FLUX DE CHALEUR SENSIBLE EN SURFACE.
! PFEVI      : FLUX DE VAPEUR D'EAU SUR SOL GELE.
! PFEVL      : FLUX DE VAPEUR D'EAU SUR EAU LIQUIDE (OU SOL HUMIDE).
! PFEVN      : FLUX DE VAPEUR D'EAU SUR NEIGE (OU GLACE).
! PFEVV      : FLUX D'EVAPOTRANSPIRATION.
! PFTR       : FLUX DE TRANSPIRATION.
! PLHS       : CHALEUR LATENTE EN SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PFRTH      : FLUX DE RAYONNEMENT THERMIQUE.

! - 1D (GEOGRAPHIQUE) .

! PGZ0F      : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE.
! PGZ0HF     : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE THERM.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        89-12, J.F. Geleyn.

!     Modifications.
!     --------------
!        02-04, LMULAF 
!        R. El Khatib : 01-08-07 Pruning options
!        03-08, modification PGZ0HF - F. Bouyssel.
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        04-02, Introduction d'une modulation du flux de chaleur dans le sol 
!               en presence de neige (NCHSP) - E. Bazile.
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        05-03, P. Marquet et J.F. Gueremy Diff. vert. des variables conserv. 
!             + F. Bouyssel.     Change in Tl computation and PDIFTQL/N -
!              Modifications introduced in CY29T2 by Y. Bouteloup
!        06-01, Remove projection on PDIFTQL,PDIFTQN - F. Bouyssel.
!        06-03, R.Brozkova - thermal roughness term over sea
!        06-03, pTKE scheme - F. Vana, J.F. Geleyn, J. Cedilnik
!        26-Sep-2006, M. Bellus - ALARO-0 phasing; reintroduction of
!               PDIFTQL/I; removed negative humidity correction PFCQNG
!               (since it is now treated in the beginning of APLPAR);
!               new input/output arguments
!        07-02, R. Brozkova - clean output of pTKE. Prepare for more general
!               computation of pTKE.
!        07-03, Introduction de PCEROV (si LFLUSO/ACFLUSO
!        08-2009, J.F. Gueremy back to negative humidity correction PFCQNG
!               dans APLPAR) J.F. Gueremy
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RPI      ,RDAY     ,RG       ,RSIGMA   ,&
 & RV       ,RCPD     ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD    ,RDT
USE YOMMCC   , ONLY : TMCC
USE YOM_YGFL , ONLY : TYPE_GFLD
USE YOMCT2   , ONLY : NSTAR2
USE YOMCT3   , ONLY : NSTEP

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TMCC)        ,INTENT(IN)    :: YDMCC
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KCSS 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRSO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKTROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKUROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKNROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXTROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXUROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXPTKEROV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCOEFN(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCD(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCDN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCDROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCEROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCT(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDQSTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEMIS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFHPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHQ(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHTR(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHU(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIVEG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSATS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTP(KLON,KCSS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVEG(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXDROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCQNG(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTQI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTKE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMU(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCHSP(KLON,KCSS) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCLL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCLN(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFEVI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFEVL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFEVN(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFEVV(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFTR(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLHS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDPTKE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFRTH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0F(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0HF(KLON) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZDSE(KLON,KLEV),ZIPOI(KLON,KLEV),ZKQRV(KLON,0:KLEV)&
 & ,ZKSRV(KLON,0:KLEV),ZKURV(KLON,0:KLEV),ZN1(KLON,KLEV),ZN2(KLON,KLEV)&
 & ,ZPOID(KLON,KLEV),ZQCOR(KLON,KLEV),ZSUB1(KLON,KLEV)&
 & ,ZSUB2(KLON,KLEV),ZXQRO(KLON,0:KLEV),ZXTRO(KLON,0:KLEV)&
 & ,ZXURO(KLON,0:KLEV),ZCHRL(KLON),ZCHRS(KLON),ZCQVQ(KLON)&
 & ,ZCQVT(KLON),ZCSDT(KLON),ZCSVT(KLON),ZCTVQ(KLON)&
 & ,ZCTVS(KLON),ZCTVT(KLON),ZDLLS(KLON),ZDLLW(KLON)&
 & ,ZDLS(KLON),ZDRCN(KLON),ZDTPL(KLON,KCSS),ZLHS(KLON)&
 & ,ZLHW(KLON),ZNTS(KLON),ZPN(KLON),ZRCOR(KLON),ZRHSQ(KLON)&
 & ,ZRHSS(KLON),ZRHST(KLON),ZSUBS(KLON),ZXDRO(KLON)&
 & ,ZXLRO(KLON),ZXSRO(KLON),ZWSI(KLON)

REAL(KIND=JPRB) :: ZLVT(KLON,0:KLEV),ZQT(KLON,KLEV),ZTL(KLON,KLEV)
REAL(KIND=JPRB) :: ZALPHA1(KLON,0:KLEV),ZCOEFA(KLON,0:KLEV),ZQICE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTKETILD(KLON,0:KLEV),ZCONVERT(KLON,0:KLEV) &
 & ,ZKERV(KLON,0:KLEV),ZTAUITKE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDBET(KLON,0:KLEV),ZBET(KLON,0:KLEV),ZKESROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTAUICONV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZNUPTKE(KLON), ZALTKE(KLON), ZLIMTKE(KLON), ZNU3TKE(KLON)

INTEGER(KIND=JPIM) :: JCSS, JLEV, JLON

REAL(KIND=JPRB) :: ZCPVMD, ZCPVMS, ZCPVMW, ZDIVIS, ZDLDQ, ZELIM,&
 & ZEPS1, ZEPS2, ZFENT, ZFQ, ZGDT, ZGDTI, ZMUL, ZPUL, ZSRVCPT2, &
 & ZMERL, ZLV, ZLS, ZICE, ZLIQ, ZTH, ZCPH, ZTLI, &
 & ZDPHI, ZRTI, ZTKES, ZEISP, ZELSP, ZQSATI, ZQSATL, ZDQSATI, ZDQSATL, &
 & ZRELAXB, ZDRELAXB, ZEPSKRV, ZEPSTKE, ZLMU
REAL(KIND=JPRB) :: ZDUP, ZDOW

REAL(KIND=JPRB) :: ZDIFTS, ZDIFTQ, ZDIFTQC, ZQCORP

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.func.h"
#include "fctdoi.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACDIFUS',0,ZHOOK_HANDLE)
ASSOCIATE(VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, VCHRNK=>YDML_PHY_MF%YRPHY0%VCHRNK, &
 & VZ0CM=>YDML_PHY_MF%YRPHY0%VZ0CM, VZIUSTAR0=>YDML_PHY_MF%YRPHY0%VZIUSTAR0, NUPTKE=>YDML_PHY_MF%YRPHY0%NUPTKE, &
 & SODELX=>YDML_PHY_MF%YRPHY1%SODELX, NTVGLA=>YDML_PHY_MF%YRPHY1%NTVGLA, NCHSP=>YDML_PHY_MF%YRPHY1%NCHSP, &
 & TMERGL=>YDML_PHY_MF%YRPHY1%TMERGL, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, LMULAF=>YDML_PHY_MF%YRPHY2%LMULAF, &
 & YTKE=>YGFL%YTKE, &
 & LPHSPSH=>YDML_PHY_MF%YRPHY%LPHSPSH, LSOLV=>YDML_PHY_MF%YRPHY%LSOLV, L3MT=>YDML_PHY_MF%YRPHY%L3MT, &
 & LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO, LPTKE=>YDML_PHY_MF%YRPHY%LPTKE, &
 & LFGEL=>YDML_PHY_MF%YRPHY%LFGEL, LCOEFK_TOMS=>YDML_PHY_MF%YRPHY%LCOEFK_TOMS, &
 & LDIFCONS=>YDML_PHY_MF%YRPHY%LDIFCONS, LMCC02=>YDMCC%LMCC02, YDPHY0=>YDML_PHY_MF%YRPHY0)
!-----------------------------------------------------------------------

!     ------------------------------------------------------------------
!     0 - FORCAGES DES FLUX DE SURFACE, DIFF VERT DES VARIABLES CONSERV.
!         SURFACE FLUXES FORCING TERMS, VERT DIFF OF CONSERV VARIABLES.

ZEPS2 = 1.E-20_JPRB

ZRELAXB=1.5_JPRB
ZDRELAXB=1._JPRB - ZRELAXB

ZEPSKRV=1.E-8_JPRB
ZEPSTKE=1.E-8_JPRB

IF ( LDIFCONS ) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      ! The conservative temperature ZZTL (Betts 1973 ; Smith 1990)
      ! and the coefficients related to it (ZCOEFA ; ZALPHA1)
      ! ZALPHA1=(dQsat/dT)(Tl) / Cp   and  ZCOEFA = PCOEFN/(1+Lv*ZALPHA1)
      ! - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      ZLV = FOLH(PT(JLON,JLEV),0.0_JPRB)
      ZLS = FOLH(PT(JLON,JLEV),1.0_JPRB)
      ZQT(JLON,JLEV) = PQ(JLON,JLEV)+PQL(JLON,JLEV)+PQI(JLON,JLEV)
      ZTL(JLON,JLEV) = PT(JLON,JLEV)&
       & -(ZLV*PQL(JLON,JLEV)+ZLS*PQI(JLON,JLEV))/PCP(JLON,JLEV)
    ENDDO
  ENDDO

!     GOING TO HALF-LEVELS FOR THE OTHER COMPUTATIONS.

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTH=(PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB
      ZLV = FOLH(ZTH,0.0_JPRB)
      ZLS = FOLH(ZTH,1.0_JPRB)
      ZQICE(JLON,JLEV) = FONICE(ZTH,YDPHY0%RDTFAC)
      ZCPH=(PCP(JLON,JLEV)+PCP(JLON,JLEV+1))*0.5_JPRB
      ZTLI=ZTH-0.5_JPRB*(ZLV*(PQL(JLON,JLEV)+PQL(JLON,JLEV+1))&
       &                +ZLS*(PQI(JLON,JLEV)+PQI(JLON,JLEV+1)))/ZCPH
      ZICE=ZQICE(JLON,JLEV)
      ZLIQ=1.0_JPRB-ZICE
      ZLVT(JLON,JLEV)=ZLV*ZLIQ+ZLS*ZICE
      ZEISP = FOEW(ZTLI,1.0_JPRB)/PAPRS(JLON,JLEV) 
      ZELSP = FOEW(ZTLI,0.0_JPRB)/PAPRS(JLON,JLEV) 
      ZQSATI = FOQS(ZEISP)
      ZQSATL = FOQS(ZELSP)
      ZSRVCPT2=1.0_JPRB/(RV*ZTLI*ZTLI*ZCPH)
      ZDQSATI=ZQSATI*FOLH(ZTLI,1.0_JPRB)*ZSRVCPT2
      ZDQSATL=ZQSATL*FOLH(ZTLI,0.0_JPRB)*ZSRVCPT2
      ZALPHA1(JLON,JLEV)=(ZLIQ*ZDQSATL+ZICE*ZDQSATI)
      ZCOEFA(JLON,JLEV)=(PCOEFN(JLON,JLEV)+PCOEFN(JLON,JLEV+1))*0.5_JPRB&
       & /((1.0_JPRB+ZDQSATL*ZLV)*ZLIQ&
       &  +(1.0_JPRB+ZDQSATI*ZLS)*ZICE)
    ENDDO
  ENDDO

ELSE

      ! - - - - - - - - - - - - - - - - - - - - - - - - -
      ! Set the "moist" variables to "dry" neutral values
      ! - - - - - - - - - - - - - - - - - - - - - - - - -
  DO JLEV = KTDIA, KLEV
   DO JLON = KIDIA, KFDIA
      ZQT(JLON,JLEV)=PQ(JLON,JLEV)
      ZTL(JLON,JLEV)=PT(JLON,JLEV)
    ENDDO
  ENDDO
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZQICE  (JLON,JLEV)=0.0_JPRB
      ZLVT   (JLON,JLEV)=0.0_JPRB
      ZALPHA1(JLON,JLEV)=0.0_JPRB
      ZCOEFA (JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.

!         AUXILIARY CONSTANTS.

ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS

!*
!     ------------------------------------------------------------------
!     II - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     CSDT).

!          COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANT (FOR
!     CSDT).

ZGDT=RG*TSPHY
ZGDTI=1.0_JPRB/ZGDT
ZPUL=2.0_JPRB*RPI/RDAY

ZEPS1=1.E-08_JPRB

IF (LMULAF) THEN
  IF (LPTKE) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        PXPTKEROV(JLON,JLEV)=SQRT(PXPTKEROV(JLON,JLEV))
      ENDDO
    ENDDO
  ENDIF
  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      PXTROV(JLON,JLEV)=MAX(PXTROV(JLON,JLEV),PXTROV(JLON,JLEV-1))
      PXUROV(JLON,JLEV)=MAX(PXUROV(JLON,JLEV),PXUROV(JLON,JLEV-1))
      PXPTKEROV(JLON,JLEV)=MAX(PXPTKEROV(JLON,JLEV),PXPTKEROV(JLON,JLEV-1))
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    PXDROV(JLON)=MAX(PXDROV(JLON),PXUROV(JLON,KLEV-1))
    PXHROV(JLON)=MAX(PXHROV(JLON),PXTROV(JLON,KLEV-1))
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     III - MISE A DES VALEURS CONSTANTES DES VALEURS DERIVEES DE
!     L'INERTIE THERMIQUE ET DU TEMPS CARACTERISTIQUE (DIURNE PAR
!     DEFINITION) DU SOL (PHYSIQUE DU SOL HYPER-SIMPLIFIEE).

!           SETTING TO CONSTANT VALUES OF THE VALUES DERIVED FROM THE
!     SOIL'S THERMAL INERTIA AND CHARACTERISTIC TIME (DIURNAL BY
!     DEFINITION) (HYPER-SIMPLIFIED SOIL PHYSICS).

! - TEMPORAIRE(S) 1D .

! ZCSDT     : PRODUIT DU PAS DE TEMPS PAR LA CONSTANTE DU SOL.
!            : PRODUCT OF THE TIME-STEP BY THE SOIL CONSTANT.
! ZDTPL     : PRODUIT DU PAS DE TEMPS PAR LA PULSATION DIURNE.
!     (KCSS)
!            : PRODUCT OF THE TIME-STEP BY THE DIURNAL PULSATION.

!     CALCULS OPTIONNELS SUIVANT LE TYPE DE SCHEMA SOL/VEG CHOISI.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN SOIL/VEG SCHEME.

IF (LSOLV) THEN
  DO JLON=KIDIA,KFDIA
    ZCSDT(JLON)=PLSM(JLON)*TSPHY*PCT(JLON)
  ENDDO
  DO JCSS=1,KCSS
    ZDIVIS=1.0_JPRB/(SODELX(0)*(SODELX(JCSS-1)+SODELX(JCSS)))
    DO JLON=KIDIA,KFDIA
      IF (NCHSP == 0) THEN
        ZDTPL(JLON,JCSS)=PLSM(JLON)*TSPHY*ZPUL*ZDIVIS
      ELSE
        ZDTPL(JLON,JCSS)=PLSM(JLON)*TSPHY*ZPUL*ZDIVIS*(1.0_JPRB-PNEIJ(JLON)&
         &**NCHSP)
      ENDIF
    ENDDO
  ENDDO
ELSE
  DO JLON=KIDIA,KFDIA
    ZCSDT(JLON)=PLSM(JLON)*TSPHY*PCT(JLON)
    ZDTPL(JLON,1)=PLSM(JLON)*TSPHY*ZPUL
  ENDDO
  DO JCSS=2,KCSS
    DO JLON=KIDIA,KFDIA
      ZDTPL(JLON,JCSS)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!*
!     -----------------------------------------------------------------
!     COMPUTATION OF PTKE RELATED VALUES

IF (LPTKE) THEN

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ZNUPTKE(JLON)=NUPTKE

      ZALTKE(JLON)=1.0_JPRB/(ZNUPTKE(JLON)*ZNUPTKE(JLON))
      ZNU3TKE(JLON)=ZNUPTKE(JLON)*ZNUPTKE(JLON)*ZNUPTKE(JLON)
      ZLIMTKE(JLON)=0.5_JPRB*ZNUPTKE(JLON)*ZNU3TKE(JLON)*ZRELAXB/(RG*RG)

      ZRTI=2._JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
      ZDPHI=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
      ZLMU=MAX(PLMU(JLON,JLEV),SQRT((ZLIMTKE(JLON)*ZDPHI/(PXPTKEROV(JLON,JLEV)&
       & *PAPRS(JLON,JLEV)*ZRTI))*MAX(PDELP(JLON,JLEV)*(PALPH(JLON,JLEV)&
       & /PLNPR(JLON,JLEV)),PDELP(JLON,JLEV+1)*(1._JPRB-PALPH(JLON,JLEV+1)&
       & /PLNPR(JLON,JLEV+1)))))
      ZCONVERT(JLON,JLEV)=PAPRS(JLON,JLEV)*ZLMU*RG*ZRTI*ZNUPTKE(JLON)/ZDPHI
      ZTAUICONV(JLON,JLEV)=TSPHY*ZNU3TKE(JLON)/(ZLMU*ZCONVERT(JLON,JLEV))
      ZKESROV(JLON,JLEV)=SQRT(PKNROV(JLON,JLEV)*PKUROV(JLON,JLEV))&
       &                 *ZLMU/PLMU(JLON,JLEV)
      ZTKETILD(JLON,JLEV)=(ZKESROV(JLON,JLEV)/ZCONVERT(JLON,JLEV))**2

      ZTKES=0.5_JPRB*(PTKE(JLON,JLEV)+PTKE(JLON,JLEV+1))

      ! CHOICE OF COEFFICIENTS FOR TKE DIFUSION AND RELAXATION
      ! THEY CAN BE COMPUTED FROM THE PREVIOUS TIMESTEP TKE VALUE
      ! OR FROM THE TILDED TKE VALUE (DIAGNOSED AT THE INSTANT
      ! TIMESTEP FROM THE EXCHANGE COEFFICIENT)

      IF ((NSTEP <= NSTAR2).AND.(YTKE%NREQIN == 0)) THEN
      ! COEFFICIENT FOR DIFFUSION CORRESPONDS TO TILDED VALUES
        ZKERV(JLON,JLEV)=ZALTKE(JLON)*ZKESROV(JLON,JLEV)

      ! COEFFICIENT FOR RELAXATION (times dt)
      ! CORRESPONDS TO TILDED VALUES
        ZTAUITKE(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZKESROV(JLON,JLEV)

      ELSE
      ! COEFFICIENT FOR DIFFUSION CORRESPONDS TO PREVIOUS TKE VALUE
        ZKERV(JLON,JLEV)=ZALTKE(JLON)*ZCONVERT(JLON,JLEV)&
         & *SQRT(MAX(ZEPSTKE,ZTKES))

      ! COEFFICIENT FOR RELAXATION (times dt)
      ! CORRESPONDS TO PREVIOUS TKE VALUE
        ZTAUITKE(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZCONVERT(JLON,JLEV)&
         & *SQRT(MAX(ZEPSTKE,ZTKES))

      ENDIF

    ENDDO
  ENDDO
ENDIF

!* !     ------------------------------------------------------------------
!     IV - CALCULS PRELIMINAIRES D'ENERGIE STATIQUE SECHE, D'HUMIDITE
!     SPECIFIQUE CORRIGEE DES VALEURS NEGATIVES PAR POMPAGE DEPUIS LE
!     BAS (SAUF DANS LE CAS DU PLUS BAS NIVEAU CONSIDERE COMME POSSEDANT
!     UNE SOURCE FICTIVE A LA SURFACE) ET DE CARACTERISTIQUE DES NIVEAUX
!     DANS L'UNITE DES COEFFICIENTS D'ECHANGE NORMALISES (KG/M**2/S)
!     POUR "POID" ET SON INVERSE POUR "IPOI".

!          PRELIMINARY COMPUTATIONS OF DRY STATIC ENERGY, SPECIFIC
!     HUMIDITY CORRECTED OF ITS NEGATIVE VALUES THROUGH UPWARDS PUMPING
!     (EXCEPT IN THE CASE OF THE LOWEST LEVEL ASSUMED TO POSSES A
!     FICTITIOUS SOURCE AT THE SURFACE) AND OF LAYERS' CHARACTERISTICS
!     IN THE UNITS OF THE NORMALIZED EXCHANGE COEFFICIENTS (KG/M**2/S)
!     FOR "POID" AND ITS INVERSE FOR "IPOI".

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZKURV     : VALEUR (DUPLIQUEE DE PKUROV) POUR LA DIFFUSION DE U,V.
!            : VALUE (DUPLICATED FROM PKUROV) FOR THE U,V DIFFUSION.
! ZXURO     : PKUROV MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : PKUROV MODIFIED TO ATTENUATE FIBRILLATIONS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZPOID     : DP/(RG*DT) POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!            : DP/(RG*DT) FOR A GIVEN LAYER AND A GIVEN TIME STEP.
! ZIPOI     : INVERSE DE ZPOID.
!            : INVERSE OF ZPOID.
! ZDSE      : TABLEAU LOCAL DES ENERGIES STATIQUE SECHES.
!            : LOCAL ARRAY FOR DRY STATIC ENERGIES.
! ZQCOR     : TABLEAU LOCAL DES Q APRES CORRECTION DES VALEURS <0.
!            : LOCAL ARRAY FOR Q AFTER CORRECTION OF NEGATIVE VALUES.

! - TEMPORAIRE(S) 1D          .

! ZXDRO     : PCDROV MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : PCDROV MODIFIED TO ATTENUATE FIBRILLATIONS.

DO JLON=KIDIA,KFDIA
  PFCQNG(JLON,KTDIA-1)=0.0_JPRB
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*ZTL(JLON,JLEV)+PAPHIF(JLON,JLEV)
    ZQCORP=ZQT(JLON,JLEV)+PFCQNG(JLON,JLEV-1)*ZIPOI(JLON,JLEV)
    PFCQNG(JLON,JLEV)=-MAX(0.0_JPRB,-ZQCORP)*ZPOID(JLON,JLEV)
    ZQCOR(JLON,JLEV)=ZQCORP+MAX(0.0_JPRB,-ZQCORP)
  ENDDO
ENDDO

!   ELIMINATION OF THE PSEUDO TKE

IF (LPTKE) THEN

!     ELIMINATION AU SOMMET.
!     ELIMINATION AT THE TOP.

  DO JLON=KIDIA,KFDIA

    ZBET(JLON,KTDIA) =          PXPTKEROV(JLON,KTDIA) *ZKERV(JLON,KTDIA)
    ZDBET(JLON,KTDIA)=(1.0_JPRB-PXPTKEROV(JLON,KTDIA))*ZKERV(JLON,KTDIA)
    ZDOW=0.5_JPRB*PALPH(JLON,KTDIA)/PLNPR(JLON,KTDIA)*ZTAUITKE(JLON,KTDIA)

    ZMUL=1.0_JPRB/(1.0_JPRB+ZBET(JLON,KTDIA)*ZIPOI(JLON,KTDIA)+ZDOW*ZRELAXB)
    ZSUB1(JLON,KTDIA)=ZMUL*(ZBET(JLON,KTDIA)*ZIPOI(JLON,KTDIA)-ZDOW*ZRELAXB)
    ZN1(JLON,KTDIA)=ZMUL*(PTKE(JLON,KTDIA)-ZIPOI(JLON,KTDIA)*ZDBET(JLON,KTDIA)&
     & *(PTKE(JLON,KTDIA)-PTKE(JLON,KTDIA+1))-ZDRELAXB*ZDOW*(PTKE(JLON,KTDIA)&
     & +PTKE(JLON,KTDIA+1))+(ZDOW+ZDOW)*ZTKETILD(JLON,KTDIA))

  ENDDO

!     ELIMINATION POUR UNE COUCHE STANDARD.
!     ELIMINATION FOR A STANDART LEVEL.

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
    
      ZBET(JLON,JLEV) =          PXPTKEROV(JLON,JLEV) *ZKERV(JLON,JLEV)
      ZDBET(JLON,JLEV)=(1.0_JPRB-PXPTKEROV(JLON,JLEV))*ZKERV(JLON,JLEV)
      ZDUP=0.5_JPRB*(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))&
       & * ZTAUITKE(JLON,JLEV-1)
      ZDOW=0.5_JPRB*PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)&
       & * ZTAUITKE(JLON,JLEV)

      ZELIM=ZBET(JLON,JLEV-1)*ZIPOI(JLON,JLEV)-ZDUP*ZRELAXB
      ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB1(JLON,JLEV-1)+(ZBET(JLON,JLEV-1)&
       & +ZBET(JLON,JLEV))*ZIPOI(JLON,JLEV)+(ZDUP+ZDOW)*ZRELAXB)
      ZSUB1(JLON,JLEV)=ZMUL*(ZBET(JLON,JLEV)*ZIPOI(JLON,JLEV)-ZDOW*ZRELAXB)
      ZN1(JLON,JLEV)=ZMUL*(PTKE(JLON,JLEV)-ZIPOI(JLON,JLEV)*(ZDBET(JLON,JLEV-1)&
       & *(PTKE(JLON,JLEV)-PTKE(JLON,JLEV-1))+ZDBET(JLON,JLEV)*(PTKE(JLON,JLEV)&
       & -PTKE(JLON,JLEV+1)))-ZDRELAXB*(ZDUP*(PTKE(JLON,JLEV)&
       & +PTKE(JLON,JLEV-1))+ZDOW*(PTKE(JLON,JLEV)+PTKE(JLON,JLEV+1)))&
       & +2.0_JPRB*(ZDUP*ZTKETILD(JLON,JLEV-1)+ZDOW*ZTKETILD(JLON,JLEV))&
       & +ZELIM*ZN1(JLON,JLEV-1))
    ENDDO
  ENDDO

!     CALCULS EN SURFACE.
!     SURFACE CALCULATIONS.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ZTKETILD(JLON,KLEV)=ZALTKE(JLON)*PCD(JLON)&
     & *(PU(JLON,KLEV)**2+PV(JLON,KLEV)**2)
    ZTAUITKE(JLON,KLEV)=TSPHY*ZNU3TKE(JLON)*RG*SQRT(ZTKETILD(JLON,KLEV))&
     & /(VKARMN*(PGZ0F(JLON)+PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)))
    ZDUP=0.5_JPRB*(1.0_JPRB-PALPH(JLON,KLEV)/PLNPR(JLON,KLEV))*&
     &  ZTAUITKE(JLON,KLEV-1)
    ZDOW=0.5_JPRB*PALPH(JLON,KLEV)/PLNPR(JLON,KLEV)*&
     &  ZTAUITKE(JLON,KLEV) 
  
    ZELIM=ZBET(JLON,KLEV-1)*ZIPOI(JLON,KLEV)-ZDUP*ZRELAXB
    ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB1(JLON,KLEV-1)+ZBET(JLON,KLEV-1)&
     & *ZIPOI(JLON,KLEV)+(ZDUP+ZDOW+ZDOW)*ZRELAXB)
    ZN1(JLON,KLEV)=ZMUL*(PTKE(JLON,KLEV)-ZIPOI(JLON,KLEV)*ZDBET(JLON,KLEV-1)&
     & *(PTKE(JLON,KLEV)-PTKE(JLON,KLEV-1))-ZDRELAXB*(ZDUP*(PTKE(JLON,KLEV)&
     & +PTKE(JLON,KLEV-1))+(ZDOW+ZDOW)*PTKE(JLON,KLEV))+2.0_JPRB*(ZDUP&
     & *ZTKETILD(JLON,KLEV-1)+ZDOW*ZTKETILD(JLON,KLEV))+ZELIM*ZN1(JLON,KLEV-1))
    PTENDPTKE(JLON,KLEV)=ZN1(JLON,KLEV)

  ENDDO

!     SUBSTITUTION POUR UNE COUCHE STANDARD ET AU SOMMET.
!     BACK-SUBSTITUTION FOR A STANDART LAYER AND AT THE TOP.

  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      ZN1(JLON,JLEV)=ZN1(JLON,JLEV)+ZSUB1(JLON,JLEV)*ZN1(JLON,JLEV+1)
      PTENDPTKE(JLON,JLEV)=ZN1(JLON,JLEV)
    ENDDO
  ENDDO

ENDIF

!     AMPLIFICATION EVENTUELLE "ANTI-FIBRILLATION" DES COEFFICIENTS
!     D'ECHANGE.
!     POTENTIAL "ANTI-FIBRILLATION" AMPLIFICATION OF THE EXCHANGE
!     COEFFICIENTS.

DO JLON=KIDIA,KFDIA
  ZXDRO(JLON)=PCDROV(JLON)*PXDROV(JLON)
ENDDO
IF (LPTKE) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZKURV(JLON,JLEV)=ZCONVERT(JLON,JLEV)*SQRT(MAX(ZEPSTKE,&
       & 0.5_JPRB*(PTENDPTKE(JLON,JLEV)+PTENDPTKE(JLON,JLEV+1))))&
       & *(PKUROV(JLON,JLEV)/ZKESROV(JLON,JLEV))
      ZXURO(JLON,JLEV)=ZKURV(JLON,JLEV)*PXUROV(JLON,JLEV)
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZKURV(JLON,JLEV)=PKUROV(JLON,JLEV)
      ZXURO(JLON,JLEV)=ZKURV(JLON,JLEV)*PXUROV(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF


!*
!     ------------------------------------------------------------------
!     V - CALCULS POUR LA QUANTITE DE MOUVEMENT : PARTIE ELIMINATION.
!     DANS TOUTE LA SUITE ON UTILISERA ZELIM POUR DESIGNER L'ELEMENT
!     SUBDIAGONAL PAR LEQUEL IL FAUT MULTIPLIER LA LIGNE PRECEDENTE POUR
!     L'ELIMINER PAR SOUSTRACTION ET ZMUL POUR DESIGNER LE FACTEUR
!     MULTIPLICATIF DESTINE A METTRE LA DIAGONALE PRINCIPALE A "1".
!     ZSUB1 ET ZSUB2 CONTIENDRONT LES COEFFICIENTS SURDIAGONAUX
!     MODIFIES QUI SERONT UTILISES LORS DE LA SUBSTITUTION (1 POUR LE
!     COUPLE U,V ET POUR S, 2 POUR Q) ET ZN1 ET ZN2 CONTIENDRONT LES
!     SECONDS MEMBRES DESTINES A DEVENIR LES VALEURS FINALES (1 POUR U
!     ET S, 2 POUR V ET Q). DANS LE CAS DE LA RESOLUTION COUPLEE POUR S
!     ET Q, ZSUBS S'INTERCALERA ENTRE ZSUB1 ET ZSUB2 INVERSE ET DE
!     MEME POUR ZNTS ENTRE ZN1 ET ZN2 INVERSE (VALEURS POUR TS).

!         COMPUTATIONS FOR MOMENTUM / ELIMINATION PART. IN ALL THE
!     FOLLOWING ONE WILL USE ZELIM TO NAME THE SUBDIAGONAL ELEMENT BY
!     WHICH ONE SHOULD MULTIPLY THE PRECEEDING LINE TO ELIMINATE IT BY
!     SUBSTRACTION AND ZMUL TO NAME THE MULTIPLYING FACTOR TO PUT THE
!     MAIN DIAGONAL TO "1". ZSUB1 AND ZSUB2 WILL CONTAIN THE MODIFIED
!     SUPERDIAGONAL COEFFICIENTS THAT WILL BE USED IN THE SUBSTITUTION
!     (1 FOR THE COUPLE U,V AND FOR S, 2 FOR Q) AND ZN1 AND ZN2 WILL
!     CONTAIN THE RIGHT HAND SIDES DUE TO BECOME THE FINAL VALUES (1 FOR
!     U AND S, 2 FOR V AND Q). IN THE CASE OF THE COUPLED SOLUTION FOR S
!     AND Q, ZSUBS WILL BE INSERTED BETWEEN ZSUB1 AND (INVERSED)
!     ZSUB2 AND THE SAME FOR ZNTS BETWEEN ZN1 AND (INVERSED) ZN2
!     (VALUES FOR TS).

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZSUB1     : TABLEAU DE COEFFICIENTS POUR LA SUBSTITUTION EN U,V ET S.
!            : ARRAY OF COEFFICIENTS FOR THE U,V AND S SUBSTITUTION.
! ZN1       : TABLEAU DE MEMBRES DE DROITE POUR U ET S (S=CP*T+PHI).
!            : ARRAY OF RIGH HAND SIDES FOR U AND S (S=CP*T+PHI).
! ZSUB2     : TABLEAU DE COEFFICIENTS POUR LA SUBSTITUTION EN Q.
!            : ARRAY OF COEFFICIENTS FOR THE Q SUBSTITUTION.
! ZN2       : TABLEAU DE MEMBRES DE DROITE POUR V ET Q.
!            : ARRAY OF RIGHT HAND SIDES FOR V AND Q.

! - TEMPORAIRE(S) 1D .

! ZSUBS     : VECTEUR DE COEFFICIENTS POUR LA SUBSTITUTION EN TS.
!            : VECTOR OF COEFFICIENTS FOR THE TS SUBSTITUTION.
! ZNTS      : VECTEUR DE MEMBRES DE DROITE POUR TS.
!            : VECTOR OF RIGHT HAND SIDES FOR TS.

!     ELIMINATION AU SOMMET.
!     ELIMINATION AT THE TOP.

DO JLON=KIDIA,KFDIA
  ZMUL=1.0_JPRB/(1.0_JPRB+ZXURO(JLON,KTDIA)*ZIPOI(JLON,KTDIA))
  ZSUB1(JLON,KTDIA)=ZMUL*ZXURO(JLON,KTDIA)*ZIPOI(JLON,KTDIA)
  ZN1(JLON,KTDIA)=ZMUL*PU(JLON,KTDIA)
  ZN2(JLON,KTDIA)=ZMUL*PV(JLON,KTDIA)
ENDDO

!     ELIMINATION POUR UNE COUCHE STANDARD.
!     ELIMINATION FOR A STANDART LEVEL.

DO JLEV=KTDIA+1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZELIM=ZXURO(JLON,JLEV-1)*ZIPOI(JLON,JLEV)
    ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB1(JLON,JLEV-1))&
     &+ZXURO(JLON,JLEV)*ZIPOI(JLON,JLEV))
    ZSUB1(JLON,JLEV)=ZMUL*ZXURO(JLON,JLEV)*ZIPOI(JLON,JLEV)
    ZN1(JLON,JLEV)=ZMUL*(PU(JLON,JLEV)+ZELIM*ZN1(JLON,JLEV-1))
    ZN2(JLON,JLEV)=ZMUL*(PV(JLON,JLEV)+ZELIM*ZN2(JLON,JLEV-1))
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     VI - CALCULS POUR LA QUANTITE DE MOUVEMENT : CONDITION DE SURFACE,
!     CALCUL DE "CHARNOCK" PUIS SUBSTITUTION AVEC CALCUL DES FLUX A TOUS
!     LES NIVEAUX.

!          COMPUTATION FOR MOMENTUM : SURFACE CONDITION, "CHARNOCK-TYPE"
!     CALCULATION THEN SUBSTITUTION WITH FLUXES' COMPUTATION AT ALL
!     LEVELS.

!     CALCULS EN SURFACE.
!     SURFACE CALCULATIONS.

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA
  ZELIM=ZXURO(JLON,KLEV-1)*ZIPOI(JLON,KLEV)
  ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB1(JLON,KLEV-1))+ZXDRO(JLON)&
   & *ZIPOI(JLON,KLEV))  
  ZN1(JLON,KLEV)=ZMUL*(PU(JLON,KLEV)+ZELIM*ZN1(JLON,KLEV-1))
  ZN2(JLON,KLEV)=ZMUL*(PV(JLON,KLEV)+ZELIM*ZN2(JLON,KLEV-1))
  ZMERL=(1.0_JPRB-PLSM(JLON))*(1.0_JPRB&
   & -MAX(0.0_JPRB,SIGN(1.0_JPRB,TMERGL-PTS(JLON))))  
  PGZ0F(JLON)=PGZ0F(JLON)+ZMERL&
   & *(RG*VZ0CM*PCD(JLON)/PCDN(JLON)+VCHRNK*PCD(JLON)&
   & *SQRT((ZN1(JLON,KLEV)**2+ZN2(JLON,KLEV)**2)&
   & *(PU(JLON,KLEV)**2+PV(JLON,KLEV)**2))-PGZ0F(JLON))  
  PGZ0HF(JLON)=PGZ0HF(JLON)+ZMERL&
   &*(PGZ0F(JLON)*EXP(-SQRT(PCD(JLON)&
   &*SQRT((ZN1(JLON,KLEV)**2+ZN2(JLON,KLEV)**2)&
   &*(PU(JLON,KLEV)**2+PV(JLON,KLEV)**2)))*VZIUSTAR0)-PGZ0HF(JLON))
  PSTRTU(JLON,KLEV)=PCDROV(JLON)*ZN1(JLON,KLEV)
  PSTRTV(JLON,KLEV)=PCDROV(JLON)*ZN2(JLON,KLEV)
ENDDO

!     SUBSTITUTION POUR UNE COUCHE STANDARD ET AU SOMMET.
!     BACK-SUBSTITUTION FOR A STANDART LAYER AND AT THE TOP.

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZN1(JLON,JLEV)=ZN1(JLON,JLEV)+ZSUB1(JLON,JLEV)*ZN1(JLON,JLEV+1)
    ZN2(JLON,JLEV)=ZN2(JLON,JLEV)+ZSUB1(JLON,JLEV)*ZN2(JLON,JLEV+1)
    PSTRTU(JLON,JLEV)=ZKURV(JLON,JLEV)*(ZN1(JLON,JLEV)-ZN1(JLON,JLEV+1))
    PSTRTV(JLON,JLEV)=ZKURV(JLON,JLEV)*(ZN2(JLON,JLEV)-ZN2(JLON,JLEV+1))
  ENDDO
ENDDO

!     CONDITION A LA LIMITE SUPERIEURE.
!     UPPER BOUNDARY CONDITION.

DO JLON=KIDIA,KFDIA
  PSTRTU(JLON,KTDIA-1)=0.0_JPRB
  PSTRTV(JLON,KTDIA-1)=0.0_JPRB
ENDDO

!*
!     ------------------------------------------------------------------
!     VII - CALCULS PRELIMINAIRES AU SOL POUR LE COUPLAGE DES SOLUTIONS
!     IMPLICITES EN S ET Q A TRAVERS LA VALEUR DE TS (ON AURA DEUX
!     VALEURS, A PRIORI EGALES, DU COEFFICIENT CH NORMALISE POUR LES
!     ETUDES DE SENSIBILITE AUX CHALEURS LATENTES/SENSIBLES).

!           PRELIMINARY COMPUTATIONS AT THE SURFACE FOR THE COUPLING OF
!     THE IMPLICIT SOLUTIONS IN S AND Q THROUGH THE TS VALUE (ONE WILL
!     HAVE TWO VALUES, A PRIORI IDENTICAL, OF THE NORMALIZED CH
!     COEFFICIENT TO ALLOW SENSITIVITY STUDIES FOR SENSIBLE/LATENT
!     HEATS).

DO JLON=KIDIA,KFDIA

!     CALCULS CORRESPONDANT A EVAPORATION/SUBLIMATION.
!     COMPUTATIONS CORRESPONDING TO EVAPORATION/SUBLIMATION.

! - TEMPORAIRE(S) 1D .

! ZPN       : PROPORTION DE LA SURFACE EVAPORANT EN PHASE GLACE.
!           : PROPORTION OF THE SURFACE EVAPORATING IN ICE PHASE.
! ZWSI      : PROPORTION DE GLACE DANS LE RESERVOIR DE SURFACE.
!           : PROPORTION OF ICE IN THE SUPERFICIAL RESERVOIR.

  IF (LNEIGE) THEN
    IF (LFGEL) THEN
      ZWSI(JLON)=MIN(1.0_JPRB,PWSI(JLON)/MAX(PWSI(JLON)+PWS(JLON),ZEPS1))
    ELSE
      ZWSI(JLON)=0.0_JPRB
    ENDIF
    ZPN(JLON)=PLSM(JLON)*(PNEIJ(JLON)+&
     & (1.0_JPRB-PNEIJ(JLON))*ZWSI(JLON))+&
     & (1.0_JPRB-PLSM(JLON))*MAX(0.0_JPRB,SIGN(1.0_JPRB,TMERGL-PTS(JLON)))  
    IF(LSOLV.AND.LMCC02)THEN
      IF(NINT(PIVEG(JLON)) == NTVGLA) ZPN(JLON)=1.0_JPRB
    ENDIF
  ELSE
    ZPN(JLON)=0.0_JPRB
  ENDIF

!     CALCUL DE LA CHALEUR LATENTE PONDEREE ET DE SES DEUX CONTRIBUTIONS
!     PAR APPEL A LA FONCTION DEFINIE.
!     COMPUTATION OF THE WEIGHTED LATENT HEAT AND OF ITS TWO COMPONENTS
!     THROUGH CALLS TO THE DEFINED FUNCTION.

! - TEMPORAIRE(S) 1D .

! ZLHW      : CONTRIBUTION DE LA PHASE LIQUIDE A LA CHALEUR LATENTE.
!            : CONTRIBUTION OF THE LIQUID PHASE TO THE LATENT HEAT.
! ZLHS      : CONTRIBUTION DE LA PHASE GLACE A LA CHALEUR LATENTE.
!            : CONTRIBUTION OF THE ICE PHASE TO THE LATENT HEAT.

  ZLHW(JLON)=(1.0_JPRB-ZPN(JLON))* FOLH (PTS(JLON),0.0_JPRB)
  ZLHS(JLON)=ZPN(JLON)* FOLH (PTS(JLON),1.0_JPRB)
  PLHS(JLON)=ZLHW(JLON)+ZLHS(JLON)

!     DIFFERENTIATION DU COEFFICIENT D'ECHANGE EN SURFACE ENTRE CHALEUR
!     LATENTE/SENSIBLE.
!     DIFFERENTIATION BETWEEN THE SURFACE EXCHANGE COEFFICIENTS FOR
!     LATENT/SENSIBLE HEAT.

!     AMPLIFICATION EVENTUELLE "ANTI-FIBRILLATION" DES COEFFICIENTS
!     D'ECHANGE.
!     POTENTIAL "ANTI-FIBRILLATION" AMPLIFICATION OF THE EXCHANGE
!     COEFFICIENTS.

! - TEMPORAIRE(S) 1D .

! ZCHRL     : COEFFICIENT D'ECHANGE PCHROV POUR LA CHALEUR LATENTE.
!            : EXCHANGE COEFFICIENT PCHROV FOR LATENT HEAT.
! ZCHRS     : COEFFICIENT D'ECHANGE PCHROV POUR LA CHALEUR SENSIBLE.
!            : EXCHANGE COEFFICIENT PCHROV FOR SENSIBLE HEAT.
! ZXLRO     : ZCHRL MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : ZCHRL MODIFIED TO ATTENUATE FIBRILLATIONS.
! ZXSRO     : ZCHRS MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : ZCHRS MODIFIED TO ATTENUATE FIBRILLATIONS.

  ZCHRL(JLON)=PCEROV(JLON)
  ZXLRO(JLON)=ZCHRL(JLON)*PXHROV(JLON)
  ZCHRS(JLON)=PCHROV(JLON)
  ZXSRO(JLON)=ZCHRS(JLON)*PXHROV(JLON)

!     CONTRIBUTION AUX COEFFICIENTS DE LA MATRICE TRIDIAGONALE.
!     CONTRIBUTION TO THE COEFFICIENTS OF THE TRIDIAGONAL MATRIX.

! - TEMPORAIRE(S) 1D .

! ZDLS      : DERIVEE EN TEMPERATURE DU FLUX TOTAL ; PARTIE SENSIBLE.
!            : TEMPERATURE DERIVATIVE OF THE TOTAL FLUX ; SENSIBLE PART.
! ZDLLW     : COMME ZDLS ; MOINS PARTIE LATENTE, PHASE LIQUIDE.
!            : AS ZDLS ; MINUS LATENT PART, LIQUID PHASE.
! ZDLLS     : COMME ZDLS ; MOINS PARTIE LATENTE, PHASE GLACE.
!            : AS ZDLS ; MINUS LATENT PART, ICE PHASE.
! ZDRCN     : MOINS DERIVEE EN TEMPERATURE DU FLUX DE RAYONNEMENT.
!            : MINUS TEMPERATURE DERIVATIVE OF THE RADIATIVE FLUX.
! ZCQVQ     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT Q A Q.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING Q TO Q.
! ZCTVQ     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT TS A Q.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING TS TO Q.
! ZRHSQ     : CONTRIBUTION AU MEMBRE DE DROITE POUR LA LIGNE EN Q.
!            : CONTRIBUTION TO THE RIGHT HAND SIDE FOR THE Q LINE.
! ZCTVS     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT TS A S.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING TS TO S.
! ZRHSS     : CONTRIBUTION AU MEMBRE DE DROITE POUR LA LIGNE EN S.
!            : CONTRIBUTION TO THE RIGHT HAND SIDE FOR THE S LINE.
! ZCSVT     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT S A TS.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING S TO TS.
! ZCTVT     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT TS A TS.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING TS TO TS.
! ZCQVT     : CONTRIBUTION AU COEFFICIENT MATRICIEL LIANT Q A TS.
!            : CONTRIBUTION TO THE MATRIX COEFFICIENT LINKING Q TO TS.
! ZRHST     : CONTRIBUTION AU MEMBRE DE DROITE POUR LA LIGNE EN TS.
!            : CONTRIBUTION TO THE RIGHT HAND SIDE FOR THE TS LINE.

  ZDLS(JLON)=(PQS(JLON)-PQ(JLON,KLEV))*ZCHRS(JLON)*ZCPVMD
  ZDLLW(JLON)=(PQS(JLON)-PQ(JLON,KLEV))*ZCHRL(JLON)*ZCPVMW*(1.0_JPRB-ZPN(JLON))
  ZDLLS(JLON)=(PQS(JLON)-PQ(JLON,KLEV))*ZCHRL(JLON)*ZCPVMS*ZPN(JLON)
  ZDLDQ=ZDLLW(JLON)+ZDLLS(JLON)-ZDLS(JLON)
  ZDRCN(JLON)=PEMIS(JLON)*4._JPRB*RSIGMA*PTS(JLON)*PTS(JLON)*PTS(JLON)
  ZCQVQ(JLON)=1.0_JPRB-PHQ(JLON)
  ZCTVQ(JLON)=PHU(JLON)*PDQSTS(JLON)
  ZRHSQ(JLON)=PHU(JLON)*(PQSATS(JLON)-PTS(JLON)*PDQSTS(JLON))
  ZCTVS(JLON)=PCPS(JLON)+ZCPVMD*PTS(JLON)*ZCTVQ(JLON)
  ZRHSS(JLON)=PAPHI(JLON,KLEV)-ZCPVMD*PTS(JLON)*ZCTVQ(JLON)*PTS(JLON)
  ZCSVT(JLON)=ZCHRS(JLON)*ZCSDT(JLON)
  ZCSVT(JLON)=ZCSVT(JLON)*PXHROV(JLON)
  ZCTVT(JLON)=ZDTPL(JLON,1)+ZCSDT(JLON)*(ZDRCN(JLON)+ZDLDQ&
   & +ZCHRL(JLON)*PLHS(JLON)*ZCTVQ(JLON)+ZCHRS(JLON)&
   & *PCPS(JLON))  
  ZCTVT(JLON)=ZCTVT(JLON)*PXHROV(JLON)
  ZCQVT(JLON)=ZCQVQ(JLON)*ZCSDT(JLON)*(ZCHRL(JLON)*PLHS(JLON)&
   & -ZCHRS(JLON)*ZCPVMD*PTS(JLON))  
  ZCQVT(JLON)=ZCQVT(JLON)*PXHROV(JLON)
  ZRHST(JLON)=ZDTPL(JLON,1)*PTP(JLON,1)+ZCSDT(JLON)&
   & *(PFRSO(JLON,KLEV)+PFRTH(JLON,KLEV)+(ZDRCN(JLON)&
   & +ZDLDQ)*PTS(JLON)-ZCHRL(JLON)*PLHS(JLON)&
   & *ZRHSQ(JLON)-ZCHRS(JLON)*(PAPHI(JLON,KLEV)-ZCPVMD&
   & *PHU(JLON)*PTS(JLON)*PQSATS(JLON)))  
   IF (LPHSPSH) THEN
     ZRHST(JLON)=ZRHST(JLON)+PFHPS(JLON)*ZCSDT(JLON)
   ENDIF
  ZRHST(JLON)=ZRHST(JLON)*PXHROV(JLON)
ENDDO

!*
!     ------------------------------------------------------------------
!     VIII - CALCULS POUR LA TEMPERATURE ET L'HUMIDITE SPECIFIQUE :
!     PARTIE ELIMINATION. COMME A LA SURFACE ON DIFFERENTIE LES
!     COEFFICIENTS NORMALISES (A PRIORI EGAUX) POUR S ET Q.

!            COMPUTATION FOR TEMPERATURE AND SPECIFIC HUMIDITY :
!     ELIMINATION PART. LIKE AT THE SURFACE ONE DIFFERENTIATES THE
!     (A PRIORI EQUAL) NORMALIZED COEFFICIENTS FOR S AND Q.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZKQRV     : VALEUR (DUPLIQUEE DE PKTROV) POUR LA DIFFUSION DE Q.
!            : VALUE (DUPLICATED FROM PKTROV) FOR THE Q DIFFUSION.
! ZKSRV     : VALEUR (DUPLIQUEE DE PKTROV) POUR LA DIFFUSION DE S.
!            : VALUE (DUPLICATED FROM PKTROV) FOR THE S DIFFUSION.
! ZXQRO     : ZKQRV MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : ZKQRV MODIFIED TO ATTENUATE FIBRILLATIONS.
! ZXTRO     : ZKSRV MODIFIE POUR ATTENUER LES FIBRILLATIONS.
!            : ZKSRV MODIFIED TO ATTENUATE FIBRILLATIONS.

!     DUPLICATION DES COEFFICIENTS D'ECHANGE VERTICAL DE S POUR Q.
!     DUPLICATION OF THE VERTICAL EXCHANGE COEFFICIENTS FOR S AND Q.

!     AMPLIFICATION EVENTUELLE "ANTI-FIBRILLATION" DES COEFFICIENTS
!     D'ECHANGE.
!     POTENTIAL "ANTI-FIBRILLATION" AMPLIFICATION OF THE EXCHANGE
!     COEFFICIENTS.

IF (LPTKE) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZKQRV(JLON,JLEV)=ZKURV(JLON,JLEV)*(PKTROV(JLON,JLEV)/MAX(ZEPSKRV,&
       & PKUROV(JLON,JLEV)))
      ZXQRO(JLON,JLEV)=ZKQRV(JLON,JLEV)*PXTROV(JLON,JLEV)
      ZKSRV(JLON,JLEV)=ZKURV(JLON,JLEV)*(PKTROV(JLON,JLEV)/MAX(ZEPSKRV,&
       & PKUROV(JLON,JLEV)))
      ZXTRO(JLON,JLEV)=ZKSRV(JLON,JLEV)*PXTROV(JLON,JLEV)
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZKQRV(JLON,JLEV)=PKTROV(JLON,JLEV)
      ZXQRO(JLON,JLEV)=ZKQRV(JLON,JLEV)*PXTROV(JLON,JLEV)
      ZKSRV(JLON,JLEV)=PKTROV(JLON,JLEV)
      ZXTRO(JLON,JLEV)=ZKSRV(JLON,JLEV)*PXTROV(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!     ELIMINATION AU SOMMET POUR L'ENERGIE STATIQUE SECHE, S.
!     TOP ELIMINATION FOR THE DRY STATIC ENERGY, S.

DO JLON=KIDIA,KFDIA
  ZMUL=1.0_JPRB/(1.0_JPRB+ZXTRO(JLON,KTDIA)*ZIPOI(JLON,KTDIA))
  ZSUB1(JLON,KTDIA)=ZMUL*ZXTRO(JLON,KTDIA)*ZIPOI(JLON,KTDIA)
  ZN1(JLON,KTDIA)=ZMUL*ZDSE(JLON,KTDIA)
ENDDO

!     ELIMINATION POUR UNE COUCHE STANDARD POUR S.
!     ELIMINATION FOR A STANDART LAYER FOR S.

DO JLEV=KTDIA+1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZELIM=ZXTRO(JLON,JLEV-1)*ZIPOI(JLON,JLEV)
    ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB1(JLON,JLEV-1))&
     &+ZXTRO(JLON,JLEV)*ZIPOI(JLON,JLEV))
    ZSUB1(JLON,JLEV)=ZMUL*ZXTRO(JLON,JLEV)*ZIPOI(JLON,JLEV)
    ZN1(JLON,JLEV)=ZMUL*(ZDSE(JLON,JLEV)+ZELIM*ZN1(JLON,JLEV-1))
  ENDDO
ENDDO

!     CALCULS D'ELIMINATION EN SURFACE POUR S, TS ET Q.
!     SURFACE ELIMINATION CALCULATION FOR S, TS AND Q.

DO JLON=KIDIA,KFDIA
  ZELIM=ZXTRO(JLON,KLEV-1)*ZIPOI(JLON,KLEV)
  ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB1(JLON,KLEV-1))+ZXSRO(JLON)&
   & *ZIPOI(JLON,KLEV))  
  ZSUB1(JLON,KLEV)=ZMUL*ZXSRO(JLON)*ZIPOI(JLON,KLEV)*ZCTVS(JLON)
  ZN1(JLON,KLEV)=ZMUL*(ZDSE(JLON,KLEV)+ZXSRO(JLON)&
   & *ZIPOI(JLON,KLEV)*ZRHSS(JLON)+ZELIM&
   & *ZN1(JLON,KLEV-1))  
  ZELIM=ZCSVT(JLON)
  ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB1(JLON,KLEV)+ZCTVT(JLON))
  ZSUBS(JLON)=ZMUL*ZCQVT(JLON)
  ZNTS(JLON)=ZMUL*(PTS(JLON)+ZRHST(JLON)+ZELIM*ZN1(JLON,KLEV))
  ZELIM=ZXLRO(JLON)*ZIPOI(JLON,KLEV)*ZCTVQ(JLON)
  ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUBS(JLON)+ZXLRO(JLON)*ZIPOI(JLON,KLEV)&
   & *ZCQVQ(JLON)+ZXQRO(JLON,KLEV-1)*ZIPOI(JLON,KLEV))  
  ZSUB2(JLON,KLEV)=ZMUL*ZXQRO(JLON,KLEV-1)*ZIPOI(JLON,KLEV)
  ZN2(JLON,KLEV)=ZMUL*(ZQCOR(JLON,KLEV)+ZXLRO(JLON)&
   & *ZIPOI(JLON,KLEV)*ZRHSQ(JLON)+ZELIM*ZNTS(JLON))  
ENDDO

!     ELIMINATION POUR UNE COUCHE STANDARD POUR L'HUMIDITE SPECIFIQUE
!     CORRIGEE DES VALEURS NEGATIVES, Q.
!     ELIMINATION FOR A STANDART LAYER FOR THE SPECIFIC HUMIDITY
!     CORRECTED FROM NEGATIVE VALUES, Q.

DO JLEV=KLEV-1,KTDIA+1,-1
  DO JLON=KIDIA,KFDIA
    ZELIM=ZXQRO(JLON,JLEV)*ZIPOI(JLON,JLEV)
    ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB2(JLON,JLEV+1))+ZXQRO(JLON,JLEV-1)&
     & *ZIPOI(JLON,JLEV))  
    ZSUB2(JLON,JLEV)=ZMUL*ZXQRO(JLON,JLEV-1)*ZIPOI(JLON,JLEV)
    ZN2(JLON,JLEV)=ZMUL*(ZQCOR(JLON,JLEV)+ZELIM*ZN2(JLON,JLEV+1))
  ENDDO
ENDDO

!     ELIMINATION AU SOMMET POUR Q.
!     TOP ELIMINATION FOR Q.

DO JLON=KIDIA,KFDIA
  ZELIM=ZXQRO(JLON,KTDIA)*ZIPOI(JLON,KTDIA)
  ZMUL=1.0_JPRB/(1.0_JPRB+ZELIM*(1.0_JPRB-ZSUB2(JLON,KTDIA+1)))
  ZN2(JLON,KTDIA)=ZMUL*(ZQCOR(JLON,KTDIA)+ZELIM*ZN2(JLON,KTDIA+1))
ENDDO

!*
!     ------------------------------------------------------------------
!     IX - CALCULS POUR LA TEMPERATURE ET L'HUMIDITE SPECIFIQUE : PARTIE
!     SUBSTITUTION ET CALCUL DES FLUX A TOUS LES NIVEAUX AINSI QU'EN
!     SURFACE ET DANS LE SOL.

!          COMPUTATIONS FOR TEMPERATURE AND SPECIFIC HUMIDITY :
!     BACK-SUBSTITUTION PART AND CALCULATION OF FLUXES AT ALL LEVELS AS
!     WELL AS AT THE SURFACE AND IN THE SOIL.

!     SUBSTITUTION POUR UNE COUCHE STANDARD POUR Q.
!     BACK-SUBSTITUTION FOR A STANDART LEVEL FOR Q.

DO JLEV=KTDIA+1,KLEV
  DO JLON=KIDIA,KFDIA
    ZN2(JLON,JLEV)=ZN2(JLON,JLEV)+ZSUB2(JLON,JLEV)*ZN2(JLON,JLEV-1)
  ENDDO
ENDDO

!     CALCULS DE SUBSTITUTION EN SURFACE POUR TS ET S.
!     SURFACE BACK-SUBSTITUTION CALCULATIONS FOR TS AND S.

DO JLON=KIDIA,KFDIA
  ZNTS(JLON)=ZNTS(JLON)+ZSUBS(JLON)*ZN2(JLON,KLEV)
  ZN1(JLON,KLEV)=ZN1(JLON,KLEV)+ZSUB1(JLON,KLEV)*ZNTS(JLON)
ENDDO

!     SUBSTITUTION POUR UNE COUCHE STANDARD ET AU SOMMET POUR S.
!     BACK-SUBSTITUTION FOR A STANDART LEVEL AND AT THE TOP FOR S.

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZN1(JLON,JLEV)=ZN1(JLON,JLEV)+ZSUB1(JLON,JLEV)*ZN1(JLON,JLEV+1)
  ENDDO
ENDDO

!     FLUX EN SURFACE ET DANS LE SOL.
!     SURFACE AND SOIL FLUXES.

! - TEMPORAIRE(S) 1D .

! ZRCOR     : CORRECTION UNIFORME DU RAYONNEMENT THERMIQUE.
!            : UNIFORM CORRECTION FOR THE THERMAL RADIATION FLUX.

DO JLON=KIDIA,KFDIA
  ZFQ=ZCHRL(JLON)*(ZCQVQ(JLON)*ZN2(JLON,KLEV)-ZCTVQ(JLON)&
   & *ZNTS(JLON)-ZRHSQ(JLON))  
  PFEVI(JLON)=ZFQ*ZWSI(JLON)*(1.0_JPRB-PNEIJ(JLON))
  PFEVL(JLON)=ZFQ*(1.0_JPRB-ZPN(JLON))
  PFEVN(JLON)=ZFQ*ZPN(JLON)
  PFCLL(JLON)=ZLHW(JLON)*ZFQ-ZDLLW(JLON)*(ZNTS(JLON)-PTS(JLON))
  PFCLN(JLON)=ZLHS(JLON)*ZFQ-ZDLLS(JLON)*(ZNTS(JLON)-PTS(JLON))
  PFCHSP(JLON,1)=(ZNTS(JLON)-PTP(JLON,1))*ZDTPL(JLON,1)&
   & /MAX(ZEPS1,ZCSDT(JLON))  
  ZFENT=ZCHRS(JLON)*(ZN1(JLON,KLEV)-ZCTVS(JLON)*ZNTS(JLON)-ZRHSS(JLON))
  PFCS(JLON)=ZFENT-ZCPVMD*PTS(JLON)*ZFQ+ZDLS(JLON)*(ZNTS(JLON)-PTS(JLON))
  ZRCOR(JLON)=-ZDRCN(JLON)*(ZNTS(JLON)-PTS(JLON))
  PFRTH(JLON,KLEV)=PFRTH(JLON,KLEV)+ZRCOR(JLON)
  PDIFTQ(JLON,KLEV)=ZFQ+PFCQNG(JLON,KLEV)
  PDIFTS(JLON,KLEV)=ZFENT

!     CALCULS OPTIONNELS POUR UN TYPE DE SCHEMA SOL/VEG.
!     OPTIONAL COMPUTATIONS FOR ONE SOIL/VEG SCHEME.

  IF (LSOLV) THEN
    PFEVV(JLON)=ZCHRL(JLON)*PVEG(JLON)*(PHV(JLON)-PNEIJ(JLON))&
     & *(ZN2(JLON,KLEV)-PQSATS(JLON)-PDQSTS(JLON)&
     & *(ZNTS(JLON)-PTS(JLON)))  
    PFTR(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,PQSATS(JLON)-PQ(JLON,KLEV)))&
     & *ZCHRL(JLON)*PHTR(JLON)*PVEG(JLON)*(1.0_JPRB-PNEIJ(JLON))&
     & *(ZN2(JLON,KLEV)-PQSATS(JLON)-PDQSTS(JLON)&
     & *(ZNTS(JLON)-PTS(JLON)))  
  ENDIF

ENDDO

!     AUTRES COUCHES PROFONDES (S'IL Y EN A).
!     OTHER DEEP LAYERS (IF ANY).

DO JCSS=2,KCSS
  DO JLON=KIDIA,KFDIA
    PFCHSP(JLON,JCSS)=(PTP(JLON,JCSS-1)-PTP(JLON,JCSS))&
     & *ZDTPL(JLON,JCSS)/MAX(ZEPS1,ZCSDT(JLON))  
  ENDDO
ENDDO

!     FLUX ATMOSPHERIQUES.
!     ATMOSPHERIC FLUXES.

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)+ZRCOR(JLON)
  ENDDO
ENDDO

IF ( (L3MT.OR.LSTRAPRO) .AND. LDIFCONS .AND. (.NOT. LCOEFK_TOMS) ) THEN

  !     ON PASSE DES VARIABLES "CONSERVATIVES" AUX VARIABLES "SECHES".
  !     FROM "MOIST AND  CONSERVATIVE" VARIABLES TO THE "DRY" ONES.

  DO JLEV = KLEV-1,KTDIA,-1
    DO JLON = KIDIA, KFDIA
      ZDIFTS=ZKSRV(JLON,JLEV)*(ZN1(JLON,JLEV)-ZN1(JLON,JLEV+1))
      ZDIFTQ=ZKQRV(JLON,JLEV)*(ZN2(JLON,JLEV)-ZN2(JLON,JLEV+1))&
       & +PFCQNG(JLON,JLEV)
      ZDIFTQC=ZCOEFA(JLON,JLEV)*(ZDIFTQ-ZALPHA1(JLON,JLEV)*ZDIFTS)
      PDIFTS(JLON,JLEV)=ZDIFTS+ZDIFTQC*ZLVT(JLON,JLEV)
      PDIFTQ(JLON,JLEV)=ZDIFTQ-ZDIFTQC
      PDIFTQL(JLON,JLEV)=ZDIFTQC*(1.0_JPRB-ZQICE(JLON,JLEV))
      PDIFTQI(JLON,JLEV)=ZDIFTQC*ZQICE(JLON,JLEV)
    ENDDO
  ENDDO

ELSE

  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      PDIFTS(JLON,JLEV)=ZKSRV(JLON,JLEV)*(ZN1(JLON,JLEV)-ZN1(JLON,JLEV+1))
      PDIFTQ(JLON,JLEV)=ZKQRV(JLON,JLEV)*(ZN2(JLON,JLEV)-ZN2(JLON,JLEV+1))&
       & +PFCQNG(JLON,JLEV)
      PDIFTQL(JLON,JLEV)=0.0_JPRB
      PDIFTQI(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO

ENDIF

!     CONDITION A LA LIMITE SUPERIEURE.
!     UPPER BOUNDARY CONDITION.

DO JLON=KIDIA,KFDIA
  PFRTH(JLON,KTDIA-1)=PFRTH(JLON,KTDIA-1)+ZRCOR(JLON)
  PDIFTQ(JLON,KTDIA-1)=0.0_JPRB
  PDIFTS(JLON,KTDIA-1)=0.0_JPRB
  PDIFTQL(JLON,KTDIA-1)=0.0_JPRB
  PDIFTQI(JLON,KTDIA-1)=0.0_JPRB
ENDDO

!     CONDITIONS A LA LIMITE INFERIEURE.
!     LOWER BOUNDARY CONDITIONS.

DO JLON=KIDIA,KFDIA
  PDIFTQL(JLON,KLEV)=0.0_JPRB
  PDIFTQI(JLON,KLEV)=0.0_JPRB
ENDDO

IF (LPTKE) THEN

!     UPDATE OF EXCHANGE COEFFICIENTS

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      PKUROV(JLON,JLEV)=ZKURV(JLON,JLEV)
      PKTROV(JLON,JLEV)=ZKSRV(JLON,JLEV)
    ENDDO
  ENDDO

!     pTKE TENDENCY EVALUATION (PTENDPTKE=(PTKE1-PTKE0)/DT)

  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      PTENDPTKE(JLON,JLEV)=(PTENDPTKE(JLON,JLEV)-PTKE(JLON,JLEV))/TSPHY
    ENDDO
  ENDDO

ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACDIFUS',1,ZHOOK_HANDLE)
END SUBROUTINE ACDIFUS
