!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACDIFV3 ( YDML_PHY_MF,                            &
 & KIDIA, KFDIA, KLON, KTDIA, KLEV, KSTEP,                   &
 & PAPRS, PAPHI, PAPHIF, PCP, PDELP, PKTROV, PXTROV,         &
 & PR, PRDELP, PCHROV, PXHROV,                               &
 & PNEBQ, PQ, PQL, PQI, PU, PV, PT, PALPH, PLNPR,            &
 & PLML, PTH_FUN, PWW_FUN, PF_EPS,                           &
 & PDIFTS, PDIFTQ, PTKE, PTENDPTKE,                          &
 & PMN2_ES, PMN2_EQ, PMN2_DS, PMN2_DQ,                       &
 & PDIFTQL, PDIFTQI,                                         &
 & PDIFWQ, PDIFWS,                                           &  
 & PFCLL, PFCLN, PFCS, PFEVI, PFEVL, PFEVN,                  &
 & PGZ0, PRTI,                                               &
 & PSC_FEVI, PSC_FEVN, PSC_FCLL, PSC_FCLN,                   &
 & PTSTAR, PTSTAR2, PTSTARQ, PTSTAR2Q )

!-------------------------------------------------------------------------------
! ACDIFV3 - TOM TERMS CORRECTION
!-------------------------------------------------------------------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!       F. Vana and J.-F. Geleyn, February 2011

!     Modifications.
!     --------------
!      J.-F. Geleyn    Mar-2011: option LDIFCONS
!      M. Jerczynski   Jun-2011: some cleaning to meet norms
!      I. Bastak-Duran, F. Vana     Jan-2012: stabilization and various fixes
!      I. Bastak-Duran, R. Brozkova Jan-2013: TOMs computations
!      I. Bastak-Duran Oct-2014: Major rewriting.
!      P. Smerkol      Sep-2016: Cleaning.
!      P. Smerkol      Sep-2019: Renaming of variables, cleaning, minor bug fixes.
!      R. Brozkova     Sep-2019: Use of Betts' temperature for ice/liquid
!                      partition in diffusion of conservative variables.
!-------------------------------------------------------------------------------
! REMARK: In cy47t1 phasing, some fixes are not yet activated. Identity
! with cy47t0 version can be obtained by commenting lines between
! !NEW{ and !}NEW and uncommenting lines between !OLD{ and !}OLD.
! Old lines will disappear in some later version.
!-------------------------------------------------------------------------------

!--- INPUT ARGUMENTS -----------------------------------------------------------

! DIMENSIONS

! KIDIA      : START OF HORIZONTAL LOOP
! KFDIA      : END OF HORIZONTAL LOOP
! KLON       : HORIZONTAL DIMENSION
! KTDIA      : START OF THE VERTICAL LOOP IN PHYSICS (IF SOME LEVELS ARE SKIPPED AT THE TOP OF THE MODEL)
! KLEV       : END OF VERTICAL LOOP AND VERTICAL DIMENSION
! KSTEP      : NUMBER OF CURRENT TIME STEP                       

! PHYSICAL VARIABLES (ALPHABETICALLY FOR EACH CATEGORY)

! 1D 

! PCHROV     : EXCHANGE COEFFICIENT AT SURFACE FOR T AND Q RENORMALIZED WITH DENSITY AND VELOCITY
! PGZ0       : G TIMES ROUGHNESS LENGTH (CURRENT)
! PRTI       : INVERSE OF R*T AT SURFACE
! PSC_FCLL   : see below
! PSC_FCLN   : see below
! PSC_FEVI   : see below
! PSC_FEVN   : see below
! PSC_*      : MULTIPLICATORS FOR CORRESPONDING FLUXES - USED AFTER TOMs FOR UPDATE OF FLUXES
! PXHROV     : ANTI-FIBRILLATION COEFFICIENT FOR PCHROV

! 2D half levels (0:KLEV)

! PAPHI      : GEOPOTENTIAL ON HALF-LEVELS
! PAPRS      : PRESSURE ON HALF-LEVELS
! PF_EPS     : TERM ASSOCIATED WITH MIXING LENGTH CONVERSION L-l_m: F_epsilon
! PKTROV     : (-rho*g/delta phi)*VERTICAL EXCHANGE COEFFICIENT FOR T AND Q
! PLML       : 'STATIC' TKE TYPE MIXING LENGTH
! PNEBQ      : Q FOR q_li DIFFUSION
! PMN2_DQ    : d(q_t)/dz
! PMN2_DS    : d(s_sL)/dz
! PMN2_EQ    : E_(q_t,s_sL)
! PMN2_ES    : E_(s_sL)
! PTH_FUN    : T_h - STABILITY FUNCTION FOR TOMs PARAMETRIZATION
! PWW_FUN    : F_ww = 2A_z - STABILITY FUNCTIONS FOR TOMs PARAMETRIZATION
! PXTROV     : ANTI-FIBRILLATION COEFFICIENT FOR PKTROV

! 2D full levels (1:KLEV)

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATICS) (PAPRSF - PRESSURE ON FULL LEVELS)
! PAPHIF     : GEOPOTENTIAL ON FULL LEVELS
! PCP        : SPECIFIC HEAT FOR AIR AT CONSTANT PRESSURE
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (FOR HYDROSTATICS)
! PR         : GAS CONSTANT FOR AIR
! PRDELP     : INVERSE OF PDELP
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOR
! PQI        : SPECIFIC QUANTITY OF ICE
! PQL        : SPECIFIC QUANTITY OF LIQUID WATER
! PU         : X-COMPONENT OF WIND - not needed in ACDIFV3! 
! PV         : Y-COMPONENT OF WIND - not needed in ACDIFV3!
! PT         : TEMPERATURE

!--- INPUT/OUTPUT ARGUMENTS ----------------------------------------------------

! 1D

! PDIFWQ     : TURBULENT FLUX OF SPECIFIC HUMIDITY (INCLUDING NEGATIVE Q) (LOCAL FROM ARP_GROUND_PARAM)
! PDIFWS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY) (LOCAL FROM ARP_GROUND PARAM)
! PFCLL      : LATENT HEAT FLUX OVER LIQUID WATER (OR WET SOIL)
! PFCLN      : LATENT HEAT FLUX OVER SNOW (OR ICE)
! PFCS       : SENSIBLE HEAT FLUX AT SURFACE
! PFEVI      : WATER VAPOR FLUX OVER FROZEN SOIL
! PFEVL      : WATER VAPOR FLUX OVER LIQUID WATER (OR WET SOIL)
! PFEVN      : WATER VAPOR FLUX OVER SNOW (OR ICE) AND FROZEN SOIL

! 2D half levels (0:KLEV)

! PDIFTS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY)
! PDIFTQ     : TURBULENT FLUX OF SPECIFIC HUMIDITY (INCLUDING NEGATIVE Q)

! 2D full levels (1:KLEV)

! PTKE       : TURBULENT KINETIC ENERGY
! PTENDPTKE  : TKE TENDENCY (USED BY PSEUDO-PROGNOSTIC TKE SCHEME).

!--- OUTPUT ARGUMENTS ----------------------------------------------------------

! 2D half levels (0:KLEV)

! PDIFTQL    : TURBULENT FLUX OF LIQUID WATER
! PDIFTQI    : TURBULENT FLUX OF ICE
! PTSTAR2    : ? (diagnostic)
! PTSTAR2Q   : ? (diagnostic)

! 2D full levels (1:KLEV)

! PTSTAR     : ? (diagnostic)
! PTSTARQ    : ? (diagnostic)

!----- MODULE IMPORTS ----------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK    ,DR_HOOK

USE YOMCST   , ONLY :  &
 & RG       ,RV       ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD    ,RDT

!------I/O PARAMETER DECLARATIONS ----------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKTROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PXTROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PXHROV(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEBQ(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTH_FUN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWW_FUN   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PF_EPS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_ES   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_EQ   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_DS (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_DQ (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLML(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTKE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENDPTKE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCLL(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCLN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEVI(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEVL(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEVN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFWQ(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFWS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSC_FEVI(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSC_FEVN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSC_FCLL(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSC_FCLN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0(KLON)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRTI(KLON)  
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTSTAR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTSTAR2(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTSTARQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTSTAR2Q(KLON,0:KLEV) 

!---- LOCAL VARIABLES DECLARATIONS ---------------------------------------------
! iterators
INTEGER(KIND=JPIM) :: JLEV, JLON, JITER, INITER
! part I
REAL(KIND=JPRB)    :: ZCEPS2, ZGDT, ZGDTI, ZEPS, ZACVSST, ZIACVSST, ZGAMMA, ZCNEBDIFF 
! part II 
REAL(KIND=JPRB)    :: ZIPOI(KLON,KLEV), ZPOID(KLON,KLEV), ZFTKE(KLON,KLEV), ZHTKE,             &
  &                   ZL_TKE(KLON,0:KLEV), ZTAU_TKE(KLON,0:KLEV), ZRHO(KLON,0:KLEV),           &
  &                   ZTAU_TKE3(KLON,0:KLEV), ZTAU2TKE(KLON,0:KLEV), ZITAU2TKE(KLON,0:KLEV),   &
  &                   ZITKE(KLON,0:KLEV), ZT_CR_QS(KLON,0:KLEV), ZT_CR_SQ(KLON,0:KLEV),        &
  &                   ZK_CR_QS(KLON,0:KLEV), ZK_CR_SQ(KLON,0:KLEV),                            & 
  &                   ZTSTAR_TERM1BH(KLON,0:KLEV), ZTSTAR_TERM3H(KLON,0:KLEV),                 &
  &                   ZTSTAR_TERM3(KLON,KLEV), ZTSTAR_TERM1A, ZTSTARQ_TERM1A, ZTSTAR_TERM1B,   &
  &                   ZTSTARQ_TERM1B, ZTSTAR_TERM13(KLON,KLEV), ZTSTARQ_TERM13(KLON,KLEV),     &
  &                   ZTSTAR_TERM2(KLON,KLEV), ZTSTARQ_TERM2(KLON,KLEV),                       &
  &                   ZTSTAR2_TERM13(KLON,0:KLEV), ZTSTAR2Q_TERM13(KLON,0:KLEV),               &
  &                   ZTSTAR2_TERM2(KLON,0:KLEV), ZRTI, ZDPHI, ZICETLI, ZAH_FUN(KLON,0:KLEV),  &
  &                   ZKTROV(KLON,0:KLEV)
! part III
REAL(KIND=JPRB)    :: ZDIFTS(KLON,0:KLEV), ZDIFTQ(KLON,0:KLEV), ZDSLOC(KLON,KLEV),             &
  &                   ZDQLOC(KLON,KLEV), ZDS(KLON,KLEV), ZDQ(KLON,KLEV), ZTSTAR(KLON,KLEV),    &
  &                   ZTSTARQ(KLON,KLEV), ZXSTAP(KLON,KLEV), ZXSTAM(KLON,KLEV),                &
  &                   ZXSTAPQ(KLON,KLEV), ZXSTAMQ(KLON,KLEV), ZCROSS(KLON,KLEV),               &
  &                   ZCROSSQ(KLON,KLEV), ZTSTAR2(KLON,0:KLEV), ZTSTAR2Q(KLON,0:KLEV),         &
  &                   ZZZ(KLON,0:KLEV), ZKTROV2(KLON,0:KLEV), ZKTROV2Q(KLON,0:KLEV),           &
  &                   ZXSTEST(KLON), ZXSTESTQ(KLON), ZDELPM(KLON,KLEV), ZDELPMQ(KLON,KLEV),    &
  &                   ZDELPP(KLON,KLEV), ZDELPPQ(KLON,KLEV), ZRAP, ZRAPQ, ZSEC, ZSECQ,         &
  &                   ZRHSS(KLON,KLEV), ZRHSQ(KLON,KLEV)
! part IV
REAL(KIND=JPRB)    :: ZMUL, ZSUB(KLON,KLEV), ZMULQ, ZSUBQ(KLON,KLEV), ZXS(KLON,KLEV),          &
  &                   ZXQ(KLON,KLEV), ZELIM, ZELIMQ, ZDSTOM(KLON,KLEV), ZDQTOM(KLON,KLEV)
! part V
REAL(KIND=JPRB)    :: ZDIFTSTOM(KLON,0:KLEV), ZDIFTQTOM(KLON,0:KLEV), ZD_DIFWQ, ZD_DIFWS
! part VI
REAL(KIND=JPRB)    :: ZTEND_TERM1, ZTEND_TERM2
! condensate flux variables
REAL(KIND=JPRB)    :: ZTH, ZLV, ZLS, ZPICE(KLON,KLEV), ZCPH, ZTLI, ZICE, ZLIQ,                 &
  &                   ZLVT(KLON,KLEV), ZEISP, ZELSP, ZQSATI, ZQSATL, ZDQSATI, ZDQSATL,         &
  &                   ZALPHA1(KLON,KLEV), ZCOEFA(KLON,KLEV), ZDIFTQC, ZDUTQL(KLON,0:KLEV),     &
  &                   ZDUTQI(KLON,0:KLEV), ZQL(KLON), ZSIGN, ZQI(KLON), ZDDTQL(KLON,0:KLEV),   &
  &                   ZDDTQI(KLON,0:KLEV), ZDTQFL(KLON), ZDTQFI(KLON)
! dr hook handle
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

!---- INCLUDES -----------------------------------------------------------------

#include "fcttrm.func.h"
#include "fctdoi.func.h"

!---- DR HOOK CALL -------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ACDIFV3',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & ENT_LAMBDA=>YDML_PHY_MF%YRPHY0%ENT_LAMBDA, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, &
 & ETKE_KA2=>YDML_PHY_MF%YRPHY0%ETKE_KA2, ETKE_KA3=>YDML_PHY_MF%YRPHY0%ETKE_KA3, &
 & ETKE_BETA_EPS=>YDML_PHY_MF%YRPHY0%ETKE_BETA_EPS, ETKE_KA1=>YDML_PHY_MF%YRPHY0%ETKE_KA1, &
 & NUPTKE=>YDML_PHY_MF%YRPHY0%NUPTKE, C_EPSILON=>YDML_PHY_MF%YRPHY0%C_EPSILON, &
 & ETKE_CG01=>YDML_PHY_MF%YRPHY0%ETKE_CG01, ETKE_CG03=>YDML_PHY_MF%YRPHY0%ETKE_CG03, &
 & ETKE_CG02=>YDML_PHY_MF%YRPHY0%ETKE_CG02, ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, &
 & ETKE_GAMMA_EPS=>YDML_PHY_MF%YRPHY0%ETKE_GAMMA_EPS, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NDIFFNEB=>YDML_PHY_MF%YRPHY%NDIFFNEB, LCOEFK_FLX=>YDML_PHY_MF%YRPHY%LCOEFK_FLX, &
 & LCOEFK_TOMS=>YDML_PHY_MF%YRPHY%LCOEFK_TOMS, LDIFCONS=>YDML_PHY_MF%YRPHY%LDIFCONS,&
 & YDPHY0=>YDML_PHY_MF%YRPHY0)

!-------------------------------------------------------------------------------
! I - Initialization of constants
!-------------------------------------------------------------------------------

ZCEPS2 = C_EPSILON * C_EPSILON * 0.25_JPRB

ZGDT = RG * TSPHY

ZGDTI = 1.0_JPRB / ZGDT

ZEPS = EPSILON(1.0_JPRB) * 1.E+4_JPRB

ZGAMMA = 1.0_JPRB                               ! used in tuning for ZKTROV

IF(NDIFFNEB==0) THEN                            ! switch for diffusion of q_li
  ZCNEBDIFF = 0.0_JPRB
ELSE
  ZCNEBDIFF = 1.0_JPRB
ENDIF

INITER=2                                        ! number of TOMs solver iterations

!-------------------------------------------------------------------------------
! II - preliminary calculations that can be done outside the solver iterations
!-------------------------------------------------------------------------------
! --- calculation of ZPOID = dp/(g*dt) and ZIPOI = (g*dt)/dp used as divergence operator
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
  ENDDO
ENDDO

IF(LCOEFK_TOMS) THEN

  ! --- current value of TKE (pTKE value plus dt*tendency from this time step) (full levels)
  ZFTKE(KIDIA:KFDIA,KTDIA:KLEV)=PTENDPTKE(KIDIA:KFDIA,KTDIA:KLEV)*TSPHY+PTKE(KIDIA:KFDIA,KTDIA:KLEV)

  ! --- dissipation length and time scales for TKE (half levels)
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ! average TKE to half levels
      ZHTKE=0.5_JPRB*(ZFTKE(JLON,JLEV)+ZFTKE(JLON,JLEV+1))
      ! calculate tau and L
      ZL_TKE(JLON,JLEV)=PLML(JLON,JLEV)
      ZTAU_TKE(JLON,JLEV)=2.0_JPRB*ZL_TKE(JLON,JLEV)/(sqrt(ZHTKE)*C_EPSILON*PF_EPS(JLON,JLEV))
      ! small tau security
      ZTAU_TKE(JLON,JLEV)=ZTAU_TKE(JLON,JLEV)+ETKE_BETA_EPS*TSPHY
      ! large tau security
      ZTAU_TKE(JLON,JLEV)=ZTAU_TKE(JLON,JLEV)/(1.0_JPRB+ETKE_GAMMA_EPS/TSPHY*ZTAU_TKE(JLON,JLEV))
      ! recalculation of L with secure tau
      ZL_TKE(JLON,JLEV)=ZTAU_TKE(JLON,JLEV)*sqrt(ZHTKE)*C_EPSILON/2.0_JPRB*PF_EPS(JLON,JLEV)
    ENDDO
  ENDDO
  ! surface - here to gain higher consistency between L and E for the lower TOMs solver boundary.
  DO JLON=KIDIA,KFDIA
    ! for surface half level take the surface full level
    ZHTKE=ZFTKE(JLON,KLEV)
    ! calculate tau and L
    ZL_TKE(JLON,KLEV)=PLML(JLON,KLEV)*(PGZ0(JLON)+PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV))/PGZ0(JLON) 
    ZTAU_TKE(JLON,KLEV)=2.0_JPRB*ZL_TKE(JLON,KLEV)/(sqrt(ZHTKE)*C_EPSILON*PF_EPS(JLON,KLEV))
    ! small tau_epsilon security
    ZTAU_TKE(JLON,KLEV)=ZTAU_TKE(JLON,KLEV)+ETKE_BETA_EPS*TSPHY
    ! large tau_epsilon security
    ZTAU_TKE(JLON,KLEV)=ZTAU_TKE(JLON,KLEV)/(1.0_JPRB+ETKE_GAMMA_EPS/TSPHY*ZTAU_TKE(JLON,KLEV))
    ! recalculation of L with secure tau
    ZL_TKE(JLON,KLEV)=ZTAU_TKE(JLON,KLEV)*sqrt(ZHTKE)*C_EPSILON/2.0_JPRB*PF_EPS(JLON,KLEV)
  ENDDO
  ! top
  ZTAU_TKE(KIDIA:KFDIA,KTDIA-1)=ZTAU_TKE(KIDIA:KFDIA,KTDIA)
  ZL_TKE(KIDIA:KFDIA,KTDIA-1)=ZL_TKE(KIDIA:KFDIA,KTDIA)

  ! --- density (half levels)
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ! average 1/RT to half levels
      ZRHO(JLON,JLEV)=2._JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))*PAPRS(JLON,JLEV)
    ENDDO
  ENDDO
  ! surface
  DO JLON=KIDIA,KFDIA
    ZRHO(JLON,KLEV)=PAPRS(JLON,KLEV)*PRTI(JLON)
  ENDDO
  ! top
  ZRHO(KIDIA:KFDIA,KTDIA-1)=ZRHO(KIDIA:KFDIA,KTDIA)

  ! --- auxiliary variables (half levels)
  ! calculated with secured L, names reflect the recalculation back to TKE
  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZTAU_TKE3(JLON,JLEV)=ZTAU_TKE(JLON,JLEV)**3
      ZTAU2TKE(JLON,JLEV)=(ZL_TKE(JLON,JLEV)/PF_EPS(JLON,JLEV))**2
      ZITAU2TKE(JLON,JLEV)=1.0_JPRB/ZTAU2TKE(JLON,JLEV)
      ZITKE(JLON,JLEV)=(ZTAU_TKE(JLON,JLEV)*PF_EPS(JLON,JLEV)/ZL_TKE(JLON,JLEV))**2
    ENDDO
  ENDDO

  ! --- T_cr^qs, T_cr^sq, K_cr^qs, K_cr^sq (half levels)
  ! ZT_CR_QS = C^01*(4/C_epsilon^2)*T_cr^qs
  ! ZT_CR_SQ = C^01*(4/C_epsilon^2)*T_cr^sq
  ! ZK_CR_QS = (C_epsilon^2/4)*e_k*K_cr^qs
  ! ZK_CR_SQ = (C_epsion^2/4)*e_k*K_cr^sq 
  ! with surface
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZT_CR_QS(JLON,JLEV)=-0.06_JPRB*ETKE_CG01*ETKE_KA1*PMN2_DQ(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*ZITAU2TKE(JLON,JLEV)
      ZT_CR_SQ(JLON,JLEV)=-0.06_JPRB*ETKE_CG01*ETKE_KA1*PMN2_DS(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*ZITAU2TKE(JLON,JLEV)
      ZK_CR_QS(JLON,JLEV)=PWW_FUN(JLON,JLEV)*PMN2_ES(JLON,JLEV)*ZTAU2TKE(JLON,JLEV)
      ZK_CR_SQ(JLON,JLEV)=PWW_FUN(JLON,JLEV)*PMN2_EQ(JLON,JLEV)*ZTAU2TKE(JLON,JLEV)
    ENDDO
  ENDDO
  ! top
  ZT_CR_QS(KIDIA:KFDIA,KTDIA-1)=ZT_CR_QS(KIDIA:KFDIA,KTDIA)
  ZT_CR_SQ(KIDIA:KFDIA,KTDIA-1)=ZT_CR_SQ(KIDIA:KFDIA,KTDIA)
  ZK_CR_QS(KIDIA:KFDIA,KTDIA-1)=ZK_CR_QS(KIDIA:KFDIA,KTDIA)
  ZK_CR_SQ(KIDIA:KFDIA,KTDIA-1)=ZK_CR_SQ(KIDIA:KFDIA,KTDIA)

  ! --- auxiliary variables for calculation of T_*^-1 in the solver loop
  ! half level variables with surface
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
!OLD{
      ZTSTAR_TERM1BH(JLON,JLEV)=ZRHO(JLON,JLEV)*PWW_FUN(JLON,JLEV)*ZL_TKE(JLON,JLEV)**2
!}OLD
!NEW{
!     ZTSTAR_TERM1BH(JLON,JLEV)=ZRHO(JLON,JLEV)*PWW_FUN(JLON,JLEV)*ZL_TKE(JLON,JLEV)**2/PF_EPS(JLON,JLEV)**2
!}NEW
      ZTSTAR_TERM3H(JLON,JLEV)=ZTSTAR_TERM1BH(JLON,JLEV)/ZTAU_TKE(JLON,JLEV)
    ENDDO
  ENDDO
  ! half level variables top
  ZTSTAR_TERM1BH(KIDIA:KFDIA,KTDIA-1)=ZTSTAR_TERM1BH(KIDIA:KFDIA,KTDIA)
  ZTSTAR_TERM3H(KIDIA:KFDIA,KTDIA-1)=ZTSTAR_TERM3H(KIDIA:KFDIA,KTDIA)
  ! full level variables
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTSTAR_TERM3(JLON,JLEV)=PRDELP(JLON,JLEV)*(ZTSTAR_TERM3H(JLON,JLEV)-ZTSTAR_TERM3H(JLON,JLEV-1))
      ZTSTAR_TERM1A=PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*PMN2_DS(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*ZITAU2TKE(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*PMN2_DS(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*ZITAU2TKE(JLON,JLEV)
      ZTSTARQ_TERM1A=PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*PMN2_DQ(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*ZITAU2TKE(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*PMN2_DQ(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*ZITAU2TKE(JLON,JLEV)
      ZTSTAR_TERM1B=-PRDELP(JLON,JLEV)*(ZTSTAR_TERM1BH(JLON,JLEV-1)*PMN2_ES(JLON,JLEV-1) &
        & -ZTSTAR_TERM1BH(JLON,JLEV)*PMN2_ES(JLON,JLEV))
      ZTSTARQ_TERM1B=-PRDELP(JLON,JLEV)*(ZTSTAR_TERM1BH(JLON,JLEV-1)*PMN2_EQ(JLON,JLEV-1) &
        & -ZTSTAR_TERM1BH(JLON,JLEV)*PMN2_EQ(JLON,JLEV))
      ZTSTAR_TERM13(JLON,JLEV)=RG*RG*(-0.06_JPRB*ETKE_CG01*ETKE_KA1*ZTSTAR_TERM1A*ZTSTAR_TERM1B &
        & +0.3_JPRB*ETKE_CG03*ETKE_KA3*ZTSTAR_TERM3(JLON,JLEV) &
        & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITKE(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITKE(JLON,JLEV)))
      ZTSTARQ_TERM13(JLON,JLEV)=RG*RG*(-0.06_JPRB*ETKE_CG01*ETKE_KA1*ZTSTARQ_TERM1A*ZTSTARQ_TERM1B &
        & +0.3_JPRB*ETKE_CG03*ETKE_KA3*ZTSTAR_TERM3(JLON,JLEV) &
        & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITKE(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITKE(JLON,JLEV)))
!OLD{
      ZTSTAR_TERM2(JLON,JLEV)=RG*RG*RG*ETKE_CG02*ETKE_KA2*ZCEPS2 &
        & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITAU2TKE(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*PMN2_ES(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITAU2TKE(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*PMN2_ES(JLON,JLEV))
      ZTSTARQ_TERM2(JLON,JLEV)=RG*RG*RG*ETKE_CG02*ETKE_KA2*ZCEPS2 &
        & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITAU2TKE(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*PMN2_EQ(JLON,JLEV-1) &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITAU2TKE(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*PMN2_EQ(JLON,JLEV))
!}OLD
!NEW{
!     ZTSTAR_TERM2(JLON,JLEV)=RG*RG*ETKE_CG02*ETKE_KA2*ZCEPS2 &
!       & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITAU2TKE(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*PMN2_ES(JLON,JLEV-1) &
!       & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITAU2TKE(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*PMN2_ES(JLON,JLEV))
!     ZTSTARQ_TERM2(JLON,JLEV)=RG*RG*ETKE_CG02*ETKE_KA2*ZCEPS2 &
!       & *(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZITAU2TKE(JLON,JLEV-1)*ZTAU_TKE3(JLON,JLEV-1)*PMN2_EQ(JLON,JLEV-1) &
!       & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZITAU2TKE(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV)*PMN2_EQ(JLON,JLEV))
!}NEW

    ENDDO
  ENDDO
  ! --- auxiliary variables for calculation of T_** in the solver loop (half levels)
  ! with surface
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTSTAR2_TERM13(JLON,JLEV)=PWW_FUN(JLON,JLEV) &
        & *(-0.06_JPRB*ETKE_CG01*ETKE_KA1*PMN2_ES(JLON,JLEV)*PMN2_DS(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV) &
        &   +0.3_JPRB*ETKE_CG03*ETKE_KA3*ZTAU_TKE(JLON,JLEV))
      ZTSTAR2Q_TERM13(JLON,JLEV)=PWW_FUN(JLON,JLEV) &
        & *(-0.06_JPRB*ETKE_CG01*ETKE_KA1*PMN2_EQ(JLON,JLEV)*PMN2_DQ(JLON,JLEV)*ZTAU_TKE3(JLON,JLEV) &
        &   +0.3_JPRB*ETKE_CG03*ETKE_KA3*ZTAU_TKE(JLON,JLEV))
!OLD{
      ZTSTAR2_TERM2(JLON,JLEV)=ETKE_CG02*ETKE_KA2*ZCEPS2*ZCEPS2*ZTAU_TKE3(JLON,JLEV)*ZTAU_TKE(JLON,JLEV) &
        & *ZITAU2TKE(JLON,JLEV)/ZRHO(JLON,JLEV)
!}OLD
!NEW{
!     ZTSTAR2_TERM2(JLON,JLEV)=ETKE_CG02*ETKE_KA2*ZCEPS2*ZTAU_TKE3(JLON,JLEV)*ZTAU_TKE(JLON,JLEV) &
!       & *ZITAU2TKE(JLON,JLEV)/ZRHO(JLON,JLEV)
!}NEW
    ENDDO
  ENDDO
  ! top
  ZTSTAR2_TERM13(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB
  ZTSTAR2Q_TERM13(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB
  ZTSTAR2_TERM2(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB

  ! --- scaling of exchange coefficient (half levels)
  ! auxiliary variable
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZRTI=2._JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
      ZDPHI = PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1) 
      ZAH_FUN(JLON,JLEV)=ETKE_CG03*ETKE_KA3*PTH_FUN(JLON,JLEV)*ZCEPS2*ZTAU_TKE(JLON,JLEV)/(ZL_TKE(JLON,JLEV))**2 &
        & *PKTROV(JLON,JLEV)/(PAPRS(JLON,JLEV)*ZRTI*RG/ZDPHI)
    ENDDO
  ENDDO
  ! top
  ZAH_FUN (KIDIA:KFDIA,KTDIA-1)=ZAH_FUN (KIDIA:KFDIA,KTDIA)
  ! surface
  ZAH_FUN (KIDIA:KFDIA,KLEV)=ZAH_FUN (KIDIA:KFDIA,KLEV-1)
  ! scaled exchange coefficient
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZKTROV(JLON,JLEV)=PKTROV(JLON,JLEV)*PXTROV(JLON,JLEV)*TSPHY/(TSPHY+ZGAMMA*ZTAU_TKE(JLON,JLEV)*ZAH_FUN(JLON,JLEV))
    ENDDO
  ENDDO
  ! top
  DO JLON=KIDIA,KFDIA
    ZKTROV(JLON,KTDIA-1)=0.0_JPRB
  ENDDO
  ! surface 
  DO JLON=KIDIA,KFDIA
    ZKTROV(JLON,KLEV)=PCHROV(JLON)*PXHROV(JLON)*TSPHY/(TSPHY+ZGAMMA*ZTAU_TKE(JLON,KLEV)*ZAH_FUN(JLON,KLEV))
  ENDDO

ENDIF  ! IF(LCOEFK_TOMS)

! --- auxiliary variables for calculating the condensate fluxes
IF (LDIFCONS) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTH                = (PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB
      ZLV                = FOLH(ZTH,0.0_JPRB)
      ZLS                = FOLH(ZTH,1.0_JPRB)
      ZPICE(JLON,JLEV)   = FONICE(ZTH,YDPHY0%RDTFAC)
      ZCPH               = (PCP(JLON,JLEV)+PCP(JLON,JLEV+1))*0.5_JPRB
      ZTLI               = ZTH-0.5_JPRB*(ZLV*(PQL(JLON,JLEV)+PQL(JLON,JLEV+1))+ZLS*(PQI(JLON,JLEV)+PQI(JLON,JLEV+1)))/ZCPH
      ZICE               = ZPICE(JLON,JLEV)
      ZLIQ               = 1.0_JPRB-ZICE
      ZLVT(JLON,JLEV)    = ZLV*ZLIQ+ZLS*ZICE
      ZICETLI            = FONICE(ZTLI,YDPHY0%RDTFAC)
      ZEISP              = FOEW(ZTLI,1.0_JPRB)/PAPRS(JLON,JLEV)
      ZELSP              = FOEW(ZTLI,0.0_JPRB)/PAPRS(JLON,JLEV)
      ZQSATI             = FOQS(ZEISP)
      ZQSATL             = FOQS(ZELSP)
      ZDQSATI            = ZQSATI* FOLH(ZTLI,1.0_JPRB)/(RV*ZTLI*ZTLI*ZCPH)
      ZDQSATL            = ZQSATL* FOLH(ZTLI,0.0_JPRB)/(RV*ZTLI*ZTLI*ZCPH)
      ZALPHA1(JLON,JLEV) = (1.0_JPRB-ZICETLI)*ZDQSATL+ZICETLI*ZDQSATI
      ZCOEFA(JLON,JLEV)  = ZCNEBDIFF*PNEBQ(JLON,JLEV)/((1.0_JPRB+ZDQSATL*ZLV)*ZLIQ+(1.0_JPRB+ZDQSATI*ZLS)*ZICE)
    ENDDO
  ENDDO
ENDIF

!-------------------------------------------------------------------------------
! III - preparation for the TOMs solver
!-------------------------------------------------------------------------------
IF(LCOEFK_TOMS) THEN

  ! --- set starting values for solver iterations to local solutions
  ZDIFTS(KIDIA:KFDIA,KTDIA-1:KLEV)=PDIFTS(KIDIA:KFDIA,KTDIA-1:KLEV)
  ZDIFTQ(KIDIA:KFDIA,KTDIA-1:KLEV)=PDIFTQ(KIDIA:KFDIA,KTDIA-1:KLEV)
!NEW{
! DO JLEV=KTDIA,KLEV
!   DO JLON=KIDIA,KFDIA
!     ZDSLOC(JLON,JLEV)=ZIPOI(JLON,JLEV)*(PDIFTS(JLON,JLEV-1)-PDIFTS(JLON,JLEV))
!     ZDQLOC(JLON,JLEV)=ZIPOI(JLON,JLEV)*(PDIFTQ(JLON,JLEV-1)-PDIFTQ(JLON,JLEV))
!     ZDS(JLON,JLEV)=ZDSLOC(JLON,JLEV)
!     ZDQ(JLON,JLEV)=ZDQLOC(JLON,JLEV)
!   ENDDO
! ENDDO
!}NEW

  ! --- solver iteration loop
  DO JITER=1,INITER

    ! --- calculate T_*^-1 term and average cross terms (full levels)
    ! --- also construct stabilized T_*^-1 terms <--- ?
    ! --- average cross terms to full levels
    !cdir unroll=8
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
!OLD{
        ZDSLOC(JLON,JLEV)=ZIPOI(JLON,JLEV)*(PDIFTS(JLON,JLEV-1)-PDIFTS(JLON,JLEV))
        ZDQLOC(JLON,JLEV)=ZIPOI(JLON,JLEV)*(PDIFTQ(JLON,JLEV-1)-PDIFTQ(JLON,JLEV))
        ZDS(JLON,JLEV)=ZDSLOC(JLON,JLEV)
        ZDQ(JLON,JLEV)=ZDQLOC(JLON,JLEV)
!}OLD
        ! T_*^-1 terms
        ZTSTAR(JLON,JLEV)=ZTSTAR_TERM13(JLON,JLEV)+PRDELP(JLON,JLEV)*ZTSTAR_TERM2(JLON,JLEV) &
          & *(ZDIFTS(JLON,JLEV-1)*ZTAU_TKE(JLON,JLEV-1)-ZDIFTS(JLON,JLEV)*ZTAU_TKE(JLON,JLEV))
        ZTSTARQ(JLON,JLEV)=ZTSTARQ_TERM13(JLON,JLEV)+PRDELP(JLON,JLEV)*ZTSTARQ_TERM2(JLON,JLEV) &
          & *(ZDIFTQ(JLON,JLEV-1)*ZTAU_TKE(JLON,JLEV-1)-ZDIFTQ(JLON,JLEV)*ZTAU_TKE(JLON,JLEV))
        ZXSTAP(JLON,JLEV)=ZTSTAR(JLON,JLEV)+ABS(ZTSTAR(JLON,JLEV))
        ZXSTAM(JLON,JLEV)=ZTSTAR(JLON,JLEV)-ABS(ZTSTAR(JLON,JLEV))
        ZXSTAPQ(JLON,JLEV)=ZTSTARQ(JLON,JLEV)+ABS(ZTSTARQ(JLON,JLEV))
        ZXSTAMQ(JLON,JLEV)=ZTSTARQ(JLON,JLEV)-ABS(ZTSTARQ(JLON,JLEV))
        ! cross terms
        ZCROSS(JLON,JLEV)=PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZK_CR_QS(JLON,JLEV-1) &
          & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZK_CR_QS(JLON,JLEV)
        ZCROSSQ(JLON,JLEV)=PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZK_CR_SQ(JLON,JLEV-1) &
          & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZK_CR_SQ(JLON,JLEV)
      ENDDO
    ENDDO
    ! --- calculate T_** term (half levels), stabilized <--- ?
    !cdir unroll=8
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ! T_** terms
        ZTSTAR2(JLON,JLEV)=MAX(0._JPRB, &
          & ZTSTAR2_TERM13(JLON,JLEV)-ZTSTAR2_TERM2(JLON,JLEV)*PMN2_ES(JLON,JLEV)*ZDIFTS(JLON,JLEV))
        ZTSTAR2Q(JLON,JLEV)=MAX(0._JPRB, &
          & ZTSTAR2Q_TERM13(JLON,JLEV)-ZTSTAR2_TERM2(JLON,JLEV)*PMN2_EQ(JLON,JLEV)*ZDIFTQ(JLON,JLEV))
      ENDDO
    ENDDO
    ! T_** top
    ZTSTAR2(KIDIA:KFDIA,KTDIA-1)=ZTSTAR2(KIDIA:KFDIA,KTDIA)
    ZTSTAR2Q(KIDIA:KFDIA,KTDIA-1)=ZTSTAR2Q(KIDIA:KFDIA,KTDIA)
    ! T_** surface
    ZTSTAR2(KIDIA:KFDIA,KLEV)=0.0_JPRB
    ZTSTAR2Q(KIDIA:KFDIA,KLEV)=0.0_JPRB

    ! --- write diagnostic fluxes
    PTSTAR2(:,:)=ZTSTAR2(:,:)
    PTSTAR(:,:)=ZTSTAR(:,:)
    PTSTAR2Q(:,:)=ZTSTAR2Q(:,:)
    PTSTARQ(:,:)=ZTSTARQ(:,:)

    ! --- auxiliary variables (half levels)
    ! standard levels
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        !ZZZ(JLON,JLEV)=0.5_JPRB*ZKTROV(JLON,JLEV)*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))/RG*PTH_FUN(JLON,JLEV)
        ! cheat ?!    
        ZZZ(JLON,JLEV)=0.5_JPRB*ZKTROV(JLON,JLEV)*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))/RG*PTH_FUN(JLON,JLEV)/TSPHY
        ZKTROV2(JLON,JLEV)=ZKTROV(JLON,JLEV)*(1.0_JPRB+PTH_FUN(JLON,JLEV)*ZTSTAR2(JLON,JLEV)/TSPHY)
        ZKTROV2Q(JLON,JLEV)=ZKTROV(JLON,JLEV)*(1.0_JPRB+PTH_FUN(JLON,JLEV)*ZTSTAR2Q(JLON,JLEV)/TSPHY)
      ENDDO
    ENDDO
    ! top
    DO JLON=KIDIA,KFDIA
      ZZZ(JLON,KTDIA-1)=0.0_JPRB
      ZKTROV2(JLON,KTDIA-1)=0.0_JPRB
!NEW{
!     ZKTROV2Q(JLON,KTDIA-1)=0.0_JPRB
!}NEW
    ENDDO
    ! surface
    DO JLON=KIDIA,KFDIA
      ! ZZZ(JLON,KLEV)=0.5_JPRB*ZKTROV(JLON,KLEV)*(PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV))/RG*PTH_FUN(JLON,KLEV)
      ! cheat ?!    
      ZZZ(JLON,KLEV)=0.5_JPRB*ZKTROV(JLON,KLEV)*(PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV))/RG*PTH_FUN(JLON,KLEV)/TSPHY
      ZKTROV2(JLON,KLEV)=ZKTROV(JLON,KLEV)*(1.0_JPRB+PTH_FUN(JLON,KLEV)*ZTSTAR2(JLON,KLEV)/TSPHY)
      ZKTROV2Q(JLON,KLEV)=ZKTROV(JLON,KLEV)*(1.0_JPRB+PTH_FUN(JLON,KLEV)*ZTSTAR2Q(JLON,KLEV)/TSPHY)
    ENDDO

    ! --- final calculation of stabilized T_*^-1 variables (full levels)
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ZXSTAP(JLON,JLEV)=ZXSTAP(JLON,JLEV)*ZZZ(JLON,JLEV-1)
        ZXSTAM(JLON,JLEV)=ZXSTAM(JLON,JLEV)*ZZZ(JLON,JLEV)
        ZXSTAPQ(JLON,JLEV)=ZXSTAPQ(JLON,JLEV)*ZZZ(JLON,JLEV-1)
        ZXSTAMQ(JLON,JLEV)=ZXSTAMQ(JLON,JLEV)*ZZZ(JLON,JLEV)
      ENDDO
    ENDDO

    ! --- protection against non-linear instability; sharing the dp resource via a guess
    DO JLON=KIDIA,KFDIA
      ZXSTEST(JLON)=0.0_JPRB
      ZXSTESTQ(JLON)=0.0_JPRB
    ENDDO
    DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
        ZXSTEST(JLON)=ZXSTEST(JLON)+(ZXSTAM(JLON,JLEV)-ZXSTEST(JLON)) &
          & /(1.0_JPRB+(MAX(0.0_JPRB,ZXSTEST(JLON)-ZXSTAM(JLON,JLEV))*PRDELP(JLON,JLEV+1)))
        ZDELPM(JLON,JLEV+1)=MAX(ZEPS*PDELP(JLON,JLEV+1),-ZXSTAM(JLON,JLEV)+ZXSTEST(JLON))
        ZXSTESTQ(JLON)=ZXSTESTQ(JLON)+(ZXSTAMQ(JLON,JLEV)-ZXSTESTQ(JLON)) &
          & /(1.0_JPRB+(MAX(0.0_JPRB,ZXSTESTQ(JLON)-ZXSTAMQ(JLON,JLEV))*PRDELP(JLON,JLEV+1)))
        ZDELPMQ(JLON,JLEV+1)=MAX(ZEPS*PDELP(JLON,JLEV+1),-ZXSTAMQ(JLON,JLEV)+ZXSTESTQ(JLON))
      ENDDO
    ENDDO
    DO JLON=KIDIA,KFDIA
      ZXSTEST(JLON)=0.0_JPRB
      ZXSTESTQ(JLON)=0.0_JPRB
    ENDDO
    DO JLEV=KTDIA+1,KLEV
      DO JLON=KIDIA,KFDIA
        ZXSTEST(JLON)=ZXSTEST(JLON)+(ZXSTAP(JLON,JLEV)-ZXSTEST(JLON)) &
         & /(1.0_JPRB+(MAX(0.0_JPRB,ZXSTAP(JLON,JLEV)-ZXSTEST(JLON))*PRDELP(JLON,JLEV-1)))
        ZDELPP(JLON,JLEV-1)=MAX(ZEPS*PDELP(JLON,JLEV-1),ZXSTAP(JLON,JLEV)-ZXSTEST(JLON))
        ZXSTESTQ(JLON)=ZXSTESTQ(JLON)+(ZXSTAPQ(JLON,JLEV)-ZXSTESTQ(JLON)) &
         & /(1.0_JPRB+(MAX(0.0_JPRB,ZXSTAPQ(JLON,JLEV)-ZXSTESTQ(JLON))*PRDELP(JLON,JLEV-1)))
        ZDELPPQ(JLON,JLEV-1)=MAX(ZEPS*PDELP(JLON,JLEV-1),ZXSTAPQ(JLON,JLEV)-ZXSTESTQ(JLON))
      ENDDO
    ENDDO
    DO JLEV=KTDIA+1,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZRAP=MAX(ZEPS,MIN(1.0_JPRB-ZEPS,ZDELPM(JLON,JLEV)/(ZDELPM(JLON,JLEV)+ZDELPP(JLON,JLEV))))
        ZDELPM(JLON,JLEV)=PDELP(JLON,JLEV)*ZRAP
        ZDELPP(JLON,JLEV)=PDELP(JLON,JLEV)*(1.0_JPRB-ZRAP)
        ZRAPQ=MAX(ZEPS,MIN(1.0_JPRB-ZEPS,ZDELPMQ(JLON,JLEV)/(ZDELPMQ(JLON,JLEV)+ZDELPPQ(JLON,JLEV))))
        ZDELPMQ(JLON,JLEV)=PDELP(JLON,JLEV)*ZRAPQ
        ZDELPPQ(JLON,JLEV)=PDELP(JLON,JLEV)*(1.0_JPRB-ZRAPQ)
      ENDDO
    ENDDO
    DO JLON=KIDIA,KFDIA
      ZDELPP(JLON,KTDIA)=PDELP(JLON,KTDIA)
      ZDELPM(JLON,KLEV) =PDELP(JLON,KLEV)
      ZDELPPQ(JLON,KTDIA)=PDELP(JLON,KTDIA)
      ZDELPMQ(JLON,KLEV) =PDELP(JLON,KLEV)
    ENDDO

    ! --- protection against non-linear instability; actual symmetric implementation
    DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
        ZSEC=MAX(0.0_JPRB,(ZXSTAM(JLON,JLEV+1)-ZXSTAM(JLON,JLEV))/ZDELPM(JLON,JLEV+1))
        ZSEC=SQRT(1.0_JPRB+ZSEC*ZSEC)
        ZXSTAM(JLON,JLEV)=ZXSTAM(JLON,JLEV+1)+(ZXSTAM(JLON,JLEV)-ZXSTAM(JLON,JLEV+1))/ZSEC
        ZSECQ=MAX(0.0_JPRB,(ZXSTAMQ(JLON,JLEV+1)-ZXSTAMQ(JLON,JLEV))/ZDELPMQ(JLON,JLEV+1))
        ZSECQ=SQRT(1.0_JPRB+ZSECQ*ZSECQ)
        ZXSTAMQ(JLON,JLEV)=ZXSTAMQ(JLON,JLEV+1)+(ZXSTAMQ(JLON,JLEV)-ZXSTAMQ(JLON,JLEV+1))/ZSECQ
      ENDDO
    ENDDO
    DO JLEV=KTDIA+1,KLEV
      DO JLON=KIDIA,KFDIA
        ZSEC=MAX(0.0_JPRB,(ZXSTAP(JLON,JLEV)-ZXSTAP(JLON,JLEV-1))/ZDELPP(JLON,JLEV-1))
        ZSEC=SQRT(1.0_JPRB+ZSEC*ZSEC)
        ZXSTAP(JLON,JLEV)=ZXSTAP(JLON,JLEV-1)+(ZXSTAP(JLON,JLEV)-ZXSTAP(JLON,JLEV-1))/ZSEC
        ZSECQ=MAX(0.0_JPRB,(ZXSTAPQ(JLON,JLEV)-ZXSTAPQ(JLON,JLEV-1))/ZDELPPQ(JLON,JLEV-1))
        ZSECQ=SQRT(1.0_JPRB+ZSECQ*ZSECQ)
        ZXSTAPQ(JLON,JLEV)=ZXSTAPQ(JLON,JLEV-1)+(ZXSTAPQ(JLON,JLEV)-ZXSTAPQ(JLON,JLEV-1))/ZSECQ
      ENDDO
    ENDDO

    ! --- calculate RHS of the solver equations (full levels)
    ! top
    DO JLON=KIDIA,KFDIA
      ZRHSS(JLON,KTDIA) = &
        & PRDELP(JLON,KTDIA)*(ZXSTAM(JLON,KTDIA)*ZDSLOC(JLON,KTDIA)+ZXSTAP(JLON,KTDIA+1)*ZDSLOC(JLON,KTDIA+1)) &
        & -RG**2*PRDELP(JLON,KTDIA)*ZTSTAR2(JLON,KTDIA)*ZZZ(JLON,KTDIA)*2.0_JPRB &
        &   *(ZDSLOC(JLON,KTDIA)-ZDSLOC(JLON,KTDIA+1))/(PAPHIF(JLON,KTDIA)-PAPHIF(JLON,KTDIA+1)) &
        & -RG**2*PRDELP(JLON,KTDIA)*ZT_CR_SQ(JLON,KTDIA)*ZZZ(JLON,KTDIA)*2.0_JPRB &
        &   *(ZDQ(JLON,KTDIA)*ZCROSSQ(JLON,KTDIA)-ZDQ(JLON,KTDIA+1)*ZCROSSQ(JLON,KTDIA+1)) &
        &   /(PAPHIF(JLON,KTDIA)-PAPHIF(JLON,KTDIA+1))
      ZRHSQ(JLON,KTDIA) = &
        & PRDELP(JLON,KTDIA)*(ZXSTAMQ(JLON,KTDIA)*ZDQLOC(JLON,KTDIA)+ZXSTAPQ(JLON,KTDIA+1)*ZDQLOC(JLON,KTDIA+1)) &
        & -RG**2*PRDELP(JLON,KTDIA)*ZTSTAR2Q(JLON,KTDIA)*ZZZ(JLON,KTDIA)*2.0_JPRB &
        &   *(ZDQLOC(JLON,KTDIA)-ZDQLOC(JLON,KTDIA+1))/(PAPHIF(JLON,KTDIA)-PAPHIF(JLON,KTDIA+1)) &
        & -RG**2*PRDELP(JLON,KTDIA)*ZT_CR_QS(JLON,KTDIA)*ZZZ(JLON,KTDIA)*2.0_JPRB &
        &   *(ZDS(JLON,KTDIA)*ZCROSS(JLON,KTDIA)-ZDS(JLON,KTDIA+1)*ZCROSS(JLON,KTDIA+1)) &
        &   /(PAPHIF(JLON,KTDIA)-PAPHIF(JLON,KTDIA+1))
    ENDDO
    ! intermediate
    !cdir unroll=8
    DO JLEV=KTDIA+1,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZRHSS(JLON,JLEV) = &
          & -PRDELP(JLON,JLEV)*(ZXSTAM(JLON,JLEV-1)*ZDSLOC(JLON,JLEV-1)-ZXSTAP(JLON,JLEV+1)*ZDSLOC(JLON,JLEV+1) &
          &   +(ZXSTAP(JLON,JLEV)-ZXSTAM(JLON,JLEV))*ZDSLOC(JLON,JLEV)) &
          & +RG**2*PRDELP(JLON,JLEV) &
          &   *(ZTSTAR2(JLON,JLEV-1)*ZZZ(JLON,JLEV-1)*2.0_JPRB &
          &   *(ZDSLOC(JLON,JLEV-1)-ZDSLOC(JLON,JLEV))/(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV)) &
          &   -ZTSTAR2(JLON,JLEV)*ZZZ(JLON,JLEV)*2.0_JPRB &
          &   *(ZDSLOC(JLON,JLEV)-ZDSLOC(JLON,JLEV+1))/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))) &
          & +RG**2*PRDELP(JLON,JLEV) &
          &   *(ZT_CR_SQ(JLON,JLEV-1)*ZZZ(JLON,JLEV-1)*2.0_JPRB &
          &   *(ZDQ(JLON,JLEV-1)*ZCROSSQ(JLON,JLEV-1)-ZDQ(JLON,JLEV)*ZCROSSQ(JLON,JLEV)) &
          &   /(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV)) &
          &   -ZT_CR_SQ(JLON,JLEV)*ZZZ(JLON,JLEV)*2.0_JPRB &
          &   *(ZDQ(JLON,JLEV)*ZCROSSQ(JLON,JLEV)-ZDQ(JLON,JLEV+1)*ZCROSSQ(JLON,JLEV+1)) &
          &   /(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)))
        ZRHSQ(JLON,JLEV) = &
          & -PRDELP(JLON,JLEV)*(ZXSTAMQ(JLON,JLEV-1)*ZDQLOC(JLON,JLEV-1)-ZXSTAPQ(JLON,JLEV+1)*ZDQLOC(JLON,JLEV+1) &
          &   +(ZXSTAPQ(JLON,JLEV)-ZXSTAMQ(JLON,JLEV))*ZDQLOC(JLON,JLEV)) &
          & +RG**2*PRDELP(JLON,JLEV) &
          &   *(ZTSTAR2Q(JLON,JLEV-1)*ZZZ(JLON,JLEV-1)*2.0_JPRB &
          &   *(ZDQLOC(JLON,JLEV-1)-ZDQLOC(JLON,JLEV))/(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV)) &
          &   -ZTSTAR2Q(JLON,JLEV)*ZZZ(JLON,JLEV)*2.0_JPRB &
          &   *(ZDQLOC(JLON,JLEV)-ZDQLOC(JLON,JLEV+1))/(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))) &
          & +RG**2*PRDELP(JLON,JLEV) &
          &   *(ZT_CR_QS(JLON,JLEV-1)*ZZZ(JLON,JLEV-1)*2.0_JPRB &
          &   *(ZDS(JLON,JLEV-1)*ZCROSS(JLON,JLEV-1)-ZDS(JLON,JLEV)*ZCROSS(JLON,JLEV)) &
          &   /(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV)) &
          &   -ZT_CR_QS(JLON,JLEV)*ZZZ(JLON,JLEV)*2.0_JPRB &
          &   *(ZDS(JLON,JLEV)*ZCROSS(JLON,JLEV)-ZDS(JLON,JLEV+1)*ZCROSS(JLON,JLEV+1)) &
          &   /(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)))
      ENDDO
    ENDDO
    ! surface
    DO JLON=KIDIA,KFDIA
      ZRHSS(JLON,KLEV) = &
        & -PRDELP(JLON,KLEV)*(ZXSTAM(JLON,KLEV-1)*ZDSLOC(JLON,KLEV-1)+ZXSTAP(JLON,KLEV)*ZDSLOC(JLON,KLEV)) &
        & +RG**2*PRDELP(JLON,KLEV)*ZTSTAR2(JLON,KLEV-1)*ZZZ(JLON,KLEV-1)*2.0_JPRB &
        &   *(ZDSLOC(JLON,KLEV-1)-ZDSLOC(JLON,KLEV))/(PAPHIF(JLON,KLEV-1)-PAPHIF(JLON,KLEV)) &
        & +RG**2*PRDELP(JLON,KLEV)*ZT_CR_SQ(JLON,KLEV-1)*ZZZ(JLON,KLEV-1)*2.0_JPRB &
        &   *(ZDQ(JLON,KLEV-1)*ZCROSSQ(JLON,KLEV-1)-ZDQ(JLON,KLEV)*ZCROSSQ(JLON,KLEV)) &
        &   /(PAPHIF(JLON,KLEV-1)-PAPHIF(JLON,KLEV))
      ZRHSQ(JLON,KLEV) = &
        & -PRDELP(JLON,KLEV)*(ZXSTAMQ(JLON,KLEV-1)*ZDQLOC(JLON,KLEV-1)+ZXSTAPQ(JLON,KLEV)*ZDQLOC(JLON,KLEV)) &
        & +RG**2*PRDELP(JLON,KLEV)*ZTSTAR2Q(JLON,KLEV-1)*ZZZ(JLON,KLEV-1)*2.0_JPRB &
        &   *(ZDQLOC(JLON,KLEV-1)-ZDQLOC(JLON,KLEV))/(PAPHIF(JLON,KLEV-1)-PAPHIF(JLON,KLEV)) &
        & +RG**2*PRDELP(JLON,KLEV)*ZT_CR_QS(JLON,KLEV-1)*ZZZ(JLON,KLEV-1)*2.0_JPRB &
        &   *(ZDS(JLON,KLEV-1)*ZCROSS(JLON,KLEV-1)-ZDS(JLON,KLEV)*ZCROSS(JLON,KLEV)) &
        &   /(PAPHIF(JLON,KLEV-1)-PAPHIF(JLON,KLEV))
    ENDDO

    !---------------------------------------------------------------------------
    ! IV - solvers
    !---------------------------------------------------------------------------
    ! --- elimination
    ! top
    DO JLON=KIDIA,KFDIA
      ZMUL=1.0_JPRB/(1.0_JPRB+ZIPOI(JLON,KTDIA)*ZKTROV2(JLON,KTDIA)-PRDELP(JLON,KTDIA)*ZXSTAM(JLON,KTDIA))
      ZSUB(JLON,KTDIA)=ZMUL*(ZIPOI(JLON,KTDIA)*ZKTROV2(JLON,KTDIA)+PRDELP(JLON,KTDIA)*ZXSTAP(JLON,KTDIA+1))
      ZMULQ=1.0_JPRB/(1.0_JPRB+ZIPOI(JLON,KTDIA)*ZKTROV2Q(JLON,KTDIA)-PRDELP(JLON,KTDIA)*ZXSTAMQ(JLON,KTDIA))
      ZSUBQ(JLON,KTDIA)=ZMULQ*(ZIPOI(JLON,KTDIA)*ZKTROV2Q(JLON,KTDIA)+PRDELP(JLON,KTDIA)*ZXSTAPQ(JLON,KTDIA+1))
      ZXS(JLON,KTDIA)=ZMUL*ZRHSS(JLON,KTDIA)
      ZXQ(JLON,KTDIA)=ZMULQ*ZRHSQ(JLON,KTDIA)
    ENDDO
    ! intermediate
    DO JLEV=KTDIA+1,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZELIM=ZIPOI(JLON,JLEV)*ZKTROV2(JLON,JLEV-1)-PRDELP(JLON,JLEV)*ZXSTAM(JLON,JLEV-1)
        ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB(JLON,JLEV-1)+ZIPOI(JLON,JLEV)*(ZKTROV2(JLON,JLEV-1)+ZKTROV2(JLON,JLEV)) &
          & -PRDELP(JLON,JLEV)*(ZXSTAM(JLON,JLEV)-ZXSTAP(JLON,JLEV)))
        ZSUB(JLON,JLEV)=ZMUL*(ZIPOI(JLON,JLEV)*ZKTROV2(JLON,JLEV)+PRDELP(JLON,JLEV)*ZXSTAP(JLON,JLEV+1))
        ZELIMQ=ZIPOI(JLON,JLEV)*ZKTROV2Q(JLON,JLEV-1)-PRDELP(JLON,JLEV)*ZXSTAMQ(JLON,JLEV-1)
        ZMULQ=1.0_JPRB/(1.0_JPRB-ZELIMQ*ZSUBQ(JLON,JLEV-1)+ZIPOI(JLON,JLEV)*(ZKTROV2Q(JLON,JLEV-1)+ZKTROV2Q(JLON,JLEV)) &
         & -PRDELP(JLON,JLEV)*(ZXSTAMQ(JLON,JLEV)-ZXSTAPQ(JLON,JLEV)))
        ZSUBQ(JLON,JLEV)=ZMULQ*(ZIPOI(JLON,JLEV)*ZKTROV2Q(JLON,JLEV)+PRDELP(JLON,JLEV)*ZXSTAPQ(JLON,JLEV+1))
        ZXS(JLON,JLEV)=ZMUL*(ZRHSS(JLON,JLEV)+ZELIM*ZXS(JLON,JLEV-1))
        ZXQ(JLON,JLEV)=ZMULQ*(ZRHSQ(JLON,JLEV)+ZELIMQ*ZXQ(JLON,JLEV-1))
      ENDDO
    ENDDO
    ! surface
    DO JLON=KIDIA,KFDIA
      ZELIM=ZIPOI(JLON,KLEV)*ZKTROV2(JLON,KLEV-1)-PRDELP(JLON,KLEV)*ZXSTAM(JLON,KLEV-1)
      ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB(JLON,KLEV-1)+ZIPOI(JLON,KLEV)*(ZKTROV2(JLON,KLEV-1)+ZKTROV2(JLON,KLEV)) &
        & +PRDELP(JLON,KLEV)*ZXSTAP(JLON,KLEV))
      ZELIMQ=ZIPOI(JLON,KLEV)*ZKTROV2Q(JLON,KLEV-1)-PRDELP(JLON,KLEV)*ZXSTAMQ(JLON,KLEV-1)
      ZMULQ=1.0_JPRB/(1.0_JPRB-ZELIMQ*ZSUBQ(JLON,KLEV-1)+ZIPOI(JLON,KLEV)*(ZKTROV2Q(JLON,KLEV-1)+ZKTROV2Q(JLON,KLEV)) &
        & +PRDELP(JLON,KLEV)*ZXSTAPQ(JLON,KLEV))
      ZXS(JLON,KLEV)=ZMUL*(ZRHSS(JLON,KLEV)+ZELIM*ZXS(JLON,KLEV-1))
      ZXQ(JLON,KLEV)=ZMULQ*(ZRHSQ(JLON,KLEV)+ZELIMQ*ZXQ(JLON,KLEV-1))
      ZDSTOM(JLON,KLEV)=ZXS(JLON,KLEV)
      ZDQTOM(JLON,KLEV)=ZXQ(JLON,KLEV)
    ENDDO
    ! back-substitution
    DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
        ZXS(JLON,JLEV)=ZXS(JLON,JLEV)+ZSUB(JLON,JLEV)*ZXS(JLON,JLEV+1)
        ZXQ(JLON,JLEV)=ZXQ(JLON,JLEV)+ZSUBQ(JLON,JLEV)*ZXQ(JLON,JLEV+1)
        ZDSTOM(JLON,JLEV)=ZXS(JLON,JLEV)
        ZDQTOM(JLON,JLEV)=ZXQ(JLON,JLEV)
      ENDDO
    ENDDO

    !---------------------------------------------------------------------------
    ! V - convert TOMs corrections back to fluxes, which are then added to the fluxes of vertical diffusion
    !---------------------------------------------------------------------------
    !  top
    ZDIFTSTOM(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB
    ZDIFTQTOM(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB
    ! with surface
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ZDIFTSTOM(JLON,JLEV)=ZDIFTSTOM(JLON,JLEV-1)-ZPOID(JLON,JLEV)*ZDSTOM(JLON,JLEV)
        ZDIFTQTOM(JLON,JLEV)=ZDIFTQTOM(JLON,JLEV-1)-ZPOID(JLON,JLEV)*ZDQTOM(JLON,JLEV)
        ZDIFTS(JLON,JLEV)=PDIFTS(JLON,JLEV)+ZDIFTSTOM(JLON,JLEV)
        ZDIFTQ(JLON,JLEV)=PDIFTQ(JLON,JLEV)+ZDIFTQTOM(JLON,JLEV)
        ! store increments for next iteration
        ZDS(JLON,JLEV)=ZDSTOM(JLON,JLEV)+ZDSLOC(JLON,JLEV)
        ZDQ(JLON,JLEV)=ZDQTOM(JLON,JLEV)+ZDQLOC(JLON,JLEV)
!OLD{
        ! cheat?
        ZDS(JLON,JLEV)=ZDSLOC(JLON,JLEV)
        ZDQ(JLON,JLEV)=ZDQLOC(JLON,JLEV)
!}OLD
      ENDDO
    ENDDO

  ENDDO ! end of solver iteration loop

  ! update surface fluxes
  DO JLON=KIDIA,KFDIA
    ZD_DIFWQ=(ZDIFTQ(JLON,KLEV)-PDIFTQ(JLON,KLEV))
    ZD_DIFWS=(ZDIFTS(JLON,KLEV)-PDIFTS(JLON,KLEV))
    PDIFWS(JLON)=PDIFWS(JLON)+ZD_DIFWS
    PFCS(JLON)=PFCS(JLON)+ZD_DIFWS
    PDIFWQ(JLON)=PDIFWQ(JLON)+ZD_DIFWQ
    PFCLL(JLON)=PFCLL(JLON)+ZD_DIFWQ*PSC_FCLL(JLON)
    PFCLN(JLON)=PFCLN(JLON)+ZD_DIFWQ*PSC_FCLN(JLON)
    PFEVI(JLON)=PFEVI(JLON)+ZD_DIFWQ*PSC_FEVI(JLON)
    PFEVN(JLON)=PFEVN(JLON)+ZD_DIFWQ*PSC_FEVN(JLON)
    PFEVL(JLON)=PFEVL(JLON)+ZD_DIFWQ*(1.0_JPRB-PSC_FEVN(JLON))
  ENDDO
  ! update fluxes in atmosphere
  PDIFTS(KIDIA:KFDIA,KTDIA-1:KLEV)=ZDIFTS(KIDIA:KFDIA,KTDIA-1:KLEV)
  PDIFTQ(KIDIA:KFDIA,KTDIA-1:KLEV)=ZDIFTQ(KIDIA:KFDIA,KTDIA-1:KLEV)

ENDIF ! LCOEFK_TOMS

!--- retrieve the condensate fluxes
IF (LDIFCONS) THEN
  ! from "moist and conservative" variables to the "dry" ones
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA, KFDIA
      ZDIFTQC=ZCOEFA(JLON,JLEV)*(PDIFTQ(JLON,JLEV)-ZALPHA1(JLON,JLEV)*PDIFTS(JLON,JLEV))
      PDIFTS(JLON,JLEV)=PDIFTS(JLON,JLEV)+ZDIFTQC*ZLVT(JLON,JLEV)
      PDIFTQ(JLON,JLEV)=PDIFTQ(JLON,JLEV)-ZDIFTQC
      PDIFTQL(JLON,JLEV)=ZDIFTQC*(1.0_JPRB-ZPICE(JLON,JLEV))
      PDIFTQI(JLON,JLEV)=ZDIFTQC*ZPICE(JLON,JLEV)
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    PDIFTQL(JLON,KTDIA-1)=0.0_JPRB
    PDIFTQI(JLON,KTDIA-1)=0.0_JPRB
    PDIFTQL(JLON,KLEV)=0.0_JPRB
    PDIFTQI(JLON,KLEV)=0.0_JPRB
    ZDUTQL(JLON,KLEV)=0.0_JPRB
    ZDUTQI(JLON,KLEV)=0.0_JPRB
    ZDDTQL(JLON,KTDIA-1)=0.0_JPRB
    ZDDTQI(JLON,KTDIA-1)=0.0_JPRB
  ENDDO
  DO JLEV=KLEV,KTDIA+1,-1
    DO JLON=KIDIA,KFDIA
      ZQL(JLON)=PQL(JLON,JLEV)-ZIPOI(JLON,JLEV)*(PDIFTQL(JLON,JLEV)+ZDUTQL(JLON,JLEV)-PDIFTQL(JLON,JLEV-1))
      ZSIGN=MAX(0.0_JPRB,SIGN(1.0_JPRB,-PDIFTQL(JLON,JLEV-1)))
      ZDUTQL(JLON,JLEV-1)=ZSIGN*MIN(MAX(0.0_JPRB,-ZQL(JLON)*ZPOID(JLON,JLEV)),MAX(0.0_JPRB,-PDIFTQL(JLON,JLEV-1)))
      ZQI(JLON)=PQI(JLON,JLEV)-ZIPOI(JLON,JLEV)*(PDIFTQI(JLON,JLEV)+ZDUTQI(JLON,JLEV)-PDIFTQI(JLON,JLEV-1))
      ZSIGN=MAX(0.0_JPRB,SIGN(1.0_JPRB,-PDIFTQI(JLON,JLEV-1)))
      ZDUTQI(JLON,JLEV-1)=ZSIGN*MIN(MAX(0.0_JPRB,-ZQI(JLON)*ZPOID(JLON,JLEV)),MAX(0.0_JPRB,-PDIFTQI(JLON,JLEV-1)))
    ENDDO
  ENDDO
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZQL(JLON)=PQL(JLON,JLEV)-ZIPOI(JLON,JLEV)*(PDIFTQL(JLON,JLEV)-PDIFTQL(JLON,JLEV-1)-ZDDTQL(JLON,JLEV-1))
      ZSIGN=MAX(0.0_JPRB,SIGN(1.0_JPRB,PDIFTQL(JLON,JLEV)))
      ZDDTQL(JLON,JLEV)=ZSIGN*MAX(MIN(0.0_JPRB,ZQL(JLON)*ZPOID(JLON,JLEV)),MIN(0.0_JPRB,-PDIFTQL(JLON,JLEV)))
      ZQI(JLON)=PQI(JLON,JLEV)-ZIPOI(JLON,JLEV)*(PDIFTQI(JLON,JLEV)-PDIFTQI(JLON,JLEV-1)-ZDDTQI(JLON,JLEV-1))
      ZSIGN=MAX(0.0_JPRB,SIGN(1.0_JPRB,PDIFTQI(JLON,JLEV)))
      ZDDTQI(JLON,JLEV)=ZSIGN*MAX(MIN(0.0_JPRB,ZQI(JLON)*ZPOID(JLON,JLEV)),MIN(0.0_JPRB,-PDIFTQI(JLON,JLEV)))
    ENDDO
  ENDDO
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTH               =(PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB
      ZDTQFL(JLON)      =ZDUTQL(JLON,JLEV)+ZDDTQL(JLON,JLEV)
      PDIFTQL(JLON,JLEV)=PDIFTQL(JLON,JLEV)+ZDTQFL(JLON)
      PDIFTQ(JLON,JLEV) =PDIFTQ(JLON,JLEV)-ZDTQFL(JLON)
      PDIFTS(JLON,JLEV) =PDIFTS(JLON,JLEV)+ZDTQFL(JLON)*FOLH(ZTH,0.0_JPRB)
      ZDTQFI(JLON)      =ZDUTQI(JLON,JLEV)+ZDDTQI(JLON,JLEV)
      PDIFTQI(JLON,JLEV)=PDIFTQI(JLON,JLEV)+ZDTQFI(JLON)
      PDIFTQ(JLON,JLEV) =PDIFTQ(JLON,JLEV)-ZDTQFI(JLON)
      PDIFTS(JLON,JLEV) =PDIFTS(JLON,JLEV)+ZDTQFI(JLON)*FOLH(ZTH,1.0_JPRB)
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      PDIFTQL(JLON,JLEV)=0.0_JPRB
      PDIFTQI(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!-------------------------------------------------------------------------------
! VI - Upgrade the TKE increment with TOMs corrections
!-------------------------------------------------------------------------------
IF((.NOT.LCOEFK_FLX).AND.LCOEFK_TOMS.AND.(KSTEP>1)) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTEND_TERM1=-(PMN2_ES(JLON,JLEV-1)*ZDIFTSTOM(JLON,JLEV-1)+PMN2_EQ(JLON,JLEV-1) &
        & *ZDIFTQTOM(JLON,JLEV-1))/ZRHO(JLON,JLEV-1)
      ZTEND_TERM2=-(PMN2_ES(JLON,JLEV)*ZDIFTSTOM(JLON,JLEV)+PMN2_EQ(JLON,JLEV) &
        & *ZDIFTQTOM(JLON,JLEV))/ZRHO(JLON,JLEV)
      PTENDPTKE(JLON,JLEV)=PTENDPTKE(JLON,JLEV)+TSPHY*(PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)*ZTEND_TERM1 &
        & +(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))*ZTEND_TERM2)
    ENDDO
  ENDDO
ENDIF

! ------- DR HOOK --------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACDIFV3',1,ZHOOK_HANDLE)

END SUBROUTINE ACDIFV3
