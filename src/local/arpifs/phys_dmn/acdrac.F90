!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACDRAC ( YDPHY0,YDPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPRS,PNBVNO,PRDELP,PU,PV,&
 ! - INPUT  1D .
 & PFPLCL,PFPLCN,PSCCH,PBCCH,&
 ! - INPUT-OUTPUT 2D .
 & PSTRCU,PSTRCV)

!**** *ACDRAC * - EFFET DES ONDES DE GRAVITE FORCEES PAR LA CONVECTION.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES EFFETS SUR LA QUANTITE DE MOUVEMENT DU DEFERLEMENT
!       DES ONDES DE GRAVITE FORCEES PAR LA CONVECTION.
!     - COMPUTATION OF CONVECTIVE GRAVITY-WAVE BREAKING EFFECTS ON
!       MOMENTUM.

!**   Interface.
!     ----------
!        *CALL* *ACDRAC*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PNBVNO     : FREQUENCE DE BRUNT-VAISALA DIVISEE PAR G FOIS LA DENSITE.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.

! - 2D (1:KLEV) .

! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D

! PSCCH      : PSEUDO-HISTORICAL ARRAY FOR CONVECTIVE CLOUD SUMMIT (1D).
! PBCCH      : PSEUDO-HISTORICAL ARRAY FOR CONVECTIVE CLOUD BASE (1D).

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PSTRCU     : FLUX "GRAVITY WAVE DRAG" + CONVECTION "U".
! PSTRCV     : FLUX "GRAVITY WAVE DRAG" + CONVECTION "V".

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!      93-01, D. Cariolle

!     Modifications.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      2003-04-20, M. Bellus: new pseudo-historical arrays PSCCH and
!         PBCCH (previously contained in PNEBH), cleaning
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      2011-06: M. Jerczynski - some cleaning to meet norms
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG
USE YOMPHY0  , ONLY : TPHY0
USE YOMPHY2  , ONLY : TPHY2

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
TYPE(TPHY2)       ,INTENT(IN)    :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNBVNO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCCH(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBCCH(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCV(KLON,0:KLEV) 

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IUPC(KLON), IDWC(KLON)
REAL(KIND=JPRB) ::ZIPOI(KLON,KLEV),ZRAPP(KLON,0:KLEV)&
 & ,ZSTRCU(KLON,0:KLEV),ZSTRCV(KLON,0:KLEV)&
 & ,ZFACS(KLON),ZSTUS(KLON),ZSTVS(KLON),ZUS(KLON)  
REAL(KIND=JPRB) :: ZCU(KLON),ZCV(KLON),ZUSO(KLON),ZVSO(KLON),ZNSO(KLON)&
 & ,ZUBA(KLON),ZVBA(KLON),ZGWC(KLON),ZPSO(KLON),ZPBA(KLON)  

INTEGER(KIND=JPIM) :: IAUPC, IBUPC, IIDWC, IIUPC, INDCO, IZGWC, JLEV, JLON

REAL(KIND=JPRB) :: ZALPHA, ZDX, ZEPS1, ZEPS2, ZGDT, ZLINP, ZPRRAP, ZUSU, ZUSUS, ZX
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACDRAC',0,ZHOOK_HANDLE)
ASSOCIATE(GWDCCO=>YDPHY0%GWDCCO, &
 & TSPHY=>YDPHY2%TSPHY)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTES DE SECURITE (POUR
!     LE CARRE DU MODULE DU VENT ET POUR LA STABILITE STATIQUE
!     NORMALISEE (INPUT)).

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS (FOR
!     THE SQUARE OF THE WIND SPEED AND FOR THE NORMALIZED STATIC
!     STABILITY (INPUT)).

ZGDT=RG*TSPHY

ZEPS1=1.E-03_JPRB
ZEPS2=1.E-09_JPRB

!*
!     ------------------------------------------------------------------
!     II - CALCULS PRELIMINAIRES DE L'EPAISSEUR EN PRESSION DES COUCHES
!     DIVISEE PAR G*DT ET DE SON INVERSE POUR CHAQUE NIVEAU.

!          PRELIMINARY COMPUTATIONS FOR THE LAYER'S PRESSURE THICKNESS
!     DIVIDED BY G*DT AND ITS INVERSE FOR ALL LEVELS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZIPOI     : (RG*DT)/DP POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!            : (RG*DT)/DP FOR A GIVEN LEVEL AND A GIVEN TIME STEP.

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     III - CALCULS AU SOMMET DU NUAGE ET INITIALISATION POUR LA BOUCLE
!     ASCENDANTE.

!           CLOUD TOP COMPUTATIONS AND INITIALIZATION FOR THE ASCENDING
!     LOOP.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZRAPP     : RAPPORT DU FLUX EN ALTITUDE A CELUI AU SOMMET DU NUAGE.
!            : RATIO OF THE UPPER AIR FLUX TO THE CLOUD UPPER BOUNDARY.

! - TEMPORAIRE(S) 1D .

! IUPC      : SOMMET DU NUAGE
!            : CLOUD UPPER BOUNDARY.
! IDWC      : BASE DU NUAGE
!            : CLOUD LOWER BOUNDARY.
! ZUS       : MODULE DU VENT AU SOMMET.
!            : WIND SPEED AT THE CLOUD UPPER BOUNDARY.
! ZUSO      : VENT U AU SOMMET.
!            : U-WIND AT THE CLOUD UPPER BOUNDARY.
! ZVSO      : VENT V AU SOMMET.
!            : V-WIND AT THE CLOUD UPPER BOUNDARY.
! ZNSO      : FREQUENCE DE BRUNT-VAISSALA NORMALISEE U AU SOMMET.
!            : SCALED B-V FREQUENCY AT THE CLOUD UPPER BOUNDARY.
! ZUBA      : VENT U A LA BASE.
!            : U-WIND AT THE CLOUD LOWER BOUNDARY.
! ZVBA      : VENT V A LA BASE.
!            : V-WIND AT THE CLOUD LOWER BOUNDARY.
! ZPSO      : PRESSION AU SOMMET.
!            : PRESSURE AT THE CLOUD UPPER BOUNDARY.
! ZPBA      : PRESSION A LA BASE.
!            : PRESSURE AT THE CLOUD LOWER BOUNDARY.
! ZFACS     : FACTEUR AU SOMMET DU NUAGE POUR LE CALCUL DE ZRAPP.
!            : SURFACE FACTOR FOR THE COMPUTATION OF ZRAPP.
! ZGWC      : MODULE DU FLUX AU SOMMET.
!            : FLUX MODULUS AT THE CLOUD UPPER BOUNDARY.
! ZSTUS     : VALEUR "LINEAIRE" DU FLUX U AU SOMMET.
!            : "LINEAR" VALUE OF THE U- UPPER CLOUD FLUX.
! ZSTVS     : VALEUR "LINEAIRE" DU FLUX V AU SOMMET.
!            : "LINEAR" VALUE OF THE V- UPPER CLOUD FLUX.
! ZCU       : COMPOSANTE U DE LA PHASE DE L'ONDE AU SOMMET.
!            : U-COMPONENT OF THE WAVE PHASE AT THE CLOUD UPPER BOUNDARY.
! ZCV       : COMPOSANTE V DE LA PHASE DE L'ONDE AU SOMMET.
!            : V-COMPONENT OF THE WAVE PHASE AT THE CLOUD UPPER BOUNDARY.

INDCO=0
DO JLON=KIDIA,KFDIA
  ZGWC(JLON)=GWDCCO*(PFPLCL(JLON,KLEV)+PFPLCN(JLON,KLEV))
  IZGWC=INT(1.0_JPRB+SIGN(0.5_JPRB,ZGWC(JLON)-ZEPS2))
  IUPC(JLON)=MAX(IZGWC*NINT(PSCCH(JLON)),KTDIA+1)
  IUPC(JLON)= MIN(IUPC(JLON),KLEV-1)
  IDWC(JLON)=MAX(NINT(PBCCH(JLON)),IUPC(JLON)+1)
  IDWC(JLON)= MIN(KLEV,IDWC(JLON))
  ZUSO(JLON)=0.0_JPRB
  ZVSO(JLON)=0.0_JPRB
  ZNSO(JLON)=0.0_JPRB
  ZUBA(JLON)=0.0_JPRB
  ZVBA(JLON)=0.0_JPRB
  ZPSO(JLON)=0.0_JPRB
  ZPBA(JLON)=0.0_JPRB
  INDCO=INDCO+IZGWC
ENDDO

IF(INDCO /= 0) THEN

!     ON NE CONTINUE QUE SI AU MOINS UNE VERTICALE EST CONVECTIVE
!     WE GO ON ONLY IF AT LEAST ONE VERTICAL IS CONVECTIVE

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZRAPP(JLON,JLEV)=1.0_JPRB
      IIUPC=MAX(0,1-IABS(JLEV-IUPC(JLON)))
      IIDWC=MAX(0,1-IABS(JLEV-IDWC(JLON)))
      ZUSO(JLON)=ZUSO(JLON)+PU(JLON,JLEV)*IIUPC
      ZVSO(JLON)=ZVSO(JLON)+PV(JLON,JLEV)*IIUPC
      ZNSO(JLON)=ZNSO(JLON)+PNBVNO(JLON,JLEV)*IIUPC
      ZUBA(JLON)=ZUBA(JLON)+PU(JLON,JLEV)*IIDWC
      ZVBA(JLON)=ZVBA(JLON)+PV(JLON,JLEV)*IIDWC
      ZPSO(JLON)=ZPSO(JLON)+PAPRS(JLON,JLEV)*IIUPC
      ZPBA(JLON)=ZPBA(JLON)+PAPRS(JLON,JLEV)*IIDWC
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZUSUS=MAX(ZEPS1,ZUSO(JLON)**2+ZVSO(JLON)**2)
    ZUS(JLON)=SQRT(ZUSUS)
    ZSTUS(JLON)=ZUSO(JLON)/ZUS(JLON)
    ZSTVS(JLON)=ZVSO(JLON)/ZUS(JLON)
    ZCU(JLON)=ZUBA(JLON)*ZSTUS(JLON)+ZVBA(JLON)*ZSTVS(JLON)
    ZCV(JLON)=ZCU(JLON)*ZSTVS(JLON)
    ZCU(JLON)=ZCU(JLON)*ZSTUS(JLON)
    ZSTUS(JLON)=ZGWC(JLON)*ZSTUS(JLON)
    ZSTVS(JLON)=ZGWC(JLON)*ZSTVS(JLON)
    ZUSUS=MAX(ZEPS1,(ZUSO(JLON)-ZCU(JLON))**2+(ZVSO(JLON)-ZCV(JLON))**2)
    ZFACS(JLON)=ZNSO(JLON)/ZUSUS**3
  ENDDO
  
!*
!     ------------------------------------------------------------------
!     IV - CALCULS ASCENDANTS DE L'ESTIMATION LINEAIRE DU FLUX RELATIF
!          ASCENDING COMPUTATIONS OF THE LINEAR ESTIMATE OF THE RELATIVE FLUX
  
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      IBUPC=(1+SIGN(1,JLEV-IUPC(JLON)))/2
      ZUSU=(0.5_JPRB*(PU(JLON,JLEV)+PU(JLON,JLEV+1))-ZCU(JLON))&
       & *(ZUSO(JLON)-ZCU(JLON))&
       & +(0.5_JPRB*(PV(JLON,JLEV)+PV(JLON,JLEV+1))-ZCV(JLON))&
       & *(ZVSO(JLON)-ZCV(JLON))  
      ZPRRAP=ZFACS(JLON)*ZUSU**3/MAX(ZEPS2,PNBVNO(JLON,JLEV))
      ZPRRAP=IBUPC+(1-IBUPC)*ZPRRAP
      ZRAPP(JLON,JLEV)=MAX(0.0_JPRB,MIN(ZRAPP(JLON,JLEV+1),ZPRRAP))
    ENDDO
  ENDDO
  
!*
!     ------------------------------------------------------------------
!     V - BOUCLE DESCENDANTE POUR LA SECURITE
!     NUMERIQUE ET LE CALCUL DEFINITIF DES FLUX AU DESSUS DU NUAGE.
!     MISE EN PSEUDO-IMPLICITE (INSPIRE PAR UNE FORMULATION FLUX DE MASSE)

!         DESCENDING LOOP FOR THE NUMERICAL
!     SECURITY AND THE FINAL FLUX CALCULATION ABOVE THE CLOUD.
!     PSEUDO-IMPLICIT ALGORITHM (INSPIRED BY A MASS-FLUX FORMULATION)
  
  DO JLON=KIDIA,KFDIA
    ZSTRCU(JLON,KTDIA-1)=0.0_JPRB
    ZSTRCV(JLON,KTDIA-1)=0.0_JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZSTRCU(JLON,JLEV)=ZRAPP(JLON,JLEV)*ZSTUS(JLON)
      ZSTRCV(JLON,JLEV)=ZRAPP(JLON,JLEV)*ZSTVS(JLON)
      ZDX=ZIPOI(JLON,JLEV)*(ZUSO(JLON)*ZSTRCU(JLON,JLEV)+ZVSO(JLON)&
       & *ZSTRCV(JLON,JLEV))  
      ZX=ZUSO(JLON)*(PU(JLON,JLEV)-ZCU(JLON))&
       & +ZVSO(JLON)*(PV(JLON,JLEV)-ZCV(JLON))  
      ZALPHA=1.0_JPRB/(1.0_JPRB+ZDX/MAX(ZEPS2,ZX))
      ZSTRCU(JLON,JLEV)=ZSTRCU(JLON,JLEV-1)-ZALPHA*&
       & (ZSTRCU(JLON,JLEV-1)-ZSTRCU(JLON,JLEV))  
      ZSTRCV(JLON,JLEV)=ZSTRCV(JLON,JLEV-1)-ZALPHA*&
       & (ZSTRCV(JLON,JLEV-1)-ZSTRCV(JLON,JLEV))  
    ENDDO
  ENDDO
  
!*
!     ------------------------------------------------------------------
!     VI - DEPOSITION DU MOMENT :
!       A L'INTERIEUR DU NUAGE, LE FLUX DE
!     MOMENT EST SUPPOSE ETRE PROPORTIONNEL A LA PRESSION.
!       EN DESSOUS DU NUAGE LE FLUX DE MOMENT EST NUL
  
!          TREATMENT OF THE MOMENTUM DEPOSITION :
!       INSIDE THE CLOUD: LINEAR FUNCTION OF PRESSURE.
!       BELOW THE CLOUD : THE FLUX IS ZERO.
  
  DO JLON=KIDIA,KFDIA
    ZSTUS(JLON)=0.0_JPRB
    ZSTVS(JLON)=0.0_JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      IIUPC=MAX(0,1-IABS(JLEV-IUPC(JLON)))
      IAUPC=(1+SIGN(1,IUPC(JLON)-JLEV))/2
      ZSTUS(JLON)=ZSTUS(JLON)+IIUPC*ZSTRCU(JLON,JLEV)
      ZSTVS(JLON)=ZSTVS(JLON)+IIUPC*ZSTRCV(JLON,JLEV)
      ZLINP=MAX(0.0_JPRB,(PAPRS(JLON,JLEV)-ZPBA(JLON))/&
       & (ZPSO(JLON)-ZPBA(JLON)))*(1-IAUPC)  
      PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV)+ZSTRCU(JLON,JLEV)*IAUPC&
       & +ZSTUS(JLON)*ZLINP  
      PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV)+ZSTRCV(JLON,JLEV)*IAUPC&
       & +ZSTVS(JLON)*ZLINP  
    ENDDO
  ENDDO

ENDIF  

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACDRAC',1,ZHOOK_HANDLE)

END SUBROUTINE ACDRAC
