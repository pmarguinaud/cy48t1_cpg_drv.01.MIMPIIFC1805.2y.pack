!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACDROV ( YDCST, YDML_PHY_MF,KIDIA,KFDIA,KLON,KLEV,KCSS,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PFPLCL,PFPLCN,PFPLSL,PFPLSN,PFRSO,PFRTH,&
 ! - INPUT  1D .
 & PC1,PC2,PC3,PCN,PCT,PD2,PFEVV,PFTR,PLAI,PNEIJ,&
 & PVEG,PWFC,PWPMX,PWL,PWLMX,PWSEQ,PWSMX,&
 & PFCHSP,PFCLL,PFCLN,PFCS,PFEVI,PFEVL,PFEVN,&
 & PLSM,PSNS,&
 & PTP,PTS,PWP,PWPI,PWS,PWSI,&
 ! - OUTPUT 1D .
 & PFGEL,PFGELS,PFLWSP,PFONTE,PRUISP,PRUISL,PRUISS)

!**** *ACDROV  * - CALCUL DES FLUX D'EAU DANS LE SOL.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX D'EAU DANS LE SOL : FONTE DE LA NEIGE, TRANSFERT
!       VERS LE SOL PROFOND, RUISSELLEMENTS EN SURFACE ET EN PROFONDEUR .
!     - COMPUTATION OF WATER FLUXES IN SOIL : SNOW MELTING, SURFACE AND
!       SOIL RUN-OFF.

!**   Interface.
!     ----------
!        *CALL* *ACDROV*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KCSS       : NBRE DE NIVEAUX DANS LE SOL PROFOND.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PFPLSL     : PRECIPITATIONS STRATIFORMES SOUS FORME LIQUIDE.
! PFPLSN     : PRECIPITATIONS STRATIFORMES SOUS FORME NEIGE.
! PFRSO      : FLUX DE RAYONNEMENT SOLAIRE.
! PFRTH      : FLUX DE RAYONNEMENT THERMIQUE.

! - 1D (PROGNOSTIQUE) .

! PSNS       : MASSE PAR UNITE DE SURFACE DE LA COUCHE NEIGEUSE.
! PTP        : TEMPERATURE DE SUBSURFACE.
! PTS        : TEMPERATURE DE SURFACE.
! PWP        : CONTENU EN EAU DU RESERVOIR EN PROFONDEUR.
! PWPI       : CONTENU EN EAU GELEE DU RESERVOIR EN PROFONDEUR.
! PWL        : CONTENU EN EAU DU RESERVOIR D'INTERCEPTION.
! PWS        : CONTENU EN EAU DU RESERVOIR DE SURFACE.
! PWSI       : CONTENU EN EAU GELEE DU RESERVOIR DE SURFACE.

! - 1D (GEOGRAPHIQUE) .

! PD2        : PROFONDEUR DU RESERVOIR TOTAL (M)
! PLSM       : INDICE TERRE/MER.
! PLAI       : INDICE FOLIAIRE.
! PNEIJ      : FRACTION DE NEIGE.
! PVEG       : FRACTION DE SOL RECOUVERT DE VEGETATION.

! - 1D (DIAGNOSTIQUE) .

! PFCHSP     : FLUX DE CHALEUR DE LA SURFACE VERS LE SOL PROFOND.
! PFCLL      : FLUX DE CHALEUR LATENTE SUR EAU LIQUIDE (OU SOL HUMIDE).
! PFCLN      : FLUX DE CHALEUR LATENTE SUR NEIGE (OU GLACE).
! PFCS       : FLUX DE CHALEUR SENSIBLE EN SURFACE.
! PFEVI      : FLUX DE VAPEUR D'EAU SUR SOL GELE.
! PFEVL      : FLUX DE VAPEUR D'EAU SUR EAU LIQUIDE (OU SOL HUMIDE).
! PFEVN      : FLUX DE VAPEUR D'EAU SUR NEIGE (OU GLACE) ET SOL GELE.
! PC1        : COEF. HYDRIQUE REPRESENTANT L'INTENSITE AVEC LAQUELLE
!              LES FLUX DE SURFACE PARTICIPENT A L'EVOLUTION DE WS.
! PC2        : COEF. HYDRIQUE TRADUISANT LA RAPIDITE DES TRANSFERTS D'EAU
!              ENTRE LES DEUX RESERVOIRS.
! PC3        : COEFFICIENT UTILE POUR LE CALCUL DU DRAINAGE.
! PCN        : COEFFICIENT THERMIQUE DE LA NEIGE.
! PCT        : COEFFICIENT THERMIQUE DU MILIEU SOL-VEGETATION.
! PFEVV      : FLUX D'EVAPOTRANSPIRATION DE LA VEGETATION.
! PFTR       : FLUX DE TRANSPIRATION DE LA VEGETATION.
! PWFC       : CAPACITE AU CHAMP.
! PWPMX      : CONTENU EN EAU MAXIMAL DU RESERVOIR PROFOND.
! PWLMX      : CONTENU EN EAU MAXIMAL DU RESERVOIR D'INTERCEPTION.
! PWSEQ      : TENEUR EN EAU DE SURFACE A L'EQUILIBRE (GRAVITE-CAPILLARITE).
! PWSMX      : CONTENU EN EAU MAXIMAL DU RESERVOIR SUPERFICIEL.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 1D (DIAGNOSTIQUE) .

! PFGEL      : FLUX DE GEL POUR L'EAU DU SOL.
! PFGELS     : FLUX DE GEL POUR L'EAU DE SURFACE.
! PFLWSP     : FLUX D'EAU LIQUIDE DE LA SURFACE VERS LE SOL PROFOND.
! PFONTE     : FLUX D'EAU CORRESPONDANT A LA FONTE DE NEIGE EN SURFACE.
! PRUISP     : FLUX DE RUISSELLEMENT EN PROFONDEUR.
! PRUISL     : FLUX DE RUISSELLEMENT DU RESERVOIR D'INTERCEPTION.
! PRUISS     : FLUX DE RUISSELLEMENT EN SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!      91-12, J. Noilhan.

!     Modifications.
!     --------------
!      2001-02, Cor. d'une petite erreur dans PFONTE - E.Bazile.
!      R. El Khatib : 01-08-07 Pruning options
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KCSS 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRSO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRTH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC1(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC2(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PC3(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCT(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PD2(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFEVV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFTR(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLAI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVEG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWFC(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWPMX(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWLMX(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSEQ(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSMX(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCHSP(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCLL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCLN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFEVI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFEVL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFEVN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTP(KLON,KCSS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWP(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWPI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSI(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFGEL(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFGELS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFLWSP(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFONTE(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRUISP(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRUISL(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRUISS(KLON) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZITS(KLON)

INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZBFLO, ZCI, ZCONV, ZEPS1, ZFGEL, ZFGELS,&
 & ZFLDT, ZFONTE, ZFONTES, ZIDAY, ZITSP, ZNEIJC, &
 & ZPCNDT, ZPCTDT, ZSNSP, ZTF, ZTPP, ZTSP, ZVEG, &
 & ZWFC, ZWPID, ZWPIMX, ZWPMX, ZWPP, ZWSMX, &
 & ZWSP, ZZ, ZZK  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACDROV',0,ZHOOK_HANDLE)
ASSOCIATE(USUPRC=>YDML_PHY_MF%YRPHY0%USUPRC, &
 & GLAIMX=>YDML_PHY_MF%YRPHY1%GLAIMX, GVEGMXS=>YDML_PHY_MF%YRPHY1%GVEGMXS, GLAIMXS=>YDML_PHY_MF%YRPHY1%GLAIMXS, &
 & GCGEL=>YDML_PHY_MF%YRPHY1%GCGEL, GWPIMX=>YDML_PHY_MF%YRPHY1%GWPIMX, GNEIMXS=>YDML_PHY_MF%YRPHY1%GNEIMXS, &
 & GCGELS=>YDML_PHY_MF%YRPHY1%GCGELS, SODELX=>YDML_PHY_MF%YRPHY1%SODELX, GNEIMX=>YDML_PHY_MF%YRPHY1%GNEIMX, &
 & TMERGL=>YDML_PHY_MF%YRPHY1%TMERGL, GVEGMX=>YDML_PHY_MF%YRPHY1%GVEGMX, WCRINC=>YDML_PHY_MF%YRPHY1%WCRINC, &
 & RCTGLA=>YDML_PHY_MF%YRPHY1%RCTGLA, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LSNV=>YDML_PHY_MF%YRPHY%LSNV, LFGEL=>YDML_PHY_MF%YRPHY%LFGEL, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE)
!-----------------------------------------------------------------------

!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     CSDT).

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANT (FOR
!     CSDT).

ZITSP=1.0_JPRB/TSPHY
ZIDAY=1.0_JPRB/YDCST%RDAY

ZEPS1=1.E-08_JPRB

!     CONVERSION M --> KG/M**2
ZCONV=1.E+3_JPRB
!*
!     ------------------------------------------------------------------
!     II - MISE A DES VALEURS CONSTANTES DES VALEURS DERIVEES DE
!     L'INERTIE THERMIQUE ET DES TEMPS CARACTERISTIQUES (DIURNE PAR
!     DEFINITION POUR LA COUCHE SUPERFICIELLE) DU SOL AINSI QUE DES
!     CONTENUS EN EAU MAXIMA DONT LE RAPPORT FIXE CELUI DES TEMPS
!     CARACTERISTIQUES (PHYSIQUE DU SOL HYPER-SIMPLIFIEE).

!          SETTING TO CONSTANT VALUES THE SOIL THERMAL INERTIA AND
!     CHARACTERISTIC TIME CONSTANTS (DIURNAL BY DEFINITION FOR THE
!     SURFACE LAYER) AS WELL AS THE MAXIMUM WATER CONTENTS WHOSE RATIO
!     IMPOSES THE OTHER RATIO OF THE CHARACTERISTIC TIME CONSTANTS
!     (HYPER-SIMPLIFIED SOIL PHYSICS).

! - TEMPORAIRE(S) 1D .

! ZPCNDT     : PRODUIT DU PAS DE TEMPS PAR LA CONSTANTE DE LA NEIGE.
!            : PRODUCT OF TIME-STEP BY THE SNOW INERTIAL CONSTANT.
! ZPCTDT     : PRODUIT DU PAS DE TEMPS PAR LA CONSTANTE DU SOL.
!            : PRODUCT OF TIME-STEP BY THE SOIL INERTIAL CONSTANT.
! ZITS      : INVERSE DU TEMPS CARACTERISTIQUE EN SURFACE.
!            : INVERSE OF THE CHARACTERISTIC SURFACE TIME CONSTANT.

DO JLON=KIDIA,KFDIA
  ZITS(JLON)=PLSM(JLON)*ZIDAY
ENDDO

!     ------------------------------------------------------------------
!     III - CALCULS PROPREMENTS DITS.

!           EFFECTIVE COMPUTATIONS.

DO JLON=KIDIA,KFDIA
!*******************************************************************************
!     CALCUL DU RUISSELLEMENT DU RESERVOIR D'INTERCEPTION.
!     COMPUTATION OF INTERCEPTION RUN-OFFS.
!*******************************************************************************

!     ZVEG EST UNE FRACTION DE VEGETATION APPARENTE:
!        QUAND IL PLEUT BEAUCOUP (CONVECTIF) UNE FRACTION DU RESERVOIR
!        FOLIAIRE SEULEMENT SE REMPLIT
!     ZVEG IS A PSEUDO VEGETATION FRACTION:
!        IT DECREASES WITH RAINFALL. THE INTERCEPTION BY VEGETATION
!        IS LIMITED BY PVEG/USUPRC
  ZVEG=PVEG(JLON)/(1.0_JPRB+USUPRC*PFPLCL(JLON,KLEV))
  ZFLDT=TSPHY*(ZVEG*(PFPLSL(JLON,KLEV)+PFPLCL(JLON,KLEV))&
   & +PFEVV(JLON)-PFTR(JLON))  
  PRUISL(JLON)=ZITSP*PLSM(JLON)*(ZFLDT-MIN(PWLMX(JLON)-PWL(JLON),&
   & MAX(-PWL(JLON),ZFLDT)))  
  ZBFLO=(1.0_JPRB-ZVEG)*(PFPLSL(JLON,KLEV)+PFPLCL(JLON,KLEV))&
   & +PRUISL(JLON)+PFEVL(JLON)-PFEVV(JLON)  

!*******************************************************************************
!     CALCULS DE FONTE EN CAS D'OPTION NEIGE.
!     MELTING CALCULATIONS IN CASE OF ACTIVATED SNOW OPTION.
!*******************************************************************************

  IF (LNEIGE) THEN
    ZPCTDT = PCT(JLON)*TSPHY*PLSM(JLON)
    ZTSP = PTS(JLON)+ZPCTDT&
     & *(PFRSO(JLON,KLEV)+PFRTH(JLON,KLEV)+PFCS(JLON)&
     & +PFCLL(JLON)+PFCLN(JLON)-PFCHSP(JLON))  
    ZSNSP = PSNS(JLON)*ZITSP+(PFPLSN(JLON,KLEV)+PFPLCN(JLON,KLEV)&
     & +PFEVN(JLON)-PFEVI(JLON))  
    IF (LSNV) THEN
      ZTF = (1.0_JPRB-PVEG(JLON))*ZTSP + PVEG(JLON)*PTP(JLON,1)
      PFONTE(JLON)=MAX(0.0_JPRB,(ZTF-(PLSM(JLON)*YDCST%RTT&
       & +(1.0_JPRB-PLSM(JLON))*TMERGL))&
       & /(MAX(ZEPS1,ZPCTDT)*YDCST%RLMLT))  
      ZNEIJC=MAX(0.0_JPRB,MIN(1.0_JPRB,PSNS(JLON)/WCRINC))
      ZPCNDT = PCN(JLON)*TSPHY*PLSM(JLON)
      PFONTE(JLON)=ZNEIJC/MAX(ZEPS1,ZPCNDT)*MAX(ZEPS1,ZPCTDT)*PFONTE(JLON)
      PFONTE(JLON)=PLSM(JLON)*MIN(ZSNSP,PFONTE(JLON))
    ELSE
      PFONTE(JLON)=PLSM(JLON)&
       & *MIN(ZSNSP,MAX(0.0_JPRB,(ZTSP-(PLSM(JLON)&
       & *YDCST%RTT+(1.0_JPRB-PLSM(JLON))*TMERGL))&
       & /(MAX(ZEPS1,ZPCTDT)*YDCST%RLMLT)))  
    ENDIF

    ZTSP = ZTSP - ZPCTDT * YDCST%RLMLT * PFONTE(JLON)

    IF (LFGEL) THEN

!  GEL DE SURFACE.
      ZZK=PLSM(JLON)*GCGELS*MAX(0.0_JPRB,1.0_JPRB-PVEG(JLON)/GVEGMXS)*&
       & MAX(0.0_JPRB,1.0_JPRB-PLAI(JLON)/GLAIMXS) *&
       & MAX(0.0_JPRB,1.0_JPRB-PNEIJ(JLON)/GNEIMXS)  
      ZZ = ZZK * (YDCST%RTT-ZTSP)/(RCTGLA*YDCST%RLMLT)
      ZWSP = MAX(0.0_JPRB,PWS(JLON))
      ZFGELS= ZWSP/PWSMX(JLON)*MAX(0.0_JPRB,ZZ)
      ZFGELS=MAX(0.0_JPRB,MIN(ZFGELS,ZWSP*ZITSP))
      ZFONTES=MIN(0.0_JPRB,ZZ)
      ZFONTES=-MAX(0.0_JPRB,MIN(-ZFONTES,PWSI(JLON)*ZITSP))
      PFGELS(JLON)=ZFGELS+ZFONTES

!   GEL PROFOND DANS LE CAS DU GEL DE SURFACE
      ZZK=PLSM(JLON)*GCGEL*MAX(0.0_JPRB,1.0_JPRB-PVEG(JLON)/GVEGMX)*&
       & MAX(0.0_JPRB,1.0_JPRB-PLAI(JLON)/GLAIMX) *&
       & MAX(0.0_JPRB,1.0_JPRB-PNEIJ(JLON)/GNEIMX)  
      ZTPP= PTP(JLON,1) + ZPCTDT * (SODELX(0)/SODELX(1)) *PFCHSP(JLON)
      ZZ = ZZK * (YDCST%RTT-ZTPP)/(RCTGLA*YDCST%RLMLT)
      ZWPP = MAX(0.0_JPRB,PWP(JLON)-PWS(JLON)-PWSI(JLON))
      ZFGEL= ZWPP/PWPMX(JLON)*MAX(0.0_JPRB,ZZ)
      ZFGEL=MAX(0.0_JPRB,MIN(ZFGEL,ZWPP*ZITSP))
      ZWPIMX=MIN(GWPIMX,PWPMX(JLON))
      ZWPID=MAX(0.0_JPRB,PWPI(JLON)-PWSI(JLON))
      ZFGEL=MAX(0.0_JPRB,MIN((ZWPIMX-ZWPID)*ZITSP,ZFGEL))
      ZFONTE=MIN(0.0_JPRB,ZZ)
      ZFONTE=-MAX(0.0_JPRB,MIN(-ZFONTE,ZWPID*ZITSP))
      PFGEL(JLON)=ZFGEL+ZFONTE
    ELSE
      PFGEL(JLON)=0.0_JPRB
      PFGELS(JLON)=0.0_JPRB
    ENDIF
  ELSE
    PFONTE(JLON)=0.0_JPRB
    PFGEL(JLON)=0.0_JPRB
    PFGELS(JLON)=0.0_JPRB
  ENDIF

!*******************************************************************************
!     CALCULS DU TRANSFERT VERTICAL ET DU RUISSELLEMENT SUPERFICIEL.
!     COMPUTATIONS OF VERTICAL TRANSFER AND OF SUPERFICIAL RUN-OFFS.
!*******************************************************************************
  PFLWSP(JLON)=PC2(JLON)*ZITS(JLON)*(PWS(JLON)-PWSEQ(JLON))
  ZBFLO= ZBFLO + PFONTE(JLON)
  ZCI=PC2(JLON)*TSPHY*ZITS(JLON)
  IF (LFGEL) THEN
    PFLWSP(JLON)=PC2(JLON)*ZITS(JLON)*&
     & (PWS(JLON)-MAX(0.0_JPRB,PWSEQ(JLON)-PWSI(JLON)))  
    ZWSMX=PWSMX(JLON)-PWSI(JLON)
    PFLWSP(JLON)=MAX(-ZITSP*PWP(JLON),PFLWSP(JLON))
  ELSE
    ZWSMX=PWSMX(JLON)
  ENDIF
  ZFLDT=TSPHY*( (PC1(JLON)*ZBFLO-PFLWSP(JLON))/(1.0_JPRB+ZCI) -PFGELS(JLON) )
  PRUISS(JLON)=ZITSP*PLSM(JLON)*(ZFLDT-MIN(ZWSMX-PWS(JLON),&
   & MAX(-PWS(JLON),ZFLDT)))  

!*******************************************************************************
!     CALCUL DU RUISSELLEMENT DU RESERVOIR PROFOND.
!     COMPUTATION OF DEPP RUN-OFFS.
!*******************************************************************************

  ZFLDT=TSPHY*(ZBFLO+PFTR(JLON)-PFGEL(JLON))
  IF (LFGEL) THEN
    ZWPMX = MAX(0.0_JPRB,PWPMX(JLON)-PWPI(JLON))
    ZWFC  = MAX(0.0_JPRB,PWFC(JLON)*PD2(JLON)*ZCONV-PWPI(JLON))
  ELSE
    ZWPMX = PWPMX(JLON)
    ZWFC  = PWFC(JLON)*PD2(JLON)*ZCONV
  ENDIF

  PRUISP(JLON)=ZITSP*PLSM(JLON)*(ZFLDT-MIN(ZWPMX&
   & -PWP(JLON),MAX(-PWP(JLON),ZFLDT)))&
   & +PC3(JLON)*ZITS(JLON)*MAX(0.0_JPRB,PWP(JLON)-ZWFC)  
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACDROV',1,ZHOOK_HANDLE)
END SUBROUTINE ACDROV
