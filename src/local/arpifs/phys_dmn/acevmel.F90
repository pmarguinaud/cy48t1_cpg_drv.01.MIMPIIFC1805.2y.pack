!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACEVMEL ( YDML_PHY_MF,KIDIA,KFDIA,KLON,&
 !-----------------------------------------------------------------------
 ! - INPUT LOGICAL
 & LDSIMP,&
 ! - INPUT 1D .
 & PDQ,PEXPN,PHPLSL,PHPLSN,PHPLSG,PIPB,PIPBR,PIPH,PIPHR,PIPOI,PLPLSL,PLPLSN,PLPLSG,&
 & PPOID,PQNST,PQRST,PQGST,PQVST,PSTAL3,PSTAN3,PSTAG3,PTST,PLPRSF,PLSCP,PRHOAIR,&
 & PRPCEFF,&
 ! - OUTPUT 1D .
 & PEVAN,PEVAR,PEVAG,PFONT,PFONTG,PTEST)

!**** *ACEVMEL* - CALCULS D'AUTO-CONVERSION.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF (SUR UN NIVEAU).
!       CALCUL DE TROIS INCREMENTS D'EVAPORATION-FONTE/GEL.
!     - COMPUTATION OF THREE EVAPORATION-MELTING/FREEZING INCREMENTS.

!**   Interface.
!     ----------
!        *CALL* *ACEVMEL*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLMPHYS" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.

! - ARGUMENT DE CONTROLE D'UNE VERSION SIMPLIFIEE (FONTE/GEL SEULS).

! LDSIMP     : IF .TRUE., CAS SIMPLIFIE.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D .

! PDQ        : DEFICIT DE SATURATION (EN TERME DE 'QW-Q').
! PEXPN      : FONCTION DE DEPENDANCE EN TEMPERATURE DES PROCESSUS < 0°C.
! PHPLSL     : FLUX DE PRECIPITATION LIQUIDE AU SOMMET DE LA COUCHE.
! PHPLSN     : FLUX DE PRECIPITATION NEIGEUSE AU SOMMET DE LA COUCHE.
! PHPLSG     : FLUX DE PRECIPITATION GRAUPEL AU SOMMET DE LA COUCHE.
! PIPB       : INVERSE DE LA PRESSION AU BAS DE LA COUCHE.
! PIPBR      : PIPB A LA PUISSANCE 5/4.
! PIPH       : INVERSE DE LA PRESSION AU SOMMET DE LA COUCHE.
! PIPHR      : PIPH A LA PUISSANCE 5/4.
! PIPOI      : FACTEUR DE CONVERSION DE D_P FLUX VERS D_T Q_X.
! PLPLSL     : FLUX DE PRECIPITATION LIQUIDE AU BAS DE LA COUCHE.
! PLPLSN     : FLUX DE PRECIPITATION NEIGEUSE AU BAS DE LA COUCHE.
! PLPLSG     : FLUX DE PRECIPITATION GRAUPEL AU BAS DE LA COUCHE.
! PPOID      : FACTEUR DE CONVERSION DE D_T Q_X VERS D_P FLUX.
! PQNST      : QUANTITE COURANTE D'EAU SOLIDE PRECIPITANTE.
! PQRST      : QUANTITE COURANTE D'EAU LIQUIDE PRECIPITANTE.
! PQGST      : QUANTITE COURANTE GRAUPEL.
! PQVST      : QUANTITE COURANTE D'EAU VAPEUR.
! PSTAL3     : PROBABILITE SOURCE 'PLUIE' POUR LA SEDIMENTATION.
! PSTAN3     : PROBABILITE SOURCE 'NEIGE' POUR LA SEDIMENTATION.
! PSTAG3     : PROBABILITE SOURCE 'GRAUPEL' POUR LA SEDIMENTATION
! PTST       : VALEUR COURANTE DE LA TEMPERATURE A L'ECHELLE DE LA MAILLE.
! PLPRSF     : PRESSION AU MILIEU DE LA COUCHE.
! PLSCP      : CHALEUR LATENTE DE FUSION DIVISEE PAR CP.
! PRHOAIR    : DENSITE DE L'AIR.
! PRPCEFF    : RAPPORT DES CONSTANTES A 0°C POUR LES CHANGEMENTS DE PHASE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 1D .

! PEVAN      : INCREMENT D'EVAPORATION DE LA NEIGE.
! PEVAR      : INCREMENT D'EVAPORATION DE LA PLUIE.
! PEVAG      : INCREMENT D'EVAPORATION GRAUPEL.
! PFONT      : INCREMENT DE FONTE DE LA NEIGE (-GEL DE LA PLUIE).
! PFONTG     : INCREMENT DE FONTE GRAUPEL
! PTEST      : TEST POUR LA PERPETUATION DU FLUX DE PRECIPITATION.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        07-02, J.-F. Geleyn (extraction from APLMPHYS).

!     Modifications.
!     --------------
!        07-05, Help for corr. of logical 'P1' bug - R. Brozkova & J.-F. Geleyn
!        07-06, No more fallspeed impact on EVA/FON - R. Brozkova & J.-F. Geleyn
!        07-07, Option of the ARPEGE scheme - J.-F. Geleyn & J.-M. Piriou
!        07-08, Progressive extinction of EVA/FON - R. Brozkova & J.-F. Geleyn
!        07-08, Local computation of the ice proportion - J.-F. Geleyn
!        07-10, Differentiation melting vs. freezing - D. Banciu & J.-F. Geleyn
!        07-12, Full evaporation simplification - R. Brozkova
!        08-02, ARPEGE limitation of the evaporation rate - J.-F. Geleyn
!        08-10, Correction (ZQS-ZQ rather than ZDQ) in ARPEGE case - B. Catry
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        J. Van den Bergh: adapting for prognostic graupel
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RTT      ,RV       ,RCS      ,RCW      ,RCPV     ,&
 &                    RLVTT    ,RLSTT    ,RETV     ,RALPW    ,RALPS    ,&
 &                    RALPD    ,RBETW    ,RBETS    ,RBETD    ,RGAMW    ,&
 &                    RGAMS    ,RGAMD

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
LOGICAL           ,INTENT(IN)    :: LDSIMP
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDQ(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEXPN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSL(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHPLSG(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPB(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPBR(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPH(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPHR(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPOI(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSL(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSN(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPLSG(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPOID(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQNST(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQRST(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQGST(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQVST(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTAL3(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTAN3(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSTAG3(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTST(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLPRSF(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCP(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRHOAIR(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRPCEFF(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEVAN(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEVAR(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEVAG(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFONT(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFONTG(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTEST(KLON)

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLON

LOGICAL :: LLEVAPX

REAL(KIND=JPRB) :: ZEVA, ZEVAPN, ZFON, ZRPTH, ZRPTB, &
 & ZEVAPO, ZFONTLP, ZFONTLM, ZFPLSL, ZFPLSN, ZFPLSG, ZEPS1, ZCOEFFC, &
 & ZCOEFFD, ZCOEFFE, ZCOEFFF, ZEXPD, ZEXPE, ZEXPP, ZCEXPN, ZFACT, ZFACT1, &
 & ZFACT2, ZIDARV, ZDELT, ZQRST, ZQNST, ZQGST, ZESW, ZSSATW, ZCONDT, ZCEV, ZARG, &
 & ZNINTS, ZINT, ZRME, ZRMN, ZRMG, ZGELSFON, ZEVASX, ZSSATI, ZQSATW, ZQSATI, &
 & ZINTW, ZINTI, ZFONTO, ZFONTLG
REAL (KIND=JPRB) :: ZGAMMA, ZGAMMAR, ZMULEV, ZMULEVR, ZMULMF, ZMULMFR, &
 & ZRPTHR, ZRPTBR, ZMELT, ZMFREE, ZEXPNI
REAL (KIND=JPRB) :: ZRPTH1, ZRPTB1, ZRPTM, ZRPTL, ZRPTME, ZRPTMER

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACEVMEL',0,ZHOOK_HANDLE)
ASSOCIATE(REVASX=>YDML_PHY_MF%YRPHY0%REVASX, EVAP=>YDML_PHY_MF%YRPHY0%EVAP, RNINTS=>YDML_PHY_MF%YRPHY0%RNINTS, &
 & RNINTR=>YDML_PHY_MF%YRPHY0%RNINTR, FONT=>YDML_PHY_MF%YRPHY0%FONT, RGELSFON=>YDML_PHY_MF%YRPHY0%RGELSFON, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LA0MPS=>YDML_PHY_MF%YRPHY%LA0MPS, LARPMPS=>YDML_PHY_MF%YRPHY%LARPMPS, LAB12=>YDML_PHY_MF%YRPHY%LAB12, &
 & LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
!-----------------------------------------------------------------------

LLEVAPX=.FALSE.
ZEVASX=2.E-07_JPRB

IF (LA0MPS) THEN

  ZEPS1=1.E-14

  ZGAMMA=0.5_JPRB
  ZGAMMAR=ZGAMMA
  ZMULEV=1.0_JPRB/((1.0_JPRB-ZGAMMA)*2.0_JPRB)
  ZMULEVR=ZMULEV
  ZMULMF=0.5_JPRB
  ZMULMFR=RGELSFON*ZMULMF

  IF (LAB12) THEN
    ZGAMMA=0.5479_JPRB
    ZGAMMAR=0.1611_JPRB
    ZMULEV=0.883_JPRB/((1.0_JPRB-ZGAMMA)*2.0_JPRB)
    ZMULEVR=2.047_JPRB/((1.0_JPRB-ZGAMMAR)*2.0_JPRB)
    ZMULMF=0.5_JPRB*0.985_JPRB
    ZMULMFR=RGELSFON*0.5_JPRB*1.231_JPRB
  ENDIF

  IF (LGRAPRO) THEN !PROGNOSTIC GRAUPEL
    DO JLON=KIDIA,KFDIA
      ZEXPNI=1.0_JPRB
      IF (LAB12) THEN
        ZEXPNI=1.0_JPRB/PEXPN(JLON)
      ENDIF

!     EVAPORATION.
!     EVAPORATION.

      IF (.NOT.LDSIMP) THEN
        ZRMN=PHPLSN(JLON)/MAX(ZEPS1,PHPLSL(JLON)/ZEXPNI+PHPLSN(JLON)+PHPLSG(JLON))
        ZRMG=PHPLSG(JLON)/MAX(ZEPS1,PHPLSL(JLON)/ZEXPNI+PHPLSN(JLON)+PHPLSG(JLON))
        ZRPTH1=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI+PHPLSG(JLON)*ZEXPNI)
        ZRPTB1=MAX(0.0_JPRB,PLPLSL(JLON)+PLPLSN(JLON)*ZEXPNI+PLPLSG(JLON)*ZEXPNI)

        ZRPTM=MAX(ZEPS1,0.5_JPRB*(ZRPTH1+ZRPTB1))

        IF (LAB12) THEN
          ZRPTL=LOG(ZRPTM)
          ZRPTME=EXP(ZGAMMA*ZRPTL)
          ZRPTMER=EXP(ZGAMMAR*ZRPTL)
        ELSE
          ZRPTME=SQRT(ZRPTM)
          ZRPTMER=ZRPTME
        ENDIF

        ZRPTH=ZRPTME*ZRPTH1/((1.-ZGAMMA)*ZRPTH1+ZGAMMA*ZRPTM)
        ZRPTHR=ZRPTMER*ZRPTH1/((1.-ZGAMMAR)*ZRPTH1+ZGAMMAR*ZRPTM)
        ZRPTB=ZRPTME*ZRPTB1/((1.-ZGAMMA)*ZRPTB1+ZGAMMA*ZRPTM)
        ZRPTBR=ZRPTMER*ZRPTB1/((1.-ZGAMMAR)*ZRPTB1+ZGAMMAR*ZRPTM)

        ZEVA=EVAP*ZMULEV*PRPCEFF(JLON)
        ZARG=ZEVA*(PIPH(JLON)-PIPB(JLON))*(ZRPTH+ZRPTB)*PIPOI(JLON)
        ZEVAPO=MAX(0.0_JPRB,PDQ(JLON)*ZARG/(1.0_JPRB+ZARG))
        PEVAN(JLON)=ZEVAPO*ZRMN
        PEVAG(JLON)=ZEVAPO*ZRMG

        ZEVA=EVAP*ZMULEVR
        ZARG=ZEVA*(PIPHR(JLON)-PIPBR(JLON))*(ZRPTHR+ZRPTBR)*PIPOI(JLON)
        ZEVAPO=MAX(0.0_JPRB,PDQ(JLON)*ZARG/(1.0_JPRB+ZARG))
        PEVAR(JLON)=ZEVAPO*(1.0_JPRB-ZRMN-ZRMG)
      ELSE
        ZRPTH1=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI+PHPLSG(JLON)*ZEXPNI)
        ZRPTB1=MAX(0.0_JPRB,PLPLSL(JLON)+PLPLSN(JLON)*ZEXPNI+PLPLSG(JLON)*ZEXPNI)
        ZRPTM=MAX(ZEPS1,0.5_JPRB*(ZRPTH1+ZRPTB1))
        ZRPTL=LOG(ZRPTM)
        ZRPTME=EXP(ZGAMMA*ZRPTL)
        ZRPTMER=EXP(ZGAMMAR*ZRPTL)
        ZRPTH=ZRPTME*ZRPTH1/((1.-ZGAMMA)*ZRPTH1+ZGAMMA*ZRPTM)
        ZRPTHR=ZRPTMER*ZRPTH1/((1.-ZGAMMAR)*ZRPTH1+ZGAMMAR*ZRPTM)
        PEVAR(JLON)=0.0_JPRB
        PEVAN(JLON)=0.0_JPRB
        PEVAG(JLON)=0.0_JPRB
      ENDIF

!       FONTE.
!       MELTING.

      ZFPLSL=MAX(0.0_JPRB,PLPLSL(JLON)-PEVAR(JLON)*PPOID(JLON)*PSTAL3(JLON))
      ZFPLSN=MAX(0.0_JPRB,PLPLSN(JLON)-PEVAN(JLON)*PPOID(JLON)*PSTAN3(JLON))
      ZFPLSG=MAX(0.0_JPRB,PLPLSG(JLON)-PEVAG(JLON)*PPOID(JLON)*PSTAG3(JLON))

      ZRPTB1=MAX(0.0_JPRB,ZFPLSL+ZFPLSN*ZEXPNI+ZFPLSG*ZEXPNI)
      ZRPTB=ZRPTME*ZRPTB1/((1.-ZGAMMA)*ZRPTB1+ZGAMMA*ZRPTM)
      ZRPTBR=ZRPTMER*ZRPTB1/((1.-ZGAMMAR)*ZRPTB1+ZGAMMAR*ZRPTM)

      ZFON=FONT*ZMULMF*PRPCEFF(JLON)
      ZARG=PLSCP(JLON)*ZFON*(PIPH(JLON)-PIPB(JLON))*(ZRPTH+ZRPTB)&
       &*PIPOI(JLON)
      ZMELT=((PTST(JLON)-RTT)/PLSCP(JLON))*ZARG/(1.0_JPRB+ZARG)
      ZFON=FONT*ZMULMFR
      ZARG=PLSCP(JLON)*ZFON*(PIPHR(JLON)-PIPBR(JLON))*(ZRPTHR+ZRPTBR)&
       &*PIPOI(JLON)
      ZMFREE=((PTST(JLON)-RTT)/PLSCP(JLON))*ZARG/(1.0_JPRB+ZARG)
      ZRMN=PHPLSN(JLON)/MAX(ZEPS1,PHPLSN(JLON)+PHPLSG(JLON))
      ZRMG=(1.0_JPRB-ZRMN)
      PFONT(JLON)=ZRMN*ZMELT*MAX(0.0_JPRB,SIGN(1.0_JPRB,PTST(JLON)-RTT))
      PFONTG(JLON)=ZMFREE+(ZRMG*ZMELT-ZMFREE)*MAX(0.0_JPRB,SIGN(1.0_JPRB,PTST(JLON)-RTT))
    ENDDO
  ELSE ! DIAGNOSTIC OR NO GRAUPEL
    DO JLON=KIDIA,KFDIA

      ZEXPNI=1.0_JPRB

      IF (LAB12) THEN
        ZEXPNI=1.0_JPRB/PEXPN(JLON)
      ENDIF

  !     EVAPORATION.
  !     EVAPORATION.

      IF (.NOT.LDSIMP) THEN
        ZRME=PHPLSN(JLON)/MAX(ZEPS1,PHPLSL(JLON)/ZEXPNI+PHPLSN(JLON))

        ZRPTH1=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI)
        ZRPTB1=MAX(0.0_JPRB,PLPLSL(JLON)+PLPLSN(JLON)*ZEXPNI)
        ZRPTM=MAX(ZEPS1,0.5_JPRB*(ZRPTH1+ZRPTB1))

        IF (LAB12) THEN
          ZRPTL=LOG(ZRPTM)
          ZRPTME=EXP(ZGAMMA*ZRPTL)
          ZRPTMER=EXP(ZGAMMAR*ZRPTL)
        ELSE
          ZRPTME=SQRT(ZRPTM)
          ZRPTMER=ZRPTME
        ENDIF

        ZRPTH=ZRPTME*ZRPTH1/((1.-ZGAMMA)*ZRPTH1+ZGAMMA*ZRPTM)
        ZRPTHR=ZRPTMER*ZRPTH1/((1.-ZGAMMAR)*ZRPTH1+ZGAMMAR*ZRPTM)
        ZRPTB=ZRPTME*ZRPTB1/((1.-ZGAMMA)*ZRPTB1+ZGAMMA*ZRPTM)
        ZRPTBR=ZRPTMER*ZRPTB1/((1.-ZGAMMAR)*ZRPTB1+ZGAMMAR*ZRPTM)

        ZEVA=EVAP*ZMULEV*PRPCEFF(JLON)
        ZARG=ZEVA*(PIPH(JLON)-PIPB(JLON))*(ZRPTH+ZRPTB)*PIPOI(JLON)
        ZEVAPO=MAX(0.0_JPRB,PDQ(JLON)*ZARG/(1.0_JPRB+ZARG))
        PEVAN(JLON)=ZEVAPO*ZRME
        ZEVA=EVAP*ZMULEVR
        ZARG=ZEVA*(PIPHR(JLON)-PIPBR(JLON))*(ZRPTHR+ZRPTBR)*PIPOI(JLON)
        ZEVAPO=MAX(0.0_JPRB,PDQ(JLON)*ZARG/(1.0_JPRB+ZARG))
        PEVAR(JLON)=ZEVAPO*(1.0_JPRB-ZRME)
      ELSE

  !     ZRPTH=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI)**ZGAMMA
  !     ZRPTHR=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI)**ZGAMMAR

        ZRPTH1=MAX(0.0_JPRB,PHPLSL(JLON)+PHPLSN(JLON)*ZEXPNI)
        ZRPTB1=MAX(0.0_JPRB,PLPLSL(JLON)+PLPLSN(JLON)*ZEXPNI)
        ZRPTM=MAX(ZEPS1,0.5_JPRB*(ZRPTH1+ZRPTB1))
        ZRPTL=LOG(ZRPTM)
        ZRPTME=EXP(ZGAMMA*ZRPTL)
        ZRPTMER=EXP(ZGAMMAR*ZRPTL)
        ZRPTH=ZRPTME*ZRPTH1/((1.-ZGAMMA)*ZRPTH1+ZGAMMA*ZRPTM)
        ZRPTHR=ZRPTMER*ZRPTH1/((1.-ZGAMMAR)*ZRPTH1+ZGAMMAR*ZRPTM)

        PEVAR(JLON)=0.0_JPRB
        PEVAN(JLON)=0.0_JPRB
      ENDIF

  !     FONTE/GEL.
  !     MELTING/FREEZING.

      ZFPLSL=MAX(0.0_JPRB,PLPLSL(JLON)-PEVAR(JLON)*PPOID(JLON)*PSTAL3(JLON))
      ZFPLSN=MAX(0.0_JPRB,PLPLSN(JLON)-PEVAN(JLON)*PPOID(JLON)*PSTAN3(JLON))

  !   ZRPTB=MAX(0.0_JPRB,ZFPLSL+ZFPLSN*ZEXPNI)**ZGAMMA
  !   ZRPTBR=MAX(0.0_JPRB,ZFPLSL+ZFPLSN*ZEXPNI)**ZGAMMAR

      ZRPTB1=MAX(0.0_JPRB,ZFPLSL+ZFPLSN*ZEXPNI)
      ZRPTB=ZRPTME*ZRPTB1/((1.-ZGAMMA)*ZRPTB1+ZGAMMA*ZRPTM)
      ZRPTBR=ZRPTMER*ZRPTB1/((1.-ZGAMMAR)*ZRPTB1+ZGAMMAR*ZRPTM)

      ZFON=FONT*ZMULMF*PRPCEFF(JLON)
      ZARG=PLSCP(JLON)*ZFON*(PIPH(JLON)-PIPB(JLON))*(ZRPTH+ZRPTB)&
       &*PIPOI(JLON)
      ZMELT=((PTST(JLON)-RTT)/PLSCP(JLON))*ZARG/(1.0_JPRB+ZARG)
      ZFON=FONT*ZMULMFR
      ZARG=PLSCP(JLON)*ZFON*(PIPHR(JLON)-PIPBR(JLON))*(ZRPTHR+ZRPTBR)&
       &*PIPOI(JLON)
      ZMFREE=((PTST(JLON)-RTT)/PLSCP(JLON))*ZARG/(1.0_JPRB+ZARG)
      PFONT(JLON)=ZMFREE+(ZMELT-ZMFREE)&
       &*MAX(0.0_JPRB,SIGN(1.0_JPRB,PTST(JLON)-RTT))
    ENDDO
  ENDIF
ENDIF

IF (LARPMPS) THEN

  ZEPS1=1.E-20

  ZCOEFFC=0.08744_JPRB
  ZCOEFFD=1.1206_JPRB
  ZEXPD=17._JPRB/24._JPRB
  ZCOEFFE=9.736_JPRB
  ZEXPE=2._JPRB/3._JPRB
  ZCOEFFF=2373.7_JPRB
  ZEXPP=1._JPRB/3._JPRB
  ZCEXPN=-0.1222_JPRB
  ZFACT=RV/2.0_JPRB
  ZIDARV=1.0_JPRB/(0.0231_JPRB*RV)

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    IF (.NOT.LDSIMP) THEN
      ZDELT=PTST(JLON)-RTT
      ZQRST=MAX(0.0_JPRB,PQRST(JLON))
      ZQNST=MAX(0.0_JPRB,PQNST(JLON))

!     EVAPORATION.
!     EVAPORATION.

      ZFACT1=ZFACT*PTST(JLON)*PLPRSF(JLON)
      ZFACT2=PLPRSF(JLON)**ZEXPP

      ZESW=FOEW(PTST(JLON),0.0_JPRB)
      ZQSATW=FOQS(ZESW/PLPRSF(JLON))
      ZSSATW=MAX(0.0_JPRB,1.0_JPRB-PQVST(JLON)/ZQSATW)
      ZCONDT=ZIDARV*(FOLH(PTST(JLON),0.0_JPRB)/PTST(JLON))**2
      ZCEV=ZSSATW*RNINTR/(PRHOAIR(JLON)*(ZCONDT+ZFACT1/ZESW))
      ZARG=PRHOAIR(JLON)*ZQRST/RNINTR
      PEVAR(JLON)=ZCEV*(ZCOEFFC*SQRT(ZARG)+ZCOEFFD*ZFACT2*ZARG**ZEXPD)*TSPHY

      ZESW=FOEW(PTST(JLON),1.0_JPRB)
      ZQSATI=FOQS(ZESW/PLPRSF(JLON))
      ZSSATI=MAX(0.0_JPRB,1.0_JPRB-PQVST(JLON)/ZQSATI)
      ZCONDT=ZIDARV*(FOLH(PTST(JLON),1.0_JPRB)/PTST(JLON))**2
      ZNINTS=RNINTS*EXP(ZCEXPN*ZDELT)
      ZCEV=ZSSATI*ZNINTS/(PRHOAIR(JLON)*(ZCONDT+ZFACT1/ZESW))
      ZARG=PRHOAIR(JLON)*ZQNST/ZNINTS
      PEVAN(JLON)=ZCEV*(ZCOEFFE*ZARG**ZEXPE+ZCOEFFF*ZFACT2*ZARG)*TSPHY

      IF (LLEVAPX) THEN
        ZARG=REVASX*TSPHY/MAX(ZEPS1,PEVAR(JLON)+PEVAN(JLON))
        ZINT=ZARG*(1.0_JPRB-EXP(-1.0_JPRB/ZARG))
        PEVAR(JLON)=ZINT*PEVAR(JLON)
        PEVAN(JLON)=ZINT*PEVAN(JLON)
      ENDIF

      ZINTW=MIN(1.0_JPRB,MAX(0.0_JPRB,(ZQSATW-PQVST(JLON)))&
       &/MAX(ZEPS1,PEVAR(JLON)+PEVAN(JLON)))
      ZINTI=MIN(1.0_JPRB,MAX(0.0_JPRB,(ZQSATI-PQVST(JLON)))&
       &/MAX(ZEPS1,PEVAR(JLON)+PEVAN(JLON)))
      PEVAR(JLON)=ZINTW*PEVAR(JLON)
      PEVAN(JLON)=ZINTI*PEVAN(JLON)
    ELSE
      ZDELT=PTST(JLON)-RTT
      PEVAR(JLON)=0.0_JPRB
      PEVAN(JLON)=0.0_JPRB
    ENDIF

!     FONTE (SANS REGEL).
!     MELTING (WITH RE-FREEZING).

!   PFONT(JLON)=MAX(0.0_JPRB,ZDELT)/PLSCP(JLON)
    PFONT(JLON)=ZDELT/PLSCP(JLON)
  ENDDO

ENDIF

!     SECURITES.
!     SECURITIES.
IF (LGRAPRO) THEN
  DO JLON=KIDIA,KFDIA
    ZFONTLP=PLPLSN(JLON)/(PPOID(JLON)*PSTAN3(JLON))
    ZFONTLM=-PLPLSL(JLON)/(PPOID(JLON)*PSTAL3(JLON))
    ZFONTLG=PLPLSG(JLON)/(PPOID(JLON)*PSTAG3(JLON))

    PFONT(JLON)=MAX(MIN(PFONT(JLON),ZFONTLP),ZFONTLM)
    PFONTG(JLON)=MAX(MIN(PFONTG(JLON),ZFONTLG),ZFONTLM)

    PEVAR(JLON)=PEVAR(JLON)-PFONT(JLON)-PFONTG(JLON)
    PEVAN(JLON)=PEVAN(JLON)+PFONT(JLON)
    PEVAG(JLON)=PEVAG(JLON)+PFONTG(JLON)

    ! PTEST=0 <-> LIQUID+SOLID PRECIP FLUX DISAPPEARS COMPLETELY
    PTEST(JLON)=1.0_JPRB-MAX(0.0_JPRB,SIGN(1.0_JPRB,PEVAR(JLON)+ZFONTLM))&
     &*MAX(0.0_JPRB,SIGN(1.0_JPRB,PEVAN(JLON)-ZFONTLP))&
     &*MAX(0.0_JPRB,SIGN(1.0_JPRB,PEVAG(JLON)-ZFONTLG))

    PEVAR(JLON)=MIN(PEVAR(JLON),-ZFONTLM)
    PEVAN(JLON)=MIN(PEVAN(JLON),ZFONTLP)
    PEVAG(JLON)=MIN(PEVAG(JLON),ZFONTLG)

    PEVAR(JLON)=PEVAR(JLON)+PFONT(JLON)+PFONTG(JLON)
    PEVAN(JLON)=PEVAN(JLON)-PFONT(JLON)
    PEVAG(JLON)=PEVAG(JLON)-PFONTG(JLON)
  ENDDO
ELSE
  DO JLON=KIDIA,KFDIA
    ZFONTLP=PLPLSN(JLON)/(PPOID(JLON)*PSTAN3(JLON))
    ZFONTLM=-PLPLSL(JLON)/(PPOID(JLON)*PSTAL3(JLON))
    PFONT(JLON)=MAX(MIN(PFONT(JLON),ZFONTLP),ZFONTLM)
    PEVAR(JLON)=PEVAR(JLON)-PFONT(JLON)
    PEVAN(JLON)=PEVAN(JLON)+PFONT(JLON)
    PTEST(JLON)=1.0_JPRB-MAX(0.0_JPRB,SIGN(1.0_JPRB,PEVAR(JLON)+ZFONTLM))&
     &*MAX(0.0_JPRB,SIGN(1.0_JPRB,PEVAN(JLON)-ZFONTLP))
    PEVAR(JLON)=MIN(PEVAR(JLON),-ZFONTLM)
    PEVAN(JLON)=MIN(PEVAN(JLON),ZFONTLP)
    PEVAR(JLON)=PEVAR(JLON)+PFONT(JLON)
    PEVAN(JLON)=PEVAN(JLON)-PFONT(JLON)
  ENDDO
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACEVMEL',1,ZHOOK_HANDLE)
END SUBROUTINE ACEVMEL

