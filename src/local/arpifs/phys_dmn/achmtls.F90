!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACHMTLS ( YDCST, YDML_PHY_MF,KIDIA,KFDIA,KLON,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PR,PT,PU,PV,&
 ! - INPUT  1D .
 & PTS,PQS,PDPHIT,PGZ0,PGZ0H,&
 ! - INPUT  LOGIQUE .
 & LDCLS,&
 ! - OUTPUT 2D .
 & PNBVNO,PMRIPP,&
 ! - OUTPUT 1D .
 & PCPS,PGWDCS,PLHS,PZPCLS,PCD,PCDN)

!**** *ACHMTLS   * - DETERMINATION DES CARACTERISTIQUES DE SURFACE.

!     Sujet.
!     ------

!     - ROUTINE DE CALCUL ACTIF .
!       DETERMINATION DES COEFFICIENTS D'ECHANGE EN
!       SURFACE PLUS LEUR AMPLIFICATION SI "ANTI-FIBRILLATION" ACTIVEE .
!     - SI L'OPTION 'CLS' EST SELECTIONNEE :
!          DETERMINATION DE PARAMETRES SUPPLEMENTAIRES NECESSAIRES POUR
!          LES CALCULS DE DIFFUSION VERTICALE ET DE TRAINEE DES ONDES DE
!          GRAVITE OROGRAPHIQUES .
!     - SI L'OPTION 'HMT' EST SELECTIONNEE :
!          INTERPOLATION DES TEMPERATURES, HUMIDITES (SPECIFIQUES ET
!          RELATIVES) ET VENTS AUX HAUTEURS "METEO" .

!     - COMPUTATION OF SURFACE EXCHANGE COEFFICIENTS PLUS THEIR AMPLIFICATION
!       FACTOR IF "ANTI-FIBRILLATION" IS ACTIVATED .
!     - IF THE SWITCH 'CLS' IS ON :
!          COMPUTATION OF ADDITIONNAL PARAMETERS NECESSARY FOR THE
!          VERTICAL DIFFUSION AND GRAVITY WAVE DRAG PARAMETERIZATIONS .
!     - IF THE SWITCH 'HMT' IS ON :
!          TEMPERATURE, MOISTURE (SPECIFIC AND RELATIVE) AND WIND
!          INTERPOLATION AT STANDARD "METEOROLOGICAL" LEVELS .

!**   Interface.
!     ----------
!        *CALL* *ACHMTLS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.


! - 1D  .

! PTS        : TEMPERATURE DE SURFACE.
! PQS        : HUMIDITE SPECIFIQUE DE SURFACE.
! PDPHIT     : HAUTEUR D INTERPOLATION POUR T ET Q (EN GEOPOTENTIEL).
! PZPCLS     : SORTIE DIAGNOSTIQUE DE PRESSION A HTQ METEO.
! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PGZ0H      : G*LONGUEUR DE RUGOSITE THERMIQUE COURANTE (SI KVCLIV >= 8)

! - LOGIQUES .

! LDCLS      : DETERMINATION COMPLETE DES CARACTERISTIQUES DE SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PNBVNO     : CARRE DE FR. BRUNT-VAISALA DIVISEE PAR G FOIS LA DENSITE.
! PMRIPP     : RICHARDSON NUMBER

! - 1D (DIAGNOSTIQUE) .

! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.
! PGWDCS     : DENSITE EN SURFACE POUR LE DRAG OROGRAPHIQUE.
! PLHS       : CHALEUR LATENTE EN SURFACE.
! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY1/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2006-08, L. Kraljevic, d'apres ACHMT

!     Modifications.
!     --------------
!        2007-05, F. Bouyssel : add PZPCLS + Cleaning
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        F. Vana  27-Jan-2012 : Update for Toucans
!        F. Bouyssel 12-Jun-2012 : LCOEFK_TOMS

!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY :  TCST     


!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS   (KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS   (KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0H(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDPHIT(KLON)
LOGICAL           ,INTENT(IN)    :: LDCLS 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNBVNO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMRIPP(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCPS  (KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWDCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLHS  (KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PZPCLS(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCD(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCDN(KLON)

!-----------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLON
REAL(KIND=JPRB) :: ZDPHI(KLON), ZRTI(KLON), ZSTABV(KLON), ZRS(KLON) 
REAL(KIND=JPRB) :: ZCPVMD, ZRVMD, ZMU, ZUSURIC, Z2B, Z3BC, ZCIS, &
 & ZU, ZRZD, ZCD0, ZSTA, ZDS, ZPD, ZCD, ZDID, ZLOS, ZLOI, ZSTAB, ZRI, ZEPS
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.ycst.h"

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ACHMTLS',0,ZHOOK_HANDLE)
ASSOCIATE(EDB=>YDML_PHY_MF%YRPHY0%EDB, EDC=>YDML_PHY_MF%YRPHY0%EDC, VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & EDD=>YDML_PHY_MF%YRPHY0%EDD, GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, EDK=>YDML_PHY_MF%YRPHY0%EDK, &
 & USURIC=>YDML_PHY_MF%YRPHY0%USURIC, USURICL=>YDML_PHY_MF%YRPHY0%USURICL, &
 & GCZ0H=>YDML_PHY_MF%YRPHY1%GCZ0H, &
 & LCOEFK_TOMS=>YDML_PHY_MF%YRPHY%LCOEFK_TOMS)
!----------------------------------------------------------------------- 

!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.

!   security constant
ZEPS    = EPSILON(1.0_JPRB)*100.0_JPRB

!         AUXILIARY CONSTANTS.

ZCPVMD=YDCST%RCPV-YDCST%RCPD
ZRVMD=YDCST%RV-YDCST%RD

!*
!     ------------------------------------------------------------------
!     IV - CALCUL DE L EPAISSEUR DE LA CLS (EN GEOPOTENTIEL).

!          COMPUTATION OF THE THICKNESS OF THE SBL (IN GEOPOTENTIAL).

! - TEMPORAIRE(S) 1D .

! ZDPHI     : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
!            : GEOPOTENTIAL THICKNESS OF THE SURFACE LEVEL.

DO JLON=KIDIA,KFDIA
  ZDPHI(JLON)=PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)
ENDDO


!*
!     ------------------------------------------------------------------
!     VI - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.

ZUSURIC=USURIC*USURICL
Z2B=2.0_JPRB*EDB
Z3BC=3._JPRB*EDB*EDC

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA
  PZPCLS(JLON)=PAPRS(JLON,KLEV)+(PAPRSF(JLON,KLEV)-PAPRS(JLON,KLEV))&
   & *PDPHIT(JLON)/ZDPHI(JLON)
  PLHS(JLON)=FOLH(PTS(JLON),0.0_JPRB)
  PCPS(JLON)=YDCST%RCPD+ZCPVMD*PQS(JLON)
  ZRS(JLON)=YDCST%RD+ZRVMD*PQS(JLON)

!     CALCUL DE STABILITE.
!     STABILITY COMPUTATION.

! - TEMPORAIRE(S) 1D .

! ZRTI      : INVERSE DE R*T.
!            : INVERSE OF R*T.
! ZSTA      : APPROXIMATION DE DELTA(PHI)*DELTA(LN(THETA)).
!            : APPROXIMATION OF DELTA(PHI)*DELTA(LN(THETA)).

  ZRTI(JLON)=2.0_JPRB/(PR(JLON,KLEV)*PT(JLON,KLEV)+YDCST%RKAPPA*ZDPHI(JLON)&
   & +ZRS(JLON)*PTS(JLON))  
  ZSTABV(JLON)=ZDPHI(JLON)*(PR(JLON,KLEV)*PT(JLON,KLEV)+YDCST%RKAPPA&
   & *ZDPHI(JLON)-ZRS(JLON)*PTS(JLON))*ZRTI(JLON)  

  IF (LCOEFK_TOMS) THEN
    ZCIS=PU(JLON,KLEV)**2+PV(JLON,KLEV)**2+(GCISMIN*ZDPHI(JLON)/YDCST%RG)**2
    ZU=SQRT(ZCIS)
    ZRZD=1.0_JPRB+ZDPHI(JLON)/PGZ0(JLON)
    PCDN(JLON)=(VKARMN/LOG(ZRZD))**2

    ZMU=LOG(PGZ0(JLON)/PGZ0H(JLON))
    ZCD0=(GCZ0H(0,1)+ZMU*(GCZ0H(1,1)+ZMU*(GCZ0H(2,1)+ZMU&
       & *GCZ0H(3,1))))/(1.5_JPRB*EDC) 
    ZPD=(GCZ0H(0,2)+ZMU*(GCZ0H(1,2)+ZMU*(GCZ0H(2,2)+ZMU*GCZ0H(3,2))))-0.5_JPRB
    ZCD=ZCD0*ZRZD**ZPD

    ZSTA=ZSTABV(JLON)/(1.0_JPRB+ZUSURIC*MAX(0.0_JPRB,ZSTABV(JLON))/ZCIS)
    ZSTAB=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZSTA))
    ZDS=SQRT(ZCIS+EDD/EDK*ABS(ZSTA))
    ZDID=1.0_JPRB/(ZU+ZCD*Z3BC*PCDN(JLON)*SQRT(ABS(ZSTA)*ZRZD))
    ZLOS=ZCIS*ZDS/(ZU*ZDS+Z2B*ABS(ZSTA))
    ZLOI=ZU-Z2B*ZSTA*ZDID
    PCD(JLON)=(ZLOI+ZSTAB*(ZLOS-ZLOI))*PCDN(JLON)/ZU

    ZRI=ZSTA/ZCIS
    PMRIPP(JLON,KLEV)=SIGN(MAX(ZEPS,ABS(ZRI)),ZRI)
  ENDIF

ENDDO
  

IF (LDCLS) THEN
  DO JLON=KIDIA,KFDIA

!     CALCULS DE UN SUR G FOIS LA FREQUENCE DE BRUNT-VAISALA DIVISEE PAR
!     LA DENSITE LE TOUT AU CARRE DE MEME QUE LA DENSITE (POUR G.W.D.).
!     COMPUTATION OF ONE OVER G TIME THE BRUNT-VAISALA FREQUENCY DIVIDED
!     BY DENSITY THE WHOLE BEING SQUARED AS WELL AS DENSITY (FOR G.W.D.)

    PNBVNO(JLON,KLEV)=ZSTABV(JLON)/(PAPRS(JLON,KLEV)*ZRTI(JLON)*ZDPHI(JLON))**2
    PGWDCS(JLON)=PAPRS(JLON,KLEV)*ZRTI(JLON)
  ENDDO
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACHMTLS',1,ZHOOK_HANDLE)
END SUBROUTINE ACHMTLS
