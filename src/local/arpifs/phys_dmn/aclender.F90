SUBROUTINE ACLENDER  ( YDPHY,YDPHY0,KIDIA,  KFDIA,    KLON,   KTDIAN, KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,  PAPHIF,   PAPRS,  PAPRSF, PT,&
 & PQV,  PQICE,  PQLI, PNLAB, PNLABCVP , PLSCPE, PU, PV, PECT,&
 ! - INPUT  1D .
 & PGZ0, &
 ! - OUTPUT 2D .
 &PUSLE,PLEND,PPHI3)

!**** *ACLENDER - CALCUL DES LONGUEURS DE MELANGE de Lenderink

!     Sujet.
!     ------

!**   Interface.
!     ----------
!        *CALL* *ACLENDER*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE, EXCEPT FOR KTDIAN.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT.
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIAN     : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL)
!              POUR LES CALCULS DE TURBULENCE + NEBULOSITE.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PT         : TEMPERATURE (APRES AJUSTEMENT CONVECTIF).
! PECT       : ENERGIE CINETIQUE TURBULENTE.
! PQV        : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQICE      : HUMIDITE SPECIFIQUE SOLIDE.
! PQLI       : HUMIDITE SPECIFIQUE LIQUIDE.
! PNLAB      : Si 1 Presence d'un nuage Shallow
! PNLABCVP   : Si 1 Presence d'un nuage Deep

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PUSLE      : INVERSE DE LA LONGUEUR DE DISSIPATION MULTIPLIEE PAR G
! PLMECT     : UNE LONGUEUR DE MELANGE (FOIS G) POUR ACNEBR
! PPHI3      : LA FONCTION DE REDESLPERGER POUR K_T

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMCST /
! COMMON/YOMPHY0/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!      2007-12-25, Y. Bouteloup.
!              - - - - - - - - - - - - - - - - - - - - - - - - - - -
!              - - - - - - - - - - - - - - - - - - - - - - - - - - -

!     Modifications.
!     --------------
!-----------------------------------------------------------------------

USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RETV     ,RKAPPA   ,RATM, RPI, RD, RV, RCPD, RCPV, RCW,&
 &                    RLVTT,RTT,RLSTT,RCS,RALPW,RBETW,RGAMW, RALPS,RBETS, RGAMS,RALPD,RBETD,RGAMD

USE YOMPHY   , ONLY : TPHY
USE YOMPHY0  , ONLY : TPHY0



IMPLICIT NONE

TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIAN 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNLAB(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNLABCVP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQICE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCPE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PECT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0(KLON) 


REAL(KIND=JPRB)   ,INTENT(OUT)    :: PLEND(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)    :: PUSLE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)    :: PPHI3(KLON,KLEV)
! Tableaux pour les sorties sur listing (1D seulement)

INTEGER(KIND=JPIM) :: JLEV, JLON

 
REAL(KIND=JPRB) ::  ZPREF, ZDTHETA, ZQC, ZCIS, ZDPHI, ZLINF
REAL(KIND=JPRB) ::  ZC1,ZPHIH,ZGLKARMN
REAL(KIND=JPRB) ::  ZB,ZC0,ZCD,ZCN,ZALPHAN,ZALPHACM,ZALPHACH,ZALPHARH,ZALPHARM,ZPIS2,ZSEUILM,ZSEUILH,ZARIM,ZARIH
REAL(KIND=JPRB) ::  ZCM_CLEAR,ZCM_CLOUD,ZCH
REAL(KIND=JPRB) ::  ZC3M_CLEAR,ZC3M_CLOUD,ZC3H_CLEAR,ZC3H_CLOUD
REAL(KIND=JPRB) ::  ZTHETAVL(KLON,KLEV),ZRI(KLON,0:KLEV),ZDTH(KLON,KLEV),ZDTH2(KLON,KLEV),ZDCIS(KLON,KLEV)

REAL(KIND=JPRB) ::  ZFM_CLEAR(KLON,KLEV),ZFM_CLOUD(KLON,KLEV),ZFH_CLEAR(KLON,KLEV),ZFH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLUPM_CLEAR(KLON,KLEV),ZLUPM_CLOUD(KLON,KLEV),ZLUPH_CLEAR(KLON,KLEV),ZLUPH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLDNM_CLEAR(KLON,KLEV),ZLDNM_CLOUD(KLON,KLEV),ZLDNH_CLEAR(KLON,KLEV),ZLDNH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLSM_CLEAR(KLON,KLEV),ZLSM_CLOUD(KLON,KLEV),ZLSH_CLEAR(KLON,KLEV),ZLSH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLINTM_CLEAR(KLON,KLEV),ZLINTM_CLOUD(KLON,KLEV),ZLINTH_CLEAR(KLON,KLEV),ZLINTH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLENDM_CLEAR(KLON,KLEV),ZLENDH_CLEAR(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLENDM_CLOUD(KLON,KLEV),ZLENDH_CLOUD(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLENDM(KLON,KLEV),ZLENDH(KLON,KLEV),ZTHETA(KLON,KLEV),ZLENDMM(KLON,KLEV)

REAL(KIND=JPRB) ::  ZNN_CLEAR(KLON,0:KLEV),ZNN_CLOUD(KLON,0:KLEV),ZRIF(KLON,KLEV)
REAL(KIND=JPRB) ::  ZLMIN(KLON,KLEV),ZGLMINF(KLON,KLEV)


!  Tableaux et variables pour le calcul de delta T humide
REAL(KIND=JPRB) ::  ZTJ,ZTDERI,ZLOG,ZQVSH1,ZDH1,ZQVSH2,ZDH2,ZNEIGE,ZDQL1,ZDQL2

REAL(KIND=JPRB) ::  ZQV(KLON,KLEV),ZQL(KLON,KLEV),ZRH(KLON,KLEV),ZCPH(KLON,KLEV),ZLH(KLON,KLEV),ZQVS(KLON,KLEV)
REAL(KIND=JPRB) ::  ZQSEUIL(KLON,KLEV),ZTHD(KLON,KLEV),ZTHU(KLON,KLEV),ZTHDC(KLON,KLEV),ZTHUC(KLON,KLEV)
REAL(KIND=JPRB) ::  ZRI2(KLON,0:KLEV),ZRIF2(KLON,KLEV)

REAL(KIND=JPRB) :: ZGZBOT(KLON),ZGZTOP(KLON),ZGZBOTCVP(KLON),ZGZTOPCVP(KLON)
! Tableaux et variables pour le calcul de Neb

REAL(KIND=JPRB) :: ZN1D(-22:11)

REAL(KIND=JPRB) ::  ZTF1,ZQCF1,ZLSCPEF1,ZTLF1,ZH1,ZEW1,ZQSATF1,ZDLEWF1,ZQSLTLF1,ZTHETAL1,ZDELTQF1,ZQWF1
REAL(KIND=JPRB) ::  ZTF2,ZQCF2,ZLSCPEF2,ZTLF2,ZH2,ZEW2,ZQSATF2,ZDLEWF2,ZQSLTLF2,ZTHETAL2,ZDELTQF2,ZQWF2
REAL(KIND=JPRB) ::  ZTH,ZQSLTLH,ZLSCPEH,ZTHETAH,ZBB,ZDTHETAL,ZEPSIG,ZEPDELT,ZEPSQ,ZINC,ZDQW,ZIGMAS,ZQ11    
REAL(KIND=JPRB) ::  ZIGMAS_CLOUD(KLON,KLEV),ZIGMAS_CLEAR(KLON,KLEV),ZAA(KLON,KLEV),ZDELTQH(KLON,KLEV)
REAL(KIND=JPRB) ::  ZNEB(KLON,KLEV),ZNEBI(KLON,KLEV),ZQVL(KLON,KLEV)
REAL(KIND=JPRB) ::  ZRESUL,ZSURSAT,ZEPNEBS,ZZN1D,ZRIH,ZOFFSET
REAL(KIND=JPRB) ::  ZDQV,ZTC
REAL(KIND=JPRB) ::  ZGZLCVPUP, ZGZLCVPDN
! Tableaux et variables pour le calcul de PHI3

REAL(KIND=JPRB) ::  ZG2LD2(KLON,KLEV),ZPHI3MAX,ZPHI3I


INTEGER(KIND=JPIM) :: INQ1,JN

REAL(KIND=JPRB) :: ZHOOK_HANDLE
     
!     FUNCTIONS THERMODYNAMIQUES DE BASE
#include "fcttrm.func.h"

!#include "abor1.intfb.h"

DATA ZN1D /&
 & 0.0_JPRB          ,  0.0_JPRB          ,  1.7225742E-05_JPRB,  0.275373E-04_JPRB ,&
 & 4.5657158E-05_JPRB,  0.748634E-04_JPRB ,  1.2344122E-04_JPRB,  0.203788E-03_JPRB ,&
 & 3.3539534E-04_JPRB,  0.553310E-03_JPRB ,  9.1189146E-04_JPRB,  0.150353E-02_JPRB ,&
 & 2.4784803E-03_JPRB,  0.408673E-02_JPRB ,  6.7381263E-03_JPRB,  0.111092E-01_JPRB ,&
 & 1.8315554E-02_JPRB,  0.301974E-01_JPRB ,  4.9787164E-02_JPRB,  0.831191E-01_JPRB ,&
 & 0.1512039_JPRB    ,  0.286653E+00_JPRB ,  0.5000000_JPRB    ,  0.691489E+00_JPRB ,&
 & 0.8413813_JPRB    ,  0.933222E+00_JPRB ,  0.9772662_JPRB    ,  0.993797E+00_JPRB ,&
 & 0.9986521_JPRB    ,  0.999768E+00_JPRB ,  0.9999684_JPRB    ,  0.9999997_JPRB    ,&
 & 1.0000000_JPRB    ,  1.000000_JPRB     /  
     
!*
!     ------------------------------------------------------------------

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACLENDER',0,ZHOOK_HANDLE)

ASSOCIATE(ALMAVX=>YDPHY0%ALMAVX, ACBRPHIM=>YDPHY0%ACBRPHIM, &
 & VKARMN=>YDPHY0%VKARMN, ALD=>YDPHY0%ALD, ECTMIN=>YDPHY0%ECTMIN, &
 & ALMAVE=>YDPHY0%ALMAVE, ARSC1=>YDPHY0%ARSC1, RICRET=>YDPHY0%RICRET, &
 & ALPHAT=>YDPHY0%ALPHAT, AKN=>YDPHY0%AKN, RFACNSM=>YDPHY0%RFACNSM, &
 & LECSHAL=>YDPHY%LECSHAL, LECDEEP=>YDPHY%LECDEEP, NLEND=>YDPHY%NLEND)
!*
!     ------------------------------------------------------------------
!     0 - CONSTANTES UTILES et initialisations
!     ------------------------------------------------------------------

ZLINF = 75.0_JPRB
ZB  = 4._JPRB
!ZB  = 3._JPRB
ZC0 = 3.75_JPRB
ZCD = 1._JPRB/ZC0/ZC0
ZCN = SQRT(SQRT(ZCD))
!ZCN = 2.0_JPRB*SQRT(SQRT(ZCD))  !<== Pour essai !!!
ZPIS2 = RPI/2.0_JPRB
ZALPHAN = ZCN*VKARMN
!ZALPHAN = 0.0_JPRB
ZOFFSET = 0.2_JPRB  ! <== Offset de la fonction Fm
ZALPHACH = 3._JPRB*ZCN*VKARMN
ZALPHACM = 3._JPRB*ZCN*VKARMN

!ZALPHACH = 1.0_JPRB
!ZALPHACM = 1.0_JPRB

ZALPHARH = ZALPHAN*ZPIS2*ZB/(ZALPHACH-ZALPHAN)
ZALPHARM = ZALPHAN*ZPIS2*ZB/(ZALPHACM-ZALPHAN)


ZCH = 0.2_JPRB
!ZCH = 0.1_JPRB   !<== Pour test (voir Lenderink et Holtslag 2004)

ZRI(:,:) = 0.0_JPRB
ZRI2(:,:) = 0.0_JPRB
ZFM_CLEAR(:,:) = 0.0_JPRB
ZFM_CLOUD(:,:) = 0.0_JPRB
ZFH_CLEAR(:,:) = 0.0_JPRB
ZFH_CLOUD(:,:) = 0.0_JPRB
ZDTH(:,:) = 0.0_JPRB
ZDTH2(:,:) = 0.0_JPRB
ZDCIS(:,:) = 0.0_JPRB
ZLSM_CLEAR(:,:) = 0.0_JPRB
ZLSM_CLOUD(:,:) = 0.0_JPRB
ZLSH_CLEAR(:,:) = 0.0_JPRB
ZLSH_CLOUD(:,:) = 0.0_JPRB
ZNN_CLEAR(:,:) = 0.0_JPRB
ZNN_CLOUD(:,:) = 0.0_JPRB
ZIGMAS_CLOUD(:,:) = 0.0_JPRB
ZIGMAS_CLEAR(:,:) = 0.0_JPRB
ZDELTQH(:,:) = 0.0_JPRB

!*
!     ------------------------------------------------------------------
!     1 - CALCULS DU NOMBRE DE RICHARDSON, de la fonction Fm et de N
!     ------------------------------------------------------------------

! En fait on a besoin des longueurs lup et ldown sur les "half" level, comme elles sont calculées
! par intégration de la fonction Fm(Ri), celle-ci et donc Ri doivent être calculés sur les "full"
! De manière naturelle Ri est calculé sur les "half", car il utilise un delta theta et theta est connu sur les "full"

! La technique utilisée ici est de calculer Ri sur les "half" puis de l'interpoller
! linéairement sur les "full"

! - - - - - - - - - - - - - - - - - - 
! CALCULS Thermodynamique utiles pour la suite
! - - - - - - - - - - - - - - - - - -
ZNEIGE            = 0.0_JPRB ! <== on ne tient pas compte de la glace
DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA
     ZQV(JLON,JLEV)    =   MAX(0.0_JPRB ,PQV(JLON,JLEV))
     ZQL(JLON,JLEV)    =   MAX(0.0_JPRB ,PQLI(JLON,JLEV))
     ZQVL(JLON,JLEV)   =   ZQV(JLON,JLEV) + ZQL(JLON,JLEV) 
     ZQSEUIL(JLON,JLEV)=   MAX(0.0_JPRB,SIGN(1.0_JPRB,ZQL(JLON,JLEV)-1.0E-6_JPRB))
     
! Calcul de R, Cp et L humide 
     ZRH(JLON,JLEV)  = RD + (RD-RV)*ZQV(JLON,JLEV)
     ZCPH(JLON,JLEV) = RCPD*(1.0_JPRB-ZQV(JLON,JLEV)-ZQL(JLON,JLEV))+RCPV*ZQV(JLON,JLEV)+RCW*ZQL(JLON,JLEV)
     ZLH(JLON,JLEV)  = RLVTT - (PT(JLON,JLEV)-RTT)*(RCW-RCPV)
     
     ZTC=PT(JLON,JLEV)-PLSCPE(JLON,JLEV)*ZQL(JLON,JLEV)

!    ZQVS(JLON,JLEV) = FOQS (FOEW (PT(JLON,JLEV),ZNEIGE )/PAPRSF(JLON,JLEV) )
     ZQVS(JLON,JLEV) = FOQS (FOEW (ZTC,ZNEIGE )/PAPRSF(JLON,JLEV) )
   ENDDO
ENDDO     


! - - - - - - - - - - - - - - - - - - 
! CALCULS DE THETAVL sur les "full"
! - - - - - - - - - - - - - - - - - -

DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA
    ZPREF              = PAPRSF(JLON,JLEV)
    ZTHETA(JLON,JLEV)  = PT(JLON,JLEV)*(RATM/ZPREF)**(RKAPPA)
    ZQC               =   ZQL(JLON,JLEV)+PQICE(JLON,JLEV)
    ZTHETAVL(JLON,JLEV) = ZTHETA(JLON,JLEV)*(1.0_JPRB+RETV*ZQV(JLON,JLEV)-ZQC)    
  ENDDO
ENDDO
  
! - - - - - - - - - - -
! CALCULS DE Ri, de N2 et de (gLd)**2 sur les "half" dans le cas clair
! - - - - - - - - - - -


DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZDTHETA = (ZTHETAVL(JLON,JLEV+1)-ZTHETAVL(JLON,JLEV))
    ZDTH(JLON,JLEV) = ZDTHETA
    ZCIS =MAX(1.0E-10_JPRB,(PU(JLON,JLEV+1)-PU(JLON,JLEV))**2 +(PV(JLON,JLEV+1)-PV(JLON,JLEV))**2)
    ZDCIS (JLON,JLEV) = ZCIS
    ZDPHI=PAPHIF(JLON,JLEV+1)-PAPHIF(JLON,JLEV)
!    ZRI(JLON,JLEV) = ZDPHI*ZDTHETA/ZCIS/ZTHETAVL(JLON,JLEV)
!    ZNN_CLEAR(JLON,JLEV) = MAX(1.0E-10_JPRB,ZDTHETA/ZDPHI/ZTHETAVL(JLON,JLEV))
    
!  On profite de cette boucle pour calculer le terme (gLd)**2 nécessaire au calcul de PHI3

    ZTHETAH = 0.5_JPRB*(ZTHETAVL(JLON,JLEV+1)+ZTHETAVL(JLON,JLEV))    
    ZG2LD2(JLON,JLEV)  = PECT(JLON,JLEV)*ZTHETAH*ZDPHI/ZDTHETA


! Calcul du terme correctif de Ri selon Geleyn 1986    

    IF (NLEND == 2) THEN
       ZLSCPEH = 0.5_JPRB*(PLSCPE(JLON,JLEV  ) + PLSCPE(JLON,JLEV  ))
       ZDQV    = ZQVL(JLON,JLEV+1)-ZQVS(JLON,JLEV+1)-(ZQVL(JLON,JLEV)-ZQVS(JLON,JLEV))
    ELSE
       ZDQV    = 0.0_JPRB
    ENDIF      
    
! Calcul de Ri et N2    (Nouveau calcul utilisant le theta du half level)
    ZRI(JLON,JLEV) = ZDPHI*(ZDTHETA+ZDQV)/ZCIS/ZTHETAH
    ZNN_CLEAR(JLON,JLEV) = MAX(1.0E-10_JPRB,ZDTHETA/ZDPHI/ZTHETAH)

  ENDDO
ENDDO

!  Midification de Ri selon Geleyn 1986 !

    
!  Nouveau calcul de Ri et N2 prenant en compte la condensation

!  On commence par calculé la température sur le half intermèdiaire des particules venant des full du 
!  dessus et du dessous par transformtation adiabatique (le delta calculé doit être très proche du calcul précédent !)

!  Calcul de la température finale d'une particule venant du full jlev
!  arrivant sur le half jlev (descente !)     


DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
! 1 adiabatique sèche
     ZTJ = PT(JLON,JLEV)*(PAPRS(JLON,JLEV)/PAPRSF(JLON,JLEV))**(ZRH(JLON,JLEV)/ZCPH(JLON,JLEV))
     ZTDERI = 0.5_JPRB*(PT(JLON,JLEV)+ZTJ)
! Correction prenant en compte le changement de phase  
     ZLOG = LOG(PAPRS(JLON,JLEV)/PAPRSF(JLON,JLEV))
     ZQVSH1 = FOQS (FOEW (ZTJ,ZNEIGE )/PAPRS(JLON,JLEV))  !<== humidité saturante du lieu d'arrivée 
     ZDQL1 = ZQSEUIL(JLON,JLEV)*(ZQVS(JLON,JLEV)-ZQVSH1)
     ZDH1 = ZCPH(JLON,JLEV)*(ZTJ-PT(JLON,JLEV))+ZLH(JLON,JLEV)*ZDQL1-ZRH(JLON,JLEV)*ZTJ*ZLOG

     ZQVSH2 = FOQS (FOEW (ZTDERI,ZNEIGE )/PAPRS(JLON,JLEV))  !<== humidité saturante du lieu d'arrivée 
     ZDQL2 = ZQSEUIL(JLON,JLEV)*(ZQVS(JLON,JLEV)-ZQVSH2)
     ZDH2 = ZCPH(JLON,JLEV)*(ZTDERI-PT(JLON,JLEV))+ZLH(JLON,JLEV)*ZDQL2-ZRH(JLON,JLEV)*ZTDERI*ZLOG

     ZTHDC(JLON,JLEV) = ZTJ + ZDH1*(ZTDERI-ZTJ)/(ZDH1-ZDH2)
     ZTHD(JLON,JLEV) = ZTJ
  ENDDO
ENDDO

!  Calcul de la température finale d'une particule venant du full jlev+1
!  arrivant sur le half jlev (montée !)     

DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
! 1 adiabatique séche
     ZTJ = PT(JLON,JLEV+1)*(PAPRS(JLON,JLEV)/PAPRSF(JLON,JLEV+1))**(ZRH(JLON,JLEV+1)/ZCPH(JLON,JLEV+1))
     ZTDERI = 0.5_JPRB*(PT(JLON,JLEV+1)+ZTJ)
! Correction prenant en compte le changement de phase  
     ZLOG = LOG(PAPRS(JLON,JLEV)/PAPRSF(JLON,JLEV+1))
     ZQVSH1 = FOQS (FOEW (ZTJ,ZNEIGE )/PAPRS(JLON,JLEV))  !<== humidité saturante du lieu d'arrivée 
     ZDQL1 = ZQSEUIL(JLON,JLEV+1)*(ZQVS(JLON,JLEV+1)-ZQVSH1)
     ZDH1 = ZCPH(JLON,JLEV+1)*(ZTJ-PT(JLON,JLEV+1))+ZLH(JLON,JLEV+1)*ZDQL1-ZRH(JLON,JLEV+1)*ZTJ*ZLOG

     ZQVSH2 = FOQS (FOEW (ZTDERI,ZNEIGE )/PAPRS(JLON,JLEV))  !<== humidité saturante du lieu d'arrivée 
     ZDQL2 = ZQSEUIL(JLON,JLEV+1)*(ZQVS(JLON,JLEV+1)-ZQVSH2)
     ZDH2 = ZCPH(JLON,JLEV+1)*(ZTDERI-PT(JLON,JLEV+1))+ZLH(JLON,JLEV+1)*ZDQL2-ZRH(JLON,JLEV+1)*ZTDERI*ZLOG
     ZTHUC(JLON,JLEV) = ZTJ + ZDH1*(ZTDERI-ZTJ)/(ZDH1-ZDH2)
     ZTHU(JLON,JLEV) = ZTJ
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
   ZTHDC(JLON,KLEV)=ZTHDC(JLON,KLEV-1)
   ZTHD(JLON,KLEV)=ZTHD(JLON,KLEV-1)
   ZTHUC(JLON,KLEV)=ZTHUC(JLON,KLEV-1)
   ZTHU(JLON,KLEV)=ZTHU(JLON,KLEV-1)
ENDDO

DO JLEV=1,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZDTH2(JLON,JLEV)=ZTHUC(JLON,JLEV)-ZTHDC(JLON,JLEV)
    ZCIS =MAX(1.0E-10_JPRB,(PU(JLON,JLEV+1)-PU(JLON,JLEV))**2 +(PV(JLON,JLEV+1)-PV(JLON,JLEV))**2)
    ZTHETAH = 0.5_JPRB*(ZTHETAVL(JLON,JLEV+1)+ZTHETAVL(JLON,JLEV))  
    ZDPHI=PAPHIF(JLON,JLEV+1)-PAPHIF(JLON,JLEV)
    ZRI2(JLON,JLEV) = ZDPHI*ZDTH2(JLON,JLEV)/ZCIS/ZTHETAH   
    ZNN_CLOUD(JLON,JLEV) = MAX(1.0E-10_JPRB,ZDTH2(JLON,JLEV)/ZDPHI/ZTHETAH)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZNN_CLEAR(JLON,KLEV) = ZNN_CLEAR(JLON,KLEV-1)
  ZNN_CLOUD(JLON,KLEV) = ZNN_CLOUD(JLON,KLEV-1)
ENDDO
  
! - - - - - - - - - - -
! Interpollation de Ri sur les "full"
! - - - - - - - - - - -

DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA
    ZRIF(JLON,JLEV) =  0.5_JPRB*(ZRI(JLON,JLEV-1) + ZRI(JLON,JLEV))
    ZRIF2(JLON,JLEV) = 0.5_JPRB*(ZRI2(JLON,JLEV-1) + ZRI2(JLON,JLEV))
  ENDDO
ENDDO

! - - - - - - - - - - -
! CALCULS DE Fm et Fh sur les "full"
! - - - - - - - - - - -


! Cas clair
DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA  
    ZSEUILM         = MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRIF(JLON,JLEV)))
    ZSEUILH         = MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRIF(JLON,JLEV)))
    ZARIM          = ZALPHARM*ZRIF(JLON,JLEV)
    ZARIH          = ZALPHARH*ZRIF(JLON,JLEV)
    ZFM_CLEAR(JLON,JLEV) = ZOFFSET+(ZALPHAN-(ZALPHACM-ZALPHAN)*(ZSEUILM*ZARIM+(1.0_JPRB-ZSEUILM)*ATAN(ZARIM))/ZPIS2)
    ZFH_CLEAR(JLON,JLEV) = ZOFFSET+(ZALPHAN-(ZALPHACH-ZALPHAN)*(ZSEUILH*ZARIH+(1.0_JPRB-ZSEUILH)*ATAN(ZARIH))/ZPIS2)
  ENDDO
ENDDO
! Cas nuageux
DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA  
    ZSEUILM         = MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRIF2(JLON,JLEV)))
    ZSEUILH         = MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRIF2(JLON,JLEV)))
    ZARIM          = ZALPHARM*ZRIF2(JLON,JLEV)
    ZARIH          = ZALPHARH*ZRIF2(JLON,JLEV)
    ZFM_CLOUD(JLON,JLEV) = ZOFFSET+(ZALPHAN-(ZALPHACM-ZALPHAN)*(ZSEUILM*ZARIM+(1.0_JPRB-ZSEUILM)*ATAN(ZARIM))/ZPIS2)
    ZFH_CLOUD(JLON,JLEV) = ZOFFSET+(ZALPHAN-(ZALPHACH-ZALPHAN)*(ZSEUILH*ZARIH+(1.0_JPRB-ZSEUILH)*ATAN(ZARIH))/ZPIS2)
  ENDDO
ENDDO


!*
!     -------------------------------------------------------------------------------
!     2 - CALCULS DE Lup, Ldown, Lint, Lmin,  Ls et La longueur finale sur les "half"
!     -------------------------------------------------------------------------------


! En haut et en bas

DO JLON=KIDIA,KFDIA
   ZLUPM_CLEAR(JLON,KLEV) = 0.0_JPRB
   ZLDNM_CLEAR(JLON,1) = 0.0_JPRB
   ZLUPH_CLEAR(JLON,KLEV) = 0.0_JPRB
   ZLDNH_CLEAR(JLON,1) = 0.0_JPRB
   ZLUPM_CLOUD(JLON,KLEV) = 0.0_JPRB
   ZLDNM_CLOUD(JLON,1) = 0.0_JPRB
   ZLUPH_CLOUD(JLON,KLEV) = 0.0_JPRB
   ZLDNH_CLOUD(JLON,1) = 0.0_JPRB
ENDDO

!!  Calcul de ZGZTOP et ZGZBOT de la cvpp et de la cvp

ZGZTOP(:) = 0.0_JPRB
ZGZBOT(:) = 100000.0_JPRB

ZGZTOPCVP(:) = 0.0_JPRB
ZGZBOTCVP(:) = 100000.0_JPRB

DO JLEV=KTDIAN,KLEV
  DO JLON=KIDIA,KFDIA
     ZGZTOP(JLON) = MAX(ZGZTOP(JLON),PAPHIF(JLON,JLEV)*PNLAB(JLON,JLEV))
     ZGZBOT(JLON) = (1.0_JPRB-PNLAB(JLON,JLEV))*ZGZBOT(JLON)&
    &             + PNLAB(JLON,JLEV)*MIN(ZGZBOT(JLON),PAPHIF(JLON,JLEV))
  ENDDO
ENDDO    

DO JLEV=KTDIAN,KLEV
  DO JLON=KIDIA,KFDIA
     ZGZTOPCVP(JLON) = MAX(ZGZTOPCVP(JLON),PAPHIF(JLON,JLEV)*PNLABCVP(JLON,JLEV))
     ZGZBOTCVP(JLON) = (1.0_JPRB-PNLABCVP(JLON,JLEV))*ZGZBOTCVP(JLON)&
    &             + PNLABCVP(JLON,JLEV)*MIN(ZGZBOTCVP(JLON),PAPHIF(JLON,JLEV))
  ENDDO
ENDDO    

!  Calcul de Lup

DO JLEV=KLEV-1,1,-1
   DO JLON=KIDIA,KFDIA
      ZLUPM_CLEAR(JLON,JLEV) = MAX(0.0_JPRB,ZLUPM_CLEAR(JLON,JLEV+1)+&
                             & ZFM_CLEAR(JLON,JLEV+1)*(PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1)))
      ZLUPH_CLEAR(JLON,JLEV) = MAX(0.0_JPRB,ZLUPH_CLEAR(JLON,JLEV+1)+&
                             & ZFH_CLEAR(JLON,JLEV+1)*(PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1)))
      ZLUPM_CLOUD(JLON,JLEV) = MAX(0.0_JPRB,ZLUPM_CLOUD(JLON,JLEV+1)+&
                             & ZFM_CLOUD(JLON,JLEV+1)*(PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1)))
      ZLUPH_CLOUD(JLON,JLEV) = MAX(0.0_JPRB,ZLUPH_CLOUD(JLON,JLEV+1)+&
                             & ZFH_CLOUD(JLON,JLEV+1)*(PAPHI(JLON,JLEV)-PAPHI(JLON,JLEV+1)))
   ENDDO
ENDDO      

!  Calcul de Ldown

DO JLEV=2,KLEV
   DO JLON=KIDIA,KFDIA
      ZLDNM_CLEAR(JLON,JLEV) = MAX(0.0_JPRB,ZLDNM_CLEAR(JLON,JLEV-1)+&
                             & ZFM_CLEAR(JLON,JLEV)*(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV)))
      ZLDNH_CLEAR(JLON,JLEV) = MAX(0.0_JPRB,ZLDNH_CLEAR(JLON,JLEV-1)+&
                             & ZFH_CLEAR(JLON,JLEV)*(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV)))
      ZLDNM_CLOUD(JLON,JLEV) = MAX(0.0_JPRB,ZLDNM_CLOUD(JLON,JLEV-1)+&
                             & ZFM_CLOUD(JLON,JLEV)*(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV)))
      ZLDNH_CLOUD(JLON,JLEV) = MAX(0.0_JPRB,ZLDNH_CLOUD(JLON,JLEV-1)+&
                             & ZFH_CLOUD(JLON,JLEV)*(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV)))
   ENDDO
ENDDO      

DO JLEV=KTDIAN,KLEV-1
  DO JLON=KIDIA,KFDIA
    
    ! On majore par la hauteur de cvpp et de cvp
    
    IF (LECSHAL) THEN
      ZLUPM_CLEAR(JLON,JLEV) = MAX(ZLUPM_CLEAR(JLON,JLEV),PNLAB(JLON,JLEV)*(ZGZTOP(JLON)-PAPHI(JLON,JLEV)))
      ZLUPM_CLOUD(JLON,JLEV) = MAX(ZLUPM_CLOUD(JLON,JLEV),PNLAB(JLON,JLEV)*(ZGZTOP(JLON)-PAPHI(JLON,JLEV)))
      ZLUPH_CLEAR(JLON,JLEV) = MAX(ZLUPH_CLEAR(JLON,JLEV),PNLAB(JLON,JLEV)*(ZGZTOP(JLON)-PAPHI(JLON,JLEV)))
      ZLUPH_CLOUD(JLON,JLEV) = MAX(ZLUPH_CLOUD(JLON,JLEV),PNLAB(JLON,JLEV)*(ZGZTOP(JLON)-PAPHI(JLON,JLEV)))
      
      ZLDNM_CLEAR(JLON,JLEV) = MAX(ZLDNM_CLEAR(JLON,JLEV),PNLAB(JLON,JLEV)*(PAPHI(JLON,JLEV)-ZGZBOT(JLON)))
      ZLDNM_CLOUD(JLON,JLEV) = MAX(ZLDNM_CLOUD(JLON,JLEV),PNLAB(JLON,JLEV)*(PAPHI(JLON,JLEV)-ZGZBOT(JLON)))
      ZLDNH_CLEAR(JLON,JLEV) = MAX(ZLDNH_CLEAR(JLON,JLEV),PNLAB(JLON,JLEV)*(PAPHI(JLON,JLEV)-ZGZBOT(JLON)))
      ZLDNH_CLOUD(JLON,JLEV) = MAX(ZLDNH_CLOUD(JLON,JLEV),PNLAB(JLON,JLEV)*(PAPHI(JLON,JLEV)-ZGZBOT(JLON)))
    ENDIF
    IF (LECDEEP) THEN  
      ZGZLCVPUP = MIN(RG*ALMAVX,ZGZTOPCVP(JLON)-PAPHI(JLON,JLEV))
      ZGZLCVPDN = MIN(RG*ALMAVX,PAPHI(JLON,JLEV)-ZGZBOTCVP(JLON))
      ZLUPM_CLEAR(JLON,JLEV) = MAX(ZLUPM_CLEAR(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPUP)
      ZLUPM_CLOUD(JLON,JLEV) = MAX(ZLUPM_CLOUD(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPUP)
      ZLUPH_CLEAR(JLON,JLEV) = MAX(ZLUPH_CLEAR(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPUP)
      ZLUPH_CLOUD(JLON,JLEV) = MAX(ZLUPH_CLOUD(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPUP)      
      
      ZLDNM_CLEAR(JLON,JLEV) = MAX(ZLDNM_CLEAR(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPDN)
      ZLDNM_CLOUD(JLON,JLEV) = MAX(ZLDNM_CLOUD(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPDN)
      ZLDNH_CLEAR(JLON,JLEV) = MAX(ZLDNH_CLEAR(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPDN)
      ZLDNH_CLOUD(JLON,JLEV) = MAX(ZLDNH_CLOUD(JLON,JLEV),PNLABCVP(JLON,JLEV)*ZGZLCVPDN)
    ENDIF  

  ENDDO  ! jlon  
ENDDO ! Jlev    

!IF (LECTLIM) THEN
!Limitation de Ldown 
  DO JLEV=2,KLEV
     DO JLON=KIDIA,KFDIA
        ZPHIH    = PAPHI(JLON,JLEV)-PAPHI(JLON,KLEV)+PGZ0(JLON)
        ZLDNH_CLEAR(JLON,JLEV) = MIN(ZPHIH, ZLDNH_CLEAR(JLON,JLEV))
        ZLDNH_CLOUD(JLON,JLEV) = MIN(ZPHIH, ZLDNH_CLOUD(JLON,JLEV))
        ZLDNM_CLEAR(JLON,JLEV) = MIN(ZPHIH, ZLDNM_CLEAR(JLON,JLEV))
        ZLDNM_CLOUD(JLON,JLEV) = MIN(ZPHIH, ZLDNM_CLOUD(JLON,JLEV))
     ENDDO
  ENDDO      
!ENDIF

DO JLEV=1,KLEV
   DO JLON=KIDIA,KFDIA
   
!  Calcul de Lint
   
      ZLINTM_CLEAR(JLON,JLEV) = (ZLDNM_CLEAR(JLON,JLEV)*ZLUPM_CLEAR(JLON,JLEV))/&
                              & MAX(1.0E-10_JPRB,ZLDNM_CLEAR(JLON,JLEV)+ZLUPM_CLEAR(JLON,JLEV))
      ZLINTH_CLEAR(JLON,JLEV) = (ZLDNH_CLEAR(JLON,JLEV)*ZLUPH_CLEAR(JLON,JLEV))/&
                              & MAX(1.0E-10_JPRB,ZLDNH_CLEAR(JLON,JLEV)+ZLUPH_CLEAR(JLON,JLEV))
      ZLINTM_CLOUD(JLON,JLEV) = (ZLDNM_CLOUD(JLON,JLEV)*ZLUPM_CLOUD(JLON,JLEV))/&
                              & MAX(1.0E-10_JPRB,ZLDNM_CLOUD(JLON,JLEV)+ZLUPM_CLOUD(JLON,JLEV))
      ZLINTH_CLOUD(JLON,JLEV) = (ZLDNH_CLOUD(JLON,JLEV)*ZLUPH_CLOUD(JLON,JLEV))/&
                              & MAX(1.0E-10_JPRB,ZLDNH_CLOUD(JLON,JLEV)+ZLUPH_CLOUD(JLON,JLEV))

!  Calcul de Ls

      ZCM_CLEAR = ZCH*MIN(1.0_JPRB,MAX(3.0_JPRB,1.0_JPRB+2.0_JPRB*ZRIF(JLON,JLEV)))
      ZCM_CLOUD = ZCH*MIN(1.0_JPRB,MAX(3.0_JPRB,1.0_JPRB+2.0_JPRB*ZRIF2(JLON,JLEV)))

!      ZCM_CLEAR = ZCH*MIN(3.0_JPRB,1.0_JPRB+2.0_JPRB*ZRIF(JLON,JLEV))
!      ZCM_CLOUD = ZCH*MIN(3.0_JPRB,1.0_JPRB+2.0_JPRB*ZRIF2(JLON,JLEV))

      ZLSM_CLEAR(JLON,JLEV) = ZCM_CLEAR*SQRT(PECT(JLON,JLEV)/ZNN_CLEAR(JLON,JLEV))
      ZLSH_CLEAR(JLON,JLEV) = ZCH*SQRT(PECT(JLON,JLEV)/ZNN_CLEAR(JLON,JLEV))
      ZLSM_CLOUD(JLON,JLEV) = ZCM_CLOUD*SQRT(PECT(JLON,JLEV)/ZNN_CLOUD(JLON,JLEV))
      ZLSH_CLOUD(JLON,JLEV) = ZCH*SQRT(PECT(JLON,JLEV)/ZNN_CLOUD(JLON,JLEV))

!  Calcul de Lmin

!      ZC1 = 0.5_JPRB*ZCN*RKAPPA*PAPHI(JLON,JLEV)/RG
      ZC1 = ZCN*RKAPPA*PAPHI(JLON,JLEV)/RG
      ZLMIN(JLON,JLEV) = ZLINF*ZC1/(ZC1+ZLINF)


!  Calcul de ZLEND selon  B.4

      ZC3M_CLEAR = ZLINTM_CLEAR(JLON,JLEV)*ZLINTM_CLEAR(JLON,JLEV) + ZLMIN(JLON,JLEV)*ZLMIN(JLON,JLEV)
      ZC3H_CLEAR = ZLINTH_CLEAR(JLON,JLEV)*ZLINTH_CLEAR(JLON,JLEV) + ZLMIN(JLON,JLEV)*ZLMIN(JLON,JLEV)
      ZLENDM_CLEAR(JLON,JLEV) = SQRT(2.0_JPRB)*ZLSM_CLEAR(JLON,JLEV)*&
                              & SQRT(ZC3M_CLEAR/(ZLSM_CLEAR(JLON,JLEV)*ZLSM_CLEAR(JLON,JLEV) + ZC3M_CLEAR))
      ZLENDH_CLEAR(JLON,JLEV) = SQRT(2.0_JPRB)*ZLSH_CLEAR(JLON,JLEV)*&
                              & SQRT(ZC3H_CLEAR/(ZLSH_CLEAR(JLON,JLEV)*ZLSH_CLEAR(JLON,JLEV) + ZC3H_CLEAR))

      ZC3M_CLOUD = ZLINTM_CLOUD(JLON,JLEV)*ZLINTM_CLOUD(JLON,JLEV) + ZLMIN(JLON,JLEV)*ZLMIN(JLON,JLEV)
      ZC3H_CLOUD = ZLINTH_CLOUD(JLON,JLEV)*ZLINTH_CLOUD(JLON,JLEV) + ZLMIN(JLON,JLEV)*ZLMIN(JLON,JLEV)
      ZLENDM_CLOUD(JLON,JLEV) = SQRT(2.0_JPRB)*ZLSM_CLOUD(JLON,JLEV)*&
                              & SQRT(ZC3M_CLOUD/(ZLSM_CLOUD(JLON,JLEV)*ZLSM_CLOUD(JLON,JLEV) + ZC3M_CLOUD))
      ZLENDH_CLOUD(JLON,JLEV) = SQRT(2.0_JPRB)*ZLSH_CLOUD(JLON,JLEV)*&
                              & SQRT(ZC3H_CLOUD/(ZLSH_CLOUD(JLON,JLEV)*ZLSH_CLOUD(JLON,JLEV) + ZC3H_CLOUD))
      
   ENDDO
ENDDO

! Calcul de la longueur moyenne sur la maille     

IF (NLEND == 3) THEN

!  Le probléme est que l'on ne connait pas ZNEB. Dans le cas LECTNEB il faut refaire des
!  calculs internes à ACTURB, en gros le calcul de sigmaS

!  Les calculs ont lieu sur les half, theta doit être connu sur les fulls

  ZEPSIG  = 1.E-12_JPRB
  ZEPDELT = 1.E-12_JPRB
  ZEPSQ   = 1.E-12_JPRB
  ZEPNEBS = 1.E-12_JPRB
  ZNEB(:,:) = 0.0_JPRB
  ZNEBI(:,:) = 0.0_JPRB

  DO JLEV=KTDIAN,KLEV-1
    DO JLON=KIDIA,KFDIA

      ZTF1     = PT (JLON,JLEV  )
      ZQCF1    = PQLI(JLON,JLEV  )+PQICE(JLON,JLEV  )
      ZQWF1    = PQV(JLON,JLEV  ) +PQLI(JLON,JLEV  ) +PQICE(JLON,JLEV  )
      ZQWF1    = MAX(ABS(ZQWF1),ZEPSQ)
      ZLSCPEF1 = PLSCPE(JLON,JLEV  )
      ZTLF1    = ZTF1-ZLSCPEF1*ZQCF1
      ZH1      = MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLF1))
      ZEW1     = FOEW(ZTLF1,ZH1)/PAPRSF(JLON,JLEV)
      ZQSATF1  = FOQS(ZEW1)
      ZDLEWF1  = FODLEW(ZTLF1,ZH1)
      ZQSLTLF1 = FODQS(ZQSATF1,ZEW1,ZDLEWF1)
      ZTHETAL1 = ZTHETA(JLON,JLEV)  *(1.0_JPRB-ZLSCPEF1*ZQCF1/ZTF1)
      ZDELTQF1 = ZQWF1-ZQSATF1
      ZDELTQF1 = SIGN(MAX(ABS(ZDELTQF1),ZEPDELT),ZDELTQF1)

      ZTF2     = PT (JLON,JLEV+1)
      ZQCF2    = PQLI(JLON,JLEV+1)+PQICE(JLON,JLEV+1)
      ZQWF2    = PQV(JLON,JLEV+1) +PQLI(JLON,JLEV+1) +PQICE(JLON,JLEV+1)
      ZQWF2    = MAX(ABS(ZQWF2),ZEPSQ)
      ZLSCPEF2 = PLSCPE(JLON,JLEV+1)
      ZTLF2    = ZTF2-ZLSCPEF2*ZQCF2
      ZH2      = MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLF2))
      ZEW2     = FOEW(ZTLF2,ZH2)/PAPRSF(JLON,JLEV+1)
      ZQSATF2  = FOQS(ZEW2)
      ZDLEWF2  = FODLEW(ZTLF2,ZH2)
      ZQSLTLF2 = FODQS(ZQSATF2,ZEW2,ZDLEWF2)
      ZTHETAL2 = ZTHETA(JLON,JLEV+1)*(1.0_JPRB-ZLSCPEF2*ZQCF2/ZTF2)
      ZDELTQF2 = ZQWF2-ZQSATF2
      ZDELTQF2 = SIGN(MAX(ABS(ZDELTQF2),ZEPDELT),ZDELTQF2)
    
      ZTH      = (ZTF1    +ZTF2    )/2.0_JPRB
      ZQSLTLH  = (ZQSLTLF1+ZQSLTLF2)/2.0_JPRB
      ZLSCPEH  = (ZLSCPEF1+ZLSCPEF2)/2.0_JPRB
      ZDELTQH(JLON,JLEV)  = (ZDELTQF1+ZDELTQF2)/2.0_JPRB
      ZTHETAH  = ZTH*(RATM/PAPRS(JLON,JLEV))**RKAPPA
      ZBB      = ZTH*ZQSLTLH/ZTHETAH
      ZDTHETAL = ZTHETAL1 - ZTHETAL2    
      ZDPHI    = PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
      ZDQW     =  PQV   (JLON,JLEV)-PQV   (JLON,JLEV+1)&
     &         +  PQLI  (JLON,JLEV)-PQLI  (JLON,JLEV+1)&
     &         +  PQICE (JLON,JLEV)-PQICE (JLON,JLEV+1)  

      ZAA(JLON,JLEV)      = 1.0_JPRB/(1.0_JPRB+ZLSCPEH*ZQSLTLH)

      ZIGMAS_CLOUD(JLON,JLEV) = MAX(ZEPSIG,ABS(0.2_JPRB*ZLENDM_CLOUD(JLON,JLEV)&
     &                        *ZAA(JLON,JLEV)*(ZDQW - ZBB*ZDTHETAL)/ZDPHI))
      ZIGMAS_CLEAR(JLON,JLEV) = MAX(ZEPSIG,ABS(0.2_JPRB*ZLENDM_CLEAR(JLON,JLEV)&
     &                        *ZAA(JLON,JLEV)*(ZDQW - ZBB*ZDTHETAL)/ZDPHI))

      ZNEBI(JLON,JLEV) = MAX(0.0_JPRB,SIGN(1.0_JPRB,ZTH-RTT))  ! <== on se limite aux nuages chauds
     ENDDO
  ENDDO


!  Boucle de Newton pour le calcul de ZNEB

   
  DO JN=1,3  !<== Boucle de Newton à trois itérations
     DO JLEV=KTDIAN,KLEV-1
        DO JLON=KIDIA,KFDIA
          ZIGMAS = ZNEB(JLON,JLEV)*ZIGMAS_CLOUD(JLON,JLEV) +&
          &        (1.0_JPRB-ZNEB(JLON,JLEV))*ZIGMAS_CLEAR(JLON,JLEV)
          ZRIH   = ZNEB(JLON,JLEV)*ZRI2(JLON,JLEV) +&
          &        (1.0_JPRB-ZNEB(JLON,JLEV))*ZRI(JLON,JLEV)
          ZRESUL = MAX(0.0_JPRB,SIGN(1.0_JPRB, RICRET-ZRIH ))
          ZSURSAT=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDELTQH(JLON,JLEV)))
          ZQ11   = ZAA(JLON,JLEV)*ZDELTQH(JLON,JLEV)/(2*ZIGMAS)
          INQ1   = MIN( MAX(-22,FLOOR(2*ZQ11) ), 10)
          ZINC   = 2.0_JPRB*ZQ11 - INQ1
!          ZNEB(JLON,JLEV) = ZNEBI(JLON,JLEV)* &
!          &         MIN(1.0_JPRB,(1.0_JPRB-ZINC)*ZN1D(INQ1)+ZINC*ZN1D(INQ1+1))
          ZZN1D  = MIN(1.0_JPRB,(1.0_JPRB-ZINC)*ZN1D(INQ1)+ZINC*ZN1D(INQ1+1))
          ZNEB(JLON,JLEV) = (ZZN1D*ZRESUL + ZSURSAT*(1.0_JPRB-ZRESUL))*RFACNSM
!         ZNEB(JLON,JLEV) = MAX( ZEPNEBS ,MIN(ZNEB(JLON,JLEV), 1.0_JPRB-ZEPNEBS) )
          ZNEB(JLON,JLEV) = ZNEBI(JLON,JLEV)*MAX( ZEPNEBS ,MIN(ZNEB(JLON,JLEV), 0.5_JPRB) )
        ENDDO  
     ENDDO
  ENDDO

!  Calcul des longueurs finales

   DO JLEV=1,KLEV
      DO JLON=KIDIA,KFDIA
         ZLENDM(JLON,JLEV) = ZNEB(JLON,JLEV)*ZLENDM_CLOUD(JLON,JLEV)+&
                           & (1.0_JPRB-ZNEB(JLON,JLEV))*ZLENDM_CLEAR(JLON,JLEV)
         ZLENDH(JLON,JLEV) = ZNEB(JLON,JLEV)*ZLENDH_CLOUD(JLON,JLEV)+&
                           & (1.0_JPRB-ZNEB(JLON,JLEV))*ZLENDH_CLEAR(JLON,JLEV)
!         ZLENDH(JLON,JLEV) = ZLENDH_CLEAR(JLON,JLEV)
      ENDDO
   ENDDO
ELSE
   DO JLEV=1,KLEV
      DO JLON=KIDIA,KFDIA
         ZLENDM(JLON,JLEV) = ZLENDM_CLEAR(JLON,JLEV)
         ZLENDH(JLON,JLEV) = ZLENDH_CLEAR(JLON,JLEV)
      ENDDO
   ENDDO
ENDIF  

!  Lissage verticale de la longueur de mélange

DO JLEV=2,KLEV-1
   DO JLON=KIDIA,KFDIA
      ZLENDMM(JLON,JLEV) = 0.5_JPRB*ZLENDM(JLON,JLEV)&
      &  + 0.25_JPRB*ZLENDM(JLON,JLEV-1)+ 0.25_JPRB*ZLENDM(JLON,JLEV+1)
   ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
   ZLENDMM(JLON,KLEV) = 0.75_JPRB*ZLENDM(JLON,KLEV) + 0.25_JPRB*ZLENDM(JLON,KLEV-1) 
   ZLENDMM(JLON,1)    = 0.75_JPRB*ZLENDM(JLON,1)    + 0.25_JPRB*ZLENDM(JLON,2)      
ENDDO
              
DO JLEV=1,KLEV
!DEC$ IVDEP
   DO JLON=KIDIA,KFDIA
    
!  Limitation par ALMAVE comme dans ACBL89
      
      ZPHIH    = PAPHI(JLON,JLEV)-PAPHI(JLON,KLEV)+PGZ0(JLON)
      ZGLKARMN = VKARMN*ZPHIH
      ZGLMINF(JLON,JLEV)  = MIN(RG*ALMAVE, ZGLKARMN)

!  Calcul de PUSLE et multiplication par G de ZLEND

!      PLEND(JLON,JLEV) = MAX(RG*ZLENDM(JLON,JLEV),ZGLMINF(JLON,JLEV))
      PLEND(JLON,JLEV) = MAX(RG*ZLENDMM(JLON,JLEV),ZGLMINF(JLON,JLEV))
      PUSLE (JLON,JLEV)=1.0_JPRB/(ALD*PLEND(JLON,JLEV))
!      PPHI3 (JLON,JLEV)=ZLENDH(JLON,JLEV)/ZLENDM(JLON,JLEV)
   ENDDO
ENDDO

!  Calcul de PHI3

ZPHI3MAX= (1.0_JPRB-ACBRPHIM)/ACBRPHIM
DO JLEV=1,KLEV-1
!DEC$ IVDEP
   DO JLON=KIDIA,KFDIA
     ZPHI3I = 1._JPRB +&
  &  MAX( ZPHI3MAX, ARSC1* MIN( 2.0_JPRB,PLEND(JLON,JLEV)*PLEND(JLON,JLEV)/ZG2LD2(JLON,JLEV)))
     PPHI3 (JLON,JLEV)= 1._JPRB / ZPHI3I
!     PPHI3 (JLON,JLEV)= PPHI3 (JLON,JLEV) / ZPHI3I
   ENDDO
ENDDO      
DO JLON=KIDIA,KFDIA
   PPHI3 (JLON,KLEV)= 1._JPRB
ENDDO   
   
   
   
!*
!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACLENDER',1,ZHOOK_HANDLE)
END SUBROUTINE ACLENDER
