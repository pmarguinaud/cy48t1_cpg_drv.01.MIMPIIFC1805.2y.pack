!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACLSPS(YDCST, YDGEOMETRY, YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PQ,PT,PDELP,PAPRSF,PCP,PR,PAPHI,PTS,PGM,PITM,&
 ! - OUTPUT 2D .
 & PFCSQL,PFCSQN,PFPFPL,PFPFPN,PFPLSL,PFPLSN)


!**** *ACLSPS * - SIMPLIFIED VERSION OF COMBINATION
! OF ACNEBSM AND ACMICRO

!     Subject.
!     --------
!     - COMPUTATION OF NEB AND QL/QI STARTING FROM QV AND QT 
!     - COMPUTATION OF STRATIFORM PRECIPITATION FLUXES (AUTOCONVERSION)
             
!**   Interface.
!     ----------
!        *CALL* *ACLSPS*

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR INPUT
!     -------------------

! - DIMENSIONAL PARAMETERS OF PHYSICS.

! KIDIA      : START OF HORIZONTAL LOOP (IST in CPG).
! KFDIA      : END OF HORIZONTAL LOOP (IEND in CPG).
! KLON       : HORIZONTAL DIMENSION.
! KTDIA      : START OF THE VERTICAL LOOP IN THE PHYSICS (1 IN GENERAL).
! KLEV       : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG in CPG).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PQ         : SPECIFIC HUMIDITY OF WATER VAPOR.
! PT         : TEMPERATURE.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.
! PAPRSF     : PRESSURE ON FULL LEVELS.
! PCP        :
! PR         :
! PAPHI      :
! PTS        :
! PGM        :
! PITM       :

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR OUTPUT.
!     ---------------------

! - 2D (0:KLEV) .

! PFCSQL     :
! PFCSQN     :
! PFPFPL     :
! PFPFPN     :
! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

!-----------------------------------------------------------------------

!     Externals.
!     ----------

!     Method.
!     -------
!     LARGE SCALE PRECIPITATION PACKAGE BASED ON SMITH SCHEME
!     SEE DOCUMENTATION ON GMAPDOC FOR DETAILS.
!     QC CALCULATED IN A DIAGNOSTIC WAY AT EACH TIME STEP
!     AND ALL QC PRECIPITATED
!     PROGNOSTIC QC TO COME IN A FUTURE RELEASE     

!     Author.
!     -------
!     JANUARY 2009 O Riviere

!     Modifications.
!     --------------
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK
USE YOMCST   , ONLY : TCST  
USE YOMVERT ,  ONLY : VP00

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PITM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPFPL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPFPN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN(KLON,0:KLEV) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZQCS(KLON,KLEV)
REAL(KIND=JPRB) :: ZNEIJ(KLON) 
REAL(KIND=JPRB) :: ZNEBS(KLON,KLEV)
REAL(KIND=JPRB) :: ZHCRICS(KLON,KLEV)
REAL(KIND=JPRB) :: ZRMF(KLON,KLEV)
REAL(KIND=JPRB) :: ZQSATS(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPSGDT,ZICE 
REAL(KIND=JPRB) :: ZQT(KLON,KLEV)
REAL(KIND=JPRB) :: ZTL(KLON,KLEV),ZQL(KLON,KLEV),ZQI(KLON,KLEV)
REAL(KIND=JPRB) :: ZERO(KLON,KLEV)
REAL(KIND=JPRB) :: ZPQL(KLON,KLEV),ZPQI(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPSG(KLON,KLEV),ZAUTOL(KLON,KLEV),ZAUTOI(KLON,KLEV)
REAL(KIND=JPRB) :: ZPAUTOL(KLON,KLEV),ZPAUTOI(KLON,KLEV)
REAL(KIND=JPRB) :: ZGDTSDP(KLON,KLEV)
INTEGER(KIND=JPIM) ::  JLEV, JLON
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fctdoi.ycst.h"
#include "acnebsm.intfb.h"
#include "acmicro.intfb.h" 
#include "fcttrm.ycst.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACLSPS',0,ZHOOK_HANDLE)
ASSOCIATE(YDSTA=>YDGEOMETRY%YRSTA, YDPHY2=>YDML_PHY_MF%YRPHY2,YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(TSPHY=>YDPHY2%TSPHY)
!-----------------------------------------------------------------------

!1/ CALL TO ACNEBSM 

!1-a PRELIMINARY COMPUTATIONS  

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZGDTSDP(JLON,JLEV) = ( YDCST%RG * TSPHY ) / PDELP(JLON,JLEV)
  ENDDO
ENDDO

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQT  (JLON,JLEV) = PQ(JLON,JLEV)
    ZTL  (JLON,JLEV) = PT(JLON,JLEV) 
    ZERO (JLON,JLEV) = 0.0_JPRB
  ENDDO
ENDDO

!1-b CALL TO ACNEBSM 

CALL ACNEBSM(YDCST,YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
               & ZTL,ZQT,ZERO,ZERO,&
               & PAPHI,PAPRSF,PCP,PR,&
               & PGM,YDGEOMETRY%YRSTA,&
               & ZQCS,ZNEBS,ZHCRICS,ZRMF,ZQSATS)

!1-c COMPUTATION OF RR FLUXES: CREATION OF QL AND QI

PFCSQL(:,:)=0.0_JPRB
PFCSQN(:,:)=0.0_JPRB

DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ZDPSGDT  = 1.0_JPRB / ZGDTSDP(JLON,JLEV)
    ZICE     = FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC)
    ZQL(JLON,JLEV) = ZQCS(JLON,JLEV)*(1.0_JPRB-ZICE)
    ZQI(JLON,JLEV) = ZQCS(JLON,JLEV)*ZICE
    PFCSQL(JLON,JLEV) = PFCSQL(JLON,JLEV-1)&
     & + ( ZQL(JLON,JLEV) - ZPQL(JLON,JLEV) ) * ZDPSGDT
    PFCSQN(JLON,JLEV) = PFCSQN(JLON,JLEV-1)&
     & + ( ZQI(JLON,JLEV) - ZPQI(JLON,JLEV) ) * ZDPSGDT
  ENDDO
ENDDO


!2/ FOR TIME BEEING ALL QL AND QI PRECIPITATED 
! WARNING: AUTOCONVERSION CODED BUT NOT IMPLEMENTED
! IN TL/AD VERSION

CALL ACMICRO(YDCST,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ZNEBS,PT,ZQL,ZQI,&
 &PTS,ZNEIJ,PITM,ZPAUTOL,ZPAUTOI)


!3/ AUTOCONVERSION FLUXES GO INTO PFPFPL AND PFPFPN ARRAYS 
! (SIMPLIFICATION OF ADVPRC): AUTOCONVERSION TENDENCIED
!ARE CONVERTED INTO FLUXES

PFPFPL(:,:)=0.0_JPRB
PFPFPN(:,:)=0.0_JPRB

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZDPSG(JLON,JLEV) = PDELP(JLON,JLEV) / YDCST%RG
    ZAUTOL(JLON,JLEV)=ZPAUTOL(JLON,JLEV)*ZDPSG(JLON,JLEV)
    ZAUTOI(JLON,JLEV)=ZPAUTOI(JLON,JLEV)*ZDPSG(JLON,JLEV)
    PFPFPL (JLON,JLEV) = PFPFPL (JLON,JLEV-1) +ZAUTOL(JLON,JLEV) 
    PFPFPN (JLON,JLEV) = PFPFPN (JLON,JLEV-1) +ZAUTOI(JLON,JLEV)
  ENDDO
ENDDO

PFPLSL(:,:)=0.0_JPRB
PFPLSN(:,:)=0.0_JPRB

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
     PFPLSL(JLON,JLEV) = PFPLSL(JLON,JLEV-1)+ZAUTOL(JLON,JLEV)
     PFPLSN(JLON,JLEV) = PFPLSN(JLON,JLEV-1)+ZAUTOI(JLON,JLEV)
  ENDDO
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACLSPS',1,ZHOOK_HANDLE)
END SUBROUTINE ACLSPS
