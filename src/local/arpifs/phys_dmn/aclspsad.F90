!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACLSPSAD(YDCST, YDGEOMETRY,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PQ5,PQL5,PQI5,PQR5,PQS5,PLIQCVPPKF,PNEBCVPPKF,&
 & PT5,PDELP5,PAPRSF5,PCP5,PR5,PAPHI5,PTS5,PGM,&
 & PQ,PQL,PQI,PT,PDELP,PAPRSF,PCP,PR,PAPHI,PTS,PITM,&
 ! - OUTPUT 2D .
 & PFCSQL5,PFCSQN5,PFPLSL5,PFPLSN5,&
 & PFCSQL,PFCSQN,PFPLSL,PFPLSN)  

!**** *ACLSPSAD * - AD VERSION OF ACLSPS 
!     Subject.
!     --------
!     - COMPUTATION OF NEB AND QL/QI STARTING FROM QV AND QT 
!     - COMPUTATION OF STRATIFORM PRECIPITATION FLUXES (AUTOCONVERSION)
             
!**   Interface.
!     ----------
!        *CALL* *ACLSPSAD*

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR INPUT
!     -------------------

! - DIMENSIONAL PARAMETERS OF PHYSICS.

! KIDIA      : START OF HORIZONTAL LOOP (IST in CPG).
! KFDIA      : END OF HORIZONTAL LOOP (IEND in CPG).
! KLON       : HORIZONTAL DIMENSION.
! KTDIA      : START OF THE VERTICAL LOOP IN THE PHYSICS (1 IN GENERAL).
! KLEV       : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG in CPG).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PAPRSF     : PRESSURE ON FULL LEVELS.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOR.
! PT         : TEMPERATURE.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR OUTPUT.
!     ---------------------

! - 2D (0:KLEV) .

! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/YOMCST /
! COMMON/YOMSIMPHL/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externals.
!     ----------

!     Method.
!     -------

!     Author.
!     -------
! jan 2009 O Riviere
! April 2011 O.Riviere LPROCLDTL and LMICROTL added
! June 2011  O.Riviere ADVPRCSAD added+modifs. of dataflow
! 2011-10-04 F. Kocaman : CY38 cleaning
! T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE YOMCST   , ONLY : TCST 
USE YOMCST   , ONLY : RG       ,RV     ,RCPV     ,&
 & RETV     ,RCW      ,RCS      ,RLVTT    ,RLSTT    ,&
 & RTT      ,RALPW    ,RBETW    ,RGAMW    ,RALPS    ,&
 & RBETS    ,RGAMS    ,RALPD    ,RBETD    ,RGAMD,RDT 
USE YOMVERT  , ONLY : VP00

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQR5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLIQCVPPKF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEBCVPPKF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS5(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PITM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCSQN(KLON,0:KLEV) 
REAL(KIND=JPRB) :: ZQCS5   (KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS   (KLON,KLEV)
REAL(KIND=JPRB) :: ZNEIJ  (KLON) 
REAL(KIND=JPRB) :: ZNEBS5  (KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS05  (KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS05 (KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS  (KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS0  (KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS0 (KLON,KLEV)
REAL(KIND=JPRB) :: ZDPSGDT,ZICE,ZICE5 
REAL(KIND=JPRB) :: ZQT(KLON,KLEV),ZQT5(KLON,KLEV)
REAL(KIND=JPRB) :: ZTL(KLON,KLEV),ZQL(KLON,KLEV),ZQI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTL5(KLON,KLEV),ZQL5(KLON,KLEV),ZQI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZERO(KLON,KLEV)
REAL(KIND=JPRB) :: ZRHCRI(KLON,KLEV),ZRMF(KLON,KLEV),ZQSATS(KLON,KLEV)
REAL(KIND=JPRB) :: ZPQL(KLON,KLEV),ZPQI(KLON,KLEV)
REAL(KIND=JPRB) :: ZPQL5(KLON,KLEV),ZPQI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPSG(KLON,KLEV),ZAUTOL(KLON,KLEV),ZAUTOI(KLON,KLEV)
REAL(KIND=JPRB) :: ZPAUTOL(KLON,KLEV),ZPAUTOI(KLON,KLEV)
REAL(KIND=JPRB) :: ZGDTSDP(KLON,KLEV)
REAL(KIND=JPRB) :: ZPFCSQL(KLON,0:KLEV),ZPFCSQN(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZPFPLSL(KLON,0:KLEV),ZPFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZPAUTOL5(KLON,KLEV),ZPAUTOI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZPFPEVPL(KLON,0:KLEV),ZPFPEVPN(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZPFPEVPL5(KLON,0:KLEV),ZPFPEVPN5(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZMODULQCPROG
REAL(KIND=JPRB) :: ZFLUXCOR,ZIGEL
INTEGER(KIND=JPIM) ::  JLEV, JLON
REAL(KIND=JPRB) :: ZHOOK_HANDLE
#include "fctdoi.ycst.h"
#include "fctdoiad.func.h"
#include "fcttrm.ycst.h"
#include "fcttrmad.func.h"
#include "acnebsm.intfb.h"
#include "acnebsmad.intfb.h"
#include "acmicroad.intfb.h"
#include "advprcsad.intfb.h"
#include "acmicro.intfb.h"

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACLSPSAD',0,ZHOOK_HANDLE)
ASSOCIATE(YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE,YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP,YDCSGLEG=>YDGEOMETRY%YRCSGLEG,YDVSPLIP=>YDGEOMETRY%YRVSPLIP, &
 & YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, YDCSGEOM=>YDGEOMETRY%YRCSGEOM,YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB, &
 & YDGSGEOM=>YDGEOMETRY%YRGSGEOM, &
 & YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, &
 & YDSPGEOM=>YDGEOMETRY%YSPGEOM, YDSIMPHL=>YDML_PHY_MF%YRSIMPHL,YDTOPH=>YDML_PHY_MF%YRTOPH, &
 & YDPHY2=>YDML_PHY_MF%YRPHY2,YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(RQICRSN=>YDPHY0%RQICRSN, RDTFAC=>YDPHY0%RDTFAC, &
 & TSPHY=>YDPHY2%TSPHY, &
 & NTPLUI=>YDTOPH%NTPLUI, &
 & LMICROTL=>YDSIMPHL%LMICROTL, LCOLLECTL=>YDSIMPHL%LCOLLECTL, &
 & LNEBCVPPKF=>YDSIMPHL%LNEBCVPPKF, LIGELREPRO=>YDSIMPHL%LIGELREPRO, &
 & RMODULQCPROG=>YDSIMPHL%RMODULQCPROG, LTRAJCOND=>YDSIMPHL%LTRAJCOND, &
 & LPROCLDTL=>YDSIMPHL%LPROCLDTL, STPRE=>YDSTA%STPRE)
ZPFPEVPL5(:,:)=0.0
ZPFPEVPN5(:,:)=0.0




ZNEIJ(:)=0.0_JPRB




!COMPUTATION OF TRAJECTORY BEFORE CALL TO AD 

IF (LPROCLDTL) THEN
  ZPQL5(:,:)=PQL5(:,:)
  ZPQI5(:,:)=PQI5(:,:)
ELSE
  ZPQL5(:,:)=0.0
  ZPQI5(:,:)=0.0
ENDIF

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZGDTSDP(JLON,JLEV) = ( YDCST%RG * TSPHY ) / PDELP5(JLON,JLEV)
  ENDDO
ENDDO


DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQT5(JLON,JLEV) = PQ5(JLON,JLEV)!+ZPQL5(JLON,JLEV)+ZPQI5(JLON,JLEV) 
    ZTL5(JLON,JLEV) = PT5(JLON,JLEV)
    ZERO (JLON,JLEV) = 0.0_JPRB
    ZQL5(JLON,JLEV)=ZPQL5(JLON,JLEV)
    ZQI5(JLON,JLEV)=ZPQI5(JLON,JLEV)
  ENDDO
ENDDO

ZMODULQCPROG=1.0
IF (LTRAJCOND) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ZPQL5(JLON,JLEV)=PQL5(JLON,JLEV)
      ZPQI5(JLON,JLEV)=PQI5(JLON,JLEV)
    ENDDO
  ENDDO
  ZMODULQCPROG=RMODULQCPROG!0.75
ENDIF
! CALL TO ACNEBSM IN ORDER TO RETRIEVE ZQCS5

  CALL ACNEBSM(YDCST,YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
               & ZTL5,ZQT5,ZQL5,ZQI5,&
               & PAPHI5,PAPRSF5,PCP5,PR5,&
               & PGM,YDGEOMETRY%YRSTA,&
               & ZQCS5,ZNEBS5,ZRHCRI,ZRMF,ZQSATS)
       
DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZICE5     = FONICE(PT5(JLON,JLEV),YDPHY0%RDTFAC)
    IF (LNEBCVPPKF) THEN
      ZNEBS5(JLON,JLEV)=PNEBCVPPKF(JLON,JLEV)
      ZQCS5(JLON,JLEV)=PLIQCVPPKF(JLON,JLEV)
     ENDIF
    ZQL5(JLON,JLEV) = ZQCS5(JLON,JLEV)*(1.0_JPRB-ZICE5)
    ZQI5(JLON,JLEV) = ZQCS5(JLON,JLEV)*ZICE5
    ZQCS05(JLON,JLEV) = ZQCS5(JLON,JLEV)
    ZNEBS05(JLON,JLEV) = ZNEBS5(JLON,JLEV)
  ENDDO
ENDDO

IF (LCOLLECTL) THEN ! OTHERWISE RECOMPUTATION OF ZPAUTOX5 not needed 

IF (LMICROTL) THEN
  
  RQICRSN=1.0_JPRB ! TO ENSURE CONSITENCY WITH ACMICROTL 
  CALL ACMICRO(YDCST,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ZNEBS5,PT5,ZQL5,ZQI5,PTS5,ZNEIJ,PITM,ZPAUTOL5,ZPAUTOI5)
ELSE
  ZPAUTOL5(:,:)=ZQL5(:,:)/TSPHY
  ZPAUTOI5(:,:)=ZQI5(:,:)/TSPHY
ENDIF     



DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZDPSGDT  = 1.0_JPRB / ZGDTSDP(JLON,JLEV)
    ZIGEL = MAX(0.0_JPRB,SIGN(1.0_JPRB,PT5(JLON,JLEV)-YDCST%RTT))
    IF (LIGELREPRO) ZIGEL=1.0_JPRB
    ZPAUTOL5(JLON,JLEV) = ZIGEL*ZPAUTOL5(JLON,JLEV)
    ZPAUTOI5(JLON,JLEV) = ZPAUTOI5(JLON,JLEV) + (1.0_JPRB-ZIGEL)*ZPAUTOL5(JLON,JLEV)
  ENDDO
ENDDO



ENDIF

!---------------------------------------------
!ADJOINT

!COPY INTENT(IN) ARRAYS INTO LOCAL VAR.
ZPFCSQL(:,:)=PFCSQL(:,:)
ZPFCSQN(:,:)=PFCSQN(:,:)
ZPFPLSL(:,:)=PFPLSL(:,:)
ZPFPLSN(:,:)=PFPLSN(:,:)

! SETTING TO ZERO LOCAL ADJ VARIABLES
ZAUTOL(:,:)=0.0_JPRB
ZPAUTOL(:,:)=0.0_JPRB
ZAUTOI(:,:)=0.0_JPRB
ZPAUTOI(:,:)=0.0_JPRB
ZQL(:,:)=0.0_JPRB
ZPQL(:,:)=0.0_JPRB
ZQI(:,:)=0.0_JPRB
ZPQI(:,:)=0.0_JPRB
ZQCS(:,:)=0.0_JPRB
ZTL(:,:)=0.0_JPRB
ZQT(:,:)=0.0_JPRB
ZQCS0(:,:)=0.0_JPRB
ZQCS(:,:)=0.0_JPRB
ZNEBS0(:,:)=0.0_JPRB
ZNEBS(:,:)=0.0_JPRB
ZPFPEVPL(:,:)=0.0_JPRB
ZPFPEVPN(:,:)=0.0_JPRB
ZFLUXCOR=0.0_JPRB


!ZPFPEVPL(:,:)=ZPFPEVPL(:,:)-ZPFCSQL(:,:)
!ZPFPEVPN(:,:)=ZPFPEVPN(:,:)-ZPFCSQN(:,:)
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZPFPEVPL(JLON,JLEV)=ZPFPEVPL(JLON,JLEV)-ZPFCSQL(JLON,JLEV) 
    ZPFPEVPN(JLON,JLEV)=ZPFPEVPN(JLON,JLEV)-ZPFCSQN(JLON,JLEV)
  ENDDO
ENDDO


! WARNING DO NOT ZERO PFCSQX !!!


IF (.NOT.LCOLLECTL) THEN
  DO JLEV = KLEV,KTDIA,-1
    DO JLON = KFDIA, KIDIA,-1
      ZAUTOL(JLON,JLEV)=ZAUTOL(JLON,JLEV)+ZPFPLSL(JLON,JLEV)
      ZPFPLSL(JLON,JLEV-1)=ZPFPLSL(JLON,JLEV-1)+ZPFPLSL(JLON,JLEV)
      ZPFPLSL(JLON,JLEV)=0.0_JPRB

      ZAUTOI(JLON,JLEV)=ZAUTOI(JLON,JLEV)+ZPFPLSN(JLON,JLEV)
      ZPFPLSN(JLON,JLEV-1)=ZPFPLSN(JLON,JLEV-1)+ZPFPLSN(JLON,JLEV)
      ZPFPLSN(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
  ZPFPLSL(:,:)=0.0_JPRB
  ZPFPLSN(:,:)=0.0_JPRB
ELSE


  CALL ADVPRCSAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,NTPLUI,KLEV,PT5,PQ5,ZQL5,ZQI5,PQR5,PQS5,ZPAUTOL5,ZPAUTOI5,&
  &ZNEBS5,PDELP5,PAPHI5,PAPRSF5,PR5,PT,PQ,ZQL,ZQI,ZPAUTOL,ZPAUTOI,ZNEBS,PDELP,PAPHI,&
  &PAPRSF,PR,PFPLSL5,PFPLSN5,ZPFPEVPL5,ZPFPEVPN5,&
  &ZPFPLSL,ZPFPLSN,ZPFPEVPL,ZPFPEVPN)
  ZPFPLSL(:,:)=0.0_JPRB
  ZPFPLSN(:,:)=0.0_JPRB
  ZPFPEVPL(:,:)=0.0_JPRB
  ZPFPEVPN(:,:)=0.0_JPRB

ENDIF

DO JLEV =  KLEV,KTDIA,-1
  DO JLON = KIDIA, KFDIA
    ZDPSGDT  = 1.0_JPRB / ZGDTSDP(JLON,JLEV)
    ZIGEL = MAX(0.0_JPRB,SIGN(1.0_JPRB,PT5(JLON,JLEV)-YDCST%RTT))
    IF (LIGELREPRO) ZIGEL=1.0_JPRB
    IF (.NOT.LCOLLECTL) THEN
      ZDPSG(JLON,JLEV) = PDELP5(JLON,JLEV) / YDCST%RG
      ZPAUTOI(JLON,JLEV)=ZPAUTOI(JLON,JLEV)+ZAUTOI(JLON,JLEV)*ZDPSG(JLON,JLEV)
      ZPAUTOL(JLON,JLEV)=ZPAUTOL(JLON,JLEV)+ZAUTOL(JLON,JLEV)*ZDPSG(JLON,JLEV) 
      ZAUTOI(JLON,JLEV)=0.0_JPRB
      ZAUTOL(JLON,JLEV)=0.0_JPRB
    ENDIF
    ZPFCSQN(JLON,JLEV-1)=ZPFCSQN(JLON,JLEV-1)+ZPFCSQN(JLON,JLEV)
    ZQI(JLON,JLEV)=ZQI(JLON,JLEV)+ZPFCSQN(JLON,JLEV)*ZMODULQCPROG*ZDPSGDT
    ZPQI(JLON,JLEV)=ZPQI(JLON,JLEV)-ZPFCSQN(JLON,JLEV)*ZDPSGDT
    ZFLUXCOR=ZFLUXCOR+ZPFCSQN(JLON,JLEV)*ZDPSGDT
    ZPFCSQN(JLON,JLEV)=0.0_JPRB
    ZPFCSQL(JLON,JLEV-1)=ZPFCSQL(JLON,JLEV-1)+ZPFCSQL(JLON,JLEV)
    ZQL(JLON,JLEV)=ZQL(JLON,JLEV)+ZPFCSQL(JLON,JLEV)*ZMODULQCPROG*ZDPSGDT
    ZPQL(JLON,JLEV)=ZPQL(JLON,JLEV)-ZPFCSQL(JLON,JLEV)*ZDPSGDT
    ZFLUXCOR=ZFLUXCOR-ZPFCSQL(JLON,JLEV)*ZDPSGDT
    ZPFCSQL(JLON,JLEV)=0.0_JPRB
    ZPAUTOL(JLON,JLEV)=ZPAUTOL(JLON,JLEV)+(1.0_JPRB-ZIGEL)*ZFLUXCOR*TSPHY
    ZFLUXCOR=0.0_JPRB
  ENDDO
ENDDO



IF (LMICROTL) THEN
  CALL ACMICROAD(YDPHY0,YDPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
  &ZNEBS5,PT5,ZQL5,ZQI5,PTS5,ZNEIJ,PITM,&
  &ZNEBS,PT,ZQL,ZQI,&
  &ZPAUTOL5,ZPAUTOI5,ZPAUTOL,ZPAUTOI)
ELSE
  ZQL(:,:)=ZQL(:,:)+ZPAUTOL(:,:)/TSPHY
  ZPAUTOL(:,:)=0.0_JPRB
  ZQI(:,:)=ZQI(:,:)+ZPAUTOI(:,:)/TSPHY
  ZPAUTOI(:,:)=0.0_JPRB
ENDIF







DO JLEV=KLEV,KTDIA,-1
!DEC$ IVDEP
  DO JLON=KFDIA,KIDIA,-1
    ZDPSGDT  = 1.0_JPRB / ZGDTSDP(JLON,JLEV)
    ZICE5     = FONICE(PT5(JLON,JLEV),YDPHY0%RDTFAC)

    IF (LNEBCVPPKF) THEN
      ZNEBS5(JLON,JLEV)=PNEBCVPPKF(JLON,JLEV)
      ZQCS5(JLON,JLEV)=PLIQCVPPKF(JLON,JLEV)
    ENDIF
    ! This test moved two blocks upstairs for optimisation purpose
    IF (PT5(JLON,JLEV)<YDCST%RTT) THEN
      ZICE=ZQCS5(JLON,JLEV)*(ZQI(JLON,JLEV)-ZQL(JLON,JLEV))
      PT(JLON,JLEV)=PT(JLON,JLEV)+FONICEAD(ZICE,PT5(JLON,JLEV),YDPHY0%RDTFAC)
    ENDIF

    ZQCS(JLON,JLEV)=ZQCS(JLON,JLEV)+ZQI(JLON,JLEV)*ZICE5
    ! Former AD formulation
    ! ZICE=ZICE+ZQI(JLON,JLEV)*ZQCS5(JLON,JLEV)
    ZQI(JLON,JLEV)=0.0_JPRB

    ZQCS(JLON,JLEV)=ZQCS(JLON,JLEV)+ZQL(JLON,JLEV)*(1.0_JPRB-ZICE5)
    ! Former AD formulation
    ! ZICE=ZICE-ZQCS5(JLON,JLEV)*ZQL(JLON,JLEV)
    ZQL(JLON,JLEV)=0.0_JPRB

  ENDDO
ENDDO


!1-b PROPER CALL TO ACNEBSMAD 

    
 CALL ACNEBSMAD(YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
               & ZTL5,ZQT5,ZPQL5,ZPQI5,&
               & PAPHI5,PAPRSF5,PCP5,PR5,ZNEIJ,PITM,PGM,YDGEOMETRY%YRSTA%SVETAF,&
               & ZTL,ZQT,ZERO,ZERO,&
               & PAPHI,PAPRSF,PCP,PR,&
               & ZQCS5,ZNEBS5,ZQCS05,ZNEBS05,&
               & ZQCS,ZNEBS,ZQCS0,ZNEBS0)



DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
  
    PQ(JLON,JLEV)=PQ(JLON,JLEV)+ZQT(JLON,JLEV)
    ZQT(JLON,JLEV)=0.0_JPRB
    PT(JLON,JLEV)=PT(JLON,JLEV)+ZTL(JLON,JLEV)
    ZTL(JLON,JLEV)=0.0_JPRB

  ENDDO
ENDDO

ZPQL(:,:)=0.0
ZPQI(:,:)=0.0
PR(:,:)=0.0_JPRB

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACLSPSAD',1,ZHOOK_HANDLE)
END SUBROUTINE ACLSPSAD
