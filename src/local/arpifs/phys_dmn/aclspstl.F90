!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACLSPSTL(YDGEOMETRY,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PQ5,PQL5,PQI5,PQR5,PQS5,PLIQCVPPKF,PNEBCVPPKF,&
 & PT5,PDELP5,PAPRSF5,PCP5,PR5,PAPHI5,PTS5,PGM,&
 & PQ,PQL,PQI,PT,PDELP,PAPRSF,PCP,PR,PAPHI,PTS,PITM,&
 ! - OUTPUT 2D .
 & PFCSQL5,PFCSQN5,PFPLSL5,PFPLSN5,&
 & PFCSQL,PFCSQN,PFPLSL,PFPLSN)

!**** *ACLSPSTL * - TL VERSION OF ACLSPS
!     Subject.
!     --------
!     - COMPUTATION OF NEB AND QL/QI STARTING FROM QV AND QT 
!     - COMPUTATION OF STRATIFORM PRECIPITATION FLUXES (AUTOCONVERSION)
!     - WARNING: AUTOCONVERSION CODED BUT NOT ENTERED IN THIS CYCLE
!      IN TL/AD CODE.       
!**   Interface.
!     ----------
!        *CALL* *ACLSPS*

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR INPUT
!     -------------------

! - DIMENSIONAL PARAMETERS OF PHYSICS.

! KIDIA      : START OF HORIZONTAL LOOP (IST in CPG).
! KFDIA      : END OF HORIZONTAL LOOP (IEND in CPG).
! KLON       : HORIZONTAL DIMENSION
! KTDIA      : START OF THE VERTICAL LOOP IN THE PHYSICS (1 IN GENERAL).
! KLEV       : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG in CPG).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PAPRSF     : PRESSURE ON FULL LEVELS.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOR.
! PT         : TEMPERATURE.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.

!-----------------------------------------------------------------------

! -   ARGUMENTS FOR OUTPUT.
!     ---------------------

! - 2D (0:KLEV) .

! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/YOMCST /
! COMMON/YOMSIMPHL/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externals.
!     ----------

!     Method.
!     -------

!     Author.
!     -------
! jan 2009 O Riviere

! Modified:
! Avril 2011: Prognostic Qc and autoconversion added
! June 2011 O.Riviere Add advprcstl + corresponding modif. in dataflow
! 2011-10-04  F. Kocaman : CY38 cleaning
! R. El Khatib 28-Feb-2012 Prevention against uninitialized variables
! T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE YOMCST   , ONLY : RG       ,RV           ,RCPV     ,&
 & RETV     ,RCW      ,RCS      ,RLVTT    ,RLSTT    ,&
 & RTT      ,RALPW    ,RBETW    ,RGAMW    ,RALPS    ,&
 & RBETS    ,RGAMS    ,RALPD    ,RBETD    ,RGAMD,RDT 
USE YOMVERT  , ONLY : VP00

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQR5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLIQCVPPKF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEBCVPPKF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS5(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PITM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN(KLON,0:KLEV)

REAL(KIND=JPRB)  :: ZVETAF(KLEV) 
REAL(KIND=JPRB) :: ZQCS5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS(KLON,KLEV)
REAL(KIND=JPRB) :: ZNEIJ(KLON) 
REAL(KIND=JPRB) :: ZNEBS5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS05(KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS05(KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS(KLON,KLEV)
REAL(KIND=JPRB) :: ZQCS0(KLON,KLEV)
REAL(KIND=JPRB) :: ZNEBS0(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPSGDT,ZICE,ZICE5 
REAL(KIND=JPRB) :: ZQT(KLON,KLEV),ZQT5(KLON,KLEV)
REAL(KIND=JPRB) :: ZTL(KLON,KLEV),ZQL(KLON,KLEV),ZQI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTL5(KLON,KLEV),ZQL5(KLON,KLEV),ZQI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZERO(KLON,KLEV)
REAL(KIND=JPRB) ::ZPQL(KLON,KLEV),ZPQI(KLON,KLEV)
REAL(KIND=JPRB) ::ZPQL5(KLON,KLEV),ZPQI5(KLON,KLEV)
REAL(KIND=JPRB) ::ZDPSG(KLON,KLEV),ZAUTOL(KLON,KLEV),ZAUTOI(KLON,KLEV)
REAL(KIND=JPRB) ::ZPAUTOL(KLON,KLEV),ZPAUTOI(KLON,KLEV)
REAL(KIND=JPRB) ::ZAUTOL5(KLON,KLEV),ZAUTOI5(KLON,KLEV)
REAL(KIND=JPRB) ::ZPAUTOL5(KLON,KLEV),ZPAUTOI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZGDTSDP(KLON,KLEV)
INTEGER(KIND=JPIM) :: JLEV, JLON
REAL(KIND=JPRB) :: ZHOOK_HANDLE
REAL(KIND=JPRB)::ZPFPEVPL(KLON,0:KLEV),ZPFPEVPN(KLON,0:KLEV)
REAL(KIND=JPRB)::ZPFPEVPL5(KLON,0:KLEV),ZPFPEVPN5(KLON,0:KLEV)
REAL(KIND=JPRB)::ZMODULQCPROG
REAL(KIND=JPRB)::ZFLUXCOR5,ZFLUXCOR,ZIGEL
#include "fctdoi.func.h"
#include "fctdoitl.func.h"
#include "fcttrm.func.h"
#include "fcttrmtl.func.h"
#include "acmicrotl.intfb.h"
#include "acnebsmtl.intfb.h"
#include "advprcstl.intfb.h"
!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACLSPSTL',0,ZHOOK_HANDLE)
ASSOCIATE(YDSTA=>YDGEOMETRY%YRSTA,   YDSIMPHL=>YDML_PHY_MF%YRSIMPHL,YDTOPH=>YDML_PHY_MF%YRTOPH,   YDPHY2=>YDML_PHY_MF%YRPHY2,&
& YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(TSPHY=>YDPHY2%TSPHY,   NTPLUI=>YDTOPH%NTPLUI,   LMICROTL=>YDSIMPHL%LMICROTL, LCOLLECTL=>YDSIMPHL%LCOLLECTL,   &
& LNEBCVPPKF=>YDSIMPHL%LNEBCVPPKF, LIGELREPRO=>YDSIMPHL%LIGELREPRO,   RMODULQCPROG=>YDSIMPHL%RMODULQCPROG,              &
& LTRAJCOND=>YDSIMPHL%LTRAJCOND,   LPROCLDTL=>YDSIMPHL%LPROCLDTL, STPRE=>YDSTA%STPRE)
ZPFPEVPL(:,:)=0.0
ZPFPEVPN(:,:)=0.0
ZPFPEVPL5(:,:)=0.0
ZPFPEVPN5(:,:)=0.0

!0/ COMPUTATION OF ZVETAF

DO JLEV=KTDIA,KLEV
  ZVETAF(JLEV)=STPRE(JLEV)/VP00
ENDDO

!0.5 INITIALIZATION OF VARIABLES WHOSE PERTURBATIONS ARE NEGLECTED

PR(:,:)=0.0_JPRB ! ???? 
ZPQL5(:,:)=0.0
ZPQI5(:,:)=0.0

ZPQL(:,:)=0.0
ZPQI(:,:)=0.0

ZNEIJ(:)=0.0

ZMODULQCPROG=1.0
IF (LTRAJCOND) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ZPQL5(JLON,JLEV)=PQL5(JLON,JLEV)
      ZPQI5(JLON,JLEV)=PQI5(JLON,JLEV)
    ENDDO
  ENDDO
  ZMODULQCPROG=RMODULQCPROG!0.75
ENDIF

IF(LPROCLDTL) THEN
  ZPQL5(KIDIA:KFDIA,KTDIA:KLEV)=PQL5(KIDIA:KFDIA,KTDIA:KLEV)
  ZPQL(KIDIA:KFDIA,KTDIA:KLEV)=PQL(KIDIA:KFDIA,KTDIA:KLEV)
  ZPQI5(KIDIA:KFDIA,KTDIA:KLEV)=PQI5(KIDIA:KFDIA,KTDIA:KLEV)
  ZPQI(KIDIA:KFDIA,KTDIA:KLEV)=PQI(KIDIA:KFDIA,KTDIA:KLEV)
ENDIF

!1/ CALL TO ACNEBSM 


!1-a PRELIMINARY COMPUTATIONS

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZGDTSDP(JLON,JLEV) = ( RG * TSPHY ) / PDELP5(JLON,JLEV)
  ENDDO
ENDDO
!WARNING: PERTURBATION OF PEDELP NEGLECTED !



DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQT5(JLON,JLEV) = PQ5(JLON,JLEV)!+ZPQL5(JLON,JLEV)+ZPQI5(JLON,JLEV) 
    ZTL5(JLON,JLEV) = PT5(JLON,JLEV)
    ZQT(JLON,JLEV)=PQ(JLON,JLEV)
    ZTL(JLON,JLEV)=PT(JLON,JLEV)
    ZERO (JLON,JLEV) = 0.0_JPRB
    IF(LPROCLDTL) THEN
      ZQL5(JLON,JLEV)=PQL5(JLON,JLEV)
      ZQL(JLON,JLEV)=PQL(JLON,JLEV)
      ZQI5(JLON,JLEV)=PQI5(JLON,JLEV)
      ZQI(JLON,JLEV)=PQI(JLON,JLEV)
    ELSE
      ZQL5(JLON,JLEV)=0.0
      ZQI5(JLON,JLEV)=0.0
      ZQL(JLON,JLEV)=0.0
      ZQI(JLON,JLEV)=0.0
    ENDIF
  ENDDO
ENDDO







!1-b PROPER CALL TO ACNEBSMTL


 CALL ACNEBSMTL(YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
               & ZTL5,ZQT5,ZPQL5,ZPQI5,&
               & PAPHI5,PAPRSF5,PCP5,PR5,ZNEIJ,PITM,PGM,ZVETAF,&
               & ZTL,ZQT,ZQL,ZQI,&
               & PAPHI,PAPRSF,PCP,PR,&
               & ZQCS5,ZNEBS5,ZQCS05,ZNEBS05,&
               & ZQCS,ZNEBS,ZQCS0,ZNEBS0)






!1-c COMPUTATION OF RR FLUXES


PFCSQL(:,:)=0.0_JPRB
PFCSQL5(:,:)=0.0_JPRB
PFCSQN(:,:)=0.0_JPRB
PFCSQN5(:,:)=0.0_JPRB

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZICE5     = FONICE(PT5(JLON,JLEV),YDPHY0%RDTFAC)
    IF (PT5(JLON,JLEV)<RTT) THEN
      ZICE = FONICETL(PT(JLON,JLEV),PT5(JLON,JLEV),YDPHY0%RDTFAC)
    ELSE
      ZICE=0.0
    ENDIF
    IF (LNEBCVPPKF) THEN
      ZNEBS5(JLON,JLEV)=PNEBCVPPKF(JLON,JLEV)
      ZQCS5(JLON,JLEV)=PLIQCVPPKF(JLON,JLEV)
     ENDIF
    ZQL5(JLON,JLEV) = ZQCS5(JLON,JLEV)*(1.0_JPRB-ZICE5)
    ZQI5(JLON,JLEV) = ZQCS5(JLON,JLEV)*ZICE5
    
    ZQL(JLON,JLEV) = ZQCS(JLON,JLEV)*(1.0_JPRB-ZICE5)-ZQCS5(JLON,JLEV)*ZICE
    ZQI(JLON,JLEV) = ZQCS(JLON,JLEV)*ZICE5+ZQCS5(JLON,JLEV)*ZICE
  ENDDO
ENDDO



!2/PRECIPITATION OF ALL QL AND QI FOR TIME BEING
! AUTOCONVERSION TO BE TESTED


IF (LMICROTL) THEN

  CALL ACMICROTL(YDPHY0,YDPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
  &ZNEBS5,PT5,ZQL5,ZQI5,PTS5,ZNEIJ,PITM,&
  &ZNEBS,PT,ZQL,ZQI,&
  &ZPAUTOL5,ZPAUTOI5,ZPAUTOL,ZPAUTOI)
ELSE
  ZPAUTOL5(KIDIA:KFDIA,KTDIA:KLEV)=ZQL5(KIDIA:KFDIA,KTDIA:KLEV)/TSPHY
  ZPAUTOL(KIDIA:KFDIA,KTDIA:KLEV)=ZQL(KIDIA:KFDIA,KTDIA:KLEV)/TSPHY
  ZPAUTOI5(KIDIA:KFDIA,KTDIA:KLEV)=ZQI5(KIDIA:KFDIA,KTDIA:KLEV)/TSPHY
  ZPAUTOI(KIDIA:KFDIA,KTDIA:KLEV)=ZQI(KIDIA:KFDIA,KTDIA:KLEV)/TSPHY
ENDIF



DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ZDPSGDT  = 1.0_JPRB / ZGDTSDP(JLON,JLEV)
    ZIGEL =MAX(0.0_JPRB,SIGN(1.0_JPRB,PT5(JLON,JLEV)-RTT))
    IF (LIGELREPRO) ZIGEL=1.0_JPRB
    ZPAUTOL5(JLON,JLEV) = ZIGEL*ZPAUTOL5(JLON,JLEV)
    ZPAUTOI5(JLON,JLEV) = ZPAUTOI5(JLON,JLEV) + (1.0_JPRB-ZIGEL)*ZPAUTOL5(JLON,JLEV)
    ZFLUXCOR5 = (1.0_JPRB-ZIGEL)*ZPAUTOL5(JLON,JLEV)*TSPHY
    ZFLUXCOR = (1.0_JPRB-ZIGEL)*ZPAUTOL(JLON,JLEV)*TSPHY
    PFCSQL5(JLON,JLEV) = PFCSQL5(JLON,JLEV-1)&
     & + ( ZQL5(JLON,JLEV) - ZPQL5(JLON,JLEV) - ZFLUXCOR5 ) * ZDPSGDT
    PFCSQN5(JLON,JLEV) = PFCSQN5(JLON,JLEV-1)&
     & + ( ZQI5(JLON,JLEV) - ZPQI5(JLON,JLEV) + ZFLUXCOR5 ) * ZDPSGDT
    PFCSQL(JLON,JLEV) = PFCSQL(JLON,JLEV-1)&
     & + ( ZMODULQCPROG*ZQL(JLON,JLEV) - ZPQL(JLON,JLEV) - ZFLUXCOR ) * ZDPSGDT
    PFCSQN(JLON,JLEV) = PFCSQN(JLON,JLEV-1)&
     & + ( ZMODULQCPROG*ZQI(JLON,JLEV) - ZPQI(JLON,JLEV) + ZFLUXCOR ) * ZDPSGDT
    IF (.NOT. LCOLLECTL) THEN
      ZDPSG(JLON,JLEV) = PDELP5(JLON,JLEV) / RG
      ZAUTOL5(JLON,JLEV)=ZPAUTOL5(JLON,JLEV)*ZDPSG(JLON,JLEV)
      ZAUTOL(JLON,JLEV)=ZPAUTOL(JLON,JLEV)*ZDPSG(JLON,JLEV)
      ZAUTOI5(JLON,JLEV)=ZPAUTOI5(JLON,JLEV)*ZDPSG(JLON,JLEV)
      ZAUTOI(JLON,JLEV)=ZPAUTOI(JLON,JLEV)*ZDPSG(JLON,JLEV)
    ENDIF
  ENDDO
ENDDO



!3/ AUTOCONVERSION TENDENCIES CONVERTED INTO FLUXES IN PFPFPL AND 
!PFPFPN






PFPLSL5(:,:)=0.0_JPRB
PFPLSN5(:,:)=0.0_JPRB
PFPLSL(:,:)=0.0_JPRB
PFPLSN(:,:)=0.0_JPRB



IF (LCOLLECTL) THEN

  CALL ADVPRCSTL(YDML_PHY_MF,KIDIA,KFDIA,KLON,NTPLUI,KLEV,PT5,PQ5,ZQL5,ZQI5,PQR5,PQS5,ZPAUTOL5,ZPAUTOI5,&
  &ZNEBS5,PDELP5,PAPHI5,PAPRSF5,PR5,PT,PQ,ZQL,ZQI,ZPAUTOL,ZPAUTOI,ZNEBS,PDELP,PAPHI,&
  &PAPRSF,PR,PFPLSL5,PFPLSN5,ZPFPEVPL5,ZPFPEVPN5,&
  &PFPLSL,PFPLSN,ZPFPEVPL,ZPFPEVPN)

ELSE

  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
       PFPLSL5(JLON,JLEV) = PFPLSL5(JLON,JLEV-1)&
       &+ZAUTOL5(JLON,JLEV)
   
       PFPLSL(JLON,JLEV) = PFPLSL(JLON,JLEV-1)&
       &+ZAUTOL(JLON,JLEV)
       
       PFPLSN5(JLON,JLEV) = PFPLSN5(JLON,JLEV-1)&
       &+ZAUTOI5(JLON,JLEV)
       PFPLSN(JLON,JLEV) = PFPLSN(JLON,JLEV-1)&
       &+ZAUTOI(JLON,JLEV)
    ENDDO
  ENDDO

ENDIF







PFCSQL5(:,:)=PFCSQL5(:,:)-ZPFPEVPL5(:,:)
PFCSQN5(:,:)=PFCSQN5(:,:)-ZPFPEVPN5(:,:)
PFCSQL(:,:)=PFCSQL(:,:)-ZPFPEVPL(:,:)
PFCSQN(:,:)=PFCSQN(:,:)-ZPFPEVPN(:,:)



END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACLSPSTL',1,ZHOOK_HANDLE)
END SUBROUTINE ACLSPSTL
