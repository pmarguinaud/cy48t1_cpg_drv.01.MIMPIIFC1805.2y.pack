!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACMODO( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
! -----------------------------------------------------------------------
! - INPUT  2D .
                  &PALPH,PAPHIF,PAPRS,PAPRSF,PCP,&
                  &PDELP,PLNPR,PQ,PQI,PQL,PQW,&
                  &PR,PRDELP,PSIGP,PT,PTW,PU,PV,PEVEL0,POME,&
                  &PZATSLC,PGEOSLC,&
                  &PZFHP,&
                  &PFPLSL,PFPLSN,PFPLSG,&
! - OUTPUT 2D .
                  &PDIFCQD,PDIFCQLD,PDIFCQID,PDIFCSD,&
                  &PFESL,PFESN,PFESG,&
                  &PSTRCUD,PSTRCVD,&
! - INPUT/OUTPUT  2D .
                  &PDDAL,PDDOM)

! **** *ACMODO * - MOIST DOWNDRAUGHT SCHEME

! Sujet.
! ------
!     - MASS FLUX DOWNDRAUGHT SCHEME EVAPORATING
!       CONDENSATE AND PRECIPITATION CONTENT
!     - ASSOCIATED LARGE SCALE EVAPORATION/SUBLIMATION  FLUX
       
! **   Interface.
!      ----------
! *CALL* *ACMODO*
! ----------------------------------------------------------------------
! - INPUT ARGUMENTS
! -------------------
! - DIMENSIONING PARAMETERS FOR PHYSICS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (NIVEL DU SOMMET
!            : DU NUAGE).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".


! - PHYSICAL VARIABLES

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PZFHP      : TOTAL PRECIPITATION HEAT FLUX 
! ZSNP       : FRACTION OF SOLID PRECIPITATION

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PEVEL0      : MEAN GRID BOX VERTICAL VELOCITY (by which PDDOM was advected)
! POME       : updraught envt vert vel*dt
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQI        :
! PQL        :
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PZATSLC    : ATTENUATION FACTOR FOR SLANTWISE CONVECTION.
! PGEOSLC    : EQUIVALENT GEOPOTENTIEL POUR CALCUL DE dln Tv/dp
! PFPLSL     : LIQUID PRECIPITATION FLUX
! PFPLSN     : SNOW PRECIPITATION FLUX

! - 1D
! PSIGP      : PRECIPITATION MESH FRACTION

! ----------------------------------------------------------------------
! -   INPUT/OUTPUT ARGUMENTS
!     ----------------------

! - 2D (0:KLEV) .

! PDDOM      : PROGNOSTIC DOWNDRAUGHT VELOCITY T9 -> T0
! PDDAL      : PROGNOSTIC DOWNDRAUGHT MESH FRACTION

! ----------------------------------------------------------------------
! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PSTRCUD     : TENDENCY FLUX "CONVECTIF" FINAL DE QUANTITE DE MOUVEMENT "U".
! PSTRCVD     : TENDENCY FLUX "CONVECTIF" FINAL DE QUANTITE DE MOUVEMENT "V".
! PFESL       : EVAPORATION FLUX (LIQUID)
! PFESN       : EVAPORATION FLUX (SNOW)

! ----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     -------------------

! ----------------------------------------------------------------------

!     Externals.
!     ---------

!     Method.
!     --------

!     Author.
!     -------
!     03-02, L. Gerard from ACCVIMPD

!     Modifications.
!     --------------
!     04-2005 : DIRECT FLUXES CALCULATION  AND NEW FRAME AL28
!     A.Bogatchev : 26-Oct-2007 removing of severe breaks of coding rools
!     K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!     P. Marguinaud (Oct 2016) : Port to single precision
! -----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1 , ONLY: JPIM, JPRB, JPRD
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
            &RCPV     ,RETV     ,RCW      ,RCS      ,RDT     ,RLVTT ,&
            &RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
            &RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
            &RGAMD


! -----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV
INTEGER(KIND=JPIM),INTENT(IN) :: KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA

REAL(KIND=JPRB) ,INTENT(IN) :: PALPH(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PCP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDELP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQ(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQI(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQW(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PT(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PSIGP(KLON)
REAL(KIND=JPRB) ,INTENT(IN) :: PTW(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PU(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PV(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PEVEL0(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: POME(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PZATSLC(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PZFHP(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQLD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQID(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCSD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFESL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFESN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFESG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDDOM(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PGEOSLC(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFPLSL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFPLSG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PSTRCUD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PSTRCVD(KLON,0:KLEV)

! -----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZSNP(KLON,0:KLEV), ZGRP(KLON,0:KLEV)

! 2D LOCALS

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
    &,ZBET(KLON,KLEV)&
    &,ZDSE(KLON,KLEV)&
    &,ZFEVP(KLON,0:KLEV),ZFORM(KLON,0:KLEV),ZFP(KLON,0:KLEV)&
    &,ZLHE(KLON,KLEV), ZOMDT(KLON,KLEV)&
    &,ZQDN(KLON,KLEV)&
    &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV),ZTDN(KLON,KLEV)&
    &,ZUM(KLON,KLEV),ZVM(KLON,KLEV)&
    &,ZDQCA(KLON,0:KLEV)

INTEGER(KIND=JPIM) :: INAV(KLON),INACT(KLON,KLEV),INBAS(KLON,KLEV)

! 1D LOCALS

!**Prognostic Convection**:
REAL(KIND=JPRB) :: ZCP(KLON),ZENTRD(KLON)&
    &,ZFP1(KLON),ZFPE(KLON)&
    &,ZLH(KLON),ZQB(KLON),ZQN(KLON)&
    &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
    &,ZS2(KLON),ZS3(KLON),ZS4(KLON)&
    &,ZS10(KLON),ZS11(KLON)&
    &,ZS12(KLON),ZS13(KLON)&
    &,ZTB(KLON),ZTN(KLON),ZQHW(KLON),ZTHW(KLON),ZDELNL(KLON)
REAL(KIND=JPRB) :: ZSIG(KLON),ZSIG9(KLON)&
    &,ZFMDQE(KLON),ZER(KLON),ZALPHA(KLON)

INTEGER(KIND=JPIM) :: INND(KLON)

INTEGER(KIND=JPIM) :: II,ILEV, IOLD, ISUM, ITOP, ITRAN, JIT, &
                 &    JLEV, JLON

REAL(KIND=JPRB) :: ZA, ZB, ZBAS, ZC, ZD, ZBU, ZCPVMD, ZCPVMS, ZCPVMW, &
          &ZDCP, ZDELQ, ZDELT, ZDELTA, ZDPSGDT, ZDQW, &
          &ZEPSA, ZEPSALN, ZEPSDAL, ZEPSDOM, ZEPSMIX, ZEPSSNP, &
          &ZEPSP, ZESP, ZEW, ZF, ZFPO, ZFFSLC, ZAUX, &
          &ZFB, ZFPB, ZHDMHE, ZIRHOGT2, &
          &ZIDT, ZGDT, ZGDT2, ZGDTI, ZGWP, ZKSG, ZKUO1, &
          &ZMELTED, ZMIX, ZMIXCIN, ZNBA, ZOMP, ZQC, ZQW, &
          &ZRVMD,  ZRHO, ZTPHY, ZTV,ZTVN, &
          &ZCHI,ZZ, ZZ2, ZZB, ZZC, ZZDQR, ZZDQS, ZZDQG, ZFMDQEL, &
          &ZINCRQ,ZDELTHW,ZLHTHW,ZCPHW,ZKAPPAP1, ZPER
LOGICAL :: LLBO 
INTEGER(KIND=JPIM) :: IDIFCQCD

REAL(KIND=JPRB) :: ZHOOK_HANDLE

! -----------------------------------------------------------------------

#include "fcttrm.func.h"
#include "fctdoi.func.h"

! -----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACMODO',0,ZHOOK_HANDLE)
ASSOCIATE(TDDBU=>YDML_PHY_MF%YRPHY0%TDDBU, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, RCIN=>YDML_PHY_MF%YRPHY0%RCIN, &
 & GDDBETA=>YDML_PHY_MF%YRPHY0%GDDBETA, GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, TDDFR=>YDML_PHY_MF%YRPHY0%TDDFR, &
 & GDDWPF=>YDML_PHY_MF%YRPHY0%GDDWPF, GDDFXM=>YDML_PHY_MF%YRPHY0%GDDFXM, TDDGP=>YDML_PHY_MF%YRPHY0%TDDGP, &
 & GDDEVF=>YDML_PHY_MF%YRPHY0%GDDEVF, TENTRD=>YDML_PHY_MF%YRPHY0%TENTRD, GCVACHI=>YDML_PHY_MF%YRPHY0%GCVACHI, &
 & GDDDP=>YDML_PHY_MF%YRPHY0%GDDDP, TFVR=>YDML_PHY_MF%YRPHY0%TFVR, TFVS=>YDML_PHY_MF%YRPHY0%TFVS, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, NBITER=>YDML_PHY_MF%YRPHY%NBITER, LCVCAS=>YDML_PHY_MF%YRPHY%LCVCAS, &
 & YDPHY0=>YDML_PHY_MF%YRPHY0, LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
! -----------------------------------------------------------------------

! *
! ------------------------------------------------------------------

!     I - AUXILIARY CONSTANTS.

! DEVELOPMENT AND TESTING SWITCHES

IDIFCQCD=1
LLBO=.FALSE.   ! .T. for Open Loop
! things that should go to YOMPHY0 or so

!------------------------------------------------------
! TO GO BACK TO THE  OLD SCHEME WITHOUT ACTIVE SEGMENTS :
IOLD=0
IF (LCVCAS) IOLD=1

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS


! *
! ------------------------------------------------------------------
!     II - COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME).

!**Prognostic Convection**
ZTPHY=TSPHY
ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDT2=ZGDT*ZGDT
ZGDTI=1.0_JPRB/ZGDT

ZEPSP=1.E-16_JPRB
ZEPSMIX=1.E-12_JPRB
ZEPSALN=1.E-11_JPRB
ZEPSDAL=1.E-12_JPRB
ZEPSSNP=1.E-12_JPRB
ZALPHA=1.E+03_JPRB

ZFP(KIDIA:KFDIA,:)=PFPLSL(KIDIA:KFDIA,:)+PFPLSN(KIDIA:KFDIA,:)

DO JLON=KIDIA,KFDIA
  ZSNP(JLON,KTDIA-1)=FONICE(PT(JLON,KTDIA),YDPHY0%RDTFAC)
ENDDO
IF (LGRAPRO) THEN
  ZFP(:,:)=ZFP(:,:)+PFPLSG(:,:)
    !cdir unroll=8
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,JLEV)-ZEPSSNP))
      ZSNP(JLON,JLEV)=ZZ*(PFPLSN(JLON,JLEV)+PFPLSG(JLON,JLEV))/(ZFP(JLON,JLEV)+(ZZ-1._JPRB))&
       & + (1._JPRB-ZZ)*FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC)
      ZGRP(JLON,JLEV)=ZZ*PFPLSG(JLON,JLEV)/(ZFP(JLON,JLEV)+(ZZ-1._JPRB))
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,KLEV)-ZEPSSNP))
    ZSNP(JLON,KLEV)=ZZ*(PFPLSN(JLON,KLEV)+PFPLSG(JLON,KLEV))/(ZFP(JLON,KLEV)+(ZZ-1._JPRB))&
     & + (1._JPRB-ZZ)*FONICE(PT(JLON,KLEV),YDPHY0%RDTFAC)
    ZGRP(JLON,KLEV)=ZZ*PFPLSG(JLON,KLEV)/(ZFP(JLON,KLEV)+(ZZ-1._JPRB))
  ENDDO
ELSE
  !cdir unroll=8
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,JLEV)-ZEPSSNP))
      ZSNP(JLON,JLEV)=ZZ*PFPLSN(JLON,JLEV)/(ZFP(JLON,JLEV)+(ZZ-1._JPRB))&
       & +(1._JPRB-ZZ)*FONICE(0.5_JPRB*(PT(JLON,JLEV)+PT(JLON,JLEV+1)),YDPHY0%RDTFAC )
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,KLEV)-ZEPSSNP))
    ZSNP(JLON,KLEV)=ZZ*PFPLSN(JLON,KLEV)/(ZFP(JLON,KLEV)+(ZZ-1._JPRB))&
       & +(1._JPRB-ZZ)*FONICE(PT(JLON,KLEV),YDPHY0%RDTFAC)
  ENDDO

ENDIF

! Activity History: advected vert. vel.>(GCVACHI)Pa/s enforces layer activity
   ZEPSDOM=ZTPHY*GCVACHI
   ZKSG=TDDFR/RG
   ZBU= ZGDT2/(RD*(1.0_JPRB+TDDBU))
   ZEPSA=-1.E-16_JPRB ! MUST BE non zero negative
! NAMELIST PARAMETER TENTRD 
!        - BUT ZENTRD(JLON) may vary along the downward loop
   ZENTRD=TENTRD

! *
! ------------------------------------------------------------------
!    III - SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT DOWNDRAUGHT)

! - TEMPORAIRE(S) 2D (1:KLEV) .
! PDIFCQD  : WATER VAPOUR MOISTURE DIFFUSION FLUX
! PDIFCQLD : CLOUD LIQUID WATER MOISTURE DIFFUSION FLUX
! PDIFCQID : CLOUD ICE WATER MOISTURE DIFFUSION FLUX
! PDIFCSD  : DRY STATIC ENERGY DIFFUSION FLUX
! ZFEVP   : PRECIPITATION EVAPORATION FLUX
! PSTRCUD  : TEND. FLUX DE QUANTITE DE MOUVEMENT "U" DU AU COURANT DESCENDANT.
! PSTRCVD  : TEND. FLUX DE QUANTITE DE MOUVEMENT "V" DU AU COURANT DESCENDANT.


!      SETTING FLUXES TO ZERO.
    ZFORM=0.0_JPRB
    ZFEVP=0.0_JPRB
    INBAS=0


!       PRELIMINARY CALCULATIONS OF ALREADY DETERMINED PROPERTIES

ZSIG9=0.0_JPRB
ZS12=0.0_JPRB

! RESET DD SPEED AND  FRACTION AT KTDIA AND HIGHER

PDDOM(KIDIA:KFDIA,1:KTDIA)=0.0_JPRB
PDDAL(KIDIA:KFDIA,1:KTDIA)=0.0_JPRB

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
! PDDOM  : ADVECTED  ABSOLUTE DD VELOCITY omega_d 
!          (no relation to omega_e)
!         BUT there is no dd with omega_d<omega_e in the ud env.
        PDDOM(JLON,JLEV)=MAX(0.0_JPRB,PDDOM(JLON,JLEV)*ZTPHY,&
                        & POME(JLON,JLEV))
        ZOMDT(JLON,JLEV)=PEVEL0(JLON,JLEV)*ZTPHY
! CLEAN PDDAL THEN AVERAGE OVER THE VERTICAL
        ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB,PDDAL(JLON,JLEV)-ZEPSALN))
        PDDAL(JLON,JLEV)=PDDAL(JLON,JLEV)*ZZ
        ZSIG9(JLON)=ZSIG9(JLON)+PDDAL(JLON,JLEV)
        ZS12(JLON)=ZS12(JLON)+ZZ
! ZLHE : ENVIRONMENT LOCAL LATENT HEAT PROFILE
! ZDSE : ENVIRONMENT DRY STATIC ENERGY PROFILE
        ZDELTA=0.5*(ZSNP(JLON,JLEV)+ZSNP(JLON,JLEV-1))
        ZLHE(JLON,JLEV)=FOLH(PTW(JLON,JLEV),ZDELTA)
        ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
  ENDDO
ENDDO

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    PGEOSLC(JLON,JLEV)=MIN(PGEOSLC(JLON,JLEV),PGEOSLC(JLON,JLEV+1)&
     & *PTW(JLON,JLEV)/PTW(JLON,JLEV+1))
  ENDDO
ENDDO
! *
! ------------------------------------------------------------------
!     IV - TOP INITIALISATION. 
!------------------------------------------------------
!  remark: KTDIA passed to the routine may be lower than 
!          model level 1 - the best is the top of the highest cloud
!------------------------------------------------------
! - 2D LOCALS 
! ZSDN       : DOWNDRAUGHT DRY STATIC ENERGY PROFILE.
! ZQDN       : DOWNDRAUGHT VAPOUR SPECIFIC CONTENT PROFILE.
! ZTDN       : DOWNDRAUGHT TEMPERATURE PROFILE.
! - 1D LOCALS
! ZTN        : TEMPERATURE OF THE DOWNDRAFT.
! ZQN        : DOWNDRAFT VAPOUR SPECIFIC CONTENT.
! INND       : ZERO IF NO PHYSICAL SOLUTION, 1 OTHERWISE

DO JLON=KIDIA,KFDIA
! Mean fraction in ZSIG9
  ZSIG9(JLON)=ZSIG9(JLON)/(MAX(1.0_JPRB,ZS12(JLON)))
  ZTN(JLON)=PTW(JLON,KTDIA)
  ZQN(JLON)=PQW(JLON,KTDIA)
  ZSDN(JLON,KTDIA)=(RCPD+ZCPVMD*ZQN(JLON))*ZTN(JLON)+PAPHIF(JLON,KTDIA)
  ZQDN(JLON,KTDIA)=ZQN(JLON)
  ZTDN(JLON,KTDIA)=ZTN(JLON)
  INACT(JLON,KTDIA)=0
  INND(JLON)=1
ENDDO

! Compute flux coefficients for advection in prognostic equation

! Remark: coefficients taken >0 in the formulation!
! BUT it could be that PDDOM<ZOMDT 
! Because ZOMDT is the mean grid box velocity, including the
! updraught  => in this case the advection term is in the oposite
! direction 
   DO JLON=KIDIA,KFDIA
      ZS12(JLON)=PDDOM(JLON,KLEV)-ZOMDT(JLON,KLEV)
   ENDDO
!cdir unroll=8
   DO JLEV=KLEV-1,KTDIA,-1
      DO JLON=KIDIA,KFDIA
         ZZ=PDDOM(JLON,JLEV)-ZOMDT(JLON,JLEV)
         ZFORM(JLON,JLEV)=0.5_JPRB*(ZZ+ZS12(JLON))
         ZS12(JLON)=ZZ
      ENDDO
   ENDDO

!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D . 

  ZFP1=0.0_JPRB
  ZFPE=0.0_JPRB
  ZS2=0.0_JPRB
  ZS3=0.0_JPRB
  ZS4=0.0_JPRB
  ZS10=0.0_JPRB
  ZS12=0.0_JPRB ! suppose PDDOM(KTDIA)=0
  ZER=0.0_JPRB
! *
! ------------------------------------------------------------------
!     V - FIRST VERTICAL LOOP (DOWNWARDS)  INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     ITOP MAY HELP AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART
!      OF THE ATMOSPHERE 

ITOP=KTDIA
DO JLEV=KTDIA+1,KLEV

!     SATURATED ADIABAT WITH ENTRAINMENT.
!     FROM FULL LEVEL JLEV-1 TO JLEV

  DO JLON=KIDIA,KFDIA

! INAV: CONDENSATE AVAILABILITY INDEX: if the net precipitation flux 
!                                      at full level JLEV-1 si >0
! INACT: FINAL ACTIVITY INDEX

! Update approximate cumulated  evaporation flux at interface Jlev-1:
! BEWARE: ZFEVP stores POSITIVE water vapour increments
!         hence ZFPE must be SUBTRACTED from the gross precip

    ZFPE(JLON)=ZFPE(JLON)+ZER(JLON)*PDDOM(JLON,JLEV-1)*ZGDTI&
        &                             *PDDAL(JLON,JLEV-1)
    ZFPO=ZFP(JLON,JLEV-1)-ZFPE(JLON)
! Net precip flux at FULL level JLEV-1
    ZFPB=0.5_JPRB*(ZFP1(JLON)+ZFPO)
    ZFP1(JLON)=ZFPO
    INAV(JLON)= MAX(0.0_JPRB,SIGN(1.0_JPRB,ZFPB-ZEPSP))
!-
!     ENTRAINEMENT.

! - TEMPORAIRE(S) 1D .

! ZTB        : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB        : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".



! ZRMIX at half JLEV-1
    ZMIX=INAV(JLON)*ZENTRD(JLON)*&
                   &   (PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
    ZTB(JLON)=ZTN(JLON)+ZMIX*(PTW(JLON,JLEV-1)-ZTN(JLON))
    ZQB(JLON)=ZQN(JLON)+ZMIX*(PQW(JLON,JLEV-1)-ZQN(JLON))
    ZQHW(JLON)=ZQB(JLON)
    ZTHW(JLON)=ZTB(JLON)
! Store ZMIX for further use (momentum profile and RCIN)
    ZRMIX(JLON,JLEV-1)=ZMIX

!   ice fraction and latent heat for mixed air temperature

!   ZDELTHW=ZSNP(JLON,JLEV-1)
    ZDELTHW=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTHW(JLON)))
    ZLHTHW=FOLH(ZTHW(JLON),ZDELTHW)
    ZCPHW=RCPD+ZCPVMD*ZQHW(JLON)

!   computation of new equilibrium of mixed air

    ZEW= FOEW (ZTHW(JLON),ZDELTHW)
    ZESP=ZEW/PAPRSF(JLON,JLEV-1)
    ZQW= FOQS (ZESP)
    ZDQW=FODQS (ZQW,ZESP, FODLEW (ZTHW(JLON),ZDELTHW))
    ZINCRQ=(ZQW-ZQHW(JLON))*ZCPHW/(ZCPHW+ZLHTHW*ZDQW)
    ZQHW(JLON)=ZQHW(JLON)+ZINCRQ

!     HYDROSTATIC COEFFICIENTS FOR THE DOWNDRAFT PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB       : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH       : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH       : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=-PALPH(JLON,JLEV-1)*(RD+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*(RD+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*RV

!     TO COMPUTE ADIABATIC PROFILE GCVADS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUI-PRESSURE TO EQUI-GEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
     &-PAPHIF(JLON,JLEV-1))/ZTB(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

    ZFFSLC=1.0_JPRB/(1.0_JPRB+PZATSLC(JLON,JLEV-1))
    ZRBB(JLON)=ZRBB(JLON)*ZFFSLC
    ZRBH(JLON)=ZRBH(JLON)*ZFFSLC
    ZRVH(JLON)=ZRVH(JLON)*ZFFSLC

! We consider the phase at the half level between JLEV-1 and JLEV

!   ZDELTA=ZSNP(JLON,JLEV-1)
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ZDELNL(JLON)=ZDELTA

!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH        : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP        : AS PZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)= FOLH (ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

  IF (INAV(JLON) <= 0) THEN

!     NO CONDENSATE AVAILABLE, useless to compute a saturated descent
!     DRY ADIABAT: ZDELQ=0
!     THE DOWNDRAUGHT HAS TO VANISH

! Should check the effect of  ZDCP !
    ZDELT= (PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))/(RCPD+ZCPVMD*ZQB(JLON))
    ZDELQ=0.0_JPRB
!    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ELSE
 
!     COMPUTE THE SATURATED ADIABAT
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.
 
    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV-1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=&
      & -((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ENDIF
  ENDDO

 
! For VECTORIAL COMPUTERS, put the longest loop inside
 
!     NEWTON LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA
      IF (INAV(JLON) > 0) THEN

 
!  NO SNOW OPTION DEPENDENT COMPUTATIONS:
!  we speak about precipitation: do not change their phase here
 

!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZDELTA=ZDELNL(JLON)
      ZEW= FOEW (ZTN(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON),ZDELTA))

!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZQN(JLON)=ZQN(JLON)+ZDELQ
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZTN(JLON)=ZTN(JLON)+ZDELT
      ENDIF
    ENDDO
  ENDDO
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ZDQCA(JLON,JLEV-1)=ZQN(JLON)-ZQHW(JLON)

!    ZDELTA=ZSNP(JLON,JLEV-1)
 
!     STABILITY TEST

! expression for Tvc and Tv, assume same condensate 
! in the draught and the environment
! NOW REFER TO THE WET BULB in the environment
     ZQC=MAX(0.0_JPRB,PQI(JLON,JLEV)+PQL(JLON,JLEV))
 
    ZTVN=(1.0_JPRB-ZQC+RETV*ZQN(JLON))*ZTN(JLON)
    ZTV=(1.0_JPRB-ZQC+RETV*PQW(JLON,JLEV))*PTW(JLON,JLEV)
    ZKUO1=(ZTVN-ZTV)

!     DOWNDRAUGHT ACTIVITY DIAGNOSTIC

! Active IF negatively buoyant 
!        OR positive velocity at previous tstep (Activity history) 
!        OR positive velocity at above layer 
!    AND THERE IS CONDENSATE AVAILABLE 
!    AND ZQN>ZQB:  see below, after getting the FINAL ZQN 
 
    II=(1-NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,0.0_JPRB+ZKUO1))))*&
   &(1-NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,0.0_JPRB+&
   &PDDOM(JLON,JLEV)-MAX(0.0_JPRB,POME(JLON,JLEV))-ZEPSDOM))))*&
   &(1-NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,0.0_JPRB+&
   & PDDOM(JLON,JLEV-1)-MAX(0.0_JPRB,POME(JLON,JLEV-1))-ZEPSDOM))))
    INACT(JLON,JLEV)=(1-II)*INAV(JLON)
 
! IF NOT ACTIVE, GO BACK TO THE BLUE POINT OF THE ENVIRONMENT
! IN CASE OF A "WARMER" DOWNDRAFT THAN THE ENVIRONMENT,
! go back to blue point (RCIN=0) or a mixture of it and dd (RCIN=1)
! ( should maybe set inact=0 then ?)
   ZMIXCIN=ZRMIX(JLON,JLEV-1)*RCIN
   ZQN(JLON)=INACT(JLON,JLEV)* MIN(ZQN(JLON),&
      & MAX(PQ(JLON,JLEV),PQW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)&
      & +ZMIXCIN*ZQN(JLON))&
      & +(1-INACT(JLON,JLEV))*PQW(JLON,JLEV)
   ZTN(JLON)=INACT(JLON,JLEV)* MIN(ZTN(JLON),&
      & MIN(PT(JLON,JLEV),PTW(JLON,JLEV))*(1.0_JPRB-ZMIXCIN)&
      & +ZMIXCIN*ZTN(JLON))&
      & +(1-INACT(JLON,JLEV))*PTW(JLON,JLEV)

!** Local Buoyancy for pronostic: Use only NEGATIVE values
!** otherwise conditions are fuzzy.
! if returned to blue point, ZKUO1 will be zero
    ZTVN=(1.0_JPRB+RETV*ZQN(JLON))*ZTN(JLON)
    ZKUO1=MIN(0.0_JPRB,ZTVN-ZTV)

!  FINAL DOWNDRAUGHT PROPERTIES at full JLEV

    ZQDN(JLON,JLEV)=ZQN(JLON)
    ZTDN(JLON,JLEV)=ZTN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQDN(JLON,JLEV))*ZTN(JLON)+PAPHIF(JLON,JLEV)
 
!   Store hd-he in ZHDMHE for further computations
!   PROVISIONAL STORAGE OF hd-hw, 
!     division by (1-sigma_d^') to be done later
 
    ZHDMHE=PCP(JLON,JLEV)*(ZTDN(JLON,JLEV)-PTW(JLON,JLEV))&
          & +ZLHE(JLON,JLEV)*(ZQDN(JLON,JLEV)-PQW(JLON,JLEV))
 
!-------------------------------------------------------------
!  PROGNOSTIC DOWNDRAUGHT VELOCITY at full JLEV
!-------------------------------------------------------------
! Fall velocity of precipitation:
! ZGWP => following the phase !

! not yet updated evaporation at JLEV
! so ZFPO is still the sum  PFPLL+PFPLN:
! ZGWP is absolute value of fall velocity *g*dt
 
! THE PHASE OF THE PRECIPITATION at  FULL LEVELS 

    IF (LGRAPRO) THEN
      ZMELTED=1.0_JPRB-((ZSNP(JLON,JLEV)-ZGRP(JLON,JLEV))+(ZSNP(JLON,JLEV-1)-ZGRP(JLON,JLEV-1)))*0.5_JPRB
    ELSE
      ZMELTED=1.0_JPRB-(ZSNP(JLON,JLEV)+ZSNP(JLON,JLEV-1))*0.5_JPRB
    ENDIF  
    ZGWP=ZGDT*(ZMELTED*TFVR&
        &    +(1.0_JPRB-ZMELTED)*TFVS)
 
! GDDWPF to modulate the influence of wP on ZOMP:
    ZRHO=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))
    ZOMP=GDDWPF*ZRHO*ZGWP  +POME(JLON,JLEV)
    ZIRHOGT2=1.0_JPRB/(ZRHO*ZRHO*ZGDT2)
! Apply drag (<0) only if  omega_d > omega_P
! BUT if must be  omegad(t+dt) > omega_P !!!
    ZCHI= RD*ZTVN*(ZENTRD(JLON) + ZKSG)/PAPRSF(JLON,JLEV) 
    ZZ2=GDDDP*SIGN(1.0_JPRB,PDDOM(JLON,JLEV))&
       & /(PAPRS(JLON,KLEV)-PAPRSF(JLON,JLEV))**GDDBETA
    ZKAPPAP1=1.0_JPRB-PR(JLON,JLEV)*PTW(JLON,JLEV)/PGEOSLC(JLON,JLEV)
 
! GGL treatment of advection (Geleyn et al.)
 
    ZA=ZKAPPAP1/PAPRSF(JLON,JLEV)-ZCHI -ZZ2
    ZZB=2.0_JPRB*ZOMP*ZCHI-ZOMDT(JLON,JLEV)*ZKAPPAP1&
     & /PAPRSF(JLON,JLEV)
    ZB=-1.0_JPRB+ ZZB -ZFORM(JLON,JLEV-1)*PRDELP(JLON,JLEV)
! AT LEVEL KLEV, NULLIFY THE NET ADVECTION, else there is
! accumulation, because we ignore the HORIZONTAL component !
! ILEV is 0 at KLEV, 1 elsewhere
    ILEV=MIN(1,KLEV-JLEV)
    ZD=0.5_JPRB*(PDDOM(JLON,JLEV)-PDDOM(JLON,JLEV+ILEV))
    ZFB=- PAPRSF(JLON,JLEV)*ZBU*ZKUO1/(ZTVN*ZTV)
    ZZC= -ZOMP*ZOMP*ZCHI +ZFB
    ZC=ZZC + PDDOM(JLON,JLEV)&
         & + ILEV*PRDELP(JLON,JLEV)* (-ZFORM(JLON,JLEV)*ZD&
         &  +ZFORM(JLON,JLEV-1)*(PDDOM(JLON,JLEV-1)-ZS12(JLON)))
    ZS12(JLON)=ZD
    ZZ=ZB*ZB-4._JPRB*ZA*ZC
! One MUST have: za<0, zb<0 - impose ZA<-1E-15 for the division
!                For the square root, ZZ>0
!                For safety, ZC>0 (predominance of negative buoyancy)
    ZZ2=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZZ))*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZC))&
       &*MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZA))&
       &*MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZB))

! IF A> -1.E-6 there is danger of unsufficient braking !
! Then we should make an additional braking ??
    ZZ=SQRT(ZZ2*ZZ)
! PDDOM independant of INACT: just >0 AND > POME
    PDDOM(JLON,JLEV)=ZZ2*MAX(0.0_JPRB, (-ZB-ZZ)/(MIN(ZA,ZEPSA)*2.0_JPRB),&
                            & POME(JLON,JLEV))

!--------------------------------------------------------------
!          NET EVAPORATION IN THE DOWNDRAUGHT
!--------------------------------------------------------------
! FORBID NEGATIVE VALUES : RESET INACT TO ZERO
 
    ZDELQ=ZQN(JLON)-ZQB(JLON)
    INACT(JLON,JLEV)= INACT(JLON,JLEV)*MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDELQ))
! ZDELQ RESET TO ZERO IF ARRIVAL LEVEL JLEV IS INACTIVE
    ZDELQ=ZDELQ*INACT(JLON,JLEV)
 
! STORE THE EVAPORATION INCREMENT at interface level JLEV-1 in ZFEVP
! INTERFACE JLEV-1 is between JLEV-1 and JLEV
    ZER(JLON)=ZDELQ
! ZS3 accumulates the work of the buoyancy force:
!   (-F_b)*sigma_d omega_d dp/(rho g) *dt
! and sigma_d out of the integral
    ZZ=INACT(JLON,JLEV)*PDELP(JLON,JLEV)/RG
    ZZ2=ZZ*ZIRHOGT2
    ZS3(JLON)= ZS3(JLON)+ZZ2* ZFB *(PDDOM(JLON,JLEV)-POME(JLON,JLEV))
    ZS10(JLON)=ZS10(JLON)+ZZ2*0.5_JPRB*&
                & (PDDOM(JLON,JLEV)-POME(JLON,JLEV))*&
                & (PDDOM(JLON,JLEV)+POME(JLON,JLEV))
   ZS4(JLON)=ZS4(JLON)+ZZ*ZHDMHE
  ENDDO

!     VERIFICATION OF THE PRESENCE OF AT LEAST ONE DOWNDRAFT ALONG THE
!     "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+INACT(JLON,JLEV)
  ENDDO

  IF ((ISUM == 0).AND.(ITOP == JLEV-1)) THEN
    ITOP=JLEV
  ENDIF

ENDDO ! JLEV downwards loop
!     QUITTING IN CASE OF NO DOWNDRAFT EVERYWHERE. THE "IF THEN /
!     ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA

!          FROM HERE WE WE CONTINUE ONLY IF THERE IS MATTER TO
!          ---------------------------------------------------

IF(ITOP < KLEV) THEN ! ELSE "PSEUDO RETURN"

! level KLEV: no entrainment there
DO JLON=KIDIA,KFDIA
   ZRMIX(JLON,KLEV)=0.0_JPRB
ENDDO
! ----------------------------------------------------------------------
! C.A.S. TREATMENT
! INBAS    : TRANSITION ARRAY:
!           1. IF HIGHEST LEVEL OF AN ACTIVE ZONE,
!           0. ELSEWHERE.
! ----------------------------------------------------------------------
! IF IOLD IS 0 A SINGLE ACTIVE ZONE IS ADMITTED;
! IN THAT CASE, INBAS IS 1 ONLY AT LEVEL ITOP.
! IF INACT=0 FOR THE WHOLE VERTICAL PROFILE, THE INTEGRALS WILL STAY 0.
! ----------------------------------------------------------------------

  DO JLON=KIDIA,KFDIA
    INBAS(JLON,ITOP)=MAX(INACT(JLON,ITOP),1-IOLD)
  ENDDO
  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA
      ITRAN=MAX((INACT(JLON,JLEV)-INACT(JLON,JLEV-1)),0)
      INBAS(JLON,JLEV)=ITRAN*IOLD
    ENDDO
  ENDDO

! ---------------------------------------------------------
!       RE-INTERPOLATIONS and Completion of stored values:
! ---------------------------------------------------------
!  VIa - Compute the constant mesh fraction :
! ---------------------------------------------------------
!  ACCUMULATION OF INTEGRAL QUANTITIES
!  PZFHP: total precipitation heat flux, used for closure (??)
!-------------------------------------------------------------
! UPWARDS VERTICAL LOOP
!cdir unroll=8
DO JLEV=KLEV,ITOP,-1
   DO JLON=KIDIA,KFDIA
      ZS2(JLON)=ZS2(JLON) +INACT(JLON,JLEV)*&
          &(PZFHP(JLON,JLEV-1)-PZFHP(JLON,JLEV))
   ENDDO
ENDDO
!  FINAL RESULT FOR ZSIG
DO JLON=KIDIA,KFDIA
   ZZ2=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS3(JLON)-ZEPSDAL))
   ZZ=ZS4(JLON)+ZS10(JLON)
   ZSIG(JLON)=ZZ2*MAX(0.0_JPRB,&
        &( ZSIG9(JLON)*ZZ+GDDEVF*ZTPHY*ZS2(JLON))&
        &/(ZZ-ZS3(JLON)+(1.0_JPRB-ZZ2)))
! SECURITY: ZSIG < PSIGP (computed by acupm)
   ZSIG(JLON)=MIN(ZSIG(JLON),PSIGP(JLON))
ENDDO


!  NOW STORE THE FULL LEVEL MESH FRACTION in PDDAL

!cdir unroll=8
   DO JLEV=KLEV,ITOP,-1
      DO JLON=KIDIA,KFDIA
        PDDAL(JLON,JLEV)=INACT(JLON,JLEV)*ZSIG(JLON)
      ENDDO
   ENDDO



!  PUT RELATIVE MASS FLUX AT INTERFACE LEVELS IN ZFORM
!  ---------------------------------------------------
!  sigma_d * (omega_d-omega_ed)
!            =sigma_d*(omega_d-omega_e)/(1-sigma_u-sigma_d)
! ZFORM is kept 0 at top (0) AND at the lowest interface (KLEV)
DO JLON=KIDIA,KFDIA
   ZFORM(JLON,KTDIA-1)=0.0_JPRB
   ZFORM(JLON,KLEV)=0.0_JPRB
ENDDO
ZFP1=0.0_JPRB
!cdir unroll=8
DO JLEV=KTDIA,KLEV-1
   DO JLON=KIDIA,KFDIA
      ZF=PDDAL(JLON,JLEV+1)&
       & *(PDDOM(JLON,JLEV+1)-POME(JLON,JLEV+1))
      ZFORM(JLON,JLEV)=0.5_JPRB*(ZFP1(JLON)+ZF)
      ZFP1(JLON)=ZF
   ENDDO
ENDDO
! LIMIT DECREASE TOWARDS THE GROUND 
! - leaving a non zero value at the surface
!cdir unroll=8
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
      ZFORM(JLON,JLEV)=MAX(ZFORM(JLON,JLEV),&
                         & ZFORM(JLON,JLEV-1)-GDDFXM*PDELP(JLON,JLEV))
   ENDDO
ENDDO

!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)+(ZFORM(JLON,JLEV)&
       & -ZFORM(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
       & *MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))))  
    ENDDO
  ENDDO

! *
!-------------------------------------------------------------------
!    VII - SECOND VERTICAL LOOP (UPWARDS) TO SOLVE THE IMPLICIT
!    ALGORITHM FOR PSEUDO ASCENDING ADVECTION: 
!             *** REMOVED ***
!-------------------------------------------------------------------

!-----------------------------------------------------------------
 
!     -------------------------------------------------------------------
!     IX - WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERME (KERSHAW & GREGORY SCHEME), 
!     CONSIDERING THE ACTIVE LAYERS
!*

INBAS=INBAS*INACT

!*
!    -------------------------------------------------------------------
!     IX B - DOWNWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

! TEMPORAIRES 1D.

! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.

! TEMPORAIRES 2D.
! ZA13      : UC0.
! ZA14      : VC0.
! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.
! ZRMIX     : ENTRAINMENT COEFFICIENT.
! ZUM       : AVERAGE U VALUE FROM THE DOWNDRAFT BASE.
! ZVM       : AVERAGE V VALUE FROM THE DOWNDRAFT BASE.
ZA13=0.0_JPRB
ZA14=0.0_JPRB
ZBET=0.0_JPRB
ZUM=0.0_JPRB
ZVM=0.0_JPRB

  DO JLON=KIDIA,KFDIA
    ZBET(JLON,ITOP)=INBAS(JLON,ITOP)
    ZUM(JLON,ITOP)=PU(JLON,ITOP)
    ZVM(JLON,ITOP)=PV(JLON,ITOP)
! BASE VELOCITY = ENVIRONMENT VELOCITY
       ZA13(JLON,ITOP)=PU(JLON,ITOP)
       ZA14(JLON,ITOP)=PV(JLON,ITOP)
  ENDDO

!     ALL VERTICAL INTEGRALS ZS ARE RESET TO ZERO WHEN PASSING INTO
!     AN INACTIVE LAYER, WHILE THE OUTPUT ARRAYS KEEP THEIR VALUE
!     UNTIL THE BEGINNING OF A NEW SEGMENT.

  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA


! Net K coefficient: the full one, here 
! (split implicit is for the expression of the tendency ->fluxes)

! ZBAS et ZNBA SONT NULS DANS LES COUCHES INACTIVES
! ZNBA EST NUL AUSSI A LA BASE
! ZBAS EST NON NUL SEULEMENT A LA BASE
      ZBAS=INBAS(JLON,JLEV)
      ZNBA=(1-INBAS(JLON,JLEV))*INACT(JLON,JLEV)

! ? Is this treatment of  ZMIX  Obsolete ?
! if zmix=0, ZBET stays 1 beyond the cloud base !
      ZMIX=MAX(ZEPSMIX,ZRMIX(JLON,JLEV-1))
!-  ZMIX=ZMIX/(1.0_JPRB+ZMIX) : done above

      ZBET(JLON,JLEV)=ZBET(JLON,JLEV-1)*(1.0_JPRB-ZMIX)*ZNBA+ZBAS

!     ZUM AND ZVM RESET TO PU(JLEV-1), PV AT FIRST ACTIVE LAYER
!     (SO CALLED B-1),I.E. VALUE OF ZUM IN THE LAYER BELOW LEVEL
!     WHERE ZBET=1 IS PU AT THE LEVEL ZBET=1.
!     ZBET IN THIS LOOP SHOULD NEVER REACH 1, MAX VALUE IS 1-ZMIX <1.

      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV-1)&
       &+(ZMIX*(PU(JLON,JLEV-1)-ZUM(JLON,JLEV-1))&
       &+TDDGP*(PU(JLON,JLEV)-PU(JLON,JLEV-1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS))*ZNBA&
       &+ (1.0_JPRB-ZNBA)*PU(JLON,JLEV)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV-1)&
       &+(ZMIX*(PV(JLON,JLEV-1)-ZVM(JLON,JLEV-1))&
       &+TDDGP*(PV(JLON,JLEV)-PV(JLON,JLEV-1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS))*ZNBA&
       &+ (1.0_JPRB-ZNBA)*PV(JLON,JLEV)

! BASE VELOCITY = ENVIRONMENT VELOCITY
!      ZA13=UC0, ZA14=VC0
!      DANS LES COUCHES INACTIVES, MULTIPLIE PAR ZBET=0

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV-1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV-1)*ZNBA+PV(JLON,JLEV)*ZBAS
    ENDDO
  ENDDO
! NOW STORE THE DOWNDRAUGHT MOMENTUM PROFILE IN ZUM, ZVM
  DO JLEV=KTDIA,ITOP-1
    DO JLON=KIDIA,KFDIA
       ZUM(JLON,JLEV)=PU(JLON,JLEV)
       ZVM(JLON,JLEV)=PV(JLON,JLEV)
    ENDDO   
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
       ZZ=1.0_JPRB-ZBET(JLON,JLEV)
       ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
       ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
    ENDDO   
  ENDDO

!*
! ------------------------------------------------------------------
!     X - FLUX COMPUTATION 


!     FINAL COMPUTATION OF FLUXES WITH SECURITY AGAINST NEGATIVE
!     RESULTING WATER CONTENTS

!! New computation of the evaporation fluxes: the old one is comented
  PFESL(:,KLEV)=0._JPRB
  PFESN(:,KLEV)=0._JPRB

  IF (LGRAPRO) THEN
    PFESG(:,KLEV)=0._JPRB
    DO JLON=KIDIA,KFDIA
      ZFMDQE(JLON)=MAX(0.0_JPRB,ZFORM(JLON,KLEV-1)*ZDQCA(JLON,KLEV-1)&
       & *ZGDTI*0.5_JPRB)
      ZER(JLON)=ZFMDQE(JLON)*PRDELP(JLON,KLEV)
! Partition of the evaporation rate between snow and rain
      ZDELTA=ZGRP(JLON,KLEV)
      ZZDQG=ZER(JLON)*ZDELTA
      ZDELTA=ZSNP(JLON,KLEV)
      ZZDQS=ZER(JLON)*ZDELTA-ZZDQG
      ZZDQR=ZER(JLON)-ZZDQS-ZZDQG
      PFESL(JLON,KLEV-1)=-ZZDQR*PDELP(JLON,KLEV)
      PFESN(JLON,KLEV-1)=-ZZDQS*PDELP(JLON,KLEV)
      PFESG(JLON,KLEV-1)=-ZZDQG*PDELP(JLON,KLEV)
    ENDDO

!cdir unroll=8
    DO JLEV=KLEV-1,ITOP+1,-1
      DO JLON=KIDIA,KFDIA
        ZFMDQEL=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)*ZDQCA(JLON,JLEV-1)&
         & *ZGDTI*0.5_JPRB)
        ZER(JLON)=(ZFMDQE(JLON)+ZFMDQEL)*PRDELP(JLON,JLEV)
        ZFMDQE(JLON)=ZFMDQEL
! Partition of the evaporation rate between snow and rain      
        ZDELTA=ZGRP(JLON,JLEV)
        ZZDQG=ZER(JLON)*ZDELTA
        ZDELTA=ZSNP(JLON,JLEV)
        ZZDQS=ZER(JLON)*ZDELTA-ZZDQG
        ZZDQR=ZER(JLON)-ZZDQS-ZZDQG
        PFESL(JLON,JLEV-1)=PFESL(JLON,JLEV) - ZZDQR*PDELP(JLON,JLEV)
        PFESN(JLON,JLEV-1)=PFESN(JLON,JLEV) - ZZDQS*PDELP(JLON,JLEV)
        PFESG(JLON,JLEV-1)=PFESG(JLON,JLEV) - ZZDQG*PDELP(JLON,JLEV)
      ENDDO
    ENDDO

    DO JLON=KIDIA,KFDIA
      ZER(JLON)=ZFMDQE(JLON)*PRDELP(JLON,ITOP)
! Partition of the evaporation rate between snow and rain      
      ZDELTA=ZGRP(JLON,ITOP)
      ZZDQG=ZER(JLON)*ZDELTA
      ZDELTA=ZSNP(JLON,ITOP)
      ZZDQS=ZER(JLON)*ZDELTA-ZZDQG
      ZZDQR=ZER(JLON)-ZZDQS-ZZDQG
      PFESL(JLON,ITOP-1)=PFESL(JLON,ITOP) - ZZDQR*PDELP(JLON,ITOP)
      PFESN(JLON,ITOP-1)=PFESN(JLON,ITOP) - ZZDQS*PDELP(JLON,ITOP)
      PFESG(JLON,ITOP-1)=PFESG(JLON,ITOP) - ZZDQG*PDELP(JLON,ITOP)
    ENDDO

    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        PFESL(JLON,JLEV)=PFESL(JLON,JLEV)-PFESL(JLON,ITOP-1)
        PFESN(JLON,JLEV)=PFESN(JLON,JLEV)-PFESN(JLON,ITOP-1)
        PFESG(JLON,JLEV)=PFESG(JLON,JLEV)-PFESG(JLON,ITOP-1)
        ZFPE(JLON)=PFESL(JLON,JLEV)+PFESN(JLON,JLEV)+PFESG(JLON,JLEV)
        ZPER=MAX(ZFP(JLON,JLEV),ZEPSP)/MAX(ZFPE(JLON),ZEPSP)
        ZALPHA(JLON)=MIN(ZALPHA(JLON),ZPER)
      ENDDO
    ENDDO

  ELSE

    DO JLON=KIDIA,KFDIA
      ZFMDQE(JLON)=MAX(0.0_JPRB,ZFORM(JLON,KLEV-1)*ZDQCA(JLON,KLEV-1)&
     & *ZGDTI*0.5_JPRB)
      ZER(JLON)=ZFMDQE(JLON)*PRDELP(JLON,KLEV)
  ! Partition of the evaporation rate between snow and rain
      ZDELTA=ZSNP(JLON,KLEV)
      ZZDQS=ZER(JLON)*ZDELTA
      ZZDQR=ZER(JLON)-ZZDQS
      PFESL(JLON,KLEV-1)=-ZZDQR*PDELP(JLON,KLEV)
      PFESN(JLON,KLEV-1)=-ZZDQS*PDELP(JLON,KLEV)
    ENDDO

  !cdir unroll=8
    DO JLEV=KLEV-1,ITOP+1,-1
      DO JLON=KIDIA,KFDIA
        ZFMDQEL=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)*ZDQCA(JLON,JLEV-1)&
         & *ZGDTI*0.5_JPRB)
        ZER(JLON)=(ZFMDQE(JLON)+ZFMDQEL)*PRDELP(JLON,JLEV)
        ZFMDQE(JLON)=ZFMDQEL
   ! Partition of the evaporation rate between snow and rain      
        ZDELTA=ZSNP(JLON,JLEV)
        ZZDQS=ZER(JLON)*ZDELTA
        ZZDQR=ZER(JLON)-ZZDQS
        PFESL(JLON,JLEV-1)=PFESL(JLON,JLEV) - ZZDQR*PDELP(JLON,JLEV)
        PFESN(JLON,JLEV-1)=PFESN(JLON,JLEV) - ZZDQS*PDELP(JLON,JLEV)
      ENDDO
    ENDDO

    DO JLON=KIDIA,KFDIA
      ZER(JLON)=ZFMDQE(JLON)*PRDELP(JLON,ITOP)
   ! Partition of the evaporation rate between snow and rain      
      ZDELTA=ZSNP(JLON,ITOP)
      ZZDQS=ZER(JLON)*ZDELTA
      ZZDQR=ZER(JLON)-ZZDQS
      PFESL(JLON,ITOP-1)=PFESL(JLON,ITOP) - ZZDQR*PDELP(JLON,ITOP)
      PFESN(JLON,ITOP-1)=PFESN(JLON,ITOP) - ZZDQS*PDELP(JLON,ITOP)
    ENDDO

    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        PFESL(JLON,JLEV)=PFESL(JLON,JLEV)-PFESL(JLON,ITOP-1)
        PFESN(JLON,JLEV)=PFESN(JLON,JLEV)-PFESN(JLON,ITOP-1)
        ZFPE(JLON)=PFESL(JLON,JLEV)+PFESN(JLON,JLEV)
        ZPER=MAX(ZFP(JLON,JLEV),ZEPSP)/MAX(ZFPE(JLON),ZEPSP)
        ZALPHA(JLON)=MIN(ZALPHA(JLON),ZPER)
      ENDDO
    ENDDO
  ENDIF


  DO JLON=KIDIA,KFDIA
    ZALPHA(JLON)=MIN(1._JPRB,ZALPHA(JLON))   
  ENDDO
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PDDAL(JLON,JLEV)=PDDAL(JLON,JLEV)*ZALPHA(JLON)
      ZFORM(JLON,JLEV)=ZFORM(JLON,JLEV)*ZALPHA(JLON)
      PFESL(JLON,JLEV)=PFESL(JLON,JLEV)*ZALPHA(JLON)
      PFESN(JLON,JLEV)=PFESN(JLON,JLEV)*ZALPHA(JLON)
    ENDDO
  ENDDO
  IF (LGRAPRO) THEN
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        PFESG(JLON,JLEV)=PFESG(JLON,JLEV)*ZALPHA(JLON)
      ENDDO
    ENDDO
    PFESG(:,ITOP-1)=0._JPRB
  ENDIF

  PFESL(:,ITOP-1)=0._JPRB
  PFESN(:,ITOP-1)=0._JPRB
      
                               
!*   ----------------------------
!    DOWNDRAUGHT DIFFUSION FLUXES
!*   ----------------------------

  ZS2=0.0_JPRB
  ZS3=0.0_JPRB
  ZS10=0.0_JPRB
  ZS11=0.0_JPRB
  ZS12=0.0_JPRB
  ZS13=0.0_JPRB


! implicit calculation: requires accumulating the fluxes 
! hence top to bottom.
! replace the downdraught values by the difference (psi-psi_d)
! assume no condensate inside the dd.

  ZQDN(KIDIA:KFDIA,:)=ZQDN(KIDIA:KFDIA,:)-PQ(KIDIA:KFDIA,:)
  ZSDN(KIDIA:KFDIA,:)=ZSDN(KIDIA:KFDIA,:)-ZDSE(KIDIA:KFDIA,:)
  ZUM(KIDIA:KFDIA,:)=ZUM(KIDIA:KFDIA,:)-PU(KIDIA:KFDIA,:)
  ZVM(KIDIA:KFDIA,:)=ZVM(KIDIA:KFDIA,:)-PV(KIDIA:KFDIA,:)

! Above itop: all fluxes are zero
! Level klev: ZFORM=0 there, hence all the fluxes are zero, too.

!cdir unroll=8
  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
      ZAUX=ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV+1)+ZFORM(JLON,JLEV))
      ZDPSGDT=PDELP(JLON,JLEV+1)*ZGDTI*0.5_JPRB
      PDIFCQD(JLON,JLEV)=ZAUX*( PDIFCQD(JLON,JLEV+1)+&
       & ZDPSGDT*(ZQDN(JLON,JLEV)+ZQDN(JLON,JLEV+1)) )
! Beware: condensate assumed zero inside the dd !!
      PDIFCQLD(JLON,JLEV)=ZAUX*( PDIFCQLD(JLON,JLEV+1)-&
       & ZDPSGDT*(PQL(JLON,JLEV)+PQL(JLON,JLEV+1)) )
      PDIFCQID(JLON,JLEV)=ZAUX*( PDIFCQID(JLON,JLEV+1)-&
       & ZDPSGDT*(PQI(JLON,JLEV)+PQI(JLON,JLEV+1)) )
      PDIFCSD(JLON,JLEV)=ZAUX*( PDIFCSD(JLON,JLEV+1)+&
       & ZDPSGDT*(ZSDN(JLON,JLEV)+ZSDN(JLON,JLEV+1)) )
      PSTRCUD(JLON,JLEV)=ZAUX*( PSTRCUD(JLON,JLEV+1)+&
       & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
      PSTRCVD(JLON,JLEV)=ZAUX*( PSTRCVD(JLON,JLEV+1)+&
       & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
    ENDDO
  ENDDO

! FINISH THE INND BUSINESS (BUT IRRELEVANT: INND is 1)
! (before evaporation flux calculation, because setting 
!  the increments to zero also forces the flux)

  IF (LLBO) INND=0 ! Open loop of DD

  DO JLEV=KTDIA,KLEV
     DO JLON=KIDIA,KFDIA
      PDDOM(JLON,JLEV)=INND(JLON)*PDDOM(JLON,JLEV)*ZIDT
      PDDAL(JLON,JLEV)=INND(JLON)*PDDAL(JLON,JLEV)
      ZFEVP(JLON,JLEV)=INND(JLON)*ZFEVP(JLON,JLEV)
      PDIFCQD(JLON,JLEV)=INND(JLON)*PDIFCQD(JLON,JLEV)
      PDIFCSD(JLON,JLEV)=INND(JLON)*PDIFCSD(JLON,JLEV)
      PSTRCUD(JLON,JLEV)=PSTRCUD(JLON,JLEV)*INND(JLON)
      PSTRCVD(JLON,JLEV)=PSTRCVD(JLON,JLEV)*INND(JLON)
      PDIFCQLD(JLON,JLEV)=IDIFCQCD*INND(JLON)*PDIFCQLD(JLON,JLEV)
      PDIFCQID(JLON,JLEV)=IDIFCQCD*INND(JLON)*PDIFCQID(JLON,JLEV)
     ENDDO
 ENDDO

ELSE ! pseudo return: BUT beware to the prognostic variables !
  PDDOM(KIDIA:KFDIA,:)=0.0_JPRB
  PDDAL(KIDIA:KFDIA,:)=0.0_JPRB
ENDIF !itop pseudo return

! -----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACMODO',1,ZHOOK_HANDLE)
END SUBROUTINE ACMODO

