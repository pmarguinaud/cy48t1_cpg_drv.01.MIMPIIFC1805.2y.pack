!OPTIONS XOPT(NOEVAL)
!OPTION! -O nochg
SUBROUTINE ACMRIP(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,KSTEP,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PQ,PQL,PQI,PCP,&
 & PALPH,PLNPR,PQSAT,PQW,PTW,&
 & PR,PT,PU,PV,&
 & PDIFTQ,PDIFTS,PSHEAR, &
 & PTKE,PTTE, &
 ! - INPUT  1D .
 & PTS,PQSH,PRS,PCPS,PRTI,PGZ0, &
 ! - INPUT  LOGIQUE .
 & LDCLS,&
 ! - OUTPUT 2D .
 & PMRIPP,PMRIFPP,&
 & PNEBCVPP,PNEBQ,PNBVNO,&
 & PFMTKE,PFTTKE,PF_EPS,PFUN_TTE,&
 & PAUTKE,PATTKE,&
 & PFHORM,PFHORH,&
 & PTH_FUN,PWW_FUN,&
 & PMRIMC,PMRICTERM,PMN2PP,&
 & PMN2_ES,PMN2_EQ,PMN2_DS,PMN2_DQ,PFMGST)

!**** *ACMRIP*  moist Ri' computation
!        *CALL* *ACMRIP*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KSTEP      : NUMERO DU PAS DE TEMPS.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQL        : EAU LIQUIDE
! PQI        : GLACE
! PCP        : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQW        : WET BULB SPECIFIC MOISTURE
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PTKE       : TKE
! PTTE       : TTE
! PTW        : WET BULB TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! - 1D (PROGNOSTIQUE) .

! PTS        : TEMPERATURE DE SURFACE.
! PQSH       : PSEUDO-HISTORICAL ARRAY FOR SURFACE MOISTURE
! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PRS        : CONSTANTE DES GAZ POUR L'AIR AU SOL.
! PRTI       : INVERSE DE R*T.
! - LOGIQUES .

! LDCLS      : DETERMINATION COMPLETE DES CARACTERISTIQUES DE SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------
! PMRIPP     : MOIST Ri '
! PMRIFPP    : MOIST Rif '
! PFMTKE     : STABILITY FUNCTION F_m
! PFTTKE     : STABILITY FUNCTION F_h
! PAUTKE     : alpha_u for dry AF scheme
! PATTKE     : alpha_theta for dry AF scheme
! PWW_FUN    : F_ww - stability functions for TOMs par.
! PTH_FUN    : T_h - stability functions for TOMs par.
! PMRIMC     : M(C) from P. Marquet's moist Ri computation - for TKE correction after TOMs
! PMRICTERM  : Rv/R.F(C)-1/M(C).T/Tv from P. Marquet's moist Ri computation - for TKE correction after TOMs
! PFHORM     : STABILITY FUNCTION FOR HORIZONTAL DIFFUSION - MOMENTUM
! PFHORH     : STABILITY FUNCTION FOR HORIZONTAL DIFFUSION - HEAT
! PFMGST     : STABILITY FUNCTION F_m FOR MOIST GUSTINESS CORRECTION
!              (PASSED TO ACMIXELEN)
!---------------------------------------------------------------


! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY1/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2012-07, I. Bastak-Duran

!     Modifications.
!     --------------
!      F. Vana 27-Jan-2012 : Phased to CY38
!      I. Bastak-Duran 19-Jan-2013 : Computation of moist Ri at the surface
!                                    and correction.
!      R. Brozkova, June-2014 : major rewriting.
!      I. Bastak-Duran, Oct-2014: corrections of Newton loop for SCQ and at the surface.
!      R. Brozkova, Apr-2016: mass flux option for non precipitating convection.
!      R. Brozkova, Sep-2017: fixed calculation of moist Richardson number at
!                             lowest model level in case of mass flux scheme.
!      R. Brozkova, May-2018: corrections in the mass flux option.
!      J. Masek, Oct-2020: Cleaned LCOEFK_RIH, introduced ACRI2PR, ACRIF2PR.
!                          Fixed bug in Rif(TKE, TTE), improved protections.
!      M. Hrastinski, Oct-2020: Moist gustiness correction moved to ACMIXELEN
!                               (stability function passed from here).
! End modifications
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RKAPPA    ,&
 & RV       ,RCPD     ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD
USE YOMQNSE  , ONLY :CHIHQNSE_S1,CHIHQNSE_S2,CHIHQNSE_S3,CHIHQNSE_S4, &
 & CHIHQNSE_U1,CHIHQNSE_U2,CHIHQNSE_U3,CHIHQNSE_U4, &
 & PHIHQNSE_S1,PHIHQNSE_S2,PHIHQNSE_S3,PHIHQNSE_S4,PHIHQNSE_S5,PHIHQNSE_S6, &
 & PHIHQNSE_U1,PHIHQNSE_U2,PHIHQNSE_U3,PHIHQNSE_U4,&
 & RQNSE_S0,RQNSE_GS1,RQNSE_GS2,RQNSE_GS3,RQNSE_GS4,&
 & RQNSE_GU1,RQNSE_GU2
 
IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTW      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTKE     (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTTE     (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSHEAR  (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTS  (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTQ  (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS      (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSH     (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRS      (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCPS     (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRTI     (KLON) 
LOGICAL           ,INTENT(IN)    :: LDCLS 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0     (KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMRIPP   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMRIFPP   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMN2PP   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMN2_ES   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMN2_EQ   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMN2_DS (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMN2_DQ (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFMTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFTTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAUTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PATTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PF_EPS   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFUN_TTE   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PWW_FUN   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTH_FUN   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEBCVPP (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEBQ (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNBVNO   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMRIMC  (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMRICTERM  (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHORM   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHORH   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFMGST   (KLON,0:KLEV)
!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZRTI(KLON,0:KLEV),ZDPHIA(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZCISA(KLON,0:KLEV),ZQTOTA(KLON,KLEV),ZSLAMA(KLON,KLEV)
REAL(KIND=JPRB) :: ZPHI3TA(KLON,0:KLEV),ZCHI3TA(KLON,0:KLEV),ZPHIQTA(KLON,0:KLEV)

INTEGER(KIND=JPIM) :: JLEV, JLON, ITTYPE, JITER
INTEGER(KIND=JPIM) :: INITER

LOGICAL :: LLAFGD, LL3DTURB, LLQ(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZDPHI, ZGLU, ZZ, ZC0,ZC1
REAL(KIND=JPRB) :: ZEPS, ZQTOT, ZQTOTI
REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZRITKE,ZRIFTKE,ZSTA,ZRITKE_F(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZCHI3TKE,ZFSFTKE
REAL(KIND=JPRB) :: ZFMTKE_DX(KLON,0:KLEV),ZFHTKE_DX(KLON,0:KLEV),&
 & ZFMTKE(KLON,0:KLEV),ZFTTKE(KLON,0:KLEV),ZSIG_AF(KLON,0:KLEV),&
 & ZFFTKE_DX(KLON,0:KLEV),ZFFTKE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDCHI3TKE(KLON,0:KLEV), ZDPHI3TKE(KLON,0:KLEV),&
 & ZDRIFTKE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDCHI3TKE_U, ZDPHI3TKE_U,ZDRIFTKE_U,ZDRRI_U,ZDPRI_U
REAL(KIND=JPRB) :: Z_ETKE_R,Z_ETKE_P
REAL(KIND=JPRB) :: ZETKE_R(KLON,0:KLEV),ZETKE_P(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZAU,ZAT

REAL(KIND=JPRB) :: ZS_FC, ZS_MC
REAL(KIND=JPRB) :: ZS_LV,&
 & ZS_LS, ZS_TS, ZS_L,&
 & ZS_Q,ZS_QT,&
 & ZS_EV,ZS_ES,ZS_E,&
 & ZS_N2_0,ZS_N2_1
REAL(KIND=JPRB) :: ZLAMCP,ZS_T,ZS_DLEV,ZS_DLES,ZS_DLE,ZS_RV
REAL(KIND=JPRB) :: ZS_DC(KLON,0:KLEV),ZS_NX4(KLON,0:KLEV),ZS_B3X(KLON,0:KLEV),&
 & ZS_A1(KLON,0:KLEV),ZS_AR(KLON,0:KLEV),ZS_X0(KLON,0:KLEV),ZS_X1(KLON,0:KLEV),&
 & ZS_X2(KLON,0:KLEV),ZS_X3(KLON,0:KLEV),ZFCN(KLON,0:KLEV),ZS_N2(KLON,0:KLEV),&
 & ZS_NSAT(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZRSTRIS(KLON,0:KLEV),ZQUUDEF(KLON,KLEV),ZQBOXDEF(KLON,KLEV)
REAL(KIND=JPRB) :: ZDQL,ZRSB(KLON),ZNSD(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZQLDN(KLON,KLEV)
REAL(KIND=JPRB) :: ZINCL1,ZRSH,ZCLEAR,ZWDEPTH
REAL(KIND=JPRB) :: ZSIGSTAB(KLON)
REAL(KIND=JPRB) :: ZNTOP(KLON),ZCLEAL(KLON),ZINCL(KLON),ZCOUNT(KLON)

REAL(KIND=JPRB) :: ZCP,ZUMCP
REAL(KIND=JPRB) :: ZRAT,ZRAT_MIN,ZRAT_MAX,ZRIF_MIN,ZRIF_MAX,ZWEIGHT,ZAA
REAL(KIND=JPRB) :: ZMRIFPP,ZMRIFPPI
REAL(KIND=JPRB) :: ZTKE_LAM5T,Z2AZ0,Z2THIRD
REAL(KIND=JPRB) :: ZDRRI,ZDPRI,ZEFB_UP
REAL(KIND=JPRB) :: ZCN,ZCN_D,ZN2DM,ZN2DMDC,ZAUX,ZFQ,ZFQ1,ZFQ2
REAL(KIND=JPRB) :: ZEPS4,ZEPS8,ZSIG

#include "acri2pr.intfb.h"
#include "acrif2pr.intfb.h"
#include "acscctr.intfb.h"
#include "fcttrm.func.h"

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACMRIP',0,ZHOOK_HANDLE)
ASSOCIATE(VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, ENT_LAMBDA=>YDML_PHY_MF%YRPHY0%ENT_LAMBDA, &
 & ETKE_LAM4=>YDML_PHY_MF%YRPHY0%ETKE_LAM4, ETKE_LAM1=>YDML_PHY_MF%YRPHY0%ETKE_LAM1, &
 & ETKE_LAM0=>YDML_PHY_MF%YRPHY0%ETKE_LAM0, ETKE_LAM3=>YDML_PHY_MF%YRPHY0%ETKE_LAM3, &
 & ETKE_LAM2=>YDML_PHY_MF%YRPHY0%ETKE_LAM2, EFB_AZ0=>YDML_PHY_MF%YRPHY0%EFB_AZ0, &
 & ETKE_OLAM=>YDML_PHY_MF%YRPHY0%ETKE_OLAM, EFB_UR=>YDML_PHY_MF%YRPHY0%EFB_UR, ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, &
 & REFB_1=>YDML_PHY_MF%YRPHY0%REFB_1, REFB_2=>YDML_PHY_MF%YRPHY0%REFB_2, REFB_3=>YDML_PHY_MF%YRPHY0%REFB_3, &
 & GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, C3TKEFREE=>YDML_PHY_MF%YRPHY0%C3TKEFREE, &
 & NUPTKE=>YDML_PHY_MF%YRPHY0%NUPTKE, C_EPSILON=>YDML_PHY_MF%YRPHY0%C_EPSILON, &
 & ETKE_R=>YDML_PHY_MF%YRPHY0%ETKE_R, &
 & ETKE_RIFC=>YDML_PHY_MF%YRPHY0%ETKE_RIFC, &
 & ETKE_MIN=>YDML_PHY_MF%YRPHY0%ETKE_MIN, &
 & ETKE_CRIT=>YDML_PHY_MF%YRPHY0%ETKE_CRIT, &
 & XMULAF=>YDML_PHY_MF%YRPHY2%XMULAF, &
 & RLOUIS_S0=>YDML_PHY_MF%YRLOUIS%RLOUIS_S0, RLOUIS_GS4=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS4, &
 & RLOUIS_GS3=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS3, RLOUIS_GS2=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS2, &
 & RLOUIS_GS1=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS1, PLOUIS_S0=>YDML_PHY_MF%YRLOUIS%PLOUIS_S0, &
 & RLOUIS_GU1=>YDML_PHY_MF%YRLOUIS%RLOUIS_GU1, RLOUIS_GU2=>YDML_PHY_MF%YRLOUIS%RLOUIS_GU2, &
 & PLOUIS_GU2=>YDML_PHY_MF%YRLOUIS%PLOUIS_GU2, PLOUIS_GU1=>YDML_PHY_MF%YRLOUIS%PLOUIS_GU1, &
 & PLOUIS_GS1=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS1, PLOUIS_GS3=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS3, &
 & PLOUIS_GS2=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS2, PLOUIS_GS4=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS4, &
 & LCOEFK_RIS=>YDML_PHY_MF%YRPHY%LCOEFK_RIS, L3MT=>YDML_PHY_MF%YRPHY%L3MT, LCOEFK_FLX=>YDML_PHY_MF%YRPHY%LCOEFK_FLX, &
 & CGTURS=>YDML_PHY_MF%YRPHY%CGTURS, LCOEFK_PTTE=>YDML_PHY_MF%YRPHY%LCOEFK_PTTE, &
 & LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO, &
 & LCOEFK_F1=>YDML_PHY_MF%YRPHY%LCOEFK_F1, &
 & LCOEFK_SCQ=>YDML_PHY_MF%YRPHY%LCOEFK_SCQ,LCOEFK_MSC=>YDML_PHY_MF%YRPHY%LCOEFK_MSC)
!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS 

LLAFGD=XMULAF /= 0.0_JPRB
LL3DTURB=.FALSE.

LLQ(1:KLON,0:KLEV)=.FALSE.
ZGLU=RG*ALMAV

Z2AZ0=2.0_JPRB/3.0_JPRB*(1.0_JPRB-3.0_JPRB*ETKE_LAM3-ETKE_LAM2)

ZCP=ETKE_OLAM/ETKE_LAM0/C3TKEFREE
ZUMCP=1.0_JPRB-ZCP
Z2THIRD=2.0_JPRB/3.0_JPRB

ZLAMCP=RCPV/RCPD-1.0_JPRB
ZC0=RCPD-RCPV
ZC1=RCPD-RCPV+ENT_LAMBDA*RCPD
! security constant
ZEPS=EPSILON(1.0_JPRB)*100.0_JPRB
ZEPS4=1E-4_JPRB
ZEPS8=1E-8_JPRB

! minimum Rif
ZRIF_MIN=-1000._JPRB
ZRIF_MAX=0.999_JPRB*ETKE_RIFC

! minimum TTE/TKE ratio in Rif diagnostics
ZRAT_MIN=(1._JPRB-ZUMCP*ZRIF_MIN)/(1._JPRB-ZRIF_MIN)
ZRAT_MAX=(1._JPRB-ZUMCP*ZRIF_MAX)/(1._JPRB-ZRIF_MAX)

ZAA=(ETKE_CRIT-ETKE_MIN)*(ETKE_CRIT-ETKE_MIN)

ZQTOT=0.0_JPRB
ZQTOTI=0.0_JPRB

!full levels
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZQTOTA(JLON,JLEV)=PQ(JLON,JLEV)+PQL(JLON,JLEV)+PQI(JLON,JLEV)
    ZSLAMA(JLON,JLEV)=RCPD*PT(JLON,JLEV)*(1.0_JPRB+ZQTOTA(JLON,JLEV)*ZLAMCP)+ &
     & PAPHIF(JLON,JLEV)-&
     & (PQL(JLON,JLEV)*RLV(PT(JLON,JLEV))+PQI(JLON,JLEV)*RLS(PT(JLON,JLEV)))
  ENDDO
ENDDO

IF     (TRIM(CGTURS) == 'RMC') THEN
  ITTYPE=1
ELSEIF (TRIM(CGTURS) == 'MD1') THEN
  ITTYPE=2
ELSEIF (TRIM(CGTURS) == 'MD2') THEN
  ITTYPE=3
ELSEIF (TRIM(CGTURS) == 'QNSE') THEN
  ITTYPE=4
ELSEIF (TRIM(CGTURS) == 'EFB') THEN
  ITTYPE=5
ELSEIF (TRIM(CGTURS) == 'LOUIS') THEN
  ITTYPE=6
ENDIF

! Number of the SCC solver iterations
INITER=3

!*
!     ------------------------------------------------------------------
!     II - SURFACE CALCULATION OF RICHARDSON NUMBER

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA
  ! ZDPHIA    : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
  !           : GEOPOTENTIAL THICKNESS OF THE SURFACE LEVEL.
  ZDPHI=PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)
  ZDPHIA(JLON,KLEV)=ZDPHI

  ZCISA(JLON,KLEV)=PU(JLON,KLEV)**2+PV(JLON,KLEV)**2+ &
   & (GCISMIN*ZDPHI/RG)**2

  ! ZSTA      : APPROXIMATION DE DELTA(PHI)*DELTA(LN(THETA)).
  !            : APPROXIMATION OF DELTA(PHI)*DELTA(LN(THETA)).
  ZSTA=ZDPHI*(PR(JLON,KLEV)*PT(JLON,KLEV)+RKAPPA &
   & *ZDPHI-PRS(JLON)*PTS(JLON))*PRTI(JLON)

  ! Ri_dry = Ri*,Ri** at surface
  PMRIPP(JLON,KLEV)=ZSTA/ZCISA(JLON,KLEV)
ENDDO

DO JLON=KIDIA,KFDIA
  ! protection against division by 0
  PMRIPP(JLON,KLEV)=SIGN(MAX(ZEPS, &
   & ABS(PMRIPP(JLON,KLEV))),PMRIPP(JLON,KLEV))

  ZDPHI=ZDPHIA(JLON,KLEV)
  PMN2_ES(JLON,KLEV)=RG/ &
   & (PT(JLON,KLEV)*PCP(JLON,KLEV)+ &
   & ZDPHI+PCPS(JLON)*PTS(JLON))*2.0_JPRB
  PMN2_EQ(JLON,KLEV)=RG*(ZC0/PCP(JLON,KLEV) &
   & +2.0_JPRB*(RV-RD)/(PR(JLON,KLEV)+PRS(JLON)))

  PMN2_DS(JLON,KLEV)=RG/ZDPHI*(PT(JLON,KLEV)*PCP(JLON,KLEV)+ &
   & ZDPHI-PCPS(JLON)*PTS(JLON))
  PMN2_DQ(JLON,KLEV)=RG/ZDPHI* &
   & (PQ(JLON,KLEV)-PQSH(JLON))

  PNEBCVPP(JLON,KLEV)=0.0_JPRB
  PNEBQ(JLON,KLEV)=0.0_JPRB
  PMRIMC(JLON,KLEV)=1.0_JPRB
  PMRICTERM(JLON,KLEV)=1.0_JPRB
  PMRIMC(JLON,KTDIA-1)=1.0_JPRB
  PMRICTERM(JLON,KTDIA-1)=1.0_JPRB
ENDDO

! surface: convert Ri to P and R
! (vertical loop KTDIA:KLEV-1 is suppressed by passing KTDIA=0, KLEV=1)
CALL ACRI2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,0,1,ITTYPE,&
 & PMRIPP(1,KLEV),ZETKE_P(1,KLEV),ZETKE_R(1,KLEV))

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA

  PMN2PP(JLON,KLEV)=PMRIPP(JLON,KLEV)*(ZCISA(JLON,KLEV)/ &
   & (ZDPHIA(JLON,KLEV)/RG)**2)
  ZRITKE_F(JLON,KLEV)=PMRIPP(JLON,KLEV)

  ZRITKE  =PMRIPP (JLON,KLEV)
  Z_ETKE_R=ZETKE_R(JLON,KLEV)
  Z_ETKE_P=ZETKE_P(JLON,KLEV)

  ! Ri -> Rif surface
  PMRIFPP(JLON,KLEV)=-(SQRT((Z_ETKE_R*(ZRITKE*C3TKEFREE+Z_ETKE_P))**2- &
   & 4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R*Z_ETKE_P**2)- &
   & (ZRITKE*C3TKEFREE+Z_ETKE_P)*Z_ETKE_R)/(2.0_JPRB*Z_ETKE_P)

  ! security
  PMRIFPP(JLON,KLEV)=MIN(PMRIFPP(JLON,KLEV),ETKE_RIFC-ZEPS8)

ENDDO

!     CALCULS DE UN SUR G FOIS LA FREQUENCE DE BRUNT-VAISALA DIVISEE PAR
!     LA DENSITE LE TOUT AU CARRE DE MEME QUE LA DENSITE (POUR G.W.D.).
!     COMPUTATION OF ONE OVER G TIME THE BRUNT-VAISALA FREQUENCY DIVIDED
!     BY DENSITY THE WHOLE BEING SQUARED AS WELL AS DENSITY (FOR G.W.D.)
IF (LDCLS) THEN
  DO JLON=KIDIA,KFDIA
    PNBVNO(JLON,KLEV)=PMRIPP(JLON,KLEV)*ZCISA(JLON,KLEV)/(PAPRS(JLON,KLEV) &
     & *PRTI(JLON)*ZDPHIA(JLON,KLEV))**2
  ENDDO
ENDIF

!new code
!*
!     ------------------------------------------------------------------
!     III - BOUCLE PASSIVE SUR LES NIVEAUX VERTICAUX.

!           PASSIVE LOOP ON VERTICAL LEVELS.

! wind shear
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZRTI(JLON,JLEV)=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV) &
     & +PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
    ZZ=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1)) &
     & -PAPHI(JLON,KLEV)+PGZ0(JLON))
    ZDPHIA(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
    ZCISA(JLON,JLEV)=(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2 &
     & +(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2 &
     & +(GCISMIN*ZDPHIA(JLON,JLEV)/(RG*(1.0_JPRB+ZZ/ZGLU)))**2
  ENDDO
ENDDO

! preparation for SCC compuation
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA


    ZS_T =(PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB
    ZS_Q =(PQ(JLON,JLEV)+PQ(JLON,JLEV+1))*0.5_JPRB
    ZS_QT =(ZQTOTA(JLON,JLEV)+ZQTOTA(JLON,JLEV+1))*0.5_JPRB
    ZS_RV=ZS_Q/(1.0_JPRB-ZS_QT)

    ZS_TS=SIGN(0.5_JPRB,ZS_T-RTT)+0.5_JPRB

    ZS_LV=RLV(ZS_T)
    ZS_LS=RLS(ZS_T)
    ZS_L=ZS_LV*ZS_TS+(1.0_JPRB-ZS_TS)*ZS_LS

    ZS_EV=FOEW(ZS_T,0.0_JPRB)
    ZS_ES=FOEW(ZS_T,1.0_JPRB)
    ZS_E=ZS_EV*ZS_TS+(1.0_JPRB-ZS_TS)*ZS_ES

    ZS_DLEV=FODLEW(ZS_T,0.0_JPRB)
    ZS_DLES=FODLEW(ZS_T,1.0_JPRB)
    ZS_DLE=ZS_DLEV*ZS_TS+(1.0_JPRB-ZS_TS)*ZS_DLES

    ZS_DC(JLON,JLEV)=MAX(ZS_T/(PAPRS(JLON,JLEV)-ZS_E) &
     & *ZS_E*ZS_DLE,ZEPS8)

    ZS_X0(JLON,JLEV)=-ZC0/(PCP(JLON,JLEV)+PCP(JLON,JLEV+1))*2.0_JPRB
    ZS_X1(JLON,JLEV) =(PCP(JLON,JLEV)*PT(JLON,JLEV) &
     & +PCP(JLON,JLEV+1)*PT(JLON,JLEV+1))*0.5_JPRB
    ZS_X2(JLON,JLEV)=1.0_JPRB/(ZS_QT-1.0_JPRB)
    ZS_X3(JLON,JLEV)=(1.0_JPRB+ZS_RV)*RV &
     & /(RD*(1.0_JPRB-ZS_QT)+RV*ZS_Q)

    ZS_AR(JLON,JLEV)=(RV-RD)/(RD*(1.0_JPRB-ZS_QT)+RV*ZS_Q)
    ZS_A1(JLON,JLEV)=(ZSLAMA(JLON,JLEV)-ZSLAMA(JLON,JLEV+1)) &
     & /ZS_X1(JLON,JLEV)+(ZS_AR(JLON,JLEV)-ZS_X0(JLON,JLEV))* &
     & (ZQTOTA(JLON,JLEV)-ZQTOTA(JLON,JLEV+1))
    ZS_B3X(JLON,JLEV)=(ZS_AR(JLON,JLEV)-ZS_X2(JLON,JLEV) &
     & /(1.0_JPRB+ZS_DC(JLON,JLEV))) &
     & *(ZQTOTA(JLON,JLEV)-ZQTOTA(JLON,JLEV+1))

    ZS_NX4(JLON,JLEV)=MAX(ZS_L*(RD*(1.0_JPRB-ZS_QT)+RV*ZS_Q)/ &
     & (ZS_X1(JLON,JLEV)*RV)-1.0_JPRB,ZEPS8)

  ENDDO
ENDDO

! f(C_n) computation - Q^=C^f(Cn)
IF (LCOEFK_SCQ.AND.(.NOT.LCOEFK_MSC)) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ! Cn
      ZCN_D=PDIFTQ(JLON,JLEV)*ZS_NX4(JLON,JLEV) &
       & *(ZS_AR(JLON,JLEV)-ZS_X2(JLON,JLEV)/(1.0_JPRB+ZS_DC(JLON,JLEV)))
      ZCN=MIN(-(PDIFTS(JLON,JLEV)/ZS_X1(JLON,JLEV)+PDIFTQ(JLON,JLEV) &
       & *(ZS_AR(JLON,JLEV)-ZS_X0(JLON,JLEV))) &
       & /SIGN(MAX(ABS(ZCN_D),ZEPS8),ZCN_D),10.0_JPRB)
      ZFCN(JLON,JLEV)=MIN(MAX(0.5_JPRB*(SQRT((6.25_JPRB*ZCN)**2+4.0_JPRB)-6.25_JPRB*ZCN), &
       & 1E-2_JPRB),5.0_JPRB)
  
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFCN(JLON,JLEV)=1.0_JPRB
    ENDDO
  ENDDO
ENDIF

IF (LCOEFK_MSC) THEN
  !MASS FLUX BASED COMPUTATION OF SCC

  ZRSTRIS(1:KLON,0:KLEV)=0._JPRB

  DO JLON=KIDIA,KFDIA

    ZQUUDEF(JLON,KLEV)=0._JPRB
    ZQUUDEF(JLON,KTDIA)=0._JPRB
    ZQBOXDEF(JLON,KLEV)=0._JPRB
    ZQBOXDEF(JLON,KTDIA)=0._JPRB
    ZNSD(JLON,KLEV)=0._JPRB
    ZS_N2(JLON,KLEV)=0._JPRB

    ZRSB(JLON)=0._JPRB

    ZCOUNT(JLON)=0.0_JPRB
    ZNTOP(JLON)=0.0_JPRB
    ZSIGSTAB(JLON)=0._JPRB
    ZCLEAL(JLON)=0._JPRB
    ZINCL(JLON)=0._JPRB
  ENDDO

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ! cloudiness=1.0 N2'/g^2*dphi
      ZS_FC=1.0_JPRB+ZS_NX4(JLON,JLEV)
      ZS_MC=(1.0_JPRB+ZS_DC(JLON,JLEV)) &
       & /(1.0_JPRB+ZS_DC(JLON,JLEV)*ZS_FC)
      ZS_NSAT(JLON,JLEV)=ZS_MC*(ZS_A1(JLON,JLEV)+ZS_NX4(JLON,JLEV) &
       & *ZS_B3X(JLON,JLEV))

    ENDDO
  ENDDO

! shallow convection limitation
  ZWDEPTH=2500._JPRB*RG
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA

      ! ZS_NSAT is the squared saturated BV frequency*z/g
      ZCLEAR=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZS_NSAT(JLON,JLEV)-ZEPS8))
      ! ZSIG is zero only at the top of a stable layer
      ZSIG=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZCLEAR-ZCLEAL(JLON)))
      ! ZINCL1 is 1 only at the base of a stable layer
      ZINCL1=MAX(0.0_JPRB,SIGN(1.0_JPRB, &
       & ZCLEAR-ZCLEAL(JLON) -0.5_JPRB))
      ! Memorize the base of current stable layer in ZINCL (back to zero at top)
      ZINCL(JLON)=ZSIG*(MAX(ZINCL(JLON),ZINCL1*JLEV))
      ! Accumulate thickness of the stable layer
      ZCOUNT(JLON)=ZSIG*(ZCOUNT(JLON)+ZCLEAR*ZDPHIA(JLON,JLEV))
      ! zsigstab goes to 1 once the stable layer has reached zwdepth:
      ! after this do not account for any further instability above
      ZSIGSTAB(JLON)=MAX(ZSIGSTAB(JLON), &
       & MAX(0.0_JPRB,SIGN(1.0_JPRB,ZCOUNT(JLON)-ZWDEPTH )) )
      ! if a thick stable layer starts at the surface, still zntop will
      ! prevent the equivalent mass flux scheme to work
      ! i.e. this is not a 'top inversion', no shallow convection
      ! ZNTOP passes from 0 to ZINCL as soon as ZSIGSTAB=1
      ZNTOP(JLON)=MAX(ZNTOP(JLON),ZINCL(JLON)*ZSIGSTAB(JLON))
      ZCLEAL(JLON)=ZCLEAR

    ENDDO
  ENDDO

  ! compute profile
  CALL ACSCCTR(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PALPH,PAPHIF,PAPRSF,&
   & PLNPR,&
   & PQ,PQI,PQL,PQSAT,PQW,&
   & PT,PTW,PTKE,PTTE,&
   & ZNTOP,&
   & ZQLDN,ZQUUDEF)

  DO JLEV=KLEV-1,KTDIA+1,-1
    DO JLON=KIDIA,KFDIA

      ZQBOXDEF(JLON,JLEV)=ZQTOTA(JLON,JLEV)-PQSAT(JLON,JLEV)

      ZDQL=ZQUUDEF(JLON,JLEV)-ZQBOXDEF(JLON,JLEV)
      ZDQL=SIGN(MAX(ZEPS8,ABS(ZDQL)),ZDQL)

      ZRSH=0.5_JPRB*(MAX(0.0_JPRB,ZQUUDEF(JLON,JLEV))-MAX(0.0_JPRB, &
       & ZQBOXDEF(JLON,JLEV)))/ZDQL

      ZRSTRIS(JLON,JLEV)=ZRSB(JLON)+ZRSH

      ZRSB(JLON)=ZRSH

      ZRSTRIS(JLON,JLEV)= MAX(0.0_JPRB,MIN(1.0_JPRB,ZRSTRIS(JLON,JLEV) ))

    ENDDO
  ENDDO
! moist BVF - first guess of Qhat and limitations
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ZS_N2(JLON,JLEV)= ZS_A1(JLON,JLEV)*(1._JPRB-ZRSTRIS(JLON,JLEV))+ &
       & ZRSTRIS(JLON,JLEV)*ZS_NSAT(JLON,JLEV)

      ZS_N2(JLON,JLEV)=MIN(MAX(ZS_N2(JLON,JLEV) &
       & ,ZS_NSAT(JLON,JLEV)),ZS_A1(JLON,JLEV))

      ! first guess of Qhat for numerical reasons when smaller than ZEPS4
      ! must be set to zero (underflow otherwise when computing C)
      PNEBQ(JLON,JLEV)=ZRSTRIS(JLON,JLEV)
      ZSIG=MAX(0.0_JPRB,SIGN(1.0_JPRB,PNEBQ(JLON,JLEV)-ZEPS4))
      PNEBQ(JLON,JLEV)=MIN(1.0_JPRB,PNEBQ(JLON,JLEV)*ZSIG)
      ! Logical mask for non-zero Qhat
      LLQ(JLON,JLEV)=PNEBQ(JLON,JLEV) > 0.0_JPRB
    ENDDO
  ENDDO

  ! Cn computation
  IF (LCOEFK_SCQ) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZCN_D=ZS_NX4(JLON,JLEV)*ZS_B3X(JLON,JLEV)
        ZCN=MIN(-ZS_A1(JLON,JLEV)/SIGN(MAX(ABS(ZCN_D),ZEPS8),ZCN_D),10.0_JPRB)
        ZFCN(JLON,JLEV)=MIN(MAX(0.5_JPRB*(SQRT((6.25_JPRB*ZCN)**2+4.0_JPRB)-6.25_JPRB*ZCN), &
         & 1E-2_JPRB),5.0_JPRB)

        PNEBCVPP(JLON,JLEV)=PNEBQ(JLON,JLEV)**(1.0_JPRB/ZFCN(JLON,JLEV))
      ENDDO
    ENDDO

    ! Newton iteration to get Qhat and C
    DO JITER=1,INITER
      DO JLEV=KTDIA,KLEV-1
        DO JLON=KIDIA,KFDIA

          IF (LLQ(JLON,JLEV)) THEN
            ZS_FC=1.0_JPRB+PNEBCVPP(JLON,JLEV)*ZS_NX4(JLON,JLEV)
            ZS_MC=(1.0_JPRB+ZS_DC(JLON,JLEV))/(1.0_JPRB+ZS_DC(JLON,JLEV)*ZS_FC)

            ! F(Q)
            ZFQ=ZS_N2(JLON,JLEV)-ZS_MC*(ZS_A1(JLON,JLEV)+ &
             & ZS_NX4(JLON,JLEV)*ZS_B3X(JLON,JLEV)*PNEBQ(JLON,JLEV))

            ! numerator ZFQ1 and denominator ZFQ2 of -dF/dQ
            ZAUX=1.0_JPRB+ZS_DC(JLON,JLEV)* &
             & (1.0_JPRB+PNEBCVPP(JLON,JLEV)*ZS_NX4(JLON,JLEV))
            ZFQ1=(1.0_JPRB+ZS_DC(JLON,JLEV))*ZS_NX4(JLON,JLEV)* &
             & (ZFCN(JLON,JLEV)*PNEBQ(JLON,JLEV)*ZAUX*ZS_B3X(JLON,JLEV)- &
             & PNEBCVPP(JLON,JLEV)*ZS_DC(JLON,JLEV)* &
             & (ZS_A1(JLON,JLEV)+ZS_B3X(JLON,JLEV)*ZS_NX4(JLON,JLEV)* &
             & PNEBQ(JLON,JLEV)))
            ZFQ2=ZFCN(JLON,JLEV)*PNEBQ(JLON,JLEV)*ZAUX*ZAUX

             ! Newton iteration of equation F(Q)=0
             PNEBQ(JLON,JLEV)=PNEBQ(JLON,JLEV)+ &
              & ZFQ*ZFQ2/SIGN(MAX(ABS(ZFQ1),ZEPS8),ZFQ1)

             ! limitation: values smaller than ZEPS4 reset to zero
             ! to prevent underflow when evaluating power
             ZSIG=MAX(0.0_JPRB,SIGN(1.0_JPRB,PNEBQ(JLON,JLEV)-ZEPS4))
             PNEBQ(JLON,JLEV)=MIN(1.0_JPRB,PNEBQ(JLON,JLEV))*ZSIG

            ! diagnose Scc from Q
            PNEBCVPP(JLON,JLEV)=PNEBQ(JLON,JLEV)**(1.0_JPRB/ZFCN(JLON,JLEV))

          ENDIF ! end mask

        ENDDO
      ENDDO
    ENDDO ! end of iteration
  ENDIF ! end of Q_hat

ENDIF ! end of mass flux block

IF (LCOEFK_RIS) THEN
  ! Shallow convection cloudiness in case of Ri*/Ri**
  ! Q^=C also first guess for Q^=C^F(Cn)
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ! no cloudiness N2'/g^2*dphi
      ZS_N2_0=ZS_A1(JLON,JLEV)

      ! cloudiness=1.0 N2'/g^2*dphi
      ZS_FC=1.0_JPRB+ZS_NX4(JLON,JLEV)
      ZS_MC=(1.0_JPRB+ZS_DC(JLON,JLEV)) &
       & /(1.0_JPRB+ZS_DC(JLON,JLEV)*ZS_FC)
      ZS_N2_1=ZS_MC*(ZS_A1(JLON,JLEV)+ZS_NX4(JLON,JLEV) &
       & *ZS_B3X(JLON,JLEV))
      ! N2'/g^2*dphi
      ZS_N2(JLON,JLEV)=PMRIPP(JLON,JLEV) &
       & *ZCISA(JLON,JLEV)/ZDPHIA(JLON,JLEV)
      !limitation
      ZS_N2(JLON,JLEV)=MIN(MAX(ZS_N2(JLON,JLEV) &
       & ,ZS_N2_1),ZS_N2_0)

      ! Q^=C also first guess for Q^=C^f(Cn)       
      !PNEBCVPP(JLON,JLEV)=(-(ZS_DC(JLON,JLEV)+1) &
      PNEBQ(JLON,JLEV)=(-(ZS_DC(JLON,JLEV)+1) &
       & *(ZS_N2(JLON,JLEV)-ZS_A1(JLON,JLEV))) &
       & /(ZS_NX4(JLON,JLEV)*(ZS_DC(JLON,JLEV) &
       & *(ZS_N2(JLON,JLEV)-ZS_B3X(JLON,JLEV))-ZS_B3X(JLON,JLEV)))

      ! if (N2_0=N2 => SCC=0) & (N2/g^2*dphi>2e-3 => SCC=0)
      ! ZS_N2_0 is equal to ZS_A1(JLON,JLEV)
      ZSIG=(1.0_JPRB-SIGN(1.0_JPRB,ZS_N2(JLON,JLEV)-ZS_A1(JLON,JLEV)))* &
       & (1.0_JPRB-SIGN(1.0_JPRB,ZS_N2(JLON,JLEV)-2E-3_JPRB))*0.25_JPRB
      !PNEBCVPP(JLON,JLEV)=ZSIG*PNEBCVPP(JLON,JLEV)
      PNEBQ(JLON,JLEV)=ZSIG*PNEBQ(JLON,JLEV)
      ! limitation
      !PNEBCVPP(JLON,JLEV)=max(min(1.0_JPRB,PNEBCVPP(JLON,JLEV)),ZEPS4)
      PNEBQ(JLON,JLEV)=MAX(MIN(1.0_JPRB,PNEBQ(JLON,JLEV)),ZEPS4)
      PNEBCVPP(JLON,JLEV)=PNEBQ(JLON,JLEV)**(1.0_JPRB/ZFCN(JLON,JLEV))

    ENDDO
  ENDDO

  IF (LCOEFK_SCQ) THEN

    !Q^=C^F(Cn) iteration
    DO JLEV=KTDIA,KLEV-1
      DO JITER=1,INITER
        DO JLON=KIDIA,KFDIA

          ZS_FC=1.0_JPRB+PNEBCVPP(JLON,JLEV)*ZS_NX4(JLON,JLEV)
          ZS_MC=(1.0_JPRB+ZS_DC(JLON,JLEV))/(1.0_JPRB+ZS_DC(JLON,JLEV)*ZS_FC)

          !Fx(C)
          ZN2DM=ZS_N2(JLON,JLEV)-ZS_MC*(ZS_A1(JLON,JLEV) &
           & +ZS_NX4(JLON,JLEV)*ZS_B3X(JLON,JLEV)*PNEBQ(JLON,JLEV))
   
          !dervative of -Fx(C) in C
          ZN2DMDC=((ZS_DC(JLON,JLEV)+1.0_JPRB)*ZS_NX4(JLON,JLEV)*(PNEBCVPP(JLON,JLEV)* &
           & ZFCN(JLON,JLEV)*ZS_B3X(JLON,JLEV)*ZS_DC(JLON,JLEV)*ZS_NX4(JLON,JLEV)*PNEBQ(JLON,JLEV)- &
           & PNEBCVPP(JLON,JLEV)*ZS_B3X(JLON,JLEV)*ZS_DC(JLON,JLEV)*ZS_NX4(JLON,JLEV)*PNEBQ(JLON,JLEV)+ &
           & ZFCN(JLON,JLEV)*ZS_B3X(JLON,JLEV)*ZS_DC(JLON,JLEV)*PNEBQ(JLON,JLEV)+ZFCN(JLON,JLEV)* &
           & ZS_B3X(JLON,JLEV)*PNEBQ(JLON,JLEV)-PNEBCVPP(JLON,JLEV)*ZS_A1(JLON,JLEV)*ZS_DC(JLON,JLEV)))/ &
           & MAX(ZFCN(JLON,JLEV)*(PNEBCVPP(JLON,JLEV)*ZS_DC(JLON,JLEV)*ZS_NX4(JLON,JLEV)+ &
           & ZS_DC(JLON,JLEV)+1.0_JPRB)**2*PNEBQ(JLON,JLEV),ZEPS8)

          PNEBQ(JLON,JLEV)=PNEBQ(JLON,JLEV)+ZN2DM/SIGN(MAX(ABS(ZN2DMDC),ZEPS8),ZN2DMDC)

          !limitation
          PNEBQ(JLON,JLEV)=MAX(MIN(1.0_JPRB,PNEBQ(JLON,JLEV)),ZEPS4)

          PNEBCVPP(JLON,JLEV)=PNEBQ(JLON,JLEV)**(1.0_JPRB/ZFCN(JLON,JLEV))

        ENDDO
      ENDDO !end of iteration
    ENDDO
  ENDIF
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZSIG=(1.0_JPRB-SIGN(1.0_JPRB,ZS_N2(JLON,JLEV)-ZS_A1(JLON,JLEV)))* &
       & (1.0_JPRB-SIGN(1.0_JPRB,ZS_N2(JLON,JLEV)-2E-3_JPRB))*0.25_JPRB
      !limitation
      PNEBQ(JLON,JLEV)=MAX(MIN(1.0_JPRB,PNEBQ(JLON,JLEV)*ZSIG),ZEPS4)

      ZSIG=(1.0_JPRB-SIGN(1.0_JPRB,ZEPS4-PNEBQ(JLON,JLEV)))*0.5_JPRB
      PNEBCVPP(JLON,JLEV)=MAX(PNEBQ(JLON,JLEV)**(1.0_JPRB/ZFCN(JLON,JLEV))*ZSIG,ZEPS4)
    ENDDO
  ENDDO

ENDIF ! end of Ri* block

DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
     
    ZS_FC=1.0_JPRB+PNEBCVPP(JLON,JLEV)*ZS_NX4(JLON,JLEV)
    ZS_MC=(1.0_JPRB+ZS_DC(JLON,JLEV)) &
     & /(1.0_JPRB+ZS_DC(JLON,JLEV)*ZS_FC)

    PMN2PP(JLON,JLEV)=ZS_MC*(ZS_A1(JLON,JLEV)+ZS_NX4(JLON,JLEV) &
     & *ZS_B3X(JLON,JLEV)*PNEBQ(JLON,JLEV))/ZDPHIA(JLON,JLEV)*RG**2

    PNBVNO(JLON,JLEV)=PMN2PP(JLON,JLEV)/(PAPRS(JLON,JLEV) &
     & *ZRTI(JLON,JLEV)*RG)**2
   
    PMRIPP(JLON,JLEV)=PMN2PP(JLON,JLEV)/(ZCISA(JLON,JLEV)*RG**2) &
     & *ZDPHIA(JLON,JLEV)**2

    PMN2_EQ(JLON,JLEV)=RG*ZS_MC*((ZS_AR(JLON,JLEV)-ZS_X0(JLON,JLEV))+ &
     & PNEBQ(JLON,JLEV)*ZS_NX4(JLON,JLEV)* &
     & (ZS_AR(JLON,JLEV)-ZS_X2(JLON,JLEV)/(1.0_JPRB+ZS_DC(JLON,JLEV))))

    PMN2_DQ(JLON,JLEV)=(ZQTOTA(JLON,JLEV)-ZQTOTA(JLON,JLEV+1)) &
     & *RG/ZDPHIA(JLON,JLEV)

    PMN2_ES(JLON,JLEV)=RG*ZS_MC/ZS_X1(JLON,JLEV)
    PMN2_DS(JLON,JLEV)=(ZSLAMA(JLON,JLEV)-ZSLAMA(JLON,JLEV+1)) &
     & *RG/ZDPHIA(JLON,JLEV)
    
  ENDDO
ENDDO
PMN2_ES(KIDIA:KFDIA,KTDIA-1)=PMN2_ES(KIDIA:KFDIA,KTDIA)
PMN2_EQ(KIDIA:KFDIA,KTDIA-1)=PMN2_EQ(KIDIA:KFDIA,KTDIA)
PMN2_DS(KIDIA:KFDIA,KTDIA-1)=PMN2_DS(KIDIA:KFDIA,KTDIA)
PMN2_DQ(KIDIA:KFDIA,KTDIA-1)=PMN2_DQ(KIDIA:KFDIA,KTDIA)

IF (LCOEFK_PTTE.AND.KSTEP > 0) THEN

  ! prognostic TTE

  ! diagnose Rif from TKE and TTE
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,PTTE(JLON,JLEV)/PTKE(JLON,JLEV)))
      ZWEIGHT=(PTKE(JLON,JLEV)-ETKE_MIN)*(PTKE(JLON,JLEV)-ETKE_MIN)+     &
       &      (PTTE(JLON,JLEV)-ETKE_MIN)*(PTTE(JLON,JLEV)-ETKE_MIN)
      ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
      ZMRIFPP=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                        &
       & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
      ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,PTTE(JLON,JLEV+1)/PTKE(JLON,JLEV+1)))
      ZWEIGHT=(PTKE(JLON,JLEV+1)-ETKE_MIN)*(PTKE(JLON,JLEV+1)-ETKE_MIN)+ &
       &      (PTTE(JLON,JLEV+1)-ETKE_MIN)*(PTTE(JLON,JLEV+1)-ETKE_MIN)
      ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
      ZMRIFPPI=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                       &
       & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
      PMRIFPP(JLON,JLEV)=0.5_JPRB*(ZMRIFPP+ZMRIFPPI)
    ENDDO
  ENDDO

  ! convert Rif to P and R
  CALL ACRIF2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
   & PMRIFPP,ZETKE_P,ZETKE_R)

  ! convert Rif to gradient Ri and store it
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZRIFTKE=PMRIFPP(JLON,JLEV)
      ZRITKE_F(JLON,JLEV)=ZRIFTKE/C3TKEFREE* &
       & (1.0_JPRB-ZRIFTKE/ZETKE_R(JLON,JLEV))/ &
       & (1.0_JPRB-ZRIFTKE/ZETKE_P(JLON,JLEV))
      ! protection against division by 0
      ZRITKE_F(JLON,JLEV)=SIGN(MAX(ZEPS, &
       & ABS(ZRITKE_F(JLON,JLEV))),ZRITKE_F(JLON,JLEV))
    ENDDO
  ENDDO

ELSE

  ! no TTE

  ! convert Ri to P and R
  CALL ACRI2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
   & PMRIPP,ZETKE_P,ZETKE_R)

  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! extract P and R
      Z_ETKE_P=ZETKE_P(JLON,JLEV)
      Z_ETKE_R=ZETKE_R(JLON,JLEV)

      ! convert gradient Ri to Rif
      ZRITKE=PMRIPP(JLON,JLEV)
      PMRIFPP(JLON,JLEV)=-(SQRT((Z_ETKE_R*(ZRITKE*C3TKEFREE+Z_ETKE_P))**2- &
       & 4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R*Z_ETKE_P**2)- &
       & (ZRITKE*C3TKEFREE+Z_ETKE_P)*Z_ETKE_R)/(2.0_JPRB*Z_ETKE_P)

      ! security
      PMRIFPP(JLON,JLEV)=MIN(PMRIFPP(JLON,JLEV),ETKE_RIFC-ZEPS8)

      ! store gradient Ri
      ZRITKE_F(JLON,JLEV)=PMRIPP(JLON,JLEV)

    ENDDO
  ENDDO

ENDIF

!*
!     ------------------------------------------------------------------
!     V - BOUCLE PASSIVE SUR LES NIVEAUX VERTICAUX.

!           PASSIVE LOOP ON VERTICAL LEVELS.
!*
!     ------------------------------------------------------------------
!     VI - COMPUTATION OF RI RELATED QUANTITIES - STABILITY FUNCTIONS AND ETC.

! new code
IF (ITTYPE==1) THEN

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

      ZCHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))
      ZPHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))

      !RMC01
      ZPHIQTA(JLON,JLEV)=1.0_JPRB
      PWW_FUN(JLON,JLEV)=Z2THIRD
      PTH_FUN(JLON,JLEV)=1.0_JPRB/(2.0_JPRB*PWW_FUN(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF
IF (ITTYPE==2) THEN

  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ZCHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))
      ZPHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))

      ! Model I
      PWW_FUN(JLON,JLEV)=Z2THIRD*(1.0_JPRB &
       & -(3.0_JPRB*ETKE_LAM3-ETKE_LAM2 &
       & +4.0_JPRB*ETKE_LAM4*PMRIFPP(JLON,JLEV))/(1.0_JPRB-PMRIFPP(JLON,JLEV)))
      ZPHIQTA(JLON,JLEV)=PWW_FUN(JLON,JLEV)/Z2AZ0
      PTH_FUN(JLON,JLEV)=1.0_JPRB/(2.0_JPRB*PWW_FUN(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF
IF (ITTYPE==3.OR.ITTYPE==4.OR.ITTYPE==6) THEN

  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ZCHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))
      ZPHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))

      ! Model II or QNSE
      PWW_FUN(JLON,JLEV)=Z2THIRD*(1.0_JPRB &
       & -(3.0_JPRB*ETKE_LAM3-ETKE_LAM2 &
       & +4.0_JPRB*ETKE_LAM4*PMRIFPP(JLON,JLEV))/(1.0_JPRB-PMRIFPP(JLON,JLEV)))
      ZTKE_LAM5T=(C3TKEFREE+ZCHI3TA(JLON,JLEV)/ZPHI3TA(JLON,JLEV)) &
       & /(C3TKEFREE+1.0_JPRB)
      ZPHIQTA(JLON,JLEV)=PWW_FUN(JLON,JLEV)/ZTKE_LAM5T/Z2AZ0
      PTH_FUN(JLON,JLEV)=1.0_JPRB/(2.0_JPRB*PWW_FUN(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF
IF (ITTYPE==5) THEN

  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ZCHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))
      ZPHI3TA(JLON,JLEV)=(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV)) &
       & /(1.0_JPRB-PMRIFPP(JLON,JLEV))

      !EFB
      ZPHIQTA(JLON,JLEV)=ZCHI3TA(JLON,JLEV)
      PWW_FUN(JLON,JLEV)=2.0_JPRB*EFB_AZ0*ZCHI3TA(JLON,JLEV)
      PTH_FUN(JLON,JLEV)=1.0_JPRB/(2.0_JPRB*PWW_FUN(JLON,JLEV))

    ENDDO
  ENDDO

ENDIF

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZCHI3TKE=ZCHI3TA(JLON,JLEV)
    PF_EPS(JLON,JLEV)=SQRT((SQRT(ZCHI3TKE*(1.0_JPRB-PMRIFPP(JLON,JLEV))) &
     & /ZCHI3TKE)**3)
    PFUN_TTE(JLON,JLEV)=ZCHI3TKE**2/SQRT(ZCHI3TKE*(1.0_JPRB-PMRIFPP(JLON,JLEV)))
    PTH_FUN(JLON,JLEV)=MAX(2.0_JPRB*PTH_FUN(JLON,JLEV)*ZPHIQTA(JLON,JLEV) &
     & /(ZPHIQTA(JLON,JLEV)+ZPHI3TA(JLON,JLEV)),0.0_JPRB)
  ENDDO
ENDDO

IF (ITTYPE == 6) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      PF_EPS(JLON,JLEV)=1.0_JPRB
    ENDDO
  ENDDO
ENDIF

PF_EPS(KIDIA:KFDIA,KTDIA-1)=PF_EPS(KIDIA:KFDIA,KTDIA)

IF (LCOEFK_F1) THEN
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    ZSIG=MAX(0._JPRB,SIGN(1._JPRB,PMRIFPP(JLON,KLEV)))
    ZFSFTKE=(1.0_JPRB-ZSIG)+ZSIG*SQRT(ZCHI3TA(JLON,KLEV)- &
     & C3TKEFREE*ZPHI3TA(JLON,KLEV)*PMRIPP(JLON,KLEV))
    PFMTKE(JLON,KLEV)=ZCHI3TA(JLON,KLEV)*ZFSFTKE
    PFTTKE(JLON,KLEV)=ZPHI3TA(JLON,KLEV)*ZFSFTKE
    ZFMTKE(JLON,KLEV)=PFMTKE(JLON,KLEV)
    ZFTTKE(JLON,KLEV)=PFTTKE(JLON,KLEV)
  ENDDO
ELSE
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PFMTKE(JLON,KLEV)=ZCHI3TA(JLON,KLEV)*SQRT(ZCHI3TA(JLON,KLEV) &
     & *(1._JPRB-PMRIFPP(JLON,KLEV)))
    PFTTKE(JLON,KLEV)=PFMTKE(JLON,KLEV)*ZPHI3TA(JLON,KLEV)/ZCHI3TA(JLON,KLEV)
    ZFMTKE(JLON,KLEV)=PFMTKE(JLON,KLEV)
    ZFTTKE(JLON,KLEV)=PFTTKE(JLON,KLEV)
  ENDDO
ENDIF

DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PFMTKE(JLON,JLEV)=ZCHI3TA(JLON,JLEV)*(ZCHI3TA(JLON,JLEV)-C3TKEFREE*ZPHI3TA(JLON,JLEV) &
     & *PMRIPP(JLON,JLEV))/SQRT(ZCHI3TA(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)))
    PFTTKE(JLON,JLEV)=PFMTKE(JLON,JLEV)*ZPHI3TA(JLON,JLEV)/ZCHI3TA(JLON,JLEV)
    ZFMTKE(JLON,JLEV)=ZCHI3TA(JLON,JLEV)*SQRT(ZCHI3TA(JLON,JLEV) &
     & *(1._JPRB-PMRIFPP(JLON,JLEV)))
    ZFTTKE(JLON,JLEV)=ZFMTKE(JLON,JLEV)*ZPHI3TA(JLON,JLEV)/ZCHI3TA(JLON,JLEV)
  ENDDO
ENDDO

! store ZFMTKE in PFMGST (to be transferred to ACMIXELEN)
PFMGST(KIDIA:KFDIA,KTDIA:KLEV)=ZFMTKE(KIDIA:KFDIA,KTDIA:KLEV)

! CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
! ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.
IF (LLAFGD) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZSIG_AF(JLON,JLEV)=SIGN(0.5_JPRB,PMRIFPP(JLON,JLEV))+0.5_JPRB
    ENDDO
  ENDDO

  IF (ITTYPE==1) THEN

    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA

        ZDRIFTKE(JLON,JLEV)=(ETKE_RIFC-PMRIFPP(JLON,JLEV))*C3TKEFREE*ETKE_R/ &
         & ((ZRITKE_F(JLON,JLEV)*C3TKEFREE+ETKE_RIFC)*ETKE_R-2.0_JPRB* &
         & PMRIFPP(JLON,JLEV)*ETKE_RIFC)
        ZDPHI3TKE(JLON,JLEV)=(ETKE_RIFC-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_RIFC)*ZDRIFTKE(JLON,JLEV)
        ZDCHI3TKE(JLON,JLEV)=(ETKE_R-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_R)*ZDRIFTKE(JLON,JLEV)
      ENDDO
    ENDDO

  ENDIF ! type 1

  IF (ITTYPE==2) THEN
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA

        ! Model I
        ZDRIFTKE(JLON,JLEV)=(ETKE_RIFC-PMRIFPP(JLON,JLEV))*C3TKEFREE*ETKE_R/ &
         & ((ZRITKE_F(JLON,JLEV)*C3TKEFREE+ETKE_RIFC)*ETKE_R-2.0_JPRB* &
         & PMRIFPP(JLON,JLEV)*ETKE_RIFC)
        ZDPHI3TKE(JLON,JLEV)=(ETKE_RIFC-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_RIFC)*ZDRIFTKE(JLON,JLEV)
        ZDCHI3TKE(JLON,JLEV)=(ETKE_R-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_R)*ZDRIFTKE(JLON,JLEV)
      ENDDO
    ENDDO

  ENDIF ! type 2

  IF (ITTYPE==3) THEN

    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA

        ! Model II
        ZDRIFTKE(JLON,JLEV)=(ETKE_RIFC-PMRIFPP(JLON,JLEV))*C3TKEFREE*ETKE_R/ &
         & ((ZRITKE_F(JLON,JLEV)*C3TKEFREE+ETKE_RIFC)*ETKE_R-2.0_JPRB* &
         & PMRIFPP(JLON,JLEV)*ETKE_RIFC)
        ZDPHI3TKE(JLON,JLEV)=(ETKE_RIFC-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_RIFC)*ZDRIFTKE(JLON,JLEV)
        ZDCHI3TKE(JLON,JLEV)=(ETKE_R-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ETKE_R)*ZDRIFTKE(JLON,JLEV)
      ENDDO
    ENDDO

  ENDIF ! type 3
  IF (ITTYPE==4) THEN
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA

        ! Unstable
        ZDRRI_U=RQNSE_GU1/(RQNSE_GU2*ZRITKE_F(JLON,JLEV)+1.0_JPRB) &
         & -(RQNSE_GU2*(RQNSE_GU1*ZRITKE_F(JLON,JLEV)+RQNSE_S0))/ &
         & (RQNSE_GU2*ZRITKE_F(JLON,JLEV)+1)**2
        ! Stable
        ZDRRI=(2.0_JPRB*RQNSE_GS1*ZRITKE_F(JLON,JLEV)+RQNSE_GS2)/ &
         & (RQNSE_GS3*ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)*RQNSE_GS4+1.0_JPRB)- &
         & ((2.0_JPRB*RQNSE_GS3*ZRITKE_F(JLON,JLEV)+RQNSE_GS4) &
         & *(RQNSE_GS1*ZRITKE_F(JLON,JLEV)**2+RQNSE_S0+ZRITKE_F(JLON,JLEV)*RQNSE_GS2)) &
         & /(RQNSE_GS3*ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)*RQNSE_GS4+1.0_JPRB)**2
        ZDRRI=ZSIG_AF(JLON,JLEV)*ZDRRI+(1.0_JPRB-ZSIG_AF(JLON,JLEV))*ZDRRI_U


        ZDRIFTKE(JLON,JLEV)=(ZETKE_P(JLON,JLEV)*ZDRRI*PMRIFPP(JLON,JLEV)**3 &
         & +(C3TKEFREE*ZETKE_R(JLON,JLEV)**2-ZETKE_P(JLON,JLEV)**2*ZDRRI)* &
         & PMRIFPP(JLON,JLEV)**2-2.0_JPRB*C3TKEFREE*ZETKE_P(JLON,JLEV)* &
         & ZETKE_R(JLON,JLEV)**2*PMRIFPP(JLON,JLEV)+C3TKEFREE* &
         & ZETKE_P(JLON,JLEV)**2*ZETKE_R(JLON,JLEV)**2) &
         & /(ZETKE_P(JLON,JLEV)*ZETKE_R(JLON,JLEV)*PMRIFPP(JLON,JLEV)**2 &
         & -2.0_JPRB*ZETKE_P(JLON,JLEV)**2*ZETKE_R(JLON,JLEV)* &
         & PMRIFPP(JLON,JLEV)+ZETKE_P(JLON,JLEV)**2*ZETKE_R(JLON,JLEV)**2)

        ZDCHI3TKE(JLON,JLEV)=((ZDRRI*PMRIFPP(JLON,JLEV))/ZETKE_R(JLON,JLEV)**2 &
         & -ZDRIFTKE(JLON,JLEV)/ZETKE_R(JLON,JLEV))/(1.0_JPRB-PMRIFPP(JLON,JLEV)) &
         & +(ZDRIFTKE(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV))) &
         & /(1.0_JPRB-PMRIFPP(JLON,JLEV))**2
        ZDPHI3TKE(JLON,JLEV)=(ZDRIFTKE(JLON,JLEV)* &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV))) &
         & /(1.0_JPRB-PMRIFPP(JLON,JLEV))**2-ZDRIFTKE(JLON,JLEV)/ &
         & (ZETKE_P(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)))

      ENDDO
    ENDDO

  ENDIF ! type 4

  IF (ITTYPE==5) THEN

    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA

        ! Unstable
        ZEFB_UP=EFB_UR &
         & /(ETKE_OLAM*EFB_UR/(EFB_AZ0*C3TKEFREE)+1.0_JPRB)
        ZDRIFTKE_U=(ZEFB_UP-PMRIFPP(JLON,JLEV))*C3TKEFREE*EFB_UR/ &
         & ((ZRITKE_F(JLON,JLEV)*C3TKEFREE+ZEFB_UP)*EFB_UR &
         & -2.0_JPRB*PMRIFPP(JLON,JLEV)*ZEFB_UP)
        ZDPHI3TKE_U=(ZEFB_UP-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*ZEFB_UP)*ZDRIFTKE_U
        ZDCHI3TKE_U=(EFB_UR-1.0_JPRB)/ &
         & ((PMRIFPP(JLON,JLEV)-1.0_JPRB)**2*EFB_UR)*ZDRIFTKE_U

        ! Stable
        ZDRIFTKE(JLON,JLEV)=(C3TKEFREE*EFB_AZ0*REFB_2*PMRIFPP(JLON,JLEV)**2+ &
         & ETKE_OLAM*REFB_1*PMRIFPP(JLON,JLEV)**2+EFB_UR*ETKE_OLAM*REFB_3* &
         & PMRIFPP(JLON,JLEV)+C3TKEFREE*EFB_AZ0*REFB_3*PMRIFPP(JLON,JLEV)- &
         & C3TKEFREE*EFB_AZ0*REFB_1*PMRIFPP(JLON,JLEV)- &
         & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_3)**2/(EFB_AZ0*(C3TKEFREE*EFB_AZ0* &
         & REFB_2**2*PMRIFPP(JLON,JLEV)**4+ETKE_OLAM*REFB_1*REFB_2 &
         & *PMRIFPP(JLON,JLEV)**4+2.0_JPRB*EFB_UR*ETKE_OLAM*REFB_2*REFB_3* &
         & PMRIFPP(JLON,JLEV)**3+2.0_JPRB*C3TKEFREE*EFB_AZ0*REFB_2*REFB_3* &
         & PMRIFPP(JLON,JLEV)**3-2.0_JPRB*C3TKEFREE*EFB_AZ0 &
         & *REFB_1*REFB_2*PMRIFPP(JLON,JLEV)**3+EFB_UR*ETKE_OLAM*REFB_3**2* &
         & PMRIFPP(JLON,JLEV)**2+C3TKEFREE*EFB_AZ0*REFB_3**2*PMRIFPP(JLON,JLEV)**2- &
         & 2.0_JPRB*C3TKEFREE*EFB_AZ0*EFB_UR*REFB_2*REFB_3*PMRIFPP(JLON,JLEV)**2- &
         & 2.0_JPRB*C3TKEFREE*EFB_AZ0*REFB_1*REFB_3*PMRIFPP(JLON,JLEV)**2 &
         & +C3TKEFREE*EFB_AZ0*REFB_1**2*PMRIFPP(JLON,JLEV)**2-2.0_JPRB* &
         & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_3**2*PMRIFPP(JLON,JLEV)+2.0_JPRB* &
         & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_1*REFB_3* &
         & PMRIFPP(JLON,JLEV)+C3TKEFREE*EFB_AZ0*EFB_UR**2*REFB_3**2))

        ZDRRI=-((EFB_UR*REFB_2-REFB_1)*REFB_3*ZDRIFTKE(JLON,JLEV)) &
         & /(REFB_2**2*PMRIFPP(JLON,JLEV)**2+2.0_JPRB*REFB_2* &
         & REFB_3*PMRIFPP(JLON,JLEV)+REFB_3**2)

        ZDPRI=-((C3TKEFREE**2*EFB_AZ0**2*EFB_UR*REFB_2-C3TKEFREE**2 &
         & *EFB_AZ0**2*REFB_1)*REFB_3*ZDRIFTKE(JLON,JLEV))/ &
         & ((C3TKEFREE**2*EFB_AZ0**2*REFB_2**2+2.0_JPRB*C3TKEFREE &
         & *EFB_AZ0*ETKE_OLAM*REFB_1*REFB_2+ETKE_OLAM**2*REFB_1**2)* &
         & PMRIFPP(JLON,JLEV)**2+((2.0_JPRB*C3TKEFREE*EFB_AZ0*EFB_UR* &
         & ETKE_OLAM+2*C3TKEFREE**2*EFB_AZ0**2)*REFB_2+(2.0_JPRB*EFB_UR* &
         & ETKE_OLAM**2+2.0_JPRB*C3TKEFREE*EFB_AZ0*ETKE_OLAM)*REFB_1) &
         & *REFB_3*PMRIFPP(JLON,JLEV)+ (EFB_UR**2*ETKE_OLAM**2+2.0_JPRB* &
         & C3TKEFREE*EFB_AZ0*EFB_UR*ETKE_OLAM+C3TKEFREE**2*EFB_AZ0**2)*REFB_3**2)

        ZDCHI3TKE(JLON,JLEV)=((ZDRRI*PMRIFPP(JLON,JLEV))/ZETKE_R(JLON,JLEV)**2 &
         & -ZDRIFTKE(JLON,JLEV)/ZETKE_R(JLON,JLEV))/ &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV)) &
         & +(ZDRIFTKE(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)/ &
         & ZETKE_R(JLON,JLEV)))/(1.0_JPRB-PMRIFPP(JLON,JLEV))**2

        ZDPHI3TKE(JLON,JLEV)=((ZDPRI*PMRIFPP(JLON,JLEV))/ZETKE_P(JLON,JLEV)**2 &
         & -ZDRIFTKE(JLON,JLEV)/ZETKE_P(JLON,JLEV))/ &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV))+(ZDRIFTKE(JLON,JLEV)* &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_P(JLON,JLEV)))/ &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV))**2
 
        ZDRIFTKE(JLON,JLEV)=ZSIG_AF(JLON,JLEV)*ZDRIFTKE(JLON,JLEV)+ &
         & (1.0_JPRB-ZSIG_AF(JLON,JLEV))*ZDRIFTKE_U
        ZDCHI3TKE(JLON,JLEV)=ZSIG_AF(JLON,JLEV)* ZDCHI3TKE(JLON,JLEV)+ &
         & (1.0_JPRB-ZSIG_AF(JLON,JLEV))* ZDCHI3TKE_U
        ZDPHI3TKE(JLON,JLEV)=ZSIG_AF(JLON,JLEV)* ZDPHI3TKE(JLON,JLEV)+ &
         & (1.0_JPRB-ZSIG_AF(JLON,JLEV))* ZDPHI3TKE_U
      ENDDO
    ENDDO

  ENDIF ! type 5
  IF (ITTYPE==6) THEN

    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ! Unstable
        ZDRRI_U=RLOUIS_GU1/(RLOUIS_GU2*ZRITKE_F(JLON,JLEV)+1.0_JPRB) &
         & -(RLOUIS_GU2*(RLOUIS_GU1*ZRITKE_F(JLON,JLEV)+RLOUIS_S0))/ &
         & (RLOUIS_GU2*ZRITKE_F(JLON,JLEV)+1)**2
        ! Stable
        ZDRRI=(2.0_JPRB*RLOUIS_GS1*ZRITKE_F(JLON,JLEV)+RLOUIS_GS2)/ &
         & (RLOUIS_GS3*ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)* &
         & RLOUIS_GS4+1.0_JPRB)-((2.0_JPRB*RLOUIS_GS3*ZRITKE_F(JLON,JLEV)+ &
         & RLOUIS_GS4)*(RLOUIS_GS1*ZRITKE_F(JLON,JLEV)**2+RLOUIS_S0+ &
         & ZRITKE_F(JLON,JLEV)*RLOUIS_GS2))/(RLOUIS_GS3* &
         & ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)*RLOUIS_GS4+1.0_JPRB)**2
        ZDRRI=ZSIG_AF(JLON,JLEV)*ZDRRI+(1.0_JPRB-ZSIG_AF(JLON,JLEV))*ZDRRI_U

        ! Unstable
        ZDPRI_U=PLOUIS_GU1/(PLOUIS_GU2*ZRITKE_F(JLON,JLEV)+1.0_JPRB) &
         & -(PLOUIS_GU2*(PLOUIS_GU1*ZRITKE_F(JLON,JLEV)+PLOUIS_S0))/ &
         & (PLOUIS_GU2*ZRITKE_F(JLON,JLEV)+1)**2
        ! Stable
        ZDPRI=(2.0_JPRB*PLOUIS_GS1*ZRITKE_F(JLON,JLEV)+PLOUIS_GS2)/ &
         & (PLOUIS_GS3*ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)* &
         & PLOUIS_GS4+1.0_JPRB)-((2.0_JPRB*PLOUIS_GS3*ZRITKE_F(JLON,JLEV)+ &
         & PLOUIS_GS4)*(PLOUIS_GS1*ZRITKE_F(JLON,JLEV)**2+PLOUIS_S0+ &
         & ZRITKE_F(JLON,JLEV)*PLOUIS_GS2))/(PLOUIS_GS3* &
         & ZRITKE_F(JLON,JLEV)**2+ZRITKE_F(JLON,JLEV)*PLOUIS_GS4+1.0_JPRB)**2
        ZDPRI=ZSIG_AF(JLON,JLEV)*ZDPRI+(1.0_JPRB-ZSIG_AF(JLON,JLEV))*ZDPRI_U

        ZDRIFTKE(JLON,JLEV)=(PMRIFPP(JLON,JLEV)**3*(ZETKE_P(JLON,JLEV)* &
         & ZDRRI-ZETKE_R(JLON,JLEV)*ZDPRI)+PMRIFPP(JLON,JLEV)**2* &
         & (-ZETKE_P(JLON,JLEV)**2*ZDRRI+ZETKE_R(JLON,JLEV)**2*(ZDPRI+1.0_JPRB)) &
         & -2.0_JPRB*ZETKE_P(JLON,JLEV)*ZETKE_R(JLON,JLEV)**2*PMRIFPP(JLON,JLEV)+ &
         & ZETKE_P(JLON,JLEV)**2*ZETKE_R(JLON,JLEV)**2)/ &
         & (ZETKE_P(JLON,JLEV)*ZETKE_R(JLON,JLEV)* &
         & (PMRIFPP(JLON,JLEV)**2-2*ZETKE_P(JLON,JLEV)*PMRIFPP(JLON,JLEV)+ &
         & ZETKE_P(JLON,JLEV)*ZETKE_R(JLON,JLEV)))

        ZDCHI3TKE(JLON,JLEV)=((ZDRRI*PMRIFPP(JLON,JLEV))/ZETKE_R(JLON,JLEV)**2 &
         & -ZDRIFTKE(JLON,JLEV)/ZETKE_R(JLON,JLEV))/(1.0_JPRB-PMRIFPP(JLON,JLEV)) &
         & +(ZDRIFTKE(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)/ZETKE_R(JLON,JLEV))) &
         & /(1.0_JPRB-PMRIFPP(JLON,JLEV))**2
        ZDPHI3TKE(JLON,JLEV)=(ZDRIFTKE(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)/ &
         & ZETKE_P(JLON,JLEV)))/(1.0_JPRB-PMRIFPP(JLON,JLEV))**2-ZDRIFTKE(JLON,JLEV)/ &
         & (ZETKE_P(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV)))

      ENDDO
    ENDDO

  ENDIF ! type 6

  ! derivatives of Fm and Fh- surface
  IF (LCOEFK_F1) THEN
    DO JLON=KIDIA,KFDIA
      ZFFTKE(JLON,KLEV)=ZCHI3TA(JLON,KLEV)*(1.0_JPRB-PMRIFPP(JLON,KLEV))
      ZFFTKE_DX(JLON,KLEV)=MAX((1.0_JPRB-PMRIFPP(JLON,KLEV))*ZDCHI3TKE(JLON,KLEV) &
       & -ZCHI3TA(JLON,KLEV)*ZDRIFTKE(JLON,KLEV),ZEPS8)
      ZSIG=MAX(0._JPRB,SIGN(1._JPRB,PMRIFPP(JLON,KLEV)))
      ZFMTKE_DX(JLON,KLEV)=(1.0_JPRB-ZSIG)*ZDCHI3TKE(JLON,KLEV)+ZSIG* &
       & ((ZFFTKE(JLON,KLEV)*ZDCHI3TKE(JLON,KLEV)+ &
       & 0.5_JPRB*ZCHI3TA(JLON,KLEV)*ZFFTKE_DX(JLON,KLEV)) &
       & /SQRT(ZFFTKE(JLON,KLEV)))
      ZFHTKE_DX(JLON,KLEV)=(1.0_JPRB-ZSIG)*ZDPHI3TKE(JLON,KLEV)+ZSIG* &
       & ((ZFFTKE(JLON,KLEV)*ZDCHI3TKE(JLON,KLEV)+ &
       & 0.5_JPRB*ZCHI3TA(JLON,KLEV)*ZFFTKE_DX(JLON,KLEV)) &
       & /SQRT(ZFFTKE(JLON,KLEV)))
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA
      ZFFTKE(JLON,KLEV)=ZCHI3TA(JLON,KLEV)*(1.0_JPRB-PMRIFPP(JLON,KLEV))
      ZFFTKE_DX(JLON,KLEV)=MAX((1.0_JPRB-PMRIFPP(JLON,KLEV))*ZDCHI3TKE(JLON,KLEV) &
       & -ZCHI3TA(JLON,KLEV)*ZDRIFTKE(JLON,KLEV),ZEPS8)
      ZFMTKE_DX(JLON,KLEV)=(ZFFTKE(JLON,KLEV)*ZDCHI3TKE(JLON,KLEV)+ &
       & 0.5_JPRB*ZCHI3TA(JLON,KLEV)*ZFFTKE_DX(JLON,KLEV)) &
       & /SQRT(ZFFTKE(JLON,KLEV))
      ZFHTKE_DX(JLON,KLEV)=(ZFFTKE(JLON,KLEV)*ZDPHI3TKE(JLON,KLEV)+ &
       & 0.5_JPRB*ZPHI3TA(JLON,KLEV)*ZFFTKE_DX(JLON,KLEV)) &
       & /SQRT(ZFFTKE(JLON,KLEV))
    ENDDO
  ENDIF

  ! derivatives of Fm and Fh- above surface
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFFTKE(JLON,JLEV)=ZCHI3TA(JLON,JLEV)*(1.0_JPRB-PMRIFPP(JLON,JLEV))
      ZFFTKE_DX(JLON,JLEV)=MAX((1.0_JPRB-PMRIFPP(JLON,JLEV))*ZDCHI3TKE(JLON,JLEV) &
       & -ZCHI3TA(JLON,JLEV)*ZDRIFTKE(JLON,JLEV),ZEPS8)
      ZFMTKE_DX(JLON,JLEV)=(ZFFTKE(JLON,JLEV)*ZDCHI3TKE(JLON,JLEV)+ &
       & 0.5_JPRB*ZCHI3TA(JLON,JLEV)*ZFFTKE_DX(JLON,JLEV)) &
       & /SQRT(ZFFTKE(JLON,JLEV))
      ZFHTKE_DX(JLON,JLEV)=(ZFFTKE(JLON,JLEV)*ZDPHI3TKE(JLON,JLEV)+ &
       & 0.5_JPRB*ZPHI3TA(JLON,JLEV)*ZFFTKE_DX(JLON,JLEV)) &
       & /SQRT(ZFFTKE(JLON,JLEV))
    ENDDO
  ENDDO

  ! COMPUTAION OF alpha_u and alpha_theta - PAUTKE,PATTKE
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      PAUTKE(JLON,JLEV)=0.5_JPRB*ZRITKE_F(JLON,JLEV)* &
       & ZFMTKE_DX(JLON,JLEV)/ZFMTKE(JLON,JLEV)
      PATTKE(JLON,JLEV)=0.5_JPRB*ZRITKE_F(JLON,JLEV)* &
       & ZFHTKE_DX(JLON,JLEV)/ZFTTKE(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF ! LLAFGD

PTH_FUN(KIDIA:KFDIA,KTDIA-1)=PTH_FUN(KIDIA:KFDIA,KTDIA)

!*
!     ------------------------------------------------------------------
!     VIII - HORIZONTAL FUNCTIONS FOR 3D-TURBULENCE

IF (LL3DTURB.AND.(ITTYPE==4)) THEN
  ! 3DTURB + QNSE

  ! QNSE fit for CHI_H and PHI_H*C3
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      IF (ZRITKE_F(JLON,JLEV) < 0.0_JPRB ) THEN
        ZAU= 1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(CHIHQNSE_U4+ZRITKE_F(JLON,JLEV)*CHIHQNSE_U3 )
        ZAU=SIGN(MAX(ZEPS,ABS(ZAU)),ZAU)
        PFHORM(JLON,JLEV)=(1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(CHIHQNSE_U2+ZRITKE_F(JLON,JLEV)*CHIHQNSE_U1 ))/ZAU
        ZAT= 1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(PHIHQNSE_U4+ZRITKE_F(JLON,JLEV)*PHIHQNSE_U3 )
        ZAT=SIGN(MAX(ZEPS,ABS(ZAT)),ZAT)
        PFHORH(JLON,JLEV)=(1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(PHIHQNSE_U2+ZRITKE_F(JLON,JLEV)*PHIHQNSE_U1 ))/ZAT &
         & *C3TKEFREE
      ELSE
        ZAU= 1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(CHIHQNSE_S4+ZRITKE_F(JLON,JLEV)*CHIHQNSE_S3 )
        ZAU=SIGN(MAX(ZEPS,ABS(ZAU)),ZAU)
        PFHORM(JLON,JLEV)=(1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(CHIHQNSE_S2+ZRITKE_F(JLON,JLEV)*CHIHQNSE_S1 ))/ZAU 
        ZAT= 1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(PHIHQNSE_S6+ZRITKE_F(JLON,JLEV)*(PHIHQNSE_S5 &
         & +ZRITKE_F(JLON,JLEV)*PHIHQNSE_S4 ))
        ZAT=SIGN(MAX(ZEPS,ABS(ZAT)),ZAT)
        PFHORH(JLON,JLEV)=(1._JPRB+ZRITKE_F(JLON,JLEV) &
         & *(PHIHQNSE_S3+ZRITKE_F(JLON,JLEV)*(PHIHQNSE_S2 &
         & +ZRITKE_F(JLON,JLEV)*PHIHQNSE_S1 )))/ZAT &
         & *C3TKEFREE
      ENDIF
    ENDDO
  ENDDO
  ! Upper bound 
  PFHORM(KIDIA:KFDIA,KTDIA-1)=PFHORM(KIDIA:KFDIA,KTDIA)
  PFHORH(KIDIA:KFDIA,KTDIA-1)=PFHORH(KIDIA:KFDIA,KTDIA)
ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACMRIP',1,ZHOOK_HANDLE)
END SUBROUTINE ACMRIP
