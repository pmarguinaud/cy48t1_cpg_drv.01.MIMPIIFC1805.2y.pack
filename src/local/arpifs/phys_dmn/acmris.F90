!OPTIONS XOPT(NOEVAL)
!OPTION! -O nochg
SUBROUTINE ACMRIS(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PDELP,PLSCPE,PQ,PQL,PQI,PQSAT,&
 & PR,PT,PU,PV,PLMU,PLMT, &
 ! - INPUT  1D .
 & PGZ0, &
 ! - OUTPUT 2D .
 & PMRIPP)

!**** *ACMRIS*  moist Ri' computation - Ri*/** with moist AF
!        *CALL* *ACMRIS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PLMT       : LONGUEUR DE MELANGE POUR L'ENERGIE.
! PLMU       : LONGUEUR DE MELANGE POUR LA QUANTITE DE MOUVEMENT.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQL        : EAU LIQUIDE
! PQI        : GLACE
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------
! PMRIPP     : MOIST Ri '
!---------------------------------------------------------------
!     Reference.
!     ----------
!        Documentation TOUCANS

!     Author.
!     -------
!        I. Bastak-Duran   

!     Modifications.
!     --------------
!     I. Bastak-Duran, F. Vana 26-Jan-2012 : Phased to CY38
!     I. Bastak-Duran 19-Jan-2013 : fixes of Ri computations.
!     R. Brozkova, I. Bastak-Duran Oct-2014: major rewriting.
!     J. Masek, Oct-2020: introduction of ACRI2PR.

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RKAPPA   ,&
 & RV       ,RCPD     ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD, RPI

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCPE   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMU     (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMT     (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0     (KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMRIPP   (KLON,0:KLEV)
INTEGER(KIND=JPIM) :: JLEV, JLON, ITTYPE

LOGICAL :: LLAFAD

REAL(KIND=JPRB) :: ZDELTA, ZGLU, ZRCVPP, ZZ
REAL(KIND=JPRB) :: ZGDT
REAL(KIND=JPRB) :: ZCPVMD, ZDCP, ZDELT1, ZDELT2, ZDSEPRIM, &
 & ZEW, ZEPS, ZEPS2, ZMLSCPE, ZQPRIM, ZQSAT1, ZQSAT2, &
 & ZTPRIM1, ZTPRIM2, ZTC
REAL(KIND=JPRB) :: ZBETA1(KLON,0:KLEV), ZDSE(KLON,KLEV), ZQSAT(KLON,KLEV)
REAL(KIND=JPRB) :: ZQTOTA(KLON,KLEV),ZSLAMA(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPHIA(KLON,0:KLEV),ZCISA(KLON,0:KLEV),ZC3TKE(KLON,0:KLEV),&
& ZAUX(KLON,0:KLEV),ZDAUX(KLON,0:KLEV),ZDELBI(KLON,0:KLEV),ZRITKEA(KLON,0:KLEV),&
& ZRITKEDA(KLON,0:KLEV),ZFTTKE_D(KLON,0:KLEV),ZFTTKE_S(KLON,0:KLEV),&
& ZRTI(KLON,0:KLEV),ZUGLMT2(KLON,0:KLEV),ZDTETA(KLON,0:KLEV),Z_ETKE_R(KLON,0:KLEV),&
& Z_ETKE_P(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZRITKE,ZRIFTKE
REAL(KIND=JPRB) :: ZFTTKE,ZFMTKE,ZPHI3TKE,ZCHI3TKE
REAL(KIND=JPRB) :: ZFTTKE_C
REAL(KIND=JPRB) ::ZS_FRAC,ZS_FTRIFC2,ZS_A,ZS_B,ZS_C,ZS_D,ZS_E,ZS_P,ZS_Q,ZS_R,  &
 & ZS_AA1, ZS_AA2, ZS_AA3, ZS_AA4
REAL(KIND=JPRB) :: ZS_CTHIRD,ZS_CW,ZS_CP,ZS_CQ,ZS_CPOM,ZS_CDIS,ZS_CPHI,ZS_ZSOL,&
 & ZS_XK2,ZS_XK,ZS_XL,ZS_XL2,ZS_SQP,ZS_SQM,ZS_SOL,ZS_CU1,ZS_CU2,ZS_CU3, &
 & ZS_CX1,ZS_CX2,ZS_CX3
REAL(KIND=JPRB) :: ZS_FTDIFF,ZS_FTDIFF2,ZS_RITKE
REAL(KIND=JPRB) :: ZDELTH_C3
REAL(KIND=JPRB) :: ZTY,ZCPDTY,ZTH,ZTHI,ZIT,ZLAMCP
REAL(KIND=JPRB) :: ZEPS8
REAL(KIND=JPRB) :: ZNEIGE

#include "acri2pr.intfb.h"

#include "fcttrm.func.h"


!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACMRIS',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & C3TKEFREE=>YDML_PHY_MF%YRPHY0%C3TKEFREE, GRCVPP=>YDML_PHY_MF%YRPHY0%GRCVPP, EFB_AZ0=>YDML_PHY_MF%YRPHY0%EFB_AZ0, &
 & GCCSV=>YDML_PHY_MF%YRPHY0%GCCSV, ETKE_RIFC_MAF=>YDML_PHY_MF%YRPHY0%ETKE_RIFC_MAF, &
 & ETKE_OLAM=>YDML_PHY_MF%YRPHY0%ETKE_OLAM, ETKE_R=>YDML_PHY_MF%YRPHY0%ETKE_R, EFB_UR=>YDML_PHY_MF%YRPHY0%EFB_UR, &
 & ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, REFB_1=>YDML_PHY_MF%YRPHY0%REFB_1, REFB_2=>YDML_PHY_MF%YRPHY0%REFB_2, &
 & REFB_3=>YDML_PHY_MF%YRPHY0%REFB_3, ETKE_RIFC=>YDML_PHY_MF%YRPHY0%ETKE_RIFC, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, XDAMP=>YDML_PHY_MF%YRPHY2%XDAMP, &
 & XMUCVPP=>YDML_PHY_MF%YRPHY2%XMUCVPP, &
 & RLOUIS_S0=>YDML_PHY_MF%YRLOUIS%RLOUIS_S0, RLOUIS_GS4=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS4, &
 & RLOUIS_GS3=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS3, RLOUIS_GS2=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS2, &
 & RLOUIS_GS1=>YDML_PHY_MF%YRLOUIS%RLOUIS_GS1, PLOUIS_S0=>YDML_PHY_MF%YRLOUIS%PLOUIS_S0, &
 & RLOUIS_GU1=>YDML_PHY_MF%YRLOUIS%RLOUIS_GU1, RLOUIS_GU2=>YDML_PHY_MF%YRLOUIS%RLOUIS_GU2, &
 & PLOUIS_GU2=>YDML_PHY_MF%YRLOUIS%PLOUIS_GU2, PLOUIS_GU1=>YDML_PHY_MF%YRLOUIS%PLOUIS_GU1, &
 & PLOUIS_GS1=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS1, PLOUIS_GS3=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS3, &
 & PLOUIS_GS2=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS2, PLOUIS_GS4=>YDML_PHY_MF%YRLOUIS%PLOUIS_GS4, &
 & L3MT=>YDML_PHY_MF%YRPHY%L3MT, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, CGTURS=>YDML_PHY_MF%YRPHY%CGTURS, &
 & LCOEFK_THS1=>YDML_PHY_MF%YRPHY%LCOEFK_THS1, LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO, &
 & LCVPP=>YDML_PHY_MF%YRPHY%LCVPP)
!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     LE CARRE DU CISAILLEMENT DE VENT).

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS (FOR
!     THE SQUARE OF THE WIND SHEAR).

ZEPS8=1E-8_JPRB
ZTY=2362.0_JPRB
ZCPDTY=ZTY*RCPD
ZCPVMD=RCPV-RCPD
ZDELTH_C3=2.0_JPRB*ETKE_RIFC_MAF/(1.0_JPRB-ETKE_RIFC_MAF)
ZLAMCP=RCPV/RCPD-1.0_JPRB

ZGDT=RG*TSPHY
ZGLU=RG*ALMAV

ZRCVPP=1.0_JPRB/GRCVPP

! security constant
ZEPS2=EPSILON(1.0_JPRB)*100.0_JPRB

ZS_CTHIRD=1.0_JPRB/3.0_JPRB

LLAFAD=XDAMP /=  0.0_JPRB
ZBETA1(KIDIA:KFDIA,KTDIA-1)=0.0_JPRB

IF     (TRIM(CGTURS) == 'CBR') THEN
  ITTYPE=1
ELSEIF (TRIM(CGTURS) == 'MD1') THEN
  ITTYPE=2
ELSEIF (TRIM(CGTURS) == 'MD2') THEN
  ITTYPE=3
ELSEIF (TRIM(CGTURS) == 'QNSE') THEN
  ITTYPE=4
ELSEIF (TRIM(CGTURS) == 'EFB') THEN
  ITTYPE=5
ELSEIF (TRIM(CGTURS) == 'LOUIS') THEN
  ITTYPE=6
ENDIF

! PREPARATIONS FOR SHALLOW CONVECTION PAR.
IF(LNEIGE)THEN
  ZNEIGE=1.0_JPRB
ELSE
  ZNEIGE=0.0_JPRB
ENDIF

IF (L3MT.OR.LSTRAPRO) THEN
  DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
       ZTC=PT(JLON,JLEV)-(FOLH(PT(JLON,JLEV),0.0_JPRB)*PQL(JLON,JLEV)&
        & + FOLH(PT(JLON,JLEV),1.0_JPRB)*PQI(JLON,JLEV))/PCP(JLON,JLEV)
       ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*ZTC+PAPHIF(JLON,JLEV)
       ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTC))*ZNEIGE
       ZEW=FOEW(ZTC,ZDELTA)
       ZEPS=ZEW/PAPRSF(JLON,JLEV)
       ZQSAT(JLON,JLEV)=FOQS(ZEPS)
     ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZQSAT(JLON,JLEV)=PQSAT(JLON,JLEV)
      ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!full levels
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZQTOTA(JLON,JLEV)=PQ(JLON,JLEV)+PQL(JLON,JLEV)+PQI(JLON,JLEV)
    ZSLAMA(JLON,JLEV)=RCPD*PT(JLON,JLEV)*(1.0_JPRB+ZQTOTA(JLON,JLEV)*ZLAMCP)+&
    & PAPHIF(JLON,JLEV)-&
    & (PQL(JLON,JLEV)*RLV(PT(JLON,JLEV))+PQI(JLON,JLEV)*RLS(PT(JLON,JLEV)))
  ENDDO
ENDDO

!half levels
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZDPHIA(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
    ZZ=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))&
     & -PAPHI(JLON,KLEV)+PGZ0(JLON))

    ZCISA(JLON,JLEV)=(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2&
     &  +(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2&
     &  +(GCISMIN*ZDPHIA(JLON,JLEV)/(RG*(1.0_JPRB+ZZ/ZGLU)))**2
    ZUGLMT2(JLON,JLEV)=SQRT(ZCISA(JLON,JLEV))*&
    & PLMT(JLON,JLEV)*PLMU(JLON,JLEV)*RG**2

    ZDTETA(JLON,JLEV)=PR(JLON,JLEV)*PT(JLON,JLEV)-PR(JLON,JLEV+1)*&
     & PT(JLON,JLEV+1)+RKAPPA*ZDPHIA(JLON,JLEV)  

    ZRTI(JLON,JLEV)=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+&
    &PR(JLON,JLEV+1)*PT(JLON,JLEV+1))

    !could be removed? - to have vertical profile of C3
    ZC3TKE(JLON,JLEV)=PLMT(JLON,JLEV)/PLMU(JLON,JLEV)
  ENDDO
ENDDO


IF (LCVPP .AND. LCOEFK_THS1) THEN  ! THS1 version
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTH=PT(JLON,JLEV)*(1.0_JPRB-&
      & (PQL(JLON,JLEV)*RLV(PT(JLON,JLEV)))+PQI(JLON,JLEV)*RLS(PT(JLON,JLEV)))+&
      & (PT(JLON,JLEV)*(RCPV/RCPD-1.0_JPRB)+ZTY)*ZQTOTA(JLON,JLEV)
      ZTHI=PT(JLON,JLEV+1)*(1.0_JPRB-&
      & (PQL(JLON,JLEV+1)*RLV(PT(JLON,JLEV+1)))+PQI(JLON,JLEV+1)*RLS(PT(JLON,JLEV+1)))+&
      & (PT(JLON,JLEV+1)*(RCPV/RCPD-1.0_JPRB)+ZTY)*ZQTOTA(JLON,JLEV+1)

      ZIT=2.0_JPRB/(PT(JLON,JLEV)+PT(JLON,JLEV+1))
      ZRITKE=ZIT*((ZTH-ZTHI)+ZDPHIA(JLON,JLEV)/RCPD)/ZCISA(JLON,JLEV)

      ZRIFTKE=-(SQRT((ZRITKE*ZDELTH_C3+ETKE_RIFC_MAF)**2&
       &       -4.0_JPRB*ZRITKE*ZDELTH_C3*ETKE_RIFC_MAF**2)&
       &       -(ZRITKE*ZDELTH_C3+ETKE_RIFC_MAF))&
       &        /(2.0_JPRB*ETKE_RIFC_MAF)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/ETKE_RIFC_MAF)/(1.0_JPRB-ZRIFTKE)

      ZDAUX(JLON,JLEV)=ZPHI3TKE*ETKE_RIFC_MAF
      ZDELBI(JLON,JLEV)=ZQSAT(JLON,JLEV)-PQ(JLON,JLEV)&
       & -ZQSAT(JLON,JLEV+1)+PQ(JLON,JLEV+1)&
       & +PQL(JLON,JLEV)*(RLV(PT(JLON,JLEV))/ZCPDTY-1.0_JPRB)&
       & +PQI(JLON,JLEV)*(RLS(PT(JLON,JLEV))/ZCPDTY-1.0_JPRB)&
       & -PQL(JLON,JLEV+1)*(RLV(PT(JLON,JLEV+1))/ZCPDTY-1.0_JPRB)&
       &  -PQI(JLON,JLEV+1)*(RLS(PT(JLON,JLEV+1))/ZCPDTY-1.0_JPRB)
      ZAUX(JLON,JLEV)=ZRCVPP *&
       & MIN(0.0_JPRB,-ZDELBI(JLON,JLEV))*ZIT*ZTY*ZDAUX(JLON,JLEV)
    ENDDO
  ENDDO
ELSEIF(LCVPP .AND. (.NOT.LCOEFK_THS1)) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDELBI(JLON,JLEV)=ZQSAT(JLON,JLEV)-ZQTOTA(JLON,JLEV)&
       & -ZQSAT(JLON,JLEV+1)+ZQTOTA(JLON,JLEV+1)
      ZDELBI(JLON,JLEV)=SIGN(1.0_JPRB,ZDELBI(JLON,JLEV))*&
      &MAX(ZEPS2,ABS(ZDELBI(JLON,JLEV)))
      ZMLSCPE=0.5_JPRB*(PLSCPE(JLON,JLEV)+PLSCPE(JLON,JLEV+1))
      !ZDAUX(JLON,JLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RD &
      ! & *(ZQTOTA(JLON,JLEV+1)-ZQTOTA(JLON,JLEV)&
      ! & +GCCSV*(ZQSAT(JLON,JLEV)-ZQSAT(JLON,JLEV+1))&
      ! & *PAPRS(JLON,JLEV)/PAPRS(JLON,KLEV))&
      ! & *ZMLSCPE-ZDTETA(JLON,JLEV)))
      ZDAUX(JLON,JLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,&
      & ZCPDTY*(ZQTOTA(JLON,JLEV+1)-ZQTOTA(JLON,JLEV))+&
      & ZSLAMA(JLON,JLEV+1)-ZSLAMA(JLON,JLEV)))

      ZAUX(JLON,JLEV)=ZRCVPP *&
       & MIN(0.0_JPRB,-ZDELBI(JLON,JLEV))*ZMLSCPE*ZDAUX(JLON,JLEV)
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDELBI(JLON,JLEV)=ZEPS2
      ZDAUX(JLON,JLEV)=0.0_JPRB
      ZAUX(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

! ZQPRIM     : SPECIFIC HUMIDITY AFTER MIXING.
! ZDSEPRIM   : DRY STATIC ENERGY AFTER MIXING.
! ZBETA1     : BETA COEFFICIENT -1, MULTIPLIED BY THE DAMPING FACTOR,
!              USED FOR INCREASING THE ACCURACY OF THE ANTIFIBRILLATION
!              COMPUTATION IN THE CASE OF SHALLOW CONVECTION  

IF (LLAFAD) THEN 
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZQPRIM=(ZQTOTA(JLON,JLEV)*PDELP(JLON,JLEV)&
       & +ZQTOTA(JLON,JLEV)*PDELP(JLON,JLEV+1))&
       & /(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1))    
      ZDSEPRIM=(ZDSE(JLON,JLEV)*PDELP(JLON,JLEV)&
       & +ZDSE(JLON,JLEV+1)*PDELP(JLON,JLEV+1))&
       & /(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1))    
      ZDCP=RCPD+ZCPVMD*ZQPRIM  
      ZTPRIM1=(ZDSEPRIM-PAPHIF(JLON,JLEV))/ZDCP  
      ZTPRIM2=(ZDSEPRIM-PAPHIF(JLON,JLEV+1))/ZDCP  
      ZDELT1=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTPRIM1))*ZNEIGE  
      ZDELT2=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTPRIM2))*ZNEIGE  
      ZEW=FOEW(ZTPRIM1,ZDELT1)  
      ZEPS=ZEW/PAPRSF(JLON,JLEV)  
      ZQSAT1=FOQS(ZEPS)  
      ZEW=FOEW(ZTPRIM2,ZDELT2)  
      ZEPS=ZEW/PAPRSF(JLON,JLEV+1)  
      ZQSAT2=FOQS (ZEPS)  
      ZBETA1(JLON,JLEV)=MAX(0.0_JPRB,(ZQSAT2-ZQSAT1)/ZDELBI(JLON,JLEV))*&
      & ZDAUX(JLON,JLEV)/XDAMP  
      ZBETA1(JLON,JLEV)=MAX(ZBETA1(JLON,JLEV),ZBETA1(JLON,JLEV-1))  
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZBETA1(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF  

!     CALCUL DE STABILITE.
!     STABILITY COMPUTATION.

DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    !'moist' Ri
    ZRITKEA(JLON,JLEV)=ZDPHIA(JLON,JLEV)*(ZDTETA(JLON,JLEV)+&
    &RD*ZAUX(JLON,JLEV))*ZRTI(JLON,JLEV)/ZCISA(JLON,JLEV) 
    ZRITKEA(JLON,JLEV)=SIGN(MAX(ZEPS2,&
    & ABS(ZRITKEA(JLON,JLEV))),ZRITKEA(JLON,JLEV))
    !'dry' Ri
    ZRITKEDA(JLON,JLEV)=ZDPHIA(JLON,JLEV)*(ZDTETA(JLON,JLEV)+&
    &XMUCVPP*RD*ZAUX(JLON,JLEV))*ZRTI(JLON,JLEV)/ZCISA(JLON,JLEV)
    ZRITKEDA(JLON,JLEV)=SIGN(MAX(ZEPS2,&
    & ABS(ZRITKEDA(JLON,JLEV))),ZRITKEDA(JLON,JLEV))
  ENDDO
ENDDO

IF (LLAFAD) THEN
 
  ! computation of P and R from dry Ri
  CALL ACRI2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
   & ZRITKEDA,Z_ETKE_P,Z_ETKE_R)

  ! computation of F_H without shallow convection - dry case
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ZRITKE=ZRITKEDA(JLON,JLEV)
   
      ZRIFTKE=-(SQRT((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2-4.0_JPRB*ZRITKE*C3TKEFREE*&
       & Z_ETKE_R(JLON,JLEV)*Z_ETKE_P(JLON,JLEV)**2)-&
       & (ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))
      !security
      ZRIFTKE=MIN(ZRIFTKE,ETKE_RIFC-ZEPS8)

      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)

      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*&
      & ZPHI3TKE*ZCHI3TKE**2,0.0_JPRB))
      ZFTTKE_D(JLON,JLEV)=ZFMTKE*ZPHI3TKE/ZCHI3TKE
    ENDDO
  ENDDO
 
  ! computation of P and R from moist Ri
  CALL ACRI2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
   & ZRITKEA,Z_ETKE_P,Z_ETKE_R)

  ! computation of F_H with shallow convection - dry case
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA

      ZRITKE=ZRITKEA(JLON,JLEV)
   
      ZRIFTKE=-(SQRT((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2-4.0_JPRB*ZRITKE*C3TKEFREE*&
       & Z_ETKE_R(JLON,JLEV)*Z_ETKE_P(JLON,JLEV)**2)-&
       & (ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))
      !security
      ZRIFTKE=MIN(ZRIFTKE,ETKE_RIFC-ZEPS8)

      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)

      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*&
      & ZPHI3TKE*ZCHI3TKE**2,0.0_JPRB))
      ZFTTKE_S(JLON,JLEV)=ZFMTKE*ZPHI3TKE/ZCHI3TKE
    ENDDO
  ENDDO

ENDIF

!Computation of modified Ri - with moist AF influence
IF (LLAFAD) THEN
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
       ! Fh(Ri')=Fh(Ri)+(Fh(Ri*)-Fh(Ri))/(1+(beta-1)/XDAMP*G*(Fh(Ri*)-Fh(Ri)))
      ZFTTKE = ZFTTKE_D(JLON,JLEV)+(ZFTTKE_S(JLON,JLEV)-ZFTTKE_D(JLON,JLEV))&
       &   /(1.0_JPRB+ZBETA1(JLON,JLEV)*(ZFTTKE_S(JLON,JLEV)-ZFTTKE_D(JLON,JLEV))&
       &   *ZGDT*2.0_JPRB/(PDELP(JLON,JLEV)+PDELP(JLON,JLEV+1))&
       &   *(ZUGLMT2(JLON,JLEV)*PAPRS(JLON,JLEV)*ZRTI(JLON,JLEV)&
       &   /(ZDPHIA(JLON,JLEV)**2)))
      ZFTTKE_C=ZFTTKE

      ZS_FRAC=ZC3TKE(JLON,JLEV)/C3TKEFREE
      ZS_FTRIFC2=(ZFTTKE*Z_ETKE_P(JLON,JLEV))**2
      ZS_A = ZS_FRAC*Z_ETKE_P(JLON,JLEV)
      ZS_B = ZS_FRAC*(-Z_ETKE_R(JLON,JLEV)-2.0_JPRB*Z_ETKE_P(JLON,JLEV))&
       &    +ZS_FTRIFC2*Z_ETKE_R(JLON,JLEV)-1.0_JPRB
      ZS_C = ZS_FRAC*(2.0_JPRB*Z_ETKE_R(JLON,JLEV)+Z_ETKE_P(JLON,JLEV))+&
       &     (1.0_JPRB-3.0_JPRB*ZS_FTRIFC2)*Z_ETKE_R(JLON,JLEV)/&
       &     Z_ETKE_P(JLON,JLEV)+2.0_JPRB
      ZS_D = ZS_FRAC*(-Z_ETKE_R(JLON,JLEV))+(3.0_JPRB*ZFTTKE**2*&
       &     Z_ETKE_P(JLON,JLEV)-2.0_JPRB)*Z_ETKE_R(JLON,JLEV)/&
       &     Z_ETKE_P(JLON,JLEV)-1.0_JPRB
      ZS_E = (1.0_JPRB-ZFTTKE**2)*Z_ETKE_R(JLON,JLEV)/Z_ETKE_P(JLON,JLEV)

      ZS_P = (-3.0_JPRB*ZS_B**2 + 8.0_JPRB*ZS_A*ZS_C)/(8.0_JPRB*ZS_A**2)
      ZS_Q = (ZS_B**3 - 4.0_JPRB*ZS_A*ZS_B*ZS_C + 8.0_JPRB*ZS_D*ZS_A**2)&
       &   /(8.0_JPRB*ZS_A**3)
      ZS_R = (-3.0_JPRB*ZS_B**4 + 16.0_JPRB*ZS_A*ZS_B**2*ZS_C -&
       &      64.0_JPRB*ZS_A**2*ZS_B*ZS_D +&
       &      256.0_JPRB*ZS_A**3*ZS_E)/(256.0_JPRB*ZS_A**4)
!   solve cubic resolvent
      ZS_AA4 =  8.0_JPRB
      ZS_AA3 = -4.0_JPRB*ZS_P
      ZS_AA2 = -8.0_JPRB*ZS_R
      ZS_AA1 =  4.0_JPRB*ZS_P*ZS_R - ZS_Q**2

      ! cubic problem only
      ZS_CW=ZS_AA3/ZS_AA4*ZS_CTHIRD
      ZS_CP=(ZS_AA2/ZS_AA4*ZS_CTHIRD-ZS_CW**2)**3
      ZS_CQ=-0.5_JPRB*(2.0_JPRB*ZS_CW**3-(ZS_AA2*ZS_CW-ZS_AA1)/ZS_AA4)
      ZS_CDIS=ZS_CQ**2+ZS_CP

      ZS_ZSOL=-HUGE(1.0_JPRB)
      IF (ZS_CDIS < 0.0_JPRB) THEN
!         three real solutions!
!         Confine the argument of ACOS to the interval [-1;1]!
        ZS_CPHI=ACOS(MIN(1._JPRB,MAX(-1._JPRB,ZS_CQ/SQRT(-ZS_CP))))
        ZS_CP=2.0_JPRB*(-ZS_CP)**(0.5_JPRB*ZS_CTHIRD)
        ZS_CU1=ZS_CP*COS((ZS_CPHI+2.0_JPRB*RPI)*ZS_CTHIRD)-ZS_CW
        ZS_CU2=ZS_CP*COS((ZS_CPHI+4.0_JPRB*RPI)*ZS_CTHIRD)-ZS_CW
        ZS_CU3=ZS_CP*COS((ZS_CPHI+6.0_JPRB*RPI)*ZS_CTHIRD)-ZS_CW
        ZS_CX1=MIN(ZS_CU1,ZS_CU2,ZS_CU3)
        ZS_CX2=MAX(MIN(ZS_CU1,ZS_CU2),MIN(ZS_CU1,ZS_CU3),&
           &  MIN(ZS_CU2,ZS_CU3))
        ZS_CX3=MAX(ZS_CU1,ZS_CU2,ZS_CU3)
        ZS_ZSOL = MAX(ZS_ZSOL,ZS_CX1,ZS_CX2,ZS_CX3)
      ELSE
!     only one real solution!
        ZS_CPHI=SQRT(ZS_CDIS)
        ZS_CX1=SIGN(ABS(ZS_CQ+ZS_CPHI)**ZS_CTHIRD,ZS_CQ+ZS_CPHI)+&
         &       SIGN(ABS(ZS_CQ-ZS_CPHI)**ZS_CTHIRD,ZS_CQ-ZS_CPHI)-ZS_CW
        ZS_ZSOL = MAX(ZS_ZSOL,ZS_CX1)
      ENDIF

      ZS_CX1 = ZS_ZSOL
      ZS_XK2 = MAX(ZEPS2,2.0_JPRB * ZS_CX1 - ZS_P)
      ZS_XK  = SQRT(ZS_XK2)
!-----------------------------------------------
      IF (ZS_XK == 0.0_JPRB) THEN
        ZS_XL2 = ZS_CX1**2 - ZS_R
!       if (ZS_XL2.lt.0.0_JPRB) then
!         'Sorry, no solution'
!       endif
        ZS_XL  = SQRT(ZS_XL2)
      ELSE
        ZS_XL = ZS_Q/(2.0_JPRB * ZS_XK)
      ENDIF
!-----------------------------------------------
      ZS_SQP = ZS_XK2 - 4.0_JPRB*(ZS_CX1 + ZS_XL)
      ZS_SQM = ZS_XK2 - 4.0_JPRB*(ZS_CX1 - ZS_XL)
  
      ZS_CPOM= ZS_B/(4.0_JPRB*ZS_A)
  
      ZS_FTDIFF=1.E+10
      ZS_FTDIFF2=1.E+10
      ZS_RITKE=0.0_JPRB
      !we have to choose correct root (out of 4)
      ! Note: It is manually unrolled here to ease vectorization

      ! first root 
      ZS_SOL = 0.5_JPRB*( ZS_XK + SQRT(ABS(ZS_SQP))) - ZS_CPOM
      ZS_SOL =MIN(1.0_JPRB-ZEPS2,ZS_SOL) !security
      ZRITKE=ZS_SOL*Z_ETKE_P(JLON,JLEV)**2/&
       &(Z_ETKE_R(JLON,JLEV)*C3TKEFREE)*&
       &(ZS_SOL-Z_ETKE_R(JLON,JLEV)/Z_ETKE_P(JLON,JLEV))/&
       & (ZS_SOL-1.0_JPRB)
      ! computation of FH
      ZRIFTKE=-(SQRT(MAX((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2&
       & -4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R(JLON,JLEV)*&
       & Z_ETKE_P(JLON,JLEV)**2,0.0_JPRB))&
       & -(ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))
  
      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)

      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*ZPHI3TKE*ZCHI3TKE**2,&
       &  0.0_JPRB))
      ZFTTKE=ZFMTKE*ZPHI3TKE/ZCHI3TKE

      ZS_FTDIFF2=ABS(ZFTTKE_C-ZFTTKE)

      ZS_RITKE=ZRITKE
      ZS_FTDIFF=ZS_FTDIFF2

      ! second root
      ZS_SOL = 0.5_JPRB*( ZS_XK - SQRT(ABS(ZS_SQP))) - ZS_CPOM
      ZS_SOL =MIN(1.0_JPRB-ZEPS2,ZS_SOL) !security
      ZRITKE=ZS_SOL*Z_ETKE_P(JLON,JLEV)**2/&
       &(Z_ETKE_R(JLON,JLEV)*C3TKEFREE)*&
       &(ZS_SOL-Z_ETKE_R(JLON,JLEV)/Z_ETKE_P(JLON,JLEV))/&
       & (ZS_SOL-1.0_JPRB)
      ! computation of FH
      ZRIFTKE=-(SQRT(MAX((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2&
       & -4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R(JLON,JLEV)*&
       & Z_ETKE_P(JLON,JLEV)**2,0.0_JPRB))&
       & -(ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))
  
      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
  
      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*ZPHI3TKE*ZCHI3TKE**2,&
       &  0.0_JPRB))
      ZFTTKE=ZFMTKE*ZPHI3TKE/ZCHI3TKE
      
      ZS_FTDIFF2=MIN(ABS(ZFTTKE_C-ZFTTKE),ZS_FTDIFF)
  
      ZS_RITKE=((1.0_JPRB-SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZRITKE+&
              & (1.0_JPRB+SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZS_RITKE)/2.0_JPRB
      ZS_FTDIFF=ZS_FTDIFF2

      ! third root
      ZS_SOL = 0.5_JPRB*(-ZS_XK + SQRT(ABS(ZS_SQM))) - ZS_CPOM
      ZS_SOL =MIN(1.0_JPRB-ZEPS2,ZS_SOL) !security
      ZRITKE=ZS_SOL*Z_ETKE_P(JLON,JLEV)**2/&
       &(Z_ETKE_R(JLON,JLEV)*C3TKEFREE)*&
       &(ZS_SOL-Z_ETKE_R(JLON,JLEV)/Z_ETKE_P(JLON,JLEV))/&
       & (ZS_SOL-1.0_JPRB)
      ! computation of FH
      ZRIFTKE=-(SQRT(MAX((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2&
       & -4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R(JLON,JLEV)*&
       & Z_ETKE_P(JLON,JLEV)**2,0.0_JPRB))&
       & -(ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))

      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
  
      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*ZPHI3TKE*ZCHI3TKE**2,&
       &  0.0_JPRB))
      ZFTTKE=ZFMTKE*ZPHI3TKE/ZCHI3TKE

      ZS_FTDIFF2=MIN(ABS(ZFTTKE_C-ZFTTKE),ZS_FTDIFF)
      ZS_RITKE=((1.0_JPRB-SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZRITKE+&
              & (1.0_JPRB+SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZS_RITKE)/2.0_JPRB
      ZS_FTDIFF=ZS_FTDIFF2
  
    ! fourth (and last) root
      ZS_SOL = 0.5_JPRB*(-ZS_XK - SQRT(ABS(ZS_SQM))) - ZS_CPOM
      ZS_SOL =MIN(1.0_JPRB-ZEPS2,ZS_SOL) !security
      ZRITKE=ZS_SOL*Z_ETKE_P(JLON,JLEV)**2/&
       &(Z_ETKE_R(JLON,JLEV)*C3TKEFREE)*&
       &(ZS_SOL-Z_ETKE_R(JLON,JLEV)/Z_ETKE_P(JLON,JLEV))/&
       & (ZS_SOL-1.0_JPRB)
      ! computation of FH
      ZRIFTKE=-(SQRT(MAX((Z_ETKE_R(JLON,JLEV)*(ZRITKE*C3TKEFREE+&
       & Z_ETKE_P(JLON,JLEV)))**2&
       & -4.0_JPRB*ZRITKE*C3TKEFREE*Z_ETKE_R(JLON,JLEV)*&
       & Z_ETKE_P(JLON,JLEV)**2,0.0_JPRB))&
       & -(ZRITKE*C3TKEFREE+Z_ETKE_P(JLON,JLEV))*&
       & Z_ETKE_R(JLON,JLEV))/(2.0_JPRB*Z_ETKE_P(JLON,JLEV))

      ZCHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE)

      ZFMTKE=SQRT(MAX(ZCHI3TKE**3-ZRITKE*ZC3TKE(JLON,JLEV)*ZPHI3TKE*ZCHI3TKE**2,&
       &  0.0_JPRB))
      ZFTTKE=ZFMTKE*ZPHI3TKE/ZCHI3TKE

      ZS_FTDIFF2=MIN(ABS(ZFTTKE_C-ZFTTKE),ZS_FTDIFF)
      ZRITKE=((1.0_JPRB-SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZRITKE+&
            & (1.0_JPRB+SIGN(1.0_JPRB,ZS_FTDIFF2-ZS_FTDIFF))*ZS_RITKE)/2.0_JPRB

      PMRIPP(JLON,JLEV)=ZRITKE
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      PMRIPP(JLON,JLEV)=ZRITKEA(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!security
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    PMRIPP(JLON,JLEV)=SIGN(MAX(ZEPS2,&
    &ABS(PMRIPP(JLON,JLEV))),PMRIPP(JLON,JLEV))
  ENDDO
ENDDO

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACMRIS',1,ZHOOK_HANDLE)
END SUBROUTINE ACMRIS
