!OPTIONS XOPT(NOEVAL)
!OPTION! -O nochg
SUBROUTINE ACMRISS( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PQ,PQL,PQI,PQSAT,&
 & PR,PT,PU,PV,PLSCPE, &
 ! - INPUT  1D .
 & PGZ0, &
 ! - OUTPUT 2D .
 & PMN2PP,PMRIPP)

!**** *ACMRIS*  moist Ri' computation - Ri*/** with moist AF
!        *CALL* *ACMRIS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQL        : EAU LIQUIDE
! PQI        : GLACE
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------
! PMRIPP     : MOIST Ri *,Ri**
!---------------------------------------------------------------
!     Auteur.
!     -------
!        2011-Nov, I. Bastak-Duran

!     Modifications.
!     --------------
!      F. Vana 27-Jan-2012 : Phased to CY38
!      I. Bastak-Duran 19-Jan-2013 : Correction of moist Ri computation.
!      I. Bastak-Duran Oct-2014: major rewriting.

!---------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RKAPPA   ,&
 & RV       ,RCPD     ,RCPV     ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD, RATM, RPI

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI      (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSCPE   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0     (KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMRIPP   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMN2PP   (KLON,0:KLEV)
INTEGER(KIND=JPIM) :: JLEV, JLON

REAL(KIND=JPRB) :: ZDELTA, ZGLU, ZRCVPP, ZZ
REAL(KIND=JPRB) :: ZGDT
REAL(KIND=JPRB) :: ZCPVMD, ZEW, ZEPS, ZEPS2, ZMLSCPE,ZTC
REAL(KIND=JPRB) :: ZQSAT(KLON,KLEV)
REAL(KIND=JPRB) :: ZQTOTA(KLON,KLEV),ZSLAMA(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPHIA(KLON,0:KLEV),ZCISA(KLON,0:KLEV),&
& ZAUX(KLON,0:KLEV),ZDAUX(KLON,0:KLEV),ZDELBI(KLON,0:KLEV),&
& ZRTI(KLON,0:KLEV),ZDTETA(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZDELTH_C3
REAL(KIND=JPRB) :: ZTY,ZCPDTY,ZTH,ZTHI,ZIT,ZLAMCP
REAL(KIND=JPRB) :: ZRITKE,ZRIFTKE,ZPHI3TKE
REAL(KIND=JPRB) :: ZNEIGE

#include "fcttrm.func.h"


!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACMRISS',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & ETKE_RIFC_MAF=>YDML_PHY_MF%YRPHY0%ETKE_RIFC_MAF, ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, &
 & GRCVPP=>YDML_PHY_MF%YRPHY0%GRCVPP, GCCSV=>YDML_PHY_MF%YRPHY0%GCCSV, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & L3MT=>YDML_PHY_MF%YRPHY%L3MT, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, LCOEFK_THS1=>YDML_PHY_MF%YRPHY%LCOEFK_THS1, &
 & LSTRAPRO=>YDML_PHY_MF%YRPHY%LSTRAPRO, LCVPP=>YDML_PHY_MF%YRPHY%LCVPP)
!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     LE CARRE DU CISAILLEMENT DE VENT).

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS (FOR
!     THE SQUARE OF THE WIND SHEAR).

ZTY=2362.0_JPRB
ZCPDTY=ZTY*RCPD
ZCPVMD=RCPV-RCPD
ZDELTH_C3=2.0_JPRB*ETKE_RIFC_MAF/(1.0_JPRB-ETKE_RIFC_MAF)
ZLAMCP=RCPV/RCPD-1.0_JPRB

ZGDT=RG*TSPHY
ZGLU=RG*ALMAV

ZRCVPP=1.0_JPRB/GRCVPP

! security constant
ZEPS2=EPSILON(1.0_JPRB)*100.0_JPRB

! PREPARATIONS FOR SHALLOW CONVECTION PAR.
IF(LNEIGE)THEN
  ZNEIGE=1.0_JPRB
ELSE
  ZNEIGE=0.0_JPRB
ENDIF

! is this IF needed?
IF (L3MT.OR.LSTRAPRO) THEN
  DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
       ZTC=PT(JLON,JLEV)-(FOLH(PT(JLON,JLEV),0.0_JPRB)*PQL(JLON,JLEV)&
        & + FOLH(PT(JLON,JLEV),1.0_JPRB)*PQI(JLON,JLEV))/PCP(JLON,JLEV)
       ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTC))*ZNEIGE
       ZEW=FOEW(ZTC,ZDELTA)
       ZEPS=ZEW/PAPRSF(JLON,JLEV)
       ZQSAT(JLON,JLEV)=FOQS(ZEPS)
     ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZQSAT(JLON,JLEV)=PQSAT(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!full levels
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZQTOTA(JLON,JLEV)=PQ(JLON,JLEV)+PQL(JLON,JLEV)+PQI(JLON,JLEV)
    ZSLAMA(JLON,JLEV)=RCPD*PT(JLON,JLEV)*(1.0_JPRB+ZQTOTA(JLON,JLEV)*ZLAMCP)+&
    & PAPHIF(JLON,JLEV)-&
    & (PQL(JLON,JLEV)*RLV(PT(JLON,JLEV))+PQI(JLON,JLEV)*RLS(PT(JLON,JLEV)))
  ENDDO
ENDDO

!half levels
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZDPHIA(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
    ZZ=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))&
     & -PAPHI(JLON,KLEV)+PGZ0(JLON))

    ZCISA(JLON,JLEV)=(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2&
     &  +(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2&
     &  +(GCISMIN*ZDPHIA(JLON,JLEV)/(RG*(1.0_JPRB+ZZ/ZGLU)))**2

    ZDTETA(JLON,JLEV)=PR(JLON,JLEV)*PT(JLON,JLEV)-PR(JLON,JLEV+1)*&
     & PT(JLON,JLEV+1)+RKAPPA*ZDPHIA(JLON,JLEV)  

    ZRTI(JLON,JLEV)=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+&
    &PR(JLON,JLEV+1)*PT(JLON,JLEV+1))

  ENDDO
ENDDO


IF (LCVPP .AND. LCOEFK_THS1) THEN  ! THS1 version
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTH=PT(JLON,JLEV)*(1.0_JPRB-&
      & (PQL(JLON,JLEV)*RLV(PT(JLON,JLEV)))+PQI(JLON,JLEV)*RLS(PT(JLON,JLEV)))+&
      & (PT(JLON,JLEV)*(RCPV/RCPD-1.0_JPRB)+ZTY)*ZQTOTA(JLON,JLEV)
      ZTHI=PT(JLON,JLEV+1)*(1.0_JPRB-&
      & (PQL(JLON,JLEV+1)*RLV(PT(JLON,JLEV+1)))+PQI(JLON,JLEV+1)*RLS(PT(JLON,JLEV+1)))+&
      & (PT(JLON,JLEV+1)*(RCPV/RCPD-1.0_JPRB)+ZTY)*ZQTOTA(JLON,JLEV+1)

      ZIT=2.0_JPRB/(PT(JLON,JLEV)+PT(JLON,JLEV+1))
      ZRITKE=ZIT*((ZTH-ZTHI)+ZDPHIA(JLON,JLEV)/RCPD)/ZCISA(JLON,JLEV)

      ZRIFTKE=-(SQRT((ZRITKE*ZDELTH_C3+ETKE_RIFC_MAF)**2&
       &       -4.0_JPRB*ZRITKE*ZDELTH_C3*ETKE_RIFC_MAF**2)&
       &       -(ZRITKE*ZDELTH_C3+ETKE_RIFC_MAF))&
       &        /(2.0_JPRB*ETKE_RIFC_MAF)
      ZPHI3TKE=(1.0_JPRB-ZRIFTKE/ETKE_RIFC_MAF)/(1.0_JPRB-ZRIFTKE)

      ZDAUX(JLON,JLEV)=ZPHI3TKE*ETKE_RIFC_MAF
      ZDELBI(JLON,JLEV)=ZQSAT(JLON,JLEV)-PQ(JLON,JLEV)&
       & -ZQSAT(JLON,JLEV+1)+PQ(JLON,JLEV+1)&
       & +PQL(JLON,JLEV)*(RLV(PT(JLON,JLEV))/ZCPDTY-1.0_JPRB)&
       & +PQI(JLON,JLEV)*(RLS(PT(JLON,JLEV))/ZCPDTY-1.0_JPRB)&
       & -PQL(JLON,JLEV+1)*(RLV(PT(JLON,JLEV+1))/ZCPDTY-1.0_JPRB)&
       &  -PQI(JLON,JLEV+1)*(RLS(PT(JLON,JLEV+1))/ZCPDTY-1.0_JPRB)
      ZAUX(JLON,JLEV)=ZRCVPP *&
       & MIN(0.0_JPRB,-ZDELBI(JLON,JLEV))*ZIT*ZTY*ZDAUX(JLON,JLEV)
    ENDDO
  ENDDO
ELSEIF(LCVPP .AND. (.NOT.LCOEFK_THS1)) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDELBI(JLON,JLEV)=ZQSAT(JLON,JLEV)-ZQTOTA(JLON,JLEV)&
       & -ZQSAT(JLON,JLEV+1)+ZQTOTA(JLON,JLEV+1)
      ZDELBI(JLON,JLEV)=SIGN(1.0_JPRB,ZDELBI(JLON,JLEV))*&
      &MAX(ZEPS2,ABS(ZDELBI(JLON,JLEV)))
      ZMLSCPE=0.5_JPRB*(PLSCPE(JLON,JLEV)+PLSCPE(JLON,JLEV+1))
      !ZDAUX(JLON,JLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RD &
      ! & *(ZQTOTA(JLON,JLEV+1)-ZQTOTA(JLON,JLEV)&
      ! & +GCCSV*(ZQSAT(JLON,JLEV)-ZQSAT(JLON,JLEV+1))&
      ! & *PAPRS(JLON,JLEV)/PAPRS(JLON,KLEV))&
      ! & *ZMLSCPE-ZDTETA(JLON,JLEV)))
      ZDAUX(JLON,JLEV)=MAX(0.0_JPRB,SIGN(1.0_JPRB,&
      & ZCPDTY*(ZQTOTA(JLON,JLEV+1)-ZQTOTA(JLON,JLEV))+&
      & ZSLAMA(JLON,JLEV+1)-ZSLAMA(JLON,JLEV)))

      ZAUX(JLON,JLEV)=ZRCVPP *&
       & MIN(0.0_JPRB,-ZDELBI(JLON,JLEV))*ZMLSCPE*ZDAUX(JLON,JLEV)
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDELBI(JLON,JLEV)=ZEPS2
      ZDAUX(JLON,JLEV)=0.0_JPRB
      ZAUX(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    PMRIPP(JLON,JLEV)=ZDPHIA(JLON,JLEV)*(ZDTETA(JLON,JLEV)+&
    &RD*ZAUX(JLON,JLEV))*ZRTI(JLON,JLEV)/ZCISA(JLON,JLEV) 
    !security
    PMRIPP(JLON,JLEV)=SIGN(MAX(ZEPS2,&
    &ABS(PMRIPP(JLON,JLEV))),PMRIPP(JLON,JLEV))

    PMN2PP(JLON,JLEV)=PMRIPP(JLON,JLEV)*ZCISA(JLON,JLEV)*&
    &(RG/ZDPHIA(JLON,JLEV))**2
  ENDDO
ENDDO

!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACMRISS',1,ZHOOK_HANDLE)
END SUBROUTINE ACMRISS
