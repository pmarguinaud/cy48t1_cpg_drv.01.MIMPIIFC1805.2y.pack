SUBROUTINE ACMTUD (YDGEM,YDDIM,YDEGEO,YDLDDH,YDMDDH,YDRIP,YDML_PHY_MF, KIDIA,KFDIA,KLON,KTDIA,KLEV,KTRA,&
!-----------------------------------------------------------------------
! - INPUT  2D .
& PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PVERVEL,PCP,PLH,PR,&
& PDELP,PLNPR,&
& PQ,PQLIS,PQSAT,PQW,&
& PRDELP,PCVGQ,PT,PTW,PU,PV,PTKE,PTRA,PSMOOTRA,PEVAPO,&
! - INPUT 1D .
& PGM, PTS,&
! - OUTPUT 2D .
& PDIFCQ,PDIFCQL,PDIFCQI,PDIFCS,PDIFCTH,PPRODTH,&
& PFCCQL,PFCCQI,PSTRCU,PSTRCV,PSTRCTRA,&
& PFHMLTS,PFHEVPP,PFPEVPCL,PFPEVPCN,&
& PFPLCL,PFPLCN,PMF_UP,PMU,PMD,&
& PNEBC,PQLIC,PTU,PQU,KNLAB,PENTR_U,PDETR_U,PENTR_D,PDETR_D,&
& PENTR_U_TOT,&
! - OUTPUT 1D .
& KNND,PCAPE,PALF,PALF_CAPE,PALF_CVGQ,&
! - INPUT/OUTPUT 2D .
& PUDAL,PUDOM,PDDAL,PDDOM,YDDDH)
!DEC$ OPTIMIZE:3

!**** *ACMTUD * - MASS FLUX PROGNOSTIC UPDRAFT OF THE PCMT CONVECTION SCHEME.

! SUJET.
!     ------

!**   INTERFACE.
!     ----------
!        *CALL* *ACMTUD*
!-----------------------------------------------------------------------
! -   INPUT ARGUMENTS.
!     ----------------

! - PHYSICS DIMENSIONNING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KTRA       : NUMBER OF TRACERS (PASSIVE VARIABLES)

! - PHYSICS VARIABLES (ALPHABETICALLY IN EACH CATEGORY)

! - 2D (0:KLEV) .

! PAPHI      : HALF LEVEL GEOPOTENTIAL
! PAPRS      : HALF LEVEL PRESSURE

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : FULL LEVEL GEOPOTENTIAL
! PAPRSF     : FULL LEVEL PRESSURE
! PVERVEL    : OMEGA/P
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PR         : CONSTANTE DE L'AIR HUMIDE.
! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : MICROPHYSICAL WATER VAPOUR SPECIFIC HUMIDITY
! PQLIS      : MICROPHYSICAL RESOLVED CLOUD WATER SPECIFIC CONTENTSIN THE TIME-STEP
! PQW        : WET BULB SPECIFIC MOISTURE
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PCVGQ      : CONVERGENCE D'HUMIDITE.
! PT         : TEMPERATURE.
! PTW        : WET BULB TEMPERATURE.
! PU         : U WIND COMPONENT
! PV         : V WIND COMPONENT
! PTRA       : TRACER CONCENTRATION
! PSMOOTRA   : SMOOTHING PARAMETER FOR TRACER CONCENTRATION
! PTKE       : TKE
! PEVAPO     : evaporation from previous time step

! - 2D (0:KLEV) .

! - 1D .

! PGM        : FACTEUR D'ECHELLE.
! PTS        : temperature de surface.

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PDIFCQ     : WATER VAPOUR CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQI    : ICE WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCQL    : LIQUID WATER CONVECTIVE SPECIFIC MOISTURE FLUX
! PDIFCS     : DRY STATIC ENERGY CONVECTIVE FLUX
! PDIFCTH    : THETA CONVECTIVE FLUX
! PPRODTH    : THERMAL PRODUCTION
! PFCCQL     : LIQUID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PFCCQI     : SOLID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PSTRCU     : U-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCV     : V-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCTRA   : TRACER CONVECTIVE FLUX
! PFHMLTS    : FLUX DE CHALEUR DU A LA FONTE/GEL.
! PFHEVPP    : FLUX DE CHALEUR DU A L'EVAPORATION DES PRECIPITATIONS.
! PFPEVPCL    : EVAPORATION DES PRECIPITATIONS LIQUIDES.
! PFPEVPCN    : EVAPORATION DES PRECIPITATIONS NEIGEUSES.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PMF_UP     : UPWARD NET MASS FLUX
! PMU        : UPDRAFT MASS FLUX
! PMD        : DOWNDRAFT MASS FLUX

! - 2D (1:KLEV) .

! KNLAB  : CONVECTIVE ACTIVITY INDEX (ACTIVE IF EQUAL TO ONE).
! PTU    : TEMPERATURE OF THE CLOUDY ASCENT.
! PQU    : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! PNEBC  : CONVECTIVE CLOUDINESS.
! PQLIC  : CONVECTIVE CONDENSATE.
! PENTR_U    : ENTRAINMENT IN UPDRAFT (IN S**-1).
! PDETR_U    : DETRAINMENT FROM UPDRAFT (IN S**-1).
! PENTR_D    : ENTRAINMENT IN DOWNDRAFT (IN S**-1).
! PDETR_D    : DETRAINMENT FROM DOWNDRAFT (IN S**-1).
! PENTR_U_TOT: ENTRAINMENT TOTAL (FROM BASE) IN UPDRAFT (IN S**-1).

! - 1D (DIAGNOSTIQUE) .

! KNND   : ZERO WHENEVER A NON PHYSICAL SITUATION OCCURS, ONE OTHERWISE
! PCAPE  : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).

!-----------------------------------------------------------------------

! -   INPUT/OUTPUT ARGUMENTS
!     ----------------------

! - 2D (0:KLEV) .

! - 2D (1:KLEV) .

! PUDAL    : PROGNOSTIC UPDRAFT MESH FRACTION
! PUDOM    : PROGNOSTIC UPDRAFT RELATIVE VELOCITY
! YDDDH    : DDH superstructure
!-----------------------------------------------------------------------

! METHOD.
!     --------

! AUTHOR.
!     -------
! 2011-04-14, J.F. GUEREMY AND J.M. PIRIOU.

! MODIFICATIONS.
!     --------------
! 2013-02-08, J.M. Piriou: protect the LOG in computing ZSIGMA.
! 2013-06-17, J.M. Piriou: LCVNHD NH effects of downdraft on updraft and conversely.
! 2013-06-17, J.M. Piriou: prognostic closure on PALF.
! 2013-07 - G. Bellon  Modifications for MUSC 
! 2014-04-01, J.M. Piriou: LLSINH: SImple NH effect.
! 2014-05   , J.F. Gueremy:
!             * resolution function for closure condition.
!             * CIN threshold for triggering condition.
! 2014-05   , J.F. Gueremy: use ZDQCA instead of ZBCC to compute condensation,
!             to apply the same "ZAUX" reduction in condensation and in transport.
! 2014-05 - D. Saint-Martin : Transport of tracers
! 2014-06 - J.F. Gueremy condensation flux using the same massflux
!             as for the transport flux
! 2014-07-29, Ryad El Khatib: optimize computation time: replace scalars by arrays, etc.
! 2014-09 - J.F. Gueremy new formulation of the resolution function for closure
!             condition and no more smoothing of dw/dp (ZALFAVEC=1.)
! 2015-02 -  I. Beau: Bugfix + some cleanings consistent with cy38 (NCVENTR options)
! 2015-09- J.M Piriou: Phasing of NWP and Climate versions
! 2015-11 - J.F. Gueremy: bugfix concerning the "mass flux" for implicit formulation
! 2015-11 - J.F. Gueremy: explicit transport flux for the dry air potential temperature
!             smoothing of mass flux for the transport fluxes
! 2015-12 - J.M. Piriou: GCVUV factor for momentum transport.
! 2016-02 - A.Alias : optimisation (E.Maisonnave)
! 2016-06 - J.F. Gueremy et J.M. Piriou: debug sign when using ZFORD for ZAUX.
! 2016-06 - J.M. Piriou: reintroduce Ryad El Khatib optimizing computations.
! R. El Khatib 12-Aug-2016 optimization by directive
! 2016-07 - D. Saint-Martin : max value for aerodynamic drag RKDX
! 2016-10 - J.M. Piriou: compute ZALFX as a function of local resolution.
! 2017-03 - J.M Piriou: Technical phasing of NWP and Climate versions (identical results).
! 2017-04 - J.F. Gueremy: New convective variables in the overshoot region
!             (activated if GCVADET > 0., 0. being the default).
! 2017-04-21 - J.M. Piriou: use RNEBCX as a maximum value for convective cloudiness.
! 2017-08-03 - J.M. Piriou: LCVRESDYN approach: convective updraft velocity originates
!              from 3 sources: buoyancy, resolved vertical velocity and TKE.
! 2017-11 - J.F. Gueremy: diag entrainement up total PENTR_U_TOT
! 2017-12 - R. Roehrig: Add downdraft mass flux diagnostic
! 2018-01 - D. St-Martin : smoothing paramater for tracer transport
! 2018-02 - J.F. Gueremy: thermal production for TKE
! 2018-08 - J. Bock: missing initialisations or definitions of
! tracer-related variables along the code
! 2018-08 - J.F. Gueremy: unused argument PSU
!-----------------------------------------------------------------------


USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMMDDH              , ONLY : TMDDH
USE PARKIND1             , ONLY : JPIM, JPRB
USE YOMHOOK              , ONLY : LHOOK,   DR_HOOK

USE YOMDIM               , ONLY : TDIM
USE YOMCST               , ONLY : RATM       ,RG       ,RKAPPA   ,&
 &                                RD       ,RV       ,RCPD     ,&
 &                                RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT    ,&
 &                                RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW    ,&
 &                                RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD    ,&
 &                                RGAMD    ,RPI      ,RA       ,&
 &                                RDT

USE YOMCT0               , ONLY : LELAM
USE YOMGEM               , ONLY : TGEM
USE YEMGEO               , ONLY : TEGEO
USE YOMLSFORC            , ONLY : LMUSCLFA,NMUSCLFA
USE DDH_MIX              , ONLY : ADD_FIELD_3D, NEW_ADD_FIELD_3D, TYP_DDH
USE YOMLDDH              , ONLY : TLDDH
USE YOMCT3               , ONLY : NSTEP
USE YOMSCM               , ONLY : NFRSCM
USE YOMRIP               , ONLY : TRIP

IMPLICIT NONE

!     DUMMY ARGUMENTS.

TYPE(TDIM)        , INTENT(IN)    :: YDDIM
TYPE(TEGEO)       , INTENT(IN)    :: YDEGEO
TYPE(TGEM)        , INTENT(IN)    :: YDGEM
TYPE(TLDDH)       , INTENT(IN)    :: YDLDDH
TYPE(TMDDH)       , INTENT(IN)    :: YDMDDH
TYPE(TRIP)        , INTENT(IN)    :: YDRIP
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM), INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM), INTENT(IN)    :: KLON
INTEGER(KIND=JPIM), INTENT(IN)    :: KTDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KTRA

REAL(KIND=JPRB), INTENT(IN)    :: PALPH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PR(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQLIS(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQW(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTRA(KLON,KLEV,KTRA)
REAL(KIND=JPRB), INTENT(IN)    :: PSMOOTRA(KTRA)
REAL(KIND=JPRB), INTENT(IN)    :: PTKE(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PEVAPO(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PTS(KLON)

INTEGER(KIND=JPIM), INTENT(INOUT) :: KNLAB(KLON,KLEV)
INTEGER(KIND=JPIM), INTENT(INOUT) :: KNND(KLON)
REAL(KIND=JPRB)   , INTENT(INOUT) :: PALF(KLON)

REAL(KIND=JPRB), INTENT(OUT)   :: PCAPE(KLON)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQ(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCTH(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PPRODTH(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCU(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCV(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCTRA(KLON,0:KLEV,KTRA)
REAL(KIND=JPRB), INTENT(OUT)   :: PMF_UP(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PMU(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PMD(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PTU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PQU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PNEBC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQLIC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFHMLTS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PENTR_U(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PDETR_U(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PENTR_D(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PDETR_D(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PENTR_U_TOT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCN(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCN(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFHEVPP(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CAPE(KLON)
REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CVGQ(KLON)

REAL(KIND=JPRB), INTENT(INOUT) :: PUDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PUDOM(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDOM(KLON,KLEV)

TYPE(TYP_DDH)  , INTENT(INOUT) :: YDDDH

! LOCALS.

REAL(KIND=JPRB) :: ZWU(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZWD(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZWU_TKE(KLON,KLEV)
REAL(KIND=JPRB) :: ZKEDD(KLON)
REAL(KIND=JPRB) :: ZWD_PLUS_BUO(KLON,KLEV)

REAL(KIND=JPRB) :: ZTALF_SCE(KLON)
REAL(KIND=JPRB) :: ZTALF_SIN(KLON)

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
  &,ZBET(KLON,KLEV)&
  &,ZDSE(KLON,KLEV)&
  &,ZIFRAC(KLON,KLEV)&
  &,ZLDN(KLON,KLEV),ZPOID(KLON,KLEV)&
  &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV)&
  &,ZTVE(KLON,KLEV),ZTVN(KLON,KLEV)&
  &,ZUM(KLON,KLEV),ZVM(KLON,KLEV),ZQBW(KLON),ZTBW(KLON)&
  &,ZFORM(KLON,0:KLEV),ZDQCA(KLON,0:KLEV)

! Tracer-related local variables
INTEGER(KIND=JPIM) :: JTRA
REAL(KIND=JPRB) :: ZA15(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZTRAM(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZTRAMD(KLON,KLEV,KTRA)
REAL(KIND=JPRB) :: ZSTRCTRAD(KLON,0:KLEV,KTRA)

INTEGER(KIND=JPIM) :: INBAS(KLON,KLEV)
REAL(KIND=JPRB) :: ZCP(KLON)&
  &,ZLB(KLON),ZLH(KLON),ZQB(KLON)&
  &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
  &,ZS15(KLON),ZS16(KLON)&
  &,ZTB(KLON)


INTEGER(KIND=JPIM) :: ISUM, ITOP, JIT, JLEV, JLON

REAL(KIND=JPRB) :: ZARLII,ZBAS, &
  &ZCPSMD, ZCPVMD, &
  &ZCPVMS, ZCPVMW, ZCPWMD, ZDCP, ZDELQ, &
  &ZDELT, ZDELTA, ZDPSGDT, ZDQW, &
  &ZFER, ZEPS16, &
  &ZESP, ZEW, ZEXP, &
  &ZAUX, ZGDT, ZGDTI, &
  &ZLIQ, ZMIX, ZNBA, ZQW, &
  &ZRVMD, ZZ, ZZVAL, &
  &ZB, ZLHTBW, &
  &ZIDT, ZTPHY, &
  &ZINCRQ,ZCPBW, &
  &ZEPSW,ZWEIGHT,ZEPS_BCCS
INTEGER(KIND=JPIM) :: ILEV
REAL(KIND=JPRB) :: ZBETA1RIO,ZA1RIO,ZBRIO,ZCRIO

REAL(KIND=JPRB) :: ZLN(KLON),ZTNH(KLON),ZQNH(KLON),ZVVER(KLON,0:KLEV),ZDWNDP(KLON,KLEV)
REAL(KIND=JPRB) :: ZCAPE(KLON),ZEPS_TUR(KLON,KLEV),ZEPS_ORG(KLON,KLEV),ZDEL_ORG(KLON,KLEV)
REAL(KIND=JPRB) :: ZKD(KLON,KLEV),ZSIGMA(KLON,0:KLEV),ZTBH(KLON),ZQBH(KLON)
REAL(KIND=JPRB) :: ZRBBS(KLON),ZRBHS(KLON),ZCPH,ZTNSEC,ZTVNL,ZTVEL,ZCAPEL(KLON)
REAL(KIND=JPRB) :: ZCIN(KLON),ZCINL(KLON)
REAL(KIND=JPRB) :: ZBUO_W(KLON,KLEV),ZFLO_OMEGA,ZDELPF,ZENTR_TUR,ZKDL,ZVVERDEN,ZVVERDM(KLON,KLEV)
REAL(KIND=JPRB) :: ZVVERDME(KLON,KLEV)
REAL(KIND=JPRB) :: ZBUO_W_POS(KLON,KLEV)
REAL(KIND=JPRB) :: ZDELTAP,ZVVERN,ZEPS_TURL,ZRKD,ZKDX,ZMOD(KLON,KLEV),ZPPS,ZMOD_MIN,ZMOD_MAX
REAL(KIND=JPRB) :: ZENTRO,ZEPS_ORG0,ZTLN,ZTLE,ZQSTLE,ZQSTLN,ZSN,ZSE,ZCHIS
REAL(KIND=JPRB) :: ZTVLN,ZTVLE,ZNCHI0,ZDCHI0,ZEPS0,ZEPSVVER,ZCHI0,ZCHI02,ZDLOGM,ZFMDQN(KLON)
REAL(KIND=JPRB) :: ZFMDSN(KLON),ZFMDS(KLON),ZFMDQ(KLON),ZDPLAB(KLON),ZS12(KLON),ZS11(KLON),ZTAU(KLON)
REAL(KIND=JPRB) :: ZDLON,ZDLONL(KLON),ZFDXTAUX(KLON),ZFORMV(KLON),ZTAU_DIAG(KLON)
REAL(KIND=JPRB) :: ZTD(KLON),ZQDN(KLON,KLEV),ZBUOY(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIVERG,ZBIN,ZTKLEV,ZTBASE(KLON),ZQBASE(KLON)
REAL(KIND=JPRB) :: ZFLUXDIAG(KLON,KLEV),ZENTR_T_LOC(KLON,KLEV),ZEMD_DIAG(KLON,KLEV)
REAL(KIND=JPRB) :: ZEPSILON_U(KLON,KLEV),ZDELTA_U(KLON,KLEV)
REAL(KIND=JPRB) :: ZDEPTH(KLON),ZVACC(KLON,KLEV)
REAL(KIND=JPRB) :: ZASCENT_BASE(KLON)
REAL(KIND=JPRB) :: ZDIAGFLO(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_ECART_TV(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_ECART_T(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAGFRICTION(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAGTRANSP(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_EPS_ORG(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_EPS_TUR(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIAG_WMAX(KLON)
REAL(KIND=JPRB) :: ZDIAG_TUDAL(KLON,KLEV)

INTEGER(KIND=JPIM) :: ICDN(KLON),INCDN(KLON,KLEV),INIVBAC(KLON,KLEV),IBAC(KLON), ISDELTAP
INTEGER(KIND=JPIM) :: IVVER,IDOMDP,IVVN,IVVX,INUA,ICHIS,IENTR,IKUO6(KLON),IQLIC(KLON)
INTEGER(KIND=JPIM) :: IKUO5(KLON),IE0,ILEVTEN(KLON),INLFC(KLON,KLEV),ILFC(KLON),ICIN

REAL(KIND=JPRB) :: ZSU(KLON,KLEV)
REAL(KIND=JPRB) :: ZTU(KLON,KLEV),ZTVU(KLON,KLEV),ZRHO(KLON,KLEV)
REAL(KIND=JPRB) :: ZQU(KLON,KLEV)
REAL(KIND=JPRB) :: ZS5(KLON)
REAL(KIND=JPRB) :: ZEPS3
REAL(KIND=JPRB) :: ZE,ZR,ZTCOND,ZTEL(KLON),ZTEN(KLON)
REAL(KIND=JPRB) :: ZDEL_ORGD(KLON,KLEV),ZEPS_ORGD(KLON,KLEV)
REAL(KIND=JPRB) :: ZQN2D(KLON,KLEV),ZSDND(KLON,KLEV),ZSN2D(KLON,KLEV),ZTN2D(KLON,KLEV)
REAL(KIND=JPRB) :: ZFORU(KLON,0:KLEV),ZFORD(KLON,0:KLEV),ZQDND(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC_C_HALF(KLON,0:KLEV),ZUMD(KLON,KLEV),ZVMD(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC_C_FULL(KLON,KLEV),ZBCC_CD_FULL(KLON,KLEV)
REAL(KIND=JPRB) :: ZBCC(KLON,KLEV)
REAL(KIND=JPRB) :: ZCCHSL,ZCCHSN
REAL(KIND=JPRB) :: ZT_DOWN(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIFF_Z_UD(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZTWU_ADV(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_BUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_DYP(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_NHD(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_FRI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_TOT_RAW(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_TOT(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWU_STAU(KLON,KLEV)

REAL(KIND=JPRB) :: ZTWD_ADV(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_BUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_DYP(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_NHD(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_FRI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_TOT_RAW(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_TOT(KLON,KLEV)
REAL(KIND=JPRB) :: ZTWD_STAU(KLON,KLEV)
REAL(KIND=JPRB) :: ZGAMMA(KLON,KLEV)
REAL(KIND=JPRB) :: ZBINSAT(KLON,KLEV)
REAL(KIND=JPRB) :: ZOMEGA_RES(KLON,KLEV)

REAL(KIND=JPRB) :: ZMEAN_U(KLON,KLEV)
REAL(KIND=JPRB) :: ZMEAN_D(KLON,KLEV)
REAL(KIND=JPRB) :: ZINTEG(KLON,KLEV)
REAL(KIND=JPRB) :: ZTARGET_WD(KLON,KLEV)
REAL(KIND=JPRB) :: ZPLH(KLON),ZRME(KLON)
REAL(KIND=JPRB) :: ZEPS1,ZEPS4
REAL(KIND=JPRB) :: ZDIFCS_DD_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDIFCS_UD_NOCORR_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZDIFCS_DD_NOCORR_DIAG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZRE(KLON,KLEV) ! Reduce Entrainment factor, total.
REAL(KIND=JPRB) :: ZRE_SATDEF(KLON,KLEV) ! Reduce Entrainment factor, due to saturation deficit.
REAL(KIND=JPRB) :: ZRE_HEIGHT(KLON,KLEV)
REAL(KIND=JPRB) :: ZRE_KEDD(KLON,KLEV)
REAL(KIND=JPRB) :: ZSATDEF(KLON),ZDENO(KLON),ZINTE(KLON),ZRH(KLON)
REAL(KIND=JPRB) :: ZBINID(KLON,KLEV),ZBINID2(KLON,KLEV),ZRT,ZIWGREAT
REAL(KIND=JPRB) :: ZBINTKE,ZBINDEPTH,ZHEIGHT,ZBINBUO(KLON,KLEV)
REAL(KIND=JPRB) :: ZDIFCQD(KLON,0:KLEV),ZDIFCSD(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZI1(KLON,KLEV),ZNND(KLON),ZSTRCUD(KLON,0:KLEV),ZSTRCVD(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZPHIF_U_EQUIP(KLON,KLEV+1) ! geopotential at full levels within updraft, under the equipressure hypothesis.
REAL(KIND=JPRB) :: ZPHIF_U(KLON,KLEV+1) ! geopotential at full levels within updraft.
REAL(KIND=JPRB) :: ZEVAPO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTMPVAR(KLON,KLEV)
REAL(KIND=JPRB) :: ZLISS(KLON,KLEV)
REAL(KIND=JPRB) :: ZRHOH(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZLOGRATIO(KLON), ZALFAVEC(KLON)
REAL(KIND=JPRB) :: ZTLE_ARR(KLON), ZTLN_ARR(KLON), ZEW_TLE(KLON), ZEW_TLN(KLON)
REAL(KIND=JPRB) :: ZLOGSIGMA(KLON), ZDELTA_ARR(KLON)
REAL(KIND=JPRB) :: ZCVGQ(KLON),ZIBCCS(KLON),ZALF_CVGQ(KLON)
REAL(KIND=JPRB) :: ZALFX(KLON),ZREDE(KLON,KLEV)
REAL(KIND=JPRB) :: ZA,ZA2,ZATRA,ZA2TRA
REAL(KIND=JPRB) :: ZC
REAL(KIND=JPRB) :: ZD
REAL(KIND=JPRB) :: ZWNEW
REAL(KIND=JPRB) :: ZENTR
REAL(KIND=JPRB) :: ZENTRX, ZENTRXRG
REAL(KIND=JPRB) :: ZRESOL
REAL(KIND=JPRB) :: ZNEBCX

INTEGER(KIND=JPIM) :: INLABD(KLON,KLEV)
INTEGER(KIND=JPIM) :: INIT0, IVALUE
REAL(KIND=JPRB) :: ZVALUE, ZVALUE_ONE, ZVALUE_GCVENTR_MIN, ZVALUE_ENTRXRG, ZVALUE_GCVEZ

REAL(KIND=JPRB) :: ZHOOK_HANDLE
!REAL(KIND=JPRB) :: ZHOOK_HANDLE_ARRAY(0:94)
LOGICAL :: LLTEXC,LLNHACC,LLCOND1
LOGICAL :: LLMUSCLFA_NSTEP

!-----------------------------------------------------------------------

#include "acmtddd.intfb.h"
#include "wrscmr.intfb.h"
#include "fcttrm.func.h"
#include "fctdoi.func.h"

IF (LHOOK) CALL DR_HOOK('ACMTUD',0,ZHOOK_HANDLE)
ASSOCIATE(GUDDD=>YDML_PHY_MF%YRPHY0%GUDDD, GFRIC=>YDML_PHY_MF%YRPHY0%GFRIC, GCVFRR=>YDML_PHY_MF%YRPHY0%GCVFRR, &
 & GMUKEDD=>YDML_PHY_MF%YRPHY0%GMUKEDD, VVX=>YDML_PHY_MF%YRPHY0%VVX, VVN=>YDML_PHY_MF%YRPHY0%VVN, &
 & TENTRX=>YDML_PHY_MF%YRPHY0%TENTRX, GSDAMIN=>YDML_PHY_MF%YRPHY0%GSDAMIN, FQLIC=>YDML_PHY_MF%YRPHY0%FQLIC, &
 & TEQC=>YDML_PHY_MF%YRPHY0%TEQC, FEVAPC=>YDML_PHY_MF%YRPHY0%FEVAPC, FENTRT=>YDML_PHY_MF%YRPHY0%FENTRT, &
 & GSDAMAX=>YDML_PHY_MF%YRPHY0%GSDAMAX, GCVADET=>YDML_PHY_MF%YRPHY0%GCVADET, GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, &
 & GALMIN=>YDML_PHY_MF%YRPHY0%GALMIN, FNEBC=>YDML_PHY_MF%YRPHY0%FNEBC, GEXPKD=>YDML_PHY_MF%YRPHY0%GEXPKD, &
 & GALMAX=>YDML_PHY_MF%YRPHY0%GALMAX, SCO=>YDML_PHY_MF%YRPHY0%SCO, TCW=>YDML_PHY_MF%YRPHY0%TCW, &
 & GCVCINC=>YDML_PHY_MF%YRPHY0%GCVCINC, GALTAU=>YDML_PHY_MF%YRPHY0%GALTAU, GCVWEXC=>YDML_PHY_MF%YRPHY0%GCVWEXC, &
 & TCA=>YDML_PHY_MF%YRPHY0%TCA, LGDDD=>YDML_PHY_MF%YRPHY0%LGDDD, ADOE=>YDML_PHY_MF%YRPHY0%ADOE, &
 & GREMIN=>YDML_PHY_MF%YRPHY0%GREMIN, GCVTRMIN=>YDML_PHY_MF%YRPHY0%GCVTRMIN, GCVTSMO=>YDML_PHY_MF%YRPHY0%GCVTSMO, &
 & GREMAX=>YDML_PHY_MF%YRPHY0%GREMAX, GAMAP1=>YDML_PHY_MF%YRPHY0%GAMAP1, &
 & GCVENTR_MIN=>YDML_PHY_MF%YRPHY0%GCVENTR_MIN, LCVNAUV=>YDML_PHY_MF%YRPHY0%LCVNAUV, &
 & LGVVEX=>YDML_PHY_MF%YRPHY0%LGVVEX, NCVSIG=>YDML_PHY_MF%YRPHY0%NCVSIG, GSDMIN=>YDML_PHY_MF%YRPHY0%GSDMIN, &
 & RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, GCVRE=>YDML_PHY_MF%YRPHY0%GCVRE, GTDCL=>YDML_PHY_MF%YRPHY0%GTDCL, &
 & ALFX=>YDML_PHY_MF%YRPHY0%ALFX, LCVEOD=>YDML_PHY_MF%YRPHY0%LCVEOD, LCVUVM=>YDML_PHY_MF%YRPHY0%LCVUVM, &
 & NCVCLOS=>YDML_PHY_MF%YRPHY0%NCVCLOS, ECMNP=>YDML_PHY_MF%YRPHY0%ECMNP, GKEDDRE=>YDML_PHY_MF%YRPHY0%GKEDDRE, &
 & LCVCONTAU=>YDML_PHY_MF%YRPHY0%LCVCONTAU, GDEPTH=>YDML_PHY_MF%YRPHY0%GDEPTH, GCVTEXC=>YDML_PHY_MF%YRPHY0%GCVTEXC, &
 & RDELXN=>YDGEM%RDELXN, RKDN=>YDML_PHY_MF%YRPHY0%RKDN, RKDX=>YDML_PHY_MF%YRPHY0%RKDX, &
 & GNHDSMOW=>YDML_PHY_MF%YRPHY0%GNHDSMOW, GNHDSMOS=>YDML_PHY_MF%YRPHY0%GNHDSMOS, &
 & GSINHR=>YDML_PHY_MF%YRPHY0%GSINHR, GCVENTRN=>YDML_PHY_MF%YRPHY0%GCVENTRN, GCHI0=>YDML_PHY_MF%YRPHY0%GCHI0, &
 & NCVENTR=>YDML_PHY_MF%YRPHY0%NCVENTR, GCVEZ=>YDML_PHY_MF%YRPHY0%GCVEZ, GCVENTRX=>YDML_PHY_MF%YRPHY0%GCVENTRX, &
 & TUDGP=>YDML_PHY_MF%YRPHY0%TUDGP, GSINHD=>YDML_PHY_MF%YRPHY0%GSINHD, GSIGA=>YDML_PHY_MF%YRPHY0%GSIGA, &
 & GNHACC=>YDML_PHY_MF%YRPHY0%GNHACC, GRBCC=>YDML_PHY_MF%YRPHY0%GRBCC, LCVIMPT=>YDML_PHY_MF%YRPHY0%LCVIMPT, &
 & GSDMAX=>YDML_PHY_MF%YRPHY0%GSDMAX, TENTR=>YDML_PHY_MF%YRPHY0%TENTR, GNHDEV=>YDML_PHY_MF%YRPHY0%GNHDEV, &
 & LSBUO=>YDML_PHY_MF%YRPHY0%LSBUO, GNHDTAU=>YDML_PHY_MF%YRPHY0%GNHDTAU, TVFC=>YDML_PHY_MF%YRPHY0%TVFC, &
 & LCVNHD=>YDML_PHY_MF%YRPHY0%LCVNHD, LCVRESDYN=>YDML_PHY_MF%YRPHY0%LCVRESDYN, LCVDS=>YDML_PHY_MF%YRPHY0%LCVDS, &
 & GFSURF=>YDML_PHY_MF%YRPHY0%GFSURF, GCLOEB=>YDML_PHY_MF%YRPHY0%GCLOEB, LCAPCIN=>YDML_PHY_MF%YRPHY0%LCAPCIN, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & NDLON=>YDDIM%NDLON, &
 & NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, NBITER=>YDML_PHY_MF%YRPHY%NBITER, &
 & EDELY=>YDEGEO%EDELY, EDELX=>YDEGEO%EDELX, &
 & REFLCAPE => YDML_PHY_MF%YRPHY0%REFLCAPE, LGPRONI1 => YDML_PHY_MF%YRPHY0%LGPRONI1, NPRONI => YDML_PHY_MF%YRPHY0%NPRONI, &
 & GPRONI => YDML_PHY_MF%YRPHY0%GPRONI, GCVUV => YDML_PHY_MF%YRPHY0%GCVUV, &
 & LFLEXDIA=>YDLDDH%LFLEXDIA,LDDH_OMP=>YDLDDH%LDDH_OMP,RSTATI=>YDRIP%RSTATI, &
 & ALFX_LR=>YDML_PHY_MF%YRPHY0%ALFX_LR, ALFX_HR=>YDML_PHY_MF%YRPHY0%ALFX_HR, GCVRESN=>YDML_PHY_MF%YRPHY0%GCVRESN, &
 & GCVRESX=>YDML_PHY_MF%YRPHY0%GCVRESX, RNEBCX=>YDML_PHY_MF%YRPHY0%RNEBCX,GCVGAX=>YDML_PHY_MF%YRPHY0%GCVGAX,GCVGAW=>YDML_PHY_MF%YRPHY0%GCVGAW, GMIRH=>YDML_PHY_MF%YRPHY0%GMIRH, YDPHY0=>YDML_PHY_MF%YRPHY0)

! INITIALISATION OF ALL intent(out) variables

PDETR_U(:,:) = 0._JPRB
PDETR_D(:,:) = 0._JPRB
PENTR_U(:,:) = 0._JPRB
PENTR_D(:,:) = 0._JPRB

PSTRCTRA(:,:,:) = 0._JPRB
PMF_UP(:,:) = 0._JPRB

PNEBC(:,:) = 0._JPRB
PQLIC(:,:) = 0._JPRB

! use a local logical for repeated tests
LLMUSCLFA_NSTEP = LMUSCLFA.AND.MOD(NSTEP,NFRSCM)==0

ZENTRX=GCVRE*TENTRX
ZENTRXRG=ZENTRX*RG

! INIT0: 
!  INIT0=-1   : INITIALIZATION FROM KTDIA TO ITOP (CHEAPEST AND GENERAL CASE).
!  INIT0=0    : INITIALIZATION TO HUGE VALUES.
!  INIT0 >= 0 : INITIALIZATION FROM 1 TO KLEV (FOR SCM PURPOSE, NEEDS WRITING WHOLE ARRAYS ON FILE).
IF(LMUSCLFA) THEN
  INIT0=1
ELSE
  INIT0=-1
ENDIF

IF (INIT0 == 0) THEN
  ZVALUE=HUGE(1._JPRB)
  ZVALUE_ONE=HUGE(1._JPRB)
  ZVALUE_GCVENTR_MIN=HUGE(1._JPRB)
  ZVALUE_ENTRXRG=HUGE(1._JPRB)
  ZVALUE_GCVEZ=HUGE(1._JPRB)
  IVALUE=HUGE(1_JPIM)
ELSE
  ZVALUE=0._JPRB
  ZVALUE_ONE=1._JPRB
  ZVALUE_GCVENTR_MIN=GCVENTR_MIN
  ZVALUE_ENTRXRG=ZENTRXRG
  ZVALUE_GCVEZ=GCVEZ
  IVALUE=0
ENDIF

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK01',0,ZHOOK_HANDLE_ARRAY(1))
IF (INIT0 >= 0) THEN
  DO JLEV=1,KLEV+1
    ZWU(:,JLEV)=ZVALUE
    ZWD(:,JLEV)=ZVALUE
  ENDDO
ENDIF
ZWU_TKE(:,:)=0._JPRB
ZREDE(:,:)=1._JPRB

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA

    ! VERTICAL VELOCITY IN M/S COMPUTED FROM VERTICAL VELOCITY IN PA/S.
    ZWU(JLON,JLEV)=-PUDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
    ZWD(JLON,JLEV)=-PDDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)

    ! Vertical velocity due to TKE processes (from Eric Bazile).
    ZWU_TKE(JLON,JLEV)=SQRT(MIN(3.0_JPRB,MAX(0._JPRB,2._JPRB/3._JPRB*PTKE(JLON,JLEV))))

    ! Gamma factor for resolved vertical velocity inside updraft.
    IF(LCVRESDYN) THEN
      ZGAMMA(JLON,JLEV)=1._JPRB+MAX(0._JPRB,MIN(GCVGAX,ZWU(JLON,JLEV)/GCVGAW))
    ELSE
      ZGAMMA(JLON,JLEV)=1._JPRB
    ENDIF

    ! Resolved omega in Pa/s. Used only in case of < 0 (resolved ascent).
    ZOMEGA_RES(JLON,JLEV)=MIN(0._JPRB,PVERVEL(JLON,JLEV)*PAPRSF(JLON,JLEV))
  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  ZWU(JLON,KLEV+1)=0._JPRB
  ZWD(JLON,KLEV+1)=0._JPRB
ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK01',1,ZHOOK_HANDLE_ARRAY(1))
IF (LLMUSCLFA_NSTEP) THEN
  ZWU(:,1:KTDIA-1)=0._JPRB
  ZWD(:,1:KTDIA-1)=0._JPRB
  CALL WRSCMR(NMUSCLFA,'ZWU_ENTREE_ACMTUD',ZWU,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZWD_ENTREE_ACMTUD',ZWD,KLON,KLEV)
ENDIF

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK02',0,ZHOOK_HANDLE_ARRAY(2))
ZKEDD(:)=0._JPRB
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ! Kinetic energy of the downdraft.
    ZKEDD(JLON)=ZKEDD(JLON)+0.5_JPRB*ZWD(JLON,JLEV)*ZWD(JLON,JLEV)*PDELP(JLON,JLEV)/RG
  ENDDO
ENDDO
IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZKEDD',ZKEDD,KLON,1)

!     -------------------
! // INITIALISATIONS

IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'PUDAL_entree_acmtud',PUDAL,KLON,KLEV)
IF(NCVCLOS == 4) THEN
  !
  !-------------------------------------------------
  ! Surface fraction PALF is prognostic.
  ! In input, PUDAL is the result of 3D advection applied to the uniform in the
  ! vertical PALF field from previous time step.
  ! PUDAL is set in output to ZSIGMA*PALF.
  ! PALF is set in output to the new prognostic value of PALF, and will be set
  ! by the caller of present routine ACMTUD in the array PUDAL, in order to be
  ! advected in 3D.
  ! Here PALF is set to the value of the 3D field PUDAL above PBL.
  ! In 1D, as there is no advection, PUDAL is nothing but the uniform value of PALF from previous time
  ! step.
  !-------------------------------------------------
  !
  ILEV=MAX(KTDIA,INT(GSIGA*REAL(KLEV)))
  DO JLON=KIDIA,KFDIA
    PALF(JLON)=PUDAL(JLON,ILEV)
  ENDDO
ELSE
  !-------------------------------------------------
  ! Surface fraction PALF is diagnostic.
  !-------------------------------------------------
  DO JLEV=1,KLEV
    PUDAL(:,JLEV)=0._JPRB
  ENDDO
ENDIF
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK02',1,ZHOOK_HANDLE_ARRAY(2))

IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'PUDAL_2',PUDAL,KLON,KLEV)
ZENTR=GCVRE*TENTR
ZTPHY=TSPHY
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK00',0,ZHOOK_HANDLE_ARRAY(0))
IF (INIT0 >= 0) THEN
  DO JLEV=1,KLEV
    PDDAL(:,JLEV)=ZVALUE
    PENTR_U(:,JLEV)=ZVALUE
    PENTR_D(:,JLEV)=ZVALUE
    PDETR_D(:,JLEV)=ZVALUE
    PENTR_U_TOT(:,JLEV)=ZVALUE
    ZEPS_TUR(:,JLEV)=ZVALUE
    ZEPS_ORG(:,JLEV)=ZVALUE
    ZEMD_DIAG(:,JLEV)=ZVALUE
    ZVACC(:,JLEV)=ZVALUE
    ZBUOY(:,JLEV)=ZVALUE
    ZENTR_T_LOC(:,JLEV)=ZVALUE
    ZFLUXDIAG(:,JLEV)=ZVALUE
    ZLISS(:,JLEV)=ZVALUE
    ZRE(:,JLEV)=ZVALUE_ONE
    ZRE_SATDEF(:,JLEV)=ZVALUE_ONE
    ZRE_HEIGHT(:,JLEV)=ZVALUE_ONE
    ZRE_KEDD(:,JLEV)=ZVALUE_ONE
    PDETR_U(:,JLEV)=ZVALUE_GCVENTR_MIN
    ZEPSILON_U(:,JLEV)=ZVALUE_ENTRXRG
    ZDELTA_U(:,JLEV)=ZVALUE_ENTRXRG
    ZMOD(:,JLEV)=ZVALUE_GCVEZ
  ENDDO
  DO JLEV=0,KLEV
    PPRODTH(:,JLEV)=ZVALUE
    ZDIFCQD(:,JLEV)=ZVALUE
    ZDIFCSD(:,JLEV)=ZVALUE
    ZSTRCUD(:,JLEV)=ZVALUE
    ZSTRCVD(:,JLEV)=ZVALUE
    ZDIFF_Z_UD(:,JLEV)=ZVALUE
    ZSIGMA(:,JLEV)=ZVALUE
  ENDDO
  ZSTRCTRAD(:,:,:)=ZVALUE
  DO JLEV=1,KLEV+1
    ZPHIF_U(:,JLEV)=ZVALUE
    ZPHIF_U_EQUIP(:,JLEV)=ZVALUE
  ENDDO
ENDIF
ZSTRCTRAD(:,KLEV,:)=0._JPRB
ZRHOH(:,:)  = 1.0_JPRB

! Compute convective response time as a function of nominal resolution.
! REFLCAPE should in this case be independant of resolution.

IF(REFLCAPE > 0._JPRB) THEN
  ! Climate tunings.
  IF (LELAM) THEN
    ZDLON=(2.0_JPRB*RPI*RA)/MIN(EDELX, EDELY)
  ELSE
    ZDLON=REAL(NDLON)
  ENDIF
  DO JLON=KIDIA,KFDIA
    ZDLONL(JLON)=MIN(ZDLON*PGM(JLON),950._JPRB)
    ZFDXTAUX(JLON)=TEQC*1.E3_JPRB*(2.234E-4_JPRB*ZDLONL(JLON)**2&
    & -0.425_JPRB*ZDLONL(JLON)+252.5_JPRB)
    ZALFX(JLON)=ALFX
  ENDDO
ELSE
  ! NWP tunings.
  DO JLON=KIDIA,KFDIA
    ZFDXTAUX(JLON)=ABS(REFLCAPE)*PGM(JLON)
    ! REDLXN is the mean model mesh in meter.
    ! ZRESOL is the local model resolution in 1/meter.
    ZRESOL=PGM(JLON)/RDELXN
    ! ZALFX varies from ALFX_LR to ALFX_HR, as resolution goes from GCVRESN to GCVRESX.
    ZALFX(JLON)=MAX(ALFX_LR,MIN(ALFX_HR,ALFX_LR+(ZRESOL-GCVRESN)/(GCVRESX-GCVRESN)*(ALFX_HR-ALFX_LR)))
  ENDDO
ENDIF

!--------------------------------------------------

!     SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT CONVECTION)
! THIS IS DONE NOW BY INITAPLPAR

!     ---------------------------
! // AUXILIARY CONSTANTS.

ZEPS0=1.E-12_JPRB
ZEPS3=1.E-01_JPRB
ZEPSVVER=1._JPRB
ZEPS16=1.E-06_JPRB
ZEPS1=1.E-10_JPRB
ZEPS4=1.E-05_JPRB
ZEPSW=0.01_JPRB
ZEPS_BCCS=2.8E-05_JPRB ! Drizzle threshold in mm/s.
ZNEBCX=MIN(1._JPRB-ZEPS0,RNEBCX)


! UPDRAFT INIT
ZVVERDM(:,:)=PUDOM(:,:)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK00',0,ZHOOK_HANDLE_ARRAY(0))
IF (INIT0 >= 0) THEN
  DO JLEV=0,KLEV
    ZFORM(:,JLEV)=ZVALUE
    ZDQCA(:,JLEV)=ZVALUE
    ZFORU(:,JLEV)=ZVALUE
!   DOWNDRAFT INIT
    ZFORD(:,JLEV)=ZVALUE
    ZBCC_C_HALF(:,JLEV)=ZVALUE
    ZDIFCS_DD_DIAG(:,JLEV)=ZVALUE
    ZDIFCS_UD_NOCORR_DIAG(:,JLEV)=ZVALUE
    ZDIFCS_DD_NOCORR_DIAG(:,JLEV)=ZVALUE
    PFHEVPP(:,JLEV)=ZVALUE
    PFHMLTS(:,JLEV)=ZVALUE
  ENDDO
  DO JLEV=1,KLEV
    ZDEL_ORGD(:,JLEV)=ZVALUE
    ZEPS_ORGD(:,JLEV)=ZVALUE
    ZQDND(:,JLEV)=ZVALUE
    ZQN2D(:,JLEV)=ZVALUE
    ZSN2D(:,JLEV)=ZVALUE
    ZSDND(:,JLEV)=ZVALUE
    ZTN2D(:,JLEV)=ZVALUE
    ZUMD(:,JLEV)=ZVALUE
    ZVMD(:,JLEV)=ZVALUE
    ZDIAGFLO(:,JLEV)=ZVALUE
    ZDIAG_ECART_TV(:,JLEV)=ZVALUE
    ZDIAG_ECART_T(:,JLEV)=ZVALUE
    ZDIAGFRICTION(:,JLEV)=ZVALUE
    ZDIAGTRANSP(:,JLEV)=ZVALUE
    ZDIAG_EPS_ORG(:,JLEV)=ZVALUE
    ZDIAG_EPS_TUR(:,JLEV)=ZVALUE
    ZDIAG_TUDAL(:,JLEV)=ZVALUE
    ZBCC(:,JLEV)=ZVALUE
    ZBCC_C_FULL(:,JLEV)=ZVALUE
    ZBUO_W(:,JLEV)=ZVALUE
    ZBUO_W_POS(:,JLEV)=ZVALUE
    ZBINBUO(:,JLEV)=ZVALUE
    ZVVERDM(:,JLEV)=ZVALUE
    INLABD(:,JLEV)=IVALUE
  ENDDO
  ZTRAMD(:,:,:) = ZVALUE
ENDIF
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK00',1,ZHOOK_HANDLE_ARRAY(0))

ZARLII=0.004_JPRB
ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS
ZCPWMD=RCW-RCPD
ZCPSMD=RCS-RCPD

ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDTI=1.0_JPRB/ZGDT

ZCCHSL=RCW-RCPD
ZCCHSN=RCS-RCPD

ZKDX=RKDX
ZDEPTH(:)=0._JPRB
ZASCENT_BASE(:)=100000._JPRB
ZDIAG_WMAX(:)=0._JPRB

LLTEXC=GCVTEXC /= 0._JPRB
ZSATDEF(:)=0._JPRB
ZRH(:)=0._JPRB
ZDENO(:)=0._JPRB

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK04',0,ZHOOK_HANDLE_ARRAY(4))
ZMOD_MIN=GCVEZ
ZMOD_MAX=1.0_JPRB

DO JLEV=1,KTDIA-1
  ZMOD(:,JLEV)=0._JPRB
ENDDO

ZCVGQ(:)=0._JPRB
DO JLEV=KTDIA,KLEV
!DEC$ VECTOR ALWAYS
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ! DIAGNOSTIC OF MAXIMUM VERTICAL VELOCITY IN THE COLUMN.
    ZDIAG_WMAX(JLON)=MAX(ZDIAG_WMAX(JLON),ZWU(JLON,JLEV))

    ! CONVECTIVE CLOUD DEPTH:
    ! CUMULATE DEPTH OF LAYERS WHERE VERTICAL VELOCITY IS GREATER THAN A GIVEN THRESHOLD.
    ZBINID(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-GTDCL))
    ZDEPTH(JLON)=ZDEPTH(JLON)+(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV))/RG*ZBINID(JLON,JLEV)

    ! ZASCENT_BASE: height (in m above sea level) of the lowest level where w_updraft > 10 m/s.
    ZIWGREAT=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-10.0_JPRB))
    ZASCENT_BASE(JLON)=ZASCENT_BASE(JLON)*(1._JPRB-ZIWGREAT)+PAPHI(JLON,JLEV)/RG*ZIWGREAT

    ! ZMOD: MODULATES ENTRAINMENT WITH Z.
    ZPPS=PAPRS(JLON,JLEV)/PAPRS(JLON,KLEV)
    ZMOD(JLON,JLEV)=MAX(ZMOD_MIN,MIN(ZMOD_MAX,ZMOD_MIN+(ZMOD_MAX-ZMOD_MIN)&
      &*MAX(0._JPRB,MIN(1._JPRB,(ZPPS-0.35_JPRB)/(0.85_JPRB-0.35_JPRB)))))

    ! DENSITY.
    ZRHO(JLON,JLEV)=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))
    ZRHOH(JLON,JLEV-1) = 0.5_JPRB*(ZRHO(JLON,JLEV-1)+ZRHO(JLON,JLEV))

    ! SATURATION DEFICIT.
    ZINTE(JLON)=PDELP(JLON,JLEV)/RG&
      & *MAX(0._JPRB,SIGN(1._JPRB,-(PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV)-2500._JPRB*RG)&
      & *(PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV)-4500._JPRB*RG)))
    ZSATDEF(JLON)=ZSATDEF(JLON)+(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*ZINTE(JLON)
    ZRH(JLON)=ZRH(JLON)+PQ(JLON,JLEV)/PQSAT(JLON,JLEV)*ZINTE(JLON)
    ZDENO(JLON)=ZDENO(JLON)+ZINTE(JLON)

    ! HUMIDITY CONVERGENCE INTEGRATED VERTICALLY OVER CONVECTIVE LEVELS.
    ZCVGQ(JLON)=ZCVGQ(JLON)+PCVGQ(JLON,JLEV)*ZBINID(JLON,JLEV)*PDELP(JLON,JLEV)/RG

    ! INDICATOR FOR RESOLVED UNDERSATURATION.
    ZBINSAT(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,GMIRH-PQ(JLON,JLEV)/PQSAT(JLON,JLEV)))
  ENDDO
ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK04',1,ZHOOK_HANDLE_ARRAY(4))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK05',0,ZHOOK_HANDLE_ARRAY(5))
DO JLON=KIDIA,KFDIA
  ZSATDEF(JLON)=ZSATDEF(JLON)/MAX(100._JPRB,ZDENO(JLON))
  ZRH(JLON)=ZRH(JLON)/MAX(100._JPRB,ZDENO(JLON))
ENDDO

! ------------------------------------------------------------------
! // COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS
! OTHER PRELIMINARY CALCULATION

! THETAE
DO JLON=KIDIA,KFDIA
  ILEVTEN(JLON)=0
  ZE=MAX(ZEPS0,PAPRSF(JLON,KTDIA)*PQ(JLON,KTDIA)&
    &/((1.0_JPRB-PQ(JLON,KTDIA))*RD/RV+PQ(JLON,KTDIA)))
  ZR=PQ(JLON,KTDIA)/(1.0_JPRB-PQ(JLON,KTDIA))*1000._JPRB
  IE0=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS0-ZE)))
  ZTCOND=IE0*2840._JPRB/(3.5_JPRB*LOG(PT(JLON,KTDIA))-LOG(ZE/100._JPRB)&
    &-4.805_JPRB)+55._JPRB+(1-IE0)*PT(JLON,KTDIA)
  ZTEN(JLON)=PT(JLON,KTDIA)*(100000._JPRB/PAPRSF(JLON,KTDIA))&
    &**(0.2854_JPRB*(1.0_JPRB-0.28E-3_JPRB*ZR))&
    &*EXP((3.376_JPRB/ZTCOND-0.00254_JPRB)*ZR*(1.0_JPRB+0.81E-3_JPRB*ZR))

  ZPLH(JLON)=0.0_JPRB
  ZRME(JLON)=0.0_JPRB

  ZPHIF_U_EQUIP(JLON,KLEV)=PAPHIF(JLON,KLEV)
  ZPHIF_U_EQUIP(JLON,KLEV+1)=PAPHI(JLON,KLEV)

  ZPHIF_U(JLON,KLEV)=PAPHIF(JLON,KLEV)
  ZPHIF_U(JLON,KLEV+1)=PAPHI(JLON,KLEV)
ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK05',1,ZHOOK_HANDLE_ARRAY(5))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK07',0,ZHOOK_HANDLE_ARRAY(7))
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZALFAVEC(JLON)=MAX(ZEPS0,PAPRSF(JLON,JLEV)*PQ(JLON,JLEV)&
      &/((1.0_JPRB-PQ(JLON,JLEV))*RD/RV+PQ(JLON,JLEV)))
    ZDELTA_ARR(JLON)=100000._JPRB/PAPRSF(JLON,JLEV)
    ZTLE_ARR(JLON)=PQ(JLON,JLEV)/(1.0_JPRB-PQ(JLON,JLEV))*1000._JPRB
    ZLOGSIGMA(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZEPS0-ZALFAVEC(JLON))))
  ENDDO
  DO JLON=KIDIA,KFDIA
    IE0=ZLOGSIGMA(JLON)
    ZR=ZTLE_ARR(JLON)
    ZTCOND=IE0*2840._JPRB/(3.5_JPRB*LOG(PT(JLON,JLEV))-LOG(ZALFAVEC(JLON)/100._JPRB)-4.805_JPRB)&
     & +55._JPRB+(1-IE0)*PT(JLON,JLEV)
    ZTEL(JLON)=PT(JLON,JLEV)*ZDELTA_ARR(JLON)&
     &**(0.2854_JPRB*(1.0_JPRB-0.28E-3_JPRB*ZR))&
     &*EXP((3.376_JPRB/ZTCOND-0.00254_JPRB)*ZR*(1.0_JPRB+0.81E-3_JPRB*ZR))
  ENDDO
!DEC$ VECTOR ALWAYS
  DO JLON=KIDIA,KFDIA
    ZTEN(JLON)=MIN(ZTEN(JLON),ZTEL(JLON))
    ILEVTEN(JLON)=MAX(JLEV*NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZTEN(JLON)-ZTEL(JLON)))),ILEVTEN(JLON))
  ENDDO
ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK07',1,ZHOOK_HANDLE_ARRAY(7))


LLNHACC=GNHACC < 0._JPRB
IF(LLNHACC) THEN
  ! In order to maintain convection in the night, where dd activity occurs,
  ! locate maximum of thetav, on the levels where vertical divergency of wd is
  ! larger than the threshold GNHACC.
  ! The result is put in (ZTBASE, ZQBASE): (T, qv) at ascent base.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK08',0,ZHOOK_HANDLE_ARRAY(8))
  DO JLON=KIDIA,KFDIA
    ZTBASE(JLON)=PT(JLON,KLEV)
    ZQBASE(JLON)=PQ(JLON,KLEV)
  ENDDO
  ILEV=MAX(KTDIA,INT(0.95_JPRB*REAL(KLEV)))
  DO JLEV=ILEV,KLEV
    DO JLON=KIDIA,KFDIA
      ZDIVERG=(ZWD(JLON,JLEV-1)-ZWD(JLON,JLEV))&
        & /(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))*RG
      ZBIN=MAX(0._JPRB,SIGN(1._JPRB,GNHACC-ZDIVERG))
      ZTKLEV=PT(JLON,JLEV)*(PAPRSF(JLON,KLEV)/PAPRSF(JLON,JLEV))**(RD/RCPD)
      ZBIN=ZBIN*MAX(0._JPRB,SIGN(1._JPRB,ZTKLEV-ZTBASE(JLON)))
      ZTBASE(JLON)=ZBIN*ZTKLEV+(1._JPRB-ZBIN)*ZTBASE(JLON)
      ZQBASE(JLON)=ZBIN*PQ(JLON,JLEV)+(1._JPRB-ZBIN)*ZQBASE(JLON)
    ENDDO
  ENDDO
ENDIF

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK20',0,ZHOOK_HANDLE_ARRAY(20))
DO JLEV=1,KTDIA-1
  ZRE(:,JLEV)=1._JPRB
ENDDO
IF(GCLOEB > 0._JPRB) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Uniform ZRE, linked to saturation deficit.
      ZRE(JLON,JLEV)=GREMIN+(GREMAX-GREMIN)&
        & *MAX(0._JPRB,MIN(1._JPRB,(ZSATDEF(JLON)-GSDMIN)/(GSDMAX-GSDMIN)))
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
    ZRE(:,JLEV)=1._JPRB
  ENDDO
ENDIF
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK20',1,ZHOOK_HANDLE_ARRAY(20))

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK21',0,ZHOOK_HANDLE_ARRAY(21))
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ! ZPOID     : DP/(RG*DT) FOR A GIVEN LEVEL AND A GIVEN TIME STEP.
    ! ZDSE      : LOCAL ARRAY FOR DRY STATIC ENERGIES.
    ZPOID(JLON,JLEV)=PDELP(JLON,JLEV)*ZGDTI
    ZDSE(JLON,JLEV)=PCP(JLON,JLEV)*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
    INBAS(JLON,JLEV)=0
  ENDDO
ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK21',1,ZHOOK_HANDLE_ARRAY(21))

!     ------------------------------------------------------------------
! // INITIALISATION AT THE BOTTOM LAYER TO START THE CLOUD PROFILE

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFORM    : PROFILE OF MASS FLUX AND LATER MASS FLUX TIME DT.
! KNLAB    : FINAL LAYER ACTIVITY INDEX
! KNND     : PHYSICAL SOLUTION FLAG

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK22',0,ZHOOK_HANDLE_ARRAY(22))
!DEC$ IVDEP
DO JLON=KIDIA,KFDIA

  ! THERMODYNAMIC CHARACTERISTICS OF THE CLOUD.

  ! ZTDN       : TEMPERATURE OF THE CLOUDY ASCENT.
  ! ZQDN       : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
  ! ZLDN       : CONDENSED SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
  ! ZTVN      : VIRTUAL CLOUD TEMPERATURE
  ! ZTVE      : VIRTUAL ENVIRONMENT TEMPERATURE

  KNLAB(JLON,KLEV)=0
  KNND(JLON)=0

  IF(LLNHACC) THEN
    ! The profile starts at lowest model level from the (T,qv) value from the
    ! level where both dd diverges and theta is maximum. This (T,qv) value
    ! has been put adiabatically at level KLEV.
    PTU(JLON,KLEV)=ZTBASE(JLON)
    PQU(JLON,KLEV)=ZQBASE(JLON)
  ELSEIF(LLTEXC) THEN
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE TEMPERATURE PLUS AN EXCESS DEPENDING ON TKE.
    PTU(JLON,KLEV)=PT(JLON,KLEV)+MAX(0.1_JPRB,MIN(2.0_JPRB,GCVTEXC*PTKE(JLON,KLEV)/RCPD))
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE WATER VAPOUR.
    PQU(JLON,KLEV)=PQ(JLON,KLEV)
  ELSE
    ! The profile starts at lowest model level from a mean between lowest model level and surface.
    PTU(JLON,KLEV)=PT(JLON,KLEV)+MAX(0._JPRB,GFSURF*(PTS(JLON)-PT(JLON,KLEV)))
    ! THE PROFILE STARTS AT LOWEST MODEL LEVEL FROM THE GRID-SCALE WATER VAPOUR.
    PQU(JLON,KLEV)=PQ(JLON,KLEV)
  ENDIF

  ZSU(JLON,KLEV)=(RCPD+ZCPVMD*PQU(JLON,KLEV))*PTU(JLON,KLEV)+ZPHIF_U(JLON,KLEV)
  ZLN(JLON)=0.0_JPRB
  ZIFRAC(JLON,KLEV)=FONICE(PTU(JLON,KLEV),YDPHY0%RDTFAC)
  ZTVN(JLON,KLEV)=PTU(JLON,KLEV)*(1.0_JPRB+RETV*PQU(JLON,KLEV)-ZLN(JLON))
  ZTVE(JLON,KLEV)=PT(JLON,KLEV)*(1.0_JPRB+RETV*PQ(JLON,KLEV)-PQLIS(JLON,KLEV))
  PQLIC(JLON,KLEV)=ZLN(JLON)
  ZLDN(JLON,KLEV)=ZLN(JLON)
  ZTNH(JLON)=PTW(JLON,KLEV)
  ZQNH(JLON)=PQW(JLON,KLEV)
  ! TEST DE SATURATION DU NIVEAU (NIVEAU DE CONDENSATION ATTEINT).
  ! INCDN     : NIVEAU DU POINT DE CONDENSATION.
  ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,PQU(JLON,KLEV)-PQSAT(JLON,KLEV))))
  INCDN(JLON,KLEV)=KLEV*ICDN(JLON)
  ! INLFC     : NIVEAU DE CONVECTION LIBRE.
  INLFC(JLON,KLEV)=0
  ZTD(JLON)=PT(JLON,KLEV)*(1.0_JPRB+(-PQLIS(JLON,KLEV)-RETV*(PQSAT(JLON,KLEV)-PQ(JLON,KLEV)))&
    & /(1.0_JPRB+RETV*PLH(JLON,KLEV)*PQSAT(JLON,KLEV)/(RV*PT(JLON,KLEV))))
  ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD(JLON)))
  ZEW=FOEW(ZTD(JLON),ZDELTA)
  ZESP=ZEW/PAPRSF(JLON,KLEV)
  ZQW=FOQS(ZESP)
  ZTD(JLON)=ICDN(JLON)*(GCVADET*ZTD(JLON)+(1.0_JPRB-GCVADET)*PTU(JLON,KLEV))+(1-ICDN(JLON))*PTU(JLON,KLEV)
  ZQW=ICDN(JLON)*(GCVADET*ZQW+(1.0_JPRB-GCVADET)*PQU(JLON,KLEV))+(1-ICDN(JLON))*PQU(JLON,KLEV)
  ZQDN(JLON,KLEV)=ZQW
  ZSDN(JLON,KLEV)=(RCPD+ZCPVMD*ZQW)*ZTD(JLON)+ZPHIF_U(JLON,KLEV)

  ! INIVBAC   : NIVEAU DE LA BASE DE L'ASCENDANCE CONVECTIVE.
  INIVBAC(JLON,KLEV)=KLEV
  ! ZVVER     : VITESSE VERT. DE LA COLONNE CONVECTIVE AUX INTERCOUCHES.
  IF (LGVVEX) THEN
    ZRT=PR(JLON,KLEV)*PT(JLON,KLEV)
    ZVVER(JLON,KLEV)=-GCVWEXC*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB&
      &,PTKE(JLON,KLEV))))*RG*PAPRS(JLON,KLEV)/ZRT
    ZVVER(JLON,KLEV-1)=-GCVWEXC*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB&
      &,PTKE(JLON,KLEV-1))))*RG*PAPRS(JLON,KLEV-1)/ZRT
  ELSE
    ZVVER(JLON,KLEV)=0.0_JPRB
    ZVVER(JLON,KLEV-1)=0.0_JPRB
  ENDIF
  IF(LCVRESDYN) THEN
    ! Updraft velocity is increased from vertical part of TKE.
    ZVVER(JLON,KLEV-1)=ZVVER(JLON,KLEV-1)-ZRHOH(JLON,KLEV-1)*RG &
      & *ZWU_TKE(JLON,KLEV-1)*MAX(0._JPRB,SIGN(1._JPRB,-1.0_JPRB-ZVVER(JLON,KLEV-1)))
  ENDIF
  ZVVERDM(JLON,KLEV)=0.5_JPRB*(ZVVER(JLON,KLEV)+ZVVER(JLON,KLEV-1))
  ! ZDWNDP    : DERIVEE VERT. DE VITESSE VERT.
  ZDWNDP(JLON,KLEV)=0.0_JPRB
  ! ZCAPE     : CAPE DU NIVEAU.
  ZCAPE(JLON)=0.0_JPRB
  ZCIN(JLON)=0.0_JPRB
  IF(NCVENTR == 1 .OR. NCVENTR == 15) THEN
    ! ZEPS_TUR    : ENTRAINEMENT TURBULENT.
    ZEPS_TUR(JLON,KLEV)=ZRE(JLON,KLEV)*ZENTRX
    ! ZEPS_ORG     : ENTRAINEMENT ORGANISE.
    ZEPS_ORG(JLON,KLEV)=ZMOD(JLON,KLEV)*ZRE(JLON,KLEV)*ZENTRX&
      &*PR(JLON,KLEV)*PT(JLON,KLEV)/PAPRSF(JLON,KLEV)
    ! ZDEL_ORG     : DETRAINEMENT ORGANISE
    ZDEL_ORG(JLON,KLEV)=0.0_JPRB
    ZEPSILON_U(JLON,KLEV)=(ZMOD(JLON,KLEV)*ZRE(JLON,KLEV)*ZENTRX+ZEPS_TUR(JLON,KLEV))*RG
    ZDELTA_U(JLON,KLEV)=ZRE(JLON,KLEV)*ZENTRX*RG
  ELSE
    ZEPSILON_U(JLON,KLEV)=ZENTRXRG
  ENDIF
  ! ZKD       : COEFFICIENT DE RESISTANCE.
  IF(NCVENTR == 1) THEN
    ZKD(JLON,KLEV)=ZKDX
  ELSE
    ZKD(JLON,KLEV)=ADOE*ZENTRX*PR(JLON,KLEV)*PT(JLON,KLEV)/PAPRSF(JLON,KLEV)
  ENDIF
  ! ZSIGMA    : FRACTION DE MAILLE COUVERTE PAR L'ASCENDANCE CONVECTIVE.
  ZSIGMA(JLON,KLEV)=1.0_JPRB
  ZSIGMA(JLON,KLEV-1)=1.0_JPRB
  ZS11(JLON)=0.0_JPRB
  ZS12(JLON)=0.0_JPRB
  ZS15(JLON)=0.0_JPRB
  ZS16(JLON)=0.0_JPRB
  ZS5(JLON)=0.0_JPRB
  ZFMDS(JLON)=0.0_JPRB
  ZFMDQ(JLON)=0.0_JPRB
ENDDO

IF(LMUSCLFA) THEN
  DO JLEV=1,KLEV
    ZRE_SATDEF(:,JLEV)=1._JPRB
    ZRE_HEIGHT(:,JLEV)=1._JPRB
    ZRE_KEDD(:,JLEV)=1._JPRB
    ZLISS(:,JLEV)=0._JPRB
    ZEMD_DIAG(:,JLEV)=0._JPRB
    ZBUOY(:,JLEV)=0._JPRB
    ZENTR_T_LOC(:,JLEV)=0._JPRB
    ZFLUXDIAG(:,JLEV)=0._JPRB
    ZEPS_ORG(:,JLEV)=0._JPRB
    ZVACC(:,JLEV)=0._JPRB
    ZBCC(:,JLEV)=0._JPRB
    ZDIAG_EPS_ORG(:,JLEV)=0._JPRB
    ZDIAG_EPS_TUR(:,JLEV)=0._JPRB
    ZDIAG_TUDAL(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=0,KLEV
    ZDIFF_Z_UD(:,JLEV)=0._JPRB
    ZDIFCS_DD_DIAG(:,JLEV)=0._JPRB
    ZDIFCS_UD_NOCORR_DIAG(:,JLEV)=0._JPRB
    ZDIFCS_DD_NOCORR_DIAG(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=0,KTDIA-1
    ZSIGMA(:,JLEV)=1._JPRB
  ENDDO
  DO JLEV=1,KTDIA-1
    ZEPSILON_U(:,JLEV)=ZENTRXRG
    ZEPS_TUR(:,JLEV)=0._JPRB
    ZDELTA_U(:,JLEV)=ZENTRXRG
    ZPHIF_U(:,JLEV)=0._JPRB
    ZDIAGFLO(:,JLEV)=0._JPRB
    ZDIAG_ECART_TV(:,JLEV)=0._JPRB
    ZDIAG_ECART_T(:,JLEV)=0._JPRB
    ZDIAGFRICTION(:,JLEV)=0._JPRB
    ZDIAGTRANSP(:,JLEV)=0._JPRB
    ZBCC_C_FULL(:,JLEV)=0._JPRB
    ZBUO_W(:,JLEV)=0._JPRB
    ZBINBUO(:,JLEV)=0._JPRB
  ENDDO
  ZDIAGFLO(:,KLEV)=0._JPRB
  ZDIAG_ECART_TV(:,KLEV)=0._JPRB
  ZDIAG_ECART_T(:,KLEV)=0._JPRB
  ZDIAGFRICTION(:,KLEV)=0._JPRB
  ZDIAGTRANSP(:,KLEV)=0._JPRB
  ZBINBUO(:,KLEV)=0._JPRB
ENDIF

DO JLEV=0,KTDIA-1
  ZFORM      (:,JLEV)=0._JPRB
  ZBCC_C_HALF(:,JLEV)=0._JPRB
ENDDO
ZLISS(:,KLEV)=0._JPRB
ZDIFF_Z_UD (:,KLEV)=0._JPRB
ZFORM      (:,KLEV)=0._JPRB
ZBCC_C_HALF(:,KLEV)=0._JPRB
ZBUO_W     (:,KLEV)=0._JPRB


!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK22',1,ZHOOK_HANDLE_ARRAY(22))

!     ------------------------------------------------------------------
! // FIRST VERTICAL LOOP (UPWARDS) INCLUDING THE SATURATED
!     ADIABATIC TYPE CALCULATION OF THE CLOUD PROFILE.

!     ITOP MAY HELP AVOIDING UNNECESSARY COMPUTATIONS IN THE UPPER PART OF THE
!     ATMOSPHERE

LLCOND1 = NCVENTR == 1

ITOP=KLEV+1
DO JLEV=KLEV-1,KTDIA,-1

  ! // SATURATED ADIABAT WITH ENTRAINMENT AND LIQUID (OR ICE) WATER SUSTENTATION.

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK23',0,ZHOOK_HANDLE_ARRAY(23))

!DEC$ VECTOR ALWAYS
  DO JLON=KIDIA,KFDIA

    ! ENTRAINMENT AT STARTING LEVEL JLEV+1

    ! ZTB       : "PTU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
    ! ZQB       : "PQU" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
    ! ZLB       : "ZLDN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".

    ! -------------------------------------------------
    ! // FRACTIONAL ENTRAINMENT RATE ZFER IN (J/KG)**-1.
    ! -------------------------------------------------

    ZFER=ZEPSILON_U(JLON,JLEV+1)/RG

    ! TO APPLY WHEN COMPUTING PROFILE AT JLEV

    ZRMIX(JLON,JLEV)=ZFER*(PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1))
    ZRMIX(JLON,JLEV)=ZRMIX(JLON,JLEV)/(1.0_JPRB+ZRMIX(JLON,JLEV))
    ZTBW(JLON)=PTU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PTW(JLON,JLEV+1)-PTU(JLON,JLEV+1))
    ZDELTA_ARR(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTBW(JLON)))

  ENDDO

! Isolate what may not vectorize
  DO JLON=KIDIA,KFDIA
    ZEW_TLE(JLON)= FOEW (ZTBW(JLON),ZDELTA_ARR(JLON))
  ENDDO

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ZQBW(JLON)=PQU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PQW(JLON,JLEV+1)-PQU(JLON,JLEV+1))
    ZTB(JLON)=PTU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PT(JLON,JLEV+1)-PTU(JLON,JLEV+1))
    ZQB(JLON)=PQU(JLON,JLEV+1)+ZRMIX(JLON,JLEV)*(PQ(JLON,JLEV+1)-PQU(JLON,JLEV+1))
    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV+1)))
    ZLB(JLON)=ZLN(JLON)+ZRMIX(JLON,JLEV)*(PQLIS(JLON,JLEV+1)*ICDN(JLON)-ZLN(JLON))
    ZTBH(JLON)=ZTNH(JLON)+ZRMIX(JLON,JLEV)*(PT(JLON,JLEV+1)-ZTNH(JLON))
    ZQBH(JLON)=ZQNH(JLON)+ZRMIX(JLON,JLEV)*(PQ(JLON,JLEV+1)-ZQNH(JLON))

    ! RE-ESTIMATE SATURATION AFTER MIXING

    ! ICE FRACTION AND LATENT HEAT FOR MIXED AIR TEMPERATURE

    ZLHTBW=FOLH(ZTBW(JLON),ZDELTA_ARR(JLON))
    ZCPBW=RCPD+ZCPVMD*ZQBW(JLON)

    ! COMPUTATION OF NEW EQUILIBRIUM OF MIXED AIR
    ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV+1)
    ZQW= FOQS (ZESP)
    ZDQW=FODQS (ZQW,ZESP, FODLEW (ZTBW(JLON),ZDELTA_ARR(JLON)))
    ZINCRQ=(ZDQW*(ZCPBW*(ZTB(JLON)-ZTBW(JLON))+ZLHTBW&
      &*(ZQB(JLON)-ZQBW(JLON)))&
      &+(ZQW-ZQBW(JLON))*ZCPBW)/(ZCPBW+ZLHTBW*ZDQW)
    ZQBW(JLON)=ZQBW(JLON)+ZINCRQ
    ZQBW(JLON)=MIN(ZQBW(JLON),ZQB(JLON)+ZLB(JLON))

    ! HYDROSTATIC COEFFICIENTS FOR THE CLOUD PROFILE.

    ! - TEMPORAIRE(S) 1D .

    ! ZRBB      : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
    ! ZRBH      : WEIGHT OF "PTU" AT "PQU=ZQB" IN THE SAME THICKNESS.
    ! ZRVH      : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBBS(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB&
      &-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBHS(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQB(JLON))
    ZRBB(JLON)=(PLNPR(JLON,JLEV+1)-PALPH(JLON,JLEV+1))*(RD*(1.0_JPRB&
      &-ZLB(JLON))+ZRVMD*ZQBH(JLON))
    ZRBH(JLON)=PALPH(JLON,JLEV)*(RD*(1.0_JPRB-ZLB(JLON))+ZRVMD*ZQBH(JLON))
    ZRVH(JLON)=PALPH(JLON,JLEV)*RV

    ! TO COMPUTE SATURATED ADIABATIC PROFILE, GCVADS ALLOWS TO GO
    ! CONTINUOUSLY FROM EQUIPRESSURE TO EQUIGEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
      & -PAPHIF(JLON,JLEV+1))/ZTBH(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRBBS(JLON)=(1.0_JPRB-GCVADS)*ZRBBS(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
      & -PAPHIF(JLON,JLEV+1))/ZTB(JLON)
    ZRBHS(JLON)=(1.0_JPRB-GCVADS)*ZRBHS(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

    ! SNOW OPTION DEPENDENT CALCULATIONS.
    ! DIAGNOSTIC ICE FRACTION
    ! ZDELTA=FONICE(ZTB(JLON))
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTBH(JLON)))

    ! CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
    ! LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

    ! ZLH       : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
    ! ZCP       : AS ZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)=FOLH(ZTBH(JLON),ZDELTA)+ZRVH(JLON)*ZTBH(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQBH(JLON)+(ZCPWMD+ZDELTA*(ZCPSMD-&
      &ZCPWMD))*ZLB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

    ! CHANGE OF LEVEL TO OBTAIN A
    ! FIRST GUESS AS STARTING POINT FOR THE NEWTON LOOP.

    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV+1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=-((ZRBB(JLON)+ZRBH(JLON))*ZTBH(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZTNH(JLON)=ZTBH(JLON)+ZDELT
    ZQNH(JLON)=ZQBH(JLON)+ZDELQ
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK23',1,ZHOOK_HANDLE_ARRAY(23))


  ! // NEWTON LOOP.

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK24',0,ZHOOK_HANDLE_ARRAY(24))
  DO JIT=1,NBITER
!   Isolate what may not vectorize
    DO JLON=KIDIA,KFDIA

      ! SNOW OPTION DEPENDENT COMPUTATIONS.
      ! DIAGNOSTIC ICE FRACTION
      ZDELTA_ARR(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTNH(JLON)))

      ! SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZEW_TLE(JLON)= FOEW (ZTNH(JLON),ZDELTA_ARR(JLON))

    ENDDO
    DO JLON=KIDIA,KFDIA
      ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTNH(JLON),ZDELTA_ARR(JLON)))

      ! INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA_ARR(JLON)*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQNH(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZQNH(JLON)=ZQNH(JLON)+ZDELQ
      ZTNH(JLON)=ZTNH(JLON)+ZDELT
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK24',1,ZHOOK_HANDLE_ARRAY(24))


  ! // STABILITY TEST, OCCASIONAL CORRECTION OF THE BOTTOM LAYER,
  ! COMPUTATION OF THE MASS FLUX PROFILE PARAMETER AND OF THE TWO
  ! INTEGRALS CONNECTED WITH THE "KUO-TYPE" CLOSURE.

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK25',0,ZHOOK_HANDLE_ARRAY(25))

! Isolate log (may not vectorize)
  DO JLON=KIDIA,KFDIA
    ZLOGRATIO(JLON)=LOG(PAPRSF(JLON,JLEV+1)/PAPRSF(JLON,JLEV))
  ENDDO
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ! // DRY ADIABAT.
    ZCPH=RCPD*(1.0_JPRB-ZQB(JLON))+RCPV*ZQB(JLON)
    ZTNSEC=(ZCPH-ZRBBS(JLON))*ZTB(JLON)/(ZCPH+ZRBHS(JLON))

    ! // TEST IF SATURATED AT TOP OF LAYER, INITIALIZE VARIABLES FOR NEXT LEVEL.
    ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZTNH(JLON)-ZTNSEC)))
    INCDN(JLON,JLEV)=MAX(INCDN(JLON,JLEV+1),JLEV*ICDN(JLON))
    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
    PTU(JLON,JLEV)=ICDN(JLON)*ZTNH(JLON)+(1-ICDN(JLON))*ZTNSEC
    PQU(JLON,JLEV)=ICDN(JLON)*ZQNH(JLON)+(1-ICDN(JLON))*ZQB(JLON)

    ! Buoyant convective condensation.
    ZBCC_C_HALF(JLON,JLEV)=MIN(0._JPRB,PQU(JLON,JLEV)-ZQB(JLON))&
      &/(PAPRSF(JLON,JLEV)-PAPRSF(JLON,JLEV+1))

    ! Integrate hydrostatics within the updraft, with the equipressure hypothesis.
    ZPHIF_U_EQUIP(JLON,JLEV)=ZPHIF_U_EQUIP(JLON,JLEV+1)&
      & +RD*0.5_JPRB*(PTU(JLON,JLEV)+PTU(JLON,JLEV+1))&
      & *(1.0_JPRB+RETV*0.5_JPRB*(PQU(JLON,JLEV)+PQU(JLON,JLEV+1)))&
      & *ZLOGRATIO(JLON)

    ! Choose between equipressure and equigeopotential hypothesis.
    ZPHIF_U(JLON,JLEV)=(1._JPRB-GCVADS)*ZPHIF_U_EQUIP(JLON,JLEV)+GCVADS*PAPHIF(JLON,JLEV)


    ! PHASE PARTITION IN THE UPDRAFT
    ! SIMPLY STORE THE UPDRAFT ICE FRACTION IN ZIFRAC

    ZDELTA=FONICE(PTU(JLON,JLEV),YDPHY0%RDTFAC)
    ZIFRAC(JLON,JLEV)=ZDELTA

    ZLIQ=ECMNP/(ZRBB(JLON)*ZTBH(JLON)+(ZRBH(JLON)+ZRVH(JLON)&
      &*(PQU(JLON,JLEV)-ZQBH(JLON)))*PTU(JLON,JLEV))
    ZEXP=EXP(-1.0_JPRB/MAX(ZARLII,ZLIQ))
    ZLN(JLON)=ICDN(JLON)*MAX(0.0_JPRB,ZLB(JLON)*ZEXP&
      &+(PQU(JLON,JLEV)-ZQBH(JLON))*ZLIQ*(ZEXP-1.0_JPRB))
    PQLIC(JLON,JLEV)=ZLN(JLON)
    !ZDQCA(JLON,JLEV)=ZQBW(JLON)-PQU(JLON,JLEV) formule ref
    ZDQCA(JLON,JLEV)=ZQB(JLON)-PQU(JLON,JLEV) ! approche 3MT.

    ZLDN(JLON,JLEV)=ZLN(JLON)
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*PQU(JLON,JLEV))*PTU(JLON,JLEV)&
      &+ZPHIF_U(JLON,JLEV)
    ZDQCA(JLON,JLEV)=ICDN(JLON)*ZDQCA(JLON,JLEV)

    ! // TEST IF LFC REACHED AT TOP OF LAYER
    ILFC(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,PR(JLON,JLEV)*PT(JLON,JLEV)&
      & -(RD*(1.0_JPRB-ZLN(JLON))+ZRVMD*ZQNH(JLON))*ZTNH(JLON))))
    INLFC(JLON,JLEV)=MAX(INLFC(JLON,JLEV+1),JLEV*ILFC(JLON))
    ILFC(JLON)=MAX(0,-ISIGN(1,-INLFC(JLON,JLEV)))

    ZTVN(JLON,JLEV)=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)-ZLN(JLON))
    ZTVE(JLON,JLEV)=PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)-PQLIS(JLON,JLEV))
    ! // COMPUTE VERTICAL VELOCITY. STEP 1: BUOYANCY.
    ! Buoyancy.
    ZBUO_W(JLON,JLEV)=RG*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))/ZTVE(JLON,JLEV)/GAMAP1

    IBAC(JLON)=MAX(0,-ISIGN(1,-INIVBAC(JLON,JLEV+1)))
    IF (LLCOND1) THEN
      ZCAPEL(JLON)=RD*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ZCINL(JLON)=-NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZCAPEL(JLON))))&
       & *ZCAPEL(JLON)*IBAC(JLON)*ICDN(JLON)*(1-ILFC(JLON))
    ELSE
      ZCAPEL(JLON)=RD*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)*MAX(0._JPRB,ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
    ENDIF

  ENDDO
! Loop is slightly reordered and split here because the (Intel) compiler fails to vectorize it.
! More powerful compiler may just fuse the two loops.
! The fused loops could be vectorized easily if certain conditional branches are
! replaced by straightforward calculations of the kind : alfa * X + (1-alfa) * Y
! Shorter loops may also reduce the pressure on the cache.
! REK.
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    IF(LSBUO) ZBUO_W(JLON,JLEV)=0.5_JPRB*(ZBUO_W(JLON,JLEV)+ZBUO_W(JLON,JLEV+1))
    ! VERTICAL COORDINATE WITHIN UPDRAFT.
    ZDIFF_Z_UD(JLON,JLEV)=ZDIFF_Z_UD(JLON,JLEV+1)+PDELP(JLON,JLEV)&
      & /PAPRSF(JLON,JLEV)*RD*(PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV))&
      & -PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)))/RG

    IF(LLNHACC .OR. LCVNHD) THEN
      ! If NH effects are taken into account, the vertical extension of updraft is
      ! estimated as levels where updraft vertical velocity is positive
      ! and buoyancy positive, in order not to take into account in the vertical
      ! extension the area above LNB, where vertical motion is due to NH effects only.
      ZBINBUO(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZBUO_W(JLON,JLEV)))
    ELSE
      ! If NH effects are not taken into account, the vertical extension of updraft is
      ! estimated as levels where updraft vertical velocity is positive.
      ! It does not depend on buoyancy.
      ZBINBUO(JLON,JLEV)=1._JPRB
    ENDIF
    ZBUO_W_POS(JLON,JLEV)=MAX(0._JPRB,ZBUO_W(JLON,JLEV)) ! positive part of the buoyancy.

    ! ZVACC: vertical acceleration in m/s2 due to buoyancy + NH effects.
    ZVACC(JLON,JLEV)=ZBUO_W(JLON,JLEV)

    ! ZFLO_OMEGA: acceleration in Pa/s2.
    ZFLO_OMEGA=-PAPRSF(JLON,JLEV)/RD/ZTVE(JLON,JLEV)*RG*ZVACC(JLON,JLEV)

    ! Diagnostics.
    ZDIAG_ECART_TV(JLON,JLEV)=ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV)
    ZDIAG_ECART_T(JLON,JLEV)=PTU(JLON,JLEV)-PT(JLON,JLEV)
    ZDIAGFLO(JLON,JLEV)=ZFLO_OMEGA

    ! // COMPUTE VERTICAL VELOCITY. STEP 2: FRICTION.
    ZDELPF=PDELP(JLON,JLEV)
    ZFER=ZEPSILON_U(JLON,JLEV+1)/RG*PR(JLON,JLEV+1)*PT(JLON,JLEV+1)/PAPRSF(JLON,JLEV+1)
    ZKDL=ZKD(JLON,JLEV+1)
    ZDIAGFRICTION(JLON,JLEV)=(ZKDL+ZFER)*ZVVER(JLON,JLEV)**2

    ! VERTICAL TRANSPORT DIAGNOSTIC.
    ZDIAGTRANSP(JLON,JLEV)=-0.5_JPRB*(ZVVER(JLON,JLEV+1)**2-ZVVER(JLON,JLEV)**2)&
      &/(PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV))


    ! // COMPUTE VERTICAL VELOCITY. STEP 3: IMPLICIT SOLVING OF THE TENDENCY EQUATION.
    ZVVERDM(JLON,JLEV)=PUDOM(JLON,JLEV)
    ZVVERDEN=2.0_JPRB*TSPHY*(0.25_JPRB*(ZKDL+ZFER)+0.5_JPRB/ZDELPF)
    ZB=0.5_JPRB*(1.0_JPRB-(ZKDL+ZFER)*TSPHY*ZVVER(JLON,JLEV))
    ZDELTAP=ZB**2&
      &-2.0_JPRB*ZVVERDEN*((ZFLO_OMEGA+(-0.5_JPRB/ZDELPF+0.25_JPRB*(ZKDL+ZFER))&
      &*ZVVER(JLON,JLEV)**2)*TSPHY-0.5_JPRB*ZVVER(JLON,JLEV)&
      &+ZVVERDM(JLON,JLEV))
    ISDELTAP=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZDELTAP)))
    ZDELTAP=ZDELTAP*ISDELTAP
    ZVVERN=(ZB-SQRT(MAX(0._JPRB,ZDELTAP)))/ZVVERDEN

    ZVVERN=MIN(0.0_JPRB,ZVVERN)*ISDELTAP

    ZS5(JLON)=ZS5(JLON)+KNLAB(JLON,JLEV+1)*ZCIN(JLON)
    ZCIN(JLON)=ZCINL(JLON)
    IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,(ZVVERN+ZVVER(JLON,JLEV))+0.0_JPRB)))
    ICIN=(1-IVVER)*IBAC(JLON)*ICDN(JLON)*(1-ILFC(JLON))&
      &*NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,(ZS5(JLON)+ZCINL(JLON)-GCVCINC)+0.0_JPRB)))

    ZVVER(JLON,JLEV-1)=(1-ICIN)*ZVVERN+ICIN*ZVVERDM(JLON,JLEV)*0.5_JPRB
    IF(LCVRESDYN) THEN
      ! Updraft velocity is increased from vertical part of TKE.
      ZVVER(JLON,JLEV)=ZVVER(JLON,JLEV)-ZRHOH(JLON,JLEV)*RG &
        & *ZWU_TKE(JLON,JLEV)*MAX(0._JPRB,SIGN(1._JPRB,-1.0_JPRB-ZVVER(JLON,JLEV)))
    ENDIF
    ZVVERDM(JLON,JLEV)=0.5_JPRB*(ZVVER(JLON,JLEV)+ZVVER(JLON,JLEV-1))

    ZDELPF=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)
    ZDWNDP(JLON,JLEV)=(ZVVERDM(JLON,JLEV+1)-ZVVERDM(JLON,JLEV))/ZDELPF

    ! J.F. Gueremy no more smoothing of dw/dp (ZALFAVEC(JLON)=1.).
    !ZALFAVEC(JLON)=MIN(1.0_JPRB,0.50_JPRB/1000._JPRB*ZDELPF+0.50_JPRB) ! smoothing.
    ZALFAVEC(JLON)=1.0_JPRB ! no smoothing.
    ZDWNDP(JLON,JLEV)=ZALFAVEC(JLON)*ZDWNDP(JLON,JLEV)+(1.0_JPRB-ZALFAVEC(JLON))*ZDWNDP(JLON,JLEV+1)
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK25',1,ZHOOK_HANDLE_ARRAY(25))

  IF(NCVENTR == 1) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK26',0,ZHOOK_HANDLE_ARRAY(26))
!   Isolate log (may not vectorize)
    DO JLON=KIDIA,KFDIA
      ZTLE_ARR(JLON)=PT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE_ARR(JLON)))
      ZEW_TLE(JLON)=FOEW(ZTLE_ARR(JLON),ZDELTA)
      ZTLN_ARR(JLON)=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN_ARR(JLON)))
      ZEW_TLN(JLON)=FOEW(ZTLN_ARR(JLON),ZDELTA)
    ENDDO
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! Vertical velocity for entrainment computation.
      IF(LCVRESDYN) THEN
        ZVVERDME(JLON,JLEV)=ZVVERDM(JLON,JLEV)+ZOMEGA_RES(JLON,JLEV)/0.04_JPRB
        ZREDE(JLON,JLEV)=MAX(TENTR/TENTRX,MIN(1.0_JPRB,(VVX-ZVVERDME(JLON,JLEV))/(VVX-VVN)))
      ELSE
        ZVVERDME(JLON,JLEV)=ZVVERDM(JLON,JLEV)
        ZREDE(JLON,JLEV)=SIN(RPI/2.0_JPRB*MAX(0.0_JPRB,MIN(1.0_JPRB&
          &,((VVX-ZVVERDME(JLON,JLEV))/(VVX-VVN)))))**2
      ENDIF

      ! -------------------------------------------------
      ! // ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      IDOMDP=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZDWNDP(JLON,JLEV))))

      IVVN=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZVVERDME(JLON,JLEV)*ZBINBUO(JLON,JLEV)-VVN)))
      IVVX=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZVVERDME(JLON,JLEV)*ZBINBUO(JLON,JLEV)-VVX)))

      ! TURBULENT ENTRAINMENT AS A FUNCTION OF VERTICAL VELOCITY AND Z.

      ZEPS_TURL=ZMOD(JLON,JLEV)*(ZENTR+(ZENTRX-ZENTR)*ZREDE(JLON,JLEV))
      ZEPS_TURL=IVVN*ZENTRX+(1-IVVN)*IVVX*ZEPS_TURL+(1-IVVX)*ZENTR

      ZRKD=ZMOD(JLON,JLEV)*(RKDN+(ZKDX-RKDN)*ZREDE(JLON,JLEV))
      ZRKD=IVVN*ZKDX+(1-IVVN)*IVVX*ZRKD+(1-IVVX)*RKDN

      INUA=KNLAB(JLON,JLEV+1)
      ZEPS_TUR(JLON,JLEV)=(1-IDOMDP)*ZEPS_TUR(JLON,JLEV+1)&
         &+IDOMDP*(INUA*MIN(ZEPS_TURL,ZEPS_TUR(JLON,JLEV+1))&
         &+(1-INUA)*ZEPS_TURL)

      ZKD(JLON,JLEV)=(1-IDOMDP)*ZKD(JLON,JLEV+1)&
         &+IDOMDP*(INUA*MIN(ZRKD,ZKD(JLON,JLEV+1))&
         &+(1-INUA)*ZRKD)

      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      ZENTRO=-IVVER/MIN(-ZEPS0,ZVVERDM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
         &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZESP=ZEW_TLN(JLON)/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN_ARR(JLON)*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE_ARR(JLON)*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/&
         &ZEPS_ORG0))))&
         &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      IF(LCVRESDYN) THEN
        ZEPS_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*ZREDE(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
        ZDEL_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*ZREDE(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)
      ELSE
      ZEPS_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)
        ZREDE(JLON,JLEV)=1._JPRB
      ENDIF

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=(ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK26',1,ZHOOK_HANDLE_ARRAY(26))

  ELSEIF(NCVENTR == 3) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK27',0,ZHOOK_HANDLE_ARRAY(27))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // ENTRAINMENT AFTER RIO ET AL. 2010.
      ! -------------------------------------------------

      ZTVNL=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)-ZLN(JLON))
      ZTVEL=PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZBUOY(JLON,JLEV)=RG*(1._JPRB-ZTVEL/ZTVNL)
      ZBETA1RIO=0.9_JPRB
      ZA1RIO=0.67_JPRB
      ZBRIO=0.002_JPRB
      ZCRIO=0.012_JPRB

      ! -------------------------------------------------
      ! ENTRAINMENT IN M**-1 AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=MAX(0._JPRB,(ZA1RIO*ZBUOY(JLON,JLEV)&
        &/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2-ZBRIO)/(1._JPRB+ZBETA1RIO))
      ZEPSILON_U(JLON,JLEV)=MAX(ZENTR*RG,MIN(ZENTRX*RG,ZEPSILON_U(JLON,JLEV)))
      ZKD(JLON,JLEV)=ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)

      ! -------------------------------------------------
      ! DETRAINMENT.
      ! -------------------------------------------------

      ZDELTA_U(JLON,JLEV)=MAX(0._JPRB,-ZA1RIO*ZBETA1RIO/(1._JPRB+ZBETA1RIO)&
        &*ZBUOY(JLON,JLEV)/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2)
      ZENTR_T_LOC(JLON,JLEV)=ZCRIO*SQRT(MAX(0._JPRB,PQU(JLON,JLEV)-PQ(JLON,JLEV))&
         &/MAX(1.E-4_JPRB,PQU(JLON,JLEV))/(MAX(1._JPRB,ZWU(JLON,JLEV)))**2)
      ZDELTA_U(JLON,JLEV)=ZDELTA_U(JLON,JLEV)+ZENTR_T_LOC(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=MAX(ZENTR*RG,MIN(ZENTRX*RG,ZDELTA_U(JLON,JLEV)))
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK27',1,ZHOOK_HANDLE_ARRAY(27))

  ELSEIF(NCVENTR == 5) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK28',0,ZHOOK_HANDLE_ARRAY(28))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! UNIFORM ENTRAINMENT / DETRAINMENT.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=ZENTRX
      ZKD(JLON,JLEV)=ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK28',1,ZHOOK_HANDLE_ARRAY(28))

  ELSEIF(NCVENTR == 6) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK29',0,ZHOOK_HANDLE_ARRAY(29))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! ENTRAINMENT / DETRAINMENT "V1" AS IN PIRIOU PHD 2005.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      IF(PAPRSF(JLON,JLEV) < 35000.) THEN
         ZEPSILON_U(JLON,JLEV)=1.2E-4
      ELSEIF(PAPRSF(JLON,JLEV) > 82000. ) THEN
         ZEPSILON_U(JLON,JLEV)=6.0E-4
      ELSE
         ZEPSILON_U(JLON,JLEV)=1.2E-4+(6.0E-4-1.2E-4)*(PAPRSF(JLON,JLEV)-35000.)/(82000.-35000.)
      ENDIF
      ZKD(JLON,JLEV)=ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK29',1,ZHOOK_HANDLE_ARRAY(29))

  ELSEIF(NCVENTR == 7) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK30',0,ZHOOK_HANDLE_ARRAY(30))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! ENTRAINMENT / DETRAINMENT "V1DEV", SAME SHAPE AS IN PIRIOU PHD 2005,
      ! BUT DIFFERENT MAGNITUDE.
      ! -------------------------------------------------

      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT).
      ! -------------------------------------------------

      IF(PAPRSF(JLON,JLEV) < 35000.) THEN
         ZEPSILON_U(JLON,JLEV)=GCVENTRN
      ELSEIF(PAPRSF(JLON,JLEV) > 82000. ) THEN
         ZEPSILON_U(JLON,JLEV)=GCVENTRX
      ELSE
         ZEPSILON_U(JLON,JLEV)=GCVENTRN+(GCVENTRX-GCVENTRN)*(PAPRSF(JLON,JLEV)-35000.)&
          &/(82000.-35000.)
      ENDIF
      ZKD(JLON,JLEV)=ADOE*ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK30',1,ZHOOK_HANDLE_ARRAY(30))

  ELSEIF(NCVENTR == 16) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK33',0,ZHOOK_HANDLE_ARRAY(33))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // UNIFORM TURBULENT ENTRAINMENT.
      ! // FRICTION FUNCTION OF ZRE.
      ! // ORGANIZED ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      ! TURBULENT ENTRAINMENT.
      ZEPS_TUR(JLON,JLEV)=ZENTRX ! ZENTRX and ZEPS_TUR are in (J/kg)**-1.
      ZKD(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*ADOE/ZRE(JLON,JLEV)**GEXPKD&
        & *PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZKD is in Pa**-1.
      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZENTR_TUR is in Pa**-1.

      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      ZENTRO=-IVVER/MIN(-ZEPS0,ZVVERDM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
         &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZTLN=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZTLE=PT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE))
      ZEW=FOEW(ZTLE,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN))
      ZEW=FOEW(ZTLN,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/&
         &ZEPS_ORG0))))&
         &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      ZEPS_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)

      ZDIAG_EPS_ORG(JLON,JLEV)=ZEPS_ORG(JLON,JLEV)&
        &*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))*RG
      ZDIAG_EPS_TUR(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*RG

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=((ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG)*GCHI0
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK33',1,ZHOOK_HANDLE_ARRAY(33))

  ELSEIF(NCVENTR == 17) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK34',0,ZHOOK_HANDLE_ARRAY(34))
!DEC$ IVDEP
   DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! UNIFORM ENTRAINMENT / DETRAINMENT.
      ! -------------------------------------------------


      ! -------------------------------------------------
      ! FRACTIONAL ENTRAINMENT/DETRAINMENT (IN M**-1, TO BE APPLIED DURING A DIAGNOSTIC ASCENT)
      ! AND AERODYNAMIC DRAG IN Pa**-1
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=ZENTRX*RG
      ZKD(JLON,JLEV)=ADOE**(1._JPRB+ZDEPTH(JLON)/7000._JPRB*24._JPRB)&
        & *ZEPSILON_U(JLON,JLEV)/RG*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZEPSILON_U(JLON,JLEV)
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK34',1,ZHOOK_HANDLE_ARRAY(34))
  ELSEIF(NCVENTR == 19) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK35',0,ZHOOK_HANDLE_ARRAY(35))
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! -------------------------------------------------
      ! // Turbulent entrainment depending on
      ! // * height
      ! // * kinetic energy of the downdraft.
      ! // ORGANIZED ENTRAINMENT AFTER GUEREMY TELLUS 2011.
      ! // DETERMINE ENTRAINED VS DETRAINED PART VIA A BUOYANCY SORTING APPROACH.
      ! -------------------------------------------------

      ! Uniform ZRE, linked to saturation deficit integrated in the vertical.
      !ZRE_SATDEF(JLON,JLEV)=GREMIN+(GREMAX-GREMIN) &
      !  & *MAX(0._JPRB,MIN(1._JPRB,(ZSATDEF(JLON)-GSDMIN)/(GSDMAX-GSDMIN)))

      ! Local ZRE, linked to local saturation deficit.
      ZRE_SATDEF(JLON,JLEV)=GREMIN+(GREMAX-GREMIN)&
        & *MAX(0._JPRB,MIN(1._JPRB,(PQSAT(JLON,JLEV)-PQ(JLON,JLEV)-GSDMIN)&
        & /(GSDMAX-GSDMIN)))

      ! Height (above surface) in m.
      ZHEIGHT=(ZPHIF_U(JLON,JLEV)-PAPHI(JLON,KLEV))/RG

      ! Reduction factor of entrainment, due to height.
      !ZRE_HEIGHT(JLON,JLEV)=1._JPRB/MAX(1._JPRB,ZHEIGHT/1000._JPRB)
      ZRE_HEIGHT(JLON,JLEV)=1.

      ! Reduction factor (ZRE_KEDD) of entrainment, due to kinetic energy of the downdraft.
      ZRE_KEDD(JLON,JLEV)=1._JPRB/(1._JPRB+GKEDDRE*ZKEDD(JLON))

      ! The reduction factor that will be applied to turbulent and organized
      ! entrainement and detrainement is the product of all reduction factors.
      ZRE(JLON,JLEV)=ZRE_SATDEF(JLON,JLEV)*ZRE_HEIGHT(JLON,JLEV)*ZRE_KEDD(JLON,JLEV)

      ! TURBULENT ENTRAINMENT.
      ! ZENTRX and ZEPS_TUR are in (J/kg)**-1.
      ZEPS_TUR(JLON,JLEV)=ZENTRX
      ZKD(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*ADOE/ZRE(JLON,JLEV)**GEXPKD&
        & *PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZKD is in Pa**-1.
      ZENTR_TUR=ZEPS_TUR(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV) ! ZENTR_TUR is in Pa**-1.

      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,PUDOM(JLON,JLEV)+0.0_JPRB)))
      ZDELPF=PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)
      ZDWNDP(JLON,JLEV)=(PUDOM(JLON,JLEV+1)-PUDOM(JLON,JLEV))/ZDELPF
      ZENTRO=-IVVER/MIN(-ZEPS0,PUDOM(JLON,JLEV))*ZDWNDP(JLON,JLEV)&
        &+(1-IVVER)*ZENTR_TUR

      ZEPS_ORG0=MAX(ABS(ZENTRO),ZENTR_TUR)

      ZTLN=PTU(JLON,JLEV)-ZLH(JLON)*ZLN(JLON)/ZCP(JLON)
      ZTLE=PT(JLON,JLEV)-PLH(JLON,JLEV)*PQLIS(JLON,JLEV)/PCP(JLON,JLEV)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLE))
      ZEW=FOEW(ZTLE,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLE=FOQS(ZESP)
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTLN))
      ZEW=FOEW(ZTLN,ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQSTLN=FOQS(ZESP)
      ZSN=PQU(JLON,JLEV)+ZLN(JLON)-ZQSTLN
      ZSE=PQ(JLON,JLEV)+PQLIS(JLON,JLEV)-ZQSTLE
      ZCHIS=ZSN/(ZSN-ZSE)
      ZCHIS=MIN(1.0_JPRB,MAX(0.0_JPRB,ZCHIS))
      ICHIS=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZCHIS+0.0_JPRB)))
      ZTVLN=ZTLN*(1.0_JPRB+RETV*(PQU(JLON,JLEV)+ZLN(JLON)))
      ZTVLE=ZTLE*(1.0_JPRB+RETV*(PQ(JLON,JLEV)+PQLIS(JLON,JLEV)))
      ZNCHI0=MAX(0.0_JPRB,ZTVN(JLON,JLEV)-ZTVLE)
      ZDCHI0=MAX(ZEPS0,ZTVN(JLON,JLEV)-(1.0_JPRB-ZCHIS)*ZTVLN-ZCHIS*ZTVLE)
      ZCHI0=ZCHIS*ZNCHI0/ZDCHI0
      ZCHI0=ICHIS*ZCHI0+(1-ICHIS)*1.0_JPRB
      ZCHI0=IVVER*MIN(1.0_JPRB,MAX(0.0_JPRB,MIN(ZCHI0,0.5_JPRB*(1.0_JPRB+ZENTRO/&
        &ZEPS_ORG0))))&
        &+(1-IVVER)*1.0_JPRB
      IENTR=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZENTRO-FENTRT*ZENTR_TUR)))
      ZCHI0=IENTR*1.0_JPRB+(1-IENTR)*ZCHI0

      ZCHI02=ZCHI0**2
      ZEPS_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*ZCHI02,ZEPS0)
      ZDEL_ORG(JLON,JLEV)=GCVRE*ZMOD(JLON,JLEV)*MAX(ZEPS_ORG0*(1.0_JPRB-ZCHI0)**2,ZEPS0)

      ZDIAG_EPS_ORG(JLON,JLEV)=ZEPS_ORG(JLON,JLEV)&
        &*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))*RG
      ZDIAG_EPS_TUR(JLON,JLEV)=ZEPS_TUR(JLON,JLEV)*RG

      ! -------------------------------------------------
      ! CHANGE UNIT: ZEPSILON_U AND ZDELTA_U IN M**-1.
      ! -------------------------------------------------

      ZEPSILON_U(JLON,JLEV)=(ZEPS_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG
      ZDELTA_U(JLON,JLEV)=((ZDEL_ORG(JLON,JLEV)*PAPRSF(JLON,JLEV)&
        &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZEPS_TUR(JLON,JLEV))*RG)*GCHI0
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK35',1,ZHOOK_HANDLE_ARRAY(35))

  ELSE

    ! NCVENTR not recognized. Entrainment and detrainment set to 0.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK36',0,ZHOOK_HANDLE_ARRAY(36))
    DO JLON=KIDIA,KFDIA
      ZEPSILON_U(JLON,JLEV)=0._JPRB
      ZKD(JLON,JLEV)=0._JPRB
      ZDELTA_U(JLON,JLEV)=0._JPRB
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK36',1,ZHOOK_HANDLE_ARRAY(36))
  ENDIF

  ! -------------------------------------------------
  ! The reducing factor ZRE is applied to the total entrainment and detrainment.
  ! -------------------------------------------------
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK37',0,ZHOOK_HANDLE_ARRAY(37))
  DO JLON=KIDIA,KFDIA
    ZEPSILON_U(JLON,JLEV)=ZRE(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)
    ZKD(JLON,JLEV)=ZRE(JLON,JLEV)*ZKD(JLON,JLEV)
    ZDELTA_U(JLON,JLEV)=ZRE(JLON,JLEV)*ZDELTA_U(JLON,JLEV)
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK37',1,ZHOOK_HANDLE_ARRAY(37))

  IF(LCVEOD) THEN

    ! -------------------------------------------------
    ! Entrainment Outside Draft (EOD) is set to a large value.
    ! "Outside Draft" means here "where vertical velocity is below a given threshold".
    ! ZBINID is the binary indicateur of Inside Draft.
    ! -------------------------------------------------
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK38',0,ZHOOK_HANDLE_ARRAY(38))
    DO JLON=KIDIA,KFDIA
      ZEPSILON_U(JLON,JLEV)=ZBINID(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)&
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX*RG
      ZKD(JLON,JLEV)=ZBINID(JLON,JLEV)*ZKD(JLON,JLEV)&
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX&
        & *PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
      ZDELTA_U(JLON,JLEV)=ZBINID(JLON,JLEV)*ZDELTA_U(JLON,JLEV)&
        & +(1._JPRB-ZBINID(JLON,JLEV))*ZENTRX*RG
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK38',1,ZHOOK_HANDLE_ARRAY(38))

  ENDIF

  ! -------------------------------------------------
  ! In case of NH approach, entrainment above LNB is set to a large value.
  ! -------------------------------------------------
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK39',0,ZHOOK_HANDLE_ARRAY(39))
  DO JLON=KIDIA,KFDIA
    ZEPSILON_U(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)&
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX*RG
    ZKD(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZKD(JLON,JLEV)&
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX&
      & *PR(JLON,JLEV)*PT(JLON,JLEV)/PAPRSF(JLON,JLEV)
    ZDELTA_U(JLON,JLEV)=ZBINBUO(JLON,JLEV)*ZDELTA_U(JLON,JLEV)&
      & +(1._JPRB-ZBINBUO(JLON,JLEV))*ZENTRX*RG
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK39',1,ZHOOK_HANDLE_ARRAY(39))

    ! -------------------------------------------------
    ! // INTEGRATE SIGMA IN THE VERTICAL.
    ! -------------------------------------------------

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK40',0,ZHOOK_HANDLE_ARRAY(40))
  IF(NCVSIG == 1 .OR. NCVSIG == 2) THEN
!   Isolate Log (may not vectorize)
    DO JLON=KIDIA,KFDIA
      ZLOGSIGMA(JLON)=LOG(MAX(1.0E-15_JPRB,ZSIGMA(JLON,JLEV)))
    ENDDO
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      IVVER=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVERDM(JLON,JLEV)+0.0_JPRB)))
      IF(LCVDS) THEN
        ZDLOGM=(ZEPS_ORG(JLON,JLEV)-ZDEL_ORG(JLON,JLEV))/(GCVRE*ZMOD(JLON,JLEV)*ZREDE(JLON,JLEV))
      ELSE
        ZDLOGM=(ZEPS_ORG(JLON,JLEV)-ZDEL_ORG(JLON,JLEV))/(GCVRE*ZREDE(JLON,JLEV))
      ENDIF
      ZEMD_DIAG(JLON,JLEV-1)=ZEPSILON_U(JLON,JLEV)-ZDELTA_U(JLON,JLEV)
      ZSIGMA(JLON,JLEV-1)=REAL(IVVER)*EXP(MIN(0._JPRB,ZLOGSIGMA(JLON)&
        &+PDELP(JLON,JLEV)*(ZDLOGM+IVVER*ZDWNDP(JLON,JLEV)&
        &*ZRE(JLON,JLEV)/MIN(-ZEPS0,ZVVERDM(JLON,JLEV)))))&
        &+REAL(1-IVVER)*1.0_JPRB
    ENDDO
  ENDIF
! Isolate Log (may not vectorize)
  DO JLON=KIDIA,KFDIA
    ZTD(JLON)=PT(JLON,JLEV)*(1.0_JPRB+(ZLN(JLON)-PQLIS(JLON,JLEV)-RETV*(PQSAT(JLON,JLEV)&
      & -PQ(JLON,JLEV)))/(1.0_JPRB+RETV*PLH(JLON,JLEV)*PQSAT(JLON,JLEV)&
      & /(RV*PT(JLON,JLEV))))
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTD(JLON)))
    ZEW_TLE(JLON)=FOEW (ZTD(JLON),ZDELTA)
  ENDDO

  IF(NCVSIG == 1) THEN
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=MAX(ZEPS0,MIN(ZSIGMA(JLON,JLEV-1),ZSIGMA(JLON,JLEV)))
    ENDDO
  ELSEIF(NCVSIG == 2) THEN
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=MAX(ZEPS0,MIN(1.0_JPRB,ZSIGMA(JLON,JLEV-1)))
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA
      ZSIGMA(JLON,JLEV-1)=1.0_JPRB
    ENDDO
  ENDIF

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
    IBAC(JLON)=MAX(0,-ISIGN(1,-INIVBAC(JLON,JLEV+1)))
    IKUO5(JLON)=MAX(0,ISIGN(1,&
     & IBAC(JLON)*INIVBAC(JLON,JLEV+1)+(1-IBAC(JLON))&
     & -ILEVTEN(JLON)))
    IKUO6(JLON)=MAX(NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVER(JLON,JLEV-1)+0.0_JPRB)))&
      & ,NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,ZVVER(JLON,JLEV)+0.0_JPRB))))

    ! KNLAB(JLON,JLEV)=0 IF ASCENT BASE ABOVE THETAE MINIMUM
    KNLAB(JLON,JLEV)=IKUO6(JLON)*IBAC(JLON)*IKUO5(JLON)
    PQLIC(JLON,JLEV)=PQLIC(JLON,JLEV)*KNLAB(JLON,JLEV)*ICDN(JLON)

    ! // FINAL VALUES OF DETRAINMENT.
    ZESP=ZEW_TLE(JLON)/PAPRSF(JLON,JLEV)
    ZQW=FOQS(ZESP)
    ZTD(JLON)=ICDN(JLON)*(GCVADET*ZTD(JLON)+(1.0_JPRB-GCVADET)*PTU(JLON,JLEV))+(1-ICDN(JLON))*PTU(JLON,JLEV)
    ZQW=ICDN(JLON)*(GCVADET*ZQW+(1.0_JPRB-GCVADET)*PQU(JLON,JLEV))+(1-ICDN(JLON))*PQU(JLON,JLEV)

    ZQDN(JLON,JLEV)=ZQW
    ZSDN(JLON,JLEV)=(RCPD+ZCPVMD*ZQW)*ZTD(JLON)+ZPHIF_U(JLON,JLEV)

    ! // IN CASE OF UNBUOYANT PARCEL ONE SETS THE STATE TO THAT OF THE ENVIRONMENT.
    INUA=KNLAB(JLON,JLEV)
    INIVBAC(JLON,JLEV)=MAX(INIVBAC(JLON,JLEV+1),JLEV)
    INIVBAC(JLON,JLEV)=INUA*INIVBAC(JLON,JLEV)+(1-INUA)*JLEV
    INIVBAC(JLON,JLEV+1)=INUA*INIVBAC(JLON,JLEV+1)
    INLFC(JLON,JLEV)=INUA*INLFC(JLON,JLEV)

    ZEPSILON_U(JLON,JLEV)=INUA*ZEPSILON_U(JLON,JLEV)+(1-INUA)*ZEPSILON_U(JLON,KLEV)
    ZVVER(JLON,JLEV-1)=INUA*ZVVER(JLON,JLEV-1)
    IF(LGVVEX) ZVVER(JLON,JLEV-1)=ZVVER(JLON,JLEV-1)-(1-INUA)*GCVWEXC&
      &*SQRT(MAX(0._JPRB,2._JPRB/3._JPRB*MIN(5.0_JPRB,PTKE(JLON,JLEV-1))))&
      &*RG*PAPRS(JLON,JLEV-1)/(PR(JLON,JLEV)*PT(JLON,JLEV))
    ZVVERDM(JLON,JLEV)=INUA*ZVVERDM(JLON,JLEV)+(1-INUA)*ZVVER(JLON,JLEV-1)
    IF(NCVSIG == 0) THEN
      ZSIGMA(JLON,JLEV-1)=1.0_JPRB
    ELSE
      ZSIGMA(JLON,JLEV-1)=INUA*ZSIGMA(JLON,JLEV-1)+(1-INUA)*1.0_JPRB
    ENDIF
    PTU(JLON,JLEV)=INUA*PTU(JLON,JLEV)+(1-INUA)*PT(JLON,JLEV)
    PQU(JLON,JLEV)=INUA*PQU(JLON,JLEV)+(1-INUA)*PQ(JLON,JLEV)

    ZSU(JLON,JLEV)=(RCPD+ZCPVMD*PQU(JLON,JLEV))*PTU(JLON,JLEV)+ZPHIF_U(JLON,JLEV)

    ZLN(JLON)=INUA*ZLN(JLON)
    ZQDN(JLON,JLEV)=INUA*ZQDN(JLON,JLEV)+(1-INUA)*PQ(JLON,JLEV)
    ZSDN(JLON,JLEV)=INUA*ZSDN(JLON,JLEV)+(1-INUA)*ZDSE(JLON,JLEV)
    ZTNH(JLON)=INUA*ZTNH(JLON)+(1-INUA)*PTW(JLON,JLEV)
    ZQNH(JLON)=INUA*ZQNH(JLON)+(1-INUA)*PQW(JLON,JLEV)

    ! // SATURATION TEST AT TOP LEVEL.
    ICDN(JLON)=NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,PQU(JLON,JLEV)-PQSAT(JLON,JLEV))))
    INCDN(JLON,JLEV)=INUA*INCDN(JLON,JLEV)+(1-INUA)*JLEV*ICDN(JLON)

    ! // MASS FLUX PROFILE.
    ZFORM(JLON,JLEV)=-1.0_JPRB*KNLAB(JLON,JLEV)*(ZVVER(JLON,JLEV) &
      & +(ZGAMMA(JLON,JLEV)-1._JPRB)*ZOMEGA_RES(JLON,JLEV))&
      & *ZSIGMA(JLON,JLEV)

    ! SUMMATIONS.

    ZFORMV(JLON)=0.5_JPRB*(ZFORM(JLON,JLEV+1)+ZFORM(JLON,JLEV))
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)*ZCAPE(JLON)
    ZBCC_C_FULL(JLON,JLEV+1)=0.5_JPRB*(ZBCC_C_HALF(JLON,JLEV)+ZBCC_C_HALF(JLON,JLEV+1))

    ZFMDQN(JLON)=0.5_JPRB*ZFORM(JLON,JLEV)*((PQU(JLON,JLEV)+PQU(JLON,JLEV+1))&
       &-(PQ(JLON,JLEV)+PQ(JLON,JLEV+1)))
    ZFMDSN(JLON)=0.5_JPRB*ZFORM(JLON,JLEV)*((ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))&
       &-(ZDSE(JLON,JLEV)+ZDSE(JLON,JLEV+1)))

    ! ZS16 = VERTICAL INTEGRAL OF DP/P FROM (dCAPE/dt) DUE TO CONVECTION.
    ZS16(JLON)=ZS16(JLON)+KNLAB(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)&
       &*RD*((1.0_JPRB+RETV*PQ(JLON,JLEV+1))/PCP(JLON,JLEV+1)&
       &*(ZFMDS(JLON)-ZFMDSN(JLON)+ZFORMV(JLON)*PLH(JLON,JLEV+1)&
       &*PDELP(JLON,JLEV+1)*ZBCC_C_FULL(JLON,JLEV+1))&
       &+RETV*PT(JLON,JLEV+1)*(ZFMDQ(JLON)-ZFMDQN(JLON)&
       &-ZFORMV(JLON)*PDELP(JLON,JLEV+1)*ZBCC_C_FULL(JLON,JLEV+1)))/PAPRSF(JLON,JLEV+1)
    ZFMDQ(JLON)=ZFMDQN(JLON)
    ZFMDS(JLON)=ZFMDSN(JLON)
    ZDPLAB(JLON)=KNLAB(JLON,JLEV+1)*PDELP(JLON,JLEV+1)*ZBINBUO(JLON,JLEV)
    ! ZS12: vertical extension of the updraft (in Pa).
    ZS12(JLON)=ZS12(JLON)+ZDPLAB(JLON)
    ! ZS11: mean vertical velocity.
    ZS11(JLON)=ZS11(JLON)+ZDPLAB(JLON)*ABS(ZVVERDM(JLON,JLEV+1))
    ZCAPE(JLON)=ZCAPEL(JLON)

    PUDOM(JLON,JLEV)=ZVVERDM(JLON,JLEV)
    PUDAL(JLON,JLEV)=0._JPRB
    PDDAL(JLON,JLEV)=0._JPRB
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK40',1,ZHOOK_HANDLE_ARRAY(40))

  ! VERIFICATION OF THE PRESENCE OF AT LEAST ONE CLOUD ALONG THE
  ! "JLON" VECTOR AND MODIFICATION OF ITOP IF NECESSARY.

  ISUM=0
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK41',0,ZHOOK_HANDLE_ARRAY(41))
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+KNLAB(JLON,JLEV)
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK41',1,ZHOOK_HANDLE_ARRAY(41))

  IF (ISUM /= 0) THEN
    ITOP=JLEV
  ENDIF
ENDDO

IF(LCVNHD) THEN

  !-------------------------------------------------
  ! Updraft and downdraft velocities are computed using coupled prognostic
  ! equations, using NH pressure-gradient terms.
  !-------------------------------------------------

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK42',0,ZHOOK_HANDLE_ARRAY(42))
  DO JLEV=1,KLEV
    KNLAB(:,JLEV)=1
  ENDDO

  !-------------------------------------------------
  ! Vertical smoothing of evaporation.
  !-------------------------------------------------

  DO JLON=KIDIA,KFDIA
    ZMEAN_D(JLON,KTDIA)=PEVAPO(JLON,KTDIA)
    ZMEAN_U(JLON,KLEV)=PEVAPO(JLON,KLEV)
    ZINTEG(JLON,KTDIA)=1.E-11_JPRB
  ENDDO
  ! DOWNWARD SMOOTHING.
  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZMEAN_D(JLON,JLEV)=(ZMEAN_D(JLON,JLEV-1)*GNHDSMOS+PEVAPO(JLON,JLEV)&
        & *(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV)))&
        & /(GNHDSMOS+ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))
      ZINTEG(JLON,JLEV)=ZINTEG(JLON,JLEV-1)+(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))&
        & *ABS(PEVAPO(JLON,JLEV))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK42',1,ZHOOK_HANDLE_ARRAY(42))
  ! UPWARD SMOOTHING.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK44',0,ZHOOK_HANDLE_ARRAY(44))
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      ZMEAN_U(JLON,JLEV)=(ZMEAN_U(JLON,JLEV+1)*GNHDSMOS+PEVAPO(JLON,JLEV)&
         & *(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1)))&
         & /(GNHDSMOS+ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK44',1,ZHOOK_HANDLE_ARRAY(44))
  ! Average upward and downward smoothings, with a weight depending on altitude.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK45',0,ZHOOK_HANDLE_ARRAY(45))
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWEIGHT=ZINTEG(JLON,JLEV)/ZINTEG(JLON,KLEV)
      ZEVAPO(JLON,JLEV)=ZWEIGHT*ZMEAN_U(JLON,JLEV)&
        & +(1._JPRB-ZWEIGHT)*ZMEAN_D(JLON,JLEV)
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK45',1,ZHOOK_HANDLE_ARRAY(45))
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZEVAPO',ZEVAPO,KLON,KLEV)

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK46',0,ZHOOK_HANDLE_ARRAY(46))
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA

      ! Temperature within the downdraft.
      ZT_DOWN(JLON,JLEV)=PT(JLON,JLEV)-GNHDEV*ZEVAPO(JLON,JLEV)&
        & *MAX(0._JPRB,SIGN(1._JPRB,ZDEPTH(JLON)-GDEPTH))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK46',1,ZHOOK_HANDLE_ARRAY(46))
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZT_DOWN',ZT_DOWN,KLON,KLEV)

  !-------------------------------------------------
  ! Updraft and downdraft prognostic equations.
  !-------------------------------------------------

  ! Tendency due to advection: direct computation.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK47',0,ZHOOK_HANDLE_ARRAY(47))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTWU_ADV(:,JLEV)=ZVALUE
      ZTWD_ADV(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTWU_ADV(:,JLEV)=0._JPRB
    ZTWD_ADV(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTWU_ADV(JLON,JLEV)=-ZWU(JLON,JLEV)*(ZWU(JLON,JLEV)-ZWU(JLON,JLEV+1))&
        & /(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))*RG
      ZTWD_ADV(JLON,JLEV)=-ZWD(JLON,JLEV)*(ZWD(JLON,JLEV)-ZWD(JLON,JLEV+1))&
        & /(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))*RG
    ENDDO
  ENDDO
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWU_ADV',ZTWU_ADV,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWD_ADV',ZTWD_ADV,KLON,KLEV)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK47',1,ZHOOK_HANDLE_ARRAY(47))

  ! Buoyancy.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK48',0,ZHOOK_HANDLE_ARRAY(48))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTWU_BUO(:,JLEV)=ZVALUE
      ZTWD_BUO(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTWU_BUO(:,JLEV)=0._JPRB
    ZTWD_BUO(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Updraft.
      ZTVNL=PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV))
      ZTVEL=PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTWU_BUO(JLON,JLEV)=MAX(0._JPRB,RG*(ZTVNL/ZTVEL-1._JPRB))
      ! Downdraft.
      ZTVNL=ZT_DOWN(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTVEL=PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV))
      ZTWD_BUO(JLON,JLEV)=MIN(0._JPRB,RG*(ZTVNL/ZTVEL-1._JPRB))
    ENDDO
  ENDDO
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWU_BUO',ZTWU_BUO,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWD_BUO',ZTWD_BUO,KLON,KLEV)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK48',1,ZHOOK_HANDLE_ARRAY(48))

  ! Dynamical production.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK49',0,ZHOOK_HANDLE_ARRAY(49))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTWU_DYP(:,JLEV)=ZVALUE
      ZTWD_DYP(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTWU_DYP(:,JLEV)=0._JPRB
    ZTWD_DYP(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ! Updraft.
      ZTWU_DYP(JLON,JLEV)=0._JPRB
      ! Downdraft due to precipitation evaporation. The only source term is the negative
      ! buoyancy due to this evaporation. No contribution here from dynamical production.
      ZTWD_DYP(JLON,JLEV)=0._JPRB
    ENDDO
  ENDDO
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWU_DYP',ZTWU_DYP,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWD_DYP',ZTWD_DYP,KLON,KLEV)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK49',1,ZHOOK_HANDLE_ARRAY(49))

  ! Friction.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK50',0,ZHOOK_HANDLE_ARRAY(50))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTWU_FRI(:,JLEV)=ZVALUE
      ZTWD_FRI(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTWU_FRI(:,JLEV)=0._JPRB
    ZTWD_FRI(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTWU_FRI(JLON,JLEV)=-GFRIC*ZWU(JLON,JLEV)**2
      ZTWD_FRI(JLON,JLEV)= GFRIC*ZWD(JLON,JLEV)**2
    ENDDO
  ENDDO
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWU_FRI',ZTWU_FRI,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWD_FRI',ZTWD_FRI,KLON,KLEV)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK50',1,ZHOOK_HANDLE_ARRAY(50))


  ! Target wd: a vertical smoothing of wd + buoyancy effect.
  ! 1. Buoyancy effect.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK51',0,ZHOOK_HANDLE_ARRAY(51))
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWD_PLUS_BUO(JLON,JLEV)=ZWD(JLON,JLEV)+TSPHY*ZTWD_BUO(JLON,JLEV)
    ENDDO
  ENDDO
  ! 2. Vertical smoothing.
  DO JLON=KIDIA,KFDIA
    ZMEAN_D(JLON,KTDIA)=0._JPRB
    ZMEAN_U(JLON,KLEV)=0._JPRB
    ZINTEG(JLON,KTDIA)=ZEPSW
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK51',1,ZHOOK_HANDLE_ARRAY(51))
  ! DOWNWARD SMOOTHING.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK53',0,ZHOOK_HANDLE_ARRAY(53))
  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZMEAN_D(JLON,JLEV)=(ZMEAN_D(JLON,JLEV-1)*GNHDSMOW+ZWD_PLUS_BUO(JLON,JLEV)&
        & *(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV)))&
        & /(GNHDSMOW+ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))
      ZINTEG(JLON,JLEV)=ZINTEG(JLON,JLEV-1)+(ZPHIF_U(JLON,JLEV-1)-ZPHIF_U(JLON,JLEV))&
        & *ABS(ZWD_PLUS_BUO(JLON,JLEV))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK53',1,ZHOOK_HANDLE_ARRAY(53))
  ! UPWARD SMOOTHING.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK54',0,ZHOOK_HANDLE_ARRAY(54))
  DO JLEV=KLEV-1,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      ZMEAN_U(JLON,JLEV)=(ZMEAN_U(JLON,JLEV+1)*GNHDSMOW+ZWD_PLUS_BUO(JLON,JLEV)&
         & *(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1)))&
         & /(GNHDSMOW+ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK54',1,ZHOOK_HANDLE_ARRAY(54))
  ! Average upward and downward smoothings, with a weight depending on altitude.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK55',0,ZHOOK_HANDLE_ARRAY(55))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTARGET_WD(:,JLEV)=ZVALUE
      ZTWU_NHD(:,JLEV)=ZVALUE
      ZTWD_NHD(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTARGET_WD(:,JLEV)=0._JPRB
    ZTWU_NHD(:,JLEV)=0._JPRB
    ZTWD_NHD(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZWEIGHT=ZINTEG(JLON,JLEV)/ZINTEG(JLON,KLEV)
      ZTARGET_WD(JLON,JLEV)=ZWEIGHT*ZMEAN_U(JLON,JLEV)&
        & +(1._JPRB-ZWEIGHT)*ZMEAN_D(JLON,JLEV)
      ZTWD_NHD(JLON,JLEV)=(ZTARGET_WD(JLON,JLEV)-ZWD_PLUS_BUO(JLON,JLEV))/TSPHY
      ZTWU_NHD(JLON,JLEV)=ZTWD_NHD(JLON,JLEV)
    ENDDO
  ENDDO
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTARGET_WD',ZTARGET_WD,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZWD_PLUS_BUO',ZWD_PLUS_BUO,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZMEAN_U',ZMEAN_U,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZMEAN_D',ZMEAN_D,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZWD_AVANT',ZWD,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWU_NHD',ZTWU_NHD,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZTWD_NHD',ZTWD_NHD,KLON,KLEV)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK55',1,ZHOOK_HANDLE_ARRAY(55))

  ! Temporal integration.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK56',0,ZHOOK_HANDLE_ARRAY(56))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZTWU_TOT_RAW(:,JLEV)=ZVALUE
      ZTWD_TOT_RAW(:,JLEV)=ZVALUE
      ZTWU_TOT(:,JLEV)=ZVALUE
      ZTWD_TOT(:,JLEV)=ZVALUE
    ENDDO
  ENDIF
  IF (LMUSCLFA) THEN
    DO JLEV=1,KLEV
      ZTWU_STAU(:,JLEV)=0._JPRB
      ZTWD_STAU(:,JLEV)=0._JPRB
    ENDDO
  ENDIF
  DO JLEV=1,KTDIA-1
    ZTWU_TOT_RAW(:,JLEV)=0._JPRB
    ZTWD_TOT_RAW(:,JLEV)=0._JPRB
    ZTWU_TOT(:,JLEV)=0._JPRB
    ZTWD_TOT(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! Pure diagnostic.
      IF(LLMUSCLFA_NSTEP) ZTWU_STAU(JLON,JLEV)=ZWU(JLON,JLEV)/120.
      IF(LLMUSCLFA_NSTEP) ZTWD_STAU(JLON,JLEV)=ZWD(JLON,JLEV)/120.

      ! Total tendency (diagnostic).
      ZTWU_TOT_RAW(JLON,JLEV)=ZTWU_ADV(JLON,JLEV)+ZTWU_BUO(JLON,JLEV)&
        & +ZTWU_DYP(JLON,JLEV)+ZTWU_FRI(JLON,JLEV)+ZTWU_NHD(JLON,JLEV)
      ZTWD_TOT_RAW(JLON,JLEV)=ZTWD_ADV(JLON,JLEV)+ZTWD_BUO(JLON,JLEV)&
        & +ZTWD_DYP(JLON,JLEV)+ZTWD_FRI(JLON,JLEV)+ZTWD_NHD(JLON,JLEV)

      ! Solve temporal equation implicitly.
      ! Updraft.
      ZA=0.5_JPRB/(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))+GFRIC
      ZB=1._JPRB/TSPHY
      ZC=-ZTWU_BUO(JLON,JLEV)-ZTWU_DYP(JLON,JLEV)-ZTWU_NHD(JLON,JLEV)-0.5_JPRB/(ZPHIF_U(JLON,JLEV)&
        & -ZPHIF_U(JLON,JLEV+1))*ZWU(JLON,JLEV+1)**2.-ZWU(JLON,JLEV)/TSPHY
      ZD=ZB*ZB-4._JPRB*ZA*ZC
      ZWNEW=(-ZB+SQRT(MAX(0._JPRB,ZD)))/(2._JPRB*ZA)
      ZTWU_TOT(JLON,JLEV)=(ZWNEW-ZWU(JLON,JLEV))/TSPHY
      ZWU(JLON,JLEV)=ZWNEW
      ! Downdraft.
      ZA=0.5_JPRB/(ZPHIF_U(JLON,JLEV)-ZPHIF_U(JLON,JLEV+1))-GFRIC
      ZC=-ZTWD_BUO(JLON,JLEV)-ZTWD_DYP(JLON,JLEV)-ZTWD_NHD(JLON,JLEV)-0.5_JPRB/(ZPHIF_U(JLON,JLEV)&
        & -ZPHIF_U(JLON,JLEV+1))*ZWD(JLON,JLEV+1)**2.-ZWD(JLON,JLEV)/TSPHY
      ZD=ZB*ZB-4._JPRB*ZA*ZC
      ZWNEW=(-ZB+SQRT(MAX(0._JPRB,ZD)))/(2._JPRB*ZA)
      ZTWD_TOT(JLON,JLEV)=(ZWNEW-ZWD(JLON,JLEV))/TSPHY
      ZWD(JLON,JLEV)=ZWNEW

      ! Check extrema.
      ZWU(JLON,JLEV)=MAX(0._JPRB,ZWU(JLON,JLEV))
      ZWD(JLON,JLEV)=MIN(0._JPRB,ZWD(JLON,JLEV))

      ! VERTICAL VELOCITY IN PA/S COMPUTED FROM VERTICAL VELOCITY IN M/S.
      PUDOM(JLON,JLEV)=-ZWU(JLON,JLEV)*PAPRSF(JLON,JLEV)&
      & *RG/(PR(JLON,JLEV)*PT(JLON,JLEV))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK56',1,ZHOOK_HANDLE_ARRAY(56))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK52',0,ZHOOK_HANDLE_ARRAY(52))
  IF(LFLEXDIA) THEN
    IF(LDDH_OMP) THEN
      !-------------------------------------------------
      ! DDH budget of wu.
      !-------------------------------------------------
      IF (.NOT.(KIDIA==1 .AND. KFDIA==KLON)) THEN
        DO JLEV=1,KLEV
          ZTMPVAR(:,JLEV)=0._JPRB
        ENDDO
      ENDIF
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZWU(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'VWU',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_ADV(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWUADV',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_BUO(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWUBUO',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_DYP(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWUDYP',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_FRI(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWUFRI',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_NHD(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWUNHD',YDDDH)
      !-------------------------------------------------
      ! DDH budget of wd.
      !-------------------------------------------------
      IF (.NOT.(KIDIA==1 .AND. KFDIA==KLON)) THEN
        DO JLEV=1,KLEV
          ZTMPVAR(:,JLEV)=0._JPRB
        ENDDO
      ENDIF
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZWD(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'VWD',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_ADV(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWDADV',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_BUO(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWDBUO',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_DYP(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWDDYP',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_FRI(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWDFRI',YDDDH)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_NHD(KIDIA:KFDIA,1:KLEV)
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'TWDNHD',YDDDH)
    ELSE
      !-------------------------------------------------
      ! DDH budget of wu.
      !------------------------------------------------- 
      IF (.NOT.(KIDIA==1 .AND. KFDIA==KLON)) THEN
        DO JLEV=1,KLEV
          ZTMPVAR(:,JLEV)=0._JPRB
        ENDDO
      ENDIF
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZWU(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'VWU','V','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_ADV(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWUADV','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_BUO(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWUBUO','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_DYP(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWUDYP','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_FRI(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWUFRI','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWU_NHD(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWUNHD','T','ARP',.TRUE.,.TRUE.)
     !-------------------------------------------------
     ! DDH budget of wd.
     !-------------------------------------------------
      IF (.NOT.(KIDIA==1 .AND. KFDIA==KLON)) THEN
        DO JLEV=1,KLEV
          ZTMPVAR(:,JLEV)=0._JPRB
        ENDDO
      ENDIF
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZWD(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'VWD','V','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_ADV(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWDADV','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_BUO(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWDBUO','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_DYP(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWDDYP','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_FRI(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWDFRI','T','ARP',.TRUE.,.TRUE.)
      ZTMPVAR(KIDIA:KFDIA,1:KLEV)=&
        & PDELP(KIDIA:KFDIA,1:KLEV)*ZTWD_NHD(KIDIA:KFDIA,1:KLEV)
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'TWDNHD','T','ARP',.TRUE.,.TRUE.)
    ENDIF
  ENDIF
  IF(LLMUSCLFA_NSTEP) THEN
    CALL WRSCMR(NMUSCLFA,'ZTWU_TOT_RAW',ZTWU_TOT_RAW,KLON,KLEV)
    CALL WRSCMR(NMUSCLFA,'ZTWD_TOT_RAW',ZTWD_TOT_RAW,KLON,KLEV)
    CALL WRSCMR(NMUSCLFA,'ZTWU_TOT',ZTWU_TOT,KLON,KLEV)
    CALL WRSCMR(NMUSCLFA,'ZTWD_TOT',ZTWD_TOT,KLON,KLEV)
    CALL WRSCMR(NMUSCLFA,'ZTWU_STAU',ZTWU_STAU,KLON,KLEV)
    CALL WRSCMR(NMUSCLFA,'ZTWD_STAU',ZTWD_STAU,KLON,KLEV)
  ENDIF
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK52',1,ZHOOK_HANDLE_ARRAY(52))
ENDIF ! LCVNHD.

!-------------------------------------------------
! CLOSURE: ESTIMATE CONVECTIVE MASSFLUX CLOSER TO EULERIAN EQUATIONS (EUL).
!-------------------------------------------------

!CALL ACMTUDEUL ( KIDIA,KFDIA,KLON,KTDIA,KLEV,&
!   &PDELP,PRDELP,PAPRS,PAPRSF,PAPHI,PAPHIF,PR,PT, PTU, PQU &
!   &)

! // QUITTING IN CASE OF NO CONVECTION EVERYWHERE.
! THE "IF THEN / ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED GIVEN ITS LENGTH
!     AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP=KTDIA

IF (ITOP < KLEV+1) THEN

  ! LAST LEVEL SUMMATION (TOP).

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK59',0,ZHOOK_HANDLE_ARRAY(59))
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ! PUDOM HAS TO BE ZERO I.E. THE LARGE SCALE VERT VEL. AT LEVEL KTDIA

    PUDOM(JLON,KTDIA)=0.0_JPRB
    ZFORMV(JLON)=ZFORM(JLON,KTDIA)
    ZS15(JLON)=ZS15(JLON)+KNLAB(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)*ZCAPE(JLON)
    ZBCC_C_FULL(JLON,KTDIA)=0.5_JPRB*(ZBCC_C_HALF(JLON,KTDIA-1)+ZBCC_C_HALF(JLON,KTDIA))
    ZFMDQN(JLON)=0.5_JPRB*ZFORM(JLON,KTDIA)*((PQU(JLON,KTDIA)+PQU(JLON,KTDIA+1))&
       &-(PQ(JLON,KTDIA)+PQ(JLON,KTDIA+1)))
    ZFMDSN(JLON)=0.5_JPRB*ZFORM(JLON,KTDIA)*((ZSU(JLON,KTDIA)+ZSU(JLON,KTDIA+1))&
       &-(ZDSE(JLON,KTDIA)+ZDSE(JLON,KTDIA+1)))

    ZS16(JLON)=ZS16(JLON)+KNLAB(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)&
       &*RD*((1.0_JPRB+RETV*PQ(JLON,KTDIA))/PCP(JLON,KTDIA)&
       &*(ZFMDS(JLON)-ZFMDSN(JLON)+ZFORMV(JLON)*PLH(JLON,KTDIA)&
       &*PDELP(JLON,KTDIA)*ZBCC_C_FULL(JLON,KTDIA))&
       &+RETV*PT(JLON,KTDIA)*(ZFMDQ(JLON)-ZFMDQN(JLON)&
       &-ZFORMV(JLON)*PDELP(JLON,KTDIA)*ZBCC_C_FULL(JLON,KTDIA)))/PAPRSF(JLON,KTDIA)
    ZDPLAB(JLON)=KNLAB(JLON,KTDIA)*PDELP(JLON,KTDIA)*ZBINBUO(JLON,KTDIA)
    ZS12(JLON)=ZS12(JLON)+ZDPLAB(JLON)
    ZS11(JLON)=ZS11(JLON)+ZDPLAB(JLON)*ABS(ZVVERDM(JLON,KTDIA))

  ENDDO

  DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      INBAS(JLON,JLEV)=KNLAB(JLON,JLEV)*(1-KNLAB(JLON,JLEV+1))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK59',1,ZHOOK_HANDLE_ARRAY(59))

  ! ------------------------------------------------------------------
  ! // MESH FRACTION, VERTICAL ENTHALPY BUDGET AND DETRAINMENT
  ! ------------------------------------------------------------------

  ! DIAGNOSTIC OF CAPE (FORECASTER NEED) WHATEVER CLOSURE.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK62',0,ZHOOK_HANDLE_ARRAY(62))
  PCAPE(:)=0.0_JPRB
  ZIBCCS(:)=0.0_JPRB ! Integral of BCC multiplied by Sigma.
  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
        ZIBCCS(JLON)=ZIBCCS(JLON)-PUDOM(JLON,JLEV)&
          & *ZBCC_C_FULL(JLON,JLEV)*ZSIGMA(JLON,JLEV)&
          & *ZBINID(JLON,JLEV)*PDELP(JLON,JLEV)/RG
    ENDDO
    IF (LCAPCIN) THEN
      DO JLON=KIDIA,KFDIA
        PCAPE(JLON)=PCAPE(JLON) +KNLAB(JLON,JLEV)*RD&
          &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
          &*(ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ENDDO
    ELSE
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        PCAPE(JLON)=PCAPE(JLON) +KNLAB(JLON,JLEV)*RD&
          &*PDELP(JLON,JLEV)/PAPRSF(JLON,JLEV)&
          &*MAX(0._JPRB,ZTVN(JLON,JLEV)-ZTVE(JLON,JLEV))
      ENDDO
    ENDIF
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK62',1,ZHOOK_HANDLE_ARRAY(62))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK63',0,ZHOOK_HANDLE_ARRAY(63))
  DO JLON=KIDIA,KFDIA
    ZTAU(JLON)=1.0_JPRB/ZFDXTAUX(JLON)*ZS12(JLON)*ZS12(JLON)/MAX(ZS11(JLON),ZEPS0)
    ZTAU_DIAG(JLON)=ZTAU(JLON)
    ! Impose tau (time consumption of CAPE) to be larger than the model time step.
    IF(LCVCONTAU) ZTAU(JLON)=MAX(TSPHY,ZTAU(JLON))
    ZS15(JLON)=MAX(0.0_JPRB,ZS15(JLON))
    ! ZALF_CVGQ : surface fraction occupied by convection, in case of CVGQ closure.
    ! As CVGQ closure is relevant for precipitating convection only,
    ! the surface computed here will be > 0 only in moist cases, where the vertical integral of
    ! BCC is larger than the drizzle threshold.
    ! In order to avoid instabilities when starting prediction from an
    ! interpolated analysis, one sets the CVGQ to 0 during the first hour of
    ! prediction.
    ZALF_CVGQ(JLON)=MAX(0._JPRB,ZCVGQ(JLON))/MAX(ZEPS_BCCS,ZIBCCS(JLON))&
      & *MAX(0._JPRB,SIGN(1._JPRB,ZIBCCS(JLON)-ZEPS_BCCS))&
      & *MAX(0._JPRB, SIGN(1._JPRB,RSTATI-3600._JPRB))


    ! EXTRA-DIAGNOSTICS
    PALF_CVGQ(JLON)=ZALF_CVGQ(JLON)
    PALF_CAPE(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0)

    IF(NCVCLOS == 1) THEN
      ! ALPHA DIAGNOSED FROM d(CAPE)/dt=-CAPE/TAU.
      PALF(JLON)=MIN(ZALFX(JLON),ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0))

    ELSEIF(NCVCLOS == 2) THEN
      ! ALPHA AS A FUNCTION OF SATURATION DEFICIT.
      PALF(JLON)=GALMIN+(GALMAX-GALMIN)&
      & *MAX(0._JPRB,MIN(1._JPRB,(GSDAMAX-ZSATDEF(JLON))&
      & /(GSDAMAX-GSDAMIN)))

      ! LIMITATION OF ALPHA IN STRATOCUMULUS CONTEXT: WHERE TKE AND CLOUD DEPTH ARE SMALL.
      ZBINTKE=MAX(0._JPRB,SIGN(1._JPRB,0.3_JPRB-PTKE(JLON,KLEV)))
      ZBINDEPTH=MAX(0._JPRB,SIGN(1._JPRB,800._JPRB-ZDEPTH(JLON)))
      PALF(JLON)=PALF(JLON)*(1._JPRB-ZBINTKE*ZBINDEPTH)

      ! TAU COMPUTED HERE FOR DIAGNOSTIC PURPOSE ONLY.
      ZTAU(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))/MAX(0.001_JPRB,PALF(JLON))

      ! Impose time consumption of CAPE to be larger than the model time step.
      IF(LCVCONTAU) PALF(JLON)=PALF(JLON)*MAX(0._JPRB,MIN(1._JPRB,ZTAU(JLON)/TSPHY))

    ELSEIF(NCVCLOS == 3) THEN
    ! ALPHA AS A FUNCTION OF DOWNDRAFT ACTIVITY.
      PALF(JLON)=GALMIN+(GALMAX-GALMIN)&
      & *MAX(0._JPRB,MIN(1._JPRB,ZKEDD(JLON)/7500._JPRB))

      ! LIMITATION OF ALPHA IN STRATOCUMULUS CONTEXT: WHERE TKE AND CLOUD DEPTH ARE SMALL.
      ZBINTKE=MAX(0._JPRB,SIGN(1._JPRB,0.3_JPRB-PTKE(JLON,KLEV)))
      ZBINDEPTH=MAX(0._JPRB,SIGN(1._JPRB,800._JPRB-ZDEPTH(JLON)))
      PALF(JLON)=PALF(JLON)*(1._JPRB-ZBINTKE*ZBINDEPTH)

      ! TAU COMPUTED HERE FOR DIAGNOSTIC PURPOSE ONLY.
      ZTAU(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))/MAX(0.001_JPRB,PALF(JLON))

    ELSEIF(NCVCLOS == 4) THEN
      ! PALF is prognostic.
      ZTALF_SCE(JLON)=PALF(JLON)*GMUKEDD*ZKEDD(JLON)
      ZTALF_SIN(JLON)=-PALF(JLON)*GALTAU
      PALF(JLON)=MAX(GALMIN,MIN(GALMAX*ZRH(JLON),PALF(JLON)+TSPHY*(ZTALF_SCE(JLON)+ZTALF_SIN(JLON))))
    ELSEIF(NCVCLOS == 5) THEN
      ! CVGQ closure.
      PALF(JLON)=ZALF_CVGQ(JLON)
    ELSEIF(NCVCLOS == 6) THEN
      ! CAPE and CVGQ mixed closure.
      ! 1. ALPHA DIAGNOSED FROM d(CAPE)/dt=-CAPE/TAU.
      PALF(JLON)=ZS15(JLON)/MAX(ZEPS16,ZS16(JLON))&
        & *MAX(0.0_JPRB,SIGN(1.0_JPRB,(ZS16(JLON)-ZEPS16)))&
        & /MAX(ZTAU(JLON),ZEPS0)
      ! 2. One uses the max value between CAPE and CVGQ approaches.
      PALF(JLON)=MIN(ZALFX(JLON),MAX(PALF(JLON),ZALF_CVGQ(JLON)))
    ELSE
      ! Constant PALF.
      PALF(JLON)=0.04_JPRB
    ENDIF

    ! FIRST TEST OF PHYSICAL VALIDITY OF THE OBTAINED PALF
    KNND(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PALF(JLON)+0.0_JPRB)))
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK63',1,ZHOOK_HANDLE_ARRAY(63))
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZS15',ZS15,KLON,1)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZS16',ZS16,KLON,1)

  ! DOWNDROUGHTS GYT2011
  IF (LGDDD .AND. .NOT.LCVNHD) THEN
    ! Diagnostic downdrafts.
    CALL ACMTDDD(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,ITOP,KLEV,KTRA,&
      &INCDN,KNLAB,&
      &PALPH,PAPHIF,PAPRS,PAPRSF,&
      &PDELP,PLH,PLNPR,PQ,PQLIC,PQLIS,PQSAT,PQW,&
      &PR,PT,PTW,PU,PV,PTRA,ILEVTEN,&
      &ZDEL_ORGD,ZEPS_ORGD,ZFORD,ZQDND,ZQN2D,ZSDND,ZSN2D,ZTN2D,&
      &ZUMD,ZVMD,ZTRAMD,ZBCC_CD_FULL,&
      &INLABD)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK64',0,ZHOOK_HANDLE_ARRAY(64))
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
       PDDAL(JLON,JLEV)=PALF(JLON)*INLABD(JLON,JLEV)*PALF(JLON)/FEVAPC
       PDDOM(JLON,JLEV)=0.5_JPRB*(ZFORD(JLON,JLEV)+ZFORD(JLON,JLEV-1))
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK64',1,ZHOOK_HANDLE_ARRAY(64))
  ELSEIF (LCVNHD) THEN
    ! Prognostic downdraft.
    ! A call to the diagnostic downdrafts is done, only to get the profile of dry
    ! static energy and water vapour.
    CALL ACMTDDD(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,ITOP,KLEV,KTRA,&
      &INCDN,KNLAB,&
      &PALPH,PAPHIF,PAPRS,PAPRSF,&
      &PDELP,PLH,PLNPR,PQ,PQLIC,PQLIS,PQSAT,PQW,&
      &PR,PT,PTW,PU,PV,PTRA,ILEVTEN,&
      &ZDEL_ORGD,ZEPS_ORGD,ZFORD,ZQDND,ZQN2D,ZSDND,ZSN2D,ZTN2D,&
      &ZUMD,ZVMD,ZTRAMD,ZBCC_CD_FULL,&
      &INLABD)
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK65',0,ZHOOK_HANDLE_ARRAY(65))
    IF (INIT0 >= 0) THEN
      DO JLEV=ITOP,KLEV
        PDDOM(:,JLEV)=ZVALUE
      ENDDO
    ENDIF
    DO JLEV=1,ITOP-1
      PDDOM(:,JLEV)=0._JPRB
    ENDDO
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
       PDDOM(JLON,JLEV)=-PAPRSF(JLON,JLEV)*RG/(PR(JLON,JLEV)*PT(JLON,JLEV))*ZWD(JLON,JLEV)
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK65',1,ZHOOK_HANDLE_ARRAY(65))
    IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'PDDOM_POST_ZWD',PDDOM,KLON,KLEV)
  ELSE
    ! No downdrafts.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK66',0,ZHOOK_HANDLE_ARRAY(66))
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
       PDDAL(JLON,JLEV)=0._JPRB
       PDDOM(JLON,JLEV)=0._JPRB
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK66',1,ZHOOK_HANDLE_ARRAY(66))
  ENDIF

  ! ------------------------------------------------------------
  ! // ADAPT THE MESH FRACTION PROFILE AND TENDENCY
  ! // AND COMPUTE MASS FLUX AT THE INTERFACE LEVELS
  ! ------------------------------------------------------------

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK67',0,ZHOOK_HANDLE_ARRAY(67))
  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      KNLAB(JLON,JLEV)=KNND(JLON)*KNLAB(JLON,JLEV)
      INBAS(JLON,JLEV)=INBAS(JLON,JLEV)*KNND(JLON)
      PUDAL(JLON,JLEV)=PALF(JLON)*0.5_JPRB*(ZSIGMA(JLON,JLEV)+ZSIGMA(JLON,JLEV-1))*KNLAB(JLON,JLEV)
      IF(LCVNHD) PDDAL(JLON,JLEV)=GUDDD*PUDAL(JLON,JLEV)
      ICDN(JLON)=MAX(0,-ISIGN(1,-INCDN(JLON,JLEV)))
      PQLIC(JLON,JLEV)=PQLIC(JLON,JLEV)*KNLAB(JLON,JLEV)
      PNEBC(JLON,JLEV)=MAX(ZEPS0,ICDN(JLON)*PALF(JLON)&
       &*0.5_JPRB*(ZSIGMA(JLON,JLEV)+ZSIGMA(JLON,JLEV-1))*KNLAB(JLON,JLEV))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK67',1,ZHOOK_HANDLE_ARRAY(67))
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'PUDAL_post_mesh',PUDAL,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZDIAG_TUDAL',ZDIAG_TUDAL,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZEMD_DIAG',ZEMD_DIAG,KLON,KLEV)
  IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZSIGMA',ZSIGMA,KLON,KLEV+1)

  ! ------------------------------------------------------------
  ! // PROTECTION AGAINST NL INSTABILITY
  ! // AND COMPUTE THE ENVIRONMENT VALUES AFFECTED Y PSEUDO-SUBSIDENCE
  ! ------------------------------------------------------------
  ! ADITIONAL VERTICAL LOOP (UPWARDS) TO MODIFY THE PREVIOUSLY
  ! COMPUTED MASS FLUX.


  ! ZFORM HAS BEEN COMPLETELY INITIALIZED TO ZERO EARLIER, SO IT IS ZERO
  ! ABOVE ITOP (AND NORMALLY ALSO AT KLEV - BUT SEE VIB...)

  ! cdir unroll=8
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK68',0,ZHOOK_HANDLE_ARRAY(68))
  DO JLEV=KLEV-1,ITOP,-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      !     NORMALISATION DE "ZFORM" A LA DIMENSION D'UNE PRESSION.
      !     NORMALIZATION OF "ZFORM" TO PRESSURE DIMENSION.
      ZFORU(JLON,JLEV)=PALF(JLON)*TSPHY*ZFORM(JLON,JLEV)
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV)-PALF(JLON)/FEVAPC*ZFORD(JLON,JLEV))
      ZFORM(JLON,JLEV)=PALF(JLON)*TSPHY*ZFORM(JLON,JLEV)
      ZFORD(JLON,JLEV)=PALF(JLON)*TSPHY*PALF(JLON)/FEVAPC*ZFORD(JLON,JLEV)
    ENDDO
  ENDDO

  IF (LGPRONI1) THEN
    DO JLEV=KLEV-1,ITOP,-1
      DO JLON=KIDIA,KFDIA      

      ! PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
      ! APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV+1)+(ZFORM(JLON,JLEV)&
        &-ZFORM(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
        &*MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV+1))))
      ZFORU(JLON,JLEV)=MAX(0.0_JPRB,ZFORU(JLON,JLEV+1)+(ZFORU(JLON,JLEV)&
        &-ZFORU(JLON,JLEV+1))/(1.0_JPRB+PRDELP(JLON,JLEV+1)&
        &*MAX(0.0_JPRB,ZFORU(JLON,JLEV)-ZFORU(JLON,JLEV+1))))
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK68',1,ZHOOK_HANDLE_ARRAY(68))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK69',0,ZHOOK_HANDLE_ARRAY(69))
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
      ! PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
      ! APPEAR DESPITE THE IMPLICIT ALGORITHM.
      ZFORD(JLON,JLEV)=MAX(0.0_JPRB,ZFORD(JLON,JLEV-1)+(ZFORD(JLON,JLEV)&
         &-ZFORD(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
         &*MAX(0.0_JPRB,ZFORD(JLON,JLEV)-ZFORD(JLON,JLEV-1))))
      ENDDO
    ENDDO
  ENDIF  
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK69',1,ZHOOK_HANDLE_ARRAY(69))



  ! -----------------------------------------------------------------
  ! // WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
  ! // PRESSURE GRADIENT TERM (KERSHAW & GREGORY SCHEME), CONSIDERING
  ! // THE ACTIVE LAYERS
  ! -----------------------------------------------------------------

  ! INITIALIZE WORK ARRAYS

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK70',0,ZHOOK_HANDLE_ARRAY(70))
  IF (INIT0 >= 0) THEN
    DO JLEV=1,KLEV
      ZUM(:,JLEV)=ZVALUE
      ZVM(:,JLEV)=ZVALUE
      ZBET(:,JLEV)=ZVALUE
      ZA13(:,JLEV)=ZVALUE
      ZA14(:,JLEV)=ZVALUE
    ENDDO
    ZA15(:,:,:) = ZVALUE
  ENDIF
  ZTRAM(:,:,:)=0.0_JPRB

  ! -------------------------------------------------------------------
  ! // UPWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
  ! ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

  ! TEMPORAIRES 1D.

  ! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.

  ! TEMPORAIRE 0D.

  ! TEMPORAIRES 2D.

  ! ZA13      : UC0.
  ! ZA14      : VC0.
  ! ZA15      : TRA0.
  ! ZUM       : AVERAGE U VALUE FROM CLOUD BASE.
  ! ZVM       : AVERAGE V VALUE FROM CLOUD BASE.
  ! ZTRAM     : AVERAGE TRACER VALUE FROM CLOUD BASE.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    ! AT BASE: ZBE=1, ZUM=PU, ZVM=PV, ZA13=PU, ZA14=PV
    ! INACTIVE LAYERS AND BASE => ZUM=PU, ZVM=PV

    ZBET(JLON,KLEV)=1.0_JPRB
    ZUM(JLON,KLEV)=PU(JLON,KLEV)
    ZVM(JLON,KLEV)=PV(JLON,KLEV)

    DO JTRA=1,KTRA
      ZTRAM(JLON,KLEV,JTRA) = PTRA(JLON,KLEV,JTRA)
    ENDDO
    ! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

    ZA13(JLON,KLEV)=PU(JLON,KLEV)
    ZA14(JLON,KLEV)=PV(JLON,KLEV)
  
    DO JTRA=1,KTRA
      ZA15(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
    ENDDO
    
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK70',1,ZHOOK_HANDLE_ARRAY(70))

  ! cdir unroll=4
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK71',0,ZHOOK_HANDLE_ARRAY(71))
  DO JLEV=KLEV-1,ITOP,-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

      ! ZNBA=1 WHERE ACTIVE AND NOT BASE
      ! ZBAS=1  AT BASE (AND THEN ACTIVE)
      ! BOTH ARE 0 IN INACTIVE LAYERS
      ZBAS=REAL(INBAS(JLON,JLEV),JPRB)
      ZNBA=REAL(1-INBAS(JLON,JLEV),JPRB)*KNLAB(JLON,JLEV)

      ! ZBET, ZUM, ZVM VALUES ARE USED ONLY IN ACTIVE LAYERS.
      ! ZBET IS 0 IN INACTIVE LAYERS
      ! 1 IN THE BASE LAYERS
      ! DECREASE UPWARDS IN THE ACTIVE LAYERS
      ! ZUM = PU IN INACTIVE LAYERS
      ! ZVM = PV IN INACTIVE LAYERS
      ! AND BOTH ARE ACCUMULATED IN ACTIVE LAYERS


      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX)+ZBAS

      ! ZUM BACK TO PU IF  KNLAB=0 OR INBAS=1
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
         &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
         &+TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
         &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
         &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
         &+TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
         &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA) = (ZTRAM(JLON,JLEV+1,JTRA)&
         &+(ZMIX*(PTRA(JLON,JLEV+1,JTRA)-ZTRAM(JLON,JLEV+1,JTRA))&
         &+TUDGP*(PTRA(JLON,JLEV,JTRA)-PTRA(JLON,JLEV+1,JTRA)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
         &+PTRA(JLON,JLEV,JTRA)*(1.0_JPRB-ZNBA)
      ENDDO

      ! CLOUD BASE VELOCITY=MEAN GRID BOX VELOCITY

      ! ZA13,ZA14 : WIND AT BASE OF ACTIVE SEGMENT
      ! SAME VALUE UP TO NEXT BASE
      ! (IN INACTIVE LAYERS, MULTIPLIED BY ZBET=0)

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
      
      DO JTRA=1,KTRA    
        ZA15(JLON,JLEV,JTRA) = ZA15(JLON,JLEV+1,JTRA)*ZNBA+PTRA(JLON,JLEV,JTRA)*ZBAS      
      ENDDO

    ENDDO
  ENDDO
  DO JLEV=1,ITOP-1
    ZUM(:,JLEV)=0._JPRB
    ZVM(:,JLEV)=0._JPRB
    ZBET(:,JLEV)=0._JPRB
    ZA13(:,JLEV)=0._JPRB
    ZA14(:,JLEV)=0._JPRB
  ENDDO
  DO JTRA=1,KTRA
    DO JLEV=1,ITOP-1
      ZTRAM(:,JLEV,JTRA) = 0._JPRB
      ZA15(:,JLEV,JTRA) = 0._JPRB
    ENDDO
  ENDDO

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK71',1,ZHOOK_HANDLE_ARRAY(71))

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK72',0,ZHOOK_HANDLE_ARRAY(72))
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
       ZZ=1.0_JPRB-ZBET(JLON,JLEV)
       ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
       ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
       DO JTRA=1,KTRA
         ZTRAM(JLON,JLEV,JTRA)=ZZ*ZTRAM(JLON,JLEV,JTRA)+ZBET(JLON,JLEV)*ZA15(JLON,JLEV,JTRA)           
       ENDDO
    ENDDO
  ENDDO

  ! ------------------------------------------------------------------
  ! // FLUXES COMPUTATION

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    IF(LCVRESDYN) THEN
      ZBCC(JLON,ITOP)=-PUDAL(JLON,ITOP)*(PUDOM(JLON,ITOP) &
        &+ZGAMMA(JLON,ITOP)*ZOMEGA_RES(JLON,ITOP)*ZBINSAT(JLON,ITOP))*ZBCC_C_FULL(JLON,ITOP) &
        &+PDDAL(JLON,ITOP)*PDDOM(JLON,ITOP)*ZBCC_CD_FULL(JLON,ITOP)
    ELSE
    ZBCC(JLON,ITOP)=-PUDAL(JLON,ITOP)*PUDOM(JLON,ITOP)*ZBCC_C_FULL(JLON,ITOP)&
                   &+PDDAL(JLON,ITOP)*PDDOM(JLON,ITOP)*ZBCC_CD_FULL(JLON,ITOP)
    ENDIF
    PFCCQL(JLON,ITOP)=PFCCQL(JLON,ITOP-1)-1._JPRB/RG*(1._JPRB-ZIFRAC(JLON,ITOP))&
      &*(-ZBCC(JLON,ITOP))*PDELP(JLON,ITOP)
    PFCCQI(JLON,ITOP)=PFCCQI(JLON,ITOP-1)-1._JPRB/RG*ZIFRAC(JLON,ITOP)&
      &*(-ZBCC(JLON,ITOP))*PDELP(JLON,ITOP)
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK72',1,ZHOOK_HANDLE_ARRAY(72))

  ! cdir unroll=8
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK74',0,ZHOOK_HANDLE_ARRAY(74))
  DO JLEV=ITOP+1,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      IF(LCVRESDYN) THEN
        ZBCC(JLON,JLEV)=-PUDAL(JLON,JLEV)*(PUDOM(JLON,JLEV) &
          &+ZGAMMA(JLON,JLEV)*ZOMEGA_RES(JLON,JLEV)*ZBINSAT(JLON,JLEV))*ZBCC_C_FULL(JLON,JLEV) &
          &+PDDAL(JLON,JLEV)*PDDOM(JLON,JLEV)*ZBCC_CD_FULL(JLON,JLEV)
      ELSE
      ZBCC(JLON,JLEV)=-PUDAL(JLON,JLEV)*PUDOM(JLON,JLEV)*ZBCC_C_FULL(JLON,JLEV)&
                     &+PDDAL(JLON,JLEV)*PDDOM(JLON,JLEV)*ZBCC_CD_FULL(JLON,JLEV)
      ENDIF
      PFCCQL(JLON,JLEV)=PFCCQL(JLON,JLEV-1)-1._JPRB/RG*(1._JPRB-ZIFRAC(JLON,JLEV))&
        &*(-ZBCC(JLON,JLEV))*PDELP(JLON,JLEV)
      PFCCQI(JLON,JLEV)=PFCCQI(JLON,JLEV-1)-1._JPRB/RG*ZIFRAC(JLON,JLEV)&
        &*(-ZBCC(JLON,JLEV))*PDELP(JLON,JLEV)
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD_74',1,ZHOOK_HANDLE74)

!IF (LHOOK) CALL DR_HOOK('ACMTUD_75',0,ZHOOK_HANDLE75)
  DO JLON=KIDIA,KFDIA
    PFCCQL(JLON,KLEV)=PFCCQL(JLON,KLEV-1)
    PFCCQI(JLON,KLEV)=PFCCQI(JLON,KLEV-1)
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD_75',1,ZHOOK_HANDLE75)

  IF(LCVUVM) THEN
    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES
    ! ---------------------------
    ! COMPUTE ZUM AND ZVM AS UNIFORM, EQUAL TO THE MEAN VALUE ALONG CONVECTIVE LEVELS.
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK76',0,ZHOOK_HANDLE_ARRAY(76))
    IF (INIT0 >=0) THEN
      DO JLEV=1,KLEV
        ZUM(:,JLEV)=ZVALUE
        ZVM(:,JLEV)=ZVALUE
      ENDDO
    ENDIF
    ZDENO(:)=0._JPRB
    ZUM(:,KLEV)=0._JPRB
    ZVM(:,KLEV)=0._JPRB
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        ZBINID2(JLON,JLEV)=MAX(0._JPRB,SIGN(1._JPRB,ZWU(JLON,JLEV)-0.1_JPRB))
        ZINTE(JLON)=ZBINID2(JLON,JLEV)*PDELP(JLON,JLEV)/RG
        ZUM(JLON,KLEV)=ZUM(JLON,KLEV)+PU(JLON,JLEV)*ZINTE(JLON)
        ZVM(JLON,KLEV)=ZVM(JLON,KLEV)+PV(JLON,JLEV)*ZINTE(JLON)
        ZDENO(JLON)=ZDENO(JLON)+ZINTE(JLON)
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK76',1,ZHOOK_HANDLE_ARRAY(76))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK77',0,ZHOOK_HANDLE_ARRAY(77))
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        ZUM(JLON,JLEV)=ZBINID2(JLON,JLEV)*ZUM(JLON,KLEV)/MAX(100._JPRB,ZDENO(JLON))&
          &+(1._JPRB-ZBINID2(JLON,JLEV))*PU(JLON,JLEV)
        ZVM(JLON,JLEV)=ZBINID2(JLON,JLEV)*ZVM(JLON,KLEV)/MAX(100._JPRB,ZDENO(JLON))&
          &+(1._JPRB-ZBINID2(JLON,JLEV))*PV(JLON,JLEV)
        DO JTRA=1,KTRA
          ZTRAM(JLON,JLEV,JTRA)=ZBINID2(JLON,JLEV)*ZTRAM(JLON,KLEV,JTRA)/MAX(100._JPRB,ZDENO(JLON))&
            &+(1._JPRB-ZBINID2(JLON,JLEV))*PTRA(JLON,JLEV,JTRA)
        ENDDO
      ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK77',1,ZHOOK_HANDLE_ARRAY(77))
  ENDIF

  IF(LCVIMPT) THEN

    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES: IMPLICIT COMPUTATION.
    ! ---------------------------

    IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'PU_IMPL',PU,KLON,KLEV)
    IF(LLMUSCLFA_NSTEP) CALL WRSCMR(NMUSCLFA,'ZUM_IMPL',ZUM,KLON,KLEV)

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK78',0,ZHOOK_HANDLE_ARRAY(78))
  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
     DO JLON=KIDIA,KFDIA
        ! REPLACE THE UPDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_U)
        ZQU(JLON,JLEV)=PQ(JLON,JLEV)-PQU(JLON,JLEV)
        ZSU(JLON,JLEV)=ZDSE(JLON,JLEV)-ZSU(JLON,JLEV)
        ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
        ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
          ! ABOVE ITOP: ALL FLUXES ARE ZERO
          ! LEVEL KLEV: ZFORM=0 THERE, HENCE ALL THE FLUXES ARE ZERO, TOO.
          ! ELSEWHERE WE NEED ZFORM >=0 - AVOID THE ROUNDING ERRORS
          ! REQUIRE AT LEAST THAT PDIFCQ<0
      ENDDO
    ENDDO
    DO JTRA=1, KTRA
      DO JLEV=ITOP,KLEV
        DO JLON=KIDIA,KFDIA
          ZTRAM(JLON,JLEV,JTRA) = PTRA(JLON,JLEV,JTRA) - ZTRAM(JLON,JLEV,JTRA)
        ENDDO
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK78',1,ZHOOK_HANDLE_ARRAY(78))

    ! cdir unroll=8
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK79',0,ZHOOK_HANDLE_ARRAY(79))
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ZAUX=ZFORU(JLON,JLEV)/(PDELP(JLON,JLEV)+ZFORU(JLON,JLEV))
          ZDPSGDT=PDELP(JLON,JLEV)*ZGDTI*0.5_JPRB
          PDIFCQ(JLON,JLEV)=ZAUX*( PDIFCQ(JLON,JLEV-1)+&
             & ZDPSGDT*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1)) )
          IF (NCVENTR == 1) THEN
            PDIFCS(JLON,JLEV)=ZAUX*( PDIFCS(JLON,JLEV-1)+&
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) )
          ELSE
            PDIFCS(JLON,JLEV)=MIN(GCVTRMIN,ZAUX*( PDIFCS(JLON,JLEV-1)+&
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) ))
          ENDIF
          PSTRCU(JLON,JLEV)=ZAUX*( PSTRCU(JLON,JLEV-1)+&
             & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
          PSTRCV(JLON,JLEV)=ZAUX*( PSTRCV(JLON,JLEV-1)+&
             & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
          DO JTRA=1, KTRA
            PSTRCTRA(JLON,JLEV,JTRA)=ZAUX*( PSTRCTRA(JLON,JLEV-1,JTRA)+&
               & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )
          ENDDO
       ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK79',1,ZHOOK_HANDLE_ARRAY(79))

    IF (LGDDD) THEN
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK80',0,ZHOOK_HANDLE_ARRAY(80))
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ! replace the DOWNDRAFT values by the difference (psi-psi_d)
          ZQU(JLON,JLEV)=PQ(JLON,JLEV)-ZQN2D(JLON,JLEV)
          ZSU(JLON,JLEV)=ZDSE(JLON,JLEV)-ZSN2D(JLON,JLEV)
          ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUMD(JLON,JLEV)
          ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVMD(JLON,JLEV)
       ENDDO
    ENDDO
          DO JTRA=1, KTRA
      DO JLEV=ITOP,KLEV
        DO JLON=KIDIA,KFDIA
          ZTRAM(JLON,JLEV,JTRA) = PTRA(JLON,JLEV,JTRA)-ZTRAMD(JLON,JLEV,JTRA)
          ENDDO
       ENDDO
    ENDDO

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK80',1,ZHOOK_HANDLE_ARRAY(80))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK81',0,ZHOOK_HANDLE_ARRAY(81))
    ZDIFCQD(:,KLEV)=0._JPRB
    ZDIFCSD(:,KLEV)=0._JPRB
    ZSTRCUD(:,KLEV)=0._JPRB
    ZSTRCVD(:,KLEV)=0._JPRB
    DO JLEV=KLEV-1,ITOP,-1
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ZAUX=ZFORD(JLON,JLEV)/(PDELP(JLON,JLEV+1)+ZFORD(JLON,JLEV))
          ZDPSGDT=PDELP(JLON,JLEV+1)*ZGDTI*0.5_JPRB
          ZDIFCQD(JLON,JLEV)=ZAUX*( ZDIFCQD(JLON,JLEV+1)+&
             & ZDPSGDT*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1)) )
          IF (NCVENTR == 1) THEN
            ZDIFCSD(JLON,JLEV)=ZAUX*( ZDIFCSD(JLON,JLEV+1)+&
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) )
          ELSE
            ZDIFCSD(JLON,JLEV)=MAX(-GCVTRMIN,ZAUX*( ZDIFCSD(JLON,JLEV+1)+&
               & ZDPSGDT*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1)) ))
          ENDIF
          ZSTRCUD(JLON,JLEV)=ZAUX*( ZSTRCUD(JLON,JLEV+1)+&
             & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
          ZSTRCVD(JLON,JLEV)=ZAUX*( ZSTRCVD(JLON,JLEV+1)+&
             & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
          DO JTRA=1,KTRA
            ZSTRCTRAD(JLON,JLEV,JTRA)=ZAUX*( ZSTRCTRAD(JLON,JLEV+1,JTRA)+&
               & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )
          ENDDO
       ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK81',1,ZHOOK_HANDLE_ARRAY(81))

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK82',0,ZHOOK_HANDLE_ARRAY(82))
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          PDIFCQ(JLON,JLEV)=PDIFCQ(JLON,JLEV)-ZDIFCQD(JLON,JLEV)
          PDIFCS(JLON,JLEV)=PDIFCS(JLON,JLEV)-ZDIFCSD(JLON,JLEV)
          PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV)-ZSTRCUD(JLON,JLEV)
          PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV)-ZSTRCVD(JLON,JLEV)
       ENDDO
    ENDDO
          DO JTRA=1,KTRA
       DO JLEV=ITOP,KLEV-1
          DO JLON=KIDIA,KFDIA
             PSTRCTRA(JLON,JLEV,JTRA)=PSTRCTRA(JLON,JLEV,JTRA)-ZSTRCTRAD(JLON,JLEV,JTRA)
          ENDDO
       ENDDO
    ENDDO

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK82',1,ZHOOK_HANDLE_ARRAY(82))
    ENDIF  !LGDDD

  ELSE ! LCVIMPT.

    ! ---------------------------
    ! // CONVECTIVE TRANSPORT FLUXES: EXPLICIT COMPUTATION.
    ! ---------------------------

  DO JLEV=ITOP,KLEV
     DO JLON=KIDIA,KFDIA
        ! REPLACE THE UPDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_U)
        ZQU(JLON,JLEV)=PQ(JLON,JLEV)-PQU(JLON,JLEV)
        ZSU(JLON,JLEV)=ZDSE(JLON,JLEV)-ZSU(JLON,JLEV)
        ZTU(JLON,JLEV)=(PT(JLON,JLEV)-PTU(JLON,JLEV))*(RATM/PAPRSF(JLON,JLEV))**RKAPPA
        ZTVU(JLON,JLEV)=(PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)-PQLIS(JLON,JLEV)) &
          &-PTU(JLON,JLEV)*(1.0_JPRB+RETV*PQU(JLON,JLEV)-PQLIC(JLON,JLEV)))
        ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
        ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
     ENDDO
  ENDDO
          DO JTRA=1, KTRA
      DO JLEV=ITOP,KLEV
         DO JLON=KIDIA,KFDIA
             ZTRAM(JLON,JLEV,JTRA) = PTRA(JLON,JLEV,JTRA) - ZTRAM(JLON,JLEV,JTRA)
          ENDDO
     ENDDO
  ENDDO

    ! cdir unroll=8
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK83',0,ZHOOK_HANDLE_ARRAY(83))
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ZDIFCS_UD_NOCORR_DIAG(JLON,JLEV)=ZGDTI*ZFORU(JLON,JLEV)&
            &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          IF (NPRONI == 1) THEN  
             ZAUX=ZFORU(JLON,JLEV)/(1._JPRB+ZFORU(JLON,JLEV)/PDELP(JLON,JLEV))
          ELSEIF (NPRONI == 2) THEN  
             ZAUX=MIN(ZFORU(JLON,JLEV),GPRONI*PDELP(JLON,JLEV))
          ELSE   
             ZAUX=ZFORU(JLON,JLEV)
          ENDIF   
          PDIFCQ(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1))
          PDIFCS(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PDIFCTH(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZTU(JLON,JLEV)+ZTU(JLON,JLEV+1))
          PPRODTH(JLON,JLEV)=-ZGDTI*ZAUX*RG*2.0_JPRB/(ZRHO(JLON,JLEV)+ZRHO(JLON,JLEV+1)) &
           &*(ZTVU(JLON,JLEV)+ZTVU(JLON,JLEV+1))/(PT(JLON,JLEV)+PT(JLON,JLEV+1))
          PSTRCU(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1))
          PSTRCV(JLON,JLEV)=ZGDTI*ZAUX*0.5_JPRB*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1))
          DO JTRA=1,KTRA
            PSTRCTRA(JLON,JLEV,JTRA)=ZGDTI*ZAUX*0.5_JPRB*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA))
          ENDDO
       ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK83',1,ZHOOK_HANDLE_ARRAY(83))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK84',0,ZHOOK_HANDLE_ARRAY(84))

    IF (LGDDD) THEN
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ! REPLACE THE DOWNDRAFT VALUES BY THE DIFFERENCE (PSI-PSI_D)
          ZQU(JLON,JLEV)=PQ(JLON,JLEV)-ZQN2D(JLON,JLEV)
          ZSU(JLON,JLEV)=ZDSE(JLON,JLEV)-ZSN2D(JLON,JLEV)
          ZTU(JLON,JLEV)=(PT(JLON,JLEV)-ZTN2D(JLON,JLEV))*(RATM/PAPRSF(JLON,JLEV))**RKAPPA
          ZTVU(JLON,JLEV)=(PT(JLON,JLEV)*(1.0_JPRB+RETV*PQ(JLON,JLEV)) &
            &-ZTN2D(JLON,JLEV)*(1.0_JPRB+RETV*ZQN2D(JLON,JLEV)))
          ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUMD(JLON,JLEV)
          ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVMD(JLON,JLEV)
       ENDDO
    ENDDO
          DO JTRA=1,KTRA
       DO JLEV=ITOP,KLEV
          DO JLON=KIDIA,KFDIA
             ZTRAM(JLON,JLEV,JTRA)=PTRA(JLON,JLEV,JTRA)-ZTRAMD(JLON,JLEV,JTRA)
          ENDDO
       ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK84',1,ZHOOK_HANDLE_ARRAY(84))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK85',0,ZHOOK_HANDLE_ARRAY(85))
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
       DO JLON=KIDIA,KFDIA
          ZDIFCS_DD_NOCORR_DIAG(JLON,JLEV)=ZGDTI*ZFORD(JLON,JLEV)&
            &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          IF (NPRONI == 1) THEN  
            ZAUX=ZFORD(JLON,JLEV)/(1._JPRB+ZFORD(JLON,JLEV)/PDELP(JLON,JLEV))
          ELSEIF (NPRONI == 2) THEN  
            ZAUX=MIN(ZFORD(JLON,JLEV),GPRONI*PDELP(JLON,JLEV))
          ELSE
            ZAUX=ZFORD(JLON,JLEV)
          ENDIF   
          PDIFCQ(JLON,JLEV)=PDIFCQ(JLON,JLEV)-ZGDTI*ZAUX&
            &*0.5_JPRB*(ZQU(JLON,JLEV)+ZQU(JLON,JLEV+1))
            PDIFCS(JLON,JLEV)=PDIFCS(JLON,JLEV)-ZGDTI*ZAUX&
              &*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PDIFCTH(JLON,JLEV)=PDIFCTH(JLON,JLEV)-ZGDTI*ZAUX&
            &*0.5_JPRB*(ZTU(JLON,JLEV)+ZTU(JLON,JLEV+1))
          PPRODTH(JLON,JLEV)=PPRODTH(JLON,JLEV)+ZGDTI*ZAUX*RG*2.0_JPRB/(ZRHO(JLON,JLEV)+ZRHO(JLON,JLEV+1)) &
            &*(ZTVU(JLON,JLEV)+ZTVU(JLON,JLEV+1))/(PT(JLON,JLEV)+PT(JLON,JLEV+1))
          ZDIFCS_DD_DIAG(JLON,JLEV)=-ZGDTI*ZAUX*0.5_JPRB*(ZSU(JLON,JLEV)+ZSU(JLON,JLEV+1))
          PSTRCU(JLON,JLEV)=PSTRCU(JLON,JLEV)-ZGDTI*ZAUX*0.5_JPRB*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1))
          PSTRCV(JLON,JLEV)=PSTRCV(JLON,JLEV)-ZGDTI*ZAUX*0.5_JPRB*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1))
          DO JTRA=1,KTRA
            PSTRCTRA(JLON,JLEV,JTRA)= PSTRCTRA(JLON,JLEV,JTRA)&
              & -ZGDTI*ZAUX*0.5_JPRB*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA))
          ENDDO
       ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD_85',1,ZHOOK_HANDLE85)
    ENDIF  !LGDDD

  ENDIF ! LCVIMPT.

  ! // SETTING ALL RESULTS TO ZERO IN CASE OF CONDENSATION UNDER A GIVEN
  ! // THRESHOLD OR "KNND" ALREADY ZERO.
  ! // EXAMPLE: SCO CAN BE SET TO THE DRIZZLE LEVEL: SCO=2.8E-5 kg/m2/s.

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK86',0,ZHOOK_HANDLE_ARRAY(86))
  DO JLON=KIDIA,KFDIA
    ZZVAL=PFCCQL(JLON,KLEV)+PFCCQI(JLON,KLEV)-SCO
    KNND(JLON)=KNND(JLON)*MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZZVAL))
  ENDDO

  IF(LCVNAUV) THEN
    ! RECOMPUTE PSTRCU AND PSTRCV FROM AN IDEA INSPIRED BY FRANCOIS BOUYSSEL (2012-08-01) AND 3MT.
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
     ZNND(JLON)=1.0_JPRB-REAL(KNLAB(JLON,KLEV))
     ZI1(JLON,KLEV)=REAL(KNLAB(JLON,KLEV))
     ZUM(JLON,KLEV)=PU(JLON,KLEV)
     ZVM(JLON,KLEV)=PV(JLON,KLEV)
     ZBET(JLON,KLEV)=REAL(KNLAB(JLON,KLEV))
     ZA13(JLON,KLEV)=PU(JLON,KLEV)
     ZA14(JLON,KLEV)=PV(JLON,KLEV)
    ENDDO
     DO JTRA=1,KTRA
      DO JLON=KIDIA,KFDIA
       ZTRAM(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
       ZA15(JLON,KLEV,JTRA)=PTRA(JLON,KLEV,JTRA)
     ENDDO
    ENDDO

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK86',1,ZHOOK_HANDLE_ARRAY(86))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK88',0,ZHOOK_HANDLE_ARRAY(88))
    DO JLEV=KLEV-1,ITOP,-1
!DEC$ IVDEP
     DO JLON=KIDIA,KFDIA
      ZI1(JLON,JLEV)=REAL(KNLAB(JLON,JLEV)*ZNND(JLON))
      ZNND(JLON)=MIN(ZNND(JLON),1.0_JPRB-REAL(KNLAB(JLON,JLEV)))
      ZBAS=REAL(KNLAB(JLON,JLEV)*(1.0_JPRB-REAL(KNLAB(JLON,JLEV+1))),JPRB)
      ZNBA=REAL(KNLAB(JLON,JLEV)*KNLAB(JLON,JLEV+1),JPRB)
      ZMIX=ZRMIX(JLON,JLEV)
      ZBET(JLON,JLEV)= ZNBA*ZBET(JLON,JLEV+1)*(1.0_JPRB-ZMIX)+ZBAS
      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV+1)&
       &+(ZMIX*(PU(JLON,JLEV+1)-ZUM(JLON,JLEV+1))&
       &+TUDGP*(PU(JLON,JLEV)-PU(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
       &+PU(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV+1)&
       &+(ZMIX*(PV(JLON,JLEV+1)-ZVM(JLON,JLEV+1))&
       &+TUDGP*(PV(JLON,JLEV)-PV(JLON,JLEV+1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
       &+PV(JLON,JLEV)*(1.0_JPRB-ZNBA)
      ZA13(JLON,JLEV)=ZA13(JLON,JLEV+1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV+1)*ZNBA+PV(JLON,JLEV)*ZBAS
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA)=(ZTRAM(JLON,JLEV+1,JTRA)&
         &+(ZMIX*(PTRA(JLON,JLEV+1,JTRA)-ZTRAM(JLON,JLEV+1,JTRA))&
         &+TUDGP*(PTRA(JLON,JLEV,JTRA)-PTRA(JLON,JLEV+1,JTRA)))&
         &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS) )*ZNBA&
         &+PTRA(JLON,JLEV,JTRA)*(1.0_JPRB-ZNBA)
        ZA15(JLON,JLEV,JTRA)=ZA15(JLON,JLEV+1,JTRA)*ZNBA+PTRA(JLON,JLEV,JTRA)*ZBAS
      ENDDO
     ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK88',1,ZHOOK_HANDLE_ARRAY(88))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK89',0,ZHOOK_HANDLE_ARRAY(89))
    DO JLEV=ITOP,KLEV
!DEC$ IVDEP
     DO JLON=KIDIA,KFDIA
      ZZ=1.0_JPRB-ZBET(JLON,JLEV)
      ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
      ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
      ZUM(JLON,JLEV)=PU(JLON,JLEV)-ZUM(JLON,JLEV)
      ZVM(JLON,JLEV)=PV(JLON,JLEV)-ZVM(JLON,JLEV)
      DO JTRA=1,KTRA
        ZTRAM(JLON,JLEV,JTRA)=ZZ*ZTRAM(JLON,JLEV,JTRA)+ZBET(JLON,JLEV)*ZA15(JLON,JLEV,JTRA)
        ZTRAM(JLON,JLEV,JTRA)=PTRA(JLON,JLEV,JTRA)-ZTRAM(JLON,JLEV,JTRA)
      ENDDO
     ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK89',1,ZHOOK_HANDLE_ARRAY(89))
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK90',0,ZHOOK_HANDLE_ARRAY(90))
    ZNND(:) = 1.0_JPRB
    PSTRCU(:,:)=0._JPRB
    PSTRCV(:,:)=0._JPRB
    PSTRCTRA(:,:,:)=0._JPRB
    DO JLEV=ITOP,KLEV-1
!DEC$ IVDEP
     DO JLON=KIDIA,KFDIA
      ZNND(JLON) = MIN(ZNND(JLON),1.0_JPRB-ZI1(JLON,JLEV))
      ZZ=(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV))&
      & /(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV-1))
      ZAUX=TVFC*ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV)+TVFC*ZFORM(JLON,JLEV))
      ZDPSGDT=PDELP(JLON,JLEV)*ZGDTI*0.5_JPRB
      PSTRCU(JLON,JLEV)=ZNND(JLON)*ZAUX*( PSTRCU(JLON,JLEV-1)+&
       & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )+&
       & (1.0_JPRB-ZNND(JLON))*PSTRCU(JLON,JLEV-1)*ZZ
      PSTRCV(JLON,JLEV)=ZNND(JLON)*ZAUX*( PSTRCV(JLON,JLEV-1)+&
       & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )+&
       & (1.0_JPRB-ZNND(JLON))*PSTRCV(JLON,JLEV-1)*ZZ
      DO JTRA=1,KTRA
        PSTRCTRA(JLON,JLEV,JTRA)=ZNND(JLON)*ZAUX*( PSTRCTRA(JLON,JLEV-1,JTRA)+&
         & ZDPSGDT*(ZTRAM(JLON,JLEV,JTRA)+ZTRAM(JLON,JLEV+1,JTRA)) )+&
         & (1.0_JPRB-ZNND(JLON))*PSTRCTRA(JLON,JLEV-1,JTRA)*ZZ
      ENDDO
     ENDDO
    ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK90',1,ZHOOK_HANDLE_ARRAY(90))
  ENDIF ! LCVNAUV

  ! ------------------------------------------------------------------
  ! BEFORE LEAVING
  ! - LEAVE IN PUDOM THE RELATIVE UPDRAFT VELOCITY FOR ADVECTION
  ! - RESET ALL FLUXES TO ZERO WHERE KNND IS ZERO
  ! AND ALSO RESET PUDOM AND PUDAL
  ! ------------------------------------------------------------------

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK91',0,ZHOOK_HANDLE_ARRAY(91))
  DO JLEV=0,KLEV
    PFHEVPP(:,JLEV)=0._JPRB
  ENDDO
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=KNND(JLON)*PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=KNND(JLON)*PFPLCN(JLON,JLEV)
      PDIFCQ(JLON,JLEV)=KNND(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCQL(JLON,JLEV)=KNND(JLON)*PDIFCQL(JLON,JLEV)
      PDIFCQI(JLON,JLEV)=KNND(JLON)*PDIFCQI(JLON,JLEV)
      PDIFCS(JLON,JLEV)=KNND(JLON)*PDIFCS(JLON,JLEV)
      PDIFCTH(JLON,JLEV)=KNND(JLON)*PDIFCTH(JLON,JLEV)
      PPRODTH(JLON,JLEV)=KNND(JLON)*PPRODTH(JLON,JLEV)
      PSTRCU(JLON,JLEV)=KNND(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=KNND(JLON)*PSTRCV(JLON,JLEV)
      PFCCQL(JLON,JLEV)=KNND(JLON)*PFCCQL(JLON,JLEV)
      PFCCQI(JLON,JLEV)=KNND(JLON)*PFCCQI(JLON,JLEV)
      PFPEVPCL(JLON,JLEV)=KNND(JLON)*PFPEVPCL(JLON,JLEV)
      PFPEVPCN(JLON,JLEV)=KNND(JLON)*PFPEVPCN(JLON,JLEV)
      PFHEVPP(JLON,JLEV)=KNND(JLON)*PFHEVPP(JLON,JLEV)
      PFHMLTS(JLON,JLEV)=KNND(JLON)*PFHMLTS(JLON,JLEV)
    ENDDO
  ENDDO
  DO JTRA=1,KTRA
    DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        PSTRCTRA(JLON,JLEV,JTRA)=REAL(KNND(JLON),JPRB)*PSTRCTRA(JLON,JLEV,JTRA)
      ENDDO
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK91',1,ZHOOK_HANDLE_ARRAY(91))

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK92',0,ZHOOK_HANDLE_ARRAY(92))
  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      KNLAB(JLON,JLEV)=KNND(JLON)*KNLAB(JLON,JLEV)
      PQLIC(JLON,JLEV)=KNND(JLON)*PQLIC(JLON,JLEV)*FQLIC*PNEBC(JLON,JLEV)*KNLAB(JLON,JLEV)
      PNEBC(JLON,JLEV)=KNND(JLON)*MAX(ZEPS0,MIN(ZNEBCX,PNEBC(JLON,JLEV)*FNEBC&
      &*NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PQLIC(JLON,JLEV)+0.0_JPRB)))))
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK92',1,ZHOOK_HANDLE_ARRAY(92))

!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK93',0,ZHOOK_HANDLE_ARRAY(93))
  IF (ITOP > 1) THEN
    ! variables used in output subroutines
    PDETR_U(KIDIA:KFDIA,1:ITOP-1)=GCVENTR_MIN
    PDETR_D(KIDIA:KFDIA,1:ITOP-1)=GCVENTR_MIN
  ENDIF
  DO JLEV=ITOP,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      PUDAL(JLON,JLEV)=KNND(JLON)*PUDAL(JLON,JLEV)
      PDDAL(JLON,JLEV)=KNND(JLON)*PDDAL(JLON,JLEV)

      IF(.NOT.LCVNHD) THEN
      ! VERTICAL VELOCITY IN M/S COMPUTED FROM VERTICAL VELOCITY IN PA/S.
        ZWU(JLON,JLEV)=-PUDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
        ZWD(JLON,JLEV)=-PDDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*RG)
      ENDIF

      IQLIC(JLON)=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PQLIC(JLON,JLEV)+0.0_JPRB)))
      ! ENTRAINMENT/DETRAINMENT (IN S**-1).
      PENTR_U_TOT(JLON,JLEV)=ZWU(JLON,JLEV)*PUDAL(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)
      PENTR_U(JLON,JLEV)=ZWU(JLON,JLEV)*PUDAL(JLON,JLEV)*ZEPSILON_U(JLON,JLEV)*IQLIC(JLON)
      PDETR_U(JLON,JLEV)=KNLAB(JLON,JLEV)*ZWU(JLON,JLEV)*PUDAL(JLON,JLEV)*ZDELTA_U(JLON,JLEV)&
         &+(1-KNLAB(JLON,JLEV))*GCVENTR_MIN
      PENTR_D(JLON,JLEV)=-ZWD(JLON,JLEV)*PDDAL(JLON,JLEV)&
         &*(ZEPS_ORGD(JLON,JLEV)*PAPRSF(JLON,JLEV)&
         &/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZENTRX)*RG*IQLIC(JLON)*GCVFRR
      PDETR_D(JLON,JLEV)=-ZWD(JLON,JLEV)*PDDAL(JLON,JLEV)&
         &*(ZDEL_ORGD(JLON,JLEV)*PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))+ZENTRX)*RG*GCVFRR
    ENDDO
  ENDDO
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK93',1,ZHOOK_HANDLE_ARRAY(93))

  ! -------------------------------------------------
  ! NUMERICAL VERTICAL FILTER OF TRANSPORT FLUXES
  ! TO AVOID 2-VERTICAL-LEVELS INSTABILITY.
  ! -------------------------------------------------

  ZA=GCVTSMO
  ZA2=ZA*0.5_JPRB
!IF (LHOOK) CALL DR_HOOK('ACMTUD:BLOCK94',0,ZHOOK_HANDLE_ARRAY(94))
  DO JLEV=KLEV-1,ITOP,-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
       PDIFCQ(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQ(JLON,JLEV)+ZA2*(PDIFCQ(JLON,JLEV-1)+PDIFCQ(JLON,JLEV+1))
       PDIFCQI(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQI(JLON,JLEV)+ZA2*(PDIFCQI(JLON,JLEV-1)+PDIFCQI(JLON,JLEV+1))
       PDIFCQL(JLON,JLEV)=(1._JPRB-ZA)*PDIFCQL(JLON,JLEV)+ZA2*(PDIFCQL(JLON,JLEV-1)+PDIFCQL(JLON,JLEV+1))
       PDIFCS(JLON,JLEV)=(1._JPRB-ZA)*PDIFCS(JLON,JLEV)+ZA2*(PDIFCS(JLON,JLEV-1)+PDIFCS(JLON,JLEV+1))
       PDIFCTH(JLON,JLEV)=(1._JPRB-ZA)*PDIFCTH(JLON,JLEV)+ZA2*(PDIFCTH(JLON,JLEV-1)+PDIFCTH(JLON,JLEV+1))
       PSTRCU(JLON,JLEV)=(1._JPRB-ZA)*PSTRCU(JLON,JLEV)+ZA2*(PSTRCU(JLON,JLEV-1)+PSTRCU(JLON,JLEV+1))
       PSTRCV(JLON,JLEV)=(1._JPRB-ZA)*PSTRCV(JLON,JLEV)+ZA2*(PSTRCV(JLON,JLEV-1)+PSTRCV(JLON,JLEV+1))
    ENDDO
  ENDDO
       DO JTRA=1,KTRA
    ZATRA=PSMOOTRA(JTRA)
    ZA2TRA=ZATRA*0.5_JPRB
    DO JLEV=KLEV-1,ITOP,-1
      DO JLON=KIDIA,KFDIA
        PSTRCTRA(JLON,JLEV,JTRA)=(1._JPRB-ZATRA)*PSTRCTRA(JLON,JLEV,JTRA) &
         &                      +ZA2TRA*(PSTRCTRA(JLON,JLEV-1,JTRA)+PSTRCTRA(JLON,JLEV+1,JTRA))
       ENDDO
    ENDDO
  ENDDO

  DO JLEV=ITOP,KLEV-1
    DO JLON=KIDIA,KFDIA
       PMF_UP(JLON,JLEV)=ZGDTI*ZFORM(JLON,JLEV)  ! "mass flux" for implicit formulation
    ENDDO
  ENDDO
ENDIF ! IF ITOP < KLEV+1

PMU(:,:)=0.0_JPRB
PMD(:,:)=0.0_JPRB
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
    PMU(JLON,JLEV) = MAX(0._JPRB,ZGDTI*ZFORU(JLON,JLEV))
    PMD(JLON,JLEV) = MAX(0._JPRB,PMU(JLON,JLEV)-ZGDTI*ZFORM(JLON,JLEV))
  ENDDO
ENDDO

!
!-------------------------------------------------
! Momentum is not transported as thermodynamic variables (theta, qt).
!-------------------------------------------------
!
DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PSTRCU(JLON,JLEV)=GCVUV*PSTRCU(JLON,JLEV)
    PSTRCV(JLON,JLEV)=GCVUV*PSTRCV(JLON,JLEV)
  ENDDO
ENDDO
!
!------------------------
! DIAGNOSTICS FOR MUSC :
!------------------------
!
IF(LLMUSCLFA_NSTEP) THEN
  ! 1D MODEL MUSC DIAGNOSTICS.
  CALL WRSCMR(NMUSCLFA,'PSTRCU_FIN',PSTRCU,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'ZDIFCS_DD_DIAG',ZDIFCS_DD_DIAG,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'ZDIFCS_UD_NOCORR_DIAG',ZDIFCS_UD_NOCORR_DIAG,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'ZDIFCS_DD_NOCORR_DIAG',ZDIFCS_DD_NOCORR_DIAG,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'ZMOD',ZMOD,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZBINID',ZBINID,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PALF',PALF,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZTALF_SCE',ZTALF_SCE,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZTALF_SIN',ZTALF_SIN,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZRE_SATDEF',ZRE_SATDEF,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZRE_HEIGHT',ZRE_HEIGHT,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZRE_KEDD',ZRE_KEDD,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZRE',ZRE,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDEPTH',ZDEPTH,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZTAU_DIAG',ZTAU_DIAG,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZSATDEF',ZSATDEF,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZTAU',ZTAU,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZBUOY',ZBUOY,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'BCC_C',ZBCC_C_FULL,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'BCC',ZBCC,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZWU',ZWU,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZWD',ZWD,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZENTR_T_LOC',ZENTR_T_LOC,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZFLUXDIAG',ZFLUXDIAG,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDIAG_EPS_ORG',ZDIAG_EPS_ORG,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDIAG_EPS_TUR',ZDIAG_EPS_TUR,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZEPSILON_U',ZEPSILON_U,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZEPS_ORG',ZEPS_ORG,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZEPS_TUR',ZEPS_TUR,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDEL_ORG',ZDEL_ORG,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDELTA_U',ZDELTA_U,KLON,KLEV)
  IF(LCVNHD) THEN
    ZFORM(:,:)=0._JPRB
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        ZFORM(JLON,JLEV)=PUDAL(JLON,JLEV)*PAPRSF(JLON,JLEV)&
          & /(PR(JLON,JLEV)*PT(JLON,JLEV))*ZWU(JLON,JLEV)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=0,KLEV
      DO JLON=KIDIA,KFDIA
        ZFORM(JLON,JLEV)=ZGDTI*ZFORM(JLON,JLEV)
      ENDDO
    ENDDO
  ENDIF
  CALL WRSCMR(NMUSCLFA,'MF',ZFORM,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'FLUX_MASSE_CV_UD',ZFORM,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'CVV',ZVVER,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'PQLIC',PQLIC,KLON,KLEV)

  ! BUDGET OF VERTICAL VELOCITY.
  CALL WRSCMR(NMUSCLFA,'BILOM',ZVVERDM,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'BILOM+TBUOY',ZDIAGFLO,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'BILOM+TFRIC',ZDIAGFRICTION,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'BILOM+TTRANSP',ZDIAGTRANSP,KLON,KLEV)

  CALL WRSCMR(NMUSCLFA,'ZBUO_W',ZBUO_W,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZVACC',ZVACC,KLON,KLEV)

  CALL WRSCMR(NMUSCLFA,'TVUD-TVENV',ZDIAG_ECART_TV,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'TUD-TENV',ZDIAG_ECART_T,KLON,KLEV)

  ! MISC.
  CALL WRSCMR(NMUSCLFA,'ZPHIF_U',ZPHIF_U,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'ZDIFF_Z_UD',ZDIFF_Z_UD,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'PCAPE',PCAPE,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZS11',ZS11,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZS12',ZS12,KLON,1)
  CALL WRSCMR(NMUSCLFA,'ZLISS',ZLISS,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZBINBUO',ZBINBUO,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PTU',PTU,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PQU',PQU,KLON,KLEV)

  CALL WRSCMR(NMUSCLFA,'PUDAL_fin_acmtud',PUDAL,KLON,KLEV)
ENDIF


END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACMTUD',1,ZHOOK_HANDLE)
END SUBROUTINE ACMTUD
