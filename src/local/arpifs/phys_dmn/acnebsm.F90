SUBROUTINE ACNEBSM ( YDCST, YDPHY0,KIDIA, KFDIA, KLON, KTDIA, KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT -
 & PT, PQ, PQL, PQI, &
 & PAPHI, PAPRSF, PCP, PR, &
 & PGM, YDSTA, &
 ! - OUTPUT -
 & PQCS, PNEBS, PRHCRI, PICE, PQSATS )  

!**** *ACNEBSM* - CALCUL DU SEUIL D'HUMIDITE CRITIQUE, DE LA 
!                 NEBULOSITE ET DE CONDENSATION - EVAPORATION
!                 SELON SMITH, QJRMS 90.
!                 COMPUTATION OF CRITICAL RELATIVE HUMIDITY,
!                 CLOUDINESS AND CONDENSATION - EVAPORATION
!                 FROM SMITH (QJRMS, 1990).

!**   Interface.
!     ----------
!        *CALL* *ACNEBSM*

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
!            : START OF HORIZONTAL LOOP 
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
!            : END OF HORIZONTAL LOOP
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
!            : HORIZONTAL DIMENSION  
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES. 
!            : START OF THE VERTICAL LOOP IN THE PHYSICS. 
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
!            : END OF VERTICAL LOOP AND VERTICAL DIMENSION

! - 1D (1:KLEV) .

! PGM        : FACTEUR D'ECHELLE.
!            : MAPPING FACTOR.
! PVETAF     : COORDONNEE VERTICALE ETA.
!            : VERTICAL COORDINATE ETA.

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
!            : GEOPOTENTIAL ON HALF-LEVELS.

! - 2D (1:KLEV) .

! PT         : TEMPERATURE
!            : TEMPERATURE
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
!            : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQL        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE LIQUIDE.
!            : SPECIFIC HUMIDITY OF LIQUID CONDENSATED WATER.
! PQI        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE GLACE.
!            : SPECIFIC HUMIDITY OF SOLID CONDENSATED WATER.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
!            : PRESSURE ON FULL LEVELS.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
!            : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
!            : GAS CONSTANT FOR AIR.

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (1:KLEV) .

! PQCS       : CONTENU "STRATIFORME" EN CONDENSAT NUAGEUX (LIQ. + SOL.).
!            : STRATIRORM CLOUD WATER (LIQUID + SOLID).
! PNEBS      : NEBULOSITE PARTIELLE STRATIFORME APRES AJUSTEMENT.
!            : STRATIFORM FRACTIONAL CLOUDINESS APRES AJUSTEMENT.
! PRHCRI     : HUMIDITE CRITIQUE POUR AJUSTEMENT.                    
!            : CRITICAL HUMIDITY FOR ADJUSTMENT.                     
! PICE       : PROPORTION DE LA GLACE.
!            : PROPORTION OF ICE.
! PQSATS     : HUMIDITE SPECIFIQUE DE SATURATION.
!            : SPECIFIC HUMIDITY OF SATURATION.

!-----------------------------------------------------------------------

!     Auteur.
!     -------
!     99-07, Philippe Lopez, CNRM/GMME/RECYF

!     Modifications.
!     --------------
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!     2004-10-15, F. Bouyssel : Introduction of PGM
!     2005-03-03, F. Bouyssel : Improve consistency between Ns and Qs
!                             : Qsat(Tl,P) instead of Qsat(T,P)
!     2005-07-18, F. Bouyssel : Introduction of RFACNSM (change in cloudiness)
!                             : Modification of RHcrit above RETAMIN
!     2005-09-26, F. Bouyssel : Introduction of RQCRNS (change in Qc0,Neb0)
!     2005-11-10, F. Bouyssel : Vertical smoothing of cloudiness (LNSMLIS)
!     2006-01-25, F. Bouyssel : Modification in the use of RFACNSM
!     2006-10-30, F. Bouyssel : Autoconversion before radiation
!     2012-02-22, R. Brozkova : Cleaning obsolet options (LNSMLIS, LAUTONEB)
!     2012-12-12, F. Bouyssel : Limitation on PNEBS as done before
!     T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!     2016-10-04, P. Marguinaud : Port to single precision
!-----------------------------------------------------------------------

USE PARKIND1  , ONLY : JPIM     ,JPRB      ,JPRD
USE YOMHOOK   , ONLY : LHOOK,   DR_HOOK

USE YOMCST    , ONLY : TCST
USE YOMPHY0   , ONLY : TPHY0
USE YOMSTA    , ONLY : TSTA

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM   (KLON) 
TYPE(TSTA)        ,INTENT(IN)    :: YDSTA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL   (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI   (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQCS  (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNEBS (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PRHCRI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PICE  (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)   :: PQSATS(KLON,KLEV) 

REAL(KIND=JPRB) :: ZRHCRI1(KLON), ZA(KLON), ZB(KLON), ZC(KLON) 
REAL(KIND=JPRB) :: ZVETAF
REAL(KIND=JPRB) :: ZFACT,ZFACT0,ZFACTA,ZFACTB,ZFACTC
REAL(KIND=JPRB) :: ZEPS1,ZRHC,ZRATQ
REAL(KIND=JPRB) :: ZSQRT6,ZRDSRV,ZQL,ZQI,ZLV
REAL(KIND=JPRB) :: ZLS,ZLEFF,ZTLIQ,ZSIGS,ZRAT2,ZQC,ZQCS
REAL(KIND=JPRB) :: ZESAT,ZESP,ZQSAT
INTEGER(KIND=JPIM) :: JLON,JLEV
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "fcttrm.ycst.h"
#include "fctdoi.ycst.h"
                                                                                
!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACNEBSM',0,ZHOOK_HANDLE)
ASSOCIATE(RHCRIT1=>YDPHY0%RHCRIT1, TEQH=>YDPHY0%TEQH, RHCRIT2=>YDPHY0%RHCRIT2, &
 & RDTFAC=>YDPHY0%RDTFAC, RQCRNS=>YDPHY0%RQCRNS, RFACNSM=>YDPHY0%RFACNSM, &
 & RETAMIN=>YDPHY0%RETAMIN, GRHCMOD=>YDPHY0%GRHCMOD)
!     ------------------------------------------------------------------

!     I - CRITICAL RELATIVE HUMIDITY PROFILE DEPENDENT ON THE LOCAL RESOLUTION.

ZFACT0=(RETAMIN*(RETAMIN-1.0_JPRB))**2
ZFACTA=(2.0_JPRB*RETAMIN-1.0_JPRB)
ZFACTB=(1.0_JPRB-3._JPRB*RETAMIN**2)
ZFACTC=(3._JPRB*RETAMIN-2.0_JPRB)*RETAMIN

DO JLON=KIDIA,KFDIA
  ZRHCRI1(JLON)=RHCRIT1+(RHCRIT2-RHCRIT1)&
   &           * GRHCMOD * EXP(-1.0_JPRB/(TEQH*PGM(JLON)))
  ZFACT=(RHCRIT2-ZRHCRI1(JLON))/ZFACT0
  ZA(JLON)=ZFACTA*ZFACT
  ZB(JLON)=ZFACTB*ZFACT
  ZC(JLON)=ZFACTC*ZFACT
ENDDO

DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    ZVETAF = MAX(YDSTA%SVETAF(JLEV),RETAMIN)
    PRHCRI(JLON,JLEV) = ZA(JLON) * ZVETAF**3&
     & + ZB(JLON) * ZVETAF**2&
     & + ZC(JLON) * ZVETAF&
     & + RHCRIT2
  ENDDO   
ENDDO   

!     II - CALCUL DE LA CONDENSATION - EVAPORATION ET DE LA NEBULOSITE
!          COMPUTATION OF CONDENSATION - EVAPORATION AND CLOUDINESS
!          FROM SMITH (QJRMS, 1990).

IF (JPRB == JPRD) THEN
  ZEPS1=1.E-12_JPRB
ELSE
  ZEPS1=1.E-06_JPRB
ENDIF
ZSQRT6 = SQRT(6._JPRB)
ZRDSRV = YDCST%RD / YDCST%RV

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
      !
    ZRHC = PRHCRI(JLON,JLEV)
    ZQL = PQL(JLON,JLEV)
    ZQI = PQI(JLON,JLEV)
    ZQC = ZQL + ZQI
    PICE(JLON,JLEV) = FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC)
    ZLV = FOLH(PT(JLON,JLEV),0.0_JPRB) 
    ZLS = FOLH(PT(JLON,JLEV),1.0_JPRB) 
    ZLEFF = ZLV * (1.0_JPRB-PICE(JLON,JLEV)) + ZLS * PICE(JLON,JLEV)
    ZTLIQ = PT(JLON,JLEV) - ( ZLV*ZQL + ZLS*ZQI ) / PCP(JLON,JLEV)
    ZESAT = FOEW(ZTLIQ,0.0_JPRB) * (1.0_JPRB-PICE(JLON,JLEV))&
     &    + FOEW(ZTLIQ,1.0_JPRB) *           PICE(JLON,JLEV)  
    ZESP = ZESAT / PAPRSF(JLON,JLEV)
    ZQSAT = FOQS(ZESP)
    PQSATS(JLON,JLEV) = ZQSAT
      !
    ZSIGS = (1.0_JPRB-ZRHC) / ZSQRT6 * ZQSAT&
     & / (1.0_JPRB + ZLEFF * ZLEFF * ZQSAT&
     & * ZRDSRV / PR(JLON,JLEV)&
     & / ZTLIQ / ZTLIQ / PCP(JLON,JLEV))  
      !
    ZRATQ = (PQ(JLON,JLEV)+ZQC)/ZQSAT
    ZRAT2 = (ZRATQ - 1.0_JPRB)/(1.0_JPRB - ZRHC)
      !
    IF(ZRATQ <= ZRHC) THEN
      ZQCS = 0.0_JPRB
      PNEBS(JLON,JLEV) = 0.0_JPRB
    ELSEIF(ZRATQ <= 1.0_JPRB.AND.ZRATQ > ZRHC) THEN
      ZQCS = (1.0_JPRB + ZRAT2)**3 / ZSQRT6
      PNEBS(JLON,JLEV) = 0.5_JPRB * (1.0_JPRB+ZRAT2)**2
    ELSEIF(ZRATQ < (2.0_JPRB-ZRHC).AND.ZRATQ > 1.0_JPRB) THEN
      ZQCS = ZRAT2 * ZSQRT6 + (1.0_JPRB - ZRAT2)**3 / ZSQRT6
      PNEBS(JLON,JLEV) = 1.0_JPRB - 0.5_JPRB*(1.0_JPRB-ZRAT2)**2
    ELSE
      ZQCS = ZRAT2 * ZSQRT6
      PNEBS(JLON,JLEV) = 1.0_JPRB
    ENDIF
      !
    PQCS (JLON,JLEV) = YDPHY0%RSURF(JLEV) * ZQCS * ZSIGS
    PNEBS(JLON,JLEV) = YDPHY0%RSURF(JLEV) * PNEBS(JLON,JLEV) * RFACNSM
    PNEBS(JLON,JLEV) = MIN(1.0_JPRB-ZEPS1,MAX(PNEBS(JLON,JLEV),ZEPS1))
      !
  ENDDO
ENDDO

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACNEBSM',1,ZHOOK_HANDLE)
END SUBROUTINE ACNEBSM
