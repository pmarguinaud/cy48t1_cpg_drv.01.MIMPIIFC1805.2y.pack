!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACNEBXRS ( YDPHY,YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PQ,PQC,PQSAT,&
 ! - OUTPUT 2D .
 & PNEB)  

!**** *ACNEBXR* - CALCUL DE LA NEBULOSITE XU-RANDALL.
!                 COMPUTATION OF XU-RANDALL CLOUDINESS.

!     Sujet.
!     ------
!     CALCUL DE LA NEBULOSITE CONFORMEMENT A XU ET RANDALL, JAS 96.
!     CLOUDINESS COMPUTATION, AS IN XU AND RANDALL, JAS 96.

!**   Interface.
!     ----------
!        *CALL* *ACNEBXRS*

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA, KFDIA : BORNES BOUCLES HORIZONTALES   (IST,IEND DANS CPG).
! KIDIA, KFDIA : START/END OF HORIZONTAL LOOP  (IST,IEND IN *CPG*).
! KLON : DIMENSION HORIZONTALE                 (NPROMA DANS CPG).
! KLON : HORIZONTAL DIMENSION                  (NPROMA IN *CPG*).
! KTDIA : DEBUT BOUCLE VERTICALE DANS LA PHYSIQUE.
! KTDIA : START OF THE VERTICAL LOOP IN THE PHYSICS (IF SOME LEVELS ARE
!                     SKIPPED AT THE TOP OF THE MODEL).
! KLEV : FIN BOUCLE VERTICE ET DIMENSION VERTICALE (NFLEV DANS CPG).
! KLEV : END OF VERTICAL LOOP AND VERTICAL DIMENSION(NFLEV IN *CPG*).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQC        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE (LIQUIDE + GLACE).
! PQC        : SPECIFIC HUMIDITY OF CONDENSATED WATER.
! PQSAT      : HUMIDITE SPECIFIQUE DE SATURATION.
! PQSAT      : SPECIFIC HUMIDITY AT SATURATION.

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (1:KLEV) .

! PNEB       : NEBULOSITE PARTIELLE "RADIATIVE".
! PNEB       : FRACTIONAL CLOUDINESS FOR RADIATION.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY0/

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2002-01, J.M. Piriou.

!     Modifications.
!     --------------
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        R.Brozkova    05-Nov-2004 new RH function

!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMPHY   , ONLY : TPHY
USE YOMPHY0  , ONLY : TPHY0
IMPLICIT NONE

TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQC(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSAT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEB(KLON,KLEV) 
INTEGER(KIND=JPIM) :: JLEV, JLON

REAL(KIND=JPRB) :: ZEPS1, ZRH, ZRHLIM, ZNEB, ZBIN, ZARGLI, ZRHEXP
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!*
!     ------------------------------------------------------------------
!     I - CALCUL DE LA NEBULOSITE.
!         CLOUDINESS COMPUTATION.

IF (LHOOK) CALL DR_HOOK('ACNEBXRS',0,ZHOOK_HANDLE)
ASSOCIATE(QXRAL=>YDPHY0%QXRAL, QXRTGH=>YDPHY0%QXRTGH, QXRR=>YDPHY0%QXRR, &
 & QXRHX=>YDPHY0%QXRHX, QXRDEL=>YDPHY0%QXRDEL, &
 & LQXRTGH=>YDPHY%LQXRTGH)
ZEPS1=1.E-6_JPRB
ZARGLI=125._JPRB**(1.0_JPRB/QXRTGH)

IF (LQXRTGH) THEN
  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZRH=MIN(ZARGLI,MAX(ZEPS1,PQ(JLON,JLEV)/PQSAT(JLON,JLEV)))
      ZRHEXP=EXP(-2.0_JPRB*ZRH**QXRTGH)
      ZRH=((1.0_JPRB-ZRHEXP)/(1.0_JPRB+ZRHEXP))**(1.0_JPRB/QXRTGH)
      ZRHLIM=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZRH))
      ZNEB=ZRHLIM**QXRR*(1.0_JPRB-&
    &  EXP(-QXRAL*PQC(JLON,JLEV)/((1.0_JPRB-ZRHLIM)*PQSAT(JLON,JLEV))**QXRDEL))
      ZBIN=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRH-1.0_JPRB))
      PNEB(JLON,JLEV)=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZBIN+(1.0_JPRB-ZBIN)*ZNEB))
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZRH=MIN(QXRHX,PQ(JLON,JLEV)/PQSAT(JLON,JLEV))
      ZRHLIM=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZRH))
      ZNEB=ZRHLIM**QXRR*(1.0_JPRB-&
    &  EXP(-QXRAL*PQC(JLON,JLEV)/((1.0_JPRB-ZRHLIM)*PQSAT(JLON,JLEV))**QXRDEL))
      ZBIN=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZRH-1.0_JPRB))
      PNEB(JLON,JLEV)=MAX(ZEPS1,MIN(1.0_JPRB-ZEPS1,ZBIN+(1.0_JPRB-ZBIN)*ZNEB))
    ENDDO
  ENDDO
ENDIF
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACNEBXRS',1,ZHOOK_HANDLE)

END SUBROUTINE ACNEBXRS
