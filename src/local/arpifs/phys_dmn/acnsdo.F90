SUBROUTINE ACNSDO( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
! -----------------------------------------------------------------------
! - INPUT  2D .
                  &PALPH,PAPHIF,PAPRS,PAPRSF,&
                  &PDELP,PLNPR,PQ,PQI,PQL,PQR,PQS,PQW,&
                  &PRDELP,PSIGP,PT,PTW,PU,PV,PVERVEL,&
                  &PZATSLC,PGEOSLC,&
                  &PFPLSL,PFPLSN,PFEVSL,PFEVSN,&
! - OUTPUT 2D .
                  &PDIFCQD,PDIFCQLD,PDIFCQID,PDIFCSD,&
                  &PFEVCL,PFEVCN,&
                  &PSTRCUD,PSTRCVD,&
! - INPUT/OUTPUT  2D .
                  &PDDAL,PDDOM)

! **** *ACNSDO * - NON SATURATED DOWNDRAFT SCHEME

! Sujet.
! ------
!     - MASS FLUX DOWNDRAFT SCHEME EVAPORATING
!       CONDENSATE AND PRECIPITATION CONTENT
!     - ASSOCIATED LARGE SCALE EVAPORATION/SUBLIMATION  FLUX
!       
! **   Interface.
!      ----------
! *CALL* *ACNSDO*
! ----------------------------------------------------------------------
! - INPUT ARGUMENTS
! -------------------
! - DIMENSIONING PARAMETERS FOR PHYSICS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (NIVEL DU SOMMET
!            : DU NUAGE).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".


! - PHYSICAL VARIABLES

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! ZSNP      : FRACTION OF SOLID PRECIPITATION

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (POUR L'HYDROSTATIQUE).
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PVERVEL    : (OMEGA/P) MEAN GRID BOX VERT. VEL. DIVIDED BY PRESSURE.
! PQ         : WATER VAPOUR SPECIFIC CONTENTS
! PQI        : CLOUD ICE SPECIFIC CONTENTS
! PQL        : CLOUD DROPLETS SPECIFIC CONTENTS
! PQR        : RAIN WATER SPECIFIC CONTENTS
! PQS        : SNOW WATER SPECIFIC CONTENTS
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PTW        : TEMPERATURE DU THERMOMETRE MOUILLE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PZATSLC    : ATTENUATION FACTOR FOR SLANTWISE CONVECTION.
! PGEOSLC    : EQUIVALENT GEOPOTENTIEL POUR CALCUL DE dln Tv/dp
! PFPLSL     : LIQUID PRECIPITATION FLUX
! PFPLSN     : SOLID PRECIPITATION FLUX
! PFEVSN     : SNOW SUBLIMATION/MELTING IN MICROPHYSICS
! PFEVSL     : RAIN EVAPORATION/MELTING IN MICROPHYSICS


! - 1D
! PSIGP      : PRECIPITATION MESH FRACTION (outside updraft)

! ----------------------------------------------------------------------
! -   INPUT/OUTPUT ARGUMENTS
!     ----------------------

! - 2D (0:KLEV) .

! PDDOM      : PROGNOSTIC ABSOLUTE DOWNDRAFT VELOCITY T9 -> T0
! PDDAL      : PROGNOSTIC DOWNDRAFT MESH FRACTION

! ----------------------------------------------------------------------
! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PSTRCUD     : TENDENCY FLUX "CONVECTIF" FINAL DE QUANTITE DE MOUVEMENT "U".
! PSTRCVD     : TENDENCY FLUX "CONVECTIF" FINAL DE QUANTITE DE MOUVEMENT "V".
! PFEVCL       : DOWNDRAFT EVAPORATION FLUX (LIQUID)
! PFEVCN       : DOWNDRAFT EVAPORATION FLUX (SOLID)

! ----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     -------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/
! COMMON/FCTTRM /

! ----------------------------------------------------------------------

!     Externals.
!     ---------

!     Method.
!     --------

!     Author.
!     -------
!     02-2011, L. Gerard from ACMODO

!     Modifications.
!     --------------
!     L. Gerard, February 2011: UNSATURATED DOWNDRAFT (Alaro-0 version)
!     L. Gerard, February 2011: UNSATURATED DOWNDRAFT (Alaro-1 version)
!     L. Gerard, December 2012: MESH FRACTION AND LCDDCSD BUSINESS
!     L. Gerard, May 2013 : LCDDCSD=T version
!     L. Gerard, March 2015: single descent, prognostic fraction, 
!                revised condensate transfer function
!     L. Gerard, September 2019: New (research) version of the routine.
! -----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1 , ONLY: JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RV       ,RCPD     ,&
            &RCPV     ,RETV     ,RCW      ,RCS      ,RLVTT ,&
            &RLSTT    ,RTT      ,RALPW    ,RBETW    ,RGAMW   ,&
            &RALPS    ,RBETS    ,RGAMS    ,RALPD    ,RBETD   ,&
            &RGAMD    ,RPI


IMPLICIT NONE


!     DUMMY INTEGER SCALARS
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV
INTEGER(KIND=JPIM),INTENT(IN) :: KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA

REAL(KIND=JPRB) ,INTENT(IN) :: PALPH(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDELP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQ(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQI(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQS(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PQW(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PT(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PSIGP(KLON)
REAL(KIND=JPRB) ,INTENT(IN) :: PTW(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PU(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PV(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PZATSLC(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQLD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCQID(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDIFCSD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFEVCL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFEVCN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PDDOM(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PGEOSLC(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFPLSL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFEVSL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN)    :: PFEVSN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PSTRCUD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PSTRCVD(KLON,0:KLEV)


REAL(KIND=JPRB) :: ZSNP(KLON,0:KLEV)

! 2D LOCALS

REAL(KIND=JPRB) :: ZA13(KLON,KLEV),ZA14(KLON,KLEV)&
    &,ZBET(KLON,KLEV),ZDDAL(KLON,KLEV)&
    &,ZDSE(KLON,KLEV),ZDDOM(KLON,KLEV+1)&
    &,ZFORA(KLON,0:KLEV)&
    &,ZFORM(KLON,0:KLEV),ZFP(KLON,0:KLEV),ZFEVM(KLON,0:KLEV)&
    &,ZLHE(KLON,KLEV)&
    &,ZQC(KLON,KLEV), ZQDN(KLON,KLEV)&
    &,ZRMIX(KLON,KLEV),ZSDN(KLON,KLEV),ZTDN(KLON,KLEV)&
    &,ZUM(KLON,KLEV),ZVM(KLON,KLEV),ZOMLS(KLON,KLEV)&
    &,ZFEVP(KLON,0:KLEV)

INTEGER(KIND=JPIM) :: INAV(KLON),INACT(KLON,KLEV),INBAS(KLON,KLEV)

! 1D LOCALS

!**Prognostic Convection**:
REAL(KIND=JPRB) :: ZBUL(KLON), ZCP(KLON),ZDQ(KLON),ZDS(KLON),ZDRAG(KLON)&
    &,ZENDYN(KLON),ZENTUPH(KLON),ZEVA(KLON),ZFP1(KLON),ZFPE(KLON)&
    &,ZHMIN(KLON),ZKOM(KLON), ZLH(KLON),ZQB(KLON),ZQN(KLON)&
    &,ZRBB(KLON),ZRBH(KLON),ZRVH(KLON)&
    &,ZCA(KLON),ZCC(KLON),ZCD(KLON),ZCL(KLON),ZCM(KLON)&
    &,ZS1(KLON),ZS10(KLON)&
    &,ZTB(KLON),ZTN(KLON),ZTVL(KLON),ZDELNL(KLON)
REAL(KIND=JPRB) :: ZER(KLON),ZFMDQE(KLON),ZSKOM(KLON),ZSUM(KLON)&
    &,ZSIG9(KLON),ZSIG(KLON),ZSIGX0(KLON),ZSIGX(KLON),ZUSIGE(KLON)

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: ISUM, ITOP, ITOP1, JIT, &
                 &    JLEV, JLON, II

!     LOCAL REAL SCALARS
REAL(KIND=JPRB) :: Z1, Z2, Z3, Z4PIDF, ZA, ZABSD, ZABSP, ZACTI, ZAUX,&
          &ZB, ZB0, ZB1, ZBAS, ZBU, ZC, ZCHI1, ZCPVMD, ZCPVMS,&
          &ZCPVMW, ZD, ZD1,ZD2,ZD3,&
          &ZDCP, ZDELQ, ZDELT, ZDELTA, ZDLNRHO, ZDP, ZDPHI, ZDPSGDT, &
          &ZDQW, ZESP, ZESIGN, ZEXP, ZEW, ZF, ZFFSLC, ZFMDQEL, &
          &ZGI, ZGDT,ZGDTI, ZIDP, ZIDT, ZINAV, &
          &ZK, ZKSG, &
          &ZMIX, ZNBA, ZNOAV, &
          &ZOA, ZOB, ZOC, ZOD, ZOF,&
          &ZOMD, ZP, ZQ, ZQE, ZQW, &
          &ZRVMD, ZSE,ZSIGE, ZTAUSIG, ZTPHY, ZTV, &
          &ZZ,ZZZ, ZZ1,ZZ2,ZZ3, ZZA,ZZB,ZZC,  ZZDQR, ZZDQS, &
          &Z_AADV, ZAO_1
REAL(KIND=JPRB) ::  Z2PI, Z4PI, ZLOAD, ZDDTRFX

LOGICAL :: LLBO, LLSMOTR

REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB), PARAMETER :: ZI3=1._JPRB/3._JPRB
REAL(KIND=JPRB), PARAMETER :: ZI27=1._JPRB/27._JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPS1=1.E-12_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPS2=1.E-2_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSDOM=1.E-6_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSALN=1.E-11_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSP=1.E-6_JPRB ! i.e. no dd below 0.0036mm/h
REAL(KIND=JPRB), PARAMETER :: ZEPSIGP=1.E-4_JPRB ! min value of psigp
REAL(KIND=JPRB), PARAMETER :: ZEVAPX=0.99_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSE=1.E-7_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSMIX=1.E-12_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSQ=1.E-14_JPRB
REAL(KIND=JPRB), PARAMETER :: ZEPSSNP=1.E-12_JPRB
REAL(KIND=JPRB), PARAMETER :: ZSIGONE=0.9999_JPRB
REAL(KIND=JPRB), PARAMETER :: ZSIGZERO=0.01_JPRB
REAL(KIND=JPRB), PARAMETER :: ZOMD0=10._JPRB
REAL(KIND=JPRB), PARAMETER :: ZCVOMDMX=180._JPRB ! Max allowed vel.
REAL(KIND=JPRB), PARAMETER :: ZKOMX=2._JPRB
REAL(KIND=JPRB), PARAMETER :: ZKOMRES=0.25_JPRB
! ------------------------------------------------------------------
#include "fcttrm.func.h"

IF (LHOOK) CALL DR_HOOK('ACNSDO',0,ZHOOK_HANDLE)
ASSOCIATE(NBITER=>YDML_PHY_MF%YRPHY%NBITER, NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, LCDDCSD=>YDML_PHY_MF%YRPHY%LCDDCSD,&
 & GCVADS=>YDML_PHY_MF%YRPHY0%GCVADS, GDDALBU=>YDML_PHY_MF%YRPHY0%GDDALBU, GDDBETA=>YDML_PHY_MF%YRPHY0%GDDBETA,&
 & GDDDP=>YDML_PHY_MF%YRPHY0%GDDDP, GDDENDYMX=>YDML_PHY_MF%YRPHY0%GDDENDYMX,GDDEVA=>YDML_PHY_MF%YRPHY0%GDDEVA,&
 & GDDFRAC=>YDML_PHY_MF%YRPHY0%GDDFRAC, GDDFXM=>YDML_PHY_MF%YRPHY0%GDDFXM, GDDWPF=>YDML_PHY_MF%YRPHY0%GDDWPF,&
 & GDDFREVS=>YDML_PHY_MF%YRPHY0%GDDFREVS, GDDFP=>YDML_PHY_MF%YRPHY0%GDDFP, GDDINHOM=>YDML_PHY_MF%YRPHY0%GDDINHOM,&
 & GDDTAUSIG=>YDML_PHY_MF%YRPHY0%GDDTAUSIG, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, &
 & TDDBU=>YDML_PHY_MF%YRPHY0%TDDBU, TDDFR=>YDML_PHY_MF%YRPHY0%TDDFR, TDDGP=>YDML_PHY_MF%YRPHY0%TDDGP,&
 & TENTRD=>YDML_PHY_MF%YRPHY0%TENTRD, TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY)
! ------------------------------------------------------------------

! *
!     I - ANCILLARY CONSTANTS.

! DEVELOPMENT AND TESTING SWITCHES

LLBO=.FALSE.   ! .T. for Open Loop
IF (LCDDCSD) THEN
   ZAO_1=1._JPRB
ELSE
   ZAO_1=0._JPRB
ENDIF
Z_AADV=1._JPRB ! set to zeo to suppress advection
ZLOAD=1._JPRB ! WATER LOADING: should be 1., 0. for testing without

Z2PI=2._JPRB*RPI
Z4PI=2._JPRB*Z2PI
LLSMOTR=GDDFXM<0.05_JPRB

ZDDTRFX=MAX(0.1_JPRB,MIN(1._JPRB, GDDFXM)) ! 1. to suppress distinction transport vs production flux
!------------------------------------------------------

ZRVMD=RV-RD
ZCPVMD=RCPV-RCPD
ZCPVMW=RCPV-RCW
ZCPVMS=RCPV-RCS

! *
! ------------------------------------------------------------------
!     II - COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR
!     THREE OF THE VERTICAL INTEGRALS OF THE SCHEME).

!**Prognostic Convection**
ZTPHY=TSPHY
ZIDT=1.0_JPRB/ZTPHY
ZGDT=RG*ZTPHY
ZGDTI=1.0_JPRB/(RG*ZTPHY)
ZGI=1._JPRB/RG
ZESIGN=0.01_JPRB
ZTAUSIG=ABS(GDDTAUSIG)

!-----------------------------------
! ZOMLS: RESOLVED OMEGA VERTICAL VELOCITY
ZOMLS(KIDIA:KFDIA,KTDIA:KLEV)=PVERVEL(KIDIA:KFDIA,KTDIA:KLEV)*&
                   & PAPRSF(KIDIA:KFDIA,KTDIA:KLEV)
ZFEVM(KIDIA:KFDIA,:)=PFEVSL(KIDIA:KFDIA,:)+PFEVSN(KIDIA:KFDIA,:)
DO JLON=KIDIA,KFDIA
   ZSNP(JLON,KTDIA-1)=1._JPRB
   ZZ=MAX(0._JPRB,SIGN(1._JPRB,PSIGP(JLON)-ZEPSIGP))
   ZER(JLON)=ZZ/(PSIGP(JLON)+1._JPRB-ZZ)
ENDDO
! Put total Intensive precipitation at half level into ZFP
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
       ZFP(JLON,JLEV)=(PFPLSL(JLON,JLEV)+PFPLSN(JLON,JLEV))*ZER(JLON)
   ENDDO
ENDDO
! Put snow fraction at half level in zsnp 
DO JLEV=KTDIA,KLEV-1
 DO JLON=KIDIA,KFDIA
   ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,JLEV)-ZEPSSNP))
   ZSNP(JLON,JLEV)=ZZ*PFPLSN(JLON,JLEV)/(ZFP(JLON,JLEV)+ZZ-1._JPRB)&
     & +(1._JPRB-ZZ)*MAX(0._JPRB,SIGN(1._JPRB,&
     &                   (RTT-(PT(JLON,JLEV)+PT(JLON,JLEV+1))*0.5_JPRB)))
 ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
   ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZFP(JLON,KLEV)-ZEPSSNP))
   ZSNP(JLON,KLEV)=ZZ*PFPLSN(JLON,KLEV)/(ZFP(JLON,KLEV)+ZZ-1._JPRB)&
     & +(1._JPRB-ZZ)*MAX(0._JPRB,SIGN(1._JPRB,(RTT-PT(JLON,KLEV))))
ENDDO
DO JLEV=KTDIA,KLEV
 DO JLON=KIDIA,KFDIA
   ZQC(JLON,JLEV)=MAX(0.0_JPRB,PQI(JLON,JLEV)+PQL(JLON,JLEV))
 ENDDO
ENDDO

ZKSG=TDDFR/RG
IF (LCDDCSD) THEN
   ZBU=GDDALBU*RG*RG/RD
ELSE
   ZBU=RG*RG/(RD*(1.0_JPRB+TDDBU))
ENDIF

! *
! ------------------------------------------------------------------
!    III - SETTING ALL FLUXES TO ZERO (FOR THE CASE WITHOUT DOWNDRAFT)

! - TEMPORAIRE(S) 2D (1:KLEV) .
! FLUXES HAVE BEEN INITIALIZED TO ZERO BY APLPAR:
!  PDIFCQD  : WATER VAPOUR MOISTURE DIFFUSION FLUX
!  PDIFCQLD : CLOUD LIQUID WATER MOISTURE DIFFUSION FLUX
!  PDIFCQID : CLOUD ICE WATER MOISTURE DIFFUSION FLUX
!  PDIFCSD  : DRY STATIC ENERGY DIFFUSION FLUX
!  PSTRCUD  : TEND. FLUX DE QUANTITE DE MOUVEMENT "U" DU AU COURANT DESCENDANT.
!  PSTRCVD  : TEND. FLUX DE QUANTITE DE MOUVEMENT "V" DU AU COURANT DESCENDANT.


!      SETTING FLUXES TO ZERO.
ZFEVP=0.0_JPRB


!       PRELIMINARY CALCULATIONS OF ALREADY DETERMINED PROPERTIES


! RESET DD SPEED AT KTDIA AND HIGHER

PDDOM(KIDIA:KFDIA,1:KTDIA)=0.0_JPRB

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
! ZLHE : ENVIRONMENT LOCAL LATENT HEAT PROFILE
! ZDSE : ENVIRONMENT DRY STATIC ENERGY PROFILE
     ZDELTA=0.5*(ZSNP(JLON,JLEV)+ZSNP(JLON,JLEV-1))
     ZLHE(JLON,JLEV)=FOLH(PTW(JLON,JLEV),ZDELTA)
     ZDSE(JLON,JLEV)=&
             & (RCPD+ZCPVMD*PQ(JLON,JLEV))*PT(JLON,JLEV)+PAPHIF(JLON,JLEV)
  ENDDO
ENDDO

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
     PGEOSLC(JLON,JLEV)=MIN(PGEOSLC(JLON,JLEV),PGEOSLC(JLON,JLEV+1)&
     & *PTW(JLON,JLEV)/PTW(JLON,JLEV+1))
  ENDDO
ENDDO
! *
! ------------------------------------------------------------------
!     IV - TOP INITIALISATION. 
!------------------------------------------------------
!  remark: KTDIA passed to the routine may be lower than 
!          model level 1 - the best is the top of the highest cloud
!------------------------------------------------------
! - 2D LOCALS 
! ZSDN       : DOWNDRAFT DRY STATIC ENERGY PROFILE.
! ZQDN       : DOWNDRAFT VAPOUR SPECIFIC CONTENT PROFILE.
! ZTDN       : DOWNDRAFT TEMPERATURE PROFILE.
! - 1D LOCALS
! ZTN        : TEMPERATURE OF THE DOWNDRAFT.
! ZQN        : DOWNDRAFT VAPOUR SPECIFIC CONTENT.
! ZSIG: DD MESH FRACTION (TAKEN CONSTANT ALONG THE VERTICAL BUT
!       ADAPTED  ON THE RUN WHEN EVAPORATION BECOMES EXCESSIVE)
!-------------------------------------------------------------------------
! SEARCH MINIMUM OF H_E BETWEEN THE SURFACE AND 500HPA
! SET ZA14 =0 ABOVE BUT STILL COULD HAVE HOLES IF H_E NOT MONOTONOUS

ZHMIN(:)=1.E16_JPRB
ZA13=0._JPRB
DO JLEV=KLEV,KTDIA, -1
  DO JLON=KIDIA,KFDIA
     ZA13(JLON,JLEV)=ZDSE(JLON,JLEV)+ZLHE(JLON,JLEV)*PQ(JLON,JLEV)
! do not search higher than 500hPa
     ZZ=MAX(0._JPRB,SIGN(1._JPRB,ZHMIN(JLON)-ZA13(JLON,JLEV)))*&
     & MAX(0._JPRB,SIGN(1._JPRB, PAPRSF(JLON,JLEV)-5.E4_JPRB))
     ZHMIN(JLON)=MIN(ZHMIN(JLON),ZA13(JLON,JLEV))
     ZA14(JLON,JLEV)=ZZ
  ENDDO
ENDDO
! SET INACT AND  FILTER OMEGA_D- BEFORE USING IT
! PDDOM was already set to zero at ktdia and above
INACT(:,:)=0_JPIM
ZDDOM(:,KTDIA)=0._JPRB
ZDDAL(:,KTDIA)=0._JPRB
ZFORA(:,:)=0._JPRB
ITOP=KLEV
DO JLEV=KTDIA+1,KLEV
   ISUM=0
   DO JLON=KIDIA,KFDIA
      INACT(JLON,JLEV)=MAX(INACT(JLON,JLEV-1),NINT(ZA14(JLON,JLEV)))
      ZDDOM(JLON,JLEV)=MIN(MAX(0._JPRB, PDDOM(JLON,JLEV)),ZCVOMDMX)&
                      & *REAL(INACT(JLON,JLEV),JPRB)
      ISUM=ISUM+INACT(JLON,JLEV)
      ZDDAL(JLON,JLEV)=REAL(INACT(JLON,JLEV),JPRB)
   ENDDO
   IF (ISUM /= 0) ITOP=MIN(ITOP,JLEV)
ENDDO
! TRICK: USE A MIRROR TO APPLY ADVECTION AT LOWEST MODEL LEVEL
DO JLON=KIDIA,KFDIA
   ZDDOM(JLON,KLEV+1)=-ZDDOM(JLON,KLEV)
ENDDO
ITOP=MAX(KTDIA+1,ITOP) ! security that should never play
ZSIG9=0._JPRB
PDDAL(KIDIA:KFDIA,1:KTDIA)=0.0_JPRB
! COMPUTE MEAN T9 FRACTION in ZSIG9
ZS10=0._JPRB
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
! CLEAN PDDAL THEN AVERAGE OVER THE VERTICAL
      ZZ=MAX(0.0_JPRB,SIGN(1.0_JPRB,PDDAL(JLON,JLEV)-ZEPSALN))
      PDDAL(JLON,JLEV)=PDDAL(JLON,JLEV)*ZZ
      ZSIG9(JLON)=ZSIG9(JLON)+PDDAL(JLON,JLEV)*PDELP(JLON,JLEV)
      ZS10(JLON)=ZS10(JLON)+ZZ*PDELP(JLON,JLEV)
   ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
   ZSIG9(JLON)=ZSIG9(JLON)/MAX(1._JPRB,ZS10(JLON))
ENDDO

! GUESS MESH FRACTION AT THE TOP: GDDFRAC*PSIGP OR ADVECTED VALUE
! ** MUST ENSURE ZSIG<1 FOR ZUSIGE **
! ** PSIGP >=0 by construction in acupu
DO JLON=KIDIA,KFDIA
  ZSIGX(JLON)=MIN(ZSIGONE,(GDDFRAC+(gddinhom-GDDFRAC)*PSIGP(JLON))&
                             & *PSIGP(JLON))
  ZSIG(JLON)=MIN(ZSIGX(JLON),MAX(ZSIG9(JLON),ZSIGZERO*PSIGP(JLON)))
  ZSIGX0(JLON)=ZSIGX(JLON)
   ZSIG9(JLON)=ZSIG(JLON)
ENDDO
ZUSIGE(KIDIA:KFDIA)=1._JPRB/(1._JPRB-ZSIG(KIDIA:KFDIA))

! Default kF gain:
IF (GDDFP(3)>0._JPRB) THEN
! COMPUTE (-Domega_d/dp)>0 at T9 and at HALF level - Forget level zero
!  (i.e. Slowing down implying detrainment/horizontal branch)
! and deduce the kF gain
   DO JLEV=ITOP,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZD=MAX(0._JPRB,(ZDDOM(JLON,JLEV)-ZDDOM(JLON,JLEV+1))/&
                     & (PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV)))
        ZVM(JLON,JLEV)=GDDFP(1)*(1._JPRB+GDDFP(3)*SQRT(ZD))
     ENDDO
   ENDDO
! Bottom
   DO JLON=KIDIA,KFDIA
      ZD=MAX(0._JPRB,ZDDOM(JLON,KLEV)/&
                  & (PAPRS(JLON,KLEV)-PAPRSF(JLON,KLEV)))
      ZVM(JLON,KLEV)=GDDFP(1)*(1._JPRB+GDDFP(3)*SQRT(ZD))
   ENDDO
ELSE
   DO JLEV=ITOP,KLEV
      DO JLON=KIDIA,KFDIA
        ZVM(JLON,JLEV)=GDDFP(1)
     ENDDO
   ENDDO
ENDIF

! HALF-LEVEL ADVECTION VELOCITY: DROP IT IF UPWARDS !
! ZFORA INITIALIZED TO ZERO ABOVE
DO JLEV=ITOP,KLEV-1
  DO JLON=KIDIA,KFDIA
    ZFORA(JLON,JLEV)=Z_AADV*MAX(0._JPRB,&
     &(ZDDOM(JLON,JLEV)+ZDDOM(JLON,JLEV+1)&
     & -MAX(0._JPRB,ZOMLS(JLON,JLEV)+ZOMLS(JLON,JLEV+1))))*0.5_JPRB
  ENDDO
ENDDO

!     INITIALIZED INTEGRALS.

! - TEMPORAIRE(S) 1D . 


! SET VALUES AT ITOP, CONSIDERED AS LAST ARRIVAL LEVEL BEFORE VERT. LOOP
DO JLON=KIDIA,KFDIA
  ZFP1(JLON)=0.0_JPRB
  ZFPE(JLON)=0.0_JPRB
  ZS10(JLON)=0.0_JPRB ! for auto-advection term at ITOP
  ZS1(JLON)=0.0_JPRB  ! T9 l-1 perturb-velocity (and zer=zacti(l-1)=0)
  ZENDYN(JLON)=0._JPRB ! DOES NOT MATTER
  ZDQ(JLON)=0._JPRB
  ZDS(JLON)=0._JPRB
! RUNNING SATURATED ADIABAT PROPERTIES : 
  ZTN(JLON)=PTW(JLON,ITOP)
  ZQN(JLON)=PQW(JLON,ITOP)
! DOWNDRAFT PROPERTIES: START FROM BLUE POINT :
  ZQDN(JLON,ITOP)=PQW(JLON,ITOP)
  ZTDN(JLON,ITOP)=PTW(JLON,ITOP)
  ZSDN(JLON,ITOP)=(RCPD+ZCPVMD*ZQDN(JLON,ITOP))&
             & *ZTDN(JLON,ITOP)+PAPHIF(JLON,ITOP)
! Envt Virtual temperature and buoyancy
  ZTVL(JLON)=(1.0_JPRB-ZQC(JLON,ITOP)+RETV*PQ(JLON,ITOP))*PT(JLON,ITOP)
  ZER(JLON)=0._JPRB
! PRECIPITATION AVAILABILITY, SEE MAIN LOOP BELOW
  ZFP1(JLON)=ZFP(JLON,ITOP)
  INAV(JLON)= MAX(0,NINT(SIGN(1._JPRB,ZFP1(JLON)-ZEPSP)))
! initialize to 1 as long as no activity above ?!
  ZKOM(JLON)=1._JPRB 
! We must consider ITOP is NOT an active level
      INACT(JLON,ITOP)=0
  ZSKOM(JLON)=0._JPRB
  ZSUM(JLON)=0._JPRB
ENDDO
ZEVA(KIDIA:KFDIA)=GDDEVA
IF (GDDWPF>1.E-5_JPRB) THEN ! smaller evaporation of intense precipitation 
   DO JLON=KIDIA,KFDIA
      ZZ=MAX(0._JPRB,ZFP(JLON,KLEV)/GDDWPF)
      ZEVA(JLON)=GDDEVA/(1._JPRB+ZZ*ZZ)
   ENDDO

ENDIF
! INITIAL VALUES (3D)
DO JLEV=ITOP-1,KLEV
   DO JLON=KIDIA,KFDIA
      ZRMIX(JLON,JLEV)=0._JPRB !3D
   ENDDO
ENDDO

! *
! ------------------------------------------------------------------
!     V - MAIN VERTICAL LOOP (DOWNWARDS)  INCLUDING THE SATURATED
!     ADIABATIC AND THE UNSATURATED DOWNDRAFT PROFILES.

!     ITOP - SKIPS UNNECESSARY COMPUTATIONS IN THE UPPER PART
!      OF THE ATMOSPHERE : START AT LEVEL OF MINIMUM he (or theta_e)
!          - IS AT LEAST KTDIA+1, HENCE ZFEVP(JLEV-2) EXISTS
! COMPUTE SEGMENTS JLEV-1 -> JLEV hence JLEV STARTS AT ITOP+1
DO JLEV=ITOP+1,KLEV

  DO JLON=KIDIA,KFDIA

! INAV: CONDENSATE AVAILABILITY INDEX: 
!  =1 if the net precip flux _IN DD COLUMN_ at bottom interface JLEV-2
!      (should be full level JLEV-1)  >0
!  =0 otherwise
! INACT: 0 above LEVEL OF MIN(he), THEN FINAL ACTIVITY INDEX

!     SATURATED REFERENCE ADIABAT WITH ENTRAINMENT (WRT ENVT WET BULB)
!     FROM FULL LEVEL JLEV-1 TO JLEV
! POSITIVE DELTA PHI TO PUT IN POSITIVE COEFFICIENTS:
    ZDPHI=(PAPHIF(JLON,JLEV-1)-PAPHIF(JLON,JLEV))
    ZDP=PAPRSF(JLON,JLEV)-PAPRSF(JLON,JLEV-1)
    ZENTUPH(JLON)=TENTRD*ZDPHI
    ZDRAG(JLON)=ZKSG*ZDPHI
! ESTIMATE ZENDYN FROM PREVIOUS TIME STEP at half level l-1
! ZDDOM(JLEV) UPDATED AT THE END OF THE LOOP
! ZER is ZACTI(JLEV-1) 
! ZD =domega_per/omega_per 
    Z1=MAX(0._JPRB,(ZDDOM(JLON,JLEV)-ZOMLS(JLON,JLEV)))
    ZZ=MAX(0._JPRB,SIGN(1._JPRB,Z1+ZS1(JLON)-ZEPS2))
    ZD=2._JPRB*(Z1-ZS1(JLON))*ZZ/(Z1+ZS1(JLON)+1._JPRB-ZZ)*ZER(JLON)
! Memory of Z1 for next level
    ZS1(JLON)=Z1
! ZENDYN is Lambda'_dyn*dp - no dyn entrainment with LCDDCSD=F
    ZENDYN(JLON)=MAX(0._JPRB, MIN(ZD,GDDENDYMX*ZDP))
!----
! First zmix used for saturated descent computation
    ZMIX=INAV(JLON)*(ZENTUPH(JLON)+ZENDYN(JLON))*ZUSIGE(JLON)
    ZMIX=ZMIX/(1.0_JPRB+ZMIX)
!----

! START LEVEL
! ZTB        : "ZTN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZQB        : "ZQN" AT THE BOTTOM OF THE SLAB WITH ENTRAINMENT "IN".
! ZDQ and ZDS COMPUTED AT THE END OF THE LOOP

    ZQB(JLON)=ZQN(JLON)+ZMIX*(PQW(JLON,JLEV-1)-ZQN(JLON)-ZDQ(JLON))
    ZTB(JLON)=ZTN(JLON)+ZMIX*(PTW(JLON,JLEV-1)-ZTN(JLON)&
            & -ZDS(JLON)/(RCPD+ZCPVMD*ZQB(JLON)))

! Store ZMIX for further use (momentum profile)
    ZRMIX(JLON,JLEV-1)=ZMIX

!   computation of new equilibrium of mixed air **SUPPRESSED**

!     HYDROSTATIC COEFFICIENTS FOR THE DOWNDRAFT PROFILE.

! - TEMPORAIRE(S) 1D .

! ZRBB       : WEIGHT OF "ZTB" IN THE GEOPOTENTIAL THICKNESS.
! ZRBH       : WEIGHT OF "ZTN" AT "ZQN=ZQB" IN THE SAME THICKNESS.
! ZRVH       : FOR THE Q DEPENDENT CORRECTION OF ZRBH.

    ZRBB(JLON)=-PALPH(JLON,JLEV-1)*(RD+ZRVMD*ZQB(JLON))
    ZRBH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*(RD+ZRVMD*ZQB(JLON))
    ZRVH(JLON)=(-PLNPR(JLON,JLEV)+PALPH(JLON,JLEV))*RV

!     TO COMPUTE ADIABATIC PROFILE GCVADS ALLOWS TO GO
!     CONTINUOUSLY FROM EQUI-PRESSURE TO EQUI-GEOPOTENTIAL APPROACH.

    ZRBB(JLON)=(1.0_JPRB-GCVADS)*ZRBB(JLON)+GCVADS*(PAPHIF(JLON,JLEV)&
     &-PAPHIF(JLON,JLEV-1))/ZTB(JLON)
    ZRBH(JLON)=(1.0_JPRB-GCVADS)*ZRBH(JLON)
    ZRVH(JLON)=(1.0_JPRB-GCVADS)*ZRVH(JLON)

    ZFFSLC=1.0_JPRB/(1.0_JPRB+PZATSLC(JLON,JLEV-1))
    ZRBB(JLON)=ZRBB(JLON)*ZFFSLC
    ZRBH(JLON)=ZRBH(JLON)*ZFFSLC
    ZRVH(JLON)=ZRVH(JLON)*ZFFSLC

! We consider the phase at the half level between JLEV-1 and JLEV

    ZDELTA=ZSNP(JLON,JLEV-1)
!    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTB(JLON)))
    ZDELNL(JLON)=ZDELTA

!     CALL TO THE DEFINED FUNCTION FOR THE COMPUTATION OF THE PSEUDO
!     LATENT HEAT AT THE BOTTOM (AMONG THREE CHARACTERISTIC PARAMETERS).

! - TEMPORAIRE(S) 1D .

! ZLH        : RUNNING VALUE OF THE PSEUDO LH DURING NEWTON'S LOOP.
! ZCP        : AS PZLH BUT FOR THE SPECIFIC HEAT CP.

    ZLH(JLON)= FOLH (ZTB(JLON),ZDELTA)+ZRVH(JLON)*ZTB(JLON)
    ZCP(JLON)=RCPD+ZCPVMD*ZQB(JLON)+ZRBH(JLON)
    ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)

!     COMPUTE THE SATURATED ADIABAT
!     CHANGE OF LEVEL TO OBTAIN A FIRST GUESS AS STARTING POINT FOR THE
!     NEWTON LOOP.
 
    ZDELT=PT(JLON,JLEV)-PT(JLON,JLEV-1)
    ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
    ZDELQ=& 
      & -((ZRBB(JLON)+ZRBH(JLON))*ZTB(JLON)+ZCP(JLON)*ZDELT)/ZLH(JLON)
    ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
    ZTN(JLON)=ZTB(JLON)+ZDELT
    ZQN(JLON)=ZQB(JLON)+ZDELQ
  ENDDO

 
! For VECTORIAL COMPUTERS, put the longest loop inside
 
!     NEWTON LOOP.

  DO JIT=1,NBITER
    DO JLON=KIDIA,KFDIA
      IF (INAV(JLON) > 0) THEN

 
!  NO SNOW OPTION DEPENDENT COMPUTATIONS:
!  we speak about precipitation: do not change their phase here
 

!     SATURATION CALCULATIONS USING DEFINED FUNCTIONS.

      ZDELTA=ZDELNL(JLON)
      ZEW= FOEW (ZTN(JLON),ZDELTA)
      ZESP=ZEW/PAPRSF(JLON,JLEV)
      ZQW= FOQS (ZESP)
      ZDQW= FODQS (ZQW,ZESP, FODLEW (ZTN(JLON),ZDELTA))

!     INCREMENTATIONS.

      ZDCP=ZRVH(JLON)+ZCPVMW+ZDELTA*(ZCPVMS-ZCPVMW)
      ZDELQ=(ZQW-ZQN(JLON))*ZCP(JLON)/(ZCP(JLON)+ZLH(JLON)*ZDQW)
      ZCP(JLON)=ZCP(JLON)+ZDCP*ZDELQ
      ZDELT=-ZDELQ*ZLH(JLON)/ZCP(JLON)
      ZQN(JLON)=ZQN(JLON)+ZDELQ
      ZLH(JLON)=ZLH(JLON)+ZDCP*ZDELT
      ZTN(JLON)=ZTN(JLON)+ZDELT
      ENDIF
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
! COMPUTE DOWNDRAFT EVAPORATION AND PROPERTIES, DEP ON ITS VELOCITY
! AT THIS STAGE INACT IS 1 BELOW THE MIN he LEVEL
     ZINAV=REAL(INAV(JLON)*INACT(JLON,JLEV))
     ZNOAV=1._JPRB-ZINAV
     ZDP=PAPRSF(JLON,JLEV)-PAPRSF(JLON,JLEV-1)
     ZIDP=1._JPRB/ZDP
     ZTV=(1.0_JPRB-ZQC(JLON,JLEV)+RETV*PQ(JLON,JLEV))*PT(JLON,JLEV)
! --------------------
! SECOND ZMIX FOR THE ARRIVAL NON-SATURATED DD PROPERTIES
! HERE ZMIX IS Lambda'*dp/2; zendyn=0 if LCDDCSD=F
! ZKOM has been estimated for the level above at the end of the loop
     ZMIX=(ZENTUPH(JLON)/ZKOM(JLON)+ZENDYN(JLON))*0.5_JPRB*ZUSIGE(JLON)
! --------------------
! COEFFICIENTS FOR LEVEL JLEV: ZVM contains the gain GDDFP(1) modified.
     Z4PIDF=ZVM(JLON,JLEV)*MAX(ZEPSP,ZFP1(JLON))**GDDFP(2)
! k=G/dp
     ZK=ZINAV/(Z4PIDF*ZDP+ZNOAV) 
! In principle, only precipitation, no cloud condensates in downdraft:
! 1-q_cd
    ZCL(JLON)=1._JPRB-ZLOAD*(PQR(JLON,JLEV)+PQS(JLON,JLEV))!-ZQC(JLON,JLEV)
    ZSIGE=1._JPRB-ZSIG(JLON)
     ZQE=PQ(JLON,JLEV)
     ZSE=ZDSE(JLON,JLEV)
     ZCP(JLON)=RCPD+ZCPVMD*ZQN(JLON)
     ZCM(JLON)=ZK*(ZQDN(JLON,JLEV-1)+ZMIX*ZQE)
     ZCC(JLON)=ZK*(1._JPRB+ZMIX)
     ZCA(JLON)=ZK*(ZSDN(JLON,JLEV-1)-PAPHIF(JLON,JLEV)+&
                & ZMIX*(ZSE-PAPHIF(JLON,JLEV)))/ZCP(JLON)
! Neglect variation of q_v with omega_d: use ZOMD0 ~ 10 Pa/s  
     ZCD(JLON)=MAX(0._JPRB,(ZCC(JLON)*ZOMD0+1._JPRB)/&
        & ((ZCC(JLON)*ZCL(JLON)+RETV*ZCM(JLON))*ZOMD0&
        &  +(ZCL(JLON)+RETV*ZQN(JLON))))
!---------------------------------------------------------
! EVALUATE omega_d at arrival level in ZDDOM, solving 3rd degree eqn
!------------------------------------
! a', b', c', d', m and n must be kept to compute q_d and T_d  below
! Final c and bulleted b and d:
     ZCHI1=GDDDP/(PAPRS(JLON,KLEV)-PAPRS(JLON,JLEV-1))**GDDBETA
! Rather complete the previous zmix
!-     ZMIX=ZENDYN(JLON)+(ZENTUPH(JLON)+ZDRAG(JLON))/ZKOM(JLON)
     ZMIX=ZMIX*ZSIGE*2._JPRB+ZDRAG(JLON)/ZKOM(JLON)
! zdlnrho=(rho^l-rho^{l-1})/rho >0
     ZZ=PAPRSF(JLON,JLEV)*ZTVL(JLON)
     ZZ2=PAPRSF(JLON,JLEV-1)*ZTV
     ZDLNRHO=LOG(MAX(1._JPRB,ZZ))-LOG(MAX(1._JPRB,ZZ2))
! zdlnrho>0 reduces the drag and k>1 increases the drag
     ZD=MAX(0._JPRB,(ZMIX-ZDLNRHO)*ZKOM(JLON)*ZIDP+ZCHI1 )
     ZB1=ZBU*PAPRSF(JLON,JLEV)/ZTV
     ZB0=ZBU*PAPRSF(JLON,JLEV)
!--------------------------------------------------------------
! UNIFIED VECTORIZABLE FORMULATION, with ZAO_1/Z_AADV 
! T9 AUTOADVECTION:
!    ZFORA=max(0,omega_d-omega) at half level at t9
!    ZFORA is zero if z_aadv=0; zer contains zacti(l-1)
     ZZA=(ZDDOM(JLON,JLEV+1)-ZDDOM(JLON,JLEV))*0.5_JPRB*ZER(JLON)
     ZAUX=ZIDP*(ZFORA(JLON,JLEV)*ZZA &
         & -ZFORA(JLON,JLEV-1)*(ZDDOM(JLON,JLEV-1)+ZS10(JLON)) )*ZER(JLON)
     ZS10(JLON)=ZZA
     Z2=ZDDOM(JLON,JLEV)*ZIDT ! *MAX(ZAO_1, ZER(JLON))
     ZZ2=ZB1+ZAUX-Z2 
     ZZ=ZIDT+ZFORA(JLON,JLEV-1)*ZIDP 
     ZOA=ZCA(JLON)*ZD
     ZOB=ZTN(JLON)*ZD+ZCA(JLON)*ZZ
     ZOC=ZCA(JLON)*ZZ2+ZTN(JLON)*ZZ-ZCD(JLON)*ZCC(JLON)*ZB0
     ZOD=ZTN(JLON)*ZZ2-ZCD(JLON)*ZB0
!--------------------------------------------------------------
! VARIABLE CHANGE: ZOA must be /=0
     ZZZ=MAX(0._JPRB,SIGN(1._JPRB,ABS(ZOA)-ZEPS1))
     ZA=ZOC/(ZOA+ZZZ-1._JPRB)
     Z1=ZOB/(ZOA+ZZZ-1._JPRB)
     Z2=Z1*Z1
     ZOF=-Z1*ZI3
     ZP=ZA-Z2*ZI3
     ZQ=ZOD/(ZOA+ZZZ-1._JPRB)+Z1*(2._JPRB*Z2-9._JPRB*ZA)*ZI27
     ZDELTA=ZQ*ZQ+4._JPRB*ZI27*ZP*ZP*ZP
     ZABSD=ABS(ZDELTA)
     ZABSP=ABS(ZP)
!(A) IF ZDELTA >0 : STORE VALUE IN ZA
     ZZA=SQRT(ZABSD)
     Z1=(-ZQ+ZZA)*0.5_JPRB
     ZZ=SIGN(1._JPRB,Z1)
     Z1=ZZ*(ZZ*Z1)**ZI3
     Z2=(-ZQ-ZZA)*0.5_JPRB
     ZZ=SIGN(1._JPRB,Z2)
     Z2=ZZ*(ZZ*Z2)**ZI3
     ZA=MAX(0._JPRB, (Z1+Z2+ZOF))
!(B) IF DELTA<=0: TAKE THE SMALLEST POSITIVE ONE INTO ZB
! FORMULA:
!     ZZB=ACOS(-ZQ*0.5_JPRB*SQRT(27._JPRB/(ZABSP*ZABSP*ZABSP)))
! or  ZZB=ATAN(SQRT(ZABSD)/(-ZQ))+MAX(0._JPRB,SIGN(1._JPRB,ZQ))*RPI
!   VERY EXCEPTIONNALLY ZQ COULD BE ZERO, hence replace /(-ZQ) by 
     ZZ=SIGN(1._JPRB,ZQ)
     ZZB=ATAN(-ZZ*SQRT(ZABSD)/MAX(ZEPSQ,ABS(ZQ))) +MAX(0._JPRB,ZZ)*RPI
     ZZC=2._JPRB*SQRT(ZABSP*ZI3)
     Z1=ZZC*COS(ZZB*ZI3)+ZOF
     Z2=ZZC*COS((ZZB+Z2PI)*ZI3)+ZOF
     Z3=ZZC*COS((ZZB+Z4PI)*ZI3)+ZOF
! SELECTION OF THE SOLUTION: 
!    RETAIN THE SMALLEST POSITIVE SOLUTION
!    BUT ALSO TRY TO PREVENT LOCAL BIG JUMP IN PROFILE
! - Replace negative ones by the largest one:
     ZZ=MAX(Z1,Z2,Z3)
     Z1=Z1-MIN(0._JPRB,SIGN(1._JPRB,Z1))*(ZZ-Z1)
     Z2=Z2-MIN(0._JPRB,SIGN(1._JPRB,Z2))*(ZZ-Z2)
     Z3=Z3-MIN(0._JPRB,SIGN(1._JPRB,Z3))*(ZZ-Z3)
! - Examine  buoyancy for each solution and reset inadequate solutions 
     ZC=ZCD(JLON)*(ZCC(JLON)*Z1+1._JPRB)
     ZZA=ZCA(JLON)*Z1+ZTN(JLON)
     ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ABS(ZZA)-ZEPS1))
     ZB=ZZ2*ZC/(ZZA+1._JPRB-ZZ2)
     Z1=Z1*MAX(0._JPRB,SIGN(1._JPRB,(ZB0*ZB-ZB1)))*ZZ2
! idem for Z2
     ZC=ZCD(JLON)*(ZCC(JLON)*Z2+1._JPRB)
     ZZA=ZCA(JLON)*Z2+ZTN(JLON)
     ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ABS(ZZA)-ZEPS1))
     ZB=ZZ2*ZC/(ZZA+1._JPRB-ZZ2)
     Z2=Z2*MAX(0._JPRB,SIGN(1._JPRB,(ZB0*ZB-ZB1)))*ZZ2
! idem for Z3
     ZC=ZCD(JLON)*(ZCC(JLON)*Z3+1._JPRB)
     ZZA=ZCA(JLON)*Z3+ZTN(JLON)
     ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ABS(ZZA)-ZEPS1))
     ZB=ZZ2*ZC/(ZZA+1._JPRB-ZZ2)
     Z3=Z3*MAX(0._JPRB,SIGN(1._JPRB,(ZB0*ZB-ZB1)))*ZZ2
! - Select Solution closest to above
     ZZ=ZDDOM(JLON,JLEV-1)
!   (a) between Z1 and Z2:
     ZD1=ABS(ZZ-Z1)
     ZD2=ABS(ZZ-Z2)
     ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ZD1-ZD2))
     Z1=ZZ2*Z2 + (1._JPRB-ZZ2)*Z1
!   (b) between Z3 and this result:
     ZD1=ABS(ZZ-Z1)
     ZD3=ABS(ZZ-Z3)
     ZZ3=MAX(0._JPRB,SIGN(1._JPRB,ZD1-ZD3))
     ZB=ZZ3*Z3 + (1._JPRB-ZZ3)*Z1
! (C) NOW SELECT THE SOLUTION FOLLOWING ZDELTA, OR ZERO IF ZOA=0=ZZZ
! IF ZZB=1: ZB (zdelta<=0), ELSE: ZA (zdelta>0)
     ZZB=MAX(0._JPRB, SIGN(1._JPRB, -ZDELTA))
!- ONLY RETAIN DOWNWARDS VELOCITY, that must be >ZOMLS
!-    ZOMD=MAX(0._JPRB, ZOMLS(JLON,JLEV), ZZZ*(ZZB*ZB + (1._JPRB-ZZB)*ZA))
     ZOMD=MAX(0._JPRB, ZZZ*(ZZB*ZB + (1._JPRB-ZZB)*ZA))
     ZDDOM(JLON,JLEV)=ZOMD
! STORE THE POSITIVE EVAPORATION INCREMENT (without mixing)
     ZDELQ=(ZQN(JLON)-ZQDN(JLON,JLEV-1))/(1._JPRB+ZK*ZOMD)
! COMPUTE DOWNDRAFT PROPERTIES WITH FOUND SS VELOCITY: MUST BE ABSOLUTE
! AND POSITIVE (WHILE ZOMLS MAY BE <0)
! 1) GO BACK TO MEAN GRID-BOX PROPERTIES AS SOON AS NO RR AVAILABLE
! Update rain water availability index: BASED on RR PRIOR TO DD
     II=INAV(JLON)
     INAV(JLON)= NINT(MAX(0.0_JPRB,SIGN(1.0_JPRB,ZFP(JLON,JLEV)-ZEPSP)))
     ZZ1=REAL(II*INAV(JLON),JPRB)     ! rain here and above: keep as is
     ZZ2=REAL((1-II)*INAV(JLON),JPRB) ! first level with rain : set WB
!    ZZ3=1._JPRB-MAX(ZZ1,ZZ2)         ! 1 if no rain, set env and omega=0
     ZZ3=REAL(1-INAV(JLON) ,JPRB)     ! same written differently
     ZZ=ZZ1/(1._JPRB+ZCC(JLON)*ZOMD)
     ZQDN(JLON,JLEV)=(ZQN(JLON)+ZCM(JLON)*ZOMD)*ZZ
     ZTDN(JLON,JLEV)=(ZTN(JLON)+ZCA(JLON)*ZOMD)*ZZ
     ZQDN(JLON,JLEV)=ZZ1*ZQDN(JLON,JLEV) &
                   & +ZZ2*PQW(JLON,JLEV)+ZZ3*PQ(JLON,JLEV)
     ZTDN(JLON,JLEV)=ZZ1*ZTDN(JLON,JLEV) &
                   & +ZZ2*PTW(JLON,JLEV)+ZZ3*PT(JLON,JLEV)
     ZC=RCPD+ZCPVMD*ZQDN(JLON,JLEV)
     ZSDN(JLON,JLEV)=ZC*ZTDN(JLON,JLEV)+PAPHIF(JLON,JLEV)
     ZDDOM(JLON,JLEV)=MAX(ZZ1,ZZ2)*ZDDOM(JLON,JLEV) 


! ACTIVITY DIAGNOSTIC: NO FURTHER EFFECT ON ABOVE PROPERTIES
! if zddom=0 and ZOMLS<0, zdelq>=0 => sign(0)=1 inadequately
! need to put positive thresholds for at least zddom
     ZACTI=ZINAV*MAX(0._JPRB,SIGN(1._JPRB,ZDDOM(JLON,JLEV)&
               &            -MAX(0._JPRB,ZOMLS(JLON,JLEV))-ZEPSDOM))&
               &*MAX(0._JPRB,SIGN(1._JPRB,ZDELQ))
! ESTIMATE LOCAL ZKOM TO USE AT NEXT HALF LEVEL (l),
! using NEW omega_d at interface (l-1) and D(omega_d) at t-
! Better keep zkom=1 at top level 
! zacti has been set to zero when zomls>=zddom hence zkom/=0
! LEAVE ZKOM=1 IF ZAO_1=0
! MUST PREVENT ZKOM Negative of approaching zero wth small sigma_d
     ZKOM(JLON)=MIN(ZKOMX,MAX(ZSIGE, (1._JPRB-ZAO_1*ZACTI*&
      &       ZOMLS(JLON,JLEV)/MAX(ZEPSDOM,ZDDOM(JLON,JLEV)) )))
! FINAL ACTIVITY INDEX AT JLEV
! INACT TO BE USED FOR BUDGETS AFTER PLUME MODEL
     INACT(JLON,JLEV)=NINT(ZACTI)*INAV(JLON)
     ZZ=REAL(INACT(JLON,JLEV),JPRB)*ZDP
     ZSKOM(JLON)=ZSKOM(JLON)+ZKOM(JLON)*ZZ
     ZSUM(JLON)=ZSUM(JLON)+ZZ
     ZER(JLON)=ZACTI             ! TO MULTIPLY ZDDOM IN AUTO-ADV
! STORE THE EVAPORATION INCREMENT at interface level JLEV-1 in ZFEVP
! INTERFACE JLEV-1 is between JLEV-1 and JLEV
     ZFEVP(JLON,JLEV-1)=ZDELQ

! UPDATE ZFP1 FOR THE NEXT ITERATION: JLEV-> JLEV+1
! BASED ON ZDDOM i.e. ABSOLUTE DD-VELOCITY  AT JLEV
! NEVER RESET ZFPE: KEEP EVAPORATION FROM THE TOP.
     ZFPE(JLON)=ZFPE(JLON)+ZACTI*ZDELQ*ZDDOM(JLON,JLEV)*ZGI
! COMPUTE  ZSIGX TO GET LESS THAN 1/3 OF EVAPORATION
! IN THE CASE THE LOCAL EVAPORATION IS INCREASING DOWNWARDS
! AND LESS THAN 99% OTHERWISE
! LIMIT ZSIG TO ZSIGX
! THIS WAY ZFP1 SHALL NEVER REACH ZERO UNLESS ZFP DOES.
     ZZ2=MAX(0._JPRB,SIGN(1._JPRB,ZDDOM(JLON,JLEV)*ZFEVP(JLON,JLEV-1)&
                     &    -ZDDOM(JLON,JLEV-1)*ZFEVP(JLON,JLEV-2)))
     ZZ=ZZ2*ZEVA(JLON)+(1._JPRB-ZZ2)*ZEVAPX
     ZZ2=0.5_JPRB/MAX(ZEPSE,ZFPE(JLON))
! AS LONG AS NO RR AVAILABLE RE-SET ZSIGX=1 AND ZSIG=ZSIG9
     Z1=ZZ/(1._JPRB+ZZ)* (ZFP(JLON,JLEV)+ZFP(JLON,JLEV-1))*ZZ2
     Z2=GDDFREVS*(ZFEVM(JLON,JLEV)+ZFEVM(JLON,JLEV-1))*ZZ2
     ZSIGX(JLON)=MAX(ZNOAV*ZSIGX0(JLON),&
         & MIN(ZSIGX(JLON), Z1, MAX(ZSIG9(JLON), Z2)))
     ZSIG(JLON)=MAX(ZSIG9(JLON)*ZNOAV, MIN(ZSIG(JLON),ZSIGX(JLON)))
     ZUSIGE(JLON)=1._JPRB/(1._JPRB-ZSIG(JLON))
     ZFP1(JLON)=(ZFP(JLON,JLEV)+ZFP(JLON,JLEV-1))*0.5_JPRB&
              & -ZSIG(JLON)*ZFPE(JLON)
! Memorize (environment)  ZTV for next level
     ZTVL(JLON)=ZTV
  ENDDO ! JLON=
ENDDO ! JLEV downwards loop

! *
! ------------------------------------------------------------------
!     VI - COMPLETE FINAL PROFILES
! ------------------------------------------------------------------
DO JLON=KIDIA,KFDIA
! IF MEAN ZKOM>ZKOMRES=0.25 subgrid, else resolved
   ZSUM(JLON)=ZSKOM(JLON)/MAX(1._JPRB,ZSUM(JLON))
   ZSKOM(JLON)=MAX(0._JPRB, SIGN(1._JPRB,ZSUM(JLON)-ZKOMRES))
ENDDO
DO JLEV=ITOP,KLEV
   DO JLON=KIDIA,KFDIA
      INACT(JLON,JLEV)=INACT(JLON,JLEV)*NINT(ZSKOM(JLON))
   ENDDO
ENDDO
DO JLEV=KLEV,ITOP+1,-1
   DO JLON=KIDIA,KFDIA
! PUT FINAL ABSOLUTE OMEGA_D AT FULL LEVELS IN ZDDOM (0 IF DD IS RESOLVED):
      ZDDOM(JLON,JLEV)=ZDDOM(JLON,JLEV) *ZSKOM(JLON)
   ENDDO
ENDDO
! Complete ZDDOM at ITOP and ABOVE  
ZDDOM(KIDIA:KFDIA,ITOP)=ZDDOM(KIDIA:KFDIA,ITOP)*&
                     & REAL(INACT(KIDIA:KFDIA,ITOP),JPRB)
! Above itop, put 0 rather than ZOMLS
DO JLEV=KTDIA,ITOP-1
  DO JLON=KIDIA,KFDIA
    ZDDOM(JLON,JLEV)=0._JPRB
  ENDDO
ENDDO
! EVOLUTION EQUATION FOR ZSIG: RELAXATION TOWARDS ZSIGX
! FIRST EVALUATE TAUSIG DEPENDING ON SIG9 and/or RR
ZER(KIDIA:KFDIA)=ZTAUSIG
IF (GDDTAUSIG<0._JPRB) THEN ! tausig decreases with larger zsig9
   DO JLON=KIDIA,KFDIA
      ZER(JLON)=ZER(JLON)*MAX(ZESIGN,(1._JPRB-ZSIG9(JLON)))
   ENDDO
ENDIF
IF (GDDWPF<0._JPRB) THEN ! tausig decreases with surf precipitation flux
   ZZ=-1._JPRB/GDDWPF
   ZZ2=1._JPRB-ZESIGN
   DO JLON=KIDIA,KFDIA
      ZER(JLON)=ZER(JLON)*(1._JPRB-MAX(0._JPRB,MIN(ZZ2, ZFP(JLON,KLEV)*ZZ)))
   ENDDO
ENDIF
DO JLON=KIDIA,KFDIA
   ZEXP=EXP(-ZTPHY/ZER(JLON))
   ZSIG(JLON)=ZSIG(JLON)*ZEXP+ZSIGX(JLON)*(1._JPRB-ZEXP)
ENDDO
! ZDDAL was 1 below top, now becomes final constant fraction.
DO JLEV=ITOP,KLEV
  DO JLON=KIDIA,KFDIA
     ZDDAL(JLON,JLEV)=ZDDAL(JLON,JLEV)*ZSIG(JLON)
  ENDDO
ENDDO

! PUT INT ITOP1 THE HIGHEST STARTING LEVEL 
ITOP1=ITOP
DO JLEV=ITOP,KLEV
  ISUM=0
  DO JLON=KIDIA,KFDIA
    ISUM=ISUM+INACT(JLON,JLEV)
  ENDDO
  IF (ISUM == 0 .AND. (ITOP1 == JLEV-1)) ITOP1=JLEV
ENDDO
! HERE BETTER AVOID COMPLETING UNWANTED LEVELS: never restart at KTDIA
! + BEWARE: WE WILL USE ZDDOM(ITOP-1)
IF(NPHYREP /= 0 .AND. NPHYREP /= -1) ITOP1=ITOP
ITOP=ITOP1


! for diagnostics, set values in all cases
ZFORA=0._JPRB
ZFORM=0._JPRB
!  QUITTING IN CASE OF NO DOWNDRAFT EVERYWHERE. 
!  THE "IF THEN / ENDIF" ENCOMPASSED SEQUENCE WILL NOT BE INDENTED 
!  GIVEN ITS LENGTH AND ITS SPECIAL CHARACTER (PSEUDO "RETURN" STATEMENT).

IF(ITOP1 < KLEV) THEN ! ELSE "PSEUDO RETURN"
! *
! ------------------------------------------------------------------
!     VII - DOWNDRAFT MASS FLUX
! ------------------------------------------------------------------
! ZFORM is kept 0 at top (0) AND at the lowest interface (KLEV)
! IF RESOLVED OMEGA <0, COMPUTE ZFORA WITH ABSOLUTE OMEGA_D
!             AND ZFORM = ZFORA
!    OTHERWISE COMPUTE ZFORA AND ZFORM WITH PERTURBATION WHERE RESOLVED
!             VELOCITY IS ACTUALLY DOWNWARDS AND MIGHT SET ZFORM/=ZFORA
ZFP1=0._JPRB
DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA
! LEAVE TO RESOLVED DOWNDRAFT WHEN ZSKOM=0 (=> INACT=0)
! INACT CONSIDERED TO BE AT THE LOWER INTERFACE...
     ZF=ZDDAL(JLON,JLEV+1)*&
       & (ZDDOM(JLON,JLEV+1)-MAX(0._JPRB,ZOMLS(JLON,JLEV+1)))*ZTPHY
     ZFORA(JLON,JLEV)=0.5_JPRB*(ZFP1(JLON)+ZF)*REAL(INACT(JLON,JLEV),JPRB)
! TRANSPORT FLUX LIMITED TO 10x THE EVAPORATING FLUX.
     ZFORM(JLON,JLEV)=ZFORA(JLON,JLEV)/&
                     & MAX(ZDDTRFX,(1._JPRB-ZSIG(JLON)))
     ZFP1(JLON)=ZF
  ENDDO
ENDDO
IF (LLSMOTR) THEN
! SECURITY: PREVENT HARSH DEPOSITION BY MISSING ACTIVE LEVEL IN PROFILE
! COMPUTE BASE PRESSURE OF EACH ACTIVE DOWDRAFT TO LIMIT DECREASE OF MASS FLUX
! TO BE NOT FASTER THAN PROP. TO PRESSURE INCREASE TOWARDS BASE
ZBUL(KIDIA:KFDIA)=0._JPRB
DO JLEV=ITOP,KLEV
   DO JLON=KIDIA,KFDIA
      ZBUL(JLON)=PAPRS(JLON,JLEV)*INACT(JLON,JLEV)&
               & +ZBUL(JLON)*(1-INACT(JLON,JLEV))
   ENDDO
ENDDO
!cdir unroll=8
DO JLEV=ITOP,KLEV
   DO JLON=KIDIA,KFDIA
! by constuction zform is zero below base, hence no risk
      ZFORM(JLON,JLEV)=MAX(ZFORM(JLON,JLEV),&
     &          ZFORM(JLON,JLEV-1)*(1._JPRB-PDELP(JLON,JLEV)/&
     &           MAX(PDELP(JLON,JLEV),ZBUL(JLON)-PAPRS(JLON,JLEV-1))))
   ENDDO
ENDDO
ENDIF


!     PROTECTION AGAINST NON-LINEAR INSTABILITY THAT CAN OCCASIONALLY
!     APPEAR DESPITE THE IMPLICIT ALGORITHM.
!     THIS SHOULD ONLY CONCERN THE TRANSPORT MASS FLUX BOTH FOR
!     DECREASE AND INCREASE
!     NOT THE PRODUCTION MASS FLUX !! ?? TO TEST *TODO*
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
       ZFORA(JLON,JLEV)=MAX(0.0_JPRB,ZFORA(JLON,JLEV-1)+(ZFORA(JLON,JLEV)&
        & -ZFORA(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
        & *MAX(0.0_JPRB,ZFORA(JLON,JLEV)-ZFORA(JLON,JLEV-1))))  
      ZFORM(JLON,JLEV)=MAX(0.0_JPRB,ZFORM(JLON,JLEV-1)+(ZFORM(JLON,JLEV)&
       & -ZFORM(JLON,JLEV-1))/(1.0_JPRB+PRDELP(JLON,JLEV)&
       & *MAX(0.0_JPRB,ZFORM(JLON,JLEV)-ZFORM(JLON,JLEV-1))))  
    ENDDO
  ENDDO


! *
!     -------------------------------------------------------------------
!     VIII - WIND RELATED COMPUTATION INCLUDING THE ENTRAINMENT AND
!     PRESSURE GRADIENT TERME (KERSHAW & GREGORY SCHEME), 
!     CONSIDERING THE ACTIVE LAYERS
!*
! ----------------------------------------------------------------------
! C.A.S. TREATMENT
! INBAS    : TRANSITION ARRAY:
!           1. IF HIGHEST LEVEL OF AN ACTIVE ZONE,
!           0. ELSEWHERE.
! ----------------------------------------------------------------------
! IF INACT=0 FOR THE WHOLE VERTICAL PROFILE, THE INTEGRALS WILL STAY 0.
! ----------------------------------------------------------------------

  DO JLON=KIDIA,KFDIA
    INBAS(JLON,ITOP)=INACT(JLON,ITOP)
  ENDDO
  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA
      INBAS(JLON,JLEV)=MAX((INACT(JLON,JLEV)-INACT(JLON,JLEV-1)),0)&
                      & *INACT(JLON,JLEV)
    ENDDO
  ENDDO

!*
!    -------------------------------------------------------------------
!     VIII B - DOWNWARDS LOOP FOR MOMENTUM ARRAYS (VERTICAL INTEGRALS)
!     ZA13, ZA14(JLON,JLEV), ZBET, ZUM, ZVM(JLON,JLEV).

! TEMPORAIRES 1D.

! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.

! TEMPORAIRES 2D.
! ZA13      : UC0.
! ZA14      : VC0.
! ZBET      : BETA COEFFICIENT FOR MOMENTUM PROFILE.
! ZRMIX     : ENTRAINMENT COEFFICIENT.
! ZUM       : AVERAGE U VALUE FROM THE DOWNDRAFT BASE.
! ZVM       : AVERAGE V VALUE FROM THE DOWNDRAFT BASE.
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
     ZA13(JLON,JLEV)=0.0_JPRB
     ZA14(JLON,JLEV)=0.0_JPRB
     ZBET(JLON,JLEV)=0.0_JPRB
     ZUM(JLON,JLEV)=0.0_JPRB
     ZVM(JLON,JLEV)=0.0_JPRB
   ENDDO
ENDDO

  DO JLON=KIDIA,KFDIA
    ZBET(JLON,ITOP)=INBAS(JLON,ITOP)
    ZUM(JLON,ITOP)=PU(JLON,ITOP)
    ZVM(JLON,ITOP)=PV(JLON,ITOP)
! BASE VELOCITY = ENVIRONMENT VELOCITY
       ZA13(JLON,ITOP)=PU(JLON,ITOP)
       ZA14(JLON,ITOP)=PV(JLON,ITOP)
  ENDDO

!     ALL VERTICAL INTEGRALS ZS ARE RESET TO ZERO WHEN PASSING INTO
!     AN INACTIVE LAYER, WHILE THE OUTPUT ARRAYS KEEP THEIR VALUE
!     UNTIL THE BEGINNING OF A NEW SEGMENT.

  DO JLEV=ITOP+1,KLEV
    DO JLON=KIDIA,KFDIA


! Net K coefficient: the full one, here 
! (split implicit is for the expression of the tendency ->fluxes)

! ZBAS et ZNBA SONT NULS DANS LES COUCHES INACTIVES
! ZNBA EST NUL AUSSI A LA BASE
! ZBAS EST NON NUL SEULEMENT A LA BASE
      ZBAS=INBAS(JLON,JLEV)
      ZNBA=(1-INBAS(JLON,JLEV))*INACT(JLON,JLEV)

! ? Is this treatment of  ZMIX  Obsolete ?
! if zmix=0, ZBET stays 1 beyond the cloud base !
      ZMIX=MAX(ZEPSMIX,ZRMIX(JLON,JLEV-1))

      ZBET(JLON,JLEV)=ZBET(JLON,JLEV-1)*(1.0_JPRB-ZMIX)*ZNBA+ZBAS

!     ZUM AND ZVM RESET TO PU(JLEV-1), PV AT FIRST ACTIVE LAYER
!     (SO CALLED B-1),I.E. VALUE OF ZUM IN THE LAYER BELOW LEVEL
!     WHERE ZBET=1 IS PU AT THE LEVEL ZBET=1.
!     ZBET IN THIS LOOP SHOULD NEVER REACH 1, MAX VALUE IS 1-ZMIX <1.

      ZUM(JLON,JLEV)=(ZUM(JLON,JLEV-1)&
       &+(ZMIX*(PU(JLON,JLEV-1)-ZUM(JLON,JLEV-1))&
       &+TDDGP*(PU(JLON,JLEV)-PU(JLON,JLEV-1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS))*ZNBA &
       &+ (1.0_JPRB-ZNBA)*PU(JLON,JLEV)
      ZVM(JLON,JLEV)=(ZVM(JLON,JLEV-1)&
       &+(ZMIX*(PV(JLON,JLEV-1)-ZVM(JLON,JLEV-1))&
       &+TDDGP*(PV(JLON,JLEV)-PV(JLON,JLEV-1)))&
       &/ (1.0_JPRB-ZBET(JLON,JLEV)+ZBAS))*ZNBA &
       &+ (1.0_JPRB-ZNBA)*PV(JLON,JLEV)

! BASE VELOCITY = ENVIRONMENT VELOCITY
!      ZA13=UC0, ZA14=VC0
!      DANS LES COUCHES INACTIVES, MULTIPLIE PAR ZBET=0

      ZA13(JLON,JLEV)=ZA13(JLON,JLEV-1)*ZNBA+PU(JLON,JLEV)*ZBAS
      ZA14(JLON,JLEV)=ZA14(JLON,JLEV-1)*ZNBA+PV(JLON,JLEV)*ZBAS
    ENDDO
  ENDDO
! NOW STORE THE DOWNDRAFT MOMENTUM PROFILE IN ZUM, ZVM
  DO JLEV=KTDIA,ITOP-1
    DO JLON=KIDIA,KFDIA
       ZUM(JLON,JLEV)=PU(JLON,JLEV)
       ZVM(JLON,JLEV)=PV(JLON,JLEV)
    ENDDO   
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
       ZZ=1.0_JPRB-ZBET(JLON,JLEV)
       ZUM(JLON,JLEV)=ZZ*ZUM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA13(JLON,JLEV)
       ZVM(JLON,JLEV)=ZZ*ZVM(JLON,JLEV)+ZBET(JLON,JLEV)*ZA14(JLON,JLEV)
    ENDDO   
  ENDDO

!*
! ------------------------------------------------------------------
!     IX - FLUX COMPUTATION 

!     FINAL COMPUTATION OF FLUXES WITH SECURITY AGAINST NEGATIVE
!     RESULTING WATER CONTENTS
!     PFEVCL, PFEVCN HAVE BEEN INITIALIZED TO ZERO IN INITAPLPAR
!     TRANSPORT FLUXES HAVE BEEN INITIALIZED TO ZERO IN APLPAR
  DO JLON=KIDIA,KFDIA
    ZFMDQE(JLON)=MAX(0.0_JPRB,ZFORA(JLON,KLEV-1)*ZFEVP(JLON,KLEV-1)&
   & *ZGDTI*0.5_JPRB)
    ZER(JLON)=ZFMDQE(JLON)*PRDELP(JLON,KLEV)
! Partition of the evaporation rate between snow and rain
    ZDELTA=ZSNP(JLON,KLEV)
    ZZDQS=ZER(JLON)*ZDELTA
    ZZDQR=ZER(JLON)-ZZDQS
    PFEVCL(JLON,KLEV-1)=-ZZDQR*PDELP(JLON,KLEV)
    PFEVCN(JLON,KLEV-1)=-ZZDQS*PDELP(JLON,KLEV)
  ENDDO

  DO JLEV=KLEV-1,ITOP+1,-1
    DO JLON=KIDIA,KFDIA
      ZAUX=1.0_JPRB/(PDELP(JLON,JLEV)+ZFORM(JLON,JLEV))
      ZFMDQEL=MAX(0.0_JPRB,ZFORA(JLON,JLEV-1)*ZFEVP(JLON,JLEV-1)&
     & *ZGDTI*0.5_JPRB)
      ZER(JLON)=ZAUX*(ZFMDQE(JLON)+ZFMDQEL+ZER(JLON)*ZFORM(JLON,JLEV))
      ZFMDQE(JLON)=ZFMDQEL
 ! Partition of the evaporation rate between snow and rain      
      ZDELTA=ZSNP(JLON,JLEV)
      ZZDQS=ZER(JLON)*ZDELTA
      ZZDQR=ZER(JLON)-ZZDQS
      PFEVCL(JLON,JLEV-1)=PFEVCL(JLON,JLEV) - ZZDQR*PDELP(JLON,JLEV)
      PFEVCN(JLON,JLEV-1)=PFEVCN(JLON,JLEV) - ZZDQS*PDELP(JLON,JLEV)
    ENDDO
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZAUX=1.0_JPRB/(PDELP(JLON,ITOP)+ZFORM(JLON,ITOP))
    ZER(JLON)=ZAUX*(ZFMDQE(JLON)+ZER(JLON)*ZFORM(JLON,ITOP))
 ! Partition of the evaporation rate between snow and rain      
    ZDELTA=ZSNP(JLON,ITOP)
    ZZDQS=ZER(JLON)*ZDELTA
    ZZDQR=ZER(JLON)-ZZDQS
    PFEVCL(JLON,ITOP-1)=PFEVCL(JLON,ITOP) - ZZDQR*PDELP(JLON,ITOP)
    PFEVCN(JLON,ITOP-1)=PFEVCN(JLON,ITOP) - ZZDQS*PDELP(JLON,ITOP)
  ENDDO
! Shift the flux by subtracting value at itop-1
  DO JLEV=ITOP,KLEV
    DO JLON=KIDIA,KFDIA
      PFEVCL(JLON,JLEV)=PFEVCL(JLON,JLEV)-PFEVCL(JLON,ITOP-1)
      PFEVCN(JLON,JLEV)=PFEVCN(JLON,JLEV)-PFEVCN(JLON,ITOP-1)
    ENDDO
  ENDDO
! Complete the subtraction at itop-1
  PFEVCL(KIDIA:KFDIA,ITOP-1)=0._JPRB
  PFEVCN(KIDIA:KFDIA,ITOP-1)=0._JPRB


      
!*   ----------------------------
!    DOWNDRAFT TRANSPORT FLUXES
!*   ----------------------------
! implicit calculation: requires accumulating the fluxes 
! hence top to bottom.
! replace the downdraft values by the difference (psi-psi_d)
! assume no condensate inside the dd.

ZBET(:,:)=REAL(INACT(:,:),JPRB)
DO JLEV=ITOP,KLEV
   DO JLON=KIDIA,KFDIA
      ZQDN(JLON,JLEV)=ZBET(JLON,JLEV)*(ZQDN(JLON,JLEV)-PQ(JLON,JLEV))
      ZSDN(JLON,JLEV)=ZBET(JLON,JLEV)*(ZSDN(JLON,JLEV)-ZDSE(JLON,JLEV))
      ZUM(JLON,JLEV)=ZBET(JLON,JLEV)*(ZUM(JLON,JLEV)-PU(JLON,JLEV))
      ZVM(JLON,JLEV)=ZBET(JLON,JLEV)*(ZVM(JLON,JLEV)-PV(JLON,JLEV))
   ENDDO
ENDDO

! Above itop: all fluxes are zero (initialized in aplpar)
! Level klev: ZFORM=0 there, hence all the fluxes are zero, too.

  DO JLEV=KLEV-1,ITOP,-1
    DO JLON=KIDIA,KFDIA
      ZAUX=ZFORM(JLON,JLEV)/(PDELP(JLON,JLEV+1)+ZFORM(JLON,JLEV))
      ZDPSGDT=PDELP(JLON,JLEV+1)*ZGDTI*0.5_JPRB
      PDIFCQD(JLON,JLEV)=ZAUX*( PDIFCQD(JLON,JLEV+1)+ &
       & ZDPSGDT*(ZQDN(JLON,JLEV)+ZQDN(JLON,JLEV+1)) )
! Beware: condensate assumed zero inside the dd !!
      PDIFCQLD(JLON,JLEV)=ZAUX*( PDIFCQLD(JLON,JLEV+1)- &
       & ZDPSGDT*(PQL(JLON,JLEV)+PQL(JLON,JLEV+1)) )
      PDIFCQID(JLON,JLEV)=ZAUX*( PDIFCQID(JLON,JLEV+1)- &
       & ZDPSGDT*(PQI(JLON,JLEV)+PQI(JLON,JLEV+1)) )
      PDIFCSD(JLON,JLEV)=ZAUX*( PDIFCSD(JLON,JLEV+1)+ &
       & ZDPSGDT*(ZSDN(JLON,JLEV)+ZSDN(JLON,JLEV+1)) )
      PSTRCUD(JLON,JLEV)=ZAUX*( PSTRCUD(JLON,JLEV+1)+ &
       & ZDPSGDT*(ZUM(JLON,JLEV)+ZUM(JLON,JLEV+1)) )
      PSTRCVD(JLON,JLEV)=ZAUX*( PSTRCVD(JLON,JLEV+1)+ &
       & ZDPSGDT*(ZVM(JLON,JLEV)+ZVM(JLON,JLEV+1)) )
    ENDDO
  ENDDO

! OPEN LOOP TESTS
IF (LLBO) THEN ! Do not care for vetorization here, because test only
      ZDDOM=0._JPRB
      ZDDAL=0._JPRB
      PDIFCQD(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
      PDIFCSD(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
      PSTRCUD(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
      PSTRCVD(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
      PDIFCQLD(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
      PDIFCQID(KIDIA:KFDIA,KTDIA-1:KLEV)=0._JPRB
ENDIF

ELSE ! pseudo return: BUT beware to the prognostic variables !
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
      ZDDOM(JLON,JLEV)=0.0_JPRB
      ZDDAL(JLON,JLEV)=0.0_JPRB
   ENDDO
ENDDO
ENDIF !itop pseudo return
DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
       PDDAL(JLON,JLEV)=ZDDAL(JLON,JLEV)
       PDDOM(JLON,JLEV)=ZDDOM(JLON,JLEV)
    ENDDO
ENDDO
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACNSDO',1,ZHOOK_HANDLE)
END SUBROUTINE ACNSDO

