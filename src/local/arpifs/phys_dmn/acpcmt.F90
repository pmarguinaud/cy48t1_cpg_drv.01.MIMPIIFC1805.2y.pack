!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACPCMT (YDCST, YDGEM,YDDIM,YDEGEO,YDLDDH,YDMDDH,YDRIP,YDML_PHY_MF,  KIDIA,KFDIA,KLON,KTDIA,KLEV,KTRA,&
  !-----------------------------------------------------------------------
  ! - INPUT  2D .
  & PALPH,PAPHI,PAPHIF,PAPRS,PAPRSF,PVERVEL,PCP,PLH,PR,&
  & PDELP,PLNPR,&
  & PQ,PQI,PQL,PRR,PS,PQLIS,PQSAT,PQW,&
  & PRDELP,PCVGQ,PT,PTW,PU,PV,PTKE,PTRA,PSMOOTRA,&
  & PQLCONV, PQICONV , PQRCONV, PQSCONV, &
  ! - INPUT 1D .
  & PGM, PTS, PQS, PNEIJ, PLSM, YDSTA, &
  ! - OUTPUT 2D .
  & PDIFCQ,PDIFCQL,PDIFCQI,PDIFCQLC,PDIFCQIC,PDIFCS,PDIFCTH,PPRODTH,&
  & PFCCQL,PFCCQN,PSTRCU,PSTRCV,PSTRCTRA,&
  & PFIMCC,PFPEVPCL,PFPEVPCN,&
  & PFPLCL,PFPLCN,PMF_UP, PMU,PMD,&
  & PFPFPCL,PFPFPCN,&
  & PFEDQLC, PFEDQIC, PFEDQRC, PFEDQSC,&
  & PFCNEGQLC,PFCNEGQIC,PFCNEGQRC,PFCNEGQSC, &
  & KNLAB,&
  & PNEBC,PQLIC,PTU,PQU,PQC_DET_PCMT,PCSGC,PEN_U_T, &
  ! - OUTPUT 1D .
  & KNND, PCAPE,PAIPCMT,PALF_CAPE,PALF_CVGQ,&
  ! - INPUT/OUTPUT 2D .
  & PUDAL,PUDOM,PDDAL,PDDOM,YDDDH)

!**** *ACPCMT * - PCMT CONVECTION SCHEME.

! SUJET.
!     ------

!**   INTERFACE.
!     ----------
!        *CALL* *ACPCMT*
!-----------------------------------------------------------------------
! -   INPUT ARGUMENTS.
!     ----------------

! - PHYSICS DIMENSIONNING PARAMETERS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KTRA       : NUMBER OF TRACERS (PASSIVE VARIABLES)

! - PHYSICS VARIABLES (ALPHABETICALLY IN EACH CATEGORY)

! - 2D (0:KLEV) .

! PAPHI      : HALF LEVEL GEOPOTENTIAL
! PAPRS      : HALF LEVEL PRESSURE

! - 2D (1:KLEV) .

! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATIC MODEL).
! PAPHIF     : FULL LEVEL GEOPOTENTIAL
! PAPRSF     : FULL LEVEL PRESSURE
! PVERVEL    : OMEGA/P.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PR         : CONSTANTE DE L'AIR HUMIDE.
! PDELP      : PRESSURE DIFFERENCE OVER THE LAYER
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (POUR L'HYDROSTATIQUE).
! PQ         : MICROPHYSICAL WATER VAPOUR SPECIFIC HUMIDITY
! PQI        : MICROPHYSICAL RESOLVED CLOUD ICE SPECIFIC CONTENT
! PQL        : MICROPHYSICAL RESOLVED CLOUD LIQUID SPECIFIC CONTENT
! PRR        : MICROPHYSICAL RESOLVED RAIN LIQUID SPECIFIC CONTENT
! PS         : MICROPHYSICAL RESOLVED RAIN ICE SPECIFIC CONTENT
! PQLIS      : MICROPHYSICAL RESOLVED CLOUD WATER SPECIFIC CONTENT IN THE TIME-STEP
! PQSAT      : SATURATION SPECIFIC MOISTURE
! PQW        : WET BULB SPECIFIC MOISTURE
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PCVGQ      : CONVERGENCE D'HUMIDITE.
! PT         : TEMPERATURE.
! PTW        : WET BULB TEMPERATURE.
! PU         : U WIND COMPONENT
! PV         : V WIND COMPONENT
! PTKE       : TKE
! PTRA       : TRACER CONCENTRATION
! PSMOOTRA   : SMOOTHING PARAMETER FOR TRACER CONCENTRATION

! - 2D (0:KLEV) .

! - 1D .

! PGM        : FACTEUR D'ECHELLE.
! PTS        : SURFACE LAYER TEMPERATURE.
! PQS        : SURFACE LAYER WATER VAPOUR.
! PNEIJ      : FRACTION OF SOIL COVERED BY SNOW.
! PLSM       : LAND/SEA MASK.
! PVETAF     : VERTICAL COORDINATE.

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS
!     ----------------

! - 2D (0:KLEV) .

! PQLCONV    : LIQUID WATER (CONVECTIVE).
! PQICONV    : ICE    WATER (CONVECTIVE).
! PQRCONV    : RAIN   WATER (CONVECTIVE).
! PQSCONV    : SNOW   WATER (CONVECTIVE).
! PDIFCQ     : FLUX CONVECTIF D'HUMIDITE SPECIFIQUE (HORS PLUIE/NEIGE).
! PDIFCQ     : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
! PDIFCQI    : FLUX CONVECTIF D'EAU SOLIDE (HORS PLUIE/NEIGE).
! PDIFCQI    : CONVECTIVE FLUX OF SOLID WATER (NOT RAIN/SNOW).
! PDIFCQL    : FLUX CONVECTIF D'EAU LIQUIDE (HORS PLUIE/NEIGE).
! PDIFCQL    : CONVECTIVE FLUX OF LIQUID WATER (NOT RAIN/SNOW).
! PDIFCQLC   : CONVECTIVE TRANSPORT FLUX OF QLC (LIQUID CONVECTIVE).
! PDIFCQIC   : CONVECTIVE TRANSPORT FLUX OF QIC (ICE CONVECTIVE).
! PDIFCS     : DRY STATIC ENERGY CONVECTIVE FLUX
! PDIFCTH    : THETA CONVECTIVE FLUX
! PPRODTH    : THERMAL PRODUCTION
! PFCCQL     : LIQUID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PFCCQN     : SOLID CONDENSATION FLUX FROM THE CONVECTIVE UPDRAFT
! PSTRCU     : U-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCV     : V-MOMENTUM CONVECTIVE SPECIFIC MOISTURE FLUX
! PSTRCTRA   : TRACER CONVECTIVE FLUX
! PFHMLTS    : FLUX DE CHALEUR DU A LA FONTE/GEL.
! PFPEVPCL    : EVAPORATION DES PRECIPITATIONS LIQUIDES.
! PFPEVPCN    : EVAPORATION DES PRECIPITATIONS NEIGEUSES.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PFPFPCL    : FLUX OF CONVECTIVE PRECIPITATION : THE GENERATION TERM.
! PFPFPCN    : FLUX OF CONVECTIVE PRECIPITATION : THE GENERATION TERM.
! PMF_UP     : UPWARD NET MASS FLUX
! PMU        : UPDRAFT MASS FLUX
! PMD        : DOWNDRAFT MASS FLUX

! - 2D (1:KLEV) .

! PNEBC  : CONVECTIVE CLOUDINESS.
! PQLIC  : CONVECTIVE CONDENSATE.
! PTU    : TEMPERATURE OF THE CLOUDY ASCENT.
! PQU    : WATER VAPOUR SPECIFIC HUMIDITY OF THE CLOUDY ASCENT.
! PQC_DET_PCMT : CLOUD CONDENSATE (QL+QI) DETRAINED BY PCMT CONVECTION.
! PEN_U_T: ENTRAINMENT TOTAL (FROM BASE) IN UPDRAFT (IN S**-1).
! KNLAB  : CONVECTIVE ACTIVITY INDEX (ACTIVE IF EQUAL TO ONE).

! - 1D (DIAGNOSTIQUE) .

! KNND   : ZERO WHENEVER A NON PHYSICAL SITUATION OCCURS, ONE OTHERWISE
! PCAPE  : CAPE (CONDITIONAL AVAILABLE POTENTIAL ENERGY).
! PAIPCMT  : ACTIVITY INDEX OF PCMT: 1. IF PCMT IS ACTIVE, 0. ELSE CASE.

!-----------------------------------------------------------------------

! -   INPUT/OUTPUT ARGUMENTS
!     ----------------------

! - 2D (0:KLEV) .

! - 2D (1:KLEV) .

! PUDAL    : PROGNOSTIC UPDRAFT MESH FRACTION
! PUDOM    : PROGNOSTIC UPDRAFT RELATIVE VELOCITY
! PDDAL    : PROGNOSTIC DOWNDRAFT MESH FRACTION
! PDDOM    : PROGNOSTIC DOWNDRAFT RELATIVE VELOCITY
! YDDDH    : DDH superstructure
!-----------------------------------------------------------------------

! METHOD.
!     --------

! AUTHOR.
!     -------
! 2011-12-12, J.M. PIRIOU AND J.F. GUEREMY.

! MODIFICATIONS.
!     --------------
! 2014-04---, D. SAINT-MARTIN : tracer transport.
! 2015-02---, I. BEAU: bugfix (argument PDDOM)
! 2015-09---, J.M PIRIOU: phasing of NWP and CLimate versions
! 2015-11---, J.F. Gueremy: explicit transport flux for the dry air potential temperature
! 2016-07---, J.F. Gueremy: Scaling factor for detrainment of liquid/ice/rain/snow added
! 2016-07---, J.F. Gueremy: Sedimentation fluxes ZSEDIQL(I)
! 2017-11---, J.F. Gueremy: diag entrainement up total PEN_U_T
! 2017-12 - R. Roehrig: Add downdraft mass flux diagnostic
! 2018-01 - D. St-Martin : smoothing paramater for tracer transport
! 2018-02---, J.F. Gueremy: thermal production for TKE (PPRODTH)
! 2018-08 - J. Bock : cosmetic cleaning
! 2018-08 - J.F. Gueremy: unused variable ZSU

!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMMDDH              , ONLY : TMDDH
USE YOMRIP               , ONLY : TRIP
USE YOMDIM               , ONLY : TDIM
USE YOMGEM               , ONLY : TGEM
USE YEMGEO               , ONLY : TEGEO
USE PARKIND1             , ONLY : JPIM, JPRB
USE YOMHOOK              , ONLY : LHOOK,   DR_HOOK

USE YOMCST               , ONLY : TCST

USE YOMLSFORC            , ONLY : LMUSCLFA,NMUSCLFA
USE DDH_MIX              , ONLY : ADD_FIELD_3D, NEW_ADD_FIELD_3D, TYP_DDH
USE YOMLDDH              , ONLY : TLDDH
USE YOMSCM               , ONLY : NFRSCM
USE YOMCT3               , ONLY : NSTEP
USE YOMSTA               , ONLY : TSTA

IMPLICIT NONE

!     DUMMY ARGUMENTS.

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(TGEM)        , INTENT(IN)    :: YDGEM
TYPE(TDIM)        , INTENT(IN)    :: YDDIM
TYPE(TEGEO)       , INTENT(IN)    :: YDEGEO
TYPE(TLDDH)       , INTENT(IN)    :: YDLDDH
TYPE(TMDDH)       , INTENT(IN)    :: YDMDDH
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TRIP)        , INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM), INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM), INTENT(IN)    :: KLON
INTEGER(KIND=JPIM), INTENT(IN)    :: KTDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KTRA

REAL(KIND=JPRB), INTENT(IN)    :: PALPH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPHIF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLH(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PR(KLON,KLEV)

REAL(KIND=JPRB), INTENT(IN)    :: PDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PLNPR(KLON,KLEV)

REAL(KIND=JPRB), INTENT(IN)    :: PQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQI(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PS(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PRR(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQLIS(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQSAT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQW(KLON,KLEV)

REAL(KIND=JPRB), INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PCVGQ(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTW(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTKE(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PTRA(KLON, KLEV, KTRA)
REAL(KIND=JPRB), INTENT(IN)    :: PSMOOTRA(KTRA)

REAL(KIND=JPRB), INTENT(IN)    :: PQLCONV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQICONV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQRCONV(KLON,KLEV)
REAL(KIND=JPRB), INTENT(IN)    :: PQSCONV(KLON,KLEV)

REAL(KIND=JPRB), INTENT(IN)    :: PGM(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PQS(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PNEIJ(KLON)
REAL(KIND=JPRB), INTENT(IN)    :: PLSM(KLON)
TYPE(TSTA),      INTENT(IN)    :: YDSTA

REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQ(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQI(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQLC(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCQIC(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCS(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PDIFCTH(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PPRODTH(KLON,0:KLEV)

REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFCCQN(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCU(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCV(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PMF_UP(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PMU(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PMD(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PSTRCTRA(KLON, 0:KLEV, KTRA)

REAL(KIND=JPRB), INTENT(INOUT)   :: PFIMCC(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPEVPCN(KLON,0:KLEV)

REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPLCN(KLON,0:KLEV)

REAL(KIND=JPRB), INTENT(INOUT)   :: PFPFPCL(KLON,0:KLEV)
REAL(KIND=JPRB), INTENT(INOUT)   :: PFPFPCN(KLON,0:KLEV)

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEDQLC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEDQIC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEDQRC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFEDQSC(KLON,0:KLEV)

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCNEGQLC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCNEGQIC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCNEGQRC(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCNEGQSC(KLON,0:KLEV)

INTEGER(KIND=JPIM), INTENT(INOUT) :: KNLAB(KLON,KLEV)
INTEGER(KIND=JPIM), INTENT(INOUT) :: KNND(KLON)

REAL(KIND=JPRB), INTENT(OUT)   :: PNEBC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQLIC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PTU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQU(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PQC_DET_PCMT(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PCSGC(KLON,KLEV)
REAL(KIND=JPRB), INTENT(OUT)   :: PEN_U_T(KLON,KLEV)

REAL(KIND=JPRB), INTENT(OUT)   :: PCAPE(KLON)
REAL(KIND=JPRB), INTENT(OUT)   :: PAIPCMT(KLON)

REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CAPE(KLON)
REAL(KIND=JPRB), INTENT(OUT)   :: PALF_CVGQ(KLON)

REAL(KIND=JPRB), INTENT(INOUT) :: PUDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PUDOM(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDAL(KLON,KLEV)
REAL(KIND=JPRB), INTENT(INOUT) :: PDDOM(KLON,KLEV)

TYPE(TYP_DDH)  , INTENT(INOUT) :: YDDDH

! LOCALS.
REAL(KIND=JPRB) :: ZEROV(KLON,KLEV)
REAL(KIND=JPRB)   :: ZTCVIM
REAL(KIND=JPRB)   :: ZICE
REAL(KIND=JPRB)   :: ZQRC   (KLON,KLEV)
REAL(KIND=JPRB)   :: ZDDW(KLON,KLEV)     ! downdraft vertical velocity
REAL(KIND=JPRB)   :: ZUDQC  (KLON,KLEV)
! ZENTR_U    : ENTRAINEMENT DANS L'UPDRAFT.
! ZDETR_U    : DETRAINEMENT DE L'UPDRAFT.
! ZENTR_D    : ENTRAINEMENT DANS LE DOWNDRAFT.
! ZDETR_D    : DETRAINEMENT DU DOWNDRAFT.
REAL(KIND=JPRB) :: ZENTR_U(KLON,KLEV),ZDETR_U(KLON,KLEV)
REAL(KIND=JPRB) :: ZDETR_U_L(KLON,KLEV),ZDETR_D_L(KLON,KLEV)
REAL(KIND=JPRB) :: ZDETR_U_I(KLON,KLEV),ZDETR_D_I(KLON,KLEV)
REAL(KIND=JPRB) :: ZDETR_U_R(KLON,KLEV),ZDETR_D_R(KLON,KLEV)
REAL(KIND=JPRB) :: ZDETR_U_S(KLON,KLEV),ZDETR_D_S(KLON,KLEV)
REAL(KIND=JPRB) :: ZENTR_D(KLON,KLEV),ZDETR_D(KLON,KLEV)
REAL(KIND=JPRB) :: ZFCCQLTMP(KLON,0:KLEV)
REAL(KIND=JPRB)   :: ZQSC   (KLON,KLEV)
REAL(KIND=JPRB)   :: ZQIC   (KLON,KLEV)
REAL(KIND=JPRB)   :: ZQLCONVPREC
REAL(KIND=JPRB)   :: ZUDW(KLON,KLEV)     ! updraft vertical velocity
REAL(KIND=JPRB)   :: ZQLC   (KLON,KLEV)
INTEGER(KIND=JPIM) :: JLEV,JLON,JTRA,INEBC,IQLIC,IQLIC_MICRO,ICV
REAL(KIND=JPRB)   :: ZW_COMPENS(KLON,KLEV)     ! compensating vertical velocity due to the updraft and downdraft
REAL(KIND=JPRB)   :: ZW_CONV(KLON,KLEV)     ! vertical velocity for cloud transport
REAL(KIND=JPRB) :: ZFCCQNTMP(KLON,0:KLEV)
! ZFHEVPPC   : FLUX DE CHALEUR DU A L'EVAPORATION DES PREC. CONVECTIVES.
! ZFHMLTSC   : FLUX DE CHALEUR DU A LA FONTE/GEL DES PREC. CONVECTIVES.
REAL(KIND=JPRB) :: ZFHEVPPC(KLON,0:KLEV),ZFHMLTSC(KLON,0:KLEV)
! ZQLIC_MICRO: convective condensate, to be used in microphysics (evaporation, etc).
! ZNEBC_MICRO: convective cloudiness, to be used in microphysics (evaporation, etc).
REAL(KIND=JPRB) :: ZQLIC_MICRO(KLON,KLEV),ZNEBC_MICRO(KLON,KLEV),ZDAL(KLON)
REAL(KIND=JPRB) :: ZQLCED(KLON,KLEV),ZQICED(KLON,KLEV)
REAL(KIND=JPRB) :: ZQ_COND_XR96(KLON,KLEV) ! QLC+QIC+QRC+QSC, FOR USE IN THE XU-RANDALL CLOUDINESS.
REAL(KIND=JPRB) :: ZCCHSL,ZCCHSN,ZEPS
!REAL(KIND=JPRB) :: ZMAX(KLON)
REAL(KIND=JPRB) :: ZDEPTH(KLON)
REAL(KIND=JPRB) :: ZHOOK_HANDLE
REAL(KIND=JPRB) :: ZTMPVAR(KLON,KLEV)

REAL(KIND=JPRB) :: ZTMIC(KLON,KLEV) ! Temperature for microphysics
REAL(KIND=JPRB) :: ZQMIC(KLON,KLEV) ! Specific moisture for microphysics
REAL(KIND=JPRB) :: ZEVAPO(KLON,KLEV) ! Evaporation from previous time step.
REAL(KIND=JPRB) :: ZALF(KLON) ! Prognostic active surface fraction.
REAL(KIND=JPRB) :: ZFRACP,ZTH
REAL(KIND=JPRB) :: ZSEDIQL(KLON,0:KLEV) ! sedimentation flux of cloud liquid water
REAL(KIND=JPRB) :: ZSEDIQI(KLON,0:KLEV) ! sedimentation flux of cloud ice water
LOGICAL         :: LLADJCLD

#include "acadvec.intfb.h"
#include "acmtentr.intfb.h"
#include "acmtud.intfb.h"
#include "acpluiz.intfb.h"
#include "wrscmr.intfb.h"
#include "acnebxrs.intfb.h"
#include "fcttrm.ycst.h"
#include "fctdoi.ycst.h"

IF (LHOOK) CALL DR_HOOK('ACPCMT',0,ZHOOK_HANDLE)
ASSOCIATE(LCVMICC=>YDML_PHY_MF%YRPHY0%LCVMICC, GCVFNEP1=>YDML_PHY_MF%YRPHY0%GCVFNEP1, &
 & RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, GCVFNEP2=>YDML_PHY_MF%YRPHY0%GCVFNEP2, GSHELL=>YDML_PHY_MF%YRPHY0%GSHELL, &
 & FNEBC=>YDML_PHY_MF%YRPHY0%FNEBC, GREDDI=>YDML_PHY_MF%YRPHY0%GREDDI, GREDDL=>YDML_PHY_MF%YRPHY0%GREDDL, &
 & GREDDR=>YDML_PHY_MF%YRPHY0%GREDDR, GREDDS=>YDML_PHY_MF%YRPHY0%GREDDS, &
 & GCVFNER=>YDML_PHY_MF%YRPHY0%GCVFNER, &
 & LCVTADV=>YDML_PHY_MF%YRPHY0%LCVTADV, GCVFNEO=>YDML_PHY_MF%YRPHY0%GCVFNEO, LCVNHD=>YDML_PHY_MF%YRPHY0%LCVNHD, &
 & NCVCLOS=>YDML_PHY_MF%YRPHY0%NCVCLOS, THPCMT=>YDML_PHY_MF%YRPHY0%THPCMT, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LNEBNXR=>YDML_PHY_MF%YRPHY%LNEBNXR, LEDKF=>YDML_PHY_MF%YRPHY%LEDKF, LCVPPKF=>YDML_PHY_MF%YRPHY%LCVPPKF, &
 & NCVQLI=>YDML_PHY_MF%YRPHY0%NCVQLI, LCORSEDIM=>YDML_PHY_MF%YRPHY0%LCORSEDIM, &
 & LFLEXDIA=>YDLDDH%LFLEXDIA, LDDH_OMP=>YDLDDH%LDDH_OMP, YDPHY0=>YDML_PHY_MF%YRPHY0)
ZEPS=1.E-12_JPRB
ZCCHSL=YDCST%RCW-YDCST%RCPD
ZCCHSN=YDCST%RCS-YDCST%RCPD
PFIMCC(:,:)=0._JPRB
ZDAL(:)=0._JPRB
PQC_DET_PCMT(:,:)=0._JPRB
ZEVAPO(:,:)=0._JPRB
ZNEBC_MICRO(:,:)=ZEPS
ZQLIC_MICRO(:,:)=0._JPRB
ZQLCED(:,:)=0._JPRB
ZQICED(:,:)=0._JPRB
ZQRC(:,:)=0._JPRB
ZQSC(:,:)=0._JPRB
PTU(:,:)=PT(:,:)
PQU(:,:)=PQ(:,:)
ZSEDIQL(:,:) = 0.0_JPRB
ZSEDIQI(:,:) = 0.0_JPRB

IF(LCVNHD) THEN
  ! Evaporation from previous time step is got from the PDDAL buffer.
  ZEVAPO(KIDIA:KFDIA,KTDIA:KLEV)=PDDAL(KIDIA:KFDIA,KTDIA:KLEV)
ENDIF

! UDPDRAFT.
CALL ACMTUD (YDGEM,YDDIM,YDEGEO,YDLDDH,YDMDDH,YDRIP,YDML_PHY_MF, KIDIA,KFDIA,KLON,KTDIA,KLEV,KTRA,&
  & PALPH, PAPHI, PAPHIF, PAPRS, PAPRSF,PVERVEL, PCP,PLH, PR,&
  & PDELP, PLNPR,&
  & PQ, PQLIS, PQSAT, PQW,&
  & PRDELP, PCVGQ,PT, PTW, PU, PV, PTKE, PTRA, PSMOOTRA, ZEVAPO, &
  & PGM,PTS,&
  & PDIFCQ, PDIFCQL, PDIFCQI, PDIFCS, PDIFCTH,PPRODTH,&
  & PFCCQL, PFCCQN, PSTRCU, PSTRCV, PSTRCTRA,&
  & ZFHMLTSC,ZFHEVPPC,PFPEVPCL,PFPEVPCN,&
  & PFPLCL,PFPLCN,PMF_UP,PMU,PMD,&
  & PNEBC,PQLIC,PTU,PQU,KNLAB,ZENTR_U,ZDETR_U,ZENTR_D,ZDETR_D,&
  & PEN_U_T,&
  & KNND,PCAPE, ZALF, PALF_CAPE, PALF_CVGQ,&
  & PUDAL,PUDOM,PDDAL,PDDOM, YDDDH)

IF(NCVQLI == 2) THEN
  ! Radiative condensates and cloudiness, due to convection, computed as ql+qi+qr+qs.
  DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
    DO JLON = KIDIA, KFDIA
      PQLIC(JLON,JLEV)=PQLCONV(JLON,JLEV)+PQICONV(JLON,JLEV) &
        & +PQRCONV(JLON,JLEV)+PQSCONV(JLON,JLEV)
      PNEBC(JLON,JLEV)=PUDAL(JLON,JLEV)*FNEBC
    ENDDO
  ENDDO
ENDIF

IF(LMUSCLFA.AND.(MOD(NSTEP,NFRSCM)==0)) THEN
  CALL WRSCMR(NMUSCLFA,'PUDAL',PUDAL,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PUDOM',PUDOM,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PDDAL',PDDAL,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PDDOM',PDDOM,KLON,KLEV)
ENDIF


! INITIALIZE CONVECTIVE PROFILES: INTRA-TIME-STEP EVOLUTION.
ZQLC(:,:)=PQLCONV(:,:)
ZQIC(:,:)=PQICONV(:,:)
ZQRC(:,:)=PQRCONV(:,:)
ZQSC(:,:)=PQSCONV(:,:)

!-------------------------------------------------
! ENTRAINMENT AND DETRAINMENT.
!-------------------------------------------------

! Scaling factor for detrainment of liquid/ice/rain/snow
DO JLEV = KTDIA-1, KLEV
  ZDETR_U_L(KIDIA:KFDIA,JLEV)=GREDDL*ZDETR_U(KIDIA:KFDIA,JLEV)
  ZDETR_D_L(KIDIA:KFDIA,JLEV)=GREDDL*ZDETR_D(KIDIA:KFDIA,JLEV)
  ZDETR_U_I(KIDIA:KFDIA,JLEV)=GREDDI*ZDETR_U(KIDIA:KFDIA,JLEV)
  ZDETR_D_I(KIDIA:KFDIA,JLEV)=GREDDI*ZDETR_D(KIDIA:KFDIA,JLEV)
  ZDETR_U_R(KIDIA:KFDIA,JLEV)=GREDDR*ZDETR_U(KIDIA:KFDIA,JLEV)
  ZDETR_D_R(KIDIA:KFDIA,JLEV)=GREDDR*ZDETR_D(KIDIA:KFDIA,JLEV)
  ZDETR_U_S(KIDIA:KFDIA,JLEV)=GREDDS*ZDETR_U(KIDIA:KFDIA,JLEV)
  ZDETR_D_S(KIDIA:KFDIA,JLEV)=GREDDS*ZDETR_D(KIDIA:KFDIA,JLEV)
ENDDO

CALL ACMTENTR(YDML_PHY_MF%YRPHY2,KIDIA, KFDIA, KLON, KTDIA, KLEV, PDELP, ZQLC, PQL, PUDAL, PDDAL, &
  & ZENTR_U, ZENTR_D, ZDETR_U_L, ZDETR_D_L, PFEDQLC)
CALL ACMTENTR(YDML_PHY_MF%YRPHY2,KIDIA, KFDIA, KLON, KTDIA, KLEV, PDELP, ZQIC, PQI, PUDAL, PDDAL, &
  & ZENTR_U, ZENTR_D, ZDETR_U_I, ZDETR_D_I, PFEDQIC)
CALL ACMTENTR(YDML_PHY_MF%YRPHY2,KIDIA, KFDIA, KLON, KTDIA, KLEV, PDELP, ZQRC, PRR, PUDAL, PDDAL, &
  & ZENTR_U, ZENTR_D, ZDETR_U_R, ZDETR_D_R, PFEDQRC)
CALL ACMTENTR(YDML_PHY_MF%YRPHY2,KIDIA, KFDIA, KLON, KTDIA, KLEV, PDELP, ZQSC, PS,  PUDAL, PDDAL, &
  & ZENTR_U, ZENTR_D, ZDETR_U_S, ZDETR_D_S, PFEDQSC)

IF(LMUSCLFA.AND.(MOD(NSTEP,NFRSCM)==0)) THEN
  CALL WRSCMR(NMUSCLFA,'ZENTR_U',ZENTR_U,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_U',ZDETR_U,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZENTR_D',ZENTR_D,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_D',ZDETR_D,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_U_L',ZDETR_U_L,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_U_I',ZDETR_U_I,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_U_R',ZDETR_U_R,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_U_S',ZDETR_U_S,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_D_L',ZDETR_D_L,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_D_I',ZDETR_D_I,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_D_R',ZDETR_D_R,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'ZDETR_D_S',ZDETR_D_S,KLON,KLEV)
  CALL WRSCMR(NMUSCLFA,'PFEDQLC',PFEDQLC,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'PFEDQIC',PFEDQIC,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'PFEDQRC',PFEDQRC,KLON,KLEV+1)
  CALL WRSCMR(NMUSCLFA,'PFEDQSC',PFEDQSC,KLON,KLEV+1)
ENDIF

DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ! UPDATE INTRA-TIME-STEP VALUES FROM ENTRAINMENT/DETRAINMENT FLUXES.
    ZQLC(JLON,JLEV)=MAX(0._JPRB,ZQLC(JLON,JLEV)-YDCST%RG*(&
      &+PFEDQLC(JLON,JLEV)-PFEDQLC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY)
    ZQIC(JLON,JLEV)=MAX(0._JPRB,ZQIC(JLON,JLEV)-YDCST%RG*(&
      &+PFEDQIC(JLON,JLEV)-PFEDQIC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY)
    ZQRC(JLON,JLEV)=MAX(0._JPRB,ZQRC(JLON,JLEV)-YDCST%RG*(&
      &+PFEDQRC(JLON,JLEV)-PFEDQRC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY)
    ZQSC(JLON,JLEV)=MAX(0._JPRB,ZQSC(JLON,JLEV)-YDCST%RG*(&
      &+PFEDQSC(JLON,JLEV)-PFEDQSC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY)
    ! DIAGNOSE CONDENSATE DETRAINED BY PCMT CONVECTION.
    PQC_DET_PCMT(JLON,JLEV)=MAX(0._JPRB,YDCST%RG*(&
      &+PFEDQLC(JLON,JLEV)-PFEDQLC(JLON,JLEV-1) &
      &+PFEDQIC(JLON,JLEV)-PFEDQIC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY)
  ENDDO
ENDDO

ZQLCED(:,:)=ZQLC(:,:)
ZQICED(:,:)=ZQIC(:,:)
! UPDATE INTRA-TIME-STEP CONVECTIVE VALUES FROM CONDENSATION FLUXES.
DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQLC(JLON,JLEV)=ZQLC(JLON,JLEV)-YDCST%RG*(&
      &-PFCCQL(JLON,JLEV)+PFCCQL(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
    ZQIC(JLON,JLEV)=ZQIC(JLON,JLEV)-YDCST%RG*(&
      &-PFCCQN(JLON,JLEV)+PFCCQN(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
  ENDDO
ENDDO

!-------------------------------------------------
! "LOPEZ" MICROPHYSICS: AUTOCONVERSION / COLLECTION / SEDIMENTATION OF PRECIPITATION / EVAPORATION OF PRECIPITATION.
!-------------------------------------------------

! QCCONV = QLCONV + QICONV.
DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQLIC_MICRO(JLON,JLEV)=ZQLC(JLON,JLEV)+ZQIC(JLON,JLEV)
    ZQ_COND_XR96(JLON,JLEV)=ZQLC(JLON,JLEV)+ZQIC(JLON,JLEV) &
     & +ZQRC(JLON,JLEV)+ZQSC(JLON,JLEV)
  ENDDO
ENDDO
ZFCCQLTMP(:,:)=0._JPRB
ZFCCQNTMP(:,:)=0._JPRB
IF(LNEBNXR) THEN
  ! XU-RANDALL 1996 CLOUDINESS.
  CALL ACNEBXRS( YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
    & PQ,ZQ_COND_XR96,PQSAT,&
    & ZNEBC_MICRO)
  DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
    DO JLON = KIDIA, KFDIA
      PQLIC(JLON,JLEV)=ZQ_COND_XR96(JLON,JLEV)
      PNEBC(JLON,JLEV)=ZNEBC_MICRO(JLON,JLEV)*FNEBC
    ENDDO
  ENDDO
  !
  ! DDH diagnostics.
  !
  IF(LFLEXDIA) THEN
    DO JLEV = KTDIA, KLEV
      DO JLON = KIDIA, KFDIA
        ZTMPVAR(JLON,JLEV)=PDELP(JLON,JLEV)*PQLIC(JLON,JLEV)
      ENDDO
    ENDDO
    IF(LDDH_OMP) THEN
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'VQX',YDDDH)
    ELSE
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'VQX','V','ARP',.TRUE.,.TRUE.)
    ENDIF
    DO JLEV = KTDIA, KLEV
      DO JLON = KIDIA, KFDIA
        ZTMPVAR(JLON,JLEV)=PDELP(JLON,JLEV)*PNEBC(JLON,JLEV)
      ENDDO
    ENDDO
    IF(LDDH_OMP) THEN
      CALL NEW_ADD_FIELD_3D(YDMDDH,ZTMPVAR,'VNC',YDDDH)
    ELSE
      CALL ADD_FIELD_3D(YDLDDH,ZTMPVAR,'VNC','V','ARP',.TRUE.,.TRUE.)
    ENDIF    
  ENDIF
ELSE
  IF(LCVMICC) THEN
    DO JLEV = KTDIA, KLEV
      DO JLON = KIDIA, KFDIA
        ZDAL(JLON)=MAX(ZDAL(JLON),PUDAL(JLON,JLEV)+PDDAL(JLON,JLEV))
      ENDDO
    ENDDO
  ENDIF
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ! CONSISTENCY BETWEEN CLOUDINESS AND CLOUD CONDENSATE:
      ! ZNEBC_MICRO=PUDAL+PDDAL WHERE >0, ZQLIC_MICRO/3.E-3 OTHERWISE
      IQLIC_MICRO=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-ZQLIC_MICRO(JLON,JLEV)+0.0_JPRB)))
      IQLIC=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-PQLIC(JLON,JLEV)+0.0_JPRB)))
      INEBC=NINT(MAX(0.0_JPRB,-SIGN(1.0_JPRB,-(PUDAL(JLON,JLEV)*IQLIC)+0.0_JPRB)))
      IF(LCVMICC) THEN
        ZNEBC_MICRO(JLON,JLEV)=(1-IQLIC_MICRO)*ZEPS &
         &+IQLIC_MICRO*(INEBC*MAX(ZEPS,(PUDAL(JLON,JLEV)+PDDAL(JLON,JLEV))) &
         &+(1-INEBC)*MAX(ZEPS,MIN(ZDAL(JLON),ZQLIC_MICRO(JLON,JLEV)/1.E-2_JPRB)))
      ELSE
        ZNEBC_MICRO(JLON,JLEV)=(1-IQLIC_MICRO)*ZEPS &
         &+IQLIC_MICRO*(INEBC*MAX(ZEPS,(PUDAL(JLON,JLEV)+PDDAL(JLON,JLEV))) &
         &+(1-INEBC)*MAX(ZEPS,MIN(0.9999_JPRB,ZQLIC_MICRO(JLON,JLEV)/3.E-3_JPRB)))
      ENDIF
    ENDDO
  ENDDO
ENDIF
ZEROV(:,:)=0._JPRB
PFCCQL(:,:)=0._JPRB
PFCCQN(:,:)=0._JPRB

! Microphysics occurs in a shell between updraft and its resolved environment.
ZTMIC(KIDIA:KFDIA,KTDIA:KLEV)=(1._JPRB-GSHELL)*PTU(KIDIA:KFDIA,KTDIA:KLEV) &
  & +GSHELL*PT(KIDIA:KFDIA,KTDIA:KLEV)
ZQMIC(KIDIA:KFDIA,KTDIA:KLEV)=(1._JPRB-GSHELL)*PQU(KIDIA:KFDIA,KTDIA:KLEV) &
  & +GSHELL*PQ(KIDIA:KFDIA,KTDIA:KLEV)

! Call microphysics.
LLADJCLD=.FALSE.
CALL ACPLUIZ (YDCST, YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
  & ZTMIC, ZQMIC, ZQLCED, ZQICED, ZQRC, ZQSC,&
  & PDELP, PAPRSF, PCP, PR, ZNEBC_MICRO, ZQLIC_MICRO,ZEROV,ZEROV,ZEROV,&
  & ZEROV, ZEROV, LLADJCLD, PAPHI,&
  & PTS, PNEIJ, PLSM, PGM, YDSTA, &
  & PFCCQL,PFCCQN, PFPLCL, PFPLCN,&
  & PFPEVPCL, PFPEVPCN, PFPFPCL, PFPFPCN, PDIFCQLC, PDIFCQIC )

IF (LCORSEDIM) THEN
  DO JLEV = 0, KLEV
    ZSEDIQL(KIDIA:KFDIA,JLEV)=PDIFCQLC(KIDIA:KFDIA,JLEV)
    ZSEDIQI(KIDIA:KFDIA,JLEV)=PDIFCQIC(KIDIA:KFDIA,JLEV)
  ENDDO
ENDIF

! UPDATE INTRA-TIME-STEP CONVECTIVE VALUES FROM MICROPHYSICAL FLUXES.
DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQLC(JLON,JLEV)=ZQLC(JLON,JLEV)-YDCST%RG*(&
      &+PFPFPCL(JLON,JLEV)-PFPFPCL(JLON,JLEV-1) &
      &+ZSEDIQL(JLON,JLEV)-ZSEDIQL(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
    ZQIC(JLON,JLEV)=ZQIC(JLON,JLEV)-YDCST%RG*(&
      &+PFPFPCN(JLON,JLEV)-PFPFPCN(JLON,JLEV-1) &
      &+ZSEDIQI(JLON,JLEV)-ZSEDIQI(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
    ZQRC(JLON,JLEV)=ZQRC(JLON,JLEV)-YDCST%RG*(&
      &+PFPLCL(JLON,JLEV)-PFPLCL(JLON,JLEV-1) &
      &-PFPFPCL(JLON,JLEV)+PFPFPCL(JLON,JLEV-1) &
      &+PFPEVPCL(JLON,JLEV)-PFPEVPCL(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
    ZQSC(JLON,JLEV)=ZQSC(JLON,JLEV)-YDCST%RG*(&
      &+PFPLCN(JLON,JLEV)-PFPLCN(JLON,JLEV-1) &
      &-PFPFPCN(JLON,JLEV)+PFPFPCN(JLON,JLEV-1) &
      &+PFPEVPCN(JLON,JLEV)-PFPEVPCN(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
  ENDDO
ENDDO

!-------------------------------------------------
! VERTICAL TRANSPORT OF QLCONV AND QICONV.
!-------------------------------------------------

DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA,KFDIA
    ZUDW(JLON,JLEV)=-PUDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*YDCST%RG)
    ZDDW(JLON,JLEV)=-PDDOM(JLON,JLEV)*PR(JLON,JLEV)*PT(JLON,JLEV)/(PAPRSF(JLON,JLEV)*YDCST%RG)
    ZW_COMPENS(JLON,JLEV)=-(PUDAL(JLON,JLEV)*ZUDW(JLON,JLEV)+PDDAL(JLON,JLEV)*ZDDW(JLON,JLEV)) &
      &/MAX(1.E-4_JPRB,1._JPRB-PUDAL(JLON,JLEV)-PDDAL(JLON,JLEV))
    ZW_CONV(JLON,JLEV)=(PUDAL(JLON,JLEV)*ZUDW(JLON,JLEV)+PDDAL(JLON,JLEV)*ZDDW(JLON,JLEV)) &
      &/MAX(1.E-4_JPRB,PUDAL(JLON,JLEV)+PDDAL(JLON,JLEV))
  ENDDO
ENDDO

! CONVECTIVE TRANSPORT OF CONVECTIVE CONDENSATES IN THE UPDRAFT.
CALL ACADVEC(YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,PDELP,PAPHI,ZQLC,ZW_CONV,PDIFCQLC)
CALL ACADVEC(YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,PDELP,PAPHI,ZQIC,ZW_CONV,PDIFCQIC)

! CONVECTIVE TRANSPORT OF RESOLVED CONDENSATES IN THE ENVIRONMENT.
CALL ACADVEC(YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,PDELP,PAPHI,PQL,ZW_COMPENS,PDIFCQL)
CALL ACADVEC(YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,PDELP,PAPHI,PQI,ZW_COMPENS,PDIFCQI)

! UPDATE PROFILES FROM TRANSPORT FLUXES.
DO JLEV = KTDIA, KLEV
  DO JLON = KIDIA, KFDIA
    ZQLC(JLON,JLEV)=ZQLC(JLON,JLEV)-YDCST%RG*(&
      &+PDIFCQLC(JLON,JLEV)-PDIFCQLC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
    ZQIC(JLON,JLEV)=ZQIC(JLON,JLEV)-YDCST%RG*(&
      &+PDIFCQIC(JLON,JLEV)-PDIFCQIC(JLON,JLEV-1) &
      &)*PRDELP(JLON,JLEV)*TSPHY
  ENDDO
ENDDO

!-------------------------------------------------
! PRODUCE ICING OF CONVECTIVE LIQUID WATER TRANSPORTED BY THE UPDRAFT.
!-------------------------------------------------

DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ZQLCONVPREC=ZQLC(JLON,JLEV)
    ZUDQC(JLON,JLEV)=ZQLC(JLON,JLEV)+ZQIC(JLON,JLEV)
    ZICE=FONICE(PTU(JLON,JLEV),YDPHY0%RDTFAC)
    ZQLC(JLON,JLEV)=ZUDQC(JLON,JLEV)*(1.0_JPRB-ZICE)
    ZQIC(JLON,JLEV)=ZUDQC(JLON,JLEV)*ZICE
    ZTCVIM=(ZQLC(JLON,JLEV)-ZQLCONVPREC)/TSPHY
    PFIMCC(JLON,JLEV)=PFIMCC(JLON,JLEV-1)-PDELP(JLON,JLEV)/YDCST%RG*ZTCVIM
  ENDDO
ENDDO

IF (LCORSEDIM) THEN
  !-------------------------------------------------
  ! ADDITION OF THE SEDIMENTATION FLUX TO THE TRANSPORT FLUX.
  !-------------------------------------------------
  DO JLEV=0,KLEV
    PDIFCQLC(KIDIA:KFDIA,JLEV)=PDIFCQLC(KIDIA:KFDIA,JLEV)+ZSEDIQL(KIDIA:KFDIA,JLEV)
    PDIFCQIC(KIDIA:KFDIA,JLEV)=PDIFCQIC(KIDIA:KFDIA,JLEV)+ZSEDIQI(KIDIA:KFDIA,JLEV)
  ENDDO
ENDIF

!
! CUMULATE DEPTH OF LAYERS WHERE VERTICAL VELOCITY IS GREATER THAN A GIVEN THRESHOLD.
ZDEPTH(:)=0._JPRB
DO JLEV=KTDIA,KLEV
   DO JLON=KIDIA,KFDIA
      ! CONVECTIVE CLOUD DEPTH:
      ! CUMULATE DEPTH OF LAYERS WHERE VERTICAL VELOCITY IS GREATER THAN A GIVEN THRESHOLD.
      ZDEPTH(JLON)=ZDEPTH(JLON)+(PAPHI(JLON,JLEV-1)-PAPHI(JLON,JLEV))/YDCST%RG &
        &*MAX(0._JPRB,SIGN(1._JPRB,ZUDW(JLON,JLEV)-1.0_JPRB))
   ENDDO
ENDDO

!-------------------------------------------------
! The PCMT call above has computed both non-precipitating and precipitating convection fluxes.
! If a separate shallow convection scheme is activated in the current physics,
! one suppresses PCMT fluxes where PCMT convection is non-precipitating.
!-------------------------------------------------
IF(LCVPPKF.OR.LEDKF) THEN
  ! PAIPCMT: Activity Index of PCMT: 1. if PCMT is active, 0. else case.
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PAIPCMT(JLON)=MAX(0._JPRB,SIGN(1._JPRB,ZDEPTH(JLON)-THPCMT))
  ENDDO
!  ! Is PCMT convection precipitating?
!  ZMAX(:)=0._JPRB
!  DO JLEV=KTDIA,KLEV
!    DO JLON=KIDIA,KFDIA
!      ZMAX(JLON)=MAX(ZMAX(JLON),PQRCONV(JLON,JLEV)+PQSCONV(JLON,JLEV))
!    ENDDO
!  ENDDO
!  ! PAIPCMT: Activity Index of PCMT: 1. if PCMT is active, 0. else case.
!  DO JLON=KIDIA,KFDIA
!    PAIPCMT(JLON)=MAX(0._JPRB,SIGN(1._JPRB,ZMAX(JLON)-THPCMT))
!  ENDDO
  ! One applies the PAIPCMT activity index to the PCMT fluxes.
  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      PDIFCQ(JLON,JLEV)=PAIPCMT(JLON)*PDIFCQ(JLON,JLEV)
      PDIFCQL(JLON,JLEV)=PAIPCMT(JLON)*PDIFCQL(JLON,JLEV)
      PDIFCQI(JLON,JLEV)=PAIPCMT(JLON)*PDIFCQI(JLON,JLEV)
      PDIFCQLC(JLON,JLEV)=PAIPCMT(JLON)*PDIFCQLC(JLON,JLEV)
      PDIFCQIC(JLON,JLEV)=PAIPCMT(JLON)*PDIFCQIC(JLON,JLEV)
      PDIFCS(JLON,JLEV)=PAIPCMT(JLON)*PDIFCS(JLON,JLEV)
      PDIFCTH(JLON,JLEV)=PAIPCMT(JLON)*PDIFCTH(JLON,JLEV)
      PFCCQL(JLON,JLEV)=PAIPCMT(JLON)*PFCCQL(JLON,JLEV)
      PFCCQN(JLON,JLEV)=PAIPCMT(JLON)*PFCCQN(JLON,JLEV)
      PSTRCU(JLON,JLEV)=PAIPCMT(JLON)*PSTRCU(JLON,JLEV)
      PSTRCV(JLON,JLEV)=PAIPCMT(JLON)*PSTRCV(JLON,JLEV)
      PSTRCTRA(JLON,JLEV,:)=PAIPCMT(JLON)*PSTRCTRA(JLON,JLEV,:)
      PFIMCC(JLON,JLEV)=PAIPCMT(JLON)*PFIMCC(JLON,JLEV)
      PFPEVPCL(JLON,JLEV)=PAIPCMT(JLON)*PFPEVPCL(JLON,JLEV)
      PFPEVPCN(JLON,JLEV)=PAIPCMT(JLON)*PFPEVPCN(JLON,JLEV)
      PFPLCL(JLON,JLEV)=PAIPCMT(JLON)*PFPLCL(JLON,JLEV)
      PFPLCN(JLON,JLEV)=PAIPCMT(JLON)*PFPLCN(JLON,JLEV)
      PFPFPCL(JLON,JLEV)=PAIPCMT(JLON)*PFPFPCL(JLON,JLEV)
      PFPFPCN(JLON,JLEV)=PAIPCMT(JLON)*PFPFPCN(JLON,JLEV)
      PFEDQLC(JLON,JLEV)=PAIPCMT(JLON)*PFEDQLC(JLON,JLEV)
      PFEDQIC(JLON,JLEV)=PAIPCMT(JLON)*PFEDQIC(JLON,JLEV)
      PFEDQRC(JLON,JLEV)=PAIPCMT(JLON)*PFEDQRC(JLON,JLEV)
      PFEDQSC(JLON,JLEV)=PAIPCMT(JLON)*PFEDQSC(JLON,JLEV)
      PFCNEGQLC(JLON,JLEV)=PAIPCMT(JLON)*PFCNEGQLC(JLON,JLEV)
      PFCNEGQIC(JLON,JLEV)=PAIPCMT(JLON)*PFCNEGQIC(JLON,JLEV)
      PFCNEGQRC(JLON,JLEV)=PAIPCMT(JLON)*PFCNEGQRC(JLON,JLEV)
      PFCNEGQSC(JLON,JLEV)=PAIPCMT(JLON)*PFCNEGQSC(JLON,JLEV)
    ENDDO
  ENDDO
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      PNEBC(JLON,JLEV)=PAIPCMT(JLON)*PNEBC(JLON,JLEV)
      PQLIC(JLON,JLEV)=PAIPCMT(JLON)*PQLIC(JLON,JLEV)
    ENDDO
  ENDDO
ELSE
  ! One has called the PCMT scheme, and no external shallow convection
  ! is activated. In this case, PCMT is expected to produce both
  ! precipitating and non-precipitating convection fluxes,
  ! the activity index PAIPCMT is set to 1..
  ZDEPTH(:)=0._JPRB
  PAIPCMT(:)=1._JPRB
ENDIF

IF(LMUSCLFA.AND.(MOD(NSTEP,NFRSCM)==0)) THEN
   CALL WRSCMR(NMUSCLFA,'KNND',REAL(KNND,JPRB),KLON,1)
   CALL WRSCMR(NMUSCLFA,'KNLAB',REAL(KNLAB,JPRB),KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'AIPCMT',PAIPCMT,KLON,1)
   CALL WRSCMR(NMUSCLFA,'PQLCONV',PQLCONV,KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'PQICONV',PQICONV,KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'PQRCONV',PQRCONV,KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'PQSCONV',PQSCONV,KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'PUDOM',PUDOM,KLON,KLEV)
   CALL WRSCMR(NMUSCLFA,'PDIFCS',PDIFCS,KLON,KLEV)
ENDIF

IF(NCVCLOS == 4) THEN
  !
  !-------------------------------------------------
  ! In case of prognostic surface active fraction, one uses the PUDAL array
  ! to advect ZALF surface active fraction in 3D from one step to the next.
  !-------------------------------------------------
  !
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      PUDAL(JLON,JLEV) = ZALF(JLON)
    ENDDO
  ENDDO
ENDIF

!
!-------------------------------------------------
! PCSGC: resolved condensates located Close to SubGrid Convection: 1. if close, 0. if "far".
!-------------------------------------------------
!
PCSGC(:,:)=1._JPRB
DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    ZFRACP=EXP((PAPRSF(JLON,JLEV)-GCVFNEP1)/GCVFNEP2)
    ZTH=(ZFRACP*ZFRACP-1._JPRB)/(ZFRACP*ZFRACP+1._JPRB)
    PCSGC(JLON,JLEV)=GCVFNER*MAX(0._JPRB,SIGN(1._JPRB,ZDEPTH(JLON)-GCVFNEO)) &
      & *0.5_JPRB*(1._JPRB-ZTH)
  ENDDO
ENDDO
IF(LMUSCLFA) CALL WRSCMR(NMUSCLFA,'PCSGC',PCSGC,KLON,KLEV)
IF(LMUSCLFA) CALL WRSCMR(NMUSCLFA,'PQC_DET_PCMT',PQC_DET_PCMT,KLON,KLEV)

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPCMT',1,ZHOOK_HANDLE)
END SUBROUTINE ACPCMT
