!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACPLUIS ( YDCST, YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPRSF,PDELP,PNEBS,PQ,PQLIS,PQW,PR,PT,&
 ! - OUTPUT 2D .
 & PFCSQL,PFCSQN,PFPLSL,PFPLSN,&
 & PFPEVPL,PFPEVPN,PFPFPL,PFPFPN)

!**** *ACPLUIS * - CALCUL DES FLUX DE PRECIPITATIONS STRATIFORMES.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX DE PRECIPITATIONS STRATIFORMES (LIQUIDE ET
!         NEIGE) SELON UN SCHEMA STATISTIQUE DE PRECIPITATIONS.
!     - COMPUTATION OF STRATIFORM PRECIPITATION FLUXES (WATER AND
!         SNOW) ACCORDING TO A PRECIPITATIONS STATISTIC SCHEME.

!**   Interface.
!     ----------
!        *CALL* *ACPLUIS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (1:KLEV) .

! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PNEBS      : NEBULOSITE STRATIFORME (SCHEMA DE RICARD).
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQLIS      : QUANTITE D'EAU LIQUIDE (SCHEMA DE RICARD).
! PQW        : HUMIDITE SPECIFIQUE DU THERMOMETRE MOUILLE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PFPEVPL    : EVAPORATION DES PRECIPITATIONS LIQUIDES.
! PFPEVPN    : EVAPORATION DES PRECIPITATIONS NEIGEUSES.
! PFPFPL     : GENERATION DES PRECIPITATIONS LIQUIDES.
! PFPFPN     : GENERATION DES PRECIPITATIONS NEIGEUSES.
! PFPLSL     : PRECIPITATIONS STRATIFORMES SOUS FORME LIQUIDE.
! PFPLSN     : PRECIPITATIONS STRATIFORMES SOUS FORME NEIGE.
! PFCSQL     : STRATIFORM CONDENSATION (LIQUID)
! PFCSQN     : STRATIFORM CONDENSATION (SOLID)

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------
!        A SCHEME FOR PREDICTING LAYER CLOUDS AND THEIR WATER CONTENT
!        IN A GENERAL CIRCULATION MODEL BY SMITH (1990).
!        IF THE SPECIFIC HUMIDITY IS TOO WEAK (PQ<QSMIN) A MODIFIED
!        KESSLER'S SCHEME IS USED.
!     Auteurs.
!     -------
!        92-01-17, CHRISTIAN CASTEJON ET ELISABETH GERARD.

!     Modifications.
!     --------------
!        02-10, Flux d'évaporation des precip LCONDWT - J.F. Gueremy
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        04-01, ZINEBGY, ZIWTNEBGY - J.F. Gueremy/P. Marquet
!        04-03, retour ariere sur la modif - J.F. Gueremy/P. Marquet
!               du 00-10 (proportion solide des precipitations...)
!        04-10, Flux de fonte/gel des precip LCONDWT - J.F. Gueremy
!        05-03, Sommes des flux de fonte/gel et d'évap strat et conv
!               effectuées dans aplpar - J.F. Gueremy
!        05-03, Change due to Lopez microphysics - F. Bouyssel
!        06-07, Mise en conformite des flux pour la nouv interface
!               PFPEVP (evap) et PFPFP(generation)
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        2011-06: M. Jerczynski - some cleaning to meet norms
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEBS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLIS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQW(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPEVPL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPEVPN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPFPL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPFPN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN(KLON,0:KLEV)

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZNEIGE(KLON),ZPLH(KLON),ZRME(KLON)
REAL(KIND=JPRB) :: ZFONTE(KLON,KLEV)

INTEGER(KIND=JPIM) :: JLEV, JLON

REAL(KIND=JPRB) :: ZCLTEND, ZDELTAQ, ZEPS, ZEPS1, ZEPS2, ZEVA,&
 & ZFON, ZFONS, ZFONT, ZGENE, ZGENN, ZH, ZHE, ZIFNSAT, &
 & ZIFSAT, ZINDQ, ZNEBS, ZNUTEND, ZPLB, ZPLBK, &
 & ZPLBS, ZPLC, ZPLS, ZQL, ZQLL, ZQLI, ZRPTB, ZRPTH, ZTENDCL, &
 & ZTENDI, ZTENDL, ZTENDNU, ZTENDT, ZTVF, ZZA, ZZB, &
 & ZZC,ZINEBGY,ZIWTNEBGY  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fctdoi.ycst.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACPLUIS',0,ZHOOK_HANDLE)
ASSOCIATE(QSMIN=>YDML_PHY_MF%YRPHY0%QSMIN, TVF=>YDML_PHY_MF%YRPHY0%TVF, RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, &
 & TCW=>YDML_PHY_MF%YRPHY0%TCW, EVAP=>YDML_PHY_MF%YRPHY0%EVAP, REVGSL=>YDML_PHY_MF%YRPHY0%REVGSL, &
 & FONT=>YDML_PHY_MF%YRPHY0%FONT, &
 & TCT=>YDML_PHY_MF%YRPHY0%TCT, TCA=>YDML_PHY_MF%YRPHY0%TCA, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LCONDWT=>YDML_PHY_MF%YRPHY%LCONDWT, LNEBGY=>YDML_PHY_MF%YRPHY%LNEBGY, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, &
 & YDPHY0=>YDML_PHY_MF%YRPHY0)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CONSTANTES DE SECURITE.

!         SECURITY CONSTANTS.

ZEPS=1.E-5_JPRB
ZEPS1=1.E-10_JPRB
ZEPS2=1.E-5_JPRB

IF (LNEBGY) THEN
  ZINEBGY=1.0_JPRB
ELSE
  ZINEBGY=0.0_JPRB
ENDIF

IF (LCONDWT.AND.LNEBGY) THEN
  ZIWTNEBGY=1.0_JPRB
ELSE
  ZIWTNEBGY=0.0_JPRB
ENDIF

!*
!     ------------------------------------------------------------------
!     II - DEBUT DES BOUCLES VERTICALE ET HORIZONTALE OU L'INFORMATION
!     SE TRANSMET DE COUCHE EN COUCHE, DU HAUT VERS LE BAS.
!     MISES A ZERO DU FLUX DE PRECIPITATIONS ET DE LA PROPORTION DE
!     NEIGE AU SOMMET.

!          BEGINNING OF THE VERTICAL AND HORIZONTAL LOOPS WHERE THE
!     INFORMATION IS PASSED FROM LAYER TO LAYER DOWNWARDS.
!     INITIALISATION OF THE PRECIPITATION FLUX AND THE SNOW PROPORTION
!     AT THE TOP.

! - TEMPORAIRES 1D .

! ZNEIGE    : PROPORTION DU FLUX DE PRECIPITATIONS SOUS FORME NEIGE.
!            : SNOW-TYPE PROPORTION OF THE PRECIPITATION FLUX.
! ZPLH      : FLUX TOTAL DE PRECIPITATIONS AU SOMMET DE LA COUCHE.
!            : TOTAL PRECIPITATION FLUX AT THE TOP OF THE LAYER.
! ZRME      : MEME TYPE DE VARIABLE QUE ZNEIGE; INTEGRE EN PLUS UNE
!              TRANSITION DE PHASE CONTINUE.
!            : SAME TYPE OF VARIABLE AS ZNEIGE; ON TOP OF THAT IT
!              INTEGRATES A CONTINUOUS TRANSITION BETWEEN ICE AND
!              LIQUID WATER.

DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

    IF (JLEV == KTDIA) THEN
      ZPLH(JLON)=0.0_JPRB
      ZNEIGE(JLON)=0.0_JPRB
      ZRME(JLON)=0.0_JPRB
    ENDIF

    IF (LNEIGE) THEN
      ZH=MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-PT(JLON,JLEV)))
    ELSE
      ZH=0.0_JPRB
    ENDIF
!*
!     ------------------------------------------------------------------
!     III - TRAITEMENT DE LA PARTIE NON SATUREE DE LA MAILLE -
!     PROCESSUS D'EVAPORATION.

!           PROCESSING OF THE NON SATURATED PART OF THE GRID -
!     EVAPORATION PROCESS.

    ZNEBS=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-PNEBS(JLON,JLEV)-ZEPS))
    ZIFNSAT=ZNEBS*(1.0_JPRB-PNEBS(JLON,JLEV))+(1.0_JPRB-ZNEBS)*ZEPS
    ZIFNSAT=1.0_JPRB/ZIFNSAT
    ZDELTAQ=PQW(JLON,JLEV)-PQ(JLON,JLEV)
    ZEVA=EVAP*(1.0_JPRB-ZRME(JLON)*(1.0_JPRB-REVGSL))
    ZPLC=SQRT(ZPLH(JLON))-ZEVA*MAX(ZDELTAQ,0.0_JPRB)*PDELP(JLON,JLEV)&
     & /PAPRSF(JLON,JLEV)**2  
    ZPLC=MAX(ZPLC,0.0_JPRB)
    ZPLC=ZPLC**2
    ZCLTEND=(ZPLC-ZPLH(JLON))*YDCST%RG/PDELP(JLON,JLEV)
    ZTENDCL=ZCLTEND*(1.0_JPRB-PNEBS(JLON,JLEV))
    ZTENDCL=-MIN(ABS(ZDELTAQ/TSPHY),-ZTENDCL)
    PFPEVPL(JLON,JLEV)=ZTENDCL*PDELP(JLON,JLEV)/YDCST%RG
    PFPEVPN(JLON,JLEV)=ZIWTNEBGY*(PFPEVPN(JLON,JLEV-1)&
     & +ZH*PFPEVPL(JLON,JLEV))
    PFPEVPL(JLON,JLEV)=ZIWTNEBGY*(PFPEVPL(JLON,JLEV-1)&
     & +(1.0_JPRB-ZH)*PFPEVPL(JLON,JLEV))
    ZCLTEND=ZTENDCL*ZIFNSAT
    ZPLC=MAX(0.0_JPRB,ZCLTEND*PDELP(JLON,JLEV)/YDCST%RG+ZPLH(JLON))

!*
!     ------------------------------------------------------------------
!     IV - TRAITEMENT DE LA PARTIE SATUREE DE LA MAILLE -
!     PROCESSUS DE CONDENSATION.

!          PROCESSING OF THE SATURATED PART OF THE GRID -
!     CONDENSATION PROCESS.

!   PHASE LIQUIDE.
!   LIQUID PHASE.

    ZIFSAT=1.0_JPRB/PNEBS(JLON,JLEV)
    ZNEBS=MAX(0.0_JPRB,SIGN(1.0_JPRB,PNEBS(JLON,JLEV)-ZEPS))
    ZQL=ZNEBS*PQLIS(JLON,JLEV)*ZIFSAT
    ZQLL=ZQL*(1.0_JPRB-ZH)
    ZZA=TCT*MAX(0.0_JPRB,1.0_JPRB-EXP(-ZQLL**2/TCW**2)) + TCA*ZPLH(JLON)
    ZTENDL=ZZA*ZQLL/(1.0_JPRB+TSPHY*ZZA)

!   PHASE GLACE.
!   ICE PHASE.

    IF (LNEIGE) THEN
      ZQLI=ZQL*ZH
      ZTVF=TVF*( ZINEBGY*(ZQLI*1000._JPRB)**0.2_JPRB&
       & +(1.0_JPRB-ZINEBGY) )  
      ZZB=YDCST%RG/PDELP(JLON,JLEV)*ZPLH(JLON)
      ZZC=YDCST%RG/PDELP(JLON,JLEV)*PAPRSF(JLON,JLEV)*ZTVF/PR(JLON,JLEV)&
       & / PT(JLON,JLEV)  
      ZTENDI=-MIN(0.0_JPRB,ZZB-ZZC*ZQLI)/(1.0_JPRB+TSPHY*ZZC)
    ELSE
      ZTENDI=0.0_JPRB
    ENDIF

! - TENDANCE D'HUMIDITE DUE A LA CONDENSATION.

    ZNUTEND=ZTENDI+ZTENDL
    ZTENDNU=ZNUTEND*PNEBS(JLON,JLEV)
    ZTENDNU=ZINEBGY*ZTENDNU+&
     & (1.0_JPRB-ZINEBGY)*MAX(0.0_JPRB,MIN(ZTENDNU,PQ(JLON,JLEV)/(2.0_JPRB*TSPHY)))  
    ZNUTEND=ZTENDNU*ZIFSAT
    ZPLS=ZNUTEND*PDELP(JLON,JLEV)/YDCST%RG+ZPLH(JLON)

!*
!     ------------------------------------------------------------------
!     V - CALCUL DU FLUX TOTAL DE PRECIPITATIONS A LA BASE DE LA COUCHE.

!         COMPUTATION OF THE TOTAL PRECIPITATION FLUX AT THE BOTTOM OF
!     THE LAYER.

! - SMITH'S SCHEME IF PQ(JLON,JLEV)>QSMIN.

    ZPLBS = PNEBS(JLON,JLEV)*ZPLS + (1.0_JPRB-PNEBS(JLON,JLEV))*ZPLC

! - KESSLER'S SCHEME IF PQ(JLON,JLEV)<QSMIN.

    ZDELTAQ=PQW(JLON,JLEV)-PQ(JLON,JLEV)
    ZPLBK=MAX(0.0_JPRB,ZPLH(JLON)-ZDELTAQ*PDELP(JLON,JLEV)/(YDCST%RG*TSPHY))
    ZRPTH=SQRT(MAX(ZPLH(JLON),ZPLBK))
    ZRPTB=MAX(0.0_JPRB,ZRPTH-ZEVA*MAX(0.0_JPRB,ZDELTAQ)*PDELP(JLON,JLEV)&
     & /PAPRSF(JLON,JLEV)**2)  
    ZPLBK=MAX(ZPLBK,ZRPTB**2)

! - FLUX TOTAL DE PRECIPITATIONS A LA BASE DE LA COUCHE.

    ZINDQ=MAX(0.0_JPRB,SIGN(1.0_JPRB,PQ(JLON,JLEV)-QSMIN))
!    ZPLB = ZPLBS*ZINDQ + ZPLBK*(_ONE_-ZINDQ)
    ZPLB = ZPLBS

!*
!     ------------------------------------------------------------------
!     VI - CALCULS DE CHANGEMENT DE LA PROPORTION NEIGEUSE (NOUVELLES
!     PRECIPITATIONS GENEREES ET FONTE OU GEL) DANS L'OPTION "NEIGE".

!          COMPUTATIONS OF THE CHANGE IN THE SNOW PROPORTION (NEWLY
!     GENERATED PRECIPITATIONS AND MELTING OR FREEZING IN THE SNOW".
!     OPTION "NEIGE").

    IF (LNEIGE) THEN
      ZGENN=(ZNEIGE(JLON)*ZPLH(JLON)&
       & +ZH*ZTENDNU*PDELP(JLON,JLEV)/YDCST%RG)&
       & /MAX(ZEPS1,(ZPLH(JLON)+ZTENDNU*PDELP(JLON,JLEV)/YDCST%RG))  
      ZHE=FONICE(PT(JLON,JLEV),YDPHY0%RDTFAC)
      ZGENE=(ZRME(JLON)*ZPLH(JLON)&
       & +ZHE*ZTENDNU*PDELP(JLON,JLEV)/YDCST%RG)&
       & /MAX(ZEPS1,(ZPLH(JLON)+ZTENDNU*PDELP(JLON,JLEV)/YDCST%RG))  
      ZFONT=FONT*(1.0_JPRB-ZRME(JLON)*(1.0_JPRB-REVGSL))
      ZFON=-2.0_JPRB*ZFONT*(PT(JLON,JLEV)-YDCST%RTT)*PDELP(JLON,JLEV)&
       & /PAPRSF(JLON,JLEV)**2&
       & / MAX(ZEPS2,SQRT(ZPLB)+SQRT(ZPLH(JLON)))  
      ZFONS=MAX(0.0_JPRB,MIN(1.0_JPRB,PNEBS(JLON,JLEV)*ZGENN&
       & +(1.0_JPRB-PNEBS(JLON,JLEV))*ZNEIGE(JLON)))  
      ZNEIGE(JLON)=MAX(0.0_JPRB,MIN(1.0_JPRB,PNEBS(JLON,JLEV)*ZGENN&
       & +(1.0_JPRB-PNEBS(JLON,JLEV))*ZNEIGE(JLON)+ZFON))  
      ZFONS=ZNEIGE(JLON)-ZFONS
      ZRME(JLON)=MAX(0.0_JPRB,MIN(1.0_JPRB,PNEBS(JLON,JLEV)*ZGENE&
       & +(1.0_JPRB-PNEBS(JLON,JLEV))*ZRME(JLON)+ZFON))  
    ELSE
      ZNEIGE(JLON)=0.0_JPRB
      ZRME(JLON)=0.0_JPRB
      ZFONS=0.0_JPRB
    ENDIF

!*
!     ------------------------------------------------------------------
!     VII - CALCUL DES FLUX DE PRECIPITATIONS A CHAQUE NIVEAU, SOUS
!     FORME LIQUIDE OU NEIGEUSE.

!           COMPUTATION OF THE PRECIPITATION FLUXES AT EACH LEVEL
!     (WATER AND SNOW).

    ZTENDT=ZTENDNU+ZTENDCL
    ZPLH(JLON)=MAX(0.0_JPRB,ZPLH(JLON)+ZTENDT*PDELP(JLON,JLEV)/YDCST%RG)
    PFPLSL(JLON,JLEV)=(1.0_JPRB-ZNEIGE(JLON))*ZPLH(JLON)
    PFPLSN(JLON,JLEV)=ZNEIGE(JLON)*ZPLH(JLON)
    PFPFPL(JLON,JLEV)=PFPLSL(JLON,JLEV)-PFPEVPL(JLON,JLEV)
    PFPFPN(JLON,JLEV)=PFPLSN(JLON,JLEV)-PFPEVPN(JLON,JLEV)
    PFCSQL(JLON,JLEV)=PFPLSL(JLON,JLEV)
    PFCSQN(JLON,JLEV)=PFPLSN(JLON,JLEV)
!     Flux de fonte
    ZFONTE(JLON,JLEV)=ZIWTNEBGY*ZFONS*ZPLH(JLON)

  ENDDO
ENDDO

!!!   le flux d'evaporation comprend le flux de fonte
!!!   changement de signe des flux PFPEVPL/N (temporaire)
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    PFPEVPL(JLON,JLEV)=-(PFPEVPL(JLON,JLEV)-ZFONTE(JLON,JLEV))
    PFPEVPN(JLON,JLEV)=-(PFPEVPN(JLON,JLEV)+ZFONTE(JLON,JLEV))
  ENDDO
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPLUIS',1,ZHOOK_HANDLE)
END SUBROUTINE ACPLUIS
