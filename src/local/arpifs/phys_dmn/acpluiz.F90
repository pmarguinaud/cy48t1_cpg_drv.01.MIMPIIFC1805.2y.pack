!-----------------------------------------------------------------------
SUBROUTINE ACPLUIZ( YDCST, YDML_PHY_MF,KIDIA, KFDIA, KLON, KTDIA, KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT -
 & PT, PQ, PQL, PQI, PQR, PQS,&
 & PDELP, PAPRSF, PCP, PR, PNEBS, PQCS, PNEB_CVPP, PQLI_CVPP,PQC_DET_PCMT,&
 & PTENDH, PTENDQ, LDADJCLD, PAPHI,&
 & PTS, PNEIJ, PLSM, PGM, PVETAF,&
 ! - OUTPUT -
 & PFCSQL, PFCSQN, PFPLSL, PFPLSN,&
 & PFPEVPL, PFPEVPN, PFPFPL, PFPFPN, PSEDIQL, PSEDIQN )

!**** *ACPLUIZ * - PROGNOSTIC CLOUD SCMEME (LOPEZ)

!**   Interface.
!     ----------
!        *CALL* *ACPLUIZ*

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE./INPUT ARGUMENTS.
!     ------------------------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
!            : START OF HORIZONTAL LOOP 
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
!            : END OF HORIZONTAL LOOP
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
!            : HORIZONTAL DIMENSION  
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES. 
!            : START OF THE VERTICAL LOOP IN THE PHYSICS. 
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
!            : END OF VERTICAL LOOP AND VERTICAL DIMENSION

! - 2D (1:KLEV)

! PT         : TEMPERATURE.
!            : TEMPERATURE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
!            : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQL        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE LIQUIDE.
!            : SPECIFIC HUMIDITY OF LIQUID CONDENSATED WATER.
! PQI        : HUMIDITE SPECIFIQUE DE L'EAU CONDENSEE GLACE.
!            : SPECIFIC HUMIDITY OF SOLID CONDENSATED WATER.
! PQR        : PRECIPITATIONS LIQUIDES.
!            : LIQUID PECIPITATIONS.
! PQS        : PRECIPITATIONS SOLIDES.
!            : SOLID PECIPITATIONS.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
!            : LAYER THICKNESS IN PRESSURE UNITS.  
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
!            : PRESSURE ON FULL LEVELS.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
!            : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
!            : GAS CONSTANT FOR AIR.
! PNEBS      : NEBULOSITE PARTIELLE STRATIFORME.
!            : STRATIFORM FRACTIONAL CLOUDINESS.
! PQCS       : CONTENU "STRATIFORME" EN CONDENSAT NUAGEUX (LIQ. + SOL.).
!            : STRATIRORM CLOUD WATER (LIQUID + SOLID).
! PNEB_CVPP  : NEBULOSITE PARTIELLE CONVECTION PEU PROFONDE.
!            : SHALLOW CONVECTION FRACTIONAL CLOUDINESS.
! PQLI_CVPP  : CONTENU "CVPP" EN CONDENSAT NUAGEUX (LIQ. + SOL.).
!            : SHALLOW CONVECTION CLOUD WATER (LIQUID + SOLID).
! PQC_DET_PCMT: CONTENU EN CONDENSAT NUAGEUX DETRAINE PAR PCMT (LIQ. + SOL.).
!            : DEEP CONVECTION DETRAINED CLOUD WATER (LIQUID + SOLID).
! PTENDH     : TENDANCE D'ENTHALPIE.
!            : ENTHALPY TENDENCY.
! PTENDQ     : TENDANCE D'HUMIDITE SPECIFIQUE.
!            : SPECIFIC HUMIDITY TENDENCY.

! - 2D (0:KLEV)

! PAPHI      : GEOPOTENTIEL SUR DEMI-NIVEAUX.
!            : GEOPOTENTIAL ON HALF-LEVELS.

! - 1D (1:KLON) .
                                                                                
! PTS        : TEMPERATURE DE SURFACE.
!            : SURFACE TEMPERATURE.
! PNEIJ      : PROPORTION DE LA MAILLE RECOUVERTE DE NEIGE.
!            : SNOW FRACTION.
! PLSM       : INDICE TERRE/MER.
!            : LAND/SEA MASK.
! PGM        : FACTEUR D'ECHELLE.
!            : MAPPING FACTOR.
! PVETAF     : COORDONNEE VERTICALE ETA.
!            : VERTICAL COORDINATE ETA.

!-----------------------------------------------------------------------

! -   ARGUMENTS EN SORTIE.
!     ---------------------------

! - 2D (0:KLEV) .

! PFCSQN     : FLUX DE CONDENSATION LIE AUX RR STRATIFORMES NEIGEUSES.
!            : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL     : FLUX DE CONDENSATION LIE AUX RR STRATIFORMES LIQUIDES.
!            : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PFPLSL     : FLUX DE PRECIPITATION LIQUIDE (PLUIE).
!            : RAIN FLUX.
! PFPLSN     : FLUX DE PRECIPITATION SOLIDE  (NEIGE).
!            : ICE PRECIPITATION FLUX.
! PFPEVPL    : FLUX ASSOCIE A L'EVAPORATION DES PRECIP.
!            : FLUX ASSOCIATED TO EVAPORATION OF PRECIPITATIONS.
! PFPEVPN    : FLUX ASSOCIE A LA SUBLIMATION DES PRECIP.
!            : FLUX ASSOCIATED TO SUBLIMATION OF PRECIPITATIONS.
! PFPFPL     : FLUX DE GENERATION DE PRECIPITATIONS LIQUIDES.
!            : FLUX OF LIQUID PRECIPITATION GENERATION.
! PFPFPN     : FLUX DE GENERATION DE PRECIPITATIONS SOLIDES.
!            : FLUX OF SOLID PRECIPITATION GENERATION.
! PSEDIQL    : FLUX SEDIMENTATION D'EAU LIQUIDE NUAGEUSE.
!            : FLUX SEDIMENTATION OF CLOUD LIQUID WATER.
! PSEDIQN    : FLUX SEDIMENTATION D'EAU SOLIDE NUAGEUSE.
!            : FLUX SEDIMENTATION OF CLOUD SOLID WATER.

!-----------------------------------------------------------------------

!     Auteur.
!     -------
!         04-10, Francois Bouyssel

!     Modifications.
!     --------------
!         05-05, F.Bouyssel : Introduction of vertical coordinate eta
!         05-07, F.Bouyssel : New arguments from aplpar to acmicro
!         05-11, F.Bouyssel : Remove ZAUTO,ZQCMIC,ZQPMIC
!         06-01, F.Bouyssel : Introduction of PQS, PFPEVPL, PFPEVPN
!         06-01, F.Bouyssel : Microphysical adjustment at the beginning
!         06-04, F.Bouyssel : Key for Smith's adjustment LADJCLD 
!         06-06, Y.Bouteloup: Statistical sedimentation (ADVPRCS)
!         09-10, F.Bouyssel : Cleaning ADVPRC and ACNEBSM
!         10-04, Y.Bouteloup: No rain production by autoconversion in
!                             case of negative temperature
!         10-07, F.Bouyssel : Bug correction related with ZDPSGDT
!         10-12, F.Bouyssel : Introduction of LADJCLD
!         11-01, F.Bouyssel : Bug correction in case of negative temperature
!       2011-06, M. Jerczynski : some cleaning to meet norms
!       2014-04, JM Piriou : add detrained condensate from PCMT convection
!       2016-06, Y. Bouteloup : adjustment done as a function of a dummy logical argument, instead of global.

!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1             , ONLY : JPIM     ,JPRB
USE YOMHOOK              , ONLY : LHOOK,   DR_HOOK

USE YOMCST               , ONLY : TCST
USE YOMLSFORC            , ONLY : LMUSCLFA,NMUSCLFA

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT     (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ     (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQR    (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP  (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP    (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR     (KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEBS  (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQCS   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEB_CVPP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLI_CVPP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQC_DET_PCMT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDH (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTENDQ (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI  (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS    (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ  (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM   (KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM   (KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVETAF(KLEV)

LOGICAL           ,INTENT(IN)    :: LDADJCLD

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQL (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQN (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPL(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPEVPN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPL (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPFPN (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSEDIQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSEDIQN(KLON,0:KLEV) 

REAL(KIND=JPRB) :: ZRHCRI (KLON,KLEV)
REAL(KIND=JPRB) :: ZRMF   (KLON,KLEV)
REAL(KIND=JPRB) :: ZQSATS (KLON,KLEV)
REAL(KIND=JPRB) :: ZT     (KLON,KLEV)
REAL(KIND=JPRB) :: ZQ     (KLON,KLEV)
REAL(KIND=JPRB) :: ZAUTOL (KLON,KLEV)
REAL(KIND=JPRB) :: ZAUTOI (KLON,KLEV)
REAL(KIND=JPRB) :: ZQL    (KLON,KLEV)
REAL(KIND=JPRB) :: ZQI    (KLON,KLEV)

REAL(KIND=JPRB) :: ZFLUXCOR(KLON,KLEV)

REAL(KIND=JPRB) :: ZDPSGDT,ZICE,ZGDT,ZGDTI,ZIGEL,ZEPS1
INTEGER(KIND=JPIM) :: JLON,JLEV
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "acmicro.intfb.h"
#include "acnebsm.intfb.h"
#include "advprcs.intfb.h"
#include "wrscmr.intfb.h"

#include "fctdoi.ycst.h"

!     ------------------------------------------------------------------

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACPLUIZ',0,ZHOOK_HANDLE)
ASSOCIATE(RDTFAC=>YDML_PHY_MF%YRPHY0%RDTFAC, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY,YDPHY0=>YDML_PHY_MF%YRPHY0)
!     ------------------------------------------------------------------

! - - - - - - - -
! ADJUSTMENT
! - - - - - - - -

ZEPS1=1.E-12_JPRB

IF ( LDADJCLD ) THEN

  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ZT(JLON,JLEV) = PT(JLON,JLEV)+TSPHY*PTENDH(JLON,JLEV)/PCP(JLON,JLEV)
      ZQ(JLON,JLEV) = MAX( 0.0_JPRB,PQ(JLON,JLEV)+TSPHY*PTENDQ(JLON,JLEV) )
    ENDDO
  ENDDO

  CALL ACNEBSM(YDCST,YDML_PHY_MF%YRPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
               & ZT,ZQ,PQL,PQI,PAPHI,PAPRSF,PCP,PR,&
               & PGM,PVETAF,&
               & PQCS,PNEBS,ZRHCRI,ZRMF,ZQSATS)

ELSE

  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      ZT(JLON,JLEV) = PT(JLON,JLEV)
      ZQ(JLON,JLEV) = PQ(JLON,JLEV)
    ENDDO
  ENDDO

ENDIF

! CLOUD OVERLAP FOR STRATIFORM AND SHALLOW CLOUDS
! -----------------------------------------------
                                                                                
IF(LMUSCLFA) CALL WRSCMR(NMUSCLFA,'PQCS_INPUT',PQCS,KLON,KLEV)
DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ZICE = FONICE(ZT(JLON,JLEV),YDPHY0%RDTFAC)
    PQCS (JLON,JLEV) = PQCS(JLON,JLEV) + PQLI_CVPP(JLON,JLEV) + PQC_DET_PCMT(JLON,JLEV)
    PNEBS(JLON,JLEV) = MIN(1.0_JPRB-ZEPS1,MAX(PNEBS(JLON,JLEV),ZEPS1))
    PNEBS(JLON,JLEV) = MAX( PNEBS(JLON,JLEV) , PNEB_CVPP(JLON,JLEV) )
    ZQL  (JLON,JLEV) = PQCS(JLON,JLEV)*(1.0_JPRB-ZICE)
    ZQI  (JLON,JLEV) = PQCS(JLON,JLEV)*ZICE
  ENDDO
ENDDO
IF(LMUSCLFA) CALL WRSCMR(NMUSCLFA,'PQCS',PQCS,KLON,KLEV)
IF(LMUSCLFA) CALL WRSCMR(NMUSCLFA,'PQLI_CVPP',PQLI_CVPP,KLON,KLEV)

! - - - - - - - -
! AUTOCONVERSION
! - - - - - - - -

CALL ACMICRO (YDCST, YDML_PHY_MF, KIDIA, KFDIA, KLON, KTDIA, KLEV,&
             & PNEBS, ZT, ZQL, ZQI, PTS, PNEIJ, PLSM,&
             & ZAUTOL, ZAUTOI )

! - - - - - - - - - - - - - - - - - -
! In case of negative temperature 
! no rain production by autoconversion 
! - - - - - - - - - - - - - - - - - -

ZGDT=YDCST%RG*TSPHY
ZGDTI=1.0_JPRB/ZGDT

DO JLEV = KTDIA, KLEV
!DEC$ IVDEP
  DO JLON = KIDIA, KFDIA
    ZDPSGDT  = ZGDTI * PDELP(JLON,JLEV)
    ZIGEL = MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-PT(JLON,JLEV)))
    ZFLUXCOR(JLON,JLEV) = ZIGEL*ZAUTOL(JLON,JLEV)
    ZAUTOL(JLON,JLEV) = ZAUTOL(JLON,JLEV) - ZFLUXCOR(JLON,JLEV)
    ZAUTOI(JLON,JLEV) = ZAUTOI(JLON,JLEV) + ZFLUXCOR(JLON,JLEV)
    ZQL(JLON,JLEV) = ZQL(JLON,JLEV) - ZFLUXCOR(JLON,JLEV)*TSPHY
    ZQI(JLON,JLEV) = ZQI(JLON,JLEV) + ZFLUXCOR(JLON,JLEV)*TSPHY
    PFCSQL(JLON,JLEV) = PFCSQL(JLON,JLEV-1) &
     & + ( ZQL(JLON,JLEV) - PQL(JLON,JLEV) ) * ZDPSGDT
    PFCSQN(JLON,JLEV) = PFCSQN(JLON,JLEV-1) &
     & + ( ZQI(JLON,JLEV) - PQI(JLON,JLEV) ) * ZDPSGDT
  ENDDO
ENDDO

! - - - - - - - - - - - - - - - - - - -
! FALLING OF PRECIPITATING PARTICLES,
! COLLECTION AND EVAPORATION PROCESSES.
! - - - - - - - - - - - - - - - - - - -

CALL ADVPRCS (YDCST, YDML_PHY_MF, KIDIA, KFDIA, KLON, KTDIA, KLEV,&
             & ZT, ZQ, ZQL, ZQI, ZAUTOL, ZAUTOI,&
             & PQR, PQS, PNEBS,&
             & PCP, PR, PAPHI, PAPRSF, PDELP,&
             & PFPLSL , PFPLSN, PFPEVPL, PFPEVPN, PFPFPL, PFPFPN,&
             & PSEDIQL, PSEDIQN )

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPLUIZ',1,ZHOOK_HANDLE)
END SUBROUTINE ACPLUIZ
