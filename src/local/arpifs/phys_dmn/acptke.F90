!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACPTKE(YGFL,YDLDDH,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,KSTEP,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PDELP,PRDELP,PKNROV,&
 & PT,PU,PV,PALPH,&
 & PAPRS,PAPRSF,PR,PTKE,PLMU,PLNPR,PF_EPS,PFUN_TTE,&
 & PMRIPP,PMRIFPP,PRHS,&
 & PMN2_ES,PMN2_EQ,PRRCOR,PDIFTQ,PDIFTS,PSHEAR,&
 !- INPUT 1D
 & PCD,PGZ0,&
 ! - INPUT/OUTPUT 2D
 & PKUROV,PKTROV,PXPTKEROV,&
 & PLMLTILD,PMXL,PLML,&
 & PTH_FUN,PWW_FUN,&
 ! - OUTPUT 2D .
 & PTENDPTKE,PTTE,PFTCNS)


!**** *ACPTKE * - Specific p-TKE computation.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCULS DE LA P-TKE. 

!**   Interface.
!     ----------
!        *CALL* *ACPTKE*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KSTEP      : NUMERO DU PAS DE TEMPS.                       

! - NOM DES VARIABLES DE LA PHYSIQUE 
! * INPUT

! - 2D (1:KLEV) .
! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES. 
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE. 
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.


! - 2D (0:KLEV) .
! PKNROV     : COEFFICIENT D'ECHANGE VERTICAL NEUTRE (U,V) EN KG/(M*M*S). 

! - 2D (1:KLEV) .
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATICS).

! - 2D (0:KLEV) .
! PAPRS      : HALF LEVEL PRESSURE
! PLMU       : MIXING LENGTH FOR MOMENTUM
! PMRIFPP    : moist Rif
! PRSH       : SOURCE TERM FROM 3D TURBULENCE FOR TKE SOLVER
! PF_EPS     : TERM ASOCIATED WITH MIX. LENGTH CONVERSION L-lm: F_epsilon
! PFUN_TTE   : Function used for computation of tte_tilde in case of LCOEFK_FLX=.FALSE.

! - 2D (1:KLEV) .
! PR         : CONSTANTE DES GAZ POUR L'AIR. 
! PTKE       : TURBULENT KINETIC ENERGY
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (FOR HYDROSTATICS).

! - 1D (DIAGNOSTIQUE) .
! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.

! * INPUT/OUTPUT ARGUMENTS

! - 2D (0:KLEV) .
! PKUROV     : COEFFICIENT D'ECHANGE VERTICAL DE U ET V EN KG/(M*M*S).
! PKTROV     : COEFFICIENT D'ECHANGE VERTICAL DE T ET Q EN KG/(M*M*S).
! PWW_FUN    : F_ww - stability functions for TOMs par.
! PTH_FUN    : T_h - stability functions for TOMs par.
! PXPTKEROV  : "ANTI-FIBRILLATION" COEFFICIENT FOR THE TKE AUTO-DIFFUSION
! PTTE       : TOTAL TURBULENT ENERGY

! * OUTPUT
 
! - 2D (1:KLEV) .
! PTENDPTKE  : TKE INCREMENT (USED BY PSEUDO-PROGNOSTIC TKE SCHEME)
! - 2D (0:KLEV) .
! PFTCNS     : Terms from prognostic equations for TKE and TTE for DDH.
! Meaning of last (3rd) index:
!   1: FTKESHRPROD  -  shear production term (TKE)
!   2: FTKEBUOYPROD -  buoyancy production/destruction term (TKE)
!   3: FTKEDISSIP   -  dissipation term (TKE)
!   4: FTKETRANSP   -  turbulent transport, i.e. diffusion of "diffusion" (TKE)
!   5: FTTEDISSIP   -  dissipation term (TTE)
!   6: FTTETRANSP   -  turbulent transport, i.e. diffusion of "diffusion" (TTE)
!   Term 1 is common for both equations.
!-----------------------------------------------------------------------
!     Auteur.
!     -------
!        2008-02, R. Brozkova (moving relevant part from ACDIFUS)

!     Modifications.
!     --------------
!      F. Vana  01-Sep-2008: all pTKE specific stuff collected here.
!      F. Vana  21-Jan-2009: bug fix of PGZ0 + new stabilization of solver
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      I. Bastak Duran 07-2012: pTKE solver moved to ACPTKES
!      I. Bastak Duran 10-2014: further rewriting
!      M. Hrastinski Sep-2019: isolating terms of TKE and TTE prognostic 
!                              equations for ALARO DDH (PFTCNS)
!      J. Masek Sep-2019: modified call to ACPTKES
!      J. Masek, I. Bastak Duran Oct-2020: cleaned LCOEFK_RIH, introduced
!         ACRIF2PR, treatment of TTE induced oscillations (ETKE_DELTA)
! End modifications
!-------------------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG
USE YOM_YGFL , ONLY : TYPE_GFLD
USE YOMLDDH  , ONLY : TLDDH

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
TYPE(TLDDH)       ,INTENT(IN)    :: YDLDDH
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 

REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKNROV(KLON,0:KLEV)  
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTKE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMU(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PF_EPS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFUN_TTE(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMRIFPP  (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMRIPP  (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_ES   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMN2_EQ   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRHS(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCD(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0(KLON)  
 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKUROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKTROV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PWW_FUN   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTH_FUN   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN) :: PRRCOR   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXPTKEROV(KLON,0:KLEV)

REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDPTKE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMLTILD(KLON,0:KLEV) ! 'STATIC' TKE type mixing length
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLML(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMXL(KLON,KLEV)

!they are actually on half levels
REAL(KIND=JPRB)   ,INTENT(IN) :: PSHEAR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN) :: PDIFTS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN) :: PDIFTQ(KLON,KLEV) 

REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTTE(KLON,KLEV) 

! Terms from prognostic TKE and TTE equations for DDH
REAL(KIND=JPRB) ,INTENT(OUT) :: PFTCNS(KLON,0:KLEV,6)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV,JLON,ITTYPE

REAL(KIND=JPRB) :: ZALTKE,ZLIMTKE,ZNU3TKE

REAL(KIND=JPRB) :: ZRTI,ZTKES
REAL(KIND=JPRB) :: ZRLM_L(KLON,0:KLEV),ZLIMTKE_L,&
& ZKERV_L(KLON,0:KLEV),ZTAUITKE_L(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZEPSKRV,ZRELAXB,ZDRELAXB
REAL(KIND=JPRB) :: ZCP,ZUMCP
REAL(KIND=JPRB) :: ZTKETILD0,ZTTETILD0

REAL(KIND=JPRB) :: ZCONVERT(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTKETILD(KLON,0:KLEV),ZTAUITKE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTTETILD(KLON,0:KLEV),ZTAUITTE(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZKERV(KLON,0:KLEV),ZTKE1(KLON,KLEV) 
REAL(KIND=JPRB) :: ZTTE1(KLON,KLEV),ZKTTERV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZKESROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTKE_LAM5T,ZNU4TKE,ZTKE_OLAMC3
REAL(KIND=JPRB) :: Z_ETKE_R(KLON,0:KLEV),Z_ETKE_P(KLON,0:KLEV),&
& ZCHI3TKE(KLON,0:KLEV),ZPHI3TKE(KLON,0:KLEV),&
& ZPHIQTKE(KLON,0:KLEV),ZFMTKE(KLON,0:KLEV),ZPRT_INV(KLON,0:KLEV),&
& ZRIFTKE(KLON,0:KLEV),ZRHO(KLON,0:KLEV),ZTERM_I(KLON,0:KLEV),&
& ZTERM_II(KLON,0:KLEV),ZDPHIA(KLON,0:KLEV),ZRLM(KLON,0:KLEV),&
& ZTAUICONV(KLON,0:KLEV),ZS2(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZSCALE,ZRAT,ZRAT_MIN,ZRAT_MAX,ZRIF_MIN,ZRIF_MAX
REAL(KIND=JPRB) :: ZMRIFPP,ZMRIFPPI,ZWEIGHT,ZAA
REAL(KIND=JPRB) :: ZRHS(KLON,KLEV),ZLML1(KLON,KLEV),ZCK
REAL(KIND=JPRB) :: Z2THIRD,Z2AZ0
REAL(KIND=JPRB) :: ZEPS8
REAL(KIND=JPRB) :: ZGLU
REAL(KIND=JPRB) :: ZKSUMTERM, ZTSUMTERM
REAL(KIND=JPRB) :: ZDENS(KLON,KLEV)  ! full level density needed for DDH

REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "acrif2pr.intfb.h"
#include "acptkes.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACPTKE',0,ZHOOK_HANDLE)
ASSOCIATE(YTKE_NL=>YGFL%YTKE_NL, YTTE_NL=>YGFL%YTTE_NL, &
 & LCOEFK_PTTE=>YDML_PHY_MF%YRPHY%LCOEFK_PTTE, &
 & LCOEFK_FLX=>YDML_PHY_MF%YRPHY%LCOEFK_FLX, LCOEFK_PL=>YDML_PHY_MF%YRPHY%LCOEFK_PL, &
 & CGTURS=>YDML_PHY_MF%YRPHY%CGTURS, &
 & NUPTKE=>YDML_PHY_MF%YRPHY0%NUPTKE,VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & ETKE_BETA_EPS=>YDML_PHY_MF%YRPHY0%ETKE_BETA_EPS, ETKE_GAMMA_EPS=>YDML_PHY_MF%YRPHY0%ETKE_GAMMA_EPS, &
 & C3TKEFREE=>YDML_PHY_MF%YRPHY0%C3TKEFREE, ETKE_OLAM=>YDML_PHY_MF%YRPHY0%ETKE_OLAM, &
 & ETKE_LAM0=>YDML_PHY_MF%YRPHY0%ETKE_LAM0, ETKE_R=>YDML_PHY_MF%YRPHY0%ETKE_R, &
 & ETKE_RIFC=>YDML_PHY_MF%YRPHY0%ETKE_RIFC, ETKE_TAULM=>YDML_PHY_MF%YRPHY0%ETKE_TAULM, &
 & ETKE_LAM2=>YDML_PHY_MF%YRPHY0%ETKE_LAM2, ETKE_LAM3=>YDML_PHY_MF%YRPHY0%ETKE_LAM3, &
 & ETKE_LAM4=>YDML_PHY_MF%YRPHY0%ETKE_LAM4, ENT_LAMBDA=>YDML_PHY_MF%YRPHY0%ENT_LAMBDA, &
 & C_EPSILON=>YDML_PHY_MF%YRPHY0%C_EPSILON, EFB_UR=>YDML_PHY_MF%YRPHY0%EFB_UR, &
 & EFB_AZ0=>YDML_PHY_MF%YRPHY0%EFB_AZ0, REFB_1=>YDML_PHY_MF%YRPHY0%REFB_1, REFB_2=>YDML_PHY_MF%YRPHY0%REFB_2, &
 & REFB_3=>YDML_PHY_MF%YRPHY0%REFB_3, GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, ALMAV=>YDML_PHY_MF%YRPHY0%ALMAV, &
 & ETKE_KLM=>YDML_PHY_MF%YRPHY0%ETKE_KLM, &
 & ETKE_MIN=>YDML_PHY_MF%YRPHY0%ETKE_MIN, &
 & ETKE_CRIT=>YDML_PHY_MF%YRPHY0%ETKE_CRIT, &
 & ETKE_DELTA=>YDML_PHY_MF%YRPHY0%ETKE_DELTA, &
 & LMULAF=>YDML_PHY_MF%YRPHY2%LMULAF, TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LSDDH=>YDLDDH%LSDDH)
!     ------------------------------------------------------------------

! * Initialization of constants
ZGLU=RG*ALMAV

IF (LCOEFK_PTTE) THEN
  ZRELAXB=1._JPRB
ELSE
  ZRELAXB=1.5_JPRB
ENDIF
ZDRELAXB=1._JPRB - ZRELAXB

ZEPSKRV=1.E-8_JPRB

ZALTKE=1.0_JPRB/(NUPTKE*NUPTKE)
ZNU3TKE=NUPTKE**3
ZLIMTKE=0.5_JPRB*NUPTKE*ZNU3TKE*ZRELAXB/(RG*RG)
ZLIMTKE_L=ZLIMTKE/(ETKE_TAULM*ETKE_KLM)

ZCP=ETKE_OLAM/ETKE_LAM0/C3TKEFREE
ZUMCP=1.0_JPRB-ZCP
ZNU4TKE=NUPTKE**4
ZTKE_OLAMC3=0.75_JPRB*ETKE_OLAM/C3TKEFREE
ZCK=ZNU4TKE/C_EPSILON

Z2THIRD=2.0_JPRB/3.0_JPRB
Z2AZ0=2.0_JPRB/3.0_JPRB*(1.0_JPRB-3.0_JPRB*ETKE_LAM3-ETKE_LAM2)

ZEPS8=1E-8_JPRB

! minimum and maximum Rif
ZRIF_MIN=-1000._JPRB
ZRIF_MAX=0.999_JPRB*ETKE_RIFC

! minimum and maximum TTE/TKE ratio in Rif diagnostics
ZRAT_MIN=(1._JPRB-ZUMCP*ZRIF_MIN)/(1._JPRB-ZRIF_MIN)
ZRAT_MAX=(1._JPRB-ZUMCP*ZRIF_MAX)/(1._JPRB-ZRIF_MAX)

ZAA=(ETKE_CRIT-ETKE_MIN)*(ETKE_CRIT-ETKE_MIN)

IF     (TRIM(CGTURS) == 'RMC') THEN
  ITTYPE=1
ELSEIF (TRIM(CGTURS) == 'MD1') THEN
  ITTYPE=2
ELSEIF (TRIM(CGTURS) == 'MD2') THEN
  ITTYPE=3
ELSEIF (TRIM(CGTURS) == 'QNSE') THEN
  ITTYPE=4
ELSEIF (TRIM(CGTURS) == 'EFB') THEN
  ITTYPE=5
ELSEIF (TRIM(CGTURS) == 'LOUIS') THEN
  ITTYPE=6
ENDIF
!     ------------------------------------------------------------------

IF (LMULAF) THEN
  DO JLEV=KTDIA,KLEV-1
     DO JLON=KIDIA,KFDIA
        PXPTKEROV(JLON,JLEV)=SQRT(PXPTKEROV(JLON,JLEV))
     ENDDO
  ENDDO
  
  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
       PXPTKEROV(JLON,JLEV)=MAX(PXPTKEROV(JLON,JLEV),PXPTKEROV(JLON,JLEV-1))
    ENDDO
  ENDDO
ENDIF

!*
!     -----------------------------------------------------------------
!     COMPUTATION OF PTKE RELATED VALUES

DO JLEV=KTDIA,KLEV-1
  DO JLON=KIDIA,KFDIA

    ZRTI=2._JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
    ZDPHIA(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
    ZRLM(JLON,JLEV)= ((ZLIMTKE*ZDPHIA(JLON,JLEV)/(PXPTKEROV(JLON,JLEV)&
     & *PAPRS(JLON,JLEV)*ZRTI))*MAX(PDELP(JLON,JLEV)*(1._JPRB-(PALPH(JLON,JLEV)&
     & /PLNPR(JLON,JLEV))),PDELP(JLON,JLEV+1)*PALPH(JLON,JLEV+1)&
     & /PLNPR(JLON,JLEV+1)))/(PLMU(JLON,JLEV)*PLMU(JLON,JLEV))
    ZRLM_L(JLON,JLEV)= ((ZLIMTKE_L*ZDPHIA(JLON,JLEV)/(PXPTKEROV(JLON,JLEV)&
     & *PAPRS(JLON,JLEV)*ZRTI))*MAX(PDELP(JLON,JLEV)*(1._JPRB-(PALPH(JLON,JLEV)&
     & /PLNPR(JLON,JLEV))),PDELP(JLON,JLEV+1)*PALPH(JLON,JLEV+1)&
     & /PLNPR(JLON,JLEV+1)))/(PLMU(JLON,JLEV)*PLMU(JLON,JLEV))
    ZCONVERT(JLON,JLEV)=PAPRS(JLON,JLEV)*PLMU(JLON,JLEV)*RG*ZRTI*NUPTKE/&
    & ZDPHIA(JLON,JLEV)
    ZTAUICONV(JLON,JLEV)=TSPHY*ZNU3TKE/(PLMU(JLON,JLEV)*ZCONVERT(JLON,JLEV))
    ZKESROV(JLON,JLEV)=SQRT(PKNROV(JLON,JLEV)*MAX(PKUROV(JLON,JLEV),ZEPS8))
  ENDDO
ENDDO

IF (KSTEP == 0.AND.YTKE_NL%NREQIN == 0) THEN
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA

    ! CHOICE OF COEFFICIENTS FOR TKE DIFUSION AND RELAXATION
    ! THEY CAN BE COMPUTED FROM THE PREVIOUS TIMESTEP TKE VALUE
    ! OR FROM THE TILDED TKE VALUE (DIAGNOSED AT THE INSTANT
    ! TIMESTEP FROM THE EXCHANGE COEFFICIENT)

      ! COEFFICIENT FOR RELAXATION (times dt)
      ! CORRESPONDS TO TILDED VALUES
      ZTAUITKE(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZKESROV(JLON,JLEV)*PF_EPS(JLON,JLEV)
      ZTAUITKE_L(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZKESROV(JLON,JLEV)/ETKE_TAULM

      ! coefficient for diffusion corresponds to tilded values
      ! (with the security for small tau_epsilon)
      ZKERV(JLON,JLEV)=ZALTKE*ZKESROV(JLON,JLEV)* &
       & MAX(1._JPRB,PF_EPS(JLON,JLEV))* &
       & MAX(1._JPRB,ZRLM(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV)))

      ZKERV_L(JLON,JLEV)=ETKE_KLM*ZALTKE*ZKESROV(JLON,JLEV)* &
       & MAX(1._JPRB,PF_EPS(JLON,JLEV))* &
       & MAX(1._JPRB,ZRLM_L(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV)))
        
      ! security for small tau_epsilon
      ZTAUITKE(JLON,JLEV)=ZTAUITKE(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV))
      ! security for large tau_epsilon
      ZTAUITKE(JLON,JLEV)=ZTAUITKE(JLON,JLEV)* &
       & (1._JPRB+ETKE_GAMMA_EPS/ZTAUITKE(JLON,JLEV))

      ! security for small tau_L
      ZTAUITKE_L(JLON,JLEV)=ZTAUITKE_L(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE_L(JLON,JLEV))
      ! security for large tau_epsilon
      ZTAUITKE_L(JLON,JLEV)=ZTAUITKE_L(JLON,JLEV)* &
       & (1._JPRB+ETKE_GAMMA_EPS/ZTAUITKE_L(JLON,JLEV))
    ENDDO
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZTKES=0.5_JPRB*(PTKE(JLON,JLEV)+PTKE(JLON,JLEV+1))
      ! COEFFICIENT FOR RELAXATION (times dt)
      ! CORRESPONDS TO PREVIOUS TKE VALUE
      ZTAUITKE(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZCONVERT(JLON,JLEV)&
       & *SQRT(ZTKES)*PF_EPS(JLON,JLEV)

      ZTAUITKE_L(JLON,JLEV)=ZTAUICONV(JLON,JLEV)*ZCONVERT(JLON,JLEV)&
       & *SQRT(ZTKES)*PF_EPS(JLON,JLEV)/ETKE_TAULM
      ! COEFFICIENT FOR DIFFUSION CORRESPONDS TO PREVIOUS TKE VALUE
      ! (with the security for small tau_epsilon)
      ZKERV(JLON,JLEV)=ZALTKE*ZCONVERT(JLON,JLEV)* &
       & SQRT(ZTKES)*MAX(1._JPRB,PF_EPS(JLON,JLEV))* &
       & MAX(1._JPRB,ZRLM(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV)))
       
      ZKERV_L(JLON,JLEV)=ETKE_KLM*ZALTKE*ZCONVERT(JLON,JLEV)* &
       & SQRT(ZTKES)*MAX(1._JPRB,PF_EPS(JLON,JLEV))* &
       & MAX(1._JPRB,ZRLM_L(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV)))
      ! security for small tau_epsilon
      ZTAUITKE(JLON,JLEV)=ZTAUITKE(JLON,JLEV)/ &
       & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,JLEV))
      ! security for large tau_epsilon
      ZTAUITKE(JLON,JLEV)=ZTAUITKE(JLON,JLEV)* &
       & (1._JPRB+ETKE_GAMMA_EPS/ZTAUITKE(JLON,JLEV))
    ENDDO
  ENDDO
ENDIF

IF (LCOEFK_PTTE) THEN

  ! dt/tau_TTE and K_TTE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTAUITTE(JLON,JLEV)=ZTAUITKE(JLON,JLEV)/ &
       & (1.0_JPRB-ZUMCP*PMRIFPP(JLON,JLEV))
      ZKTTERV(JLON,JLEV)=ZKERV(JLON,JLEV)/ &
       & (1.0_JPRB-ZUMCP*PMRIFPP(JLON,JLEV))
    ENDDO
  ENDDO

  ! squared wind shear (unprotected against zero), not needed at KLEV
  IF (ETKE_DELTA /= 0) THEN 
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZS2(JLON,JLEV)=((PU(JLON,JLEV)-PU(JLON,JLEV+1))**2+  &
         &              (PV(JLON,JLEV)-PV(JLON,JLEV+1))**2)* &
         &             (RG/ZDPHIA(JLON,JLEV))**2
      ENDDO
    ENDDO
  ENDIF

ENDIF

! source terms computed from fluxes
IF (LCOEFK_FLX.AND.KSTEP > 0) THEN
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZRHO(JLON,JLEV)=2._JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+ &
       & PR(JLON,JLEV+1)*PT(JLON,JLEV+1))*PAPRS(JLON,JLEV)
      ZTERM_I (JLON,JLEV)=PSHEAR(JLON,JLEV)*RG/ZDPHIA(JLON,JLEV)
      ZTERM_II(JLON,JLEV)=-(PMN2_ES(JLON,JLEV)*PDIFTS(JLON,JLEV)+ &
       & PMN2_EQ(JLON,JLEV)*PDIFTQ(JLON,JLEV))
      ZTKETILD(JLON,JLEV)=(ZTERM_I(JLON,JLEV)+ZTERM_II(JLON,JLEV))* &
       & TSPHY/(ZTAUITKE(JLON,JLEV)*ZRHO(JLON,JLEV))
    ENDDO
  ENDDO
     
  IF (LCOEFK_PTTE) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZTTETILD(JLON,JLEV)=TSPHY/ZTAUITTE(JLON,JLEV)/ &
         & ZRHO(JLON,JLEV)*ZTERM_I(JLON,JLEV)
      ENDDO
    ENDDO
  ENDIF
ELSE 
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZTKETILD(JLON,JLEV)=PKUROV(JLON,JLEV)*PKNROV(JLON,JLEV)/ &
       & ZCONVERT(JLON,JLEV)**2
    ENDDO
  ENDDO
  IF (LCOEFK_PTTE) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZTTETILD(JLON,JLEV)=ZTAUITKE(JLON,JLEV)/ZTAUITTE(JLON,JLEV)* &
         & (PKNROV(JLON,JLEV)/ZCONVERT(JLON,JLEV))**2*PFUN_TTE(JLON,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF

!     CALCULS EN SURFACE.
!     SURFACE CALCULATIONS.

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA

  ! FV: This is bit inconsistent as the E_tilde and mixing lenght
  !     are obtained in this case for the for the full level.
  ZTKETILD(JLON,KLEV)=ZALTKE*PCD(JLON)*(PU(JLON,KLEV)**2+PV(JLON,KLEV)**2)
  ZTAUITKE(JLON,KLEV)=TSPHY*ZNU3TKE*RG*SQRT(ZTKETILD(JLON,KLEV))/ &
   & (VKARMN*(PGZ0(JLON)+PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)))*PF_EPS(JLON,KLEV)

  ! security for small tau_epsilon
  ZTAUITKE(JLON,KLEV)=ZTAUITKE(JLON,KLEV)/ &
   & (1._JPRB+ETKE_BETA_EPS*ZTAUITKE(JLON,KLEV))
  ! security for large tau_epsilon
  ZTAUITKE(JLON,KLEV)=ZTAUITKE(JLON,KLEV)* &
   & (1._JPRB+ETKE_GAMMA_EPS/ZTAUITKE(JLON,KLEV))
ENDDO

IF (LCOEFK_PTTE) THEN
  DO JLON=KIDIA,KFDIA
    ZTTETILD(JLON,KLEV)=ZTKETILD(JLON,KLEV)* &
     & (1.0_JPRB-ZUMCP*PMRIFPP(JLON,KLEV))/ &
     & (1.0_JPRB-PMRIFPP(JLON,KLEV))
    ZTAUITTE(JLON,KLEV)=ZTAUITKE(JLON,KLEV)/ &
     & (1.0_JPRB-ZUMCP*PMRIFPP(JLON,KLEV))
  ENDDO
ENDIF

IF (.NOT.LCOEFK_PTTE) THEN

  ! TKE solver
  CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
    & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
    & PRDELP,PALPH,PLNPR,PTKE,PRHS,&
    & ZTAUITKE,ZKERV,PXPTKEROV,ZTKETILD,1.0_JPRB,&
    & ZTKE1)

ELSE

  ! TTE initialisation
  IF (KSTEP == 0.AND.YTTE_NL%NREQIN == 0) THEN
    DO JLEV=KTDIA,KLEV
      DO JLON=KIDIA,KFDIA
        PTTE(JLON,JLEV)=PTKE(JLON,JLEV)*(1.0_JPRB+ZCP*PMRIFPP(JLON,JLEV)/ &
         & (1.0_JPRB-PMRIFPP(JLON,JLEV)))
      ENDDO
    ENDDO
  ENDIF

  ZRHS(KIDIA:KFDIA,KTDIA:KLEV)=0.0_JPRB  ! no source term

  ! TKE solver - preliminary step
  CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
   & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PRDELP,PALPH,PLNPR,PTKE,ZRHS,&
   & ZTAUITKE,ZKERV,PXPTKEROV,ZTKETILD,1.0_JPRB,&
   & ZTKE1)

  ! TTE SOLVER - preliminary step
  CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
   & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PRDELP,PALPH,PLNPR,PTTE,ZRHS,&
   & ZTAUITTE,ZKTTERV,PXPTKEROV,ZTTETILD,1.0_JPRB,&
   & ZTTE1)

  IF ( ETKE_DELTA /= 0 ) THEN

    ! update Rif, stability functions, K_M, K_H, terms I, II, and tilded
    ! energies
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA

        ! Rif_m computation from TKE and TTE
        ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,ZTTE1(JLON,JLEV)/ZTKE1(JLON,JLEV)))
        ZWEIGHT=(ZTKE1(JLON,JLEV)-ETKE_MIN)*(ZTKE1(JLON,JLEV)-ETKE_MIN)+     &
         &      (ZTTE1(JLON,JLEV)-ETKE_MIN)*(ZTTE1(JLON,JLEV)-ETKE_MIN)
        ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
        ZMRIFPP=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                          &
         & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
        ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,ZTTE1(JLON,JLEV+1)/ZTKE1(JLON,JLEV+1)))
        ZWEIGHT=(ZTKE1(JLON,JLEV+1)-ETKE_MIN)*(ZTKE1(JLON,JLEV+1)-ETKE_MIN)+ &
         &      (ZTTE1(JLON,JLEV+1)-ETKE_MIN)*(ZTTE1(JLON,JLEV+1)-ETKE_MIN)
        ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
        ZMRIFPPI=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                         &
         & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
        ZRIFTKE(JLON,JLEV)=0.5_JPRB*(ZMRIFPP+ZMRIFPPI)

        ! convert Rif to P and R
        CALL ACRIF2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
         & ZRIFTKE,Z_ETKE_P,Z_ETKE_R)

        ! stability functions
        ZCHI3TKE(JLON,JLEV)=(1.0_JPRB-ZRIFTKE(JLON,JLEV)/Z_ETKE_R(JLON,JLEV))/ &
         & (1.0_JPRB-ZRIFTKE(JLON,JLEV))
        ZPHI3TKE(JLON,JLEV)=(1.0_JPRB-ZRIFTKE(JLON,JLEV)/Z_ETKE_P(JLON,JLEV))/ &
         & (1.0_JPRB-ZRIFTKE(JLON,JLEV))

        ZFMTKE(JLON,JLEV)=SQRT(ZCHI3TKE(JLON,JLEV)**3* &
         & (1.0_JPRB-ZRIFTKE(JLON,JLEV)))
        ZPRT_INV(JLON,JLEV)=C3TKEFREE*ZPHI3TKE(JLON,JLEV)/ZCHI3TKE(JLON,JLEV)

        ! K_M and K_H (scaled by rho/dz)
        PKUROV(JLON,JLEV)=ZCONVERT(JLON,JLEV)*SQRT( &
         & 0.5_JPRB*(ZTKE1(JLON,JLEV)+ZTKE1(JLON,JLEV+1)))*    &
         & SQRT(ZFMTKE(JLON,JLEV))
        PKTROV(JLON,JLEV)=PKUROV(JLON,JLEV)*ZPRT_INV(JLON,JLEV)

        ! dz/rho
        ZSCALE=(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))* &
         & ZDPHIA(JLON,JLEV)/(2._JPRB*PAPRS(JLON,JLEV)*RG)

        ! updated terms I and II
        ZTERM_I (JLON,JLEV)= ZSCALE*PKUROV(JLON,JLEV)*ZS2(JLON,JLEV)
        ZTERM_II(JLON,JLEV)=-ZSCALE*PKTROV(JLON,JLEV)*ZS2(JLON,JLEV)* &
         & PMRIPP(JLON,JLEV)

        ! updated tilded energies
        ZTKETILD0=0.5_JPRB*(ZTERM_I(JLON,JLEV)+ZTERM_II(JLON,JLEV))* &
         & TSPHY/ZTAUITKE(JLON,JLEV)
        ZTTETILD0=0.5_JPRB*ZTERM_I(JLON,JLEV)*TSPHY/ZTAUITTE(JLON,JLEV)

        ! tilded energies weighted between original and updated values
        ZTKETILD(JLON,JLEV)=ZTKETILD(JLON,JLEV)+ &
         & ETKE_DELTA*(ZTKETILD0-ZTKETILD(JLON,JLEV))
        ZTTETILD(JLON,JLEV)=ZTTETILD(JLON,JLEV)+ &
         & ETKE_DELTA*(ZTTETILD0-ZTTETILD(JLON,JLEV))

      ENDDO
    ENDDO

    ! TKE solver with updated tilded energies
    CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
     & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
     & PRDELP,PALPH,PLNPR,PTKE,ZRHS,&
     & ZTAUITKE,ZKERV,PXPTKEROV,ZTKETILD,1.0_JPRB,&
     & ZTKE1)

    ! TTE solver with updated tilded energies
    CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
     & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
     & PRDELP,PALPH,PLNPR,PTTE,ZRHS,&
     & ZTAUITTE,ZKTTERV,PXPTKEROV,ZTTETILD,1.0_JPRB,&
     & ZTTE1)

  ENDIF

  ! copy new TTE value to dummy argument
  PTTE(KIDIA:KFDIA,KTDIA:KLEV)=ZTTE1(KIDIA:KFDIA,KTDIA:KLEV)

ENDIF

!   PSEUDO TKE SOLVER - PROGNOSTIC L
IF (LCOEFK_PL) THEN
  ZRHS(KIDIA:KFDIA,KTDIA:KLEV)=0.0_JPRB  ! no source term
  CALL ACPTKES(YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY0,YDML_PHY_MF%YRPHY2,&
    & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
    & PRDELP,PALPH,PLNPR,PMXL,ZRHS,&
    & ZTAUITKE_L,ZKERV_L,PXPTKEROV,PLMLTILD,1.0_JPRB,&
    & ZLML1)

  PMXL(KIDIA:KFDIA,KTDIA:KLEV-1)=ZLML1(KIDIA:KFDIA,KTDIA:KLEV-1)
  !surface
  PMXL(KIDIA:KFDIA,KLEV)=(PALPH(KIDIA:KFDIA,KLEV)/PLNPR(KIDIA:KFDIA,KLEV)*&
     & PLMLTILD(KIDIA:KFDIA,KLEV-1) +&
     & (1.0_JPRB-PALPH(KIDIA:KFDIA,KLEV)/PLNPR(KIDIA:KFDIA,KLEV))*&
     & PLMLTILD(KIDIA:KFDIA,KLEV))

!update of mixing length for TOMs only
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
     PLML(JLON,JLEV)=(PMXL(JLON,JLEV)+PMXL(JLON,JLEV+1))*0.5_JPRB*PRRCOR(JLON,JLEV)
    ENDDO
  ENDDO

ENDIF

! Isolation of TKE and TTE budget terms for ALARO DDH
IF (LSDDH) THEN  ! condition for DDH
  PFTCNS(:,:,:)=0.0_JPRB
  IF (KSTEP > 0) THEN
    ! TKE budget terms
    DO JLEV=KTDIA+1,KLEV-1
      DO JLON=KIDIA,KFDIA
        ! shear production term
        PFTCNS(JLON,JLEV,1)=0.5_JPRB*(ZTERM_I (JLON,JLEV-1)+ZTERM_I (JLON,JLEV))
        ! buoyancy production/destruction term
        PFTCNS(JLON,JLEV,2)=0.5_JPRB*(ZTERM_II(JLON,JLEV-1)+ZTERM_II(JLON,JLEV))
        ! TKE dissipation term 
        ZDENS(JLON,JLEV)=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))
        PFTCNS(JLON,JLEV,3)=ZDENS(JLON,JLEV)*ZTAUITKE(JLON,JLEV)* &
         & PTKE(JLON,JLEV)/TSPHY
        ! turbulent transport of TKE
        ZKSUMTERM=PFTCNS(JLON,JLEV,1)+PFTCNS(JLON,JLEV,2)+PFTCNS(JLON,JLEV,3)
        PFTCNS(JLON,JLEV,4)=((ZTKE1(JLON,JLEV)-PTKE(JLON,JLEV))/TSPHY)* &
         & ZDENS(JLON,JLEV)-ZKSUMTERM
      ENDDO
    ENDDO

    ! Surface treatment (assume constant fluxes)
    DO JLON=KIDIA,KFDIA
      PFTCNS(JLON,KLEV,1)=ZTERM_I (JLON,KLEV-1)
      PFTCNS(JLON,KLEV,2)=ZTERM_II(JLON,KLEV-1)
      ZDENS (JLON,KLEV)  =PAPRSF(JLON,KLEV)/(PR(JLON,KLEV)*PT(JLON,KLEV))
      PFTCNS(JLON,KLEV,3)=-0.5_JPRB* &
       & (ZDENS(JLON,KLEV-1)*ZTAUITKE(JLON,KLEV-1)*PTKE(JLON,KLEV-1)+ &
       &  ZDENS(JLON,KLEV  )*ZTAUITKE(JLON,KLEV  )*PTKE(JLON,KLEV  ))/TSPHY
      ZKSUMTERM=0.5_JPRB*( &
       & PFTCNS(JLON,KLEV-1,1)+PFTCNS(JLON,KLEV-1,2)+PFTCNS(JLON,KLEV-1,3)+ &
       & PFTCNS(JLON,KLEV  ,1)+PFTCNS(JLON,KLEV  ,2)+PFTCNS(JLON,KLEV  ,3))
      PFTCNS(JLON,KLEV,4)=0.5_JPRB*( &
       & ZDENS(JLON,KLEV-1)*(ZTKE1(JLON,KLEV-1)-PTKE(JLON,KLEV-1))+ &
       & ZDENS(JLON,KLEV  )*(ZTKE1(JLON,KLEV  )-PTKE(JLON,KLEV  ))  &
       & )/TSPHY-ZKSUMTERM
    ENDDO

    ! Upper boundary treatment (assume constant fluxes)
    DO JLON=KIDIA,KFDIA
      PFTCNS(JLON,KTDIA,1)=ZTERM_I (JLON,KTDIA+1)
      PFTCNS(JLON,KTDIA,2)=ZTERM_II(JLON,KTDIA+1)
      ZDENS (JLON,KTDIA)  =PAPRSF(JLON,KTDIA)/(PR(JLON,KTDIA)*PT(JLON,KTDIA))
      PFTCNS(JLON,KTDIA,3)=-0.5_JPRB* &
       & (ZDENS(JLON,KTDIA+1)*ZTAUITKE(JLON,KTDIA+1)*PTKE(JLON,KTDIA+1)+ &
       &  ZDENS(JLON,KTDIA  )*ZTAUITKE(JLON,KTDIA  )*PTKE(JLON,KTDIA  ))/TSPHY
      ZKSUMTERM=0.5_JPRB*( &
       & PFTCNS(JLON,KTDIA+1,1)+PFTCNS(JLON,KTDIA+1,2)+PFTCNS(JLON,KTDIA+1,3)+ &
       & PFTCNS(JLON,KTDIA  ,1)+PFTCNS(JLON,KTDIA  ,2)+PFTCNS(JLON,KTDIA  ,3))
      PFTCNS(JLON,KTDIA,4)=0.5_JPRB*( &
       & ZDENS(JLON,KTDIA+1)*(ZTKE1(JLON,KTDIA+1)-PTKE(JLON,KTDIA+1))+ &
       & ZDENS(JLON,KTDIA  )*(ZTKE1(JLON,KTDIA  )-PTKE(JLON,KTDIA  ))  &
       & )/TSPHY-ZKSUMTERM
    ENDDO

    ! TTE budget terms
    IF (LCOEFK_PTTE) THEN
      DO JLEV=KTDIA+1,KLEV-1
        DO JLON=KIDIA,KFDIA
          ! TTE dissipation term [kg*m^{-1}*s^{-3}]
          PFTCNS(JLON,JLEV,5)=-ZDENS(JLON,JLEV)*ZTAUITTE(JLON,JLEV)* &
           & PTTE(JLON,JLEV)/TSPHY
          ! turbulent transport of TTE [kg*m^{-1}*s^{-3}]
          ZTSUMTERM=PFTCNS(JLON,JLEV,1)-PFTCNS(JLON,JLEV,5)
          PFTCNS(JLON,JLEV,6)=((ZTTE1(JLON,JLEV)-PTTE(JLON,JLEV))/TSPHY)* &
           & ZDENS(JLON,JLEV)-ZTSUMTERM
        ENDDO
      ENDDO

      ! Surface treatment (assume constant fluxes)
      DO JLON=KIDIA,KFDIA
        PFTCNS(JLON,KLEV,5)=-0.5_JPRB* &
         & (ZDENS(JLON,KLEV-1)*ZTAUITTE(JLON,KLEV-1)*PTTE(JLON,KLEV-1)+ &
         &  ZDENS(JLON,KLEV  )*ZTAUITTE(JLON,KLEV  )*PTTE(JLON,KLEV  ))/TSPHY
        ZTSUMTERM=0.5_JPRB*( &
         & PFTCNS(JLON,KLEV-1,1)+PFTCNS(JLON,KLEV-1,5)+ &
         & PFTCNS(JLON,KLEV  ,1)+PFTCNS(JLON,KLEV  ,5))
        PFTCNS(JLON,KLEV,6)=0.5_JPRB*( &
         & ZDENS(JLON,KLEV-1)*(ZTTE1(JLON,KLEV-1)-PTTE(JLON,KLEV-1))+ &
         & ZDENS(JLON,KLEV  )*(ZTTE1(JLON,KLEV  )-PTTE(JLON,KLEV  ))  &
         & )/TSPHY-ZTSUMTERM
      ENDDO

      ! Upper boundary treatment (assume constant fluxes)
      DO JLON=KIDIA,KFDIA
        PFTCNS(JLON,KTDIA,5)=-0.5_JPRB* &
         & (ZDENS(JLON,KTDIA+1)*ZTAUITTE(JLON,KTDIA+1)*PTTE(JLON,KTDIA+1)+ &
         &  ZDENS(JLON,KTDIA  )*ZTAUITTE(JLON,KTDIA  )*PTTE(JLON,KTDIA  ))/TSPHY
        ZTSUMTERM=0.5_JPRB*( &
         & PFTCNS(JLON,KTDIA+1,1)+PFTCNS(JLON,KTDIA+1,5)+ &
         & PFTCNS(JLON,KTDIA  ,1)+PFTCNS(JLON,KTDIA  ,5))
        PFTCNS(JLON,KTDIA,6)=0.5_JPRB*( &
         & ZDENS(JLON,KTDIA+1)*(ZTTE1(JLON,KTDIA+1)-PTTE(JLON,KTDIA+1))+ &
         & ZDENS(JLON,KTDIA  )*(ZTTE1(JLON,KTDIA  )-PTTE(JLON,KTDIA  ))  &
         & )/TSPHY-ZTSUMTERM
      ENDDO

    ENDIF  ! LCOEFK_PTTE
  ENDIF    ! KSTEP > 0
ENDIF      ! LSDDH

!* !     ------------------------------------------------------------------
! * Modification of exchange coefficients
IF (LCOEFK_PTTE.AND.KSTEP > 0) THEN

  ! Rif_m computation from TKE and TTE
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,PTTE(JLON,JLEV)/ZTKE1(JLON,JLEV)))
      ZWEIGHT=(ZTKE1(JLON,JLEV)-ETKE_MIN)*(ZTKE1(JLON,JLEV)-ETKE_MIN)+     &
       &      (PTTE (JLON,JLEV)-ETKE_MIN)*(PTTE (JLON,JLEV)-ETKE_MIN)
      ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
      ZMRIFPP=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                          &
       & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
      ZRAT=MAX(ZRAT_MIN,MIN(ZRAT_MAX,PTTE(JLON,JLEV+1)/ZTKE1(JLON,JLEV+1)))
      ZWEIGHT=(ZTKE1(JLON,JLEV+1)-ETKE_MIN)*(ZTKE1(JLON,JLEV+1)-ETKE_MIN)+ &
       &      (PTTE (JLON,JLEV+1)-ETKE_MIN)*(PTTE (JLON,JLEV+1)-ETKE_MIN)
      ZWEIGHT=ZAA/(ZAA+ZWEIGHT)
      ZMRIFPPI=ZWEIGHT*ZRIF_MAX+(1._JPRB-ZWEIGHT)*                         &
       & (ZRAT-1._JPRB)/(ZRAT-ZUMCP)
      ZRIFTKE(JLON,JLEV)=0.5_JPRB*(ZMRIFPP+ZMRIFPPI)
    ENDDO
  ENDDO

  ! convert Rif to P and R
  CALL ACRIF2PR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,ITTYPE,&
   & ZRIFTKE,Z_ETKE_P,Z_ETKE_R)

  ! chi_3 and phi_3
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZCHI3TKE(JLON,JLEV)=(1.0_JPRB-ZRIFTKE(JLON,JLEV)/ &
       & Z_ETKE_R(JLON,JLEV))/(1.0_JPRB-ZRIFTKE(JLON,JLEV))
      ZPHI3TKE(JLON,JLEV)=(1.0_JPRB-ZRIFTKE(JLON,JLEV)/ &
       & Z_ETKE_P(JLON,JLEV))/(1.0_JPRB-ZRIFTKE(JLON,JLEV))
    ENDDO
  ENDDO

  ! stability functions Phi_Q, F_WW and T_H for TOMS computation
  IF (ITTYPE==1) THEN 
    ! RMC01
    DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        ZPHIQTKE(JLON,JLEV)=1.0_JPRB
        PWW_FUN(JLON,JLEV)=Z2THIRD
        PTH_FUN(JLON,JLEV)=ZTKE_OLAMC3/(2.0_JPRB*PWW_FUN(JLON,JLEV))
      ENDDO
    ENDDO
  ELSEIF (ITTYPE==2) THEN 
    ! Model I
    DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        PWW_FUN(JLON,JLEV)=Z2THIRD*(1.0_JPRB-(3.0_JPRB*ETKE_LAM3-ETKE_LAM2+ &
         & 4.0_JPRB*ETKE_LAM4*ZRIFTKE(JLON,JLEV))/ &
         & (1.0_JPRB-ZRIFTKE(JLON,JLEV)))
        ZPHIQTKE(JLON,JLEV)=PWW_FUN(JLON,JLEV)/Z2AZ0
        PTH_FUN(JLON,JLEV)=ZTKE_OLAMC3/(2.0_JPRB*PWW_FUN(JLON,JLEV))
      ENDDO
    ENDDO
  ELSEIF ((ITTYPE==3).OR.(ITTYPE==4)) THEN 
    ! Model II or QNSE
    DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
        PWW_FUN(JLON,JLEV)=Z2THIRD*(1.0_JPRB-(3.0_JPRB*ETKE_LAM3-ETKE_LAM2+ &
         & 4.0_JPRB*ETKE_LAM4*ZRIFTKE(JLON,JLEV))/ &
         & (1.0_JPRB-ZRIFTKE(JLON,JLEV)))
        ZTKE_LAM5T=(C3TKEFREE+ZCHI3TKE(JLON,JLEV)/ &
         & ZPHI3TKE(JLON,JLEV))/(C3TKEFREE+1.0_JPRB)
        ZPHIQTKE(JLON,JLEV)=PWW_FUN(JLON,JLEV)/ZTKE_LAM5T/Z2AZ0
        PTH_FUN(JLON,JLEV)=ZTKE_OLAMC3/(2.0_JPRB*PWW_FUN(JLON,JLEV))
      ENDDO
    ENDDO
  ELSEIF (ITTYPE==5) THEN 
    ! EFB
    DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
      DO JLON=KIDIA,KFDIA
       ZPHIQTKE(JLON,JLEV)=ZCHI3TKE(JLON,JLEV)
       PWW_FUN(JLON,JLEV)=2.0_JPRB*EFB_AZ0*ZCHI3TKE(JLON,JLEV)
       PTH_FUN(JLON,JLEV)=ZTKE_OLAMC3/(2.0_JPRB*PWW_FUN(JLON,JLEV))
      ENDDO
    ENDDO
  ELSEIF (ITTYPE == 6) THEN 
    CALL ABOR1('ACPTKE: LCOEFK_PTTE=T AND CGTURS=LOUIS is not coded') 
  ENDIF

  ! STABILITY FUNCTIONS FM, 1/sigma and T_h'', F_ww
  DO JLEV=KTDIA,KLEV-1
!DEC$ IVDEP
    DO JLON=KIDIA,KFDIA
      ZFMTKE(JLON,JLEV)=SQRT(ZCHI3TKE(JLON,JLEV)**3* &
       & (1.0_JPRB-ZRIFTKE(JLON,JLEV)))
      ZPRT_INV(JLON,JLEV)=C3TKEFREE*ZPHI3TKE(JLON,JLEV)/ZCHI3TKE(JLON,JLEV)
      PTH_FUN(JLON,JLEV)=MAX(2.0_JPRB*PTH_FUN(JLON,JLEV)*ZPHIQTKE(JLON,JLEV)/ &
       & (ZPHIQTKE(JLON,JLEV)+ZPHI3TKE(JLON,JLEV)),0.0_JPRB)
    ENDDO
  ENDDO
 
  ! K_M/H computation
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      PKUROV(JLON,JLEV)=ZCONVERT(JLON,JLEV)* &
       & SQRT(0.5_JPRB*(ZTKE1(JLON,JLEV)+ZTKE1(JLON,JLEV+1)))* &
       & SQRT(ZFMTKE(JLON,JLEV))
      PKTROV(JLON,JLEV)=PKUROV(JLON,JLEV)*ZPRT_INV(JLON,JLEV)
    ENDDO
  ENDDO
ELSE ! pTKE code
  ! K_M/H computation
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ! preparation only: keeps the ratio of original PKTROV, PKUROV
      PKTROV(JLON,JLEV)=(PKTROV(JLON,JLEV)/MAX(ZEPSKRV,PKUROV(JLON,JLEV)))
      ! now the PKUROV,PKTROV are redefined
      PKUROV(JLON,JLEV)=ZCONVERT(JLON,JLEV)* &
       & SQRT(0.5_JPRB*(ZTKE1(JLON,JLEV)+ZTKE1(JLON,JLEV+1)))* &
       & (PKUROV(JLON,JLEV)/ZKESROV(JLON,JLEV))
      PKTROV(JLON,JLEV)=PKUROV(JLON,JLEV)*PKTROV(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!     pTKE TENDENCY EVALUATION (PTENDPTKE=(PTKE1-PTKE0)/DT)

!cdir unroll=8
DO JLEV=KTDIA,KLEV
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    PTENDPTKE(JLON,JLEV)=(ZTKE1(JLON,JLEV)-PTKE(JLON,JLEV))/TSPHY
  ENDDO
ENDDO
!     -----------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPTKE',1,ZHOOK_HANDLE)
END SUBROUTINE ACPTKE
