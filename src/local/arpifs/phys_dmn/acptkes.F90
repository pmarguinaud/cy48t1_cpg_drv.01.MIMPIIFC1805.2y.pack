!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACPTKES(YDPHY,YDPHY0,YDPHY2,&
 & KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PRDELP,PALPH,PLNPR,PVAR,PRHS,&
 & PTAUITKE,PKERV,PXPTKEROV,PVARTILD,&
 ! SCALAR
 & PTAU_MULT,&
 ! - OUTPUT 2D .
 & PVAR1)


!**** *ACPTKES * - pTKE solver, 
!       used also for porg. mix. length,
!       and Tompkins prognostic cloudiness computation

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCULS DE LA P-TKE. 

!**   Interface.
!     ----------
!        *CALL* *PTKESOLV*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE 
! * INPUT

! - 2D (1:KLEV) .
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PALPH      : LOG(PAPRS(JLEV)/PAPRSF(JLEV)) (FOR HYDROSTATICS).
! PLNPR      : LOG(PAPRS(JLEV)/PAPRS(JLEV-1)) (FOR HYDROSTATICS).
! PVAR       : TURBULENT KINETIC ENERGY
! PRSH       : SOURCE TERM FROM 3D TURBULENCE FOR TKE SOLVER

! * INPUT ARGUMENTS

! - 2D (0:KLEV) .
! PXPTKEROV  : "ANTI-FIBRILLATION" COEFFICIENT FOR THE TKE AUTO-DIFFUSION
! PTAUITKE   : dt/tau_epsilon
! PKERV      : K_E - TKE autodiffsion coefficient
! PVARTILD   : stationary value of prognostic variable

! * OUTPUT
 
! - 2D (1:KLEV) .
! PVAR1  : VALUE OF PROGNOSTIC VARIABLE IN NEXT TIME STEP
!-----------------------------------------------------------------------
!     Auteur.
!     -------
!        2008-02, R. Brozkova (moving relevant part from ACDIFUS)

!     Modifications.
!     --------------
!      F. Vana  01-Sep-2008: all pTKE specific stuff collected here.
!      F. Vana  21-Jan-2009: bug fix of PGZ0 + new stabilization of solver
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      I. Bastak Duran 07-2012: ACPTKES is solver from ACPTKE
!      J. Masek 09-2019: introduction of ETKE_MIN
!      J. Masek 10-2020: no overimplicitness for prognostic TTE
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG
USE YOMPHY   , ONLY : TPHY
USE YOMPHY0  , ONLY : TPHY0
USE YOMPHY2  , ONLY : TPHY2

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
TYPE(TPHY2)       ,INTENT(IN)    :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 

REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALPH(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVAR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLNPR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRHS(KLON,KLEV)
 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTAUITKE(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKERV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PXPTKEROV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVARTILD(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTAU_MULT

REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVAR1(KLON,KLEV)
!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV, JLON

REAL(KIND=JPRB) :: ZGDT

REAL(KIND=JPRB) :: ZRELAXB, ZDRELAXB
REAL(KIND=JPRB) :: ZDOW, ZDUP, ZMUL, ZELIM

REAL(KIND=JPRB) :: ZDBET(KLON,0:KLEV),ZBET(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZIPOI(KLON,KLEV), ZSUB1(KLON,KLEV), ZN1(KLON,KLEV)
REAL(KIND=JPRB) :: ZTAUITKE(KLON,0:KLEV), ZKERV(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZHOOK_HANDLE


!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACPTKES',0,ZHOOK_HANDLE)
ASSOCIATE(LCOEFK_PTTE=>YDPHY%LCOEFK_PTTE, ETKE_MIN=>YDPHY0%ETKE_MIN, &
 & TSPHY=>YDPHY2%TSPHY)
!     ------------------------------------------------------------------

! * Initialization of constants

IF (LCOEFK_PTTE) THEN
  ZRELAXB=1._JPRB
ELSE
  ZRELAXB=1.5_JPRB
ENDIF
ZDRELAXB=1._JPRB - ZRELAXB

ZGDT=RG*TSPHY

ZTAUITKE(KIDIA:KFDIA,KTDIA:KLEV)=PTAUITKE(KIDIA:KFDIA,KTDIA:KLEV)*PTAU_MULT
ZKERV(KIDIA:KFDIA,KTDIA:KLEV)=PKERV(KIDIA:KFDIA,KTDIA:KLEV)*PTAU_MULT
 

!* !     ------------------------------------------------------------------

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZIPOI     : (RG*DT)/DP POUR UNE COUCHE ET UN PAS DE TEMPS DONNES.
!           : (RG*DT)/DP FOR A GIVEN LAYER AND A GIVEN TIME STEP.

! - TEMPORAIRE(S) 1D          .

!cdir unroll=8
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZIPOI(JLON,JLEV)=PRDELP(JLON,JLEV)*ZGDT
  ENDDO
ENDDO
!     ------------------------------------------------------------------

!   PSEUDO TKE SOLVER

!     ELIMINATION AU SOMMET.
!     ELIMINATION AT THE TOP.

DO JLON=KIDIA,KFDIA

  ZBET(JLON,KTDIA) =          PXPTKEROV(JLON,KTDIA) *ZKERV(JLON,KTDIA)
  ZDBET(JLON,KTDIA)=(1.0_JPRB-PXPTKEROV(JLON,KTDIA))*ZKERV(JLON,KTDIA)
  ZDOW=0.5_JPRB*(1._JPRB-PALPH(JLON,KTDIA)/PLNPR(JLON,KTDIA))&
   & *ZTAUITKE(JLON,KTDIA)

  ZMUL=1.0_JPRB/(1.0_JPRB+ZBET(JLON,KTDIA)*ZIPOI(JLON,KTDIA)+ZDOW*ZRELAXB)
  ZSUB1(JLON,KTDIA)=ZMUL*(ZBET(JLON,KTDIA)*ZIPOI(JLON,KTDIA)-ZDOW*ZRELAXB)
  ZN1(JLON,KTDIA)=ZMUL*(PVAR(JLON,KTDIA)-ZIPOI(JLON,KTDIA)*ZDBET(JLON,KTDIA)&
   & *(PVAR(JLON,KTDIA)-PVAR(JLON,KTDIA+1))-ZDRELAXB*ZDOW*(PVAR(JLON,KTDIA)&
   & +PVAR(JLON,KTDIA+1))+(ZDOW+ZDOW)*PVARTILD(JLON,KTDIA)+PRHS(JLON,KTDIA))

ENDDO

!     ELIMINATION POUR UNE COUCHE STANDARD.
!     ELIMINATION FOR A STANDART LEVEL.

DO JLEV=KTDIA+1,KLEV-1
  DO JLON=KIDIA,KFDIA

    ZBET(JLON,JLEV) =          PXPTKEROV(JLON,JLEV) *ZKERV(JLON,JLEV)
    ZDBET(JLON,JLEV)=(1.0_JPRB-PXPTKEROV(JLON,JLEV))*ZKERV(JLON,JLEV)
    ZDUP=0.5_JPRB*PALPH(JLON,JLEV)/PLNPR(JLON,JLEV)&
     & * ZTAUITKE(JLON,JLEV-1)
    ZDOW=0.5_JPRB*(1.0_JPRB-PALPH(JLON,JLEV)/PLNPR(JLON,JLEV))&
     & * ZTAUITKE(JLON,JLEV)

    ZELIM=ZBET(JLON,JLEV-1)*ZIPOI(JLON,JLEV)-ZDUP*ZRELAXB
    ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB1(JLON,JLEV-1)+(ZBET(JLON,JLEV-1)&
     & +ZBET(JLON,JLEV))*ZIPOI(JLON,JLEV)+(ZDUP+ZDOW)*ZRELAXB)
    ZSUB1(JLON,JLEV)=ZMUL*(ZBET(JLON,JLEV)*ZIPOI(JLON,JLEV)-ZDOW*ZRELAXB)
    ZN1(JLON,JLEV)=ZMUL*(PVAR(JLON,JLEV)-ZIPOI(JLON,JLEV)*(ZDBET(JLON,JLEV-1)&
     & *(PVAR(JLON,JLEV)-PVAR(JLON,JLEV-1))+ZDBET(JLON,JLEV)*(PVAR(JLON,JLEV)&
     & -PVAR(JLON,JLEV+1)))-ZDRELAXB*(ZDUP*(PVAR(JLON,JLEV)&
     & +PVAR(JLON,JLEV-1))+ZDOW*(PVAR(JLON,JLEV)+PVAR(JLON,JLEV+1)))&
     & +2.0_JPRB*(ZDUP*PVARTILD(JLON,JLEV-1)+ZDOW*PVARTILD(JLON,JLEV))&
     & +PRHS(JLON,JLEV)+ZELIM*ZN1(JLON,JLEV-1))
  ENDDO
ENDDO

!     CALCULS EN SURFACE.
!     SURFACE CALCULATIONS.

DO JLON=KIDIA,KFDIA
  ZDUP=0.5_JPRB*PALPH(JLON,KLEV)/PLNPR(JLON,KLEV)*&
   &  ZTAUITKE(JLON,KLEV-1)
  ZDOW=0.5_JPRB*(1.0_JPRB-PALPH(JLON,KLEV)/PLNPR(JLON,KLEV))*&
   &  ZTAUITKE(JLON,KLEV)

  ZELIM=ZBET(JLON,KLEV-1)*ZIPOI(JLON,KLEV)-ZDUP*ZRELAXB
  ZMUL=1.0_JPRB/(1.0_JPRB-ZELIM*ZSUB1(JLON,KLEV-1)+ZBET(JLON,KLEV-1)&
   & *ZIPOI(JLON,KLEV)+(ZDUP+ZDOW+ZDOW)*ZRELAXB)
  ZN1(JLON,KLEV)=ZMUL*(PVAR(JLON,KLEV)-ZIPOI(JLON,KLEV)*ZDBET(JLON,KLEV-1)&
   & *(PVAR(JLON,KLEV)-PVAR(JLON,KLEV-1))-ZDRELAXB*(ZDUP*(PVAR(JLON,KLEV)&
   & +PVAR(JLON,KLEV-1))+(ZDOW+ZDOW)*PVAR(JLON,KLEV))+2.0_JPRB*(ZDUP&
   & *PVARTILD(JLON,KLEV-1)+ZDOW*PVARTILD(JLON,KLEV))+PRHS(JLON,KLEV)&
   & +ZELIM*ZN1(JLON,KLEV-1))
  PVAR1(JLON,KLEV)=ZN1(JLON,KLEV)
ENDDO

!     SUBSTITUTION POUR UNE COUCHE STANDARD ET AU SOMMET.
!     BACK-SUBSTITUTION FOR A STANDART LAYER AND AT THE TOP.

DO JLEV=KLEV-1,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZN1(JLON,JLEV)=ZN1(JLON,JLEV)+ZSUB1(JLON,JLEV)*ZN1(JLON,JLEV+1)
    PVAR1(JLON,JLEV)=ZN1(JLON,JLEV)
  ENDDO
ENDDO

PVAR1(KIDIA:KFDIA,KTDIA:KLEV)=MAX(PVAR1(KIDIA:KFDIA,KTDIA:KLEV),ETKE_MIN)

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACPTKES',1,ZHOOK_HANDLE)
END SUBROUTINE ACPTKES
