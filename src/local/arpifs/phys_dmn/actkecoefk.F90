!OPTIONS XOPT(NOEVAL)
!OPTION! -O nochg
SUBROUTINE ACTKECOEFK ( YDPHY0,YDPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PFMTKE,PFTTKE,PAUTKE,PATTKE,&
 & PR,PT,PU,PV,PLMU,PLMT, &
 ! - INPUT  1D .
 & PGZ0,  &
 ! - OUTPUT 2D .
 & PKTROV,PKUROV,PKNROV,PXTROV,PXUROV,PXPTKEROV)  

!**** *ACTKECOEFK* - COEFFICIENTS D'ECHANGE VERTICAL TURBULENT.
!                  - COMPUTATION of "static" EXCANGE COEFICIENTS         

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES COEFFICIENTS D'ECHANGE VERTICAL TURBULENT (DIMENSION
!       (DP/(G*DT)) ET DE LA STABILITE STATIQUE (DIMENSION (U/DP)**2),
!       AINSI QUE DES FACTEURS D'AMPLIFICATION "ANTI-FIBRILLATION" DES
!       PREMIERS .
!     - COMPUTATION OF VERTICAL TURBULENT EXCHANGE COEFFICIENTS
!       (DIMENSION (DP/(G*DT)) AND OF STATIC STABILITY (DIMENSION
!       (U/DP)**2), AS WELL AS OF THE "ANTI-FIBRILLATION" AMPLIFICATION
!       FACTORS OF THE FORMERS .

!**   Interface.
!     ----------
!        *CALL* *ACTKECOEFK*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PFMTKE     : STABILITY FUNCTION F_m
! PFTTKE     : STABILITY FUNCTION F_h
! PAUTKE     : alpha_u for dry AF scheme
! PATTKE     : alpha_theta for dry AF scheme
! PLMT       : LONGUEUR DE MELANGE POUR L'ENERGIE.
! PLMU       : LONGUEUR DE MELANGE POUR LA QUANTITE DE MOUVEMENT.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PKTROV     : COEFFICIENT D'ECHANGE VERTICAL DE T ET Q EN KG/(M*M*S).
! PKUROV     : COEFFICIENT D'ECHANGE VERTICAL DE U ET V EN KG/(M*M*S).
! PKNROV     : COEFFICIENT D'ECHANGE VERTICAL NEUTRE EN KG/(M*M*S).
! PXTROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKTROV.
! PXUROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PKUROV.
! PXPTKEROV  : MULTIPLICATEUR "ANTI-FIBRILLATION" POUR "PKEROV".
!-----------------------------------------------------------------------
!     Externes.
!     ---------

!     Methode.
!     --------

!     Auteur.
!     -------
!      Ivan Bastak Duran after ACCOEFK  (September 2010)

!     Modifications.
!     --------------
!      F. Vana 15-Mar-2011: Horizontal QNSE fits
!      2011-06: M. Jerczynski - some cleaning to meet norms
!      I. Bastak-Duran, F. Vana  27-Jan-2012 : various fixes
!      I. Bastak-Duran, 20-Jan-2013 : rewriting. 
!      I. Bastak-Duran, Oct-2014 : rewriting. 
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG  
USE YOMPHY0  , ONLY : TPHY0
USE YOMPHY2  , ONLY : TPHY2

IMPLICIT NONE

TYPE(TPHY0)       ,INTENT(IN) :: YDPHY0
TYPE(TPHY2)       ,INTENT(IN) :: YDPHY2
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF   (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS    (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV       (KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFMTKE   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFTTKE   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAUTKE   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PATTKE   (KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMU     (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLMT     (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0     (KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKTROV   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKUROV   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PKNROV   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXTROV   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXUROV   (KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXPTKEROV(KLON,0:KLEV)
INTEGER(KIND=JPIM) :: JLEV, JLON

LOGICAL :: LLAFGD

REAL(KIND=JPRB) :: ZA1, ZAPRIM, ZAT, ZAU,&
 & ZB1, ZBE, ZBETA, ZBETAP, ZBPRIM, ZC1, ZCIS, &
 & ZCPRIM, ZCRIT1, ZCRIT2, &
 & ZDELTA1, ZDENUM, &
 & ZGA, ZGAMDZ, ZGKT, ZGKU, &
 & ZGLMU2, ZGLU, &
 & ZMULA2, ZRHOH, &
 & ZSDPRIM, ZC3,&
 & ZTAU, ZU, ZZ
REAL(KIND=JPRB) :: ZRTI(KLON,0:KLEV),ZDPHIA(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZSIG

!     CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('ACTKECOEFK',0,ZHOOK_HANDLE)
ASSOCIATE(ALMAV=>YDPHY0%ALMAV, GCISMIN=>YDPHY0%GCISMIN, VKARMN=>YDPHY0%VKARMN, &
 & TSPHY=>YDPHY2%TSPHY, XMULAF=>YDPHY2%XMULAF)
!   COMPUTATION OF CONSTANTS 
ZGLU=RG*ALMAV

LLAFGD=XMULAF /= 0.0_JPRB
!     ------------------------------------------------------------------
!     I - BOUCLE PASSIVE SUR LES NIVEAUX VERTICAUX.

!           PASSIVE LOOP ON VERTICAL LEVELS.
DO JLEV=KTDIA,KLEV-1

!*
!     ------------------------------------------------------------------
!     II - CALCULS PROPREMENT DITS.

!          EFFECTIVE CALCULATIONS.

  DO JLON=KIDIA,KFDIA
    ZZ=VKARMN*(0.5_JPRB*(PAPHIF(JLON,JLEV)+PAPHIF(JLON,JLEV+1))&
     & -PAPHI(JLON,KLEV)+PGZ0(JLON))
    ZGLMU2=(PLMU(JLON,JLEV)*RG)**2
    ZC3=PLMT(JLON,JLEV)/PLMU(JLON,JLEV)

    ZDPHIA(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)

!     CISAILLEMENT DE VENT.
!     WIND SHEAR.

    ZCIS=(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2+(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2&
     & +(GCISMIN*ZDPHIA(JLON,JLEV)/(RG*(1.0_JPRB+ZZ/ZGLU)))**2
    ZU=SQRT(ZCIS)
    ZRTI(JLON,JLEV)=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+&
    & PR(JLON,JLEV+1)*PT(JLON,JLEV+1))


! COMPUTAION OF EXCHANGE COEFFICIENTS K_m-PKUROV,K_h-PKTROV,K_N-PKNROV
    PKNROV(JLON,JLEV)=ZU*ZGLMU2*&
     &  PAPRS(JLON,JLEV)*ZRTI(JLON,JLEV)/ZDPHIA(JLON,JLEV)**2
    PKUROV(JLON,JLEV)=PFMTKE(JLON,JLEV)*PKNROV(JLON,JLEV)
    PKTROV(JLON,JLEV)=PFTTKE(JLON,JLEV)*ZC3*PKNROV(JLON,JLEV)
  ENDDO
ENDDO


! 'Dry' Antifibrilation scheme - BEGIN
PXUROV(KIDIA:KFDIA,KTDIA:KLEV-1)=1.0_JPRB
PXTROV(KIDIA:KFDIA,KTDIA:KLEV-1)=1.0_JPRB
PXPTKEROV(KIDIA:KFDIA,KTDIA:KLEV-1)=1.0_JPRB
    

IF (LLAFGD) THEN
  IF(XMULAF > 0.0_JPRB) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        PXUROV(JLON,JLEV)=MAX(PXUROV(JLON,JLEV),XMULAF)
        PXTROV(JLON,JLEV)=MAX(PXTROV(JLON,JLEV),XMULAF)
        PXPTKEROV(JLON,JLEV)=PXUROV(JLON,JLEV)
      ENDDO
    ENDDO
  ELSE
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        !     CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
        !     ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.

        ! alpha_u and alpha_theta - ZAU,ZAT
        ZAU=PAUTKE(JLON,JLEV)
        ZAT=PATTKE(JLON,JLEV)
        ! CONSTRAINTS FOR 'DRY' ANTIFIBRILLATION SCHEME
        !Check for AF constrains : 2-3.alpha_u+2.alpha_th<=2
        ZSIG=SIGN(0.5_JPRB,1.5_JPRB*ZAU-ZAT)+0.5_JPRB

        ! COMPUTAION OF decentering factor - beta - ZBETA - BEGIN
        ZBE=3._JPRB+ZAT-2.0_JPRB*ZAU
        ZGA=2.0_JPRB+ZSIG*(2.0_JPRB*ZAT-3._JPRB*ZAU)

        ZGAMDZ=4._JPRB*TSPHY*(RG/ZDPHIA(JLON,JLEV))
        ZRHOH=PAPRS(JLON,JLEV)*ZRTI(JLON,JLEV)
        ZGKU=ZGAMDZ*PKUROV(JLON,JLEV)/ZRHOH
        ZGKT=ZGAMDZ*PKTROV(JLON,JLEV)/ZRHOH
        ZMULA2=XMULAF**2
        ZDENUM=ZGKU*ZGKT
        ZA1=(1.0_JPRB+ZGKU)*(1.0_JPRB+ZGKT)
        ZB1=(1.0_JPRB+ZGKU)*ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*(1.0_JPRB+ZGKT)&
         & *ZGKU*(1.0_JPRB-ZAU)
        ZC1=ZDENUM*ZGA
        ZDELTA1=ZB1**2-4._JPRB*ZA1*ZC1
        ZCRIT1=MAX(SIGN(1.0_JPRB,ZDELTA1),0.0_JPRB)
        ZTAU=-0.5_JPRB * ZCRIT1*(ZB1+SQRT(ABS(ZDELTA1)))/ZA1
        ZCRIT2=MAX(SIGN(1.0_JPRB,XMULAF-ZTAU),0.0_JPRB)
        ZAPRIM=ZMULA2*ZDENUM
        ZBPRIM=ZMULA2*(ZGKU+ZGKT)+XMULAF*ZDENUM*ZBE
        ZCPRIM=ZMULA2+XMULAF*(ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*ZGKU*(1.0_JPRB-ZAU))&
         & +ZDENUM*ZGA
        ZSDPRIM=SQRT(ABS(ZBPRIM**2-4._JPRB*ZAPRIM*ZCPRIM))
        ZBETAP= 0.5_JPRB*(-ZBPRIM +ZSDPRIM)/MAX(ZAPRIM,1.0_JPRB-ZCRIT2)
        ZBETA=1.0_JPRB+ZCRIT2*(ZBETAP-1.0_JPRB)

        PXUROV(JLON,JLEV)=MAX(PXUROV(JLON,JLEV),ZBETA)
        PXTROV(JLON,JLEV)=MAX(PXTROV(JLON,JLEV),ZBETA)
        PXPTKEROV(JLON,JLEV)=PXUROV(JLON,JLEV)
      ENDDO
    ENDDO
  ENDIF
ENDIF


!     ------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACTKECOEFK',1,ZHOOK_HANDLE)
END SUBROUTINE ACTKECOEFK
