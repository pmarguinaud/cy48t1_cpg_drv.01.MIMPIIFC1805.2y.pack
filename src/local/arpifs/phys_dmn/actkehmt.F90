!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACTKEHMT(KIDIA,KFDIA,KLON,KLEV,LDZ0H,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PAPRSF,PCP,PQ,PR,PT,PU,PV,&
 & PFPLSH,PFPLCH,&
 ! - INPUT  1D .
 & PDPHIT,PDPHIV,PGZ0F,PGZ0HF,PGZ0RLF,PHV,PLSM,&
 & PNEIJG,PNEIJV,PSNS,PTS,PVEG0,PWFC,PWS,PWSI,&
 ! - INPUT  LOGIQUE .
 & LDCLS,LDHMT,&
 ! - OUTPUT 2D .
 & PNBVNO,PMRIPP,&
 ! - OUTPUT 1D .
 & PCD,PCDN,PCDROV,PCH,PCHROV,PCPS,PDQSTS,PGWDCS,&
 & PGZ0,PGZ0H,PHQ,PHU,PNEIJ,PQCLS,PQS,PQSATS,&
 & PRHCLS,PRS,PRTI,PSTAB,PTCLS,PUCLS,PVCLS,PNUCLS,PNVCLS,PZPCLS,PVEG,&
 & PXDROV,PXHROV,PURAF,PVRAF)

!**** *ACTKEHMT   * - COMPUTATION of drag coefficients PCD and PCH with AF scheme
!**** *ACTKEHMT   * - INTERP. DES PARAMETRES U,V,T,Q AUX HAUTEURS CHOISIES.

!     Sujet.
!     ------

!     - ROUTINE DE CALCUL ACTIF .
!       DETERMINATION DE 
!       LES COEFFICIENTS D'ECHANGE EN
!       SURFACE PLUS LEUR AMPLIFICATION SI "ANTI-FIBRILLATION" ACTIVEE .
!     - SI L'OPTION 'CLS' EST SELECTIONNEE :
!          DETERMINATION DE PARAMETRES SUPPLEMENTAIRES NECESSAIRES POUR
!          LES CALCULS DE DIFFUSION VERTICALE ET DE TRAINEE DES ONDES DE
!          GRAVITE OROGRAPHIQUES .
!     - SI L'OPTION 'HMT' EST SELECTIONNEE :
!          INTERPOLATION DES TEMPERATURES, HUMIDITES (SPECIFIQUES ET
!          RELATIVES) ET VENTS AUX HAUTEURS "METEO" .

!     - COMPUTATION OF 
!       SURFACE EXCHANGE COEFFICIENTS PLUS THEIR AMPLIFICATION
!       FACTOR IF "ANTI-FIBRILLATION" IS ACTIVATED .
!     - IF THE SWITCH 'CLS' IS ON :
!          COMPUTATION OF ADDITIONNAL PARAMETERS NECESSARY FOR THE
!          VERTICAL DIFFUSION AND GRAVITY WAVE DRAG PARAMETERIZATIONS .
!     - IF THE SWITCH 'HMT' IS ON :
!          TEMPERATURE, MOISTURE (SPECIFIC AND RELATIVE) AND WIND
!          INTERPOLATION AT STANDARD "METEOROLOGICAL" LEVELS .

!**   Interface.
!     ----------
!        *CALL* *ACTKEHMT*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! LDZ0H      : SI PGZ0H CONTIENT LA RUGOSITE THERMIQUE COURANTE.
! LDZ0H      : IF PGZ0H CONTAINS THERMAL RUGOSITY.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PFPLSH     : CHAMP "HISTORIQUE" DES PRECIPITATIONS STRATIFORMES.
! PFPLCH     : CHAMP "HISTORIQUE" DES PRECIPITATIONS CONVECTIVES.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (PROGNOSTIQUE) .

! PSNS       : MASSE PAR UNITE DE SURFACE DE LA COUCHE NEIGEUSE.
! PTS        : TEMPERATURE DE SURFACE.
! PWS        : CONTENU EN EAU DU RESERVOIR DE SURFACE.
! PWSI       : CONTENU EN EAU GELEE DU RESERVOIR DE SURFACE.

! - 1D (SPECIFIQUE SELON L'UTILISATION DE ACHMT) .

! PDPHIT     : HAUTEUR D INTERPOLATION POUR T ET Q (EN GEOPOTENTIEL).
! PDPHIV     : HAUTEUR D INTERPOLATION POUR U ET V (EN GEOPOTENTIEL).
! PHV        : RESISTANCE AU FLUX D'EVAPOTRANSPIRATION.
! PWFC       : TENEUR EN EAU A LA CAPACITE AU CHAMPS.

! - 1D (DIAGNOSTIQUE) .

! PNEIJG     : PROPORTION DE SOL RECOUVERTE DE NEIGE.
! PNEIJV     : PROPORTION DE VEGETATION RECOUVERTE DE NEIGE.

! - 1D (GEOGRAPHIQUE) .

! PGZ0F      : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE.
! PGZ0HF     : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE THERM.
! PGZ0RLF    : GRAVITE * LONGUEUR DE RUGOSITE DUE AU RELIEF.
! PLSM       : INDICE TERRE/MER.
! PVEG0      : PROPORTION DE SOL COUVERTE DE VEGETATION.

! - LOGIQUES .

! LDCLS      : DETERMINATION COMPLETE DES CARACTERISTIQUES DE SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PNBVNO     : CARRE DE FR. BRUNT-VAISALA DIVISEE PAR G FOIS LA DENSITE.
! PMRIPP     : RICHARDSON NUMBER

! - 1D (DIAGNOSTIQUE) .

! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PCDROV     : PCD RENORME EN DENSITE FOIS VITESSE.
! PCH        : COEFFICIENT D'ECHANGE EN SURFACE POUR T ET Q.
! PCHROV     : PCH RENORME EN DENSITE FOIS VITESSE.
! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.
! PDQSTS     : DERIVEE DE PQSATS PAR RAPPORT A LA TEMPERATURE.
! PGWDCS     : DENSITE EN SURFACE POUR LE DRAG OROGRAPHIQUE.
! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PGZ0H      : G*LONGUEUR DE RUGOSITE THERMIQUE COURANTE (SI LDZ0H).
! PHQ        : POIDS DE L'HUMIDITE DE L'AIR DANS L'HUMIDITE DE SURFACE.
! PHU        : POIDS DE L'HUMIDITE SATURANTE DANS L'HUMIDITE DE SURFACE.
! PNEIJ      : PROPORTION DE LA MAILLE RECOUVERTE DE NEIGE.
! PQCLS      : SORTIE DIAGNOSTIQUE DE L'HUMIDITE SPECIFIQUE A HTQ METEO.
! PQS        : HUMIDITE SPECIFIQUE DE SURFACE.
! PQSATS     : HUMIDITE SPECIFIQUE DE SATURATION EN SURFACE.
! PRHCLS     : SORTIE DIAGNOSTIQUE DE L'HUMIDITE RELATIVE A HTQ METEO.
! PRS        : CONSTANTE DES GAZ POUR L'AIR AU SOL.
! PRTI       : INVERSE DE R*T.
! PSTAB      : INDICE DE STABILITE A LA SURFACE.
! PTCLS      : SORTIE DIAGNOSTIQUE DE LA TEMPERATURE A HTQ METEO.
! PUCLS      : SORTIE DIAGNOSTIQUE DU VENT EN X A HUV METEO.
! PVCLS      : SORTIE DIAGNOSTIQUE DU VENT EN Y A HUV METEO.
! PNUCLS     : SORTIE DIAGNOSTIQUE DU VENT NEUTRE EN X A HUV METEO.
! PNVCLS     : SORTIE DIAGNOSTIQUE DU VENT NEUTRE EN Y A HUV METEO.
! PZPCLS     : SORTIE DIAGNOSTIQUE DE PRESSION A HTQ METEO.
! PVEG       : PROPORTION DE VEGETATION APPARENTE (NON RECOUVERTE DE NEIGE).
! PXDROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PCDROV.
! PXHROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PCHROV.
! PURAF      : SORTIE DIAGNOSTIQUE DU VENT DE RAFALES TURBULENTES EN X
! PVRAF      : SORTIE DIAGNOSTIQUE DU VENT DE RAFALES TURBULENTES EN Y

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     *ACRI2PR*
!     *ACNTCLS*

!     Methode.
!     --------

!     Auteur.
!     -------
!        2012-01, I. Bastak-Duran after ACHMT.

!     Modifications.
!     --------------
!       I. Bastak-Duran, F. Vana 27-Jan-2012 : various fixes and updates
!       I. Bastak-Duran 15-Jul-2012 : major rewriting.
!       I. Bastak-Duran 20-Oct-2014 : adapting entry/output to ACHMT &
!                                     new surface handling.
!       J. Masek           Sep-2016 : Correct interpolation for lowest
!                                     model level below observation height.
!       J. Masek           Sep-2018 :
!       Bug corrections and updates in LZ0HSREL=T branch: consistent
!       snow fraction for roughness and albedo; single value of dynamical
!       roughness; quadratic averaging. Corrected also thermal roughness
!       of snow for LSNV=T and LZ0HSREL=F (missing factor STHER).
!       J. Masek           Oct-2020 : Introduction of ACRI2PR.
!-----------------------------------------------------------------------
! WARNING: Argument PGZ0RLF is available only for LSNV=T! For LSNV=F
! and LZ0HSREL=T it can be reconstructed from PGZ0F, PGZ0HF and STHER.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY :  RG       ,RCPV     ,RETV     ,&
 & RCS      ,RLVTT    ,RLSTT    ,RTT      ,RALPW    ,&
 & RBETW    ,RGAMW    ,RALPS    ,RBETS    ,RGAMS    ,&
 & RALPD    ,RBETD    ,RGAMD    ,RCW      ,RV       ,&
 & RPI      ,RD       ,RCPD     ,RKAPPA

USE YOMPHY   , ONLY : YRPHY
USE YOMPHY0  , ONLY : YRPHY0
USE YOMPHY1  , ONLY : YRPHY1
USE YOMCLI   , ONLY : YRCLI
USE YOMPHY2  , ONLY : YRPHY2
USE YOMQNSE  , ONLY : RQNSE_S0,RQNSE_GS1,RQNSE_GS2,RQNSE_GS3,RQNSE_GS4,&
 & RQNSE_GU1,RQNSE_GU2
USE YOMLOUIS  , ONLY : YRLOUIS

!-----------------------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
LOGICAL, INTENT(IN) :: LDZ0H
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDPHIT(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDPHIV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0F(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0HF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0RLF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVEG0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWFC(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSI(KLON) 
LOGICAL           ,INTENT(IN)    :: LDCLS 
LOGICAL           ,INTENT(IN)    :: LDHMT 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PNBVNO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMRIPP(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCD(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDN(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCDROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCH(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDQSTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWDCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0H(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHQ(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHU(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEIJ(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQSATS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRHCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRTI(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTAB(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNUCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNVCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PZPCLS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVEG(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXDROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PURAF(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PVRAF(KLON) 

REAL(KIND=JPRB) :: ZDELT(KLON),ZDPHI(KLON,KLEV),ZESP(KLON)&
 & ,ZU(KLON),ZCIS(KLON)


INTEGER(KIND=JPIM) :: JLON, JLEV, ITTYPE

LOGICAL :: LLAFGD, LLM123

REAL(KIND=JPRB) :: ZA1, ZAPRIM, ZAT, ZAU,&
 & ZB1, ZBE, ZBETA, ZBETAP, ZBPRIM, ZC1, &
 & ZCPRIM,ZCRIT1, ZCRIT2, ZDELTA1, &
 & ZDENUM,ZEW, ZGA, ZGAMDZ, ZGKT, ZGKU,  &
 & ZMULA2, ZSDPRIM, ZTAU, ZSCRAF, ZEPS,ZRCORR,ZEPS8
REAL(KIND=JPRB) :: ZSIG
REAL(KIND=JPRB) :: ZRITKE,ZSTA,&
& ZDRRI,ZDPRI,ZDRRI_U,ZDPRI_U,ZDRIFTKE_U,ZEFB_UP,ZCD,ZFP,&
& ZDCHI3TKE_U, ZDPHI3TKE_U,ZFSFTKE

REAL(KIND=JPRB) :: ZAUTKE(KLON), ZATTKE(KLON),ZSIG_AF(KLON)
REAL(KIND=JPRB) :: ZMRIFPP(KLON),ZCHI3TA(KLON),ZPHI3TA(KLON),&
& ZFMTKE(KLON),ZFTTKE(KLON),ZDRIFTKE(KLON),ZDCHI3TKE(KLON),&
& ZDPHI3TKE(KLON),ZFMTKE_DX(KLON),ZFHTKE_DX(KLON),&
& ZFFTKE_DX(KLON),ZFFTKE(KLON)
REAL(KIND=JPRB) :: ZCDNH(KLON) 
REAL(KIND=JPRB) :: ZETKE_R(KLON),ZETKE_P(KLON)
REAL(KIND=JPRB) :: ZCPVMD,ZRVMD,ZGZ0N,ZWFC,ZWS,ZDEW,ZRZD,ZRZH,Z0CR,ZUZ0CN
REAL(KIND=JPRB) :: ZHOOK_HANDLE

TYPE(MODEL_PHYSICS_MF_TYPE) :: YLML_PHY_MF

!-----------------------------------------------------------------------

#include "acri2pr.intfb.h"
#include "actkecls.intfb.h"

#include "fcttrm.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACTKEHMT',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YRPHY0%GCISMIN, VKARMN=>YRPHY0%VKARMN, &
 & C3TKEFREE=>YRPHY0%C3TKEFREE, VZIUSTAR0=>YRPHY0%VZIUSTAR0, &
 & UTILGUST=>YRPHY0%UTILGUST, ETKE_OLAM=>YRPHY0%ETKE_OLAM, &
 & ETKE_R=>YRPHY0%ETKE_R, EFB_UR=>YRPHY0%EFB_UR, EFB_AZ0=>YRPHY0%EFB_AZ0, &
 & REFB_1=>YRPHY0%REFB_1, REFB_2=>YRPHY0%REFB_2, REFB_3=>YRPHY0%REFB_3, &
 & ETKE_RIFC=>YRPHY0%ETKE_RIFC, RRGAMMA=>YRPHY0%RRGAMMA, &
 & RRSCALE=>YRPHY0%RRSCALE, &
 & RD1=>YRPHY1%RD1, WSMX=>YRPHY1%WSMX, ALRCN2=>YRPHY1%ALRCN2, &
 & ALRCN1=>YRPHY1%ALRCN1, WCRIN=>YRPHY1%WCRIN, GCONV=>YRPHY1%GCONV, &
 & RZHZ0M=>YRPHY1%RZHZ0M, &
 & TSPHY=>YRPHY2%TSPHY, GZ0RAF=>YRPHY2%GZ0RAF, LRAFTUR=>YRPHY2%LRAFTUR, &
 & XMULAF=>YRPHY2%XMULAF, FACRAF=>YRPHY2%FACRAF, &
 & RLOUIS_S0=>YRLOUIS%RLOUIS_S0, RLOUIS_GS4=>YRLOUIS%RLOUIS_GS4, &
 & RLOUIS_GS3=>YRLOUIS%RLOUIS_GS3, RLOUIS_GS2=>YRLOUIS%RLOUIS_GS2, &
 & RLOUIS_GS1=>YRLOUIS%RLOUIS_GS1, PLOUIS_S0=>YRLOUIS%PLOUIS_S0, &
 & RLOUIS_GU1=>YRLOUIS%RLOUIS_GU1, RLOUIS_GU2=>YRLOUIS%RLOUIS_GU2, &
 & PLOUIS_GU2=>YRLOUIS%PLOUIS_GU2, PLOUIS_GU1=>YRLOUIS%PLOUIS_GU1, &
 & PLOUIS_GS1=>YRLOUIS%PLOUIS_GS1, PLOUIS_GS3=>YRLOUIS%PLOUIS_GS3, &
 & PLOUIS_GS2=>YRLOUIS%PLOUIS_GS2, PLOUIS_GS4=>YRLOUIS%PLOUIS_GS4, &
 & LCOEFK_F1=>YRPHY%LCOEFK_F1, LSOLV=>YRPHY%LSOLV, CGTURS=>YRPHY%CGTURS, &
 & LNEIGE=>YRPHY%LNEIGE, LSNV=>YRPHY%LSNV, LZ0HSREL=>YRPHY%LZ0HSREL, &
 & LRRGUST=>YRPHY%LRRGUST, LFGEL=>YRPHY%LFGEL)
!-----------------------------------------------------------------------
!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE

!         COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANTS 

ZCPVMD=RCPV-RCPD
ZRVMD=RV-RD


!   ZGZ0N   : VALEUR MIN. DE G FOIS LA LONG. DE RUG. DYN. SANS RELIEF.
!            : MIN VALUE OF G * DYN. ROUGHNESS L. WITHOUT TOPOGRAPHY.

ZGZ0N=RG*YRCLI%SZZ0D

!*
!     ------------------------------------------------------------------
!     I- PASSAGE EN GEOPOTENTIEL POUR LES CONSTANTES RELATIVES A LA
!     LONGUEUR DE RUGOSITE EN CAS DE NEIGE.

!           GOING TO GEOPOTENTIAL FOR CONSTANTS RELATIVE TO ROUGHNESS
!     LENGTH IN CASE OF SNOW COVER.

Z0CR=RG*ALRCN1
ZUZ0CN=1.0_JPRB/(RG*ALRCN2)

!   security constant
ZEPS    = EPSILON(1.0_JPRB)*100.0_JPRB
ZEPS8=1E-8_JPRB



IF     (TRIM(CGTURS) == 'RMC') THEN
  ITTYPE=1
ELSEIF (TRIM(CGTURS) == 'MD1') THEN
  ITTYPE=2
ELSEIF (TRIM(CGTURS) == 'MD2') THEN
  ITTYPE=3
ELSEIF (TRIM(CGTURS) == 'QNSE') THEN
  ITTYPE=4
ELSEIF (TRIM(CGTURS) == 'EFB') THEN
  ITTYPE=5
ELSEIF (TRIM(CGTURS) == 'LOUIS') THEN
  ITTYPE=6
ENDIF

LLM123=(ITTYPE == 1).OR.(ITTYPE == 2).OR.(ITTYPE == 3)
LLAFGD=XMULAF /= 0.0_JPRB

!*
!     ------------------------------------------------------------------
!     II - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.

! ZDPHI     : FULL LEVEL GEOPOTENTIAL RELATIVE TO SURFACE.
DO JLEV=1,KLEV
  DO JLON=KIDIA,KFDIA
    ZDPHI(JLON,JLEV)=PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
! ZU        : MODULE DU VENT DE SURFACE.
!           : SURFACE WIND SPEED.

  ZCIS(JLON)=PU(JLON,KLEV)**2+PV(JLON,KLEV)**2+&
   &(GCISMIN*ZDPHI(JLON,KLEV)/RG)**2
  ZU(JLON)=SQRT(ZCIS(JLON))

!     CALCULS CONCERNANT LA PRESENCE OU NON DE NEIGE AU SOL.
!     COMPUTATIONS CONCERNING THE PRESENCE OR ABSENCE OF SNOW ON GROUND.

! - TEMPORAIRE(S) 1D .

! ZDELT     : UN POUR CALCULS DE SATURATION SUR GLACE, ZERO SINON.
!           : ONE FOR SATURATION CALCULATION FOR ICE, ZERO OTHERWISE.

  IF (LNEIGE) THEN
    IF (LSNV) THEN
      PNEIJ(JLON)=PLSM(JLON)* &
       & ((1.0_JPRB-PVEG0(JLON))*PNEIJG(JLON) + PVEG0(JLON)*PNEIJV(JLON))  
      PVEG (JLON)=(1.0_JPRB-PNEIJV(JLON))*PVEG0(JLON)
      PGZ0 (JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0F(JLON)**2+ &
       & PNEIJV(JLON)*(Z0CR**2+PGZ0RLF(JLON)**2))
    ELSEIF (LZ0HSREL) THEN
      ! consistent new treatment
      PNEIJ(JLON)=PNEIJG(JLON)+PVEG0(JLON)*(PNEIJV(JLON)-PNEIJG(JLON))
      PVEG (JLON)=(1.0_JPRB-PNEIJV(JLON))*PVEG0(JLON)
      PGZ0 (JLON)=SQRT((1.0_JPRB-PNEIJ(JLON))*PGZ0F(JLON)**2+PNEIJ(JLON)* &
       & (Z0CR**2+MAX(PGZ0F(JLON)**2-(PGZ0HF(JLON)/YRCLI%STHER)**2,0._JPRB)))
    ELSE
      ! inconsistent old treatment
      PNEIJ(JLON)=PNEIJG(JLON)
      PVEG (JLON)=PVEG0(JLON)
      PGZ0 (JLON)=PGZ0F(JLON)+(Z0CR-PGZ0F(JLON))*PLSM(JLON)&
       & *PSNS(JLON)/(PSNS(JLON)+WCRIN*(1+ZUZ0CN*PGZ0F(JLON)))  
    ENDIF
    ZDELT(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PTS(JLON)))
  ELSE
    PNEIJ(JLON)=0.0_JPRB
    PVEG (JLON)=PVEG0(JLON)
    PGZ0 (JLON)=PGZ0F(JLON)
    ZDELT(JLON)=0.0_JPRB
  ENDIF

!     CAS DE DEUX LONGUEURS DE RUGOSITE (SUR CONTINENT NON TOTALEMENT
!     COUVERT DE NEIGE).
!     CASE OF TWO ROUGHNESS LENGTHS (ON PARTIALLY SNOW-FREE LAND ONLY).

  IF (LDZ0H) THEN

    IF (LNEIGE) THEN
      IF (LSNV) THEN
        IF (LZ0HSREL) THEN
          PGZ0H(JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0HF(JLON)**2+ &
           & PNEIJV(JLON)*(Z0CR*YRCLI%STHER)**2)
        ELSE
          PGZ0H(JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0HF(JLON)**2+ &
           & PNEIJV(JLON)*((Z0CR*YRCLI%STHER)**2+(PGZ0RLF(JLON)*YRCLI%STHER)**2))
        ENDIF
      ELSEIF (LZ0HSREL) THEN
        PGZ0H(JLON)=SQRT((1.0_JPRB-PNEIJ(JLON))*PGZ0HF(JLON)**2+ &
         & PNEIJ(JLON)*(Z0CR*YRCLI%STHER)**2)
      ELSE
        ! bugged, kept for ALARO-1 backward compatibility: linear
        ! averaging of roughness between ground with subgrid
        ! orography and snow without subgrid orography; missing
        ! factor STHER at Z0CR; third value of snow fraction introduced
        PGZ0H(JLON)=PGZ0HF(JLON)+(Z0CR-PGZ0HF(JLON))*PLSM(JLON)&
         & *PSNS(JLON)/(PSNS(JLON)+WCRIN*(1+ZUZ0CN*PGZ0HF(JLON)))
      ENDIF
    ELSE
      PGZ0H(JLON)=PGZ0HF(JLON)
    ENDIF

! PGZ0H = ZGZ0 OVER SEA UNLESS REQUIRED IN NAMPHY1 (CLIMATE RUNS)

     IF (VZIUSTAR0 > 0) THEN
       PGZ0H(JLON)=PLSM(JLON)*PGZ0H(JLON)&
        & +(1.0_JPRB-PLSM(JLON))*PGZ0HF(JLON)
     ELSE    
       PGZ0H(JLON)=MIN(PGZ0(JLON),PLSM(JLON)*PGZ0H(JLON)&
        & +(1.0_JPRB-PLSM(JLON))*PGZ0(JLON)*RZHZ0M)  
     ENDIF

  ELSE

    PGZ0H(JLON)=PGZ0(JLON)

  ENDIF

!     CALCUL DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATION WITH CALL TO DEFINED FUNCTIONS.

! - TEMPORAIRE(S) 1D .

! ZESP      : PRESSION DE VAPEUR SATURANTE SUR PRESSION.
!            : SATURATION WATER VAPOUR PRESSURE OVER PRESSURE.

  ZEW= FOEW (PTS(JLON),ZDELT(JLON))
  ZESP(JLON)=ZEW/PAPRS(JLON,KLEV)
  PQSATS(JLON)= FOQS (ZESP(JLON))

!     CALCULS RELATIFS A L'HUMIDITE DE SURFACE.
!     COMPUTATIONS CONCERNING SURFACE HUMIDITY.

!     CALCULS OPTIONNELS SUIVANT LE TYPE DE SCHEMA SOL/VEG CHOISI.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN SOIL/VEG SCHEME.

  IF (LSOLV) THEN
    ZWFC=PWFC(JLON)*RD1*GCONV
    IF (LFGEL) THEN
      ZWS=PWS(JLON)+PWSI(JLON)
    ELSE
      ZWS=PWS(JLON)
    ENDIF
    IF (LSNV) THEN
      PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJG(JLON))*&
       & (0.5_JPRB*(1.0_JPRB-COS(RPI*MIN(ZWS/ZWFC,1.0_JPRB)))-1.0_JPRB)  
      PHU(JLON)=MAX(PHU(JLON),MIN(PQ(JLON,KLEV)/PQSATS(JLON),1.0_JPRB))
      PQS(JLON)= ( PVEG(JLON)*PHV(JLON) +&
       & PVEG0(JLON)*PNEIJV(JLON) +&
       & (1.0_JPRB-PVEG0(JLON))*PHU(JLON) ) * PQSATS(JLON) +&
       & (1.0_JPRB-PHV(JLON))*PVEG(JLON) * PQ(JLON,KLEV)  
    ELSE
      PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJ(JLON))*&
       & (0.5_JPRB*(1.0_JPRB-COS(RPI*MIN(ZWS/ZWFC,1.0_JPRB)))-1.0_JPRB)  
      PHU(JLON)=MAX(PHU(JLON),MIN(PQ(JLON,KLEV)/PQSATS(JLON),1.0_JPRB))
      PQS(JLON)= ( PVEG(JLON)*PHV(JLON) +&
       & (1.0_JPRB-PVEG(JLON))*PHU(JLON)  ) * PQSATS(JLON) +&
       & (1.0_JPRB-PHV(JLON))*PVEG(JLON) * PQ(JLON,KLEV)  
    ENDIF
  ELSE
    PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJ(JLON))&
     & *(0.5_JPRB*(1.0_JPRB-COS(RPI*PWS(JLON)/WSMX))-1.0_JPRB)  
    PHQ(JLON)=(1.0_JPRB-PHU(JLON))*PVEG(JLON)
    ZDEW=MAX(0.0_JPRB,SIGN(1.0_JPRB,PQ(JLON,KLEV)-PQSATS(JLON)))
    PHU(JLON)=PHU(JLON)+ZDEW*PHQ(JLON)
    PHQ(JLON)=PHQ(JLON)-ZDEW*PHQ(JLON)
    PQS(JLON)=PHU(JLON)*PQSATS(JLON)+PHQ(JLON)*PQ(JLON,KLEV)
  ENDIF

  PCPS(JLON)=RCPD+ZCPVMD*PQS(JLON)
  PRS(JLON)=RD+ZRVMD*PQS(JLON)

!     CALCULS GEOMETRIQUES.
!     GEOMETRIC CALCULATIONS.

  ZRZD=1.0_JPRB+ZDPHI(JLON,KLEV)/PGZ0(JLON)
  PCDN(JLON)=(VKARMN/LOG(ZRZD))**2

!     CALCUL PRELIMINAIRES NECESSAIRES A LA DETERMINATION DES
!     COEFFICIENTS D'ECHANGE CD ET CH DANS LE CAS DE DEUX RUGOSITES
!     DYNAMIQUE Z0 ET THERMIQUE ZOH.

!     PRELIMINARY COMPUTATIONS NECESSARY FOR THE DETERMINATION OF
!     EXCHANGE COEFFICIENTS CD AND CH IN THE CASE OF TWO ROUGHNESS
!     LENGTHS Z0 DYNAMICAL AND ZOH THERMAL.

! - TEMPORAIRE(S) 1D .

! ZCDNH     : COEFFICIENT THERMIQUE NEUTRE D'ECHANGE EN SURFACE.
!            : NEUTRAL SURFACE THERMAL EXCHANGE COEFFICIENT.

  ZRZH=1.0_JPRB+ZDPHI(JLON,KLEV)/PGZ0H(JLON)
  ZCDNH(JLON)=C3TKEFREE*VKARMN**2/(LOG(ZRZH)*LOG(ZRZD))

  PRTI(JLON)=2.0_JPRB/(PR(JLON,KLEV)*PT(JLON,KLEV)+RKAPPA*ZDPHI(JLON,KLEV)&
  & +PRS(JLON)*PTS(JLON))  

! ZSTA      : APPROXIMATION DE DELTA(PHI)*DELTA(LN(THETA)).
!           : APPROXIMATION OF DELTA(PHI)*DELTA(LN(THETA)).
  ZSTA=ZDPHI(JLON,KLEV)*(PR(JLON,KLEV)*PT(JLON,KLEV)+RKAPPA &
   &  *ZDPHI(JLON,KLEV)-PRS(JLON)*PTS(JLON))*PRTI(JLON)

  ! Ri_dry = Ri*,Ri** at surface
  PMRIPP(JLON,KLEV)=ZSTA/ZCIS(JLON)
  ! protection against division by 0
  PMRIPP(JLON,KLEV)=SIGN(MAX(ZEPS,ABS(PMRIPP(JLON,KLEV))),PMRIPP(JLON,KLEV))
ENDDO

!*
!     ------------------------------------------------------------------
!     III - COMPUTATION OF STABILITY DEPENDENCY FUNCTIONS

! surface: convert Ri to P and R
! (vertical loop KTDIA:KLEV-1 is suppressed by passing KTDIA=0, KLEV=1)
YLML_PHY_MF%YRPHY0=YRPHY0
CALL ACRI2PR(YLML_PHY_MF,KIDIA,KFDIA,KLON,0,1,ITTYPE,&
 & PMRIPP(1,KLEV),ZETKE_P,ZETKE_R)

! convert gradient Ri to Rif
DO JLON=KIDIA,KFDIA
  ZRITKE=PMRIPP(JLON,KLEV)
  ZMRIFPP(JLON)=-(SQRT((ZETKE_R(JLON)*(ZRITKE*C3TKEFREE+ZETKE_P(JLON)))**2- &
  & 4.0_JPRB*ZRITKE*C3TKEFREE*ZETKE_R(JLON)*ZETKE_P(JLON)**2)- &
  & (ZRITKE*C3TKEFREE+ZETKE_P(JLON))*ZETKE_R(JLON))/(2.0_JPRB*ZETKE_P(JLON))
  !security
  ZMRIFPP(JLON)=MIN(ZMRIFPP(JLON),ETKE_RIFC-ZEPS8)
ENDDO

!     CALCULS DE UN SUR G FOIS LA FREQUENCE DE BRUNT-VAISALA DIVISEE PAR
!     LA DENSITE LE TOUT AU CARRE DE MEME QUE LA DENSITE (POUR G.W.D.).
!     COMPUTATION OF ONE OVER G TIME THE BRUNT-VAISALA FREQUENCY DIVIDED
!     BY DENSITY THE WHOLE BEING SQUARED AS WELL AS DENSITY (FOR G.W.D.)
IF (LDCLS) THEN
  DO JLON=KIDIA,KFDIA
    PNBVNO(JLON,KLEV)=PMRIPP(JLON,KLEV)*ZCIS(JLON)/(PAPRS(JLON,KLEV) &
   &   *PRTI(JLON)*ZDPHI(JLON,KLEV))**2
  ENDDO
ENDIF

DO JLON=KIDIA,KFDIA
  ZCHI3TA(JLON)=(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_R(JLON))&
  & /(1.0_JPRB-ZMRIFPP(JLON))
  ZPHI3TA(JLON)=(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_P(JLON))&
  & /(1.0_JPRB-ZMRIFPP(JLON))
ENDDO

IF(LCOEFK_F1) THEN
  DO JLON=KIDIA,KFDIA
   ZSIG=MAX(0._JPRB,SIGN(1._JPRB,ZMRIFPP(JLON)))
   ZFSFTKE=(1.0_JPRB-ZSIG)+ZSIG*SQRT(ZCHI3TA(JLON)-&
   & C3TKEFREE*ZPHI3TA(JLON)*PMRIPP(JLON,KLEV))
   ZFMTKE(JLON)=ZCHI3TA(JLON)*ZFSFTKE
   ZFTTKE(JLON)=ZPHI3TA(JLON)*ZFSFTKE
  ENDDO
ELSE
  DO JLON=KIDIA,KFDIA
    ZFMTKE(JLON)=ZCHI3TA(JLON)*SQRT(ZCHI3TA(JLON)*(1._JPRB-ZMRIFPP(JLON)))
    ZFTTKE(JLON)=ZFMTKE(JLON)*ZPHI3TA(JLON)/ZCHI3TA(JLON)
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     IV - COMPUTATION OF DRAG COEFFICIENTS


DO JLON=KIDIA,KFDIA
  ! COMPUTAION OF DRAG COEFFICIENTS PCD,PCH
  !     CALCULS POUR LES COMPOSANTES DU VENT DE CD ET DE SON PRODUIT PAR
  !     DENSITE FOIS MODULE DU VENT.
  !     COMPUTATIONS FOR MOMENTUM OF CD AND OF ITS PRODUCT BY DENSITY
  !     TIME WIND SPEED.

  PCD(JLON)=ZFMTKE(JLON)*PCDN(JLON)

  !     IDEM POUR LA TEMPERATURE ET L'HUMIDITE.
  !     THE SAME FOR TEMPERATURE AND HUMIDITY.

  PCH(JLON)=ZFTTKE(JLON)*ZCDNH(JLON)
  PSTAB(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,PMRIPP(JLON,KLEV)))
ENDDO

!*
!     ------------------------------------------------------------------
!     V - CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
!     ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.
IF (LLAFGD) THEN
  DO JLON=KIDIA,KFDIA
        ZSIG_AF(JLON)=SIGN(0.5_JPRB,ZMRIFPP(JLON))+0.5_JPRB
  ENDDO

  IF (LLM123) THEN

    DO JLON=KIDIA,KFDIA
      ZDRIFTKE(JLON)=(ETKE_RIFC-ZMRIFPP(JLON))*C3TKEFREE*ETKE_R/&
       & ((PMRIPP(JLON,KLEV)*C3TKEFREE+ETKE_RIFC)*ETKE_R-2.0_JPRB*      &
       & ZMRIFPP(JLON)*ETKE_RIFC)
      ZDPHI3TKE(JLON)=(ETKE_RIFC-1.0_JPRB)/&
       & ((ZMRIFPP(JLON)-1.0_JPRB)**2*ETKE_RIFC)*ZDRIFTKE(JLON)
      ZDCHI3TKE(JLON)=(ETKE_R-1.0_JPRB)/&
       & ((ZMRIFPP(JLON)-1.0_JPRB)**2*ETKE_R)*ZDRIFTKE(JLON)
    ENDDO

  ELSEIF (ITTYPE==4) THEN

    DO JLON=KIDIA,KFDIA

      ! unstable
      ZDRRI_U=RQNSE_GU1/(RQNSE_GU2*PMRIPP(JLON,KLEV)+1.0_JPRB) &
       & -(RQNSE_GU2*(RQNSE_GU1*PMRIPP(JLON,KLEV)+RQNSE_S0))/  &
       & (RQNSE_GU2*PMRIPP(JLON,KLEV)+1)**2
      ! stable
      ZDRRI=(2.0_JPRB*RQNSE_GS1*PMRIPP(JLON,KLEV)+RQNSE_GS2)/&
       & (RQNSE_GS3*PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*RQNSE_GS4+1.0_JPRB)- &
       & ((2.0_JPRB*RQNSE_GS3*PMRIPP(JLON,KLEV)+RQNSE_GS4)                        &
       & *(RQNSE_GS1*PMRIPP(JLON,KLEV)**2+RQNSE_S0+PMRIPP(JLON,KLEV)*RQNSE_GS2))&
       & /(RQNSE_GS3*PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*RQNSE_GS4+1.0_JPRB)**2
      ZDRRI=ZSIG_AF(JLON)*ZDRRI+(1.0_JPRB-ZSIG_AF(JLON))*ZDRRI_U

      ZDRIFTKE(JLON)=(ZETKE_P(JLON)*ZDRRI*ZMRIFPP(JLON)**3&
       & +(C3TKEFREE*ZETKE_R(JLON)**2-ZETKE_P(JLON)**2*ZDRRI)*  &
       & ZMRIFPP(JLON)**2-2.0_JPRB*C3TKEFREE*ZETKE_P(JLON)*     &
       & ZETKE_R(JLON)**2*ZMRIFPP(JLON)+C3TKEFREE*              &
       & ZETKE_P(JLON)**2*ZETKE_R(JLON)**2)                     &
       & /(ZETKE_P(JLON)*ZETKE_R(JLON)*ZMRIFPP(JLON)**2    &
       & -2.0_JPRB*ZETKE_P(JLON)**2*ZETKE_R(JLON)*              &
       & ZMRIFPP(JLON)+ZETKE_P(JLON)**2*ZETKE_R(JLON)**2)

      ZDCHI3TKE(JLON)=((ZDRRI*ZMRIFPP(JLON))/ZETKE_R(JLON)**2    &
       & -ZDRIFTKE(JLON)/ZETKE_R(JLON))/(1.0_JPRB-ZMRIFPP(JLON))  &
       & +(ZDRIFTKE(JLON)*(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_R(JLON))) &
       & /(1.0_JPRB-ZMRIFPP(JLON))**2
      ZDPHI3TKE(JLON)=(ZDRIFTKE(JLON)*              &
       & (1.0_JPRB-ZMRIFPP(JLON)/ZETKE_P(JLON)))     &
       & /(1.0_JPRB-ZMRIFPP(JLON))**2-ZDRIFTKE(JLON)/&
       & (ZETKE_P(JLON)*(1.0_JPRB-ZMRIFPP(JLON)))

    ENDDO

  ELSEIF (ITTYPE==5) THEN

    DO JLON=KIDIA,KFDIA

      ! unstable
      ZEFB_UP=EFB_UR&
       & /(ETKE_OLAM*EFB_UR/(EFB_AZ0*C3TKEFREE)+1.0_JPRB)
      ZDRIFTKE_U=(ZEFB_UP-ZMRIFPP(JLON))*C3TKEFREE*EFB_UR/     &
       &         ((PMRIPP(JLON,KLEV)*C3TKEFREE+ZEFB_UP)*EFB_UR &
       & -2.0_JPRB*ZMRIFPP(JLON)*ZEFB_UP)
      ZDPHI3TKE_U=(ZEFB_UP-1.0_JPRB)/ &
       & ((ZMRIFPP(JLON)-1.0_JPRB)**2*ZEFB_UP)*ZDRIFTKE_U
      ZDCHI3TKE_U=(EFB_UR-1.0_JPRB)/ &
       & ((ZMRIFPP(JLON)-1.0_JPRB)**2*EFB_UR)*ZDRIFTKE_U

      ! stable
      ZDRIFTKE(JLON)=(C3TKEFREE*EFB_AZ0*REFB_2*ZMRIFPP(JLON)**2+           &
       & ETKE_OLAM*REFB_1*ZMRIFPP(JLON)**2+EFB_UR*ETKE_OLAM*REFB_3*        &
       & ZMRIFPP(JLON)+C3TKEFREE*EFB_AZ0*REFB_3*ZMRIFPP(JLON)-             &
       & C3TKEFREE*EFB_AZ0*REFB_1*ZMRIFPP(JLON)-                           &
       & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_3)**2/(EFB_AZ0*(C3TKEFREE*EFB_AZ0*  &
       & REFB_2**2*ZMRIFPP(JLON)**4+ETKE_OLAM*REFB_1*REFB_2                &
       & *ZMRIFPP(JLON)**4+2.0_JPRB*EFB_UR*ETKE_OLAM*REFB_2*REFB_3*        &
       & ZMRIFPP(JLON)**3+2.0_JPRB*C3TKEFREE*EFB_AZ0*REFB_2*REFB_3*        &
       & ZMRIFPP(JLON)**3-2.0_JPRB*C3TKEFREE*EFB_AZ0*                      &
       & REFB_1*REFB_2*ZMRIFPP(JLON)**3+EFB_UR*ETKE_OLAM*REFB_3**2*        &
       & ZMRIFPP(JLON)**2+C3TKEFREE*EFB_AZ0*REFB_3**2*ZMRIFPP(JLON)**2-    &
       & 2.0_JPRB*C3TKEFREE*EFB_AZ0*EFB_UR*REFB_2*REFB_3*ZMRIFPP(JLON)**2- &
       & 2.0_JPRB*C3TKEFREE*EFB_AZ0*REFB_1*REFB_3*ZMRIFPP(JLON)**2         &
       & +C3TKEFREE*EFB_AZ0*REFB_1**2*ZMRIFPP(JLON)**2-2.0_JPRB*           &
       & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_3**2*ZMRIFPP(JLON)+2.0_JPRB*        &
       & C3TKEFREE*EFB_AZ0*EFB_UR*REFB_1*REFB_3*                           &
       & ZMRIFPP(JLON)+C3TKEFREE*EFB_AZ0*EFB_UR**2*REFB_3**2))

      ZDRRI=-((EFB_UR*REFB_2-REFB_1)*REFB_3*ZDRIFTKE(JLON)) &
       & /(REFB_2**2*ZMRIFPP(JLON)**2+2.0_JPRB*REFB_2*      &
       & REFB_3*ZMRIFPP(JLON)+REFB_3**2)

      ZDPRI=-((C3TKEFREE**2*EFB_AZ0**2*EFB_UR*REFB_2-C3TKEFREE**2      &
       & *EFB_AZ0**2*REFB_1)*REFB_3*ZDRIFTKE(JLON))/                   &
       & ((C3TKEFREE**2*EFB_AZ0**2*REFB_2**2+2.0_JPRB*C3TKEFREE        &
       & *EFB_AZ0*ETKE_OLAM*REFB_1*REFB_2+ETKE_OLAM**2*REFB_1**2)*     &
       & ZMRIFPP(JLON)**2+((2.0_JPRB*C3TKEFREE*EFB_AZ0*EFB_UR*         &
       & ETKE_OLAM+2*C3TKEFREE**2*EFB_AZ0**2)*REFB_2+(2.0_JPRB*EFB_UR* &
       & ETKE_OLAM**2+2.0_JPRB*C3TKEFREE*EFB_AZ0*ETKE_OLAM)*REFB_1)    &
       & *REFB_3*ZMRIFPP(JLON)+ (EFB_UR**2*ETKE_OLAM**2+2.0_JPRB*      &
       & C3TKEFREE*EFB_AZ0*EFB_UR*ETKE_OLAM+C3TKEFREE**2*EFB_AZ0**2)*REFB_3**2)

      ZDCHI3TKE(JLON)=((ZDRRI*ZMRIFPP(JLON))/ZETKE_R(JLON)**2- &
       & ZDRIFTKE(JLON)/ZETKE_R(JLON))/                        &
       & (1.0_JPRB-ZMRIFPP(JLON))+                             &
       & (ZDRIFTKE(JLON)*(1.0_JPRB-ZMRIFPP(JLON)/              &
       & ZETKE_R(JLON)))/(1.0_JPRB-ZMRIFPP(JLON))**2

      ZDPHI3TKE(JLON)=((ZDPRI*ZMRIFPP(JLON))/ZETKE_P(JLON)**2-  &
       & ZDRIFTKE(JLON)/ZETKE_P(JLON))/                         &
       & (1.0_JPRB-ZMRIFPP(JLON))+(ZDRIFTKE(JLON)*              &
       & (1.0_JPRB-ZMRIFPP(JLON)/ZETKE_P(JLON)))/               &
       & (1.0_JPRB-ZMRIFPP(JLON))**2

      ZDRIFTKE(JLON)=ZSIG_AF(JLON)*ZDRIFTKE(JLON)+    &
       & (1.0_JPRB-ZSIG_AF(JLON))*ZDRIFTKE_U
      ZDCHI3TKE(JLON)=ZSIG_AF(JLON)* ZDCHI3TKE(JLON)+ &
       & (1.0_JPRB-ZSIG_AF(JLON))* ZDCHI3TKE_U
      ZDPHI3TKE(JLON)=ZSIG_AF(JLON)* ZDPHI3TKE(JLON)+ &
       & (1.0_JPRB-ZSIG_AF(JLON))* ZDPHI3TKE_U
    ENDDO

  ELSEIF (ITTYPE==6) THEN

    DO JLON=KIDIA,KFDIA

      ! unstable
      ZDRRI_U=RLOUIS_GU1/(RLOUIS_GU2*PMRIPP(JLON,KLEV)+1.0_JPRB) &
       & -(RLOUIS_GU2*(RLOUIS_GU1*PMRIPP(JLON,KLEV)+RLOUIS_S0))/ &
       & (RLOUIS_GU2*PMRIPP(JLON,KLEV)+1)**2
      ! stable
      ZDRRI=(2.0_JPRB*RLOUIS_GS1*PMRIPP(JLON,KLEV)+RLOUIS_GS2)/        &
       & (RLOUIS_GS3*PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*           &
       & RLOUIS_GS4+1.0_JPRB)-((2.0_JPRB*RLOUIS_GS3*PMRIPP(JLON,KLEV)+ &
       & RLOUIS_GS4)*(RLOUIS_GS1*PMRIPP(JLON,KLEV)**2+RLOUIS_S0+       &
       & PMRIPP(JLON,KLEV)*RLOUIS_GS2))/(RLOUIS_GS3*                   &
       & PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*RLOUIS_GS4+1.0_JPRB)**2
      ZDRRI=ZSIG_AF(JLON)*ZDRRI+(1.0_JPRB-ZSIG_AF(JLON))*ZDRRI_U

      ! unstable
      ZDPRI_U=PLOUIS_GU1/(PLOUIS_GU2*PMRIPP(JLON,KLEV)+1.0_JPRB)   &
       & -(PLOUIS_GU2*(PLOUIS_GU1*PMRIPP(JLON,KLEV)+PLOUIS_S0))/   &
       & (PLOUIS_GU2*PMRIPP(JLON,KLEV)+1)**2
      ! stable
      ZDPRI=(2.0_JPRB*PLOUIS_GS1*PMRIPP(JLON,KLEV)+PLOUIS_GS2)/        &
       & (PLOUIS_GS3*PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*           &
       & PLOUIS_GS4+1.0_JPRB)-((2.0_JPRB*PLOUIS_GS3*PMRIPP(JLON,KLEV)+ &
       & PLOUIS_GS4)*(PLOUIS_GS1*PMRIPP(JLON,KLEV)**2+PLOUIS_S0+       &
       & PMRIPP(JLON,KLEV)*PLOUIS_GS2))/(PLOUIS_GS3*                   &
       & PMRIPP(JLON,KLEV)**2+PMRIPP(JLON,KLEV)*PLOUIS_GS4+1.0_JPRB)**2
      ZDPRI=ZSIG_AF(JLON)*ZDPRI+(1.0_JPRB-ZSIG_AF(JLON))*ZDPRI_U

      ZDRIFTKE(JLON)=(ZMRIFPP(JLON)**3*(ZETKE_P(JLON)*               &
       & ZDRRI-ZETKE_R(JLON)*ZDPRI)+ZMRIFPP(JLON)**2*                &
       & (-ZETKE_P(JLON)**2*ZDRRI+ZETKE_R(JLON)**2*(ZDPRI+1.0_JPRB)) &
       &-2.0_JPRB*ZETKE_P(JLON)*ZETKE_R(JLON)**2*ZMRIFPP(JLON)+      &
       & ZETKE_P(JLON)**2*ZETKE_R(JLON)**2)/                         &
       &(ZETKE_P(JLON)*ZETKE_R(JLON)*                                &
       & (ZMRIFPP(JLON)**2-2*ZETKE_P(JLON)*ZMRIFPP(JLON)+            &
       & ZETKE_P(JLON)*ZETKE_R(JLON)))

      ZDCHI3TKE(JLON)=((ZDRRI*ZMRIFPP(JLON))/ZETKE_R(JLON)**2        &
       & -ZDRIFTKE(JLON)/ZETKE_R(JLON))/(1.0_JPRB-ZMRIFPP(JLON))     &
       & +(ZDRIFTKE(JLON)*(1.0_JPRB-ZMRIFPP(JLON)/ZETKE_R(JLON)))    &
       & /(1.0_JPRB-ZMRIFPP(JLON))**2
      ZDPHI3TKE(JLON)=(ZDRIFTKE(JLON)*(1.0_JPRB-ZMRIFPP(JLON)/       &
       & ZETKE_P(JLON)))/(1.0_JPRB-ZMRIFPP(JLON))**2-ZDRIFTKE(JLON)/ &
       & (ZETKE_P(JLON)*(1.0_JPRB-ZMRIFPP(JLON)))
    ENDDO

  ENDIF ! type 6

  ! derivatives of Fm and Fh - surface
  IF (LCOEFK_F1) THEN
    DO JLON=KIDIA,KFDIA
      ZSIG=MAX(0._JPRB,SIGN(1._JPRB,ZMRIFPP(JLON)))
      ZFFTKE(JLON)=ZCHI3TA(JLON)*(1.0_JPRB-ZMRIFPP(JLON))
      ZFFTKE_DX(JLON)=(1.0_JPRB-ZMRIFPP(JLON))*ZDCHI3TKE(JLON)&
       & -ZCHI3TA(JLON)*ZDRIFTKE(JLON)
      ZFMTKE_DX(JLON)=(1.0_JPRB-ZSIG)*ZDCHI3TKE(JLON)+ZSIG*&
       &((ZFFTKE(JLON)*ZDCHI3TKE(JLON)+&
       & 0.5_JPRB*ZCHI3TA(JLON)*ZFFTKE_DX(JLON))&
       &/SQRT(ZFFTKE(JLON)))
      ZFHTKE_DX(JLON)=(1.0_JPRB-ZSIG)*ZDPHI3TKE(JLON)+ZSIG*&
       &((ZFFTKE(JLON)*ZDPHI3TKE(JLON)+&
       & 0.5_JPRB*ZPHI3TA(JLON)*ZFFTKE_DX(JLON))&
       &/SQRT(ZFFTKE(JLON)))
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA
      ZFFTKE(JLON)=ZCHI3TA(JLON)*(1.0_JPRB-ZMRIFPP(JLON))
      ZFFTKE_DX(JLON)=(1.0_JPRB-ZMRIFPP(JLON))*ZDCHI3TKE(JLON)&
       & -ZCHI3TA(JLON)*ZDRIFTKE(JLON)
      ZFMTKE_DX(JLON)=(ZFFTKE(JLON)*ZDCHI3TKE(JLON)+&
       & 0.5_JPRB*ZCHI3TA(JLON)*ZFFTKE_DX(JLON))&
       &/SQRT(ZFFTKE(JLON))
      ZFHTKE_DX(JLON)=(ZFFTKE(JLON)*ZDPHI3TKE(JLON)+&
       & 0.5_JPRB*ZPHI3TA(JLON)*ZFFTKE_DX(JLON))&
       &/SQRT(ZFFTKE(JLON))
    ENDDO
  ENDIF

  ! COMPUTAION OF alpha_u and alpha_theta - PAUTKE,PATTKE
  DO JLON=KIDIA,KFDIA
    ZAUTKE(JLON)=0.5_JPRB*PMRIPP(JLON,KLEV)* &
     & ZFMTKE_DX(JLON)/ZFMTKE(JLON)
    ZATTKE(JLON)=0.5_JPRB*PMRIPP(JLON,KLEV)* &
     & ZFHTKE_DX(JLON)/ZFTTKE(JLON)
  ENDDO
ENDIF ! LLAFGD

! 'Dry' Antifibrilation scheme
PXDROV(KIDIA:KFDIA)=1.0_JPRB
PXHROV(KIDIA:KFDIA)=1.0_JPRB
IF (LLAFGD) THEN
  IF (XMULAF > 0.0_JPRB) THEN
    DO JLON=KIDIA,KFDIA
      PXDROV(JLON)=MAX(PXDROV(JLON),XMULAF)
      PXHROV(JLON)=MAX(PXHROV(JLON),XMULAF)
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA

      !     CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
      !     ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.

      ! alpha_u and alpha_theta - ZAU,ZAT
      ZAU=ZAUTKE(JLON)
      ZAT=ZATTKE(JLON)
      ! CONSTRAINTS FOR 'DRY' ANTIFIBRILLATION SCHEME
      !Check for AF constrains : 2-3.alpha_u+2.alpha_th<=2
      ZSIG=SIGN(0.5_JPRB,1.5_JPRB*ZAU-ZAT)+0.5_JPRB

      ZBE=3._JPRB+ZAT-2.0_JPRB*ZAU
      ZGA=2.0_JPRB+ZSIG*(2.0_JPRB*ZAT-3._JPRB*ZAU)
      ZGAMDZ=4._JPRB*TSPHY*(RG/ZDPHI(JLON,KLEV))
      ZGKU=ZGAMDZ*PCD(JLON)*ZU(JLON)
      ZGKT=ZGAMDZ*PCH(JLON)*ZU(JLON)
      ZMULA2=XMULAF**2
      ZDENUM=ZGKU*ZGKT
      ZA1=(1.0_JPRB+ZGKU)*(1.0_JPRB+ZGKT)
      ZB1=(1.0_JPRB+ZGKU)*ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*(1.0_JPRB+ZGKT)*ZGKU*(1.0_JPRB-ZAU)
      ZC1=ZDENUM*ZGA
      ZDELTA1=ZB1**2-4._JPRB*ZA1*ZC1
      ZCRIT1=MAX(SIGN(1.0_JPRB,ZDELTA1),0.0_JPRB)
      ZTAU=-0.5_JPRB * ZCRIT1*(ZB1+SQRT(ABS(ZDELTA1)))/ZA1
      ZCRIT2=MAX(SIGN(1.0_JPRB,XMULAF-ZTAU),0.0_JPRB)
      ZAPRIM=ZMULA2*ZDENUM
      ZBPRIM=ZMULA2*(ZGKU+ZGKT)+XMULAF*ZDENUM*ZBE
      ZCPRIM=ZMULA2+XMULAF*(ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*ZGKU*(1.0_JPRB-ZAU))&
       & +ZDENUM*ZGA  
      ZSDPRIM=SQRT(ABS(ZBPRIM**2-4._JPRB*ZAPRIM*ZCPRIM))
      ZBETAP= 0.5_JPRB*(-ZBPRIM +ZSDPRIM)/MAX(ZAPRIM,1.0_JPRB-ZCRIT2)
      ZBETA=1.0_JPRB+ZCRIT2*(ZBETAP-1.0_JPRB)

      PXDROV(JLON)=MAX(PXDROV(JLON),ZBETA)
      PXHROV(JLON)=MAX(PXHROV(JLON),ZBETA)
    ENDDO
  ENDIF
ENDIF

!*
!     ------------------------------------------------------------------
!     VI - CORRECTION DES COEFFICIENTS TURBULENTS EN PRESENCE DE PRECIPITATIONS.
!     CORRECTION OF TURBULENT COEFFICIENTS IN CASE OF PRECIPITATIONS.

IF (LRRGUST) THEN
  DO JLON=KIDIA,KFDIA
    ZCD=ZFMTKE(JLON)*PCDN(JLON)
    ZFP=MAX(0.0_JPRB,PFPLSH(JLON,KLEV)+PFPLCH(JLON,KLEV))
    ZRCORR=SQRT(1.0_JPRB+((((ZFP/(ZFP+RRSCALE))**RRGAMMA)*UTILGUST)**2) &
     & /(ZCD*ZU(JLON)**2))
    PCD(JLON)=PCD(JLON)*ZRCORR
    PCH(JLON)=PCH(JLON)*ZRCORR
    PCDN(JLON)=PCDN(JLON)*ZRCORR
    ZCDNH(JLON)=ZCDNH(JLON)*ZRCORR
  ENDDO
ENDIF

IF (LDCLS) THEN
  ! ZDELT     : UN POUR CALCULS DE SATURATION SUR GLACE, ZERO SINON.
  !            : ONE FOR SATURATION CALCULATION FOR ICE, ZERO OTHERWISE.
  IF (LNEIGE) THEN
    DO JLON=KIDIA,KFDIA
      ZDELT(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PTS(JLON)))
    ENDDO
  ELSE
    DO JLON=KIDIA,KFDIA
      ZDELT(JLON)=0.0_JPRB
    ENDDO
  ENDIF

  DO JLON=KIDIA,KFDIA
    ! ZESP      : PRESSION DE VAPEUR SATURANTE SUR PRESSION.
    !            : SATURATION WATER VAPOUR PRESSURE OVER PRESSURE.
    ZEW= FOEW (PTS(JLON),ZDELT(JLON))
    ZESP(JLON)=ZEW/PAPRS(JLON,KLEV)

    !     CALCUL DE LA DERIVEE PAR RAPPORT A TS DE QSATS ET DES COEFFICIENTS
    !     D'ECHANGE MULTIPLIES PAR DENSITE ET MODULE DU VENT.
    !     COMPUTATION OF THE DERIVATIVE OF QSATS WITH RESPECT TO TS AND OF
    !     THE EXCHANGE COEFFICIENTS MULTIPLIED BY DENSITY AND WIND SPEED.

    PDQSTS(JLON)= FODQS (PQSATS(JLON) , ZESP(JLON),&
     & FODLEW (PTS(JLON) , ZDELT(JLON)))  

    PCDROV(JLON)=PCD(JLON)*ZU(JLON)*PAPRS(JLON,KLEV)*PRTI(JLON)
    PCHROV(JLON)=PCH(JLON)*ZU(JLON)*PAPRS(JLON,KLEV)*PRTI(JLON)

    PGWDCS(JLON)=PAPRS(JLON,KLEV)*PRTI(JLON)
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     VII - INTERPOLATION.

!           INTERPOLATION.

IF (LDHMT) THEN

  CALL ACTKECLS ( KIDIA,KFDIA,KLON,KLEV,&
   & PAPRS,PAPRSF,PCP,PQ,PT,PU,PV,&
   & PCD,PCDN,ZCDNH,PCH,PCPS,ZDPHI,PDPHIT,PDPHIV,&
   & PQS,PSTAB,PTS,&
   & PQCLS,PRHCLS,PTCLS,PUCLS,PVCLS,PNUCLS,PNVCLS,PZPCLS)

  IF (LRAFTUR) THEN
    DO JLON=KIDIA,KFDIA
      ZSCRAF=(PCD(JLON)*(PU(JLON,KLEV)**2+PV(JLON,KLEV)**2)/ &
       & (PUCLS(JLON)**2+PVCLS(JLON)**2+(GCISMIN*PDPHIV(JLON)/RG)**2))/ &
       & (1.0_JPRB+(LOG(1.0_JPRB+PDPHIV(JLON)/GZ0RAF)/ &
       & LOG(1.0_JPRB+PDPHIV(JLON)/PGZ0(JLON)))**2)  

      ZSCRAF=1.0_JPRB+FACRAF*SQRT(ZSCRAF)
      PURAF(JLON)=ZSCRAF*PUCLS(JLON)
      PVRAF(JLON)=ZSCRAF*PVCLS(JLON)
    ENDDO
  ENDIF

ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACTKEHMT',1,ZHOOK_HANDLE)
END SUBROUTINE ACTKEHMT
