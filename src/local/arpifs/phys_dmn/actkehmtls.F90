!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACTKEHMTLS ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PU,PV,&
 & PFMTKE,PFTTKE,PAUTKE,PATTKE,PRCORR,&
 ! - INPUT  1D .
 & PCDN,PCDNH,PRTI,&
 ! - INPUT  LOGIQUE .
 & LDCLS,&
 ! - OUTPUT 1D .
 & PCD,PCHROV,PGWDCS,PXHROV)

!**** *ACTKEHMTLS   * - DETERMINATION DES CARACTERISTIQUES DE SURFACE.
!**** *ACTKEHMTLS   * - INTERP. DES PARAMETRES U,V,T,Q AUX HAUTEURS CHOISIES.

!     Sujet.
!     ------


!     - ROUTINE DE CALCUL ACTIF .
!       DETERMINATION DE 
!       LES COEFFICIENTS D'ECHANGE EN
!       SURFACE PLUS LEUR AMPLIFICATION SI "ANTI-FIBRILLATION" ACTIVEE .
!     - SI L'OPTION 'CLS' EST SELECTIONNEE :
!          DETERMINATION DE PARAMETRES SUPPLEMENTAIRES NECESSAIRES POUR
!          LES CALCULS DE DIFFUSION VERTICALE ET DE TRAINEE DES ONDES DE
!          GRAVITE OROGRAPHIQUES .

!     - COMPUTATION OF 
!       SURFACE EXCHANGE COEFFICIENTS PLUS THEIR AMPLIFICATION
!       FACTOR IF "ANTI-FIBRILLATION" IS ACTIVATED .
!     - IF THE SWITCH 'CLS' IS ON :
!          COMPUTATION OF ADDITIONNAL PARAMETERS NECESSARY FOR THE
!          VERTICAL DIFFUSION AND GRAVITY WAVE DRAG PARAMETERIZATIONS .


!**   Interface.
!     ----------
!        *CALL* *ACTKEHMTLS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.
! PFMTKE     : STABILITY FUNCTION F_m
! PFTTKE     : STABILITY FUNCTION F_h
! PAUTKE     : alpha_u for dry AF scheme
! PATTKE     : alpha_theta for dry AF scheme
! PRCORR     : MOIST GUSTINESS COEFFICIENT


! - 1D (DIAGNOSTIQUE) .
! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PRTI       : INVERSE DE R*T.

! LDCLS      : DETERMINATION COMPLETE DES CARACTERISTIQUES DE SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D (DIAGNOSTIQUE) .

! PCD        : COEFFICIENT D'ECHANGE EN SURFACE POUR U ET V.
! PCHROV     : ZCH RENORME EN DENSITE FOIS VITESSE.
! PGWDCS     : DENSITE EN SURFACE POUR LE DRAG OROGRAPHIQUE.
! PXHROV     : MULTIPLICATEUR "ANTI-FIBRILLATION" DE PCHROV.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Methode.
!     --------

!     Auteur.
!     -------
!        May 2011, F. Vana 

!     Modifications.
!     --------------
!        2012-07, I. Bastak-Duran: major rewriting. 
!
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG 

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFMTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFTTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAUTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PATTKE(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORR(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRTI(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCDNH(KLON) 
LOGICAL           ,INTENT(IN)    :: LDCLS 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCD(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDN(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCHROV(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGWDCS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PXHROV(KLON) 
!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZDPHI(KLON), ZU(KLON)  
REAL(KIND=JPRB) :: ZZCH(KLON) 

INTEGER(KIND=JPIM) :: JLON

LOGICAL :: LLAFGD

REAL(KIND=JPRB) :: ZA1, ZAPRIM, ZAT, ZAU,&
 & ZB1, ZBE, ZBETA, ZBETAP, ZBPRIM, ZC1, &
 & ZCIS, ZCPRIM,ZCRIT1, ZCRIT2, ZDELTA1, &
 & ZDENUM, ZGA, ZGAMDZ, ZGKT, ZGKU,  &
 & ZMULA2, ZSDPRIM, ZTAU,ZRCORR
REAL(KIND=JPRB) :: ZSIG

REAL(KIND=JPRB) :: ZHOOK_HANDLE


!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACTKEHMTLS',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, XMULAF=>YDML_PHY_MF%YRPHY2%XMULAF, &
 & LRRGUST=>YDML_PHY_MF%YRPHY%LRRGUST)
!-----------------------------------------------------------------------

!*

!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES ET CONSTANTE DE SECURITE (POUR
!     LE CARRE DU CISAILLEMENT DE VENT).

!          COMPUTATION OF DERIVED PARAMETERS AND SECURITY CONSTANT (FOR
!     THE SQUARE OF THE WIND SHEAR).


LLAFGD=XMULAF /= 0.0_JPRB

!*
!     ------------------------------------------------------------------
!     II - CALCUL DE L EPAISSEUR DE LA CLS (EN GEOPOTENTIEL).

!          COMPUTATION OF THE THICKNESS OF THE SBL (IN GEOPOTENTIAL).

! - TEMPORAIRE(S) 1D .

! ZDPHI     : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
!            : GEOPOTENTIAL THICKNESS OF THE SURFACE LEVEL.

DO JLON=KIDIA,KFDIA
  ZDPHI(JLON)=PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)
ENDDO

!*
!     ------------------------------------------------------------------
!     III - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.


DO JLON=KIDIA,KFDIA

#if defined(NECSX)
  ZGAMDZ    =-999._JPRB
  ZGKU      =-999._JPRB
  ZGKT      =-999._JPRB
  ZMULA2    =-999._JPRB
  ZDENUM    =-999._JPRB
  ZA1       =-999._JPRB
  ZB1       =-999._JPRB
  ZC1       =-999._JPRB
  ZDELTA1   =-999._JPRB
  ZCRIT1    =-999._JPRB
  ZTAU      =-999._JPRB
  ZCRIT2    =-999._JPRB
  ZAPRIM    =-999._JPRB
  ZBPRIM    =-999._JPRB
  ZCPRIM    =-999._JPRB
  ZSDPRIM   =-999._JPRB
  ZBETAP    =-999._JPRB
  ZBETA     =-999._JPRB
#endif

!     CISAILLEMENT DE VENT.
!     WIND SHEAR.

! - TEMPORAIRE(S) 1D .

! ZU        : MODULE DU VENT DE SURFACE.
!            : SURFACE WIND SPEED.

  ZCIS=PU(JLON,KLEV)**2+PV(JLON,KLEV)**2+(GCISMIN*ZDPHI(JLON)/RG)**2
  ZU(JLON)=SQRT(ZCIS)


! COMPUTAION OF DRAG COEFFICIENTS PCD,ZCH
!     CALCULS POUR LES COMPOSANTES DU VENT DE CD ET DE SON PRODUIT PAR
!     DENSITE FOIS MODULE DU VENT.
!     COMPUTATIONS FOR MOMENTUM OF CD AND OF ITS PRODUCT BY DENSITY
!     TIME WIND SPEED.

     PCD(JLON)=PFMTKE(JLON,KLEV)*PCDN(JLON)

!     IDEM POUR LA TEMPERATURE ET L'HUMIDITE.
!     THE SAME FOR TEMPERATURE AND HUMIDITY.

     ZZCH(JLON)=PFTTKE(JLON,KLEV)*PCDNH(JLON)

! 'Dry' Antifibrilation scheme - BEGIN

  PXHROV(JLON)=1.0_JPRB

!     CALCUL ANTI-FIBRILLATION "GIRARD-DELAGE".
!     ANTI-FIBRILLATION "GIRARD-DELAGE" COMPUTATION.

  IF (LLAFGD) THEN
! alpha_u and alpha_theta - ZAU,ZAT
    ZAU=PAUTKE(JLON,KLEV)
    ZAT=PATTKE(JLON,KLEV)
! CONSTRAINTS FOR 'DRY' ANTIFIBRILLATION SCHEME
    !Check for AF constrains : 2-3.alpha_u+2.alpha_th<=2
    ZSIG=SIGN(0.5_JPRB,1.5_JPRB*ZAU-ZAT)+0.5_JPRB

    ZBE=3._JPRB+ZAT-2.0_JPRB*ZAU
    ZGA=2.0_JPRB+ZSIG*(2.0_JPRB*ZAT-3._JPRB*ZAU)
    IF(XMULAF > 0.0_JPRB) THEN
      PXHROV(JLON)=MAX(PXHROV(JLON),XMULAF)
    ELSE
      ZGAMDZ=4._JPRB*TSPHY*(RG/ZDPHI(JLON))
      ZGKU=ZGAMDZ*PCD(JLON)*ZU(JLON)
      ZGKT=ZGAMDZ*ZZCH(JLON)*ZU(JLON)
      ZMULA2=XMULAF**2
      ZDENUM=ZGKU*ZGKT
      ZA1=(1.0_JPRB+ZGKU)*(1.0_JPRB+ZGKT)
      ZB1=(1.0_JPRB+ZGKU)*ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*(1.0_JPRB+ZGKT)*ZGKU*(1.0_JPRB-ZAU)
      ZC1=ZDENUM*ZGA
      ZDELTA1=ZB1**2-4._JPRB*ZA1*ZC1
      ZCRIT1=MAX(SIGN(1.0_JPRB,ZDELTA1),0.0_JPRB)
      ZTAU=-0.5_JPRB * ZCRIT1*(ZB1+SQRT(ABS(ZDELTA1)))/ZA1
      ZCRIT2=MAX(SIGN(1.0_JPRB,XMULAF-ZTAU),0.0_JPRB)
      ZAPRIM=ZMULA2*ZDENUM
      ZBPRIM=ZMULA2*(ZGKU+ZGKT)+XMULAF*ZDENUM*ZBE
      ZCPRIM=ZMULA2+XMULAF*(ZGKT*(1.0_JPRB+ZAT)+2.0_JPRB*ZGKU*(1.0_JPRB-ZAU))&
       & +ZDENUM*ZGA  
      ZSDPRIM=SQRT(ABS(ZBPRIM**2-4._JPRB*ZAPRIM*ZCPRIM))
      ZBETAP= 0.5_JPRB*(-ZBPRIM +ZSDPRIM)/MAX(ZAPRIM,1.0_JPRB-ZCRIT2)
      ZBETA=1.0_JPRB+ZCRIT2*(ZBETAP-1.0_JPRB)

      PXHROV(JLON)=MAX(PXHROV(JLON),ZBETA)
    ENDIF
  ENDIF

!     CORRECTION DES COEFFICIENTS TURBULENTS EN PRESENCE DE PRECIPITATIONS.
!     CORRECTION OF TURBULENT COEFFICIENTS IN CASE OF PRECIPITATIONS.

  IF (LRRGUST) THEN
    ZRCORR=PRCORR(JLON,KLEV)**2
    PCD(JLON)=PCD(JLON)*ZRCORR
    ZZCH(JLON)=ZZCH(JLON)*ZRCORR
    PCDN(JLON)=PCDN(JLON)*ZRCORR
  ENDIF

ENDDO

IF (LDCLS) THEN
  DO JLON=KIDIA,KFDIA

!     CALCUL DE LA DERIVEE PAR RAPPORT A TS DE QSATS ET DES COEFFICIENTS
!     D'ECHANGE MULTIPLIES PAR DENSITE ET MODULE DU VENT.
!     COMPUTATION OF THE DERIVATIVE OF QSATS WITH RESPECT TO TS AND OF
!     THE EXCHANGE COEFFICIENTS MULTIPLIED BY DENSITY AND WIND SPEED.

    PCHROV(JLON)=ZZCH(JLON)*ZU(JLON)*PAPRS(JLON,KLEV)*PRTI(JLON)

!     CALCULS DE UN SUR G FOIS LA FREQUENCE DE BRUNT-VAISALA DIVISEE PAR
!     LA DENSITE LE TOUT AU CARRE DE MEME QUE LA DENSITE (POUR G.W.D.).
!     COMPUTATION OF ONE OVER G TIME THE BRUNT-VAISALA FREQUENCY DIVIDED
!     BY DENSITY THE WHOLE BEING SQUARED AS WELL AS DENSITY (FOR G.W.D.)

    PGWDCS(JLON)=PAPRS(JLON,KLEV)*PRTI(JLON)
  ENDDO
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACTKEHMTLS',1,ZHOOK_HANDLE)
END SUBROUTINE ACTKEHMTLS
