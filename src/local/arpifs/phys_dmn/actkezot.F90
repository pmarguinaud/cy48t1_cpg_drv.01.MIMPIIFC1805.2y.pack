!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACTKEZOT ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KLEV,KVCLIV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PAPRS,PQ,PR,PT,PU,PV,&
 ! - INPUT  1D .
 & PGZ0F,PGZ0HF,PGZ0RLF,PHV,PLSM,&
 & PNEIJG,PNEIJV,PSNS,PTS,PVEG0,PWFC,PWS,PWSI,&
 ! - OUTPUT 1D .
 & PCDN,PCDNH,PCPS,&
 & PGZ0,PGZ0H,PHQ,PHU,PNEIJ,PQS,PQSATS,&
 & PRS,PRTI,PVEG)

!**** *ACTKEZOT   * - DETERMINATION DES CARACTERISTIQUES DE SURFACE.

!     Sujet.
!     ------

!     - ROUTINE DE CALCUL ACTIF .
!       DETERMINATION DE CARACTERISTIQUES DE SURFACE CONCERNANT LA
!       NEIGE, L'HUMIDITE DU SOL

!     - COMPUTATION OF SURFACE CHARACTERISTICS FOR SNOW, SOIL HUMIDITY

!**   Interface.
!     ----------
!        *CALL* *ACTKEHMT*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KVCLIV     : NOMBRE DE CHAMPS POUR LA VEGETATION (NVCLIV DANS CPG).
! KVCLIV     : NUMBER OF FIELDS FOR VEGETATION (NVCLIV IN CPG).

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.
! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.
! PU         : COMPOSANTE EN X DU VENT.
! PV         : COMPOSANTE EN Y DU VENT.

! - 1D (PROGNOSTIQUE) .

! PSNS       : MASSE PAR UNITE DE SURFACE DE LA COUCHE NEIGEUSE.
! PTS        : TEMPERATURE DE SURFACE.
! PWS        : CONTENU EN EAU DU RESERVOIR DE SURFACE.
! PWSI       : CONTENU EN EAU GELEE DU RESERVOIR DE SURFACE.

! - 1D (SPECIFIQUE SELON L'UTILISATION DE ACHMT) .

! PHV        : RESISTANCE AU FLUX D'EVAPOTRANSPIRATION.
! PWFC       : TENEUR EN EAU A LA CAPACITE AU CHAMPS.

! - 1D (DIAGNOSTIQUE) .

! PNEIJG     : PROPORTION DE SOL RECOUVERTE DE NEIGE.
! PNEIJV     : PROPORTION DE VEGETATION RECOUVERTE DE NEIGE.

! - 1D (GEOGRAPHIQUE) .

! PGZ0F      : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE.
! PGZ0HF     : CHAMP TEMPOREL DE G FOIS LA LONGUEUR DE RUGOSITE THERM.
! PGZ0RLF    : GRAVITE * LONGUEUR DE RUGOSITE DUE AU RELIEF.
! PLSM       : INDICE TERRE/MER.
! PVEG0      : PROPORTION DE SOL COUVERTE DE VEGETATION.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D (DIAGNOSTIQUE) .

! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PCDNH      : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE. for heat
! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.
! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PGZ0H      : G*LONGUEUR DE RUGOSITE THERMIQUE COURANTE (SI KVCLIV >= 8)
! PHQ        : POIDS DE L'HUMIDITE DE L'AIR DANS L'HUMIDITE DE SURFACE.
! PHU        : POIDS DE L'HUMIDITE SATURANTE DANS L'HUMIDITE DE SURFACE.
! PNEIJ      : PROPORTION DE LA MAILLE RECOUVERTE DE NEIGE.
! PQS        : HUMIDITE SPECIFIQUE DE SURFACE.
! PQSATS     : HUMIDITE SPECIFIQUE DE SATURATION EN SURFACE.
! PRS        : CONSTANTE DES GAZ POUR L'AIR AU SOL.
! PRTI       : INVERSE DE R*T.
! PVEG       : PROPORTION DE VEGETATION APPARENTE (NON RECOUVERTE DE NEIGE).

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY1/
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2012-07, I. Bastak-Duran after ACTKEZOT  (extracted from ACHMT)

!     Modifications.
!     --------------
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RPI      ,RG       ,RD       ,RV       ,&
 & RCPD     ,RCPV     ,RKAPPA  ,RETV     ,RCW      ,&
 & RCS      ,RLVTT    ,RLSTT   ,RTT     ,RALPW     ,RBETW    ,&
 & RGAMW    ,RALPS    ,RBETS   ,RGAMS   ,RALPD     ,RBETD    ,&
 & RGAMD
USE YOMCLI   , ONLY : STHER    ,SZZ0D

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KVCLIV
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0F(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0HF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0RLF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVEG0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWFC(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSI(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDNH(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0H(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHQ(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHU(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEIJ(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQSATS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRTI(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVEG(KLON) 
!-----------------------------------------------------------------------

REAL(KIND=JPRB) ::ZDELT(KLON),ZDPHI(KLON),ZESP(KLON)&
 & ,ZU(KLON)



INTEGER(KIND=JPIM) :: JLON

LOGICAL :: LLZ0H

REAL(KIND=JPRB) :: ZEPS,ZCPVMD,ZRVMD,ZGZ0N, ZGZ0MR, ZRZDMR ,&
& ZEW, ZWFC, ZWS, ZDEW, ZRZD, ZRZH, Z0CR, ZUZ0CN,ZCIS


REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "fcttrm.func.h"
#include "fzh.func.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACTKEZOT',0,ZHOOK_HANDLE)
ASSOCIATE(GCISMIN=>YDML_PHY_MF%YRPHY0%GCISMIN, VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & C3TKEFREE=>YDML_PHY_MF%YRPHY0%C3TKEFREE, VZIUSTAR0=>YDML_PHY_MF%YRPHY0%VZIUSTAR0, &
 & RD1=>YDML_PHY_MF%YRPHY1%RD1, WSMX=>YDML_PHY_MF%YRPHY1%WSMX, ALRCN2=>YDML_PHY_MF%YRPHY1%ALRCN2, &
 & ALRCN1=>YDML_PHY_MF%YRPHY1%ALRCN1, WCRIN=>YDML_PHY_MF%YRPHY1%WCRIN, GCONV=>YDML_PHY_MF%YRPHY1%GCONV, &
 & RZHZ0M=>YDML_PHY_MF%YRPHY1%RZHZ0M, &
 & LSOLV=>YDML_PHY_MF%YRPHY%LSOLV, LNEIGE=>YDML_PHY_MF%YRPHY%LNEIGE, LSNV=>YDML_PHY_MF%YRPHY%LSNV, &
 & LZ0HSREL=>YDML_PHY_MF%YRPHY%LZ0HSREL, LFGEL=>YDML_PHY_MF%YRPHY%LFGEL)
!-----------------------------------------------------------------------
!*
!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.

!   security constant
ZEPS    = EPSILON(1.0_JPRB)*100.0_JPRB

!         AUXILIARY CONSTANTS.


ZCPVMD=RCPV-RCPD
ZRVMD=RV-RD

! LLZ0H      : DIFFERENTES LONGUEURS DE RUGOSITE DYN/THER.

LLZ0H=(KVCLIV>=8).AND.LSOLV

!   ZGZ0N   : VALEUR MIN. DE G FOIS LA LONG. DE RUG. DYN. SANS RELIEF.
!            : MIN VALUE OF G * DYN. ROUGHNESS L. WITHOUT TOPOGRAPHY.

ZGZ0N=RG*SZZ0D


!*
!     ------------------------------------------------------------------
!     II - PASSAGE EN GEOPOTENTIEL POUR LES CONSTANTES RELATIVES A LA
!     LONGUEUR DE RUGOSITE EN CAS DE NEIGE.

!           GOING TO GEOPOTENTIAL FOR CONSTANTS RELATIVE TO ROUGHNESS
!     LENGTH IN CASE OF SNOW COVER.

Z0CR=RG*ALRCN1
ZUZ0CN=1.0_JPRB/(RG*ALRCN2)

!*
!     ------------------------------------------------------------------
!     III - CALCUL DE L EPAISSEUR DE LA CLS (EN GEOPOTENTIEL).

!          COMPUTATION OF THE THICKNESS OF THE SBL (IN GEOPOTENTIAL).

! - TEMPORAIRE(S) 1D .

! ZDPHI     : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
!            : GEOPOTENTIAL THICKNESS OF THE SURFACE LEVEL.

DO JLON=KIDIA,KFDIA
  ZDPHI(JLON)=PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)
ENDDO

!*
!     ------------------------------------------------------------------
!     IV - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.


DO JLON=KIDIA,KFDIA

!     CALCULS CONCERNANT LA PRESENCE OU NON DE NEIGE AU SOL.
!     COMPUTATIONS CONCERNING THE PRESENCE OR ABSENCE OF SNOW ON GROUND.

! - TEMPORAIRE(S) 1D .

! ZDELT     : UN POUR CALCULS DE SATURATION SUR GLACE, ZERO SINON.
!            : ONE FOR SATURATION CALCULATION FOR ICE, ZERO OTHERWISE.

  IF (LNEIGE) THEN
    IF (LSNV) THEN
      PNEIJ(JLON)=PLSM(JLON) *&
       & ((1.0_JPRB-PVEG0(JLON))*PNEIJG(JLON) + PVEG0(JLON)*PNEIJV(JLON))  
      PVEG(JLON)=(1.0_JPRB-PNEIJV(JLON))*PVEG0(JLON)
      PGZ0(JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0F(JLON)**2+&
       & PNEIJV(JLON)*(Z0CR**2+PGZ0RLF(JLON)**2))  
    ELSE
      PNEIJ(JLON)=PLSM(JLON)*PSNS(JLON)/(PSNS(JLON)+WCRIN)
      PVEG(JLON)=PVEG0(JLON)
      PGZ0(JLON)=PGZ0F(JLON)+(Z0CR-PGZ0F(JLON))*PLSM(JLON)&
       & *PSNS(JLON)/(PSNS(JLON)+WCRIN*(1+ZUZ0CN*PGZ0F(JLON)))  
    ENDIF
    ZDELT(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PTS(JLON)))
  ELSE
    PNEIJ(JLON)=0.0_JPRB
    PVEG(JLON)=PVEG0(JLON)
    PGZ0(JLON)=PGZ0F(JLON)
    ZDELT(JLON)=0.0_JPRB
  ENDIF

!     CAS DE DEUX LONGUEURS DE RUGOSITE (SUR CONTINENT NON TOTALEMENT
!     COUVERT DE NEIGE).
!     CASE OF TWO ROUGHNESS LENGTHS (ON PARTIALLY SNOW-FREE LAND ONLY).

  IF (LLZ0H) THEN

    IF (LNEIGE) THEN
      IF (LSNV) THEN
        IF (LZ0HSREL) THEN
          PGZ0H(JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0HF(JLON)**2&
           &+PNEIJV(JLON)*(STHER*Z0CR)**2)
        ELSE
          PGZ0H(JLON)=SQRT((1.0_JPRB-PNEIJV(JLON))*PGZ0HF(JLON)**2+&
           & PNEIJV(JLON)   *&
           & (Z0CR**2+(PGZ0RLF(JLON)*PGZ0HF(JLON)/PGZ0F(JLON))**2))  
        ENDIF
      ELSE
        IF (LZ0HSREL) THEN
          PGZ0H(JLON)=PGZ0HF(JLON)+(Z0CR*STHER-PGZ0HF(JLON))&
           &*PLSM(JLON)*PSNS(JLON)/(PSNS(JLON)+WCRIN&
           &*(1+ZUZ0CN*PGZ0HF(JLON)))
        ELSE
          PGZ0H(JLON)=PGZ0HF(JLON)+(Z0CR-PGZ0HF(JLON))*PLSM(JLON)&
           & *PSNS(JLON)/(PSNS(JLON)+WCRIN*(1+ZUZ0CN*PGZ0HF(JLON)))  
        ENDIF
      ENDIF
    ELSE
      PGZ0H(JLON)=PGZ0HF(JLON)
    ENDIF

! PGZ0H = ZGZ0 OVER SEA UNLESS REQUIRED IN NAMPHY1 (CLIMATE RUNS)

     IF (VZIUSTAR0 > 0) THEN
       PGZ0H(JLON)=PLSM(JLON)*PGZ0H(JLON)&
        & +(1.0_JPRB-PLSM(JLON))*PGZ0HF(JLON)
     ELSE    
       PGZ0H(JLON)=MIN(PGZ0(JLON),PLSM(JLON)*PGZ0H(JLON)&
        & +(1.0_JPRB-PLSM(JLON))*PGZ0(JLON)*RZHZ0M)  
     ENDIF

  ELSE

    PGZ0H(JLON)=PGZ0(JLON)

  ENDIF

!     CALCUL DE SATURATION UTILISANT LES FONCTIONS DEFINIES.
!     SATURATION CALCULATION WITH CALL TO DEFINED FUNCTIONS.

! - TEMPORAIRE(S) 1D .

! ZESP      : PRESSION DE VAPEUR SATURANTE SUR PRESSION.
!            : SATURATION WATER VAPOUR PRESSURE OVER PRESSURE.

  ZEW= FOEW (PTS(JLON),ZDELT(JLON))
  ZESP(JLON)=ZEW/PAPRS(JLON,KLEV)
  PQSATS(JLON)= FOQS (ZESP(JLON))

!     CALCULS RELATIFS A L'HUMIDITE DE SURFACE.
!     COMPUTATIONS CONCERNING SURFACE HUMIDITY.

!     CALCULS OPTIONNELS SUIVANT LE TYPE DE SCHEMA SOL/VEG CHOISI.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN SOIL/VEG SCHEME.

  IF (LSOLV) THEN
    ZWFC=PWFC(JLON)*RD1*GCONV
    IF (LFGEL) THEN
      ZWS=PWS(JLON)+PWSI(JLON)
    ELSE
      ZWS=PWS(JLON)
    ENDIF
    IF (LSNV) THEN
      PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJG(JLON))*&
       & (0.5_JPRB*(1.0_JPRB-COS(RPI*MIN(ZWS/ZWFC,1.0_JPRB)))-1.0_JPRB)  
      PHU(JLON)=MAX(PHU(JLON),MIN(PQ(JLON,KLEV)/PQSATS(JLON),1.0_JPRB))
      PQS(JLON)= ( PVEG(JLON)*PHV(JLON) +&
       & PVEG0(JLON)*PNEIJV(JLON) +&
       & (1.0_JPRB-PVEG0(JLON))*PHU(JLON) ) * PQSATS(JLON) +&
       & (1.0_JPRB-PHV(JLON))*PVEG(JLON) * PQ(JLON,KLEV)  
    ELSE
      PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJ(JLON))*&
       & (0.5_JPRB*(1.0_JPRB-COS(RPI*MIN(ZWS/ZWFC,1.0_JPRB)))-1.0_JPRB)  
      PHU(JLON)=MAX(PHU(JLON),MIN(PQ(JLON,KLEV)/PQSATS(JLON),1.0_JPRB))
      PQS(JLON)= ( PVEG(JLON)*PHV(JLON) +&
       & (1.0_JPRB-PVEG(JLON))*PHU(JLON)  ) * PQSATS(JLON) +&
       & (1.0_JPRB-PHV(JLON))*PVEG(JLON) * PQ(JLON,KLEV)  
    ENDIF
  ELSE
    PHU(JLON)=1.0_JPRB+PLSM(JLON)*(1.0_JPRB-PNEIJ(JLON))&
     & *(0.5_JPRB*(1.0_JPRB-COS(RPI*PWS(JLON)/WSMX))-1.0_JPRB)  
    PHQ(JLON)=(1.0_JPRB-PHU(JLON))*PVEG(JLON)
    ZDEW=MAX(0.0_JPRB,SIGN(1.0_JPRB,PQ(JLON,KLEV)-PQSATS(JLON)))
    PHU(JLON)=PHU(JLON)+ZDEW*PHQ(JLON)
    PHQ(JLON)=PHQ(JLON)-ZDEW*PHQ(JLON)
    PQS(JLON)=PHU(JLON)*PQSATS(JLON)+PHQ(JLON)*PQ(JLON,KLEV)
  ENDIF

  PCPS(JLON)=RCPD+ZCPVMD*PQS(JLON)
  PRS(JLON)=RD+ZRVMD*PQS(JLON)

!     CALCULS GEOMETRIQUES.
!     GEOMETRIC CALCULATIONS.

  ZRZD=1.0_JPRB+ZDPHI(JLON)/PGZ0(JLON)
  PCDN(JLON)=(VKARMN/LOG(ZRZD))**2

!     CALCUL PRELIMINAIRES NECESSAIRES A LA DETERMINATION DES
!     COEFFICIENTS D'ECHANGE CD ET CH DANS LE CAS DE DEUX RUGOSITES
!     DYNAMIQUE Z0 ET THERMIQUE ZOH.

!     PRELIMINARY COMPUTATIONS NECESSARY FOR THE DETERMINATION OF
!     EXCHANGE COEFFICIENTS CD AND CH IN THE CASE OF TWO ROUGHNESS
!     LENGTHS Z0 DYNAMICAL AND ZOH THERMAL.

! - TEMPORAIRE(S) 1D .

! PCDNH     : COEFFICIENT THERMIQUE NEUTRE D'ECHANGE EN SURFACE.
!            : NEUTRAL SURFACE THERMAL EXCHANGE COEFFICIENT.

  ZRZH=FZH(ZDPHI(JLON),PGZ0H(JLON))
  IF (LZ0HSREL.AND.LSNV) THEN
    ZGZ0MR=PLSM(JLON)*SQRT(MAX(ZGZ0N**2,PGZ0(JLON)**2&
     &-PGZ0RLF(JLON)**2))+(1.0_JPRB-PLSM(JLON))*PGZ0(JLON)
    ZGZ0MR=MAX(ZGZ0MR,PGZ0H(JLON))
    ZRZDMR=1.0_JPRB+ZDPHI(JLON)/ZGZ0MR
  ELSEIF (LZ0HSREL.AND.(.NOT.LSNV)) THEN
    ZGZ0MR=PLSM(JLON)*PGZ0H(JLON)/STHER+(1.0_JPRB-PLSM(JLON))*PGZ0(JLON)
    ZRZDMR=1.0_JPRB+ZDPHI(JLON)/ZGZ0MR
  ELSE
    ZGZ0MR=PGZ0(JLON)
    ZRZDMR=ZRZD
  ENDIF
  PCDNH(JLON)=C3TKEFREE*VKARMN**2/(LOG(ZRZH)*LOG(ZRZDMR))

!     CISAILLEMENT DE VENT.
!     WIND SHEAR.

! - TEMPORAIRE(S) 1D .

! ZU        : MODULE DU VENT DE SURFACE.
!            : SURFACE WIND SPEED.

  ZCIS=PU(JLON,KLEV)**2+PV(JLON,KLEV)**2+(GCISMIN*ZDPHI(JLON)/RG)**2
  ZU(JLON)=SQRT(ZCIS)
  PRTI(JLON)=2.0_JPRB/(PR(JLON,KLEV)*PT(JLON,KLEV)+RKAPPA*ZDPHI(JLON)&
  & +PRS(JLON)*PTS(JLON))  
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACTKEZOT',1,ZHOOK_HANDLE)
END SUBROUTINE ACTKEZOT
