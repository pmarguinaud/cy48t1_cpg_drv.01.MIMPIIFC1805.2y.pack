!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACTKEZOTLS ( YDPHY0,KIDIA,KFDIA,KLON,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 & PAPHI,PAPHIF,PR,PT,&
 ! - INPUT  1D .
 & PGZ0,PGZ0H,&
 & PTS,PQS,&
 ! - OUTPUT 1D .
 & PCDN,PCDNH,PCPS,PRTI,PRS)

!**** *ACTKEZOTLS   * - DETERMINATION DES CARACTERISTIQUES DE SURFACE.

!     Sujet.
!     ------

!     - ROUTINE DE CALCUL ACTIF .
!       DETERMINATION DE CARACTERISTIQUES DE SURFACE

!     - COMPUTATION OF SURFACE CHARACTERISTICS
!**   Interface.
!     ----------
!        *CALL* *ACTKEZOTLS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIEL AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PT         : TEMPERATURE.

! - 1D (PROGNOSTIQUE) .

! PTS        : TEMPERATURE DE SURFACE.
! PQS        : HUMIDITE SPECIFIQUE DE SURFACE.

! - 1D (DIAGNOSTIQUE) .

! PGZ0       : G FOIS LA LONGUEUR DE RUGOSITE COURANTE.
! PGZ0H      : G*LONGUEUR DE RUGOSITE THERMIQUE COURANTE (SI KVCLIV >= 8)

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 1D (DIAGNOSTIQUE) .

! PCDN       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE.
! PCDNH       : COEFFICIENT NEUTRE D'ECHANGE EN SURFACE. for heat
! PCPS       : CHALEUR MASSIQUE A PRESSION CONSTANTE DE L'AIR AU SOL.
! PRTI       : INVERSE DE R*T.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY1/

!-----------------------------------------------------------------------

!     Methode.
!     --------

!     Auteur.
!     -------
!        2012-07, I. Bastak-Duran after ACTKEZOTLS  (extracted from ACHMT)

!     Modifications.
!     --------------
!
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE YOMCST   , ONLY : RD       ,RV       ,&
 & RCPD     ,RCPV     ,RKAPPA
USE YOMPHY0  , ONLY : TPHY0

!-----------------------------------------------------------------------

#define FZH(X,Y) 1.0_JPRB+X/Y

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY0)       ,INTENT(IN)    :: YDPHY0
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0H(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCDNH(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRTI(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRS(KLON)
!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZDPHI(KLON)
!REAL(KIND=JPRB) :: ZRS(KLON) 

INTEGER(KIND=JPIM) :: JLON

REAL(KIND=JPRB) :: ZCPVMD,ZRVMD,ZRZD,ZRZH
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACTKEZOTLS',0,ZHOOK_HANDLE)
ASSOCIATE(VKARMN=>YDPHY0%VKARMN, C3TKEFREE=>YDPHY0%C3TKEFREE)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CONSTANTES AUXILIAIRES.

!         AUXILIARY CONSTANTS.


ZCPVMD=RCPV-RCPD
ZRVMD=RV-RD


!     ------------------------------------------------------------------
!     II - CALCUL DE L EPAISSEUR DE LA CLS (EN GEOPOTENTIEL).

!          COMPUTATION OF THE THICKNESS OF THE SBL (IN GEOPOTENTIAL).

! - TEMPORAIRE(S) 1D .

! ZDPHI     : EPAISSEUR EN GEOPOTENTIEL DU NIVEAU DE SURFACE.
!            : GEOPOTENTIAL THICKNESS OF THE SURFACE LEVEL.

DO JLON=KIDIA,KFDIA
  ZDPHI(JLON)=PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV)
ENDDO

!*
!     ------------------------------------------------------------------
!     III - CALCULS PROPREMENT DITS DES CARACTERISTIQUES DE SURFACE.

!          EFFECTIVE CALCULATIONS OF THE SURFACE CHARACTERISTICS.

!DEC$ IVDEP
DO JLON=KIDIA,KFDIA


!     CALCULS RELATIFS A L'HUMIDITE DE SURFACE.
!     COMPUTATIONS CONCERNING SURFACE HUMIDITY.

!     CALCULS OPTIONNELS SUIVANT LE TYPE DE SCHEMA SOL/VEG CHOISI.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN SOIL/VEG SCHEME.

  PCPS(JLON)=RCPD+ZCPVMD*PQS(JLON)
  PRS(JLON)=RD+ZRVMD*PQS(JLON)

!     CALCULS GEOMETRIQUES.
!     GEOMETRIC CALCULATIONS.

  ZRZD=1.0_JPRB+ZDPHI(JLON)/PGZ0(JLON)
  PCDN(JLON)=(VKARMN/LOG(ZRZD))**2

!     CALCUL PRELIMINAIRES NECESSAIRES A LA DETERMINATION DES
!     COEFFICIENTS D'ECHANGE CD ET CH DANS LE CAS DE DEUX RUGOSITES
!     DYNAMIQUE Z0 ET THERMIQUE ZOH.

!     PRELIMINARY COMPUTATIONS NECESSARY FOR THE DETERMINATION OF
!     EXCHANGE COEFFICIENTS CD AND CH IN THE CASE OF TWO ROUGHNESS
!     LENGTHS Z0 DYNAMICAL AND ZOH THERMAL.

! - TEMPORAIRE(S) 1D .

! PCDNH     : COEFFICIENT THERMIQUE NEUTRE D'ECHANGE EN SURFACE.
!            : NEUTRAL SURFACE THERMAL EXCHANGE COEFFICIENT.

  ZRZH=FZH(ZDPHI(JLON),PGZ0H(JLON))
  PCDNH(JLON)=C3TKEFREE*VKARMN**2/(LOG(ZRZH)*LOG(ZRZD))

!     CALCUL DE STABILITE.
!     STABILITY COMPUTATION.

! - TEMPORAIRE(S) 1D .

  PRTI(JLON)=2.0_JPRB/(PR(JLON,KLEV)*PT(JLON,KLEV)+RKAPPA*ZDPHI(JLON)&
   & +PRS(JLON)*PTS(JLON))  
ENDDO

#undef FZH
!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACTKEZOTLS',1,ZHOOK_HANDLE)
END SUBROUTINE ACTKEZOTLS
