!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACUPD( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
! ----------------------------------------------------------------------
! - INPUT  2D .
                &PAPRSF,PCP, PLHS, PLHV, PR,&
                &PIPOI, PPOID, PFALLR, PFALLS,PFALLG, &
                &PFPEVPSL,PFPEVPSN,PFPEVPSG,PFPEVPCL,PFPEVPCN,PFPEVPCG,PFHP,&
                &PDIFCSD,PDIFCQD,PDIFCQLD,PDIFCQID,&
! - INPUT/OUTPUT  2D .
                &PZQV,PZQL,PZQI,PZQR,PZQS,PZQG,PZT,PENTCH,&
                &PFCQVNG,PFCQING,PFCQLNG,PFCQRNG,PFCQSNG,PFCQGNG,&
                &PFPLSL, PFPLSN, PFPLSG)
! **** *ACUPD * - UPDATE INTERNAL STATE AFTER DOWNDRAUGHT
! **   Interface.
!      ----------
! *CALL* *ACUPD*
! ----------------------------------------------------------------------
! - INPUT ARGUMENTS
! -------------------

! - DIMENSIONING PARAMETERS FOR PHYSICS

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (NIVEL DU SOMMET
!            : DU NUAGE).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".


! - PHYSICAL VARIABLES

! - 2D (1:KLEV) .
! PAPRSF    : Pressure at full levels
! PCP       : Mean grid box specific heat cp
! PLHS      : Sublimation latent heat
! PLHV      : Vaporization latent heat
! PR        : Mean grid box perfect gas constant
! PIPOI     : g*dt/delta p
! PPOID     : delta p/g*dt
! PFALLR    : Rain fall velocity
! PFALLS    : Snow fall velocity

! - 2D FLUXES (0:KLEV) .
! PFPEVPSL Liquid precipitation evaporation in APLMPHYS
! PFPEVPSN Solid precipitation evaporation in APLMPHYS
! PFPEVPCL Liquid precipitation evaporation in DD
! PFPEVPCN Solid precipitation evaporation in DD
! PFHP   : Latent heat flux associated to precipitation for dd closure
! PDIFCSD downdraft entalphy flux increment
! PDIFCQD downdraft water vapour flux increment
! PDIFCQLD downdraft cloud water flux increment
! PDIFCQID downdraft cloud ice flux increment

! ----------------------------------------------------------------------
! -   OUTPUT ARGUMENTS (I/O because initialized to zero in aplpar)
!     ----------------
! - 1D
! ----------------------------------------------------------------------
! -   INPUT/OUTPUT ARGUMENTS (I/O because update here)
!     ----------------------
! - 2D (1:KLEV)
! PZQI :  cloud ice of the cascade.
! PZQL :  cloud water of the cascade.
! PZQV :  water vapour of the cascade.
! PZQR :  rain of the cascade.
! PZQS :  snow of the cascade.
! PZT  :  temperature of the cascade.

! - 2D FLUXES (0:KLEV)
! PFCQVNG : water vapour negative values
! PFCQING : cloud ice negative values
! PFCQLNG : cloud water negative values
! PFCQRNG : rain negative values
! PFCQSNG : snow negative values
! PFPLSL Total liquid precipitation
! PFPLSN Total solid precipitation
! ----------------------------------------------------------------------
!     Author.
!     -------
!     01-2007, L. Gerard 

!     Modifications.
!     --------------
!        2007  J.F. Geleyn, A posteriori Sedimentation
!     04-2007, L. Gerard , revision of LUDEN concept
!        A.Bogatchev : 26-Oct-2007 removing of severe breaks of coding rools 
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        R. Brozkova (Nov 2010): precipitation flux correction
!        2011-06: M. Jerczynski - some cleaning to meet norms
!        R. Brozkova (Jun 2012): sedimentation and flux correction
!        R. Brozkova (Oct 2012): settings for detrainment
!        R. Brozkova (Nov 2013): correction for negative rain
!        A. Trojakova (Oct 2020): reproducibility fix for graupel
! ----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1 , ONLY: JPIM, JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

! ----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN) :: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV
INTEGER(KIND=JPIM),INTENT(IN) :: KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA
REAL(KIND=JPRB) ,INTENT(IN) :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PCP(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PLHS(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PLHV(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PIPOI(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PPOID(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFALLR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFALLS(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFALLG(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFPEVPSL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFPEVPSN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFPEVPSG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPEVPCL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPEVPCN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPEVPCG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PFHP(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDIFCSD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDIFCQD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDIFCQLD(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(IN) :: PDIFCQID(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQI(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQL(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQV(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQR(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQS(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZQG(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PZT(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PENTCH(KLON,KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQVNG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQING(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQLNG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQRNG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQSNG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFCQGNG(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPLSL(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB) ,INTENT(INOUT) :: PFPLSG(KLON,0:KLEV)

! ----------------------------------------------------------------------

INTEGER (KIND=JPIM) :: JLEV, JLON
REAL(KIND=JPRB) :: ZEPS1, ZRHOAIR, ZSTAL0, ZSTAL1, ZSTAL2,&
                &  ZSTAN0, ZSTAN1, ZSTAN2, ZSTAG0, ZSTAG1, ZSTAG2, &
                &  ZZSLS, ZZSNS, ZZSGS, ZCORS, ZCORG, ZCOR, ZTT, ZNUMIN, ZIEV
REAL(KIND=JPRB) :: ZQX1, ZDQR, ZDQS, ZDQG, ZDQL, ZDQI, ZDQC, &
                &  ZQV0, ZDQV, ZDEVQR, ZDEVQS, ZDEVQG,  ZPHCLOS
REAL(KIND=JPRB) :: ZGRAPRO
REAL(KIND=JPRB) :: ZCFPLSL(KLON,0:KLEV), ZCFPLSN(KLON,0:KLEV),ZCFPLSG(KLON,0:KLEV),&
                &  ZSTAL3(KLON), ZSTAN3(KLON), ZSTAG3(KLON)
REAL(KIND=JPRB) :: ZFCQRNG(KLON,0:KLEV),ZFCQSNG(KLON,0:KLEV),ZFCQGNG(KLON,0:KLEV),&
                  &ZFCQVNG(KLON,0:KLEV),ZFCQING(KLON,0:KLEV),&
                  &ZFCQLNG(KLON,0:KLEV),ZFPEVPCL(KLON,0:KLEV),&
                  &ZFPEVPCN(KLON,0:KLEV),ZFPEVPCG(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

! ----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACUPD',0,ZHOOK_HANDLE)
ASSOCIATE(GPEFDC=>YDML_PHY_MF%YRPHY0%GPEFDC, GDDEVF=>YDML_PHY_MF%YRPHY0%GDDEVF, GPEIPHI=>YDML_PHY_MF%YRPHY0%GPEIPHI, &
 & GPETAU=>YDML_PHY_MF%YRPHY0%GPETAU, TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LENTCH=>YDML_PHY_MF%YRPHY%LENTCH, LNSDO=>YDML_PHY_MF%YRPHY%LNSDO, LSEDLAG=>YDML_PHY_MF%YRPHY%LSEDLAG, &
 & LCDDEVPRO=>YDML_PHY_MF%YRPHY%LCDDEVPRO,  LSEDSTA=>YDML_PHY_MF%YRPHY%LSEDSTA,&
 & LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
! ----------------------------------------------------------------------

! *
! ------------------------------------------------------------------
!     I - INITIALISATIONS

ZEPS1=1.E-10_JPRB

IF (LCDDEVPRO) THEN
  ZPHCLOS=GDDEVF
ELSEIF (LNSDO) THEN 
  ZPHCLOS=0._JPRB
ELSE
  ZPHCLOS=1._JPRB
ENDIF

ZFCQING(:,:)=0.0_JPRB
ZFCQLNG(:,:)=0.0_JPRB
ZFCQRNG(:,:)=0.0_JPRB
ZFCQSNG(:,:)=0.0_JPRB
ZFCQVNG(:,:)=0.0_JPRB
ZFCQGNG(:,:)=0.0_JPRB
IF (LGRAPRO) THEN
  ZGRAPRO=1._JPRB
ELSE
  ZGRAPRO=0._JPRB
ENDIF

! ------------------------------------------------------------------
!    II - CALCULATIONS

! SEDIMENTATION A POSTERIORI CORRECTION FOR DOWNDRAFTS.

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    ZCFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV)
    ZCFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV)
    ZCFPLSG(JLON,JLEV)=PFPLSG(JLON,JLEV)
    ZFPEVPCL(JLON,JLEV)=PFPEVPCL(JLON,JLEV)
    ZFPEVPCN(JLON,JLEV)=PFPEVPCN(JLON,JLEV)
    ZFPEVPCG(JLON,JLEV)=PFPEVPCG(JLON,JLEV)
  ENDDO
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA

    ZRHOAIR=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PZT(JLON,JLEV))
    ZZSLS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/PFALLR(JLON,JLEV))
    ZZSNS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/PFALLS(JLON,JLEV))
    ZZSGS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/PFALLG(JLON,JLEV))

    IF (LSEDSTA) THEN
      ZSTAL0=EXP(-ZZSLS)
      ZSTAL1=(1.0_JPRB-ZSTAL0)/ZZSLS
      ZSTAL2=ZSTAL0/(1._JPRB+ZZSLS+0.5_JPRB*(SQRT((1._JPRB+ZZSLS)&
       &**2+4._JPRB*ZZSLS)-(1._JPRB+ZZSLS)))
      ZSTAL3(JLON)=0.5_JPRB*(ZSTAL1+ZSTAL2)
      ZSTAN0=EXP(-ZZSNS)
      ZSTAN1=(1.0_JPRB-ZSTAN0)/ZZSNS
      ZSTAN2=ZSTAN0/(1._JPRB+ZZSNS+0.5_JPRB*(SQRT((1._JPRB+ZZSNS)&
       &**2+4._JPRB*ZZSNS)-(1._JPRB+ZZSNS)))
      ZSTAN3(JLON)=0.5_JPRB*(ZSTAN1+ZSTAN2)
      ZSTAG0=EXP(-ZZSGS)
      ZSTAG1=(1.0_JPRB-ZSTAG0)/ZZSGS
      ZSTAG2=ZSTAG0/(1._JPRB+ZZSGS+0.5_JPRB*(SQRT((1._JPRB+ZZSGS)&
       &**2+4._JPRB*ZZSGS)-(1._JPRB+ZZSGS)))
      ZSTAG3(JLON)=0.5_JPRB*(ZSTAG1+ZSTAG2)
    ENDIF
    
    IF (LSEDLAG) THEN
      ZSTAL0=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSLS))
      ZSTAL1=MIN(1.0_JPRB,1.0_JPRB/ZZSLS)
      ZSTAL2=MAX(0.0_JPRB,1.0_JPRB-ZZSLS)
      ZSTAL3(JLON)=0.5_JPRB*(ZSTAL1+ZSTAL2)
      ZSTAN0=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSNS))
      ZSTAN1=MIN(1.0_JPRB,1.0_JPRB/ZZSNS)
      ZSTAN2=MAX(0.0_JPRB,1.0_JPRB-ZZSNS)
      ZSTAN3(JLON)=0.5_JPRB*(ZSTAN1+ZSTAN2)
      ZSTAG0=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSGS))
      ZSTAG1=MIN(1.0_JPRB,1.0/ZZSGS)
      ZSTAG2=MAX(0.0_JPRB,1.0_JPRB-ZZSGS)
      ZSTAG3(JLON)=0.5_JPRB*(ZSTAG1+ZSTAG2)
    ENDIF

    PFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV-1)&
     &+(ZCFPLSN(JLON,JLEV)-ZCFPLSN(JLON,JLEV-1))&
     &-ZSTAN3(JLON)*(ZFPEVPCN(JLON,JLEV)-ZFPEVPCN(JLON,JLEV-1))
    ZCORS=MIN(0._JPRB,PFPLSN(JLON,JLEV))

    PFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV)-ZCORS

    PFPLSG(JLON,JLEV)=(1._JPRB-ZGRAPRO)*PFPLSG(JLON,JLEV)&
     &+ZGRAPRO*(PFPLSG(JLON,JLEV-1)&
     &+(ZCFPLSG(JLON,JLEV)-ZCFPLSG(JLON,JLEV-1))&
     &-ZSTAG3(JLON)*(ZFPEVPCG(JLON,JLEV)-ZFPEVPCG(JLON,JLEV-1)))
    ZCORG=ZGRAPRO*MIN(0._JPRB,PFPLSG(JLON,JLEV))

    PFPLSG(JLON,JLEV)=PFPLSG(JLON,JLEV)-ZCORG

    PFPEVPCN(JLON,JLEV)=PFPEVPCN(JLON,JLEV-1)&
     &+ZFPEVPCN(JLON,JLEV)-ZFPEVPCN(JLON,JLEV-1)+ZCORS/ZSTAN3(JLON)
    PFPEVPCL(JLON,JLEV)=PFPEVPCL(JLON,JLEV-1)&
     &+ZFPEVPCL(JLON,JLEV)-ZFPEVPCL(JLON,JLEV-1)-ZCORS/ZSTAL3(JLON)
    PFPEVPCG(JLON,JLEV)=(1._JPRB-ZGRAPRO)*PFPEVPCG(JLON,JLEV)&
     &+ZGRAPRO*(PFPEVPCG(JLON,JLEV-1)&
     &+ZFPEVPCG(JLON,JLEV)-ZFPEVPCG(JLON,JLEV-1)+ZCORG/ZSTAG3(JLON))

    PFPEVPCL(JLON,JLEV)=PFPEVPCL(JLON,JLEV)-ZCORG/ZSTAL3(JLON)

    PFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV-1)&
     &+(ZCFPLSL(JLON,JLEV)-ZCFPLSL(JLON,JLEV-1))&
     &-ZSTAL3(JLON)*(PFPEVPCL(JLON,JLEV)-PFPEVPCL(JLON,JLEV-1))

    ! prevent negative rain
    ZCOR=MIN(0._JPRB,PFPLSL(JLON,JLEV))

    PFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV)-ZCOR
    PFPEVPCL(JLON,JLEV)=PFPEVPCL(JLON,JLEV) + ZCOR/ZSTAL3(JLON)

  ENDDO
ENDDO

!cdir unroll=8
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA

    !   UPDATE NEGATIVE VALUES CORRECTION FLUXES
    !   ----------------------------------------

    ! rain due to sedimentation and evaporation
    ZDEVQR=-PIPOI(JLON,JLEV)*&
      & (PFPEVPCL(JLON,JLEV)-PFPEVPCL(JLON,JLEV-1))
    ZQX1=PZQR(JLON,JLEV)+ZDEVQR-PIPOI(JLON,JLEV)*(0.0_JPRB&
      & +(PFPLSL(JLON,JLEV) - PFPLSL(JLON,JLEV-1))&
      & -(ZCFPLSL(JLON,JLEV) - ZCFPLSL(JLON,JLEV-1)) )
    PZQR(JLON,JLEV)=MAX(0.0_JPRB,ZQX1)
    ZDQR=MAX(0.0_JPRB,ZQX1)-ZQX1
    ZFCQRNG(JLON,JLEV)=ZFCQRNG(JLON,JLEV-1)-ZDQR*PPOID(JLON,JLEV)
    PFCQRNG(JLON,JLEV)=PFCQRNG(JLON,JLEV)+ZFCQRNG(JLON,JLEV)

    ! snow due to sedimentation and evaporation
    ZDEVQS=-PIPOI(JLON,JLEV)*&
      & (PFPEVPCN(JLON,JLEV)-PFPEVPCN(JLON,JLEV-1))
    ZQX1=PZQS(JLON,JLEV)+ZDEVQS-PIPOI(JLON,JLEV)*(0.0_JPRB&
      & +(PFPLSN(JLON,JLEV) - PFPLSN(JLON,JLEV-1))&
      & -(ZCFPLSN(JLON,JLEV) - ZCFPLSN(JLON,JLEV-1)) )
    PZQS(JLON,JLEV)=MAX(0.0_JPRB,ZQX1)
    ZDQS=MAX(0.0_JPRB,ZQX1)-ZQX1
    ZFCQSNG(JLON,JLEV)=ZFCQSNG(JLON,JLEV-1)-ZDQS*PPOID(JLON,JLEV)
    PFCQSNG(JLON,JLEV)=PFCQSNG(JLON,JLEV)+ZFCQSNG(JLON,JLEV)

    ! graupel due to sedimentation and evaporation
    ZDEVQG=-PIPOI(JLON,JLEV)*&
      & (PFPEVPCG(JLON,JLEV)-PFPEVPCG(JLON,JLEV-1))
    ZQX1=PZQG(JLON,JLEV)+ZDEVQG-PIPOI(JLON,JLEV)*(0.0_JPRB  &
      & +(PFPLSG(JLON,JLEV) - PFPLSG(JLON,JLEV-1)) &
      & -(ZCFPLSG(JLON,JLEV) - ZCFPLSG(JLON,JLEV-1)) )
    PZQG(JLON,JLEV)=(1._JPRB-ZGRAPRO)*PZQG(JLON,JLEV)+ZGRAPRO*MAX(0.0_JPRB,ZQX1)
    ZDQG=ZGRAPRO*(MAX(0.0_JPRB,ZQX1)-ZQX1)
    ZFCQGNG(JLON,JLEV)=ZFCQGNG(JLON,JLEV-1)-ZDQG*PPOID(JLON,JLEV)
    PFCQGNG(JLON,JLEV)=PFCQGNG(JLON,JLEV)+ZFCQGNG(JLON,JLEV)

    ! cloud ice due to downdraft transport
    ZQX1=PZQI(JLON,JLEV)-PIPOI(JLON,JLEV)*&
      & (PDIFCQID(JLON,JLEV)-PDIFCQID(JLON,JLEV-1))
    PZQI(JLON,JLEV)=MAX(0.0_JPRB,ZQX1)
    ZDQI=MAX(0.0_JPRB,ZQX1)-ZQX1
    ZFCQING(JLON,JLEV)=ZFCQING(JLON,JLEV-1)-ZDQI*PPOID(JLON,JLEV)
    PFCQING(JLON,JLEV)=PFCQING(JLON,JLEV)+ZFCQING(JLON,JLEV)

    ! cloud water due to downdraft transport
    ZQX1=PZQL(JLON,JLEV)-PIPOI(JLON,JLEV)*&
      & (PDIFCQLD(JLON,JLEV)-PDIFCQLD(JLON,JLEV-1))
    PZQL(JLON,JLEV)=MAX(0.0_JPRB,ZQX1)
    ZDQL=MAX(0.0_JPRB,ZQX1)-ZQX1
    ZFCQLNG(JLON,JLEV)=ZFCQLNG(JLON,JLEV-1)-ZDQL*PPOID(JLON,JLEV)
    PFCQLNG(JLON,JLEV)=PFCQLNG(JLON,JLEV)+ZFCQLNG(JLON,JLEV)

    ZDQC=ZDQI+ZDQL+ZDQR+ZDQS+ZDQG

    ! water vapour due to downdraft transport and evaporation
    ! plus pumping due to negative values corrections
    ZQX1=PZQV(JLON,JLEV)-PIPOI(JLON,JLEV)* (&
      &  (PDIFCQD(JLON,JLEV)-PDIFCQD(JLON,JLEV-1))&
      & -(PFPEVPCL(JLON,JLEV)-PFPEVPCL(JLON,JLEV-1))&
      & -(PFPEVPCN(JLON,JLEV)-PFPEVPCN(JLON,JLEV-1))&
      & -ZGRAPRO*(PFPEVPCG(JLON,JLEV)-PFPEVPCG(JLON,JLEV-1)) )
    ZQV0=ZQX1-PIPOI(JLON,JLEV)*(0.0_JPRB-ZFCQVNG(JLON,JLEV-1)&
      & -ZFCQLNG(JLON,JLEV-1)-ZFCQING(JLON,JLEV-1)&
      & -ZFCQRNG(JLON,JLEV-1)-ZFCQSNG(JLON,JLEV-1)&
      & -ZGRAPRO*ZFCQGNG(JLON,JLEV-1) )
    PZQV(JLON,JLEV)=MAX(0.0_JPRB,ZQV0-ZDQC)
    ZDQV=MAX(0.0_JPRB,ZQV0-ZDQC)-ZQX1
    ZFCQVNG(JLON,JLEV)=ZFCQVNG(JLON,JLEV-1)-ZDQV*PPOID(JLON,JLEV)
    PFCQVNG(JLON,JLEV)=PFCQVNG(JLON,JLEV)+ZFCQVNG(JLON,JLEV)

    ! temperature due to downdraft transport, resolved evaporation modulated
    ! by the downdraft closure and evaporation in downdraft
    PZT(JLON,JLEV)=PZT(JLON,JLEV)-( PIPOI(JLON,JLEV)*&
     &  ( PDIFCSD(JLON,JLEV)-PDIFCSD(JLON,JLEV-1)&
     & +(PFHP(JLON,JLEV)-PFHP(JLON,JLEV-1))*ZPHCLOS )&
     & + PLHV(JLON,JLEV)*ZDEVQR+PLHS(JLON,JLEV)*ZDEVQS )/PCP(JLON,JLEV)

  ENDDO
ENDDO

IF(LENTCH) THEN
  ZTT=TSPHY/GPETAU
  ZNUMIN=GPEIPHI
!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA

  !   UPDATE FOR ADAPTATIVE DETRAINMENT.
  !   ----------------------------------

    ZIEV=MAX(0._JPRB,PFPEVPCL(JLON,KLEV)+PFPEVPCN(JLON,KLEV)+&
     &                           ZGRAPRO*PFPEVPCG(JLON,KLEV)+&
     &               PFPEVPSL(JLON,KLEV)+PFPEVPSN(JLON,KLEV)+&
     &                           ZGRAPRO*PFPEVPSG(JLON,KLEV))
    PENTCH(JLON,KLEV)=(PENTCH(JLON,KLEV)+(GPEFDC*ZIEV&
     & +ZNUMIN)*ZTT)/(1._JPRB+ZTT)

  ENDDO
ENDIF

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACUPD',1,ZHOOK_HANDLE)
END SUBROUTINE ACUPD
