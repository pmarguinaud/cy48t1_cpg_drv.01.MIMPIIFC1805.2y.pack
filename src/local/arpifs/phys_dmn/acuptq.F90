SUBROUTINE ACUPTQ ( KPROMA, KSTART, KPROF, KFLEV,LDPTQ,&
 !  Variables 2D Input
 & PFRSO  , PFRTH  ,                  &
 & PDIFCQ , PDIFCS , PDIFTQ , PDIFTS ,&
 & PFCCQL , PFCCQN , PFPLCL,  PFPLCN, &
 & PFECL  , PFECN  , PFACL  , PFACN  ,&
 & PRDELP , PT     , PQ     ,&
 !  Variables 1D Input
 & PTS    ,&
 !  Variables 2D Output
 & PTENDH ,PTENDQ )

!     ------------------------------------------------------------------
!     EVOLUTION DE (T,Qv) APRES TURBULENCE ET CONVECTION
!     ------------------------------------------------------------------

!     ARGUMENTS D ENTREE
!     ------------------
! KPROMA     : DIMENSION HORIZONTALE
! KSTART     : DEBUT DE LA BOUCLE HORIZONTALE
! KPROF      : BORNE HORIZONTALE DES CALCULS
! KFLEV      : DIMENSION ET BORNE VERTICALE

! --- INPUT 2D (0:KLEV).
!     -----------------.
! PFRSO      : SOLAR FLUX
! PFRTH      : THERMAL FLUX
! PDIFCQ     : FLUX CONVECTIF D'HUMIDITE SPECIFIQUE (HORS COND. ET RR).
! PDIFCS     : FLUX CONVECTIF D'ENTHALPIE .
! PDIFTQ     : FLUX TURBULENT (ET Q NEGATIF) D'HUMIDITE SPECIFIQUE.
! PDIFTS     : FLUX TURBULENT D'ENTHALPIE (OU D'ENERGIE STATIQUE SECHE).
! PFCCQL     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES LIQUIDES.
! PFCCQN     : FLUX DE CONDENSATION LIE AUX RR CONVECTIVES NEIGEUSES.
! PFPLCL     : PRECIPITATIONS CONVECTIVES SOUS FORME LIQUIDE.
! PFPLCN     : PRECIPITATIONS CONVECTIVES SOUS FORME NEIGE.
! PFECL      : FLUX DE PRECIPITATIONS DU A L'EVAPORATION CONVECTIVE.
! PFECN      : FLUX DE PRECIPITATIONS DU A LA SUBLIMATION CONVECTIVE.
! PFACL      : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.
! PFACN      : FLUX DE PRECIPITATION CONVECTIVE: TERME DE GENERATION/FORMATION.

! --- INPUT 2D (1:KLEV).
!     -----------------.
! PRDELP     : INVERSE DE L'EPAISSEUR EN PRESSION DE LA COUCHE.
! PT         : TEMPERATURE.
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.

! - 1D.
!   --.
! PTS        : TEMPERATURE DE SURFACE.

! --- SORTIES 2D (1:KFLEV).
!     --------------------.
! PTENDH     : TENDANCE DE L'ENTHALPIE.
! PTENDQ     : TENDANCE DE L'HUMIDITE.

! --- AUTEUR.
!     ------.
!     25/10/10 F.BOUYSSEL.

! --- MODIFICATION.
!     ------------.
!     10/07/19 Y.BOUTELOUP : Add PFRSO and PFRTH for Tiedtke/Bechtold scheme     
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG, RCPD, RCW, RCS, RLVZER, RLSZER

!-----------------------------------------------------------------------

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTART 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROF 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRSO   (KPROMA,0:KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFRTH   (KPROMA,0:KFLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCQ  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFCS  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTQ  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDIFTS  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCCQL  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFCCQN  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCL  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCN  (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFECL   (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFECN   (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFACL   (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFACN   (KPROMA,0:KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP  (KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT      (KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ      (KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS     (KPROMA)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDH  (KPROMA,KFLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDQ  (KPROMA,KFLEV) 
LOGICAL           ,INTENT(IN)    :: LDPTQ
!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZGSDP (KPROMA,KFLEV)
REAL(KIND=JPRB) :: ZJTOT (KPROMA,0:KFLEV)
REAL(KIND=JPRB) :: ZTI   (KPROMA,0:KFLEV)

INTEGER(KIND=JPIM) :: JLEV, JROF

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACUPTQ',0,ZHOOK_HANDLE)
!-----------------------------------------------------------------------

! Calculation of temperatures on half levels

DO JROF = KSTART, KPROF
  ZTI(JROF,0)=PT(JROF,1)
  ZTI(JROF,KFLEV)=PTS(JROF)
ENDDO

DO JLEV= 1, KFLEV-1
  DO JROF = KSTART, KPROF
    ZTI(JROF,JLEV)=(PT(JROF,JLEV)+PT(JROF,JLEV+1))*0.5_JPRB
  ENDDO
ENDDO

! Computation of the total enthalpy flux J_tot

DO JLEV= 0, KFLEV
  DO JROF = KSTART, KPROF
     IF (LDPTQ) THEN
        ZJTOT(JROF,JLEV) = PFRSO(JROF,JLEV)+PFRTH(JROF,JLEV)+&
            & PDIFTS(JROF,JLEV)+PDIFCS(JROF,JLEV)+&
            & (RCW-RCPD)*PFPLCL(JROF,JLEV)*ZTI(JROF,JLEV)+&
            & (RCS-RCPD)*PFPLCN(JROF,JLEV)*ZTI(JROF,JLEV)-&
            & RLVZER*(PFCCQL(JROF,JLEV)-PFECL(JROF,JLEV))-&
            & RLSZER*(PFCCQN(JROF,JLEV)-PFECN(JROF,JLEV))
      ELSE      
        ZJTOT(JROF,JLEV) = PFRSO(JROF,JLEV)+PFRTH(JROF,JLEV)+&
            & PDIFTS(JROF,JLEV)
      ENDIF      
  ENDDO
ENDDO

!CDIR UNROLL=8
DO JLEV = 1, KFLEV
  DO JROF = KSTART, KPROF

    ZGSDP(JROF,JLEV)=RG*PRDELP(JROF,JLEV)

    IF (LDPTQ) THEN
       PTENDQ(JROF,JLEV) = ZGSDP(JROF,JLEV)*(&
        & (PDIFTQ (JROF,JLEV-1) - PDIFTQ (JROF,JLEV)) +&
        & (PDIFCQ (JROF,JLEV-1) - PDIFCQ (JROF,JLEV)) +&
        & (PFCCQL (JROF,JLEV-1) - PFCCQL (JROF,JLEV)) +&
        & (PFCCQN (JROF,JLEV-1) - PFCCQN (JROF,JLEV)) -&
        & (PFECL  (JROF,JLEV-1) - PFECL  (JROF,JLEV)) -&
        & (PFECN  (JROF,JLEV-1) - PFECN  (JROF,JLEV)))
       PTENDH(JROF,JLEV) = ZGSDP(JROF,JLEV)*(ZJTOT  (JROF,JLEV-1) - ZJTOT  (JROF,JLEV)) 
    ELSE    
       PTENDQ(JROF,JLEV) = MAX(0._JPRB,ZGSDP(JROF,JLEV)*(PDIFTQ (JROF,JLEV-1) - PDIFTQ (JROF,JLEV)))
       PTENDH(JROF,JLEV) = MAX(0._JPRB,ZGSDP(JROF,JLEV)*(ZJTOT  (JROF,JLEV-1) - ZJTOT  (JROF,JLEV)))
    ENDIF   

  ENDDO
ENDDO

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACUPTQ',1,ZHOOK_HANDLE)
END SUBROUTINE ACUPTQ
