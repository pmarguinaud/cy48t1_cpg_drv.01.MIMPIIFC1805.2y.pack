SUBROUTINE APL_ARPEGE_ALBEDO_COMPUTATION (YDMF_PHYS_BASE_STATE, YDCPG_DIM, YDMF_PHYS, YDMF_PHYS_SURF, YDMODEL, PALBD, PALBP, PEPS0, PFLU_EMIS, PFLU_NEIJ, PMERL, PRDG_MU0)

USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(CPG_DIM_TYPE),             INTENT(IN)    :: YDCPG_DIM
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBD(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBP(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PEPS0
REAL(KIND=JPRB),                INTENT(OUT)   :: PFLU_EMIS (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_NEIJ (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PMERL(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0 (YDCPG_DIM%KLON)


INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG


! OF ALBEDO COMPUTATION (IF NOT SURFEX)
IF (.NOT.YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
!DEC$ IVDEP
  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)-PFLU_NEIJ(JLON)*(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)&
    & -MAX(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON),YDMODEL%YRML_PHY_MF%YRPHY1%ALCRIN))
    PFLU_EMIS(JLON)=YDMF_PHYS_SURF%GSD_VF%PEMISF(JLON)-PFLU_NEIJ(JLON)*(YDMF_PHYS_SURF%GSD_VF%PEMISF(JLON)-YDMODEL%YRML_PHY_MF%YRPHY1%EMCRIN)
  ENDDO

    
      ! diffuse and direct (parallel) albedo in NSW solar intervals
      
!DEC$ IVDEP
  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    PMERL(JLON)=(1.0_JPRB-YDMF_PHYS_SURF%GSD_VF%PLSM(JLON))*(1.0_JPRB&
    & -MAX(0.0_JPRB,SIGN(1.0_JPRB,YDMODEL%YRML_PHY_MF%YRPHY1%TMERGL-YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON))))
    PFLU_EMIS(JLON)=PFLU_EMIS(JLON)*YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)+PMERL(JLON)*YDMODEL%YRML_PHY_MF%YRPHY1%EMMMER&
    & +(1.0_JPRB-YDMF_PHYS_SURF%GSD_VF%PLSM(JLON))*(1.0_JPRB-PMERL(JLON))*YDMODEL%YRML_PHY_MF%YRPHY1%EMMGLA
  ENDDO
  DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
    DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      PALBD(JLON,JSG)=(1.0_JPRB-PMERL(JLON))*YDMF_PHYS%OUT%ALB(JLON)+&
      & PMERL(JLON) *YDMODEL%YRML_PHY_MF%YRPHY1%ALBMED
      PALBP(JLON,JSG)=(1.0_JPRB-PMERL(JLON))*YDMF_PHYS%OUT%ALB(JLON)+&
      & PMERL(JLON) *&
      & MAX(0.037_JPRB/(1.1_JPRB*PRDG_MU0(JLON)**1.4_JPRB+0.15_JPRB),PEPS0)
    ENDDO
  ENDDO
ENDIF  ! .NOT.LMSE


END SUBROUTINE
