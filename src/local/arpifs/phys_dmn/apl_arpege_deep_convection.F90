SUBROUTINE APL_ARPEGE_DEEP_CONVECTION (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,    &
& YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDVARS, YDMODEL, PFPCOR, PQV, PTENHA, PTENQVA, PTENT)

!**** *APL_ARPEGE_DEEP_CONVECTION*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD  &
                       , ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE YOMCFU             , ONLY : TCFU
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),             INTENT(IN)    :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PFPCOR(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENHA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENQVA(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

#include "acuptq.intfb.h"
#include "cucalln_mf.intfb.h"
#include "culight.intfb.h"

INTEGER(KIND=JPIM) :: IBASC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: IBOTSC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICBOT(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ICTOP(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ISPPN2D
INTEGER(KIND=JPIM) :: ITOPC(YDCPG_OPTS%KLON)
INTEGER(KIND=JPIM) :: ITYPE(YDCPG_OPTS%KLON)
LOGICAL :: LLCUM(YDCPG_OPTS%KLON)
LOGICAL :: LLDSLPHY
LOGICAL :: LLLAND(YDCPG_OPTS%KLON)
LOGICAL :: LLLINOX(YDCPG_OPTS%KLON)
LOGICAL :: LLSC(YDCPG_OPTS%KLON)
LOGICAL :: LLSHCV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZACPR(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCDEPTH(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCEN(1,1,1)
REAL(KIND=JPRB) :: ZCTOPH(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZCUCONVCA(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZDIFF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZDXTDK(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZFCQLF(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFCQLI(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFHPCL(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFHPCN(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGAW(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZGEOM1(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGEOMH(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZGP2DSPPA(YDCPG_OPTS%KLON,1)
REAL(KIND=JPRB) :: ZICE(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZLCRIT_AER(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLIGH_CTG(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZLISUM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLRAIN(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLUDE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZLUDELI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,4)
REAL(KIND=JPRB) :: ZMFD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFDDE_RATE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZMFUDE_RATE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZPRECMX(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZRSUD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,2)
REAL(KIND=JPRB) :: ZSCAV(1)
REAL(KIND=JPRB) :: ZSNDE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,2)
REAL(KIND=JPRB) :: ZTENC(1,1,1)
REAL(KIND=JPRB) :: ZTENQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZTENV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZVDIFTS
REAL(KIND=JPRB) :: ZVERVEL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZWMEAN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB) :: ZWMFU(YDCPG_OPTS%KLON)

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JLON
LOGICAL :: LLPTQ


REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DEEP_CONVECTION', 0, ZHOOK_HANDLE)

! DEEP CONVECTION SCHEME : IFS deep convection scheme

!=PARALLEL

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZGEOM1(JLON,JLEV)     = YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF(JLON,JLEV)-YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,YDCPG_OPTS%KFLEVG)
    ZVERVEL(JLON,JLEV)    = YDCPG_DYN0%CTY%VVEL(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF(JLON,JLEV)
    ZLISUM(JLON,JLEV)     = 0.0_JPRB
    ZLCRIT_AER(JLON,JLEV) = 5.E-4_JPRB
  ENDDO
ENDDO

DO JLEV=0,YDCPG_OPTS%KFLEVG
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZGEOMH(JLON,JLEV)=YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,JLEV)-YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(JLON,YDCPG_OPTS%KFLEVG)
    ZFCQLF(JLON,JLEV)=0.0_JPRB
    ZFCQLI(JLON,JLEV)=0.0_JPRB
  ENDDO
ENDDO

DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
  LLLAND(JLON)    = (YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)>0.5_JPRB)
  LLSHCV(JLON)    = .FALSE.
  ZCUCONVCA(JLON) = 0.0_JPRB
  ZACPR(JLON)     = 0.0_JPRB
  ZDXTDK(JLON)    = YDGEOMETRY%YRGEM%RDELXN/YDVARS%GEOMETRY%GM%T0(JLON)
ENDDO

!=END PARALLEL

LLPTQ = .FALSE.

!=PARALLEL

CALL ACUPTQ (YDMODEL%YRCST,  YDCPG_OPTS%KLON, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, LLPTQ, YDMF_PHYS%OUT%FRSO, &
& YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTS,                       &
& YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, YDMF_PHYS%OUT%FPEVPCL,                    &
& YDMF_PHYS%OUT%FPEVPCN, YDMF_PHYS%OUT%FPFPCL, YDMF_PHYS%OUT%FPFPCN, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,                   &
& YDMF_PHYS_BASE_STATE%T, YDMF_PHYS_BASE_STATE%Q, YDMF_PHYS_BASE_STATE%YGSP_RR%T, PTENHA, PTENQVA )

!=END PARALLEL
    

!=PARALLEL

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA 
    PTENT(JLON,JLEV) = PTENHA(JLON,JLEV)/YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JLON,JLEV)
    ZTENQ(JLON,JLEV) = PTENQVA(JLON,JLEV)
    ZTENU(JLON,JLEV) = 0.0_JPRB
    ZTENV(JLON,JLEV) = 0.0_JPRB
  ENDDO
ENDDO

!=END PARALLEL

LLDSLPHY=.TRUE.
ZVDIFTS = 0._JPRB
ISPPN2D = 0

!=PARALLEL
         
CALL CUCALLN_MF (YDCPG_OPTS%RPLDARE, YDCPG_OPTS%RPLRG, YDCPG_OPTS%NSTEP, YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST,         &
& YDMODEL%YRML_PHY_RAD%YRERAD, YDMODEL%YRML_PHY_SLIN, YDMODEL%YRML_PHY_EC, YDMODEL%YRML_GCONF%YGFL,                        &
& YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, 0, YDCPG_OPTS%KFLEVG, ZDXTDK, ISPPN2D, LLLAND,                      &
& LLDSLPHY, YDMODEL%YRML_PHY_MF%YRPHY2%TSPHY, ZVDIFTS, YDMF_PHYS_BASE_STATE%T, PQV, YDMF_PHYS_BASE_STATE%U,                &
& YDMF_PHYS_BASE_STATE%V, ZLISUM, ZVERVEL, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTS, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, &
& YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, ZGEOM1, ZGEOMH, YDVARS%GEOMETRY%GM%T0,      &
& ZCUCONVCA, ZGP2DSPPA, PTENT, ZTENQ, ZTENU, ZTENV, ZACPR, ITOPC, IBASC, ITYPE, ICBOT, ICTOP, IBOTSC,                      &
& LLCUM, LLSC, LLSHCV, ZLCRIT_AER, ZLU, ZLUDE, ZLUDELI, ZSNDE, ZMFU, ZMFD, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS,       &
& ZFHPCL, ZFHPCN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, ZLRAIN, ZRSUD, YDMF_PHYS%OUT%STRCU, YDMF_PHYS%OUT%STRCV,       &
& ZFCQLF, ZFCQLI, ZMFUDE_RATE, ZMFDDE_RATE, YDMF_PHYS%OUT%CAPE, ZWMEAN, ZDIFF, 0, ZCEN, ZTENC, ZSCAV)

!=END PARALLEL

!=PARALLEL

DO JLEV=0,YDCPG_OPTS%KFLEVG
  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA 
    PFPCOR  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)+YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FCCQL  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)
    YDMF_PHYS%OUT%FCCQN  (JLON,JLEV)=YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FPFPCL (JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)
    YDMF_PHYS%OUT%FPFPCN (JLON,JLEV)=YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    YDMF_PHYS%OUT%FPEVPCL(JLON,JLEV)=0._JPRB
    YDMF_PHYS%OUT%FPEVPCN(JLON,JLEV)=0._JPRB
  ENDDO
ENDDO

!=END PARALLEL

  
IF(YDCPG_OPTS%LFLASH) THEN

! LIGHTNING PARAMETERIZATION. OUTPUT IS PFLASH, TOTAL LIGHTNING FLASH RATES.

!=PARALLEL

  ZGAW(:)=0._JPRB
  CALL CULIGHT (YDCPG_OPTS%RPLDARE, YDCPG_OPTS%RPLRG, YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST, YDMODEL%YRML_PHY_EC%YREPHY,      &
  & YDMODEL%YRML_GCONF%YGFL, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA,                                                                 &                        
  & YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, ZGAW, ZGAW, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, &
  & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, LLLAND, YDMF_PHYS_BASE_STATE%T,                       &
  & ZLU, ZMFU, YDMF_PHYS%OUT%CAPE, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, LLCUM, ICBOT, ICTOP,                                &
  & LLLINOX, YDMF_PHYS%OUT%FLASH, ZLIGH_CTG, ZCTOPH, ZPRECMX, ZICE, ZCDEPTH, ZWMFU) 

! LIGHTNING FLASH RATES ARE CONVERTED IN fl/km2/s BEFORE ENTERING CFU TIME ACCUMULATION.

  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    YDMF_PHYS%OUT%FLASH(JLON)=YDMF_PHYS%OUT%FLASH(JLON)/86400._JPRB
  ENDDO

!=END PARALLEL

ENDIF

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_DEEP_CONVECTION', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_DEEP_CONVECTION
