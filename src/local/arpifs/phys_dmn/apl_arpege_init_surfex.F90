SUBROUTINE APL_ARPEGE_INIT_SURFEX (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_DIM, YDCPG_OPTS, YDCPG_MISC, &
& YDCPG_GPAR, YDMF_PHYS, YDVARS, YDMODEL, PALBD, PALBP, PFLU_EMIS, PSGROUPEL, PSRAIN, PSSNOW, PTSN)

USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE, CPG_OPTS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),             INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBD(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PALBP(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PFLU_EMIS (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSGROUPEL(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSRAIN(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSSNOW(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTSN(YDCPG_DIM%KLON)

#include "abor1.intfb.h"
#include "aro_ground_diag_z0.h"

INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG


!*
!     ------------------------------------------------------------------
!     4.TER.- INITIALISATIONS LIEES AU SCHEMA DE SURFACE EXTERNALISE
!     ------------------------------------------------------------------

IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN

!     INITIALISATION DU SCHEMA DE SURFACE EXTERNALISE ET DES
!     VARIABLES PSEUDO-HISTORIQUES ASSOCIEES

  IF ( (YDMODEL%YRML_PHY_MF%YRPARAR%NSWB_MNH /= YDMODEL%YRML_PHY_RAD%YRERAD%NSW) ) CALL ABOR1(                   &
                                                                                   & 'APLPAR: NSWB_MNH not = NSW'&
                                                                                   & )

  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDMF_PHYS%OUT%GZ0     (JLON) = YDCPG_GPAR%GZ0(JLON)
    YDMF_PHYS%OUT%GZ0H    (JLON) = YDCPG_GPAR%GZ0H(JLON)
    PFLU_EMIS    (JLON) = YDCPG_GPAR%VEMIS(JLON)
    YDCPG_MISC%QS      (JLON) = YDCPG_GPAR%VQS(JLON)
    YDMF_PHYS_BASE_STATE%YGSP_RR%T      (JLON) = YDCPG_GPAR%VTS(JLON)
    PSRAIN   (JLON) = YDCPG_GPAR%RAIN(JLON)
    PSSNOW   (JLON) = YDCPG_GPAR%SNOW(JLON)
    PSGROUPEL(JLON) = 0._JPRB
    PTSN     (JLON) = YDMF_PHYS_BASE_STATE%YGSP_RR%T(JLON)
  ENDDO

    
      ! FMR  radiation => NSW solar bands
  DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
    DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      PALBP(JLON,JSG) = YDCPG_GPAR%ALBDIR(JLON,JSG)
      PALBD(JLON,JSG) = YDCPG_GPAR%ALBSCA(JLON,JSG)
    ENDDO
  ENDDO
  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=0.0_JPRB
  ENDDO
  DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
    DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS%OUT%ALB(JLON)+0.5*(PALBP(JLON,JSG)+PALBD(JLON,JSG))
    ENDDO
  ENDDO
  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    YDMF_PHYS%OUT%ALB(JLON)=YDMF_PHYS%OUT%ALB(JLON)/REAL(YDMODEL%YRML_PHY_RAD%YRERAD%NSW,JPRB)
  ENDDO
    

  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    IF (PFLU_EMIS(JLON)==0._JPRB) THEN
      PFLU_EMIS(JLON) = 0.99_JPRB
      YDMF_PHYS_BASE_STATE%YGSP_RR%T  (JLON) = 288.0_JPRB
      YDMF_PHYS%OUT%ALB (JLON) = 0.1_JPRB
    ENDIF
  ENDDO

ENDIF  ! LMSE
!
! Define z0;z0h if it's necessary
!
IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE.AND.YDCPG_DIM%KSTEP == 0) THEN
  CALL ARO_GROUND_DIAG_Z0( YDCPG_DIM%KBL, YDCPG_DIM%KGPCOMP, YDCPG_DIM%KFDIA-YDCPG_DIM%KIDIA+1, YDCPG_DIM%KIDIA,                                      &
  & YDCPG_DIM%KFDIA, YDGEOMETRY%YRDIM%NDGUNG, YDGEOMETRY%YRDIM%NDGUXG, YDGEOMETRY%YRDIM%NDLUNG, YDGEOMETRY%YRDIM%NDLUXG,                              &
  & YDVARS%GEOMETRY%RINDX%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA), YDVARS%GEOMETRY%RINDY%T0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA),                             &
  & YDMODEL%YRML_PHY_MF%YRARPHY%LSURFEX_KFROM, YDMF_PHYS%OUT%GZ0(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA), YDMF_PHYS%OUT%GZ0H(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)&
  & )
ENDIF



END SUBROUTINE
