SUBROUTINE APL_ARPEGE_RADIATION (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_DIM, YDCPG_MISC, YDCPG_GPAR, &
& YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, PAER, PAERINDS, PALBD, PALBP, PCEMTR, PCTRSO,     &
& PFLU_EMIS, PFLU_QSAT, PQO3, PQR, PQS, PQV, PRDG_MU0, PRDG_MU0LU,    &
& PRDG_MU0M, PSFSWDIF, PSFSWDIR, PSUDU, PTENT, PTRSOD, PTRSODIF, PTRSODIR)

USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),             INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(IN)    :: PAER(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG,6)
REAL(KIND=JPRB),                INTENT(IN)    :: PAERINDS(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBD(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBP(YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCEMTR(YDCPG_DIM%KLON,0:1)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCTRSO(YDCPG_DIM%KLON,0:1)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_EMIS (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_QSAT (YDCPG_DIM%KLON, 1:YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQO3(YDCPG_DIM%KLON,0:YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQR(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQS(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQV(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0 (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0LU (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0M (YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIF (YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIR (YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PSUDU(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(INOUT) :: PTENT(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSOD(YDCPG_DIM%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIF (YDCPG_DIM%KLON,YDCPG_DIM%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIR (YDCPG_DIM%KLON,YDCPG_DIM%KSW)

#include "acdayd.intfb.h"
#include "acrso.intfb.h"
#include "radheat.intfb.h"
#include "recmwf.intfb.h"

REAL(KIND=JPRB)     :: ZCGA_DST (1,1,1)
REAL(KIND=JPRB)     :: ZPIZA_DST (1,1,1)
REAL(KIND=JPRB)     :: ZTAUREL_DST (1,1,1)
REAL(KIND=JPRB)     :: ZFRSODS(YDCPG_DIM%KLON)
REAL(KIND=JPRB)     :: ZFSDNN(YDCPG_DIM%KLON)
REAL(KIND=JPRB)     :: ZFSDNV(YDCPG_DIM%KLON)
REAL(KIND=JPRB)     :: ZSDUR(YDCPG_DIM%KLON)
REAL(KIND=JPRB)     :: ZDSRP(YDCPG_DIM%KLON) 
REAL(KIND=JPRB)     :: ZALB(YDCPG_DIM%KLON)
LOGICAL             :: LLCALLRAD
INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG

LLCALLRAD=(MOD(YDCPG_DIM%KSTEP,YDMODEL%YRML_PHY_RAD%YRERAD%NRADFR) == 0 )
IF (YDMODEL%YRML_PHY_MF%YRPHY%NCALLRAD==2) LLCALLRAD=(LLCALLRAD.AND.(YDCPG_DIM%KSTEP<=YDMODEL%YRML_GCONF%YRRIP%NSTOP-1))

    ! ---- Intermittent call to radiation scheme
IF (LLCALLRAD) THEN
  CALL RECMWF(YDGEOMETRY%YRDIMV, YDMODEL, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG,         &
  & YDCPG_DIM%KSW, PALBD, PALBP, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,         &
  & YDCPG_MISC%NEB, PQO3, PAER, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, PFLU_EMIS, PRDG_MU0M, PQV,                    &
  & PFLU_QSAT, YDCPG_MISC%QICE, YDCPG_MISC%QLI, PQS, PQR, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_BASE_STATE%T,         &
  & YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDMF_PHYS%RAD%EMTD, YDMF_PHYS%RAD%EMTU, YDMF_PHYS%RAD%TRSW, YDMF_PHYS%OUT%FRTHC,  &
  & YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRSO, PSFSWDIR, PSFSWDIF, ZFSDNN, ZFSDNV,                  &
  & PCTRSO, PCEMTR, PTRSOD, PTRSODIR, PTRSODIF, ZPIZA_DST, ZCGA_DST, ZTAUREL_DST, PAERINDS, YDVARS%GEOMETRY%GELAM%T0, &
  & YDVARS%GEOMETRY%GEMU%T0, YDCPG_GPAR%SWDIR, YDCPG_GPAR%SWDIF, PRDG_MU0LU, YDMF_PHYS%OUT%ALB, YDMF_PHYS%RAD%RMOON   &
  &         )
ELSE
  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
    DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
      PTRSODIR(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JSG)=YDCPG_GPAR%SWDIR(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JSG)
      PTRSODIF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JSG)=YDCPG_GPAR%SWDIF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,JSG)
    ENDDO
  ENDIF
ENDIF

YDMF_PHYS%OUT%FRSOLU(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDMF_PHYS%RAD%RMOON(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)

    ! ---- Flux update and radiative heating rates
CALL RADHEAT ( YDMODEL%YRML_PHY_RAD%YRERAD, YDMODEL%YRML_PHY_RAD%YRERDI, YDMODEL%YRML_PHY_MF, YDCPG_DIM%KIDIA, &
& YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, PFLU_EMIS,          &
& YDMF_PHYS%RAD%EMTD, PRDG_MU0, PQV, PTENT, YDMF_PHYS%RAD%TRSW, PTRSOD, YDMF_PHYS_BASE_STATE%YGSP_RR%T,        &
& YDMODEL%YRML_PHY_MF%YRPHY2%TSPHY, PTRSODIR, PTRSODIF, PALBD, PALBP, YDMF_PHYS%OUT%FRSO, YDMF_PHYS%OUT%FRTH,  &
& YDMF_PHYS%OUT%FRSODS, YDMF_PHYS%OUT%FRTHDS, PCEMTR, PCTRSO, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRTHC,        &
& PSUDU, ZSDUR, ZDSRP, PSFSWDIR, PSFSWDIF, YDMF_PHYS%OUT%FRSOPS, ZFRSODS, YDMF_PHYS%OUT%FRSOPT   )


    ! ---- Take into account day duration depending on altitude.
IF(YDMODEL%YRML_PHY_MF%YRPHY%LDAYD) CALL ACDAYD(YDMODEL%YRML_GCONF%YRRIP, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA,               &
                                    & YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, YDCPG_DIM%KTDIA, YDMODEL%YRML_PHY_G%YRDPHY%NTSSG, &
                                    & YDVARS%GEOMETRY%GEMU%T0, PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,              &
                                    & YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, YDMF_PHYS%OUT%FRSO)

    ! ---- Correct solar absorption as a function of pmu0.
IF(YDMODEL%YRML_PHY_MF%YRPHY0%GRSO < 1._JPRB) CALL ACRSO(YDMODEL%YRML_PHY_MF%YRPHY0, YDCPG_DIM%KIDIA,                                 &
                                              & YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, YDCPG_DIM%KTDIA,                   &
                                              & YDMODEL%YRML_PHY_G%YRDPHY%NTSSG, YDVARS%GEOMETRY%GEMU%T0,                             &
                                              & PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, &
                                              & YDMF_PHYS%OUT%FRSO  )

IF(.NOT.YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
  DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
    ZALB(JLON)=0.0_JPRB
    DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
      ZALB(JLON)=ZALB(JLON)+0.5_JPRB*(PALBD(JLON,JSG)+PALBP(JLON,JSG))
    ENDDO
    ZALB(JLON)=ZALB(JLON)/REAL(YDMODEL%YRML_PHY_RAD%YRERAD%NSW, JPRB)
    YDMF_PHYS%OUT%FRSODS(JLON)=YDMF_PHYS%OUT%FRSO(JLON,YDCPG_DIM%KFLEVG,1)/(1.0_JPRB-ZALB(JLON))
  ENDDO
ENDIF
    ! Compute Sunshine Duration (in seconds)
!DEC$ IVDEP
DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
  IF(YDMF_PHYS%OUT%FRSODS(JLON) >= YDMODEL%YRML_PHY_RAD%YRERDI%RSUNDUR) THEN
    YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)=YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)+1.0_JPRB*YDMODEL%YRML_GCONF%YRRIP%TSTEP
  ENDIF
ENDDO

    ! Direct normal irradiance with securities
DO JLON = YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA
  YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)
  IF (PRDG_MU0(JLON) > 3.0E-02_JPRB) THEN
    YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)/PRDG_MU0(JLON)
  ENDIF
  YDMF_PHYS%OUT%FRSDNI(JLON)=MAX(0.0_JPRB,YDMF_PHYS%OUT%FRSDNI(JLON))
ENDDO
  
    ! global normal irradiance
DO JLON=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
  YDMF_PHYS%OUT%FRSGNI(JLON)=YDMF_PHYS%OUT%FRSDNI(JLON)+0.5_JPRB*(                         &
  & (1.0_JPRB+PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSOPS(JLON)       )+ &
  & (1.0_JPRB-PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSO  (JLON,YDCPG_DIM%KFLEVG,1)))
ENDDO


END SUBROUTINE
