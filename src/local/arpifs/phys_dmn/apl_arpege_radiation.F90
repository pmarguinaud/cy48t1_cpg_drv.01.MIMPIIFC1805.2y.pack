SUBROUTINE APL_ARPEGE_RADIATION (YDMF_PHYS_BASE_STATE, YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS,          &
& YDCPG_MISC, YDCPG_GPAR, YDMF_PHYS, YDMF_PHYS_SURF, YDVARS, YDMODEL, PAER, PAERINDS, PALBD, PALBP, &
& PCEMTR, PCTRSO, PFLU_EMIS, PFLU_QSAT, PQO3, PQR, PQS, PQV, PRDG_MU0, PRDG_MU0LU, PRDG_MU0M,       &
& PSFSWDIF, PSFSWDIR, PSUDU, PTENT, PTRSOD, PTRSODIF, PTRSODIR)

!**** *APL_ARPEGE_RADIATION*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE, CPG_DYN_TYPE, &
                              & CPG_SL2_TYPE, CPG_GPAR_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD &
                       , ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_GPAR_TYPE),            INTENT(INOUT) :: YDCPG_GPAR
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(IN)    :: PAER(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,6)
REAL(KIND=JPRB),                INTENT(IN)    :: PAERINDS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBD(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PALBP(YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCEMTR(YDCPG_OPTS%KLON,0:1)
REAL(KIND=JPRB),                INTENT(OUT)   :: PCTRSO(YDCPG_OPTS%KLON,0:1)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_EMIS (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PFLU_QSAT (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQO3(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0 (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0LU (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(IN)    :: PRDG_MU0M (YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PSFSWDIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(IN)    :: PSUDU(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(INOUT) :: PTENT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSOD(YDCPG_OPTS%KLON)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIF (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)
REAL(KIND=JPRB),                INTENT(OUT)   :: PTRSODIR (YDCPG_OPTS%KLON,YDCPG_OPTS%KSW)

#include "acdayd.intfb.h"
#include "acrso.intfb.h"
#include "radheat.intfb.h"
#include "recmwf.intfb.h"

REAL(KIND=JPRB)     :: ZCGA_DST (1,1,1)
REAL(KIND=JPRB)     :: ZPIZA_DST (1,1,1)
REAL(KIND=JPRB)     :: ZTAUREL_DST (1,1,1)
REAL(KIND=JPRB)     :: ZFRSODS(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFSDNN(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZFSDNV(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZSDUR(YDCPG_OPTS%KLON)
REAL(KIND=JPRB)     :: ZDSRP(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB)     :: ZALB(YDCPG_OPTS%KLON)
LOGICAL             :: LLCALLRAD
INTEGER (KIND=JPIM) :: JLON
INTEGER (KIND=JPIM) :: JSG

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_RADIATION', 0, ZHOOK_HANDLE)

LLCALLRAD=(MOD(YDCPG_OPTS%KSTEP,YDMODEL%YRML_PHY_RAD%YRERAD%NRADFR) == 0 )
IF (YDMODEL%YRML_PHY_MF%YRPHY%NCALLRAD==2) LLCALLRAD=(LLCALLRAD.AND.(YDCPG_OPTS%KSTEP<=YDMODEL%YRML_GCONF%YRRIP%NSTOP-1))

! Intermittent call to radiation scheme

IF (LLCALLRAD) THEN

!=PARALLEL

  CALL RECMWF(YDGEOMETRY%YRDIMV, YDMODEL, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG,     &
  & YDCPG_OPTS%KSW, PALBD, PALBP, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,        &
  & YDCPG_MISC%NEB, PQO3, PAER, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, PFLU_EMIS, PRDG_MU0M, PQV,                    &
  & PFLU_QSAT, YDCPG_MISC%QICE, YDCPG_MISC%QLI, PQS, PQR, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_BASE_STATE%T,         &
  & YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDMF_PHYS%RAD%EMTD, YDMF_PHYS%RAD%EMTU, YDMF_PHYS%RAD%TRSW, YDMF_PHYS%OUT%FRTHC,  &
  & YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRSO, PSFSWDIR, PSFSWDIF, ZFSDNN, ZFSDNV,                  &
  & PCTRSO, PCEMTR, PTRSOD, PTRSODIR, PTRSODIF, ZPIZA_DST, ZCGA_DST, ZTAUREL_DST, PAERINDS, YDVARS%GEOMETRY%GELAM%T0, &
  & YDVARS%GEOMETRY%GEMU%T0, YDCPG_GPAR%SWDIR, YDCPG_GPAR%SWDIF, PRDG_MU0LU, YDMF_PHYS%OUT%ALB, YDMF_PHYS%RAD%RMOON)

!=END PARALLEL

ELSE

!=PARALLEL

  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN
    DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PTRSODIR(JLON,JSG)=YDCPG_GPAR%SWDIR(JLON,JSG)
        PTRSODIF(JLON,JSG)=YDCPG_GPAR%SWDIF(JLON,JSG)
      ENDDO
    ENDDO
  ENDIF

!=END PARALLEL

ENDIF

!=PARALLEL

YDMF_PHYS%OUT%FRSOLU(:)=YDMF_PHYS%RAD%RMOON(:)

! Flux update and radiative heating rates
CALL RADHEAT ( YDMODEL%YRML_PHY_RAD%YRERAD, YDMODEL%YRML_PHY_RAD%YRERDI, YDMODEL%YRML_PHY_MF, YDCPG_BNDS%KIDIA,    &
& YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,                      &
& PFLU_EMIS, YDMF_PHYS%RAD%EMTD, PRDG_MU0, PQV, PTENT, YDMF_PHYS%RAD%TRSW, PTRSOD, YDMF_PHYS_BASE_STATE%YGSP_RR%T, &
& YDMODEL%YRML_PHY_MF%YRPHY2%TSPHY, PTRSODIR, PTRSODIF, PALBD, PALBP, YDMF_PHYS%OUT%FRSO, YDMF_PHYS%OUT%FRTH,      &
& YDMF_PHYS%OUT%FRSODS, YDMF_PHYS%OUT%FRTHDS, PCEMTR, PCTRSO, YDMF_PHYS%OUT%FRSOC, YDMF_PHYS%OUT%FRTHC,            &
& PSUDU, ZSDUR, ZDSRP, PSFSWDIR, PSFSWDIF, YDMF_PHYS%OUT%FRSOPS, ZFRSODS, YDMF_PHYS%OUT%FRSOPT   )

!=END PARALLEL

! Take into account day duration depending on altitude.
IF(YDMODEL%YRML_PHY_MF%YRPHY%LDAYD) THEN

!=PARALLEL

  CALL ACDAYD(YDMODEL%YRCST, YDMODEL%YRML_GCONF%YRRIP, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, &
  & YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDCPG_OPTS%KTDIA, YDMODEL%YRML_PHY_G%YRDPHY%NTSSG, &
  & YDVARS%GEOMETRY%GEMU%T0, PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,                 &
  & YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, YDMF_PHYS%OUT%FRSO)

!=END PARALLEL

ENDIF

! Correct solar absorption as a function of pmu0.

IF(YDMODEL%YRML_PHY_MF%YRPHY0%GRSO < 1._JPRB) THEN

!=PARALLEL

  CALL ACRSO(YDMODEL%YRML_PHY_MF%YRPHY0, YDCPG_BNDS%KIDIA,                                &
  & YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG,                                 &
  & YDCPG_OPTS%KTDIA, YDMODEL%YRML_PHY_G%YRDPHY%NTSSG, YDVARS%GEOMETRY%GEMU%T0,           &
  & PRDG_MU0, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, &
  & YDMF_PHYS%OUT%FRSO)

!=END PARALLEL

ENDIF

IF(.NOT.YDMODEL%YRML_PHY_MF%YRARPHY%LMSE) THEN

!=PARALLEL

  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZALB(JLON)=0.0_JPRB
    DO JSG=1,YDMODEL%YRML_PHY_RAD%YRERAD%NSW
      ZALB(JLON)=ZALB(JLON)+0.5_JPRB*(PALBD(JLON,JSG)+PALBP(JLON,JSG))
    ENDDO
    ZALB(JLON)=ZALB(JLON)/REAL(YDMODEL%YRML_PHY_RAD%YRERAD%NSW, JPRB)
    YDMF_PHYS%OUT%FRSODS(JLON)=YDMF_PHYS%OUT%FRSO(JLON,YDCPG_OPTS%KFLEVG,1)/(1.0_JPRB-ZALB(JLON))
  ENDDO

!=END PARALLEL

ENDIF


! Compute Sunshine Duration (in seconds)

!=PARALLEL

!DEC$ IVDEP
DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
  IF(YDMF_PHYS%OUT%FRSODS(JLON) >= YDMODEL%YRML_PHY_RAD%YRERDI%RSUNDUR) THEN
    YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)=YDMF_PHYS_SURF%GSD_VD%PSUND(JLON)+1.0_JPRB*YDMODEL%YRML_GCONF%YRRIP%TSTEP
  ENDIF
ENDDO


! Direct normal irradiance with securities

DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
  YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)
  IF (PRDG_MU0(JLON) > 3.0E-02_JPRB) THEN
    YDMF_PHYS%OUT%FRSDNI(JLON)=YDMF_PHYS%OUT%FRSOPS(JLON)/PRDG_MU0(JLON)
  ENDIF
  YDMF_PHYS%OUT%FRSDNI(JLON)=MAX(0.0_JPRB,YDMF_PHYS%OUT%FRSDNI(JLON))
ENDDO
  
! Global normal irradiance

DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
  YDMF_PHYS%OUT%FRSGNI(JLON)=YDMF_PHYS%OUT%FRSDNI(JLON)+0.5_JPRB*(                     &
  & (1.0_JPRB+PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSOPS(JLON))+ &
  & (1.0_JPRB-PRDG_MU0(JLON))*(YDMF_PHYS%OUT%FRSODS(JLON)-YDMF_PHYS%OUT%FRSO  (JLON,YDCPG_OPTS%KFLEVG,1)))
ENDDO

!=END PARALLEL

IF (LHOOK) CALL DR_HOOK ('APL_ARPEGE_RADIATION', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_ARPEGE_RADIATION
