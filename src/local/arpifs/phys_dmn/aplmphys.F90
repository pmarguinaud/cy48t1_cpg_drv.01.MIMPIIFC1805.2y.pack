!OPTIONS XOPT(NOEVAL)
SUBROUTINE APLMPHYS ( YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 !-----------------------------------------------------------------------
 ! - INPUT 2D .
 & PAPRS,PAPRSF,PCP,PQMP,PQIMP,PQLMP,PQNMP,PQRMP,PQGMP,PR,PTMP,&
 & PIPOI,PDQ,PDQM,PLHS,PLHV,PNEBM,PPOID,PRCVOTT,PTCORR,&
 ! - INPUT 1D .
 & PLSM,PNEIJ,PTS,PDECRD,&
 ! - INPUT/OUTPUT 2D .
 & PFCSQL,PFCSQN,&
 ! - OUTPUT 2D .
 & PFALLR,PFALLS,PFALLG,&
 & PFASL,PFASN,PFASG,PFESL,PFESN,PFESG,PFPLSL,PFPLSN,PFPLSG,PSEDIQL,PSEDIQI,&
 & PMELNET,PMELGET,&
 ! - OUTPUT 1D .
 & PGRVINT)

!**** *APLMPHYS * - CALCUL DES FLUX DE PRECIPITATION.

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX DE PRECIPITATION
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF PRECIPITATION FLUXES
!              (WATER AND SNOW) .

!     - CALCUL DES PSEUDO-FLUX LIES AUX PRECIPITATIONS
!              (LIQUIDE ET NEIGE) .
!     - COMPUTATION OF PSEUDO-FLUXES LINKED TO PRECIPITATIONS
!              (WATER AND SNOW) .

!**   Interface.
!     ----------
!        *CALL* *APLMPHYS*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.

! - 2D (1:KLEV) .

! PAPRSF     : PRESSION AUX NIVEAUX PLEIN.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE.
! PQMP       : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQIMP      : RATIO OF SUSPENDED ICE
! PQLMP      : RATIO OF SUSPENDED LIQUID WATER
! PQNMP      : RATIO OF SNOW
! PQRMP      : RATIO OF RAIN WATER
! PQGMP      : RATIO OF GRAUPEL
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PTMP       : TEMPERATURE.
! PIPOI      : FACTEUR DE CONVERSION DE D_P FLUX VERS D_T Q_X.
! PDQ        : DEFICIT DE SATURATION (EN TERME DE 'QW-Q').
! PDQM       : DEFICIT MAXIMUM DE SATURATION.
! PLHS       : CHALEUR LATENTE DE SUBLIMATION.
! PLHV       : CHALEUR LATENTE DE VAPORISATION.
! PNEBM      : NEBULOSITE POUR LA MICROPHYSIQUE.
! PPOID      : FACTEUR DE CONVERSION DE D_T Q_X VERS D_P FLUX.
! PRCVOTT    : DEGRE D'INHOMOGENEITE SUPPOSE DES PRECIPITATIONS.
! PTCORR     : CORRECTION DE TEMPERATURE POUR LE NUAGE CONVECTIF.

! - 1D .

! PLSM       : LAND-SEA MASK
! PNEIJ      : SNOW FRACTION
! PTS        : SURFACE TEMPERATURE
! PDECRD     : DECORRELATION DEPTH FOR CLOUD OVERLAPS [Pa]

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTR?E-SORTIE.
!     --------------------------

! - 2D (0:KLEV) .

! PROGNOSTIC PSEUDO FLUXES (WITH RESPECT TO VAPOUR)

! PFCSQL     : STRATIFORM CONDENSATION (LIQUID)
! PFCSQN     : STRATIFORM CONDENSATION (SOLID)

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PFPLSL     : PRECIPITATIONS STRATIFORMES SOUS FORME LIQUIDE.
! PFPLSN     : PRECIPITATIONS STRATIFORMES SOUS FORME NEIGE.
! PFPLSG     : PRECIPITATIONS STRATIFORMES SOUS FORME GRAUPEL.
! PSEDIQL    : FLUX DE SEDIMENTATION D'EAU LIQUIDE.
! PSEDIQI    : FLUX DE SEDIMENTATION D'EAU SOLIDE.

! PROGNOSTIC PSEUDO FLUXES (WITH RESPECT TO VAPOUR)

! PFASL      : STRATIFORM AUTOCONVERSION (LIQUID).
! PFASN      : STRATIFORM AUTOCONVERSION (SOLID).
! PFASG      : STRATIFORM AUTOCONVERSION (GRAUPEL). 
! PFESL      : STRATIFORM EVAPORATION OF RAIN.
! PFESN      : STRATIFORM EVAPORATION OF SNOW
! PFESG      : STRATIFORM EVAPORATION OF GRAUPEL. 

! - 2D (KLEV) .

! FALL VELOCITIES

! PFALLR     : FALL VELOCITY OF RAIN.
! PFALLS     : FALL VELOCITY OF SNOW.
! PFALLG     : FALL VELOCITY OF GRAUPEL.
! PMELNET    : NET MELTING RATE (SNOW).
! PMELGET    : NET MELTING RATE (GRAUPEL).

! - 1D .

! PGRVINT    : INTEGRALE VERTICALE DE LA QUANTITE DE GRAUPEL (KG/M**2).
!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/YOMPHY0/
! COMMON/YOMPHY2/

!-----------------------------------------------------------------------

!     Externes.
!     ---------
!        *ACACON*
!        *ACCOLL*
!        *ACEVMEL*

!     Methode.
!     --------

!     Auteur.
!     -------
!        06-08, B. Catry & J.-F. Geleyn.

!     Modifications.
!     --------------
!        06-12, Version with geometry of clouds and precipitation - J.-F. Geleyn
!        07-01, Addition of a diagnostic 'graupel' - R. Brozkova & J.-F. Geleyn
!        07-03, Correction of ZRCOLL0 (0.2577 => 4.085) - J.-F. Geleyn
!        07-05, Correction of a logical 'P1' bug - R. Brozkova & J.-F. Geleyn
!        07-06, No more fallspeed impact on EVA/FON - R. Brozkova & J.-F. Geleyn
!        07-07, Geometry's influence on Qr, Qs, delta_Qv and T - J.-F. Geleyn
!        07-07, Preparation for the option of the ARPEGE scheme - J.-F. Geleyn
!        07-08, Cleaning following the evolution of ACEVMEL - J.-F. Geleyn
!        07-12, Consequences of the introduction of APLMINI - R. Brozkova
!        08-10, Reduction of ZCFLIM by a factor 10 - R. Brozkova
!        08-10, Passing q vapour as argument to ACEVMEL - B. Catry
!        K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!        R. Brozkova (Nov 2010): fix for variable falling speed
!        R. Brozkova (Dec 2011): cloud water sedimentation and graupel amount
!        R. Brozkova (Jun 2012): graupel sedimentation
!        J. Van Den Bergh (Oct 2010): introduction of prognostic graupel
!        J. Masek (Apr 2016): Exponential-random cloud overlap with variable
!                             decorrelation depth.
!        P. Marguinaud (Oct 2016) : Port to single precision
!        L. Gerard (Sep 2019): New temperature dependency for autoconversion.
!        A. Trojakova (Oct 2020): Reproducibility fix for graupel.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB      ,JPRD
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RTT

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQIMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQNMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQRMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQGMP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTMP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIPOI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDQM(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLHS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLHV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEBM(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPOID(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCVOTT(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTCORR(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEIJ(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDECRD(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFALLR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFALLS(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFALLG(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFASL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFASN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFASG(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFESL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFESN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFESG(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSG(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSEDIQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSEDIQI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMELNET(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMELGET(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGRVINT(KLON)

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZFCSQL(KLON,0:KLEV),ZFCSQN(KLON,0:KLEV)&
 & ,ZQLMP(KLON,KLEV),ZQIMP(KLON,KLEV)&
 & ,ZIPB(KLON),ZIPH(KLON),ZEVGSL(KLON),ZDQ(KLON),ZTST(KLON),ZCOLRL(KLON)&
 & ,ZEXPA(KLON),ZEXPN(KLON),ZCOLRI(KLON),ZCOLNL(KLON),ZCOLNI(KLON),ZLSCP(KLON)&
 & ,ZACORL(KLON),ZACONI(KLON),ZACONL(KLON),ZEVAR(KLON),ZEVAN(KLON),ZFONTN(KLON)&
 & ,ZSTAL0(KLON),ZSTAL1(KLON),ZSTAL2(KLON),ZSTAL3(KLON),ZHPLSL(KLON)&
 & ,ZSTAN0(KLON),ZSTAN1(KLON),ZSTAN2(KLON),ZSTAN3(KLON),ZHPLSN(KLON)&
 & ,ZSTAQL1(KLON),ZSTAQL2(KLON),ZSTAQI1(KLON),ZSTAQI2(KLON)&
 & ,ZLPLSL(KLON),ZLPLSN(KLON),ZQRST(KLON),ZQNST(KLON),ZQLST(KLON),ZQIST(KLON)&
 & ,ZIPOI(KLON),ZPOID(KLON),ZPART(KLON),ZTEST(KLON),ZRCOLL(KLON),ZRCOLLP(KLON)&
 & ,ZEVGSLP(KLON),ZRMG(KLON),ZFSGRPL(KLON),ZQGRPL(KLON),ZQGRPN(KLON)&
 & ,ZQGRPG(KLON),ZZRHO(KLON),ZZPRSF(KLON),ZQVST(KLON)&
 & ,ZIPBR(KLON),ZIPHR(KLON),ZRPCEFF(KLON),ZRPCEFFP(KLON),ZZRHOPR(KLON)&
 & ,ZQGST(KLON),ZACOGI(KLON),ZACOGL(KLON),ZCOLGI(KLON),ZCOLGL(KLON)&
 & ,ZHPLSG(KLON),ZLPLSG(KLON),ZEVAG(KLON),ZFONTG(KLON)&
 & ,ZSVSL(KLON),ZSVSN(KLON),ZSVSG(KLON)

REAL(KIND=JPRB) :: ZIPLSLO(KLON),ZIPLSLE(KLON),ZIPLSNO(KLON),ZIPLSNE(KLON)&
 & ,ZPRPLO(KLON),ZPRPLE(KLON),ZNEBLOC(KLON),ZZACONI(KLON),ZZACORL(KLON)&
 & ,ZZACONL(KLON),ZZCOLNI(KLON),ZZCOLNL(KLON),ZZCOLRI(KLON),ZZCOLRL(KLON)&
 & ,Z1COLNI(KLON),Z1COLNL(KLON),Z1COLRI(KLON),Z1COLRL(KLON),Z2COLNI(KLON)&
 & ,Z2COLNL(KLON),Z2COLRI(KLON),Z2COLRL(KLON),ZZQLST(KLON),ZZQIST(KLON)&
 & ,ZZEVAN(KLON),ZZEVAR(KLON),ZZFONTN(KLON),Z1EVAN(KLON),Z1EVAR(KLON)&
 & ,Z1FONTN(KLON),Z2EVAN(KLON),Z2EVAR(KLON),Z2FONTN(KLON),ZOPLSLO(KLON)&
 & ,ZOPLSLE(KLON),ZOPLSNO(KLON),ZOPLSNE(KLON),ZZQRST(KLON),ZZQNST(KLON)&
 & ,ZIPLRLE(KLON),ZIPLRNE(KLON),ZOPLRLE(KLON),ZOPLRNE(KLON),Z3EVAN(KLON)&
 & ,Z3EVAR(KLON),Z3FONTN(KLON),ZSPRPLE(KLON),ZDQ3(KLON),ZDQ4(KLON),ZQRS1(KLON)&
 & ,ZQRS2(KLON),ZQRS3(KLON),ZQRS4(KLON),ZQNS1(KLON),ZQNS2(KLON),ZQNS3(KLON)&
 & ,ZQNS4(KLON),ZQRSC(KLON),ZQNSC(KLON),ZQVS3(KLON),ZQVS4(KLON),ZQVSC(KLON)&
 & ,ZSTAG0(KLON),ZSTAG1(KLON),ZSTAG2(KLON),ZSTAG3(KLON)&
 & ,ZOPLSGE(KLON),ZOPLSGO(KLON),ZOPLRGE(KLON)&
 & ,ZIPLSGO(KLON),ZIPLSGE(KLON),ZIPLRGE(KLON),ZQGS1(KLON),ZQGS2(KLON)&
 & ,ZQGS3(KLON),ZQGS4(KLON),ZQGSC(KLON)&
 & ,ZZACOGI(KLON),ZZACOGL(KLON),ZZQGST(KLON),Z1COLGI(KLON),Z1COLGL(KLON)& 
 & ,ZZCOLGI(KLON),ZZCOLGL(KLON),Z2COLGI(KLON),Z2COLGL(KLON)&
 & ,Z1EVAG(KLON),Z2EVAG(KLON),Z3EVAG(KLON),ZZEVAG(KLON)&
 & ,Z1FONTG(KLON),Z2FONTG(KLON),Z3FONTG(KLON),ZZFONTG(KLON)

INTEGER(KIND=JPIM) :: JLEV, JLON, ILEV

LOGICAL :: LLRNUMX, LLPSGRP, LLSIMP

REAL(KIND=JPRB) :: ZEPS1, ZCFLIM, ZEXTMP, ZEXFALL, ZEVGSL0, ZRCOLL0, ZRHOAIR, &
 & ZFALLL, ZFALLN, ZFPLSL, ZFPLSN, ZFPLSG, ZZSLS, ZZSNS, ZMUL, ZZNEBLOC, ZIMUL, ZEPS2, &
 & ZZPRPLE, ZZTEST, ZIPLSL, ZIPLSN, ZIPLSG, ZWEIGHT, ZPREF, ZFALQL, ZFALQI, &
 & ZZSLQL, ZZSNQI, ZAQL, ZAQI, ZFALLG, ZZSGS, ZRPCEFF0
REAL (KIND=JPRB) :: ZEXFALLR, ZMULFAL, ZMULFALR, ZEXPRHOR
REAL (KIND=JPRB) :: ZCLOV, ZNEBLOCO, ZUMNLOC, ZBETABR1, ZBETABR2, ZNEBLI
REAL (KIND=JPRB) :: ZGRAPRO

REAL(KIND=JPRB), PARAMETER :: ZALPHA=-0.1572_JPRB, ZBETA=-4.9632_JPRB

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "acacon.intfb.h"
#include "accoll.intfb.h"
#include "acevmel.intfb.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('APLMPHYS',0,ZHOOK_HANDLE)
ASSOCIATE(TFVL=>YDML_PHY_MF%YRPHY0%TFVL, TFVI=>YDML_PHY_MF%YRPHY0%TFVI, FSPRAIN=>YDML_PHY_MF%YRPHY0%FSPRAIN, &
 & REVAPN=>YDML_PHY_MF%YRPHY0%REVAPN, TFVR=>YDML_PHY_MF%YRPHY0%TFVR, &
 & TFVS=>YDML_PHY_MF%YRPHY0%TFVS, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LSEDLAG=>YDML_PHY_MF%YRPHY%LSEDLAG, LSEDCL=>YDML_PHY_MF%YRPHY%LSEDCL, LAB12=>YDML_PHY_MF%YRPHY%LAB12, &
 & LFSFIX=>YDML_PHY_MF%YRPHY%LFSFIX, LFSVAR=>YDML_PHY_MF%YRPHY%LFSVAR, LSEDSTA=>YDML_PHY_MF%YRPHY%LSEDSTA, &
 & LGRAPRO=>YDML_PHY_MF%YRPHY%LGRAPRO)
!-----------------------------------------------------------------------

!*
!     ------------------------------------------------------------------
!     I - CONDITION A LA LIMITE SUPERIEURE, SUBSTITUTION P-Z  ET PARAMETRES.

!         UPPER BOUNDARY CONDITION P-Z EXCHANGE AND PARAMETERS.

! - TEMPORAIRE(S) 2D .

! ZFCSQL    : PSEUDO-FLUX TEMPORAIRE POUR CONDENSATION MOINS EVAPORATION.
!           : TEMPORARY PSEUDO-FLUX FOR CONDENSATION MINUS EVAPORATION.
! ZFCSQN    : PSEUDO-FLUX TEMPORAIRE POUR CONDENSATION SOLIDE MOINS SUBLIMATION.
!           : TEMPORARY PSEUDO-FLUX FOR SOLID CONDENSATION MINUS SUBLIMATION.

! - TEMPORAIRE(S) 1D .

! ZIPH      : INVERSE DE LA PRESSION AU SOMMET DE LA COUCHE.
!           : INVERSE PRESSURE AT THE TOP OF THE LAYER.
! ZIPHR     : ZIPH A LA PUISSANCE 5/4.
!           : ZIPH TO THE POWER 5/4.
! ZQGRPG    : VARIABLE AUXILIAIRE POUR LA QUANTITE DE GRAUPEL(FLUX/VITESSE).
!           : AUXILIARY VARIABLE FOR THE GRAUPEL QUANTITY (FLUX/SPEED).
! ZQGRPL    : VARIABLE AUXILIAIRE POUR LA QUANTITE DE PLUIE (FLUX/VITESSE).
!           : AUXILIARY VARIABLE FOR THE RAIN QUANTITY (FLUX/SPEED).
! ZQGRPN    : VARIABLE AUXILIAIRE POUR LA QUANTITE DE TYPE GLACE (FLUX/VITESSE).
!           : AUXILIARY VARIABLE FOR THE ICE-TYPE QUANTITY (FLUX/SPEED).
! ZRMG      : PROPORTION DU FLUX DE PRECIP. GLACEE TOMBANT EN GRAUPRL.
!           : GRAUPEL PROPORTION OF THE ICE PRECIPITATION FLUX.

ZEPS1=1.E-10_JPRB
IF (JPRD == JPRB) THEN
  ZEPS2=1.E-14_JPRB
ELSE
  ZEPS2=1.E-10_JPRB
ENDIF
ZCFLIM=0.04_JPRB
ZPREF=9000._JPRB

ZEXTMP=0.0231_JPRB
ZEVGSL0=3.959_JPRB
ZRCOLL0=4.085_JPRB
ZRPCEFF0=REVAPN
ZEXFALL=1._JPRB/6._JPRB
ZEXFALLR=ZEXFALL
ZMULFAL=1.0_JPRB
ZMULFALR=ZMULFAL
ZEXPRHOR=4.0_JPRB

IF (LAB12) THEN
  ZRPCEFF0=0.9694_JPRB*REVAPN
  ZEXFALLR=0.3_JPRB
  ZMULFALR=2.253_JPRB
  ZEXPRHOR=1.8_JPRB
ENDIF

LLRNUMX=.TRUE.

IF (LGRAPRO) THEN
  LLPSGRP=.FALSE.
  ZGRAPRO=1._JPRB
ELSE
  LLPSGRP=.TRUE.
  ZGRAPRO=0._JPRB
ENDIF

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    ZFCSQL(JLON,JLEV)=PFCSQL(JLON,JLEV)
    ZFCSQN(JLON,JLEV)=PFCSQN(JLON,JLEV)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  PSEDIQL(JLON,KTDIA-1)=0.0_JPRB
  PSEDIQI(JLON,KTDIA-1)=0.0_JPRB
  PFPLSL(JLON,KTDIA-1)=0.0_JPRB
  PFPLSN(JLON,KTDIA-1)=0.0_JPRB
  PFESL(JLON,KTDIA-1)=0.0_JPRB
  PFESN(JLON,KTDIA-1)=0.0_JPRB
  PFCSQL(JLON,KTDIA-1)=0.0_JPRB
  PFCSQN(JLON,KTDIA-1)=0.0_JPRB
  PFASL(JLON,KTDIA-1)=0.0_JPRB
  PFASN(JLON,KTDIA-1)=0.0_JPRB
  ZIPH(JLON)=1.0_JPRB/MAX(ZEPS1,PAPRS(JLON,KTDIA-1))
  ZIPHR(JLON)=ZIPH(JLON)

  IF (LAB12) THEN
    ZIPHR(JLON)=ZIPH(JLON)*SQRT(SQRT(ZIPH(JLON)))
  ENDIF

  ZEXPN(JLON)=MIN(1.0_JPRB,EXP(ZEXTMP*(PTMP(JLON,KTDIA)-RTT)))
  ZEVGSL(JLON)=(1.0_JPRB/ZEXPN(JLON))*ZEVGSL0
  ZRCOLL(JLON)=(1.0_JPRB/ZEXPN(JLON))*ZRCOLL0
  ZRPCEFF(JLON)=ZRPCEFF0
  ZRMG(JLON)=0.0_JPRB
  PGRVINT(JLON)=0.0_JPRB
  ZQGRPL(JLON)=0.0_JPRB
  ZQGRPN(JLON)=0.0_JPRB
  ZQGRPG(JLON)=0.0_JPRB
  ZSVSL(JLON)=0.0_JPRB
  ZSVSN(JLON)=0.0_JPRB
  PFPLSG(JLON,KTDIA-1)=0.0_JPRB
  PFESG(JLON,KTDIA-1)=0.0_JPRB
  PFASG(JLON,KTDIA-1)=0.0_JPRB
  ZQGRPG(JLON)=0.0_JPRB
  ZSVSG(JLON)=0.0_JPRB
ENDDO

DO JLON=KIDIA,KFDIA
  ZNEBLOC(JLON)=PNEBM(JLON,KTDIA)
  ZPRPLO(JLON)=0.0_JPRB
  ZPRPLE(JLON)=0.0_JPRB
  ZIPLSLO(JLON)=0.0_JPRB
  ZIPLSLE(JLON)=0.0_JPRB
  ZIPLSNO(JLON)=0.0_JPRB
  ZIPLSNE(JLON)=0.0_JPRB
  ZIPLRLE(JLON)=0.0_JPRB
  ZIPLRNE(JLON)=0.0_JPRB
  ZIPLSGO(JLON)=0.0_JPRB
  ZIPLSGE(JLON)=0.0_JPRB
  ZIPLRGE(JLON)=0.0_JPRB
  ZFONTG(JLON)=0.0_JPRB
  ZEVAG(JLON)=0.0_JPRB
  ZCOLGI(JLON)=0.0_JPRB
  ZCOLGL(JLON)=0.0_JPRB
  ZACOGI(JLON)=0.0_JPRB
  ZACOGL(JLON)=0.0_JPRB
  ZQGST(JLON)=0.0_JPRB
  ZQGSC(JLON)=0.0_JPRB
  ZHPLSG(JLON)=0.0_JPRB
  ZLPLSG(JLON)=0.0_JPRB
ENDDO

!*
!     ------------------------------------------------------------------
!     II - CALCULS PROPREMENTS DITS DANS UNE BOUCLE VERTICALE OU
!     L'INFORMATION SE TRANSMET DE COUCHE A COUCHE.

!          EFFECTIVE CALCULATIONS IN A VERTICAL LOOP WHERE THE
!     INFORMATION IS PASSED FROM LAYER TO LAYER.
!          START AT TOP LAYER.

DO JLEV=KTDIA,KLEV

  ILEV=MAX(KTDIA,JLEV-1)

!     CALCULS PRELIMINAIRES ET PREPARATION DE LA DEPENDANCE EN TEMPERATURE
!     POUR LES CONSTANTES NEIGEUSES (LES DEPENDANCES, CHOISIES D'APRES
!     LOPEZ (2002), SONT REGROUPEES).

!     PRELIMINARY COMPUTATIONS AND PREPARATIOB OF THE TEMPERATURE DEPENDENCY
!     FOR THE SNOW-RELATED CONSTANTS (ALL DEPENDENCIES, TAKEN FROM
!     LOPEZ (2002), ARE MERGED).

! - TEMPORAIRE(S) 1D.

! ZQIST     : VALEUR COURRANTE DE QI.
!           : RUNNING VALUE OF QI.
! ZQLST     : VALEUR COURRANTE DE QL.
!           : RUNNING VALUE OF QL.
! ZTST      : VALEUR COURRANTE DE T.
!           : RUNNING VALUE OF T.
! ZDQ       : COPIE LOCALE DE PDQ.
!           : LOCAL COPY OF PDQ.
! ZIPOI     : COPIE LOCALE DE PIPOI.
!           : LOCAL COPY OF PIPOI.
! ZPOID     : COPIE LOCALE DE PPOID.
!           : LOCAL COPY OF PPOID.
! ZZPRSF    : COPIE LOCALE DE PAPRSF.
!           : LOCAL COPY OF PAPRSF.

! ZEXPN     : FACTEUR COMMUN DE DEPENDANCE EN TEMPERATURE (CROISSANT AVEC T).
!           : COMMON TEMPERATURE DEPENDENCY FACTOR (INCREASING WITH T).
! ZEVGSLP   : RAPPORT PUR DES VITESSES DE CHUTE PLUIE/NEIGE (DEPENDANT DE T).
!           : PURE RATIO OF FALL VELOCITIES FOR RAIN VS. SNOW (T-DEPENDENT).
! ZEVGSL    : MEME RAPPORT INCLUANT L'EFFET GRAUPEL.
!           : SAME RATIO BUT INCLUDING THE GRAUPEL EFFECT.
! ZIPB      : INVERSE DE LA PRESSION A LA BASE DE LA COUCHE.
!           : INVERSE PRESSURE AT THE BOTTOM OF THE LAYER.
! ZIPBR     : ZIPB A LA PUISSANCE 5/4.
!           : ZIPB TO THE POWER 5/4.
! ZLSCP     : CHALEUR LATENTE DE FUSION DIVISEE PAR CP.
!           : MELTING LATENT HEAT DIVIDED BY CP.
! ZRCOLLP   : RAPPORT PUR DES EFFICACITES DE COLLECTION NEIGE/PLUIE (DEP. DE T).
!           : PURE RATIO OF COLLECTION EFFICIENCIES FOR SNOW VS. RAIN (T-DEP.).
! ZRCOLL    : MEME RAPPORT INCLUANT L'EFFET GRAUPEL.
!           : SAME RATIO BUT INCLUDING THE GRAUPEL EFFECT.
! ZRPCEFFP  : RAPPORT PUR DES EFFICACITES DE CHANGEMENT DE PHASE NEIGE/PLUIE.
!           : PURE RATIO OF PHASE CHANGE EFFICIENCIES FOR SNOW VS. RAIN.
! ZRPCEFF   : MEME RAPPORT INCLUANT L'EFFET GRAUPEL.
!           : SAME RATIO BUT INCLUDING THE GRAUPEL EFFECT.

! ZFSGRPL   : FLUX DE PRECIPITATION GLACE TOMBANT COMME DIAGNOSTIC GRAUPEL (PAS NEIGE).
!           : FLUX OF ICE PHASE PRECIPITATION FALLING AS DIAGNOSTIC GRAUPEL (NOT SNOW).

  DO JLON=KIDIA,KFDIA

!     COPIES LOCALES.
!     LOCAL COPIES.

    ZMUL=1.0_JPRB/MAX(ZEPS1,ZNEBLOC(JLON))
    ZQLST(JLON)=PQLMP(JLON,JLEV)*ZMUL
    ZQIST(JLON)=PQIMP(JLON,JLEV)*ZMUL
!     COMPUTE MASS FRACTIONS IN THE FOUR GEOMETRIC AREAS
!     1: SEEDED CLOUDY
!     2: NON-SEEDED CLOUDY
!     3: SEEDED CLEAR SKY
!     4: NON-SEEDED CLEAR SKY
!     C: CLOUD AVERAGED
!     R/N/G : RAIN/SNOW/GRAUPEL

    ZMUL=1.0_JPRB/MAX(ZEPS2,PFPLSL(JLON,JLEV-1))
    ZIMUL=MAX(0.0_JPRB,SIGN(1.0_JPRB,PFPLSL(JLON,JLEV-1)-ZEPS2))
    ZQRS1(JLON)=PQRMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSLO(JLON)-1.0_JPRB))
    ZQRS2(JLON)=PQRMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLRLE(JLON)-1.0_JPRB))
    ZQRSC(JLON)=ZPRPLO(JLON)*ZQRS1(JLON)+(1.0_JPRB-ZPRPLO(JLON))*ZQRS2(JLON)
    ZQRS4(JLON)=ZQRS2(JLON)
    ZQRS3(JLON)=PQRMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSLE(JLON)-1.0_JPRB))
    ZMUL=1.0_JPRB/MAX(ZEPS2,PFPLSN(JLON,JLEV-1))
    ZIMUL=MAX(0.0_JPRB,SIGN(1.0_JPRB,PFPLSN(JLON,JLEV-1)-ZEPS2))
    ZQNS1(JLON)=PQNMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSNO(JLON)-1.0_JPRB))
    ZQNS2(JLON)=PQNMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLRNE(JLON)-1.0_JPRB))
    ZQNSC(JLON)=ZPRPLO(JLON)*ZQNS1(JLON)+(1.0_JPRB-ZPRPLO(JLON))*ZQNS2(JLON)
    ZQNS4(JLON)=ZQNS2(JLON)
    ZQNS3(JLON)=PQNMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSNE(JLON)-1.0_JPRB))
    IF (LGRAPRO) THEN
      ZMUL=1.0_JPRB/MAX(ZEPS2,PFPLSG(JLON,JLEV-1))
      ZIMUL=MAX(0.0_JPRB,SIGN(1.0_JPRB,PFPLSG(JLON,JLEV-1)-ZEPS2))
      ZQGS1(JLON)=PQGMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSGO(JLON)-1.0_JPRB))
      ZQGS2(JLON)=PQGMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLRGE(JLON)-1.0_JPRB))
      ZQGSC(JLON)=ZPRPLO(JLON)*ZQGS1(JLON)+(1.0_JPRB-ZPRPLO(JLON))*ZQGS2(JLON)
      ZQGS4(JLON)=ZQGS2(JLON)
      ZQGS3(JLON)=PQGMP(JLON,JLEV)*(1.0_JPRB+ZIMUL*(ZMUL*ZIPLSGE(JLON)-1.0_JPRB))
    ENDIF
!    LOCAL COPY TEMPERATURE
    ZTST(JLON)=PTMP(JLON,JLEV)
!    SATURATION DEFICIT IN CLEAR SKY PARTS
    ZDQ(JLON)=MIN(PDQM(JLON,JLEV),PDQ(JLON,JLEV)/MAX(ZEPS1,1.0_JPRB&
     &-ZNEBLOC(JLON)))
    ZDQ4(JLON)=MIN(PDQM(JLON,JLEV),ZDQ(JLON)*(1.0_JPRB-(1.0_JPRB&
     &-PRCVOTT(JLON,JLEV-1))*ZPRPLE(JLON))/MAX(ZEPS1,1.0_JPRB-ZPRPLE(JLON)))
    ZDQ3(JLON)=(ZDQ(JLON)-(1.0_JPRB-ZPRPLE(JLON))*ZDQ4(JLON))&
     &/MAX(ZEPS1,ZPRPLE(JLON))
!    QW IN CLOUDY PART
    ZQVSC(JLON)=PQMP(JLON,JLEV)+PDQ(JLON,JLEV)
!    QV IN SEEDED AND NON-SEEDED CLEAR SKY PARTS
    ZQVS4(JLON)=ZQVSC(JLON)-ZDQ4(JLON)
    ZQVS3(JLON)=ZQVSC(JLON)-ZDQ3(JLON)
!    LOCAL COPY CONVERSION FACTOR, PRESSURE 
    ZIPOI(JLON)=PIPOI(JLON,JLEV)
    ZPOID(JLON)=PPOID(JLON,JLEV)
    ZZPRSF(JLON)=PAPRSF(JLON,JLEV)

!     DEPENDANCES EN TEMPERATURE (POUR LA NEIGE ET LA GLACE) & AUTRES CHAMPS 1D.
!     TEMPERATURE DEPENDENCIES (FOR SNOW AND ICE) AND OTHER 1D ARRAYS.

    ZEXPA(JLON)=(1.0_JPRB-TANH(ZALPHA*(PTMP(JLON,JLEV)-RTT)+ZBETA))*0.5_JPRB
    ZEXPN(JLON)=MIN(1.0_JPRB,EXP(ZEXTMP*(PTMP(JLON,JLEV)-RTT)))
    ZRCOLLP(JLON)=(1.0_JPRB/ZEXPN(JLON))*ZRCOLL0
    ZRPCEFFP(JLON)=ZRPCEFF0
    ZEVGSLP(JLON)=(1.0_JPRB/ZEXPN(JLON))*ZEVGSL0
    ZQLMP(JLON,JLEV)=MAX(0._JPRB,PQLMP(JLON,JLEV))
    ZQIMP(JLON,JLEV)=MAX(0._JPRB,PQIMP(JLON,JLEV))
    ZWEIGHT=1.0_JPRB-EXP((PAPRS(JLON,JLEV-1)-PAPRS(JLON,JLEV))/ZPREF)
    ZRCOLL(JLON)=ZRCOLL(JLON)+ZWEIGHT*(1.0_JPRB/(ZRMG(JLON)+(1.0_JPRB&
     &-ZRMG(JLON))/ZRCOLLP(JLON))-ZRCOLL(JLON))
    ZRPCEFF(JLON)=ZRPCEFF(JLON)+ZWEIGHT*(1.0_JPRB/(ZRMG(JLON)+(1.0_JPRB&
     &-ZRMG(JLON))/ZRPCEFFP(JLON))-ZRPCEFF(JLON))
    ZEVGSL(JLON)=ZEVGSL(JLON)+ZWEIGHT*(ZRMG(JLON)+(1.0_JPRB-ZRMG(JLON))&
     &*ZEVGSLP(JLON)-ZEVGSL(JLON))
    ZIPB(JLON)=1.0_JPRB/PAPRS(JLON,JLEV)
    ZIPBR(JLON)=ZIPB(JLON)

    IF (LAB12) THEN
      ZIPBR(JLON)=ZIPB(JLON)*SQRT(SQRT(ZIPB(JLON)))
    ENDIF

    ZLSCP(JLON)=(PLHS(JLON,JLEV)-PLHV(JLON,JLEV))/PCP(JLON,JLEV)

!     DEBUT DES CALCULS POUR LE DIAGNOSTIC GRAUPEL.
!     START OF THE DIAGNOSTIC GRAUPEL-LINKED COMPUTATION.

    ZFSGRPL(JLON)=ZRMG(JLON)*PFPLSN(JLON,JLEV-1)
  ENDDO

!     PREPARATION POUR LA SEDIMENTATION.

!     PREPARATION FOR SEDIMENTATION.

! - TEMPORAIRE(S) 1D.

! ZSTAL0    : PROBABILITE DE TRANSMISSION 'PLUIE' POUR LA SEDIMENTATION.
!           : 'RAIN' TRANSMISSION PROBABILITY FOR SEDIMENTATION.
! ZSTAL1    : PROBABILITE DE DEVERSEMENT 'PLUIE' POUR LA SEDIMENTATION.
!           : 'RAIN' ESCAPE PROBABILITY FOR SEDIMENTATION.
! ZSTAL2    : PROBABILITE CHUTE DE DESSUS 'PLUIE' POUR LA SEDIMENTATION.
!           : 'RAIN' FALL FROM LAYER ABOVE PROBABILITY FOR SEDIMENTATION.
! ZSTAL3    : PROBABILITE SOURCE 'PLUIE' POUR LA SEDIMENTATION.
!           : 'RAIN' SOURCE PROBABILITY FOR SEDIMENTATION.
! ZSTAN0    : PROBABILITE DE TRANSMISSION 'NEIGE' POUR LA SEDIMENTATION.
!           : 'SNOW' TRANSMISSION PROBABILITY FOR SEDIMENTATION.
! ZSTAN1    : PROBABILITE DE DEVERSEMENT 'NEIGE' POUR LA SEDIMENTATION.
!           : 'SNOW' ESCAPE PROBABILITY FOR SEDIMENTATION.
! ZSTAN2    : PROBABILITE CHUTE DE DESSUS 'NEIGE' POUR LA SEDIMENTATION.
!           : 'SNOW' ARRIVAL FROM LAYER ABOVE PROBABILITY FOR SEDIMENTATION.
! ZSTAN3    : PROBABILITE SOURCE 'NEIGE' POUR LA SEDIMENTATION.
!           : 'SNOW' SOURCE PROBABILITY FOR SEDIMENTATION.
! ZZRHO     : DENSITE LOCALE DE L'AIR.
!           : LOCAL AIR DENSITY.

  DO JLON=KIDIA,KFDIA

!     INVERSE DES NOMBRES DE COURANT POUR LA SEDIMENTATION ET PD-FONCTIONS.
!     INVERSE COURANT NUMBERS FOR SEDIMENTATION AND PD-FUNCTIONS.

    IF (LFSVAR) THEN
      ZRHOAIR=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PTMP(JLON,JLEV))
      ZZRHO(JLON)=ZRHOAIR

      IF (LAB12) THEN
        ZZRHOPR(JLON)=ZRHOAIR**ZEXPRHOR
      ELSE
        ZZRHOPR(JLON)=(ZRHOAIR*ZRHOAIR)*(ZRHOAIR*ZRHOAIR)
      ENDIF

      ZFPLSL=MAX(0.0_JPRB,ZSVSL(JLON)+0.5_JPRB*PPOID(JLON,JLEV)&
      &*(PQRMP(JLON,JLEV)+ZQLMP(JLON,JLEV)+PQRMP(JLON,ILEV)+ZQLMP(JLON,ILEV)))
      ZFPLSN=MAX(0.0_JPRB,ZSVSN(JLON)+0.5_JPRB*PPOID(JLON,JLEV)&
      &*(PQNMP(JLON,JLEV)+ZQIMP(JLON,JLEV)+PQNMP(JLON,ILEV)+ZQIMP(JLON,ILEV)))
      ZFALLL=FSPRAIN*(ZFPLSL/ZZRHOPR(JLON))**ZEXFALLR
      ZFALLL=ZFALLL*ZMULFALR

      ZFALLN=FSPRAIN*(ZFPLSN/((ZRHOAIR*ZRHOAIR)*(ZRHOAIR*ZRHOAIR)))**ZEXFALL&
       &/ZEVGSL(JLON)
      IF (LGRAPRO) THEN
        ZFPLSG=MAX(0.0_JPRB,ZSVSG(JLON)+0.5_JPRB*PPOID(JLON,JLEV)&
         &*(PQGMP(JLON,JLEV)+PQGMP(JLON,ILEV)))
        ZFALLG=FSPRAIN*(ZFPLSG/ZZRHOPR(JLON))**ZEXFALLR
        ZFALLG=ZFALLG*ZMULFALR
      ELSE
        ZFALLG=ZFALLN*ZEVGSL(JLON)
      ENDIF
    ENDIF

    IF (LFSFIX) THEN
      ZRHOAIR=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PTMP(JLON,JLEV))
      ZZRHO(JLON)=ZRHOAIR
      ZFALLL=TFVR
      ZFALLN=TFVS
      ZFALLG=TFVR
    ENDIF

    ZFALLL=MAX(ZFALLL,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    ZZSLS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/ZFALLL)
    ZFALLN=MAX(ZFALLN,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    ZZSNS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/ZFALLN)
    ZFALLG=MAX(ZFALLG,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    ZZSGS=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/ZFALLG)


    IF (LSEDSTA) THEN
      ZSTAL0(JLON)=EXP(-ZZSLS)
      ZSTAL1(JLON)=(1.0_JPRB-ZSTAL0(JLON))/ZZSLS
      ZSTAL2(JLON)=ZSTAL0(JLON)/(1._JPRB+ZZSLS+0.5_JPRB*(SQRT((1._JPRB+ZZSLS)&
       &**2+4._JPRB*ZZSLS)-(1._JPRB+ZZSLS)))
      ZSTAL3(JLON)=0.5_JPRB*(ZSTAL1(JLON)+ZSTAL2(JLON))
      IF (LFSVAR) THEN
        ZSTAL2(JLON)=(ZSTAL2(JLON)+ZSTAL3(JLON))/(1._JPRB+ZSTAL3(JLON))
      ENDIF
      ZSTAN0(JLON)=EXP(-ZZSNS)
      ZSTAN1(JLON)=(1.0_JPRB-ZSTAN0(JLON))/ZZSNS
      ZSTAN2(JLON)=ZSTAN0(JLON)/(1._JPRB+ZZSNS+0.5_JPRB*(SQRT((1._JPRB+ZZSNS)&
       &**2+4._JPRB*ZZSNS)-(1._JPRB+ZZSNS)))
      ZSTAN3(JLON)=0.5_JPRB*(ZSTAN1(JLON)+ZSTAN2(JLON))
      IF (LFSVAR) THEN
        ZSTAN2(JLON)=(ZSTAN2(JLON)+ZSTAN3(JLON))/(1._JPRB+ZSTAN3(JLON))
      ENDIF
      ZSTAG0(JLON)=EXP(-ZZSGS)
      ZSTAG1(JLON)=(1.0_JPRB-ZSTAG0(JLON))/ZZSGS
      ZSTAG2(JLON)=ZSTAG0(JLON)/(1._JPRB+ZZSGS+0.5_JPRB*(SQRT((1._JPRB+ZZSGS)&
       &**2+4._JPRB*ZZSGS)-(1._JPRB+ZZSGS)))
      ZSTAG3(JLON)=0.5_JPRB*(ZSTAG1(JLON)+ZSTAG2(JLON))
      IF (LFSVAR) THEN
        ZSTAG2(JLON)=(ZSTAG2(JLON)+ZSTAG3(JLON))/(1._JPRB+ZSTAG3(JLON))
      ENDIF
    ENDIF

    IF (LSEDLAG) THEN
      ZSTAL0(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSLS))
      ZSTAL1(JLON)=MIN(1.0_JPRB,1.0_JPRB/ZZSLS)
      ZSTAL2(JLON)=MAX(0.0_JPRB,1.0_JPRB-ZZSLS)
      ZSTAL3(JLON)=0.5_JPRB*(ZSTAL1(JLON)+ZSTAL2(JLON))
      IF (LFSVAR) THEN
        ZSTAL2(JLON)=(ZSTAL2(JLON)+ZSTAL3(JLON))/(1._JPRB+ZSTAL3(JLON))
      ENDIF
      ZSTAN0(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSNS))
      ZSTAN1(JLON)=MIN(1.0_JPRB,1.0_JPRB/ZZSNS)
      ZSTAN2(JLON)=MAX(0.0_JPRB,1.0_JPRB-ZZSNS)
      ZSTAN3(JLON)=0.5_JPRB*(ZSTAN1(JLON)+ZSTAN2(JLON))
      IF (LFSVAR) THEN
        ZSTAN2(JLON)=(ZSTAN2(JLON)+ZSTAN3(JLON))/(1._JPRB+ZSTAN3(JLON))
      ENDIF
      ZSTAG0(JLON)=MAX(0.0_JPRB,SIGN(1.0_JPRB,1.0_JPRB-ZZSGS))
      ZSTAG1(JLON)=MIN(1.0_JPRB,1.0_JPRB/ZZSGS)
      ZSTAG2(JLON)=MAX(0.0_JPRB,1.0_JPRB-ZZSGS)
      ZSTAG3(JLON)=0.5_JPRB*(ZSTAG1(JLON)+ZSTAG2(JLON))
      IF (LFSVAR) THEN
        ZSTAG2(JLON)=(ZSTAG2(JLON)+ZSTAG3(JLON))/(1._JPRB+ZSTAG3(JLON))
      ENDIF
    ENDIF

!   sedimentation of cloud water and ice.

    ZFALQL=MAX(TFVL,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    ZZSLQL=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/ZFALQL)
    ZFALQI=MAX(TFVI,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    ZZSNQI=MAX(ZEPS1,(PPOID(JLON,JLEV)/ZRHOAIR)/ZFALQI)

    ZSTAQL1(JLON)=MIN(1.0_JPRB,1.0_JPRB/ZZSLQL)
    ZSTAQL2(JLON)=MAX(0.0_JPRB,1.0_JPRB-ZZSLQL)
    ZSTAQI1(JLON)=MIN(1.0_JPRB,1.0_JPRB/ZZSNQI)
    ZSTAQI2(JLON)=MAX(0.0_JPRB,1.0_JPRB-ZZSNQI)

  ENDDO

!     AUTOCONVERSION (INCLUANT LE PROCESSUS DE WEGENER-BERGERON-FINDEISEN).
!     AUTOCONVERSION (INCLUDING THE WEGENER-BERGERON-FINDEISEN PROCESS).

! - TEMPORAIRE(S) 1D.

! ZPART     : COPIE LOCALE DE LA NEBULOSITE.
!           : LOCAL COPY OF CLOUDINESS.
! ZQNST     : VALEUR COURRANTE DE QN.
!           : RUNNING VALUE OF QN.
! ZQRST     : VALEUR COURRANTE DE QR.
!           : RUNNING VALUE OF QR.
! ZQGST     : VALEUR COURRANTE DE QG
!           : RUNNING VALUE OF QG
! ZACONI    : INCREMENT D'AUTOCONVERSION GLACE-NEIGE.
!           : ICE-SNOW AUTOCONVERSION INCREMENT.
! ZACORL    : INCREMENT D'AUTOCONVERSION EAU-PLUIE.
!           : WATER-RAIN AUTOCONVERSION INCREMENT.
! ZACONL    : INCREMENT D'AUTOCONVERSION EAU-NEIGE (PROCESSUS W-B-F).
!           : WATER-SNOW AUTOCONVERSION INCREMENT (W-B-F PROCESS).
! ZACOGI    : INCREMENT D'AUTOCONVERSION GLACE-GRAUPEL.
!           : ICE-GRAUPEL AUTOCONVERSION INCREMENT
! ZACOGL    : INCREMENT D'AUTOCONVERSION EAU-GRAUPEL.
!           : WATER-GRAUPEL AUTOCONVERSION INCREMENT (W-B-F PROCESS).
  DO JLON=KIDIA,KFDIA
    ZQRST(JLON)=ZQRSC(JLON)
    ZQNST(JLON)=ZQNSC(JLON)
    IF (LGRAPRO) THEN
      ZQGST(JLON)=ZQGSC(JLON)
    ENDIF  
    ZPART(JLON)=ZNEBLOC(JLON)
  ENDDO

  LLSIMP=.FALSE.

  CALL ACACON (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &           LLSIMP,&
    &           ZEXPA,ZEXPN,ZLSCP,ZPART,PLSM,PNEIJ,PTS,&
!   INPUT/OUTPUT
    &           ZQIST,ZQLST,ZQNST,ZQRST,ZQGST,ZTST,&
!   OUTPUT
    &           ZACONI,ZACONL,ZACORL,ZACOGI,ZACOGL)

!     SEDIMENTATION STATISTIQUE.
!     STATISTICAL SEDIMENTATION.

  DO JLON=KIDIA,KFDIA
    ZZACONI(JLON)=ZACONI(JLON)
    ZZACORL(JLON)=ZACORL(JLON)
    ZZACONL(JLON)=ZACONL(JLON)

    ZACONI(JLON)=ZZACONI(JLON)*ZPART(JLON)
    ZACORL(JLON)=ZZACORL(JLON)*ZPART(JLON)
    ZACONL(JLON)=ZZACONL(JLON)*ZPART(JLON)

    PFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV-1)*ZSTAL2(JLON)+PPOID(JLON,JLEV)&
     &*(ZSTAL1(JLON)*PQRMP(JLON,JLEV)+ZSTAL3(JLON)*ZACORL(JLON))
    PFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV-1)*ZSTAN2(JLON)+PPOID(JLON,JLEV)&
     &*(ZSTAN1(JLON)*PQNMP(JLON,JLEV)+ZSTAN3(JLON)*(ZACONL(JLON)+ZACONI(JLON)))
    ZFSGRPL(JLON)=ZFSGRPL(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*(ZSTAG1(JLON)&
     &*ZQGRPG(JLON)+ZSTAG3(JLON)*ZACONL(JLON))
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZZACOGI(JLON)=ZACOGI(JLON)
      ZZACOGL(JLON)=ZACOGL(JLON)

      ZACOGI(JLON)=ZZACOGI(JLON)*ZPART(JLON)
      ZACOGL(JLON)=ZZACOGL(JLON)*ZPART(JLON)

      PFPLSG(JLON,JLEV)=PFPLSG(JLON,JLEV-1)*ZSTAG2(JLON)+PPOID(JLON,JLEV)&
       &*(ZSTAG1(JLON)*PQGMP(JLON,JLEV)+ZSTAG3(JLON)*(ZACOGL(JLON)+ZACOGI(JLON)))
    ENDDO
  ENDIF

!     COLLECTION.
!     COLLECTION.

! - TEMPORAIRE(S) 1D.

! ZCOLNI    : INCREMENT DE COLLECTION GLACE-HEIGE.
!           : ICE-SNOW COLLECTION INCREMENT.
! ZCOLNL    : INCREMENT DE COLLECTION EAU-NEIGE.
!           : WATER-SNOW COLLECTION INCREMENT.
! ZCOLRI    : INCREMENT DE COLLECTION GLACE-PLUIE.
!           : ICE-RAIN COLLECTION INCREMENT.
! ZCOLRL    : INCREMENT DE COLLECTION EAU-PLUIE.
!           : WATER-RAIN COLLECTION INCREMENT.
! ZCOLGI    : INCREMENT DE COLLECTION GLACE-GRAUPEL.
!           : ICE-GRAUPEL COLLECTION INCREMENT.
! ZCOLGL    : INCREMENT DE COLLECTION EAU-GRAUPEL
!           : WATER-GRAUPEL COLLECTION INCREMENT
! ZHPLSL    : COPIE LOCALE PFPLSL AU SOMMET DE LA COUCHE.
!           : LOCAL COPY OF PFPLSL AT THE TOP OF THE LAYER.
! ZHPLSN    : COPIE LOCALE PFPLSN AU SOMMET DE LA COUCHE.
!           : LOCAL COPY OF PFPLSN AT THE TOP OF THE LAYER.
! ZHPLSG    : COPIE LOCALE OF PFPLSG AU SOMMET DE LA COUCHE
!           : LOCAL COPY OF PFPLSG AT THE TOP OF THE LAYER  
! ZLPLSL    : COPIE LOCALE PFPLSL A LA BASE DE LA COUCHE.
!           : LOCAL COPY OF PFPLSL AT THE BOTTOM OF THE LAYER.
! ZLPLSN    : COPIE LOCALE PFPLSN A LA BASE DE LA COUCHE.
!           : LOCAL COPY OF PFPLSN AT THE BOTTOM OF THE LAYER.
! ZLPLSG    : COPIE LOCALE PFPLSG A LA BASE DE LA COUCHE
!           : LOCAL COPY OF PFPLSG AT THE BOTTOM OF THE LAYER 

! FIRST PASS (SEEDED PART)

  DO JLON=KIDIA,KFDIA
    ZPART(JLON)=ZNEBLOC(JLON)*ZPRPLO(JLON)

    ZHPLSL(JLON)=ZIPLSLO(JLON)
    ZHPLSN(JLON)=ZIPLSNO(JLON)
    ZLPLSL(JLON)=ZHPLSL(JLON)*ZSTAL2(JLON)+PPOID(JLON,JLEV)*(ZSTAL1(JLON)&
     &*ZQRS1(JLON)+ZSTAL3(JLON)*ZZACORL(JLON))
    ZLPLSN(JLON)=ZHPLSN(JLON)*ZSTAN2(JLON)+PPOID(JLON,JLEV)*(ZSTAN1(JLON)&
     &*ZQNS1(JLON)+ZSTAN3(JLON)*(ZZACONL(JLON)+ZZACONI(JLON)))

    ZZQLST(JLON)=ZQLST(JLON)
    ZZQIST(JLON)=ZQIST(JLON)
    ZZQRST(JLON)=ZQRST(JLON)
    ZZQNST(JLON)=ZQNST(JLON)
    ZQRST(JLON)=ZQRS1(JLON)+ZZQRST(JLON)-ZQRSC(JLON)
    ZQNST(JLON)=ZQNS1(JLON)+ZZQNST(JLON)-ZQNSC(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZHPLSG(JLON)=ZIPLSGO(JLON)
      ZLPLSG(JLON)=ZHPLSG(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*(ZSTAG1(JLON)&
       &*ZQGS1(JLON)+ZSTAG3(JLON)*(ZZACOGL(JLON)+ZZACOGI(JLON)))
      ZZQGST(JLON)=ZQGST(JLON)
      ZQGST(JLON)=ZQGS1(JLON)+ZZQGST(JLON)-ZQGSC(JLON)
    ENDDO
  ENDIF

  CALL ACCOLL (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &           ZEXPN,ZHPLSL,ZHPLSN,ZHPLSG,ZLPLSL,ZLPLSN,ZLPLSG,ZLSCP,ZPART,ZRCOLL,ZZRHO,&
!   INPUT/OUTPUT
    &           ZQIST,ZQLST,ZQNST,ZQRST,ZQGST,ZTST,&
!   OUTPUT
    &           ZCOLNI,ZCOLNL,ZCOLRI,ZCOLRL,ZCOLGI,ZCOLGL)

!     SEDIMENTATION STATISTIQUE.
!     STATISTICAL SEDIMENTATION.

  DO JLON=KIDIA,KFDIA
    Z1COLNI(JLON)=ZCOLNI(JLON)
    Z1COLNL(JLON)=ZCOLNL(JLON)
    Z1COLRI(JLON)=ZCOLRI(JLON)
    Z1COLRL(JLON)=ZCOLRL(JLON)
    Z1COLGI(JLON)=ZCOLGI(JLON)
    Z1COLGL(JLON)=ZCOLGL(JLON)

    ZZCOLNI(JLON)=Z1COLNI(JLON)*ZPART(JLON)
    ZZCOLNL(JLON)=Z1COLNL(JLON)*ZPART(JLON)
    ZZCOLRI(JLON)=Z1COLRI(JLON)*ZPART(JLON)
    ZZCOLRL(JLON)=Z1COLRL(JLON)*ZPART(JLON)
    ZZCOLGI(JLON)=Z1COLGI(JLON)*ZPART(JLON)
    ZZCOLGL(JLON)=Z1COLGL(JLON)*ZPART(JLON)
  ENDDO

! SECOND PASS (NOT SEEDED PART)

  DO JLON=KIDIA,KFDIA
    ZPART(JLON)=ZNEBLOC(JLON)*(1.0_JPRB-ZPRPLO(JLON))

    ZHPLSL(JLON)=ZIPLRLE(JLON)
    ZHPLSN(JLON)=ZIPLRNE(JLON)
    ZLPLSL(JLON)=ZHPLSL(JLON)*ZSTAL2(JLON)+PPOID(JLON,JLEV)*(ZSTAL1(JLON)&
     &*ZQRS2(JLON)+ZSTAL3(JLON)*ZZACORL(JLON))
    ZLPLSN(JLON)=ZHPLSN(JLON)*ZSTAN2(JLON)+PPOID(JLON,JLEV)*(ZSTAN1(JLON)&
     &*ZQNS2(JLON)+ZSTAN3(JLON)*(ZZACONL(JLON)+ZZACONI(JLON)))

    ZQLST(JLON)=ZZQLST(JLON)
    ZQIST(JLON)=ZZQIST(JLON)
    ZQRST(JLON)=ZQRS2(JLON)+ZZQRST(JLON)-ZQRSC(JLON)
    ZQNST(JLON)=ZQNS2(JLON)+ZZQNST(JLON)-ZQNSC(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZHPLSG(JLON)=ZIPLRGE(JLON)
      ZLPLSG(JLON)=ZHPLSG(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*(ZSTAG1(JLON)&
       &*ZQGS2(JLON)+ZSTAG3(JLON)*(ZZACOGL(JLON)+ZZACOGI(KLON)))
      ZQGST(JLON)=ZQGS2(JLON)+ZZQGST(JLON)-ZQGSC(JLON)
    ENDDO
  ENDIF

  CALL ACCOLL (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &           ZEXPN,ZHPLSL,ZHPLSN,ZHPLSG,ZLPLSL,ZLPLSN,ZLPLSG,ZLSCP,ZPART,ZRCOLL,ZZRHO,&
!   INPUT/OUTPUT
    &           ZQIST,ZQLST,ZQNST,ZQRST,ZQGST,ZTST,&
!   OUTPUT
    &           ZCOLNI,ZCOLNL,ZCOLRI,ZCOLRL,ZCOLGI,ZCOLGL)

!     SEDIMENTATION STATISTIQUE.
!     STATISTICAL SEDIMENTATION.

  DO JLON=KIDIA,KFDIA
    Z2COLNI(JLON)=ZCOLNI(JLON)
    Z2COLNL(JLON)=ZCOLNL(JLON)
    Z2COLRI(JLON)=ZCOLRI(JLON)
    Z2COLRL(JLON)=ZCOLRL(JLON)

    ZCOLNI(JLON)=ZZCOLNI(JLON)+Z2COLNI(JLON)*ZPART(JLON)
    ZCOLNL(JLON)=ZZCOLNL(JLON)+Z2COLNL(JLON)*ZPART(JLON)
    ZCOLRI(JLON)=ZZCOLRI(JLON)+Z2COLRI(JLON)*ZPART(JLON)
    ZCOLRL(JLON)=ZZCOLRL(JLON)+Z2COLRL(JLON)*ZPART(JLON)

    ZZCOLNI(JLON)=Z1COLNI(JLON)*ZPRPLO(JLON)+Z2COLNI(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))
    ZZCOLNL(JLON)=Z1COLNL(JLON)*ZPRPLO(JLON)+Z2COLNL(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))
    ZZCOLRI(JLON)=Z1COLRI(JLON)*ZPRPLO(JLON)+Z2COLRI(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))
    ZZCOLRL(JLON)=Z1COLRL(JLON)*ZPRPLO(JLON)+Z2COLRL(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))

    ZZQRST(JLON)=ZZQRST(JLON)+ZZCOLRL(JLON)+ZZCOLRI(JLON)
    ZZQNST(JLON)=ZZQNST(JLON)+ZZCOLNL(JLON)+ZZCOLNI(JLON)

    PFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV)+PPOID(JLON,JLEV)*ZSTAL3(JLON)&
     &*(ZCOLRL(JLON)+ZCOLRI(JLON))
    PFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV)+PPOID(JLON,JLEV)*ZSTAN3(JLON)&
     &*(ZCOLNL(JLON)+ZCOLNI(JLON))
    ZFSGRPL(JLON)=ZFSGRPL(JLON)+PPOID(JLON,JLEV)*ZSTAG3(JLON)*(ZCOLNL(JLON)&
     &+ZCOLNI(JLON))*ZRMG(JLON)/(ZRMG(JLON)+(1.0_JPRB-ZRMG(JLON))*ZRCOLLP(JLON))
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      Z2COLGI(JLON)=ZCOLGI(JLON)   
      Z2COLGL(JLON)=ZCOLGL(JLON)

      ZCOLGI(JLON)=ZZCOLGI(JLON)+Z2COLGI(JLON)*ZPART(JLON)
      ZCOLGL(JLON)=ZZCOLGL(JLON)+Z2COLGL(JLON)*ZPART(JLON)

      ZZCOLGI(JLON)=Z1COLGI(JLON)*ZPRPLO(JLON)+Z2COLGI(JLON)*(1.0_JPRB&
       &-ZPRPLO(JLON))
      ZZCOLGL(JLON)=Z1COLGL(JLON)*ZPRPLO(JLON)+Z2COLGL(JLON)*(1.0_JPRB&
       &-ZPRPLO(JLON))

      ZZQGST(JLON)=ZZQGST(JLON)+ZZCOLGL(JLON)+ZZCOLGI(JLON)

      PFPLSG(JLON,JLEV)=PFPLSG(JLON,JLEV)+PPOID(JLON,JLEV)*ZSTAG3(JLON)&
       &*(ZCOLGL(JLON)+ZCOLGI(JLON))
    ENDDO
  ENDIF

!     CALCULS D'EVAPORATION PUIS DE FONTE DES FLUX DE PRECIPITATIONS.

!     EVAPORATION AND MELTING COMPUTATIONS FOR THE PRECIPITATION FLUXES.

! - TEMPORAIRE(S) 1D.

! ZEVAN     : INCREMENT D'EVAPORATION DE LA NEIGE.
!           : SNOW EVAPORATION INCREMENT.
! ZEVAR     : INCREMENT D'EVAPORATION DE LA PLUIE.
!           : RAIN EVAPORATION INCREMENT.
! ZEVAG     : INCREMENT D'EVAPORATION DU GRAUPEL.
!           : GRAUPEL EVAPORATION INCREMENT.
! ZFONTN    : INCREMENT DE FONTE DE LA NEIGE.
!           : SNOW MELTING INCREMENT.
! ZFONTG    : INCREMENT DE FONTE DU GRAUPE (-GEL DE LA PLUIE).
!           : GRAUPEL MELT (-RAIN FREEZING) INCREMENT.
! ZTEST     : TEST POUR LA PERPETUATION DU FLUX DE PRECIPITATIONS.
!           : TEST FOR THE PERPETUATION OF THE PRECIPITATION FLUX.

! FIRST PASS (SEEDED CLEAR SKY PART)

  DO JLON=KIDIA,KFDIA
    ZPART(JLON)=(1.0_JPRB-ZNEBLOC(JLON))*ZPRPLE(JLON)

    ZHPLSL(JLON)=ZIPLSLE(JLON)
    ZHPLSN(JLON)=ZIPLSNE(JLON)
    ZLPLSL(JLON)=ZHPLSL(JLON)*ZSTAL2(JLON)+PPOID(JLON,JLEV)*ZSTAL1(JLON)&
     &*ZQRS3(JLON)
    ZLPLSN(JLON)=ZHPLSN(JLON)*ZSTAN2(JLON)+PPOID(JLON,JLEV)*ZSTAN1(JLON)&
     &*ZQNS3(JLON)

    ZQRST(JLON)=ZQRS3(JLON)
    ZQNST(JLON)=ZQNS3(JLON)
    ZQVST(JLON)=ZQVS3(JLON)
    ZDQ(JLON)=ZDQ3(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZHPLSG(JLON)=ZIPLSGE(JLON)
      ZLPLSG(JLON)=ZHPLSG(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*ZSTAG1(JLON)&
       &*ZQGS3(JLON)
      ZQGST(JLON)=ZQGS3(JLON)
    ENDDO
  ENDIF

  LLSIMP=.FALSE.

  CALL ACEVMEL (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &            LLSIMP,&
    &            ZDQ,ZEXPN,ZHPLSL,ZHPLSN,ZHPLSG,ZIPB,ZIPBR,ZIPH,ZIPHR,ZIPOI,ZLPLSL,&
    &            ZLPLSN,ZLPLSG,ZPOID,ZQNST,ZQRST,ZQGST,ZQVST,ZSTAL3,ZSTAN3,ZSTAG3,ZTST,ZZPRSF,&
    &            ZLSCP,ZZRHO,ZRPCEFF,&
!   OUTPUT
    &            ZEVAN,ZEVAR,ZEVAG,ZFONTN,ZFONTG,ZTEST)

  DO JLON=KIDIA,KFDIA
    ZSPRPLE(JLON)=ZPRPLE(JLON)
    ZPRPLE(JLON)=ZPRPLE(JLON)*ZTEST(JLON)

    ZOPLSLE(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
     &*(ZEVAR(JLON)-ZFONTN(JLON)))
    ZOPLSNE(JLON)=MAX(0.0_JPRB,ZLPLSN(JLON)-ZPOID(JLON)*ZSTAN3(JLON)&
     &*(ZEVAN(JLON)+ZFONTN(JLON)))

    Z1EVAN(JLON)=ZEVAN(JLON)
    Z1EVAR(JLON)=ZEVAR(JLON)
    Z1FONTN(JLON)=ZFONTN(JLON)

    ZZEVAN(JLON)=Z1EVAN(JLON)*ZPART(JLON)
    ZZEVAR(JLON)=Z1EVAR(JLON)*ZPART(JLON)
    ZZFONTN(JLON)=Z1FONTN(JLON)*ZPART(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZOPLSLE(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
       &*(ZEVAR(JLON)-ZFONTN(JLON)-ZFONTG(JLON)))
      ZOPLSGE(JLON)=MAX(0.0_JPRB,ZLPLSG(JLON)-ZPOID(JLON)*ZSTAG3(JLON)&
       &*(ZEVAG(JLON)+ZFONTG(JLON)))

      Z1EVAG(JLON)=ZEVAG(JLON)
      Z1FONTG(JLON)=ZFONTG(JLON)

      ZZEVAG(JLON)=Z1EVAG(JLON)*ZPART(JLON)
      ZZFONTG(JLON)=Z1FONTG(JLON)*ZPART(JLON)
    ENDDO
  ENDIF

! SECOND PASS (NON-SEEDED CLEAR SKY PART)

  DO JLON=KIDIA,KFDIA
    ZPART(JLON)=(1.0_JPRB-ZNEBLOC(JLON))*(1.0_JPRB-ZSPRPLE(JLON))

    ZHPLSL(JLON)=ZIPLRLE(JLON)
    ZHPLSN(JLON)=ZIPLRNE(JLON)
    ZLPLSL(JLON)=ZHPLSL(JLON)*ZSTAL2(JLON)+PPOID(JLON,JLEV)*ZSTAL1(JLON)&
     &*ZQRS4(JLON)
    ZLPLSN(JLON)=ZHPLSN(JLON)*ZSTAN2(JLON)+PPOID(JLON,JLEV)*ZSTAN1(JLON)&
     &*ZQNS4(JLON)

    ZQRST(JLON)=ZQRS4(JLON)
    ZQNST(JLON)=ZQNS4(JLON)
    ZQVST(JLON)=ZQVS4(JLON)
    ZDQ(JLON)=ZDQ4(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZHPLSG(JLON)=ZIPLRGE(JLON)
      ZLPLSG(JLON)=ZHPLSG(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*ZSTAG1(JLON)&
       &*ZQGS4(JLON)
      ZQGST(JLON)=ZQGS4(JLON)
    ENDDO
  ENDIF

  LLSIMP=.FALSE.

  CALL ACEVMEL (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &            LLSIMP,&
    &            ZDQ,ZEXPN,ZHPLSL,ZHPLSN,ZHPLSG,ZIPB,ZIPBR,ZIPH,ZIPHR,ZIPOI,ZLPLSL,&
    &            ZLPLSN,ZLPLSG,ZPOID,ZQNST,ZQRST,ZQGST,ZQVST,ZSTAL3,ZSTAN3,ZSTAG3,ZTST,ZZPRSF,&
    &            ZLSCP,ZZRHO,ZRPCEFF,&
!   OUTPUT
    &            ZEVAN,ZEVAR,ZEVAG,ZFONTN,ZFONTG,ZTEST)

  DO JLON=KIDIA,KFDIA
    ZOPLRLE(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
     &*(ZEVAR(JLON)-ZFONTN(JLON)))
    ZOPLRNE(JLON)=MAX(0.0_JPRB,ZLPLSN(JLON)-ZPOID(JLON)*ZSTAN3(JLON)&
     &*(ZEVAN(JLON)+ZFONTN(JLON)))

    Z2EVAN(JLON)=ZEVAN(JLON)
    Z2EVAR(JLON)=ZEVAR(JLON)
    Z2FONTN(JLON)=ZFONTN(JLON)

    ZZEVAN(JLON)=ZZEVAN(JLON)+Z2EVAN(JLON)*ZPART(JLON)
    ZZEVAR(JLON)=ZZEVAR(JLON)+Z2EVAR(JLON)*ZPART(JLON)
    ZZFONTN(JLON)=ZZFONTN(JLON)+Z2FONTN(JLON)*ZPART(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZOPLRLE(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
       &*(ZEVAR(JLON)-ZFONTN(JLON)-ZFONTG(JLON)))
      ZOPLRGE(JLON)=MAX(0.0_JPRB,ZLPLSG(JLON)-ZPOID(JLON)*ZSTAG3(JLON)&
       &*(ZEVAG(JLON)+ZFONTG(JLON)))

      Z2EVAG(JLON)=ZEVAG(JLON)
      Z2FONTG(JLON)=ZFONTG(JLON)
      ZZEVAG(JLON)=ZZEVAG(JLON)+Z2EVAG(JLON)*ZPART(JLON)
      ZZFONTG(JLON)=ZZFONTG(JLON)+Z2FONTG(JLON)*ZPART(JLON)
    ENDDO
  ENDIF

! THIRD PASS (CLOUDY PART - FOR THE MELTING)

  DO JLON=KIDIA,KFDIA
    ZPART(JLON)=ZNEBLOC(JLON)

    ZHPLSL(JLON)=ZIPLSLO(JLON)*ZPRPLO(JLON)+ZIPLRLE(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))
    ZHPLSN(JLON)=ZIPLSNO(JLON)*ZPRPLO(JLON)+ZIPLRNE(JLON)*(1.0_JPRB&
     &-ZPRPLO(JLON))
    ZLPLSL(JLON)=ZHPLSL(JLON)*ZSTAL2(JLON)+PPOID(JLON,JLEV)*(ZSTAL1(JLON)&
     &*ZQRSC(JLON)+ZSTAL3(JLON)*(ZZACORL(JLON)+ZZCOLRL(JLON)&
     &+ZZCOLRI(JLON)))
    ZLPLSN(JLON)=ZHPLSN(JLON)*ZSTAN2(JLON)+PPOID(JLON,JLEV)*(ZSTAN1(JLON)&
     &*ZQNSC(JLON)+ZSTAN3(JLON)*(ZZACONL(JLON)+ZZACONI(JLON)+ZZCOLNL(JLON)&
     &+ZZCOLNI(JLON)))

    ZDQ(JLON)=0.0_JPRB
    ZQRST(JLON)=ZZQRST(JLON)
    ZQNST(JLON)=ZZQNST(JLON)
    ZQVST(JLON)=ZQVSC(JLON)
    ZTST(JLON)=ZTST(JLON)+PTCORR(JLON,JLEV)*PRCVOTT(JLON,JLEV-1)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZHPLSG(JLON)=ZIPLSGO(JLON)*ZPRPLO(JLON)+ZIPLRGE(JLON)*(1.0_JPRB&
       &-ZPRPLO(JLON))
      ZLPLSG(JLON)=ZHPLSG(JLON)*ZSTAG2(JLON)+PPOID(JLON,JLEV)*(ZSTAG1(JLON)&
       &*ZQGSC(JLON)+ZSTAG3(JLON)*(ZZACOGL(JLON)+ZZACOGI(KLON)+ZZCOLGL(JLON)&
       &+ZZCOLGI(JLON)))
      ZQGST(JLON)=ZZQGST(JLON)
    ENDDO
  ENDIF

  LLSIMP=.TRUE.

  CALL ACEVMEL (YDML_PHY_MF,KIDIA,KFDIA,KLON,&
!   INPUT
    &            LLSIMP,&
    &            ZDQ,ZEXPN,ZHPLSL,ZHPLSN,ZHPLSG,ZIPB,ZIPBR,ZIPH,ZIPHR,ZIPOI,ZLPLSL,&
    &            ZLPLSN,ZLPLSG,ZPOID,ZQNST,ZQRST,ZQGST,ZQVST,ZSTAL3,ZSTAN3,ZSTAG3,ZTST,ZZPRSF,&
    &            ZLSCP,ZZRHO,ZRPCEFF,&
!   OUTPUT
    &            ZEVAN,ZEVAR,ZEVAG,ZFONTN,ZFONTG,ZTEST)

  DO JLON=KIDIA,KFDIA
    ZOPLSLO(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
     &*(ZEVAR(JLON)-ZFONTN(JLON)))
    ZOPLSNO(JLON)=MAX(0.0_JPRB,ZLPLSN(JLON)-ZPOID(JLON)*ZSTAN3(JLON)&
     &*(ZEVAN(JLON)+ZFONTN(JLON)))

    Z3EVAN(JLON)=ZEVAN(JLON)
    Z3EVAR(JLON)=ZEVAR(JLON)
    Z3FONTN(JLON)=ZFONTN(JLON)

    ZEVAN(JLON)=ZZEVAN(JLON)+Z3EVAN(JLON)*ZPART(JLON)
    ZEVAR(JLON)=ZZEVAR(JLON)+Z3EVAR(JLON)*ZPART(JLON)
    ZFONTN(JLON)=ZZFONTN(JLON)+Z3FONTN(JLON)*ZPART(JLON)
  ENDDO

  IF (LGRAPRO) THEN
    DO JLON=KIDIA,KFDIA
      ZOPLSLO(JLON)=MAX(0.0_JPRB,ZLPLSL(JLON)-ZPOID(JLON)*ZSTAL3(JLON)&
       &*(ZEVAR(JLON)-ZFONTN(JLON)-ZFONTG(JLON)))
      ZOPLSGO(JLON)=MAX(0.0_JPRB,ZLPLSG(JLON)-ZPOID(JLON)*ZSTAG3(JLON)&
       &*(ZEVAG(JLON)+ZFONTG(JLON)))

      Z3EVAG(JLON)=ZEVAG(JLON)
      Z3FONTG(JLON)=ZFONTG(JLON)
      ZEVAG(JLON)=ZZEVAG(JLON)+Z3EVAG(JLON)*ZPART(JLON)
      ZFONTG(JLON)=ZZFONTG(JLON)+Z3FONTG(JLON)*ZPART(JLON)
    ENDDO
  ENDIF

!     VALEURS DES PSEUDO-FLUX, FIN DU CALCUL DE SEDIMENTATION, VALEURS DES
!     FLUX ET ROTATION A LA FIN DU CALCUL DE COUCHE.

!     VALUES OF PSEUDO-FLUXES, END OF THE SEDIMENTATION COMPUTATION, VALUES
!     OF THE FLUXES AND SWAP AT THE END OF THE LAYER'S COMPUTATIONS.

  DO JLON=KIDIA,KFDIA
    PMELNET(JLON,JLEV)=ZFONTN(JLON)+(ZCOLRI(JLON)-ZCOLNL(JLON)-ZACONL(JLON))
    PMELGET(JLON,JLEV)=ZFONTG(JLON)+(ZCOLGI(JLON)-ZCOLGL(JLON)-ZACOGL(JLON))

    PFCSQL(JLON,JLEV)=PFCSQL(JLON,JLEV-1)+ZFCSQL(JLON,JLEV)-ZFCSQL(JLON,JLEV-1)&
     &+PPOID(JLON,JLEV)*(ZCOLRI(JLON)-ZCOLNL(JLON)-ZACONL(JLON)-ZGRAPRO*(ZCOLGL(JLON)+ZACOGL(JLON)))

    PFCSQN(JLON,JLEV)=PFCSQN(JLON,JLEV-1)+ZFCSQN(JLON,JLEV)-ZFCSQN(JLON,JLEV-1)&
     &-PPOID(JLON,JLEV)*(ZCOLRI(JLON)-ZCOLNL(JLON)-ZACONL(JLON)-ZGRAPRO*(ZCOLGL(JLON)+ZACOGL(JLON)))

! POSSIBLE BUG IN ZAQL/ZAQI    
    PFASL(JLON,JLEV)=PFASL(JLON,JLEV-1)+PPOID(JLON,JLEV)*(ZACORL(JLON)&
     &+ZCOLRL(JLON)+ZCOLRI(JLON))
    ZAQL=MAX(0.0_JPRB,PQLMP(JLON,JLEV)-ZACORL(JLON)-ZCOLRL(JLON)-ZACONL(JLON)-ZCOLNL(JLON)-ZGRAPRO*(ZACOGL(JLON)+ZCOLGL(JLON)))
    PFASN(JLON,JLEV)=PFASN(JLON,JLEV-1)+PPOID(JLON,JLEV)*(ZACONI(JLON)&
     &+ZCOLNI(JLON)+ZACONL(JLON)+ZCOLNL(JLON))
    ZAQI=MAX(0.0_JPRB,PQIMP(JLON,JLEV)-ZACONI(JLON)-ZCOLNI(JLON)-ZCOLRI(JLON)-ZGRAPRO*(ZACOGI(JLON)+ZCOLGI(JLON)))
    PFASG(JLON,JLEV)=PFASG(JLON,JLEV-1)+PPOID(JLON,JLEV)*(ZACOGI(JLON)&
     &+ZCOLGI(JLON)+ZACOGL(JLON)+ZCOLGL(JLON))

    ZEVAR(JLON)=ZEVAR(JLON)-ZFONTN(JLON)-ZGRAPRO*ZFONTG(JLON)
    ZEVAN(JLON)=ZEVAN(JLON)+ZFONTN(JLON)
    ZEVAG(JLON)=ZEVAG(JLON)+ZFONTG(JLON)

    PFPLSL(JLON,JLEV)=MAX(0.0_JPRB,PFPLSL(JLON,JLEV)-ZEVAR(JLON)&
     &*PPOID(JLON,JLEV)*ZSTAL3(JLON))
    PFESL(JLON,JLEV)=PFESL(JLON,JLEV-1)+ZEVAR(JLON)*PPOID(JLON,JLEV)
    PFPLSN(JLON,JLEV)=MAX(0.0_JPRB,PFPLSN(JLON,JLEV)-ZEVAN(JLON)&
     &*PPOID(JLON,JLEV)*ZSTAN3(JLON))
    PFESN(JLON,JLEV)=PFESN(JLON,JLEV-1)+ZEVAN(JLON)*PPOID(JLON,JLEV)
    PFPLSG(JLON,JLEV)=ZGRAPRO*MAX(0.0_JPRB,PFPLSG(JLON,JLEV)-ZEVAG(JLON)&
     &*PPOID(JLON,JLEV)*ZSTAG3(JLON))
    PFESG(JLON,JLEV)=PFESG(JLON,JLEV-1)+ZEVAG(JLON)*PPOID(JLON,JLEV)

    IF (LLPSGRP) THEN
      ZFSGRPL(JLON)=MIN(PFPLSN(JLON,JLEV),MAX(0.0_JPRB,ZFSGRPL(JLON)&
       &-PPOID(JLON,JLEV)*ZSTAG3(JLON)*(ZRMG(JLON)*ZEVAN(JLON)+(1.0_JPRB&
       &-ZRMG(JLON))*MIN(0.0_JPRB,ZFONTN(JLON)))))
    ENDIF

    ZEVAR(JLON)=ZEVAR(JLON)+ZFONTN(JLON)+ZGRAPRO*ZFONTG(JLON)
    ZEVAN(JLON)=ZEVAN(JLON)-ZFONTN(JLON)
    ZEVAG(JLON)=ZEVAG(JLON)-ZFONTG(JLON)

    ZIPH(JLON)=ZIPB(JLON)
    ZIPHR(JLON)=ZIPBR(JLON)
    IF (LSEDCL) THEN
      PSEDIQL(JLON,JLEV)=ZSTAQL1(JLON)*PPOID(JLON,JLEV)*ZAQL&
       &+ZSTAQL2(JLON)*PSEDIQL(JLON,JLEV-1)
      PSEDIQI(JLON,JLEV)=ZSTAQI1(JLON)*PPOID(JLON,JLEV)*ZAQI&
       &+ZSTAQI2(JLON)*PSEDIQI(JLON,JLEV-1)
    ELSE
      PSEDIQL(JLON,JLEV)=0.0_JPRB
      PSEDIQI(JLON,JLEV)=0.0_JPRB
    ENDIF
    IF (LLPSGRP) THEN
      ZRMG(JLON)=ZFSGRPL(JLON)/MAX(ZEPS2,PFPLSN(JLON,JLEV))
    ELSE
      ZRMG(JLON)=0.0_JPRB
    ENDIF
  ENDDO

!     DIAGNOSTIC DES VITESSES DE CHUTE ET CALCULS AUXILIAIRES POUR LE GRAUPEL.
!     DIAGNOSTIC OF FALL-SPEEDS AND AUXILIARY COMPUTATIONS FOR THE GRAUPEL.

!DEC$ IVDEP
  DO JLON=KIDIA,KFDIA
    IF (LFSVAR) THEN
      ZRHOAIR=ZZRHO(JLON)
      ZFPLSL=MAX(0.0_JPRB,0.5_JPRB*(PFPLSL(JLON,JLEV-1)+PFPLSL(JLON,JLEV)))
      ZFALLL=FSPRAIN*(ZFPLSL/ZZRHOPR(JLON))**ZEXFALLR
      ZFALLL=ZFALLL*ZMULFALR
      ZFPLSN=MAX(0.0_JPRB,0.5_JPRB*(PFPLSN(JLON,JLEV-1)+PFPLSN(JLON,JLEV)))
      ZFALLN=FSPRAIN*(ZFPLSN/((ZRHOAIR*ZRHOAIR)*(ZRHOAIR*ZRHOAIR)))**ZEXFALL&
       &/ZEVGSL(JLON)
      IF (LGRAPRO) THEN
        ZFPLSG=MAX(0.0_JPRB,0.5_JPRB*(PFPLSG(JLON,JLEV-1)+PFPLSG(JLON,JLEV)))
        ZFALLG=FSPRAIN*(ZFPLSG/ZZRHOPR(JLON))**ZEXFALLR
        ZFALLG=ZFALLG*ZMULFALR
        ZSVSG(JLON)=ZFPLSG
      ELSE
        ZFALLG=ZFALLN*ZEVGSL(JLON)
      ENDIF
      ZSVSL(JLON)=ZFPLSL
      ZSVSN(JLON)=ZFPLSN
    ENDIF

    IF (LFSFIX) THEN
      ZRHOAIR=ZZRHO(JLON)
      ZFALLL=TFVR
      ZFALLN=TFVS
      ZFALLG=TFVR
    ENDIF

    PFALLR(JLON,JLEV)=MAX(ZFALLL,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    PFALLS(JLON,JLEV)=MAX(ZFALLN,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)
    PFALLG(JLON,JLEV)=MAX(ZFALLG,(PPOID(JLON,JLEV)/ZRHOAIR)*ZCFLIM)

    ZQGRPN(JLON)=(1.0_JPRB/(ZRHOAIR*PFALLS(JLON,JLEV)))*PFPLSN(JLON,JLEV)
    ZQGRPL(JLON)=(1.0_JPRB/(ZRHOAIR*PFALLR(JLON,JLEV)))*PFPLSL(JLON,JLEV)

    IF (LGRAPRO) THEN
      ZQGRPG(JLON)=(1.0_JPRB/(ZRHOAIR*PFALLG(JLON,JLEV)))*PFPLSG(JLON,JLEV)
    ELSE
      ZQGRPG(JLON)=MIN(ZQGRPN(JLON),(1.0_JPRB/(ZRHOAIR*PFALLG(JLON,JLEV)))&
       &*ZFSGRPL(JLON))
    ENDIF
    PGRVINT(JLON)=PGRVINT(JLON)+PPOID(JLON,JLEV)*TSPHY*ZQGRPG(JLON)
  ENDDO

!     RECOUVREMENT DES ZONES NUAGEUSES ET PLUVIEUSES.
!     OVERLAP OF CLOUDY AND RAINY AREAS: COMPUTE SEEDED PROPORTIONS OF 
!     CLOUDY AND CLEAR SKY PARTS, AND COMPUTE PARTIAL PRECIPITATION FLUXES
!     FOR THE FOUR GEOMETRIC AREAS.

!     USE NEW INTERPOLATION SCHEME.  
!     WITH ZCLOV=1, OLD RESULTS ARE REPRODUCED (LLRNUMX=TRUE)
!     "    ZCLOV=0, "                        " (LLRNUMX=FALSE)

  IF(JLEV /= KLEV) THEN
    DO JLON=KIDIA,KFDIA

      ! INTRODUCING THE DECORRELATION DEPTH

      ZCLOV=EXP(-(PAPRSF(JLON,JLEV+1)-PAPRSF(JLON,JLEV))/PDECRD(JLON))
      
      ZZPRPLE=ZPRPLE(JLON)
      ZZNEBLOC=ZNEBLOC(JLON)
      ZNEBLOC(JLON)=PNEBM(JLON,JLEV+1)
      ZNEBLOCO=ZCLOV*ZNEBLOC(JLON)
      ZUMNLOC=1.0_JPRB-ZNEBLOC(JLON)
      
      ! UPDATING OF SEEDED FRACTIONS
  
      ! ALPHA 
      ZMUL=1.0_JPRB/MAX(ZEPS1,(1.0_JPRB-ZNEBLOCO))
      ZPRPLE(JLON)=((ZNEBLOCO+ZZNEBLOC-MIN(ZNEBLOCO,ZZNEBLOC))&
       &*(1.0_JPRB-ZZPRPLE)-ZNEBLOCO+ZZPRPLE)*ZMUL
      ZPRPLE(JLON)=MAX(0.0_JPRB,MIN(1.0_JPRB,ZPRPLE(JLON)))
      ! BETA
      ZBETABR1=(1.0_JPRB-ZCLOV)*(ZZNEBLOC+ZZPRPLE*(ZNEBLOC(JLON)&
       &-ZZNEBLOC))
      ZBETABR2=MIN(ZNEBLOCO,ZZNEBLOC)*(1.0_JPRB-ZZPRPLE)+ZZPRPLE&
       &*ZNEBLOC(JLON)
      ZNEBLI=1.0_JPRB/MAX(ZEPS1,ZNEBLOC(JLON))
      ZPRPLO(JLON)=ZBETABR1*ZMUL+ZBETABR2*(ZNEBLI-(1.0_JPRB-ZCLOV)*ZMUL)
      ZPRPLO(JLON)=MAX(0.0_JPRB,MIN(1.0_JPRB,ZPRPLO(JLON)))

      ! NON-SEEDED CLEAR SKY PARTIAL FLUXES
      
      ZIPLRLE(JLON)=ZOPLRLE(JLON)
      ZIPLRNE(JLON)=ZOPLRNE(JLON)

      ! UPDATING OF PARTIAL FLUXES IN SEEDED AREAS: 

      ZIPLSL=MAX(0.0_JPRB,PFPLSL(JLON,JLEV)-ZOPLRLE(JLON)*(1.0_JPRB&
       &-ZZPRPLE)*(1.0_JPRB-ZZNEBLOC))
      ZIPLSN=MAX(0.0_JPRB,PFPLSN(JLON,JLEV)-ZOPLRNE(JLON)*(1.0_JPRB&
       &-ZZPRPLE)*(1.0_JPRB-ZZNEBLOC))
      
      ZMUL=1.0_JPRB/MAX(ZEPS1,ZNEBLOC(JLON)*ZPRPLO(JLON))
      ZIPLSLO(JLON)=MAX(0.0_JPRB,(ZIPLSL-ZOPLSLE(JLON)*ZPRPLE(JLON)&
       &*ZUMNLOC))*ZMUL
      ZIPLSNO(JLON)=MAX(0.0_JPRB,(ZIPLSN-ZOPLSNE(JLON)*ZPRPLE(JLON)&
       &*ZUMNLOC))*ZMUL
      
      ! TWO CASES, DISTINGUISHED BY TEST ON SIGN(ZCLOV*ZNEBLOC-ZZNEBLOC)
      ZZTEST=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZNEBLOCO-ZZNEBLOC))
      
      ZMUL=1.0_JPRB/MAX(ZEPS1,ZBETABR1+ZUMNLOC*ZBETABR2*ZNEBLI)
      ZIPLSLO(JLON)=ZZTEST*ZIPLSLO(JLON)+(1.0_JPRB-ZZTEST)*ZMUL&
       &*((ZCLOV*ZUMNLOC+(1.0_JPRB-ZCLOV)*ZZNEBLOC)&
       &*ZOPLSLO(JLON)&
       &+(ZZPRPLE*(1.0_JPRB-ZCLOV)*(1.0_JPRB-ZZNEBLOC))&
       &*ZOPLSLE(JLON))
      ZIPLSNO(JLON)=ZZTEST*ZIPLSNO(JLON)+(1.0_JPRB-ZZTEST)*ZMUL&
       &*((ZCLOV*ZUMNLOC+(1.0_JPRB-ZCLOV)*ZZNEBLOC)&
       &*ZOPLSNO(JLON)&
       &+(ZZPRPLE*(1.0_JPRB-ZCLOV)*(1.0_JPRB-ZZNEBLOC))&
       &*ZOPLSNE(JLON))
      
      ZMUL=1.0_JPRB/MAX(ZEPS1,ZUMNLOC*ZPRPLE(JLON))
      ZIPLSLE(JLON)=MAX(0.0_JPRB,(ZIPLSL-ZIPLSLO(JLON)*ZPRPLO(JLON)&
       &*ZNEBLOC(JLON)))*ZMUL
      ZIPLSNE(JLON)=MAX(0.0_JPRB,(ZIPLSN-ZIPLSNO(JLON)*ZPRPLO(JLON)&
       &*ZNEBLOC(JLON)))*ZMUL

      ZIPLSLE(JLON)=ZZTEST*ZOPLSLE(JLON)+(1.0_JPRB-ZZTEST)*ZIPLSLE(JLON)
      ZIPLSNE(JLON)=ZZTEST*ZOPLSNE(JLON)+(1.0_JPRB-ZZTEST)*ZIPLSNE(JLON)
!GRAUPEL
!        ZIPLSGE(JLON)=ZZTEST*ZOPLSGE(JLON)+(1.0_JPRB-ZZTEST)*ZIPLSGE(JLON)
!
      IF (LGRAPRO) THEN
        ZIPLRGE(JLON)=ZOPLRGE(JLON)

        ZIPLSG=MAX(0.0_JPRB,PFPLSG(JLON,JLEV)-ZOPLRGE(JLON)*(1.0_JPRB&
         &-ZZPRPLE)*(1.0_JPRB-ZZNEBLOC))

        ZMUL=1.0_JPRB/MAX(ZEPS1,ZNEBLOC(JLON)*ZPRPLO(JLON))
        ZIPLSGO(JLON)=MAX(0.0_JPRB,(ZIPLSG-ZOPLSGE(JLON)*ZPRPLE(JLON)&
         &*ZUMNLOC))*ZMUL

        ZZTEST=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZNEBLOCO-ZZNEBLOC))

        ZMUL=1.0_JPRB/MAX(ZEPS1,ZBETABR1+ZUMNLOC*ZBETABR2*ZNEBLI)
        ZIPLSGO(JLON)=ZZTEST*ZIPLSGO(JLON)+(1.0_JPRB-ZZTEST)*ZMUL&
         &*((ZCLOV*ZUMNLOC+(1.0_JPRB-ZCLOV)*ZZNEBLOC)&
         &*ZOPLSGO(JLON)&
         &+(ZZPRPLE*(1.0_JPRB-ZCLOV)*(1.0_JPRB-ZZNEBLOC))&
         &*ZOPLSGE(JLON))

        ZMUL=1.0_JPRB/MAX(ZEPS1,ZUMNLOC*ZPRPLE(JLON))
        ZIPLSGE(JLON)=MAX(0.0_JPRB,(ZIPLSG-ZIPLSGO(JLON)*ZPRPLO(JLON)&
         &*ZNEBLOC(JLON)))*ZMUL

        ZIPLSGE(JLON)=ZZTEST*ZOPLSGE(JLON)+(1.0_JPRB-ZZTEST)*ZIPLSGE(JLON)
      ENDIF   
    ENDDO
  ENDIF

ENDDO
!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('APLMPHYS',1,ZHOOK_HANDLE)
END SUBROUTINE APLMPHYS

