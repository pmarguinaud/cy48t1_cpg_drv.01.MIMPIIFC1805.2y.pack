!OPTIONS XOPT(NOEVAL)
SUBROUTINE APLPARS(YDCPG_BNDS, YDCPG_OPTS, YDMF_PHYS_BASE_STATE, &
 & YDCPG_MISC, YDMF_PHYS_SURF, YDMF_PHYS, &
 & YDGEOMETRY, YDRCOEF, YDCST, YDML_PHY_MF, &
 & KTDIA , &
 & KSGST  , &
 !---------------------------------------------------------------------
 ! - INPUT .
 & PCVGQ  ,&
 & PQS1   ,&
 & PMU0   , PGM    ,&
 & PAC    , PCOR   ,&
 & PRAB3C , PRAB3N , PRAB4C , PRAB4N , PRAB6C , PRAB6N ,&
 & PRAT1C , PRAT1N , PRAT2C , PRAT2N , PRAT3C , PRAT3N ,&
 & PRAT4C , PRAT4N , PRAT5C , PRAT5N)

!**** *APLPARS - CALL THE SIMPLIFIED PHYSICAL PARAMETRIZATIONS.

!     Subject.
!     --------
!     - CALL THE SUBROUTINES OF THE SIMPLIFIED PHYSICAL PARAMETRIZATIONS.

!**   Interface.
!     ----------
!        *CALL* *APLPARS*

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------

! - DIMENSIONAL PARAMETERS.

! KIDIA, KFDIA : START/END OF HORIZONTAL LOOP  (IST,IEND IN *CPG*).
! KLON : HORIZONTAL DIMENSION                  (NPROMA IN *CPG*).
! KTDIA : START OF THE VERTICAL LOOP IN THE PHYSICS (IF SOME LEVELS ARE
!                      SKIPPED AT THE TOP OF THE MODEL).
! KLEV  : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG IN *CPG*)
! KSGST : NUMBER OF SUBGRID SURFACE TEMPERATURES AND FLUXES
!                      (NTSSG IN *CPG*)

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIAL ON HALF-LEVELS.
! PAPRS      : PRESSURE ON HALF-LEVELS.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIAL ON FULL LEVELS.
! PAPRSF     : PRESSURE ON FULL LEVELS.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.
! PRDELP     : INVERSE OF PDELP.
! PU         : X-COMPONENT OF WIND.
! PV         : Y-COMPONENT OF WIND.
! PT         : TEMPERATURE.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PCP        : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PCVGQ      : CONVERGENCE OF HUMIDITY.

! - 1D (PROGNOSTIC QUANTITIES) .

! PSNS       : MASS OF SNOW PER UNIT SURFACE.
! PTS        : SURFACE LAYER TEMPERATURE.
! PQS        : SURFACE SPECIFIC HUMIDITY.

! - 1D (GEOGRAPHICAL DISTRIBUTION) .

! PGETRL     : STANDARD DEVIATION OF OROGRAPHY  (UNIT: J/KG).
! PLSM       : LAND/SEA MASK.
! PVRLAN     : ANISOTROPY OF THE RELIEF (1 = ISOTROPY).
! PVRLDI     : ANGLE OF MAXIMAL GRADIENT OF RELIEF WITH x-AXIS.
! PGZ0F      : GRAVITY * ROUGHNESS LENGTH (TIME VARIABLE).
! PALBF      : BACKGROUND SURFACE SHORTWAVE ALBEDO (SNOW-FREE).

! - 1D (DIAGNOSTIC QUANTITIES).

! PMU0       : LOCAL COSINE OF INSTANTENOUS SOLAR ZENITH ANGLE.
! PNEBH      : CONVECTIVE CLOUDINESS.

! - CONSTANTES

! PAC       : CURTIS MATRIX FOR THERMAL RADIATION FOR CLEAR SKY
! PCOR      : CORRECTION FOR CLOUDINESS FOR THERMAL RADIATION

! PRAB3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.  ---
! PRAB4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAB6C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAB3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | from bottom
! PRAB4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               | to given
! PRAB6N    : CLOUDY DIFFUSE REFLECTIVITY.              ---  level

! PRAT1C    : CLEAR-SKY PARALLEL TRANSMISSIVITY.        ---
! PRAT2C    : CLEAR-SKY PARALLEL/DIFFUSE TRANSMISSIVITY.   |
! PRAT3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.     |
! PRAT4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAT5C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAT1N    : CLOUDY PARALLEL TRANSMISSIVITY.              | from top
! PRAT2N    : CLOUDY PARALLEL/DIFFUSE TRANSMISSIVITY.      | to given
! PRAT3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | level
! PRAT4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               |
! PRAT5N    : CLOUDY DIFFUSE REFLECTIVITY.              ---

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS.
!     --------------------

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PDIFCQ     : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
! PDIFCS     : CONVECTIVE FLUX OF ENTHALPY (NOT RAIN/SNOW).
! PDIFTQ     : TURBULENT FLUX (INC. Q NEGATIVE) OF SPECIFIC HUMIDITY.
! PDIFTS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY).
! PFCCQN     : CONVECTIVE CONDENSATION FLUX FOR ICE.
! PFCCQL     : CONVECTIVE CONDENSATION FLUX FOR LIQUID WATER.
! PFPLCL     : CONVECTIVE PRECIPITATION AS RAIN.
! PFPLCN     : CONVECTIVE PRECIPITATION AS SNOW.
! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.
! PFRSO      : SHORTWAVE RADIATIVE FLUX.
! PFRTH      : LONGWAVE RADIATIVE FLUX.
! PFCSQN     : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL     : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PSTRCU     : CONVECTIVE FLUX OF MOMENTUM "U".
! PSTRCV     : CONVECTIVE FLUX OF MOMENTUM "V".
! PSTRDU     : GRAVITY WAVE DRAG FLUX "U".
! PSTRDV     : GRAVITY WAVE DRAG FLUX "V".
! PSTRTU     : TURBULENT FLUX OF MOMENTUM "U".
! PSTRTV     : TURBULENT FLUX OF MOMENTUM "V".
! PSTRMU     : MESOSPHERIC FLUX FOR "U"-MOMENTUM.
! PSTRMV     : MESOSPHERIC FLUX FOR "V"-MOMENTUM.
! PFRMH      : MESOSPHERIC ENTHALPY FLUX.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------
!     - THE INITIALIZATION OF THE PHYSICAL FLUXES.
!     - CALL THE ACTUAL PHYSICS SUBROUTINES OF SIMPLIFIED PHYSICAL
!        PARAMETRIZATIONS.

!     Author.
!     -------
!      95-12-05 M. Janiskova

!     Modifications.
!     --------------
!      01-03, mesospheric drag - F. Bouyssel
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      05-10, PQ introduced in ACDRME (P.Simon) - A. Alias
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMRCOEF , ONLY : TRCOEF
USE YOMCST   , ONLY : TCST
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_MISC_TYPE
USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(CPG_BNDS_TYPE),            INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),            INTENT(IN)    :: YDCPG_OPTS
TYPE(MF_PHYS_BASE_STATE_TYPE),  INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TRCOEF)      ,INTENT(IN)    :: YDRCOEF
TYPE(TCST)        ,INTENT(IN)    :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KSGST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCVGQ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS1(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(YDCPG_OPTS%KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAC(YDCPG_OPTS%KFLEVG+1,YDCPG_OPTS%KFLEVG+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCOR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5C(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5N(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) 

!     ------------------------------------------------------------------

! - 2D (0:KLEV) .

! ZKTROV     : VERTICAL EXCHANGE COEFFICIENT OF T AND Q IN KG/(M*M*S).
! ZKUROV     : VERTICAL EXCHANGE COEFFICIENT OF U AND V IN KG/(M*M*S).
! ZXTROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKTROV.
! ZXUROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKUROV.
! ZFPLC      : SUM OF CONVECTIVE PRECIPITATION AS RAIN AND AS SNOW.

! - 1D (DIAGNOSTIC) .

! ZCDROV     : PCD NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZCHROV     : PCH NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZXDROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCDROV.
! ZXHROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCHROV.
! ZNEIJ      : FRACTION OF SOIL COVERED BY SNOW.
! ZFCLL      : LATENT HEAT FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFCLN      : LATENT HEAT FLUX OVER SNOW (OR ICE).
! ZFCS       : SENSIBLE HEAT FLUX AT SURFACE LEVEL.
! ZFEVL      : WATER VAPOR FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFEVN      : WATER VAPOR FLUX OVER SNOW (OR ICE).
! ZLHS       : LATENT HEAT AT THE SURFACE.
! ZALB       : MODEL SURFACE SHORT ALBEDO.

! - 1D (GEOGRAPHIC) .

! ZGZ0       : GRAVITY * ROUGHNESS LENGTH.

! - OTHER TEMPORARY 1D FIELD

! ZDELT     : ONE FOR SATURATION CALCULATION FOR ICE, ZERO OTHERWISE.
! ZESP      : SATURATION WATER VAPOUR PRESSURE OVER PRESSURE.
! ZQSATS    : SPECIFIC HUMIDITY OF SATURATION AT THE SURFACE.
! ZWSMX     : MAXIMUM CONTENT OF THE SURFACE RESERVOIR.
! ZFRMQ     : Humidity MESOSPHERIQC flux

REAL(KIND=JPRB) :: ZXTROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZXUROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZKTROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG),ZKUROV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFPLC (YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB) :: ZFRMQ(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)

REAL(KIND=JPRB) :: ZXDROV(YDCPG_OPTS%KLON),ZXHROV(YDCPG_OPTS%KLON),ZCDROV(YDCPG_OPTS%KLON),ZCHROV(YDCPG_OPTS%KLON),&
 & ZGZ0(YDCPG_OPTS%KLON),ZNEIJ(YDCPG_OPTS%KLON),&
 & ZLHS(YDCPG_OPTS%KLON),ZFEVL(YDCPG_OPTS%KLON),ZFEVN(YDCPG_OPTS%KLON),&
 & ZFCLL(YDCPG_OPTS%KLON),ZFCLN(YDCPG_OPTS%KLON),ZFCS(YDCPG_OPTS%KLON),&
 & ZALB(YDCPG_OPTS%KLON)

REAL(KIND=JPRB) :: ZNTS(YDCPG_OPTS%KLON)

INTEGER(KIND=JPIM) :: IJN, JLEV, JLON

REAL(KIND=JPRB) :: Z0CR, Z0CRIW
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "acconv.intfb.h"
#include "acdifsp.intfb.h"
#include "acdragl.intfb.h"
#include "acdrme.intfb.h"
#include "acqwlsr.intfb.h"
#include "acrads.intfb.h"
#include "actsec.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('APLPARS',0,ZHOOK_HANDLE)
ASSOCIATE(YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE,YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP,YDCSGLEG=>YDGEOMETRY%YRCSGLEG,YDVSPLIP=>YDGEOMETRY%YRVSPLIP,YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM,YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB,YDGSGEOM=>YDGEOMETRY%YRGSGEOM, &
 & YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, &
 & YDSPGEOM=>YDGEOMETRY%YSPGEOM, &
 & YDPHY=>YDML_PHY_MF%YRPHY,YDTOPH=>YDML_PHY_MF%YRTOPH,YDSIMPHL=>YDML_PHY_MF%YRSIMPHL, &
 & YDPHY1=>YDML_PHY_MF%YRPHY1,YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(TEQK=>YDPHY0%TEQK, &
 & ALCRIN=>YDPHY1%ALCRIN, ALRCN2=>YDPHY1%ALRCN2, ALRCN1=>YDPHY1%ALRCN1, &
 & WCRIN=>YDPHY1%WCRIN, &
 & RMESOQ=>YDTOPH%RMESOQ, RMESOT=>YDTOPH%RMESOT, RMESOU=>YDTOPH%RMESOU, &
 & LSTRASP=>YDSIMPHL%LSTRASP, LRRMESSP=>YDSIMPHL%LRRMESSP, &
 & LGWDSP=>YDSIMPHL%LGWDSP, LVDIFSP=>YDSIMPHL%LVDIFSP, &
 & LCVRASP=>YDSIMPHL%LCVRASP, LRAYSP=>YDSIMPHL%LRAYSP, &
 & LCONDWT=>YDPHY%LCONDWT, LMPHYS=>YDPHY%LMPHYS, &
 & LNEIGE=>YDPHY%LNEIGE, LTLADDIA=>YDRCOEF%LTLADDIA, &
 & STTEM=>YDSTA%STTEM)
!     ------------------------------------------------------------------

!*
!**
!     -----------------------------------------------------------------
!     I - SOME INITIALIZATION AND SET VARIABLES TO ZERO IN THE CASE OF
!         NON-CALLING PARAMETRIZATIONS AND AVOIDING OF OVERWRITTING OF
!         THE FLUXES WHICH ARE ALREADY COMPUTED IN FULL PHYSICS.
!     ------------------------------------------------------------------

IJN=YDCPG_OPTS%KLON

IF (LMPHYS) THEN

!=PARALLEL

  IF (LCVRASP) THEN
    YDMF_PHYS%OUT%DIFCQ(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%DIFCS(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FCCQN(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FCCQL(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FPLCL(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FPLCN(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRCU(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRCV(:,:) = 0.0_JPRB
  ENDIF
  IF (LGWDSP) THEN
    YDMF_PHYS%OUT%STRDU(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRDV(:,:) = 0.0_JPRB
  ENDIF
  IF (LSTRASP) THEN
    YDMF_PHYS%OUT%FPLSL(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FPLSN(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FCSQN(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FCSQL(:,:) = 0.0_JPRB
  ENDIF
  IF (LVDIFSP) THEN
    YDMF_PHYS%OUT%DIFTQ(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%DIFTS(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRTU(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRTV(:,:) = 0.0_JPRB
  ENDIF
  IF (LRAYSP.AND.(YDCPG_OPTS%KSTEP > 1.OR.LTLADDIA)) THEN
    YDMF_PHYS%OUT%FRSO(:,:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FRTH(:,:,:) = 0.0_JPRB
  ENDIF
  IF (LRRMESSP) THEN
    YDMF_PHYS%OUT%STRMU(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%STRMV(:,:) = 0.0_JPRB
    YDMF_PHYS%OUT%FRMH (:,:) = 0.0_JPRB
  ENDIF

!=END PARALLEL

ELSE

!=PARALLEL

  YDMF_PHYS%OUT%DIFCQ(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%DIFCS(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%DIFTQ(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%DIFTS(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FPLCL(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FPLCN(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FPLSL(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FPLSN(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FCCQN(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FCCQL(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FCSQN(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FCSQL(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRCU(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRCV(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRDU(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRDV(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRTU(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRTV(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRMU(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%STRMV(:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FRMH (:,:) = 0.0_JPRB

  YDMF_PHYS%OUT%FRSO(:,:,:) = 0.0_JPRB
  YDMF_PHYS%OUT%FRTH(:,:,:) = 0.0_JPRB

!=END PARALLEL

ENDIF

!=PARALLEL

ZFCLL(:) = 0.0_JPRB
ZFCLN(:) = 0.0_JPRB
ZFCS(:)  = 0.0_JPRB
ZFEVL(:) = 0.0_JPRB
ZFEVN(:) = 0.0_JPRB
ZLHS(:)  = 0.0_JPRB
ZFRMQ(:,:)  = 0.0_JPRB

!=END PARALLEL

!*
!     -------------------------------------------------------------------
!     II - COMPUTATION OF FRACTION OF SOIL COVERED BY SNOW AND ROUGHNESS
!     -------------------------------------------------------------------

!=PARALLEL

DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA

!     COMPUTATIONS CONCERNING THE PRESENCE OR ABSENCE OF SNOW ON GROUND.

  Z0CR=YDCST%RG*ALRCN1
  Z0CRIW=1.0_JPRB/(YDCST%RG*ALRCN2)

  IF (LNEIGE) THEN
    ZNEIJ(JLON)=YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)*YDMF_PHYS_BASE_STATE%YGSP_SG%F(JLON,1)/(YDMF_PHYS_BASE_STATE%YGSP_SG%F(JLON,1)+WCRIN)
    ZGZ0(JLON)=YDMF_PHYS_SURF%GSD_VF%PZ0F(JLON)+(Z0CR-YDMF_PHYS_SURF%GSD_VF%PZ0F(JLON))*YDMF_PHYS_SURF%GSD_VF%PLSM(JLON)&
     & *YDMF_PHYS_BASE_STATE%YGSP_SG%F(JLON,1)/(YDMF_PHYS_BASE_STATE%YGSP_SG%F(JLON,1)+WCRIN+Z0CRIW*YDMF_PHYS_SURF%GSD_VF%PZ0F(JLON))  
  ELSE
    ZNEIJ(JLON)=0.0_JPRB
    ZGZ0(JLON)=YDMF_PHYS_SURF%GSD_VF%PZ0F(JLON)
  ENDIF

ENDDO

!=END PARALLEL

!*
!     ------------------------------------------------------------------
!     III - COMPUTATION OF VERTICAL TURBULENT EXCHANGE COEFFICIENTS
!           AND SURFACE EXCHANGE COEFFICIENTS AND COMPUTATION OF
!           VERTICAL TURBULENT DIFFUSION
!     ------------------------------------------------------------------

IF ( LVDIFSP) THEN

!=PARALLEL

  DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA

    ZNTS(JLON)=YDMF_PHYS_SURF%GSP_RR%PT_T1(JLON)

  ENDDO

  CALL ACTSEC(YDCST,YDPHY,YDPHY0,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI,YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,YDMF_PHYS_BASE_STATE%Q,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%U,YDMF_PHYS_BASE_STATE%V,&
   & YDCPG_MISC%QS,YDMF_PHYS_BASE_STATE%YGSP_RR%T,ZGZ0,&
   & ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & ZCDROV,ZCHROV,&
   & ZXDROV,ZXHROV)

  CALL ACDIFSP(YDCST,YDML_PHY_MF,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI,YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%Q,&
   & YDMF_PHYS_BASE_STATE%U,YDMF_PHYS_BASE_STATE%V,ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & ZCDROV,ZCHROV,ZXDROV,ZXHROV,&
   & YDMF_PHYS_SURF%GSD_VF%PLSM,ZNEIJ,YDCPG_MISC%QS,YDMF_PHYS_BASE_STATE%YGSP_RR%T,ZNTS,YDMF_PHYS_SURF%GSP_RR%PT_T1,PQS1,&
   & YDMF_PHYS%OUT%DIFTQ,YDMF_PHYS%OUT%DIFTS,YDMF_PHYS%OUT%STRTU,YDMF_PHYS%OUT%STRTV,&
   & ZFCLL,ZFCLN,ZFCS,ZFEVL,ZFEVN,ZLHS)

!=END PARALLEL

ENDIF

!*
!     ------------------------------------------------------------------
!     IV - COMPUTATION OF OROGRAPHIC GRAVITY WAVE DRAG
!     ------------------------------------------------------------------

IF ( LGWDSP ) THEN

!=PARALLEL

  CALL ACDRAGL(YDCST,YDML_PHY_MF,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI,YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,&
   & YDMF_PHYS_BASE_STATE%U,YDMF_PHYS_BASE_STATE%V,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%Q,&
   & YDMF_PHYS_SURF%GSD_VF%PGETRL,YDMF_PHYS_SURF%GSD_VF%PVRLAN,YDMF_PHYS_SURF%GSD_VF%PVRLDI,YDMF_PHYS_BASE_STATE%YGSP_RR%T,YDCPG_MISC%QS,&
   & YDMF_PHYS%OUT%STRDU,YDMF_PHYS%OUT%STRDV)

!=END PARALLEL

ENDIF

!*
!     ------------------------------------------------------------------
!     V - COMPUTING CONVECTIVE PRECIPITATIONS, DEEP CONVECTIVE SCHEME
!     ------------------------------------------------------------------

IF ( LVDIFSP.AND.LCVRASP ) THEN

!=PARALLEL

!        THE DYNAMIC TENDENCY OF Q IS MULTIPLIED BY CORRECTION FACTOR
!        OF LOCAL RESOLUTION OF THE MODEL AND BY ARGUMENT OF THE
!        EVAPORATION OF THE SURFACE

  DO JLEV=KTDIA,YDCPG_OPTS%KFLEVG
    DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PCVGQ(JLON,JLEV)=PCVGQ(JLON,JLEV)/(1.0_JPRB+PGM(JLON)*TEQK)&
       & -YDCST%RG*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP(JLON,JLEV)&
       & *(YDMF_PHYS%OUT%DIFTQ(JLON,JLEV)-YDMF_PHYS%OUT%DIFTQ(JLON,JLEV-1))  
    ENDDO
  ENDDO

!=END PARALLEL

ENDIF

IF ( LCVRASP ) THEN

!=PARALLEL

  CALL ACCONV(YDCST,YDML_PHY_MF,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,PCVGQ,&
   & YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,YDMF_PHYS_BASE_STATE%Q,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%U,YDMF_PHYS_BASE_STATE%V,&
   & YDMF_PHYS_BASE_STATE%YGSP_RR%T,&
   & YDMF_PHYS%OUT%DIFCQ,YDMF_PHYS%OUT%DIFCS,YDMF_PHYS%OUT%FPLCL,YDMF_PHYS%OUT%FPLCN,YDMF_PHYS%OUT%STRCU,YDMF_PHYS%OUT%STRCV)

!=END PARALLEL

ENDIF

!*
!     ------------------------------------------------------------------
!     VI - COMPUTATION OF WET-BULB CHARACTERISTICS AND STRATIFORM
!          PRECIPITATION FLUXES
!     ------------------------------------------------------------------
IF ( LSTRASP ) THEN

!=PARALLEL

  CALL ACQWLSR(YDCST,YDML_PHY_MF,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%Q,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,&
   & YDMF_PHYS%OUT%FPLSL,YDMF_PHYS%OUT%FPLSN)

!=END PARALLEL

ENDIF

!*
!     -------------------------------------------------------------------
!     VII - COMPUTATION OF RADIATIVE FLUXES
!     -------------------------------------------------------------------

IF (LRAYSP.AND.(YDCPG_OPTS%KSTEP > 1.OR.LTLADDIA)) THEN

!=PARALLEL

!        ALBEDO AND EMISSIVITY WITH SNOW

  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    ZALB(JLON)=YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)-ZNEIJ(JLON)*(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON)-MAX(YDMF_PHYS_SURF%GSD_VF%PALBF(JLON),ALCRIN))
  ENDDO

  DO JLEV=KTDIA-1,YDCPG_OPTS%KFLEVG
    DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      ZFPLC(JLON,JLEV)=YDMF_PHYS%OUT%FPLCL(JLON,JLEV)+YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
    ENDDO
  ENDDO

!        COMPUTATION OF THE FLUXES

  CALL ACRADS(YDCST,YDGEOMETRY%YRVAB,YDML_PHY_MF,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,IJN,&
   & YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF,YDMF_PHYS_BASE_STATE%Q,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,&
   & ZFPLC,ZALB,PMU0,YDMF_PHYS_BASE_STATE%YGSP_RR%T,&
   & PAC,PCOR,&
   & PRAB3C,PRAB3N,PRAB4C,PRAB4N,PRAB6C,PRAB6N,&
   & PRAT1C,PRAT1N,PRAT2C,PRAT2N,PRAT3C,PRAT3N,&
   & PRAT4C,PRAT4N,PRAT5C,PRAT5N,&
   & YDMF_PHYS%OUT%FRSO,YDMF_PHYS%OUT%FRTH)

!=END PARALLEL

ENDIF

!*
!     ------------------------------------------------------------------
!     VIII - COMPUTES THE MOMENTUM FLUX DUE TO A MESOSPHERIC DRAG AND 
!            THE HEAT FLUX CORRESPONDING TO A RELAXATION OF TEMPERATURE 
!            TOWARDS THE STANDARD ATMOSPHERE
!     ------------------------------------------------------------------

IF ( LRRMESSP ) THEN

!=PARALLEL

  CALL ACDRME(YDCST, YDSTA, YDML_PHY_MF%YRPHY2,YDTOPH,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,YDCPG_OPTS%KLON,KTDIA,YDCPG_OPTS%KFLEVG,&
   & YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP,YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP,YDMF_PHYS_BASE_STATE%T,YDMF_PHYS_BASE_STATE%Q,YDMF_PHYS_BASE_STATE%U,YDMF_PHYS_BASE_STATE%V, YDMF_PHYS%OUT%FRMH,ZFRMQ,YDMF_PHYS%OUT%STRMU,YDMF_PHYS%OUT%STRMV)

!=END PARALLEL

ENDIF

!*
!     ------------------------------------------------------------------
!     IX - COMPUTATION OF THE FLUXES OF ENTHALPY AND SENSIBLE HEAT
!          (THE FUNCTION OF THE FLUXES OF PRECIPITATIONS AND
!           CONDENSATION)
!     ------------------------------------------------------------------

!      - IF IT IS NOT LIQUID WATER, THEN
!                   THE FLUX OF CONDENSATION = THE FLUX OF PRECIPITATION

IF (.NOT.LCONDWT) THEN

!=PARALLEL

  DO JLEV = KTDIA, YDCPG_OPTS%KFLEVG
    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      YDMF_PHYS%OUT%FCCQL(JLON,JLEV) = YDMF_PHYS%OUT%FPLCL(JLON,JLEV)
      YDMF_PHYS%OUT%FCCQN(JLON,JLEV) = YDMF_PHYS%OUT%FPLCN(JLON,JLEV)
      YDMF_PHYS%OUT%FCSQL(JLON,JLEV) = YDMF_PHYS%OUT%FPLSL(JLON,JLEV)
      YDMF_PHYS%OUT%FCSQN(JLON,JLEV) = YDMF_PHYS%OUT%FPLSN(JLON,JLEV)
    ENDDO
  ENDDO

!=END PARALLEL

ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('APLPARS',1,ZHOOK_HANDLE)
END SUBROUTINE APLPARS
