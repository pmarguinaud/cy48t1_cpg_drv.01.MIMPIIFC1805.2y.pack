!OPTIONS XOPT(NOEVAL)
SUBROUTINE APLPARS(YDGEOMETRY, YDRCOEF, YDCST, YDML_PHY_MF, &
 & KIDIA , KFDIA  , KLON  ,  KTDIA , KLEV   ,&
 & KSGST  , KSTEP  ,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & PAPHI  , PAPRS  , PAPHIF , PAPRSF , PDELP  , PRDELP ,&
 & PU     , PV     , PT     , PQ     , PCP    , PCVGQ  ,&
 & PSNS   , PTS    , PQS    ,&
 & PTS1   , PQS1   ,&
 & PGETRL , PLSM   , PGZ0F  , PVRLAN , PVRLDI ,&
 & PALBF  , PMU0   , PGM    ,&
 & PAC    , PCOR   ,&
 & PRAB3C , PRAB3N , PRAB4C , PRAB4N , PRAB6C , PRAB6N ,&
 & PRAT1C , PRAT1N , PRAT2C , PRAT2N , PRAT3C , PRAT3N ,&
 & PRAT4C , PRAT4N , PRAT5C , PRAT5N,&
 ! - OUTPUT .
 & PDIFCQ , PDIFCS , PDIFTQ , PDIFTS ,&
 & PFCCQL , PFCCQN , PFCSQL , PFCSQN ,&
 & PFPLCL , PFPLCN , PFPLSL , PFPLSN , PFRSO  , PFRTH  ,&
 & PSTRCU , PSTRCV , PSTRDU , PSTRDV , PSTRTU , PSTRTV ,&
 & PSTRMU , PSTRMV , PFRMH)

!**** *APLPARS - CALL THE SIMPLIFIED PHYSICAL PARAMETRIZATIONS.

!     Subject.
!     --------
!     - CALL THE SUBROUTINES OF THE SIMPLIFIED PHYSICAL PARAMETRIZATIONS.

!**   Interface.
!     ----------
!        *CALL* *APLPARS*

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------

! - DIMENSIONAL PARAMETERS.

! KIDIA, KFDIA : START/END OF HORIZONTAL LOOP  (IST,IEND IN *CPG*).
! KLON : HORIZONTAL DIMENSION                  (NPROMA IN *CPG*).
! KTDIA : START OF THE VERTICAL LOOP IN THE PHYSICS (IF SOME LEVELS ARE
!                      SKIPPED AT THE TOP OF THE MODEL).
! KLEV  : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG IN *CPG*)
! KSGST : NUMBER OF SUBGRID SURFACE TEMPERATURES AND FLUXES
!                      (NTSSG IN *CPG*)

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIAL ON HALF-LEVELS.
! PAPRS      : PRESSURE ON HALF-LEVELS.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIAL ON FULL LEVELS.
! PAPRSF     : PRESSURE ON FULL LEVELS.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.
! PRDELP     : INVERSE OF PDELP.
! PU         : X-COMPONENT OF WIND.
! PV         : Y-COMPONENT OF WIND.
! PT         : TEMPERATURE.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PCP        : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PCVGQ      : CONVERGENCE OF HUMIDITY.

! - 1D (PROGNOSTIC QUANTITIES) .

! PSNS       : MASS OF SNOW PER UNIT SURFACE.
! PTS        : SURFACE LAYER TEMPERATURE.
! PQS        : SURFACE SPECIFIC HUMIDITY.

! - 1D (GEOGRAPHICAL DISTRIBUTION) .

! PGETRL     : STANDARD DEVIATION OF OROGRAPHY  (UNIT: J/KG).
! PLSM       : LAND/SEA MASK.
! PVRLAN     : ANISOTROPY OF THE RELIEF (1 = ISOTROPY).
! PVRLDI     : ANGLE OF MAXIMAL GRADIENT OF RELIEF WITH x-AXIS.
! PGZ0F      : GRAVITY * ROUGHNESS LENGTH (TIME VARIABLE).
! PALBF      : BACKGROUND SURFACE SHORTWAVE ALBEDO (SNOW-FREE).

! - 1D (DIAGNOSTIC QUANTITIES).

! PMU0       : LOCAL COSINE OF INSTANTENOUS SOLAR ZENITH ANGLE.
! PNEBH      : CONVECTIVE CLOUDINESS.

! - CONSTANTES

! PAC       : CURTIS MATRIX FOR THERMAL RADIATION FOR CLEAR SKY
! PCOR      : CORRECTION FOR CLOUDINESS FOR THERMAL RADIATION

! PRAB3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.  ---
! PRAB4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAB6C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAB3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | from bottom
! PRAB4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               | to given
! PRAB6N    : CLOUDY DIFFUSE REFLECTIVITY.              ---  level

! PRAT1C    : CLEAR-SKY PARALLEL TRANSMISSIVITY.        ---
! PRAT2C    : CLEAR-SKY PARALLEL/DIFFUSE TRANSMISSIVITY.   |
! PRAT3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.     |
! PRAT4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAT5C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAT1N    : CLOUDY PARALLEL TRANSMISSIVITY.              | from top
! PRAT2N    : CLOUDY PARALLEL/DIFFUSE TRANSMISSIVITY.      | to given
! PRAT3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | level
! PRAT4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               |
! PRAT5N    : CLOUDY DIFFUSE REFLECTIVITY.              ---

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS.
!     --------------------

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PDIFCQ     : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
! PDIFCS     : CONVECTIVE FLUX OF ENTHALPY (NOT RAIN/SNOW).
! PDIFTQ     : TURBULENT FLUX (INC. Q NEGATIVE) OF SPECIFIC HUMIDITY.
! PDIFTS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY).
! PFCCQN     : CONVECTIVE CONDENSATION FLUX FOR ICE.
! PFCCQL     : CONVECTIVE CONDENSATION FLUX FOR LIQUID WATER.
! PFPLCL     : CONVECTIVE PRECIPITATION AS RAIN.
! PFPLCN     : CONVECTIVE PRECIPITATION AS SNOW.
! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.
! PFRSO      : SHORTWAVE RADIATIVE FLUX.
! PFRTH      : LONGWAVE RADIATIVE FLUX.
! PFCSQN     : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL     : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PSTRCU     : CONVECTIVE FLUX OF MOMENTUM "U".
! PSTRCV     : CONVECTIVE FLUX OF MOMENTUM "V".
! PSTRDU     : GRAVITY WAVE DRAG FLUX "U".
! PSTRDV     : GRAVITY WAVE DRAG FLUX "V".
! PSTRTU     : TURBULENT FLUX OF MOMENTUM "U".
! PSTRTV     : TURBULENT FLUX OF MOMENTUM "V".
! PSTRMU     : MESOSPHERIC FLUX FOR "U"-MOMENTUM.
! PSTRMV     : MESOSPHERIC FLUX FOR "V"-MOMENTUM.
! PFRMH      : MESOSPHERIC ENTHALPY FLUX.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------
!     - THE INITIALIZATION OF THE PHYSICAL FLUXES.
!     - CALL THE ACTUAL PHYSICS SUBROUTINES OF SIMPLIFIED PHYSICAL
!        PARAMETRIZATIONS.

!     Author.
!     -------
!      95-12-05 M. Janiskova

!     Modifications.
!     --------------
!      01-03, mesospheric drag - F. Bouyssel
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      05-10, PQ introduced in ACDRME (P.Simon) - A. Alias
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK
USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE YOMRCOEF , ONLY : TRCOEF
USE YOMCST   , ONLY : TCST

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TRCOEF)      ,INTENT(IN)    :: YDRCOEF
TYPE(TCST)        ,INTENT(IN)    :: YDCST
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSGST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCVGQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS1(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS1(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGETRL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0F(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRLAN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRLDI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAC(KLEV+1,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCOR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCCQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSO(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRTH(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRDU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRDV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRMU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRMV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRMH(KLON,0:KLEV) 

!     ------------------------------------------------------------------

! - 2D (0:KLEV) .

! ZKTROV     : VERTICAL EXCHANGE COEFFICIENT OF T AND Q IN KG/(M*M*S).
! ZKUROV     : VERTICAL EXCHANGE COEFFICIENT OF U AND V IN KG/(M*M*S).
! ZXTROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKTROV.
! ZXUROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKUROV.
! ZFPLC      : SUM OF CONVECTIVE PRECIPITATION AS RAIN AND AS SNOW.

! - 1D (DIAGNOSTIC) .

! ZCDROV     : PCD NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZCHROV     : PCH NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZXDROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCDROV.
! ZXHROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCHROV.
! ZNEIJ      : FRACTION OF SOIL COVERED BY SNOW.
! ZFCLL      : LATENT HEAT FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFCLN      : LATENT HEAT FLUX OVER SNOW (OR ICE).
! ZFCS       : SENSIBLE HEAT FLUX AT SURFACE LEVEL.
! ZFEVL      : WATER VAPOR FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFEVN      : WATER VAPOR FLUX OVER SNOW (OR ICE).
! ZLHS       : LATENT HEAT AT THE SURFACE.
! ZALB       : MODEL SURFACE SHORT ALBEDO.

! - 1D (GEOGRAPHIC) .

! ZGZ0       : GRAVITY * ROUGHNESS LENGTH.

! - OTHER TEMPORARY 1D FIELD

! ZDELT     : ONE FOR SATURATION CALCULATION FOR ICE, ZERO OTHERWISE.
! ZESP      : SATURATION WATER VAPOUR PRESSURE OVER PRESSURE.
! ZQSATS    : SPECIFIC HUMIDITY OF SATURATION AT THE SURFACE.
! ZWSMX     : MAXIMUM CONTENT OF THE SURFACE RESERVOIR.
! ZFRMQ     : Humidity MESOSPHERIQC flux

REAL(KIND=JPRB) :: ZXTROV(KLON,0:KLEV),ZXUROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZKTROV(KLON,0:KLEV),ZKUROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZFPLC (KLON,0:KLEV)
REAL(KIND=JPRB) :: ZFRMQ(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZXDROV(KLON),ZXHROV(KLON),ZCDROV(KLON),ZCHROV(KLON),&
 & ZGZ0(KLON),ZNEIJ(KLON),&
 & ZLHS(KLON),ZFEVL(KLON),ZFEVN(KLON),&
 & ZFCLL(KLON),ZFCLN(KLON),ZFCS(KLON),&
 & ZALB(KLON)

REAL(KIND=JPRB) :: ZNTS(KLON)

INTEGER(KIND=JPIM) :: IJN, JLEV, JLON

REAL(KIND=JPRB) :: Z0CR, Z0CRIW
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "acconv.intfb.h"
#include "acdifsp.intfb.h"
#include "acdragl.intfb.h"
#include "acdrme.intfb.h"
#include "acqwlsr.intfb.h"
#include "acrads.intfb.h"
#include "actsec.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('APLPARS',0,ZHOOK_HANDLE)
ASSOCIATE(YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE,YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP,YDCSGLEG=>YDGEOMETRY%YRCSGLEG,YDVSPLIP=>YDGEOMETRY%YRVSPLIP,YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM,YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB,YDGSGEOM=>YDGEOMETRY%YRGSGEOM, &
 & YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, &
 & YDSPGEOM=>YDGEOMETRY%YSPGEOM, &
 & YDPHY=>YDML_PHY_MF%YRPHY,YDTOPH=>YDML_PHY_MF%YRTOPH,YDSIMPHL=>YDML_PHY_MF%YRSIMPHL, &
 & YDPHY1=>YDML_PHY_MF%YRPHY1,YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(TEQK=>YDPHY0%TEQK, &
 & ALCRIN=>YDPHY1%ALCRIN, ALRCN2=>YDPHY1%ALRCN2, ALRCN1=>YDPHY1%ALRCN1, &
 & WCRIN=>YDPHY1%WCRIN, &
 & RMESOQ=>YDTOPH%RMESOQ, RMESOT=>YDTOPH%RMESOT, RMESOU=>YDTOPH%RMESOU, &
 & LSTRASP=>YDSIMPHL%LSTRASP, LRRMESSP=>YDSIMPHL%LRRMESSP, &
 & LGWDSP=>YDSIMPHL%LGWDSP, LVDIFSP=>YDSIMPHL%LVDIFSP, &
 & LCVRASP=>YDSIMPHL%LCVRASP, LRAYSP=>YDSIMPHL%LRAYSP, &
 & LCONDWT=>YDPHY%LCONDWT, LMPHYS=>YDPHY%LMPHYS, &
 & LNEIGE=>YDPHY%LNEIGE, LTLADDIA=>YDRCOEF%LTLADDIA, &
 & STTEM=>YDSTA%STTEM)
!     ------------------------------------------------------------------

!*
!**
!     -----------------------------------------------------------------
!     I - SOME INITIALIZATION AND SET VARIABLES TO ZERO IN THE CASE OF
!         NON-CALLING PARAMETRIZATIONS AND AVOIDING OF OVERWRITTING OF
!         THE FLUXES WHICH ARE ALREADY COMPUTED IN FULL PHYSICS.
!     ------------------------------------------------------------------

IJN=KLON

IF (LMPHYS) THEN
  IF (LCVRASP) THEN
    PDIFCQ(:,:) = 0.0_JPRB
    PDIFCS(:,:) = 0.0_JPRB
    PFCCQN(:,:) = 0.0_JPRB
    PFCCQL(:,:) = 0.0_JPRB
    PFPLCL(:,:) = 0.0_JPRB
    PFPLCN(:,:) = 0.0_JPRB
    PSTRCU(:,:) = 0.0_JPRB
    PSTRCV(:,:) = 0.0_JPRB
  ENDIF
  IF (LGWDSP) THEN
    PSTRDU(:,:) = 0.0_JPRB
    PSTRDV(:,:) = 0.0_JPRB
  ENDIF
  IF (LSTRASP) THEN
    PFPLSL(:,:) = 0.0_JPRB
    PFPLSN(:,:) = 0.0_JPRB
    PFCSQN(:,:) = 0.0_JPRB
    PFCSQL(:,:) = 0.0_JPRB
  ENDIF
  IF (LVDIFSP) THEN
    PDIFTQ(:,:) = 0.0_JPRB
    PDIFTS(:,:) = 0.0_JPRB
    PSTRTU(:,:) = 0.0_JPRB
    PSTRTV(:,:) = 0.0_JPRB
  ENDIF
  IF (LRAYSP.AND.(KSTEP > 1.OR.LTLADDIA)) THEN
    PFRSO(:,:,:) = 0.0_JPRB
    PFRTH(:,:,:) = 0.0_JPRB
  ENDIF
  IF (LRRMESSP) THEN
    PSTRMU(:,:) = 0.0_JPRB
    PSTRMV(:,:) = 0.0_JPRB
    PFRMH (:,:) = 0.0_JPRB
  ENDIF

ELSE
  PDIFCQ(:,:) = 0.0_JPRB
  PDIFCS(:,:) = 0.0_JPRB
  PDIFTQ(:,:) = 0.0_JPRB
  PDIFTS(:,:) = 0.0_JPRB
  PFPLCL(:,:) = 0.0_JPRB
  PFPLCN(:,:) = 0.0_JPRB
  PFPLSL(:,:) = 0.0_JPRB
  PFPLSN(:,:) = 0.0_JPRB
  PFCCQN(:,:) = 0.0_JPRB
  PFCCQL(:,:) = 0.0_JPRB
  PFCSQN(:,:) = 0.0_JPRB
  PFCSQL(:,:) = 0.0_JPRB
  PSTRCU(:,:) = 0.0_JPRB
  PSTRCV(:,:) = 0.0_JPRB
  PSTRDU(:,:) = 0.0_JPRB
  PSTRDV(:,:) = 0.0_JPRB
  PSTRTU(:,:) = 0.0_JPRB
  PSTRTV(:,:) = 0.0_JPRB
  PSTRMU(:,:) = 0.0_JPRB
  PSTRMV(:,:) = 0.0_JPRB
  PFRMH (:,:) = 0.0_JPRB

  PFRSO(:,:,:) = 0.0_JPRB
  PFRTH(:,:,:) = 0.0_JPRB
ENDIF

ZFCLL(:) = 0.0_JPRB
ZFCLN(:) = 0.0_JPRB
ZFCS(:)  = 0.0_JPRB
ZFEVL(:) = 0.0_JPRB
ZFEVN(:) = 0.0_JPRB
ZLHS(:)  = 0.0_JPRB
ZFRMQ(:,:)  = 0.0_JPRB

!*
!     -------------------------------------------------------------------
!     II - COMPUTATION OF FRACTION OF SOIL COVERED BY SNOW AND ROUGHNESS
!     -------------------------------------------------------------------
DO JLON=KIDIA,KFDIA

!     COMPUTATIONS CONCERNING THE PRESENCE OR ABSENCE OF SNOW ON GROUND.

  Z0CR=YDCST%RG*ALRCN1
  Z0CRIW=1.0_JPRB/(YDCST%RG*ALRCN2)

  IF (LNEIGE) THEN
    ZNEIJ(JLON)=PLSM(JLON)*PSNS(JLON)/(PSNS(JLON)+WCRIN)
    ZGZ0(JLON)=PGZ0F(JLON)+(Z0CR-PGZ0F(JLON))*PLSM(JLON)&
     & *PSNS(JLON)/(PSNS(JLON)+WCRIN+Z0CRIW*PGZ0F(JLON))  
  ELSE
    ZNEIJ(JLON)=0.0_JPRB
    ZGZ0(JLON)=PGZ0F(JLON)
  ENDIF

ENDDO

!*
!     ------------------------------------------------------------------
!     III - COMPUTATION OF VERTICAL TURBULENT EXCHANGE COEFFICIENTS
!           AND SURFACE EXCHANGE COEFFICIENTS AND COMPUTATION OF
!           VERTICAL TURBULENT DIFFUSION
!     ------------------------------------------------------------------
IF ( LVDIFSP) THEN
  DO JLON=KIDIA, KFDIA

    ZNTS(JLON)=PTS1(JLON)

  ENDDO

  CALL ACTSEC(YDPHY,YDPHY0,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI,PAPHIF,PAPRS,PAPRSF,PQ,PT,PU,PV,&
   & PQS,PTS,ZGZ0,&
   & ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & ZCDROV,ZCHROV,&
   & ZXDROV,ZXHROV)

  CALL ACDIFSP(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI,PAPHIF,PDELP,PRDELP,PT,PQ,&
   & PU,PV,ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & ZCDROV,ZCHROV,ZXDROV,ZXHROV,&
   & PLSM,ZNEIJ,PQS,PTS,ZNTS,PTS1,PQS1,&
   & PDIFTQ,PDIFTS,PSTRTU,PSTRTV,&
   & ZFCLL,ZFCLN,ZFCS,ZFEVL,ZFEVN,ZLHS)
ENDIF

!*
!     ------------------------------------------------------------------
!     IV - COMPUTATION OF OROGRAPHIC GRAVITY WAVE DRAG
!     ------------------------------------------------------------------
IF ( LGWDSP ) THEN
  CALL ACDRAGL(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI,PAPHIF,PAPRS,PAPRSF,PDELP,PRDELP,&
   & PU,PV,PT,PQ,&
   & PGETRL,PVRLAN,PVRLDI,PTS,PQS,&
   & PSTRDU,PSTRDV)
ENDIF

!*
!     ------------------------------------------------------------------
!     V - COMPUTING CONVECTIVE PRECIPITATIONS, DEEP CONVECTIVE SCHEME
!     ------------------------------------------------------------------

IF ( LVDIFSP.AND.LCVRASP ) THEN
!        THE DYNAMIC TENDENCY OF Q IS MULTIPLIED BY CORRECTION FACTOR
!        OF LOCAL RESOLUTION OF THE MODEL AND BY ARGUMENT OF THE
!        EVAPORATION OF THE SURFACE

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      PCVGQ(JLON,JLEV)=PCVGQ(JLON,JLEV)/(1.0_JPRB+PGM(JLON)*TEQK)&
       & -YDCST%RG*PRDELP(JLON,JLEV)&
       & *(PDIFTQ(JLON,JLEV)-PDIFTQ(JLON,JLEV-1))  
    ENDDO
  ENDDO
ENDIF

IF ( LCVRASP ) THEN
  CALL ACCONV(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHIF,PAPRS,PAPRSF,PCVGQ,&
   & PDELP,PQ,PRDELP,PT,PU,PV,&
   & PTS,&
   & PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV)
ENDIF

!*
!     ------------------------------------------------------------------
!     VI - COMPUTATION OF WET-BULB CHARACTERISTICS AND STRATIFORM
!          PRECIPITATION FLUXES
!     ------------------------------------------------------------------
IF ( LSTRASP ) THEN
  CALL ACQWLSR(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PQ,PT,PAPRSF,PDELP,&
   & PFPLSL,PFPLSN)
ENDIF

!*
!     -------------------------------------------------------------------
!     VII - COMPUTATION OF RADIATIVE FLUXES
!     -------------------------------------------------------------------

IF (LRAYSP.AND.(KSTEP > 1.OR.LTLADDIA)) THEN
!        ALBEDO AND EMISSIVITY WITH SNOW

  DO JLON=KIDIA,KFDIA
    ZALB(JLON)=PALBF(JLON)-ZNEIJ(JLON)*(PALBF(JLON)-MAX(PALBF(JLON),ALCRIN))
  ENDDO

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZFPLC(JLON,JLEV)=PFPLCL(JLON,JLEV)+PFPLCN(JLON,JLEV)
    ENDDO
  ENDDO

!        COMPUTATION OF THE FLUXES

  CALL ACRADS(YDGEOMETRY%YRVAB,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,IJN,&
   & PT,PAPRSF,PQ,PDELP,PRDELP,&
   & ZFPLC,ZALB,PMU0,PTS,&
   & PAC,PCOR,&
   & PRAB3C,PRAB3N,PRAB4C,PRAB4N,PRAB6C,PRAB6N,&
   & PRAT1C,PRAT1N,PRAT2C,PRAT2N,PRAT3C,PRAT3N,&
   & PRAT4C,PRAT4N,PRAT5C,PRAT5N,&
   & PFRSO,PFRTH)
ENDIF

!*
!     ------------------------------------------------------------------
!     VIII - COMPUTES THE MOMENTUM FLUX DUE TO A MESOSPHERIC DRAG AND 
!            THE HEAT FLUX CORRESPONDING TO A RELAXATION OF TEMPERATURE 
!            TOWARDS THE STANDARD ATMOSPHERE
!     ------------------------------------------------------------------

IF ( LRRMESSP ) THEN
  CALL ACDRME(YDML_PHY_MF%YRPHY2,YDTOPH,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PCP,PDELP,PT,PQ,PU,PV,&
   & PFRMH,ZFRMQ,PSTRMU,PSTRMV,&
   & RMESOT,RMESOQ,RMESOU,STTEM)
ENDIF

!*
!     ------------------------------------------------------------------
!     IX - COMPUTATION OF THE FLUXES OF ENTHALPY AND SENSIBLE HEAT
!          (THE FUNCTION OF THE FLUXES OF PRECIPITATIONS AND
!           CONDENSATION)
!     ------------------------------------------------------------------

!      - IF IT IS NOT LIQUID WATER, THEN
!                   THE FLUX OF CONDENSATION = THE FLUX OF PRECIPITATION

IF (.NOT.LCONDWT) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      PFCCQL(JLON,JLEV) = PFPLCL(JLON,JLEV)
      PFCCQN(JLON,JLEV) = PFPLCN(JLON,JLEV)
      PFCSQL(JLON,JLEV) = PFPLSL(JLON,JLEV)
      PFCSQN(JLON,JLEV) = PFPLSN(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('APLPARS',1,ZHOOK_HANDLE)
END SUBROUTINE APLPARS
