!OPTIONS XOPT(NOEVAL)
SUBROUTINE APLPARSAD(YDCST, YDGEOMETRY, YDML_PHY_MF,KIDIA , KFDIA , KLON , KTDIA , KLEV ,&
 & KSGST  ,&
 !---------------------------------------------------------------------
 ! - INPUT .
 & PAPHI5 , PAPRS5 , PAPHIF5, PAPRSF5, PDELP5 , PRDELP5,&
 & PU5    , PV5    , PT5    , PQ5    , PQL5   , PQI5   ,PCP5   , PCVGQ5 ,&
 & PAPHI  , PAPRS  , PAPHIF , PAPRSF , PDELP  , PRDELP ,&
 & PU     , PV     , PT     , PQ     , PQL    , PQI    ,PCP    , PCVGQ  ,&
 & PSNS   , PTS    , PQS    , PTS1   , PQS1   ,&
 & PGETRL , PLSM   , PGZ0F  , PVRLAN , PVRLDI ,&
 & PALBF  , PMU0   , PGM    ,&
 & PAC    , PCOR   ,&
 & PRAB3C , PRAB3N , PRAB4C , PRAB4N , PRAB6C , PRAB6N ,&
 & PRAT1C , PRAT1N , PRAT2C , PRAT2N , PRAT3C , PRAT3N ,&
 & PRAT4C , PRAT4N , PRAT5C , PRAT5N ,&
 ! - OUTPUT .                                       ,
 & PDIFCQ5, PDIFCS5, PDIFTQ5, PDIFTS5,&
 & PFCCQL5, PFCCQN5, PFCSQL5, PFCSQN5,&
 & PFPLCL5, PFPLCN5, PFPLSL5, PFPLSN5, PFRSO5 , PFRTH5 ,&
 & PSTRCU5, PSTRCV5, PSTRDU5, PSTRDV5, PSTRTU5, PSTRTV5,&
 & PKTROV5, PKUROV5, PXTROV5, PXUROV5,&
 & PCDROV5, PCHROV5, PXDROV5, PXHROV5,PTRAJGWD5,&
 & PZQL5,PZQI5,PZQR5,PZQS5,PZLIQCVPPKF,PZNEBSCVPPKF,&
 & PSTRMU5, PSTRMV5, PFRMH5 ,&
 & PDIFCQ , PDIFCS , PDIFTQ , PDIFTS ,&
 & PFCCQL , PFCCQN , PFCSQL , PFCSQN ,&
 & PFPLCL , PFPLCN , PFPLSL , PFPLSN , PFRSO  , PFRTH  ,&
 & PSTRCU , PSTRCV , PSTRDU , PSTRDV , PSTRTU , PSTRTV ,&
 & PSTRMU , PSTRMV , PFRMH  , PTRIGCONV5)

!**** *APLPARSAD - ADJOINT OF CALLING THE SIMPLIFIED
!                  PHYSICAL PARAMETRIZATIONS.

!     Subject.
!     --------
!     - CALL THE ADJOINT OF SUBROUTINES OF THE SIMPLIFIED PHYSICAL
!       PARAMETRIZATIONS.

!**   Interface.
!     ----------
!        *CALL* *APLPARSAD*

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------

! - DIMENSIONAL PARAMETERS.

! KIDIA, KFDIA : START/END OF HORIZONTAL LOOP  (IST,IEND IN *CPG*).
! KLON : HORIZONTAL DIMENSION                  (NPROMA IN *CPG*).
! KTDIA : START OF THE VERTICAL LOOP IN THE PHYSICS (IF SOME LEVELS ARE
!                      SKIPPED AT THE TOP OF THE MODEL).
! KLEV  : END OF VERTICAL LOOP AND VERTICAL DIMENSION (NFLEVG IN *CPG*)
! KSGST : NUMBER OF SUBGRID SURFACE TEMPERATURES AND FLUXES
!                      (NTSSG IN *CPG*)

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PAPHI      : GEOPOTENTIAL ON HALF-LEVELS.
! PAPRS      : PRESSURE ON HALF-LEVELS.

!  The same but for trajectory
!  ---------------------------
! PAPHI5     : GEOPOTENTIAL ON HALF-LEVELS.
! PAPRS5     : PRESSURE ON HALF-LEVELS.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIAL ON FULL LEVELS.
! PAPRSF     : PRESSURE ON FULL LEVELS.
! PDELP      : LAYER THICKNESS IN PRESSURE UNITS.
! PRDELP     : INVERSE OF PDELP.
! PU         : X-COMPONENT OF WIND.
! PV         : Y-COMPONENT OF WIND.
! PT         : TEMPERATURE.
! PQ         : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQL        : LIQUID WATER.
! PQI        : ICE.
! PCP        : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PCVGQ      : CONVERGENCE OF HUMIDITY.

!  The same but for trajectory
!  ---------------------------
! PAPHIF5    : GEOPOTENTIAL ON FULL LEVELS.
! PAPRSF5    : PRESSURE ON FULL LEVELS.
! PDELP5     : LAYER THICKNESS IN PRESSURE UNITS.
! PRDELP5    : INVERSE OF PDELP.
! PU5        : X-COMPONENT OF WIND.
! PV5        : Y-COMPONENT OF WIND.
! PT5        : TEMPERATURE.
! PQ5        : SPECIFIC HUMIDITY OF WATER VAPOUR.
! PQL5       : LIQUID WATER.
! PQI5       : ICE.
! PCP5       : SPECIFIC HEAT AT CONSTANT PRESSURE FOR AIR.
! PCVGQ5     : CONVERGENCE OF HUMIDITY.

!  The trajectory
!  ---------------
! - 2D (0:KLEV)

! PKTROV5    : VERTICAL EXCHANGE COEFFICIENT OF T AND Q IN KG/(M*M*S).
! PKUROV5    : VERTICAL EXCHANGE COEFFICIENT OF U AND V IN KG/(M*M*S).
! PXTROV5    : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKTROV.
! PXUROV5    : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKUROV.

! - 1D

! PCDROV5    : PCD NORMALIZED BY WIND SPEED TIMES DENSITY.
! PCHROV5    : PCH NORMALIZED BY WIND SPEED TIMES DENSITY.
! PXDROV5    : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCDROV.
! PXHROV5    : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCHROV.

!2D (0:KLEV)
! PTRAJGWD5 : ZRAPP TAKEN FROM ACDRAG 

! -----------------
! - 1D (PROGNOSTIC QUANTITIES) .

! PSNS       : MASS OF SNOW PER UNIT SURFACE.
! PTS        : SURFACE LAYER TEMPERATURE.
! PQS        : SURFACE SPECIFIC HUMIDITY.

! - 1D (GEOGRAPHICAL DISTRIBUTION) .

! PGETRL     : STANDARD DEVIATION OF OROGRAPHY  (UNIT: J/KG).
! PLSM       : LAND/SEA MASK.
! PVRLAN     : ANISOTROPY OF THE RELIEF (1 = ISOTROPY).
! PVRLDI     : ANGLE OF MAXIMAL GRADIENT OF RELIEF WITH x-AXIS.
! PGZ0F      : GRAVITY * ROUGHNESS LENGTH (TIME VARIABLE).
! PALBF      : BACKGROUND SURFACE SHORTWAVE ALBEDO (SNOW-FREE).

! - 1D (DIAGNOSTIC QUANTITIES).

! PMU0       : LOCAL COSINE OF INSTANTENOUS SOLAR ZENITH ANGLE.

! - CONSTANTES

! PAC       : COURTIS MATRIX FOR THERMAL RADIATION FOR CLEAR SKY
! PCOR      : CORRECTION FOR CLOUDINESS FOR THERMAL RADIATION

! PRAB3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.  ---
! PRAB4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAB6C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAB3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | from bottom
! PRAB4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               | to given
! PRAB6N    : CLOUDY DIFFUSE REFLECTIVITY.              ---  level

! PRAT1C    : CLEAR-SKY PARALLEL TRANSMISSIVITY.        ---
! PRAT2C    : CLEAR-SKY PARALLEL/DIFFUSE TRANSMISSIVITY.   |
! PRAT3C    : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.     |
! PRAT4C    : CLEAR-SKY DIFFUSE TRANSMISSIVITY.            | equivalent
! PRAT5C    : CLEAR-SKY DIFFUSE REFLECTIVITY.              | coefficients
! PRAT1N    : CLOUDY PARALLEL TRANSMISSIVITY.              | from top
! PRAT2N    : CLOUDY PARALLEL/DIFFUSE TRANSMISSIVITY.      | to given
! PRAT3N    : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.        | level
! PRAT4N    : CLOUDY DIFFUSE TRANSMISSIVITY.               |
! PRAT5N    : CLOUDY DIFFUSE REFLECTIVITY.              ---

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS.
!     --------------------

! - THE NAMES OF THE PHYSICAL VARIABLES.

! - 2D (0:KLEV) .

! PDIFCQ     : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
! PDIFCS     : CONVECTIVE FLUX OF ENTHALPY (NOT RAIN/SNOW).
! PDIFTQ     : TURBULENT FLUX (INC. Q NEGATIVE) OF SPECIFIC HUMIDITY.
! PDIFTS     : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY).
! PFCCQN     : CONVECTIVE CONDENSATION FLUX FOR ICE.
! PFCCQL     : CONVECTIVE CONDENSATION FLUX FOR LIQUID WATER.
! PFPLCL     : CONVECTIVE PRECIPITATION AS RAIN.
! PFPLCN     : CONVECTIVE PRECIPITATION AS SNOW.
! PFPLSL     : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN     : STRATIFORM PRECIPITATION AS SNOW.
! PFRSO      : SHORTWAVE RADIATIVE FLUX.
! PFRTH      : LONGWAVE RADIATIVE FLUX.
! PFCSQN     : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL     : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PSTRCU     : CONVECTIVE FLUX OF MOMENTUM "U".
! PSTRCV     : CONVECTIVE FLUX OF MOMENTUM "V".
! PSTRDU     : GRAVITY WAVE DRAG FLUX "U".
! PSTRDV     : GRAVITY WAVE DRAG FLUX "V".
! PSTRTU     : TURBULENT FLUX OF MOMENTUM "U".
! PSTRTV     : TURBULENT FLUX OF MOMENTUM "V".
! PSTRMU     : MESOSPHERIC FLUX FOR "U"-MOMENTUM.
! PSTRMV     : MESOSPHERIC FLUX FOR "V"-MOMENTUM.
! PFRMH      : MESOSPHERIC ENTHALPY FLUX.
! PQS        : SURFACE SPECIFIC HUMIDITY.

!  The same but for trajectory
!  ---------------------------
! PDIFCQ5    : CONVECTIVE FLUX OF SPECIFIC HUMIDITY (NOT RAIN/SNOW).
! PDIFCS5    : CONVECTIVE FLUX OF ENTHALPY (NOT RAIN/SNOW).
! PDIFTQ5    : TURBULENT FLUX (INC. Q NEGATIVE) OF SPECIFIC HUMIDITY.
! PDIFTS5    : TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY).
! PFCCQN5    : CONVECTIVE CONDENSATION FLUX FOR ICE.
! PFCCQL5    : CONVECTIVE CONDENSATION FLUX FOR LIQUID WATER.
! PFPLCL5    : CONVECTIVE PRECIPITATION AS RAIN.
! PFPLCN5    : CONVECTIVE PRECIPITATION AS SNOW.
! PFPLSL5    : STRATIFORM PRECIPITATION AS RAIN.
! PFPLSN5    : STRATIFORM PRECIPITATION AS SNOW.
! PFRSO5     : SHORTWAVE RADIATIVE FLUX.
! PFRTH5     : LONGWAVE RADIATIVE FLUX.
! PFCSQN5    : STRATIFORM CONDENSATION FLUX FOR ICE.
! PFCSQL5    : STRATIFORM CONDENSATION FLUX FOR LIQUID WATER.
! PSTRCU5    : CONVECTIVE FLUX OF MOMENTUM "U".
! PSTRCV5    : CONVECTIVE FLUX OF MOMENTUM "V".
! PSTRDU5    : GRAVITY WAVE DRAG FLUX "U".
! PSTRDV5    : GRAVITY WAVE DRAG FLUX "V".
! PSTRTU5    : TURBULENT FLUX OF MOMENTUM "U".
! PSTRTV5    : TURBULENT FLUX OF MOMENTUM "V".
! PSTRMU5    : MESOSPHERIC FLUX FOR "U"-MOMENTUM.
! PSTRMV5    : MESOSPHERIC FLUX FOR "V"-MOMENTUM.
! PFRMH5     : MESOSPHERIC ENTHALPY FLUX.

!-----------------------------------------------------------------------

! -   IMPLICIT ARGUMENTS.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------
!     - ADJOINT OF THE INITIALIZATION OF THE PHYSICAL FLUXES.
!     - CALL THE ADJOINT OF PHYSICS SUBROUTINES OF SIMPLIFIED
!       PHYSICAL PARAMETRIZATIONS.

!     Author.
!     -------
!      96-01-18 M.Janiskova

!     Modifications.
!     --------------
!      01-03, mesospheric drag - F. Bouyssel
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      O.Riviere Nov 09 Add PTRAJGWD5 for LGWDSPNL option
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      O.Riviere Feb 11: add QL/QI in TL/AD
!      F. Kocaman 2011-10-04 : CY38 cleaning
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSGST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRDELP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PU5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PV5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVGQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCVGQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS1(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS1(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGETRL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0F(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRLAN(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVRLDI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAC(KLEV+1,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCOR(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB3N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB4N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAB6N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT1N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT2N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT3N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT4N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5C(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAT5N(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCQ5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCS5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTS5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCCQL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCCQN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCSQN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSO5(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRTH5(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCU5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRDU5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRDV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTU5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKTROV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PKUROV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXTROV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXUROV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCDROV5(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCHROV5(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXDROV5(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PXHROV5(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTRAJGWD5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZQR5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZQS5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZQL5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZQI5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZLIQCVPPKF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PZNEBSCVPPKF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRMU5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRMV5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRMH5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFCS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFTS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCCQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFCSQN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLCN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFRSO(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFRTH(KLON,0:KLEV,KSGST+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRCV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRDU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRDV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRTV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRMU(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRMV(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFRMH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTRIGCONV5(KLON) 

!     ------------------------------------------------------------------

! - 2D (0:KLEV) .

! ZKTROV     : VERTICAL EXCHANGE COEFFICIENT OF T AND Q IN KG/(M*M*S).
! ZKUROV     : VERTICAL EXCHANGE COEFFICIENT OF U AND V IN KG/(M*M*S).
! ZXTROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKTROV.
! ZXUROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PKUROV.
! ZFPLC      : SUM OF CONVECTIVE PRECIPITATION AS RAIN AND AS SNOW

! The same but for trajectory
! ---------------------------
! ZFPLC5     : SUM OF CONVECTIVE PRECIPITATION AS RAIN AND AS SNOW

! - 1D (DIAGNOSTIC) .

! ZCDROV     : PCD NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZCHROV     : PCH NORMALIZED BY WIND SPEED TIMES DENSITY.
! ZXDROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCDROV.
! ZXHROV     : "ANTI-FIBRILLATION" MULTIPLICATOR OF PCHROV.
! ZNEIJ      : FRACTION OF SOIL COVERED BY SNOW.
! ZFCLL      : LATENT HEAT FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFCLN      : LATENT HEAT FLUX OVER SNOW (OR ICE).
! ZFCS       : SENSIBLE HEAT FLUX AT SURFACE LEVEL.
! ZFEVL      : WATER VAPOR FLUX OVER LIQUID WATER (OR WET SOIL).
! ZFEVN      : WATER VAPOR FLUX OVER SNOW (OR ICE).
! ZLHS       : LATENT HEAT AT THE SURFACE.
! ZALB       : MODEL SURFACE SHORT ALBEDO.

! - 1D (GEOGRAPHIC) .

! ZGZ0       : GRAVITY * ROUGHNESS LENGTH.

REAL(KIND=JPRB) :: ZXTROV(KLON,0:KLEV),ZXUROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZKTROV(KLON,0:KLEV),ZKUROV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZFPLC (KLON,0:KLEV)

REAL(KIND=JPRB) :: ZXDROV(KLON),ZXHROV(KLON),ZCDROV(KLON),ZCHROV(KLON),&
 & ZGZ0(KLON),ZNEIJ(KLON),&
 & ZLHS(KLON),ZFEVL(KLON),ZFEVN(KLON),&
 & ZFCLL(KLON),ZFCLN(KLON),ZFCS(KLON),&
 & ZALB(KLON)


REAL(KIND=JPRB) :: ZPR (KLON,KLEV),ZPR5(KLON,KLEV)

REAL(KIND=JPRB)   :: ZDPTS(KLON) 


REAL(KIND=JPRB) :: ZNTS(KLON),ZTSDUM(KLON),ZERO1(KLON,KLEV)

REAL(KIND=JPRB) :: ZFPLC5(KLON,0:KLEV)
INTEGER(KIND=JPIM) :: IJN, JLEV, JLON

REAL(KIND=JPRB) :: Z0CR, Z0CRIW
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "acconvad.intfb.h"
#include "acconvsad.intfb.h"
#include "acdifspad.intfb.h"
#include "acdraglad.intfb.h"
#include "acdrmead.intfb.h"
#include "acqwlsrad.intfb.h"
#include "acradsad.intfb.h"
#include "actsecad.intfb.h"
#include "aclspsad.intfb.h"


!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('APLPARSAD',0,ZHOOK_HANDLE)
ASSOCIATE(YDVAB=>YDGEOMETRY%YRVAB,YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE,YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP,YDCSGLEG=>YDGEOMETRY%YRCSGLEG,YDVSPLIP=>YDGEOMETRY%YRVSPLIP,YDVSLETA=>YDGEOMETRY%YRVSLETA, &
 & YDHSLMER=>YDGEOMETRY%YRHSLMER, &
 & YDCSGEOM=>YDGEOMETRY%YRCSGEOM,YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB,YDGSGEOM=>YDGEOMETRY%YRGSGEOM, &
 & YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, &
 & YDSPGEOM=>YDGEOMETRY%YSPGEOM, &
 & YDPHY=>YDML_PHY_MF%YRPHY,YDTOPH=>YDML_PHY_MF%YRTOPH,YDSIMPHL=>YDML_PHY_MF%YRSIMPHL, &
 & YDPHY1=>YDML_PHY_MF%YRPHY1,YDPHY0=>YDML_PHY_MF%YRPHY0)

ASSOCIATE(TEQK=>YDPHY0%TEQK, &
 & ALCRIN=>YDPHY1%ALCRIN, ALRCN2=>YDPHY1%ALRCN2, ALRCN1=>YDPHY1%ALRCN1, &
 & WCRIN=>YDPHY1%WCRIN, &
 & RMESOT=>YDTOPH%RMESOT, RMESOU=>YDTOPH%RMESOU, &
 & LCVRASBM=>YDSIMPHL%LCVRASBM, LPROCLDTL=>YDSIMPHL%LPROCLDTL, &
 & LSTRASP=>YDSIMPHL%LSTRASP, LSTRASPN=>YDSIMPHL%LSTRASPN, &
 & LRRMESSP=>YDSIMPHL%LRRMESSP, LGWDSP=>YDSIMPHL%LGWDSP, &
 & LVDIFSP=>YDSIMPHL%LVDIFSP, LCVRASP=>YDSIMPHL%LCVRASP, &
 & LRAYSP=>YDSIMPHL%LRAYSP, &
 & LNEIGE=>YDPHY%LNEIGE, STTEM=>YDSTA%STTEM)
!-----------------------------------------------------------------------

!*
!**
!     -------------------------------------------------------------------
!     0. 1 - Some initialization and set variables to zero in the case of
!            of non-calling parametrizations.
!     -------------------------------------------------------------------

IJN=KLON

PDIFCQ5(:,:) = 0.0_JPRB
PDIFCS5(:,:) = 0.0_JPRB
PDIFTS5(:,:) = 0.0_JPRB

IF (.NOT.(LVDIFSP.AND.LCVRASP )) THEN
  PDIFTQ5(:,:) = 0.0_JPRB
ENDIF
IF (.NOT.(LRAYSP.AND.LCVRASP)) THEN
  PFPLCL5(:,:) = 0.0_JPRB
  PFPLCN5(:,:) = 0.0_JPRB
ENDIF

PFPLSL5(:,:) = 0.0_JPRB
PFPLSN5(:,:) = 0.0_JPRB
PFCCQN5(:,:) = 0.0_JPRB
PFCCQL5(:,:) = 0.0_JPRB
PFCSQN5(:,:) = 0.0_JPRB
PFCSQL5(:,:) = 0.0_JPRB
PSTRCU5(:,:) = 0.0_JPRB
PSTRCV5(:,:) = 0.0_JPRB
PSTRDU5(:,:) = 0.0_JPRB
PSTRDV5(:,:) = 0.0_JPRB
PSTRTU5(:,:) = 0.0_JPRB
PSTRTV5(:,:) = 0.0_JPRB
PSTRMU5(:,:) = 0.0_JPRB
PSTRMV5(:,:) = 0.0_JPRB
PFRMH5 (:,:) = 0.0_JPRB

PFRSO5(:,:,:) = 0.0_JPRB
PFRTH5(:,:,:) = 0.0_JPRB

!*
!     -------------------------------------------------------------------
!     0. 2 - Computation of fraction of soil covered by snow
!            and roughness
!     -------------------------------------------------------------------
DO JLON=KIDIA,KFDIA

!     Computations concerning the presence or absence of snow on ground.

  Z0CR=YDCST%RG*ALRCN1
  Z0CRIW=1.0_JPRB/(YDCST%RG*ALRCN2)

  IF (LNEIGE) THEN
    ZNEIJ(JLON)=PLSM(JLON)*PSNS(JLON)/(PSNS(JLON)+WCRIN)
    ZGZ0(JLON)=PGZ0F(JLON)+(Z0CR-PGZ0F(JLON))*PLSM(JLON)*&
     & PSNS(JLON)&
     & /(PSNS(JLON)+WCRIN+Z0CRIW*PGZ0F(JLON))  
  ELSE
    ZNEIJ(JLON)=0.0_JPRB
    ZGZ0(JLON)=PGZ0F(JLON)
  ENDIF

ENDDO

!*
!     ------------------------------------------------------------------
!     0. 5 - Computation of convective precipitations, deep convective
!            scheme
!     ------------------------------------------------------------------

!        If (LCVRASP and LVDIFSP) then the multiplication of dynamic
!        tendency of q by correction factor of local resolution of
!        the model and by argument of the evaporation of the surface
!        is already done in APLPARSADT

!*
!     -------------------------------------------------------------------
!     0. 7 - Computation of radiative fluxes
!     -------------------------------------------------------------------

IF (LRAYSP) THEN
  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZFPLC5(JLON,JLEV)=PFPLCL5(JLON,JLEV)+PFPLCN5(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!     INITIALIZATION OF THE LOCAL VARIABLES

ZKTROV(:,:) = 0.0_JPRB
ZKUROV(:,:) = 0.0_JPRB
ZXTROV(:,:) = 0.0_JPRB
ZXUROV(:,:) = 0.0_JPRB
ZFPLC (:,:) = 0.0_JPRB

ZCDROV(:)= 0.0_JPRB
ZCHROV(:)= 0.0_JPRB
ZXDROV(:)= 0.0_JPRB
ZXHROV(:)= 0.0_JPRB
ZFCLL(:) = 0.0_JPRB
ZFCLN(:) = 0.0_JPRB
ZFCS(:)  = 0.0_JPRB
ZFEVL(:) = 0.0_JPRB
ZFEVN(:) = 0.0_JPRB
ZLHS(:)  = 0.0_JPRB




ZDPTS(:)=0.0_JPRB

!*
!     ------------------------------------------------------------------
!     IX - COMPUTATION OF THE FLUXES OF ENTHALPY AND SENSIBLE HEAT
!          (THE FUNCTION OF THE FLUXES OF PRECIPITATIONS AND
!           CONDENSATION)
!     ------------------------------------------------------------------

!      - IF IT IS NOT LIQUID WATER, THEN
!                   THE FLUX OF CONDENSATION = THE FLUX OF PRECIPITATION

IF (.NOT.LSTRASPN) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      PFPLSN(JLON,JLEV)=PFPLSN(JLON,JLEV)+PFCSQN(JLON,JLEV)
      PFCSQN(JLON,JLEV)=0.0_JPRB

      PFPLSL(JLON,JLEV)=PFPLSL(JLON,JLEV)+PFCSQL(JLON,JLEV)
      PFCSQL(JLON,JLEV)=0.0_JPRB

      PFPLCN(JLON,JLEV)=PFPLCN(JLON,JLEV)+PFCCQN(JLON,JLEV)
      PFCCQN(JLON,JLEV)=0.0_JPRB

      PFPLCL(JLON,JLEV)=PFPLCL(JLON,JLEV)+PFCCQL(JLON,JLEV)
      PFCCQL(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     VIII - COMPUTES THE MOMENTUM FLUX DUE TO A MESOSPHERIC DRAG AND 
!            THE HEAT FLUX CORRESPONDING TO A RELAXATION OF TEMPERATURE 
!            TOWARDS THE STANDARD ATMOSPHERE
!     ------------------------------------------------------------------

IF ( LRRMESSP ) THEN
  CALL ACDRMEAD(YDML_PHY_MF%YRPHY2,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PCP5,PDELP5,PT5,PU5,PV5,&
   & PCP,PDELP,PT,PU,PV,&
   & PFRMH5,PSTRMU5,PSTRMV5,&
   & PFRMH,PSTRMU,PSTRMV,&
   & RMESOT,RMESOU,STTEM)
ENDIF

!*
!     -------------------------------------------------------------------
!     VII - COMPUTATION OF RADIATIVE FLUXES
!     -------------------------------------------------------------------

IF (LRAYSP) THEN

!        ALBEDO AND EMISSIVITY WITH SNOW

  DO JLON=KIDIA,KFDIA
    ZALB(JLON)=PALBF(JLON)-ZNEIJ(JLON)*(PALBF(JLON)-MAX(PALBF(JLON),ALCRIN))
  ENDDO

!        COMPUTATION OF THE FLUXES

  CALL ACRADSAD(YDGEOMETRY%YRVAB,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,IJN,&
   & PT5,PAPRSF5,PQ5,PDELP5,PRDELP5,ZFPLC5,&
   & PT,PAPRSF,PQ,PDELP,PRDELP,ZFPLC,&
   & ZALB,PMU0,PTS,&
   & PAC,PCOR,&
   & PRAB3C,PRAB3N,PRAB4C,PRAB4N,PRAB6C,PRAB6N,&
   & PRAT1C,PRAT1N,PRAT2C,PRAT2N,PRAT3C,PRAT3N,&
   & PRAT4C,PRAT4N,PRAT5C,PRAT5N,&
   & PFRSO5,PFRTH5,&
   & PFRSO,PFRTH)

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      PFPLCL(JLON,JLEV)=PFPLCL(JLON,JLEV)+ZFPLC(JLON,JLEV)
      PFPLCN(JLON,JLEV)=PFPLCN(JLON,JLEV)+ZFPLC(JLON,JLEV)
      ZFPLC(JLON,JLEV)=0.0_JPRB
    ENDDO
  ENDDO

ENDIF

!*
!     ------------------------------------------------------------------
!     VI - COMPUTATION OF WET-BULB CHARACTERISTICS AND STRATIFORM
!          PRECIPITATION FLUXES
!     ------------------------------------------------------------------
IF ( LSTRASP ) THEN
  CALL ACQWLSRAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PQ5,PT5,PAPRSF5,PDELP5,&
   & PQ,PT,PAPRSF,PDELP,&
   & PFPLSL5,PFPLSN5,&
   & PFPLSL,PFPLSN)
ENDIF

IF(LPROCLDTL) THEN
   PZQL5(:,:)=PQL5(:,:)
   PZQI5(:,:)=PQI5(:,:)
ENDIF



IF(LSTRASPN) THEN
   ZPR5(:,:)=YDCST%RD+(YDCST%RV-YDCST%RD)*PQ5(:,:)
   ZPR(KIDIA:KFDIA,KTDIA:KLEV)=0.0_JPRB
   CALL ACLSPSAD(YDCST,YDGEOMETRY,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   &PQ5,PZQL5,PZQI5,PZQR5,PZQS5,PZLIQCVPPKF,PZNEBSCVPPKF,&
   &PT5,PDELP5,PAPRSF5,PCP5,ZPR5,PAPHI5,PTS,PGM,&
   &PQ,PQL,PQI,PT,PDELP,PAPRSF,PCP,ZPR,PAPHI,ZDPTS,PLSM,&
   &PFCSQL5,PFCSQN5,PFPLSL5,PFPLSN5,&
   &PFCSQL,PFCSQN,PFPLSL,PFPLSN)
ENDIF



!*
!     ------------------------------------------------------------------
!     V - COMPUTING CONVECTIVE PRECIPITATIONS, DEEP CONVECTIVE SCHEME
!     ------------------------------------------------------------------

IF (LCVRASBM)  THEN
  ZERO1(:,:)=0.0 

  CALL ACCONVSAD(YDML_PHY_MF%YRPHY2,YDSIMPHL,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
  &PAPHIF5,PAPRS5,PAPRSF5,PCVGQ5,&
  &PDELP5,PQ5,PRDELP5,PT5,PU5,PV5,&
  &PAPHIF,PAPRS,PAPRSF,PCVGQ,&
  &ZERO1,PQ,ZERO1,PT,PU,PV,&!TOTO1 AU LIEU DE PDELP et PRDELP !!
  &PTS,ZTSDUM,&
  &PDIFCQ5,PDIFCS5,PFPLCL5,PFPLCN5,PSTRCU5,PSTRCV5,&
  &PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV,PTRIGCONV5)
ENDIF




IF ( LCVRASP ) THEN
  CALL ACCONVAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHIF5,PAPRS5,PAPRSF5,PCVGQ5,&
   & PDELP5,PQ5,PRDELP5,PT5,PU5,PV5,&
   & PAPHIF,PAPRS,PAPRSF,PCVGQ,&
   & PDELP,PQ,PRDELP,PT,PU,PV,&
   & PTS,&
   & PDIFCQ5,PDIFCS5,PFPLCL5,PFPLCN5,&
   & PSTRCU5,PSTRCV5,&
   & PDIFCQ,PDIFCS,PFPLCL,PFPLCN,PSTRCU,PSTRCV)
ENDIF




IF ( LVDIFSP.AND.LCVRASP ) THEN
!        THE DYNAMIC TENDENCY OF Q IS MULTIPLIED BY CORRECTION FACTOR
!        OF LOCAL RESOLUTION OF THE MODEL AND BY ARGUMENT OF THE
!        EVAPORATION OF THE SURFACE

  DO JLEV=KLEV,KTDIA,-1
    DO JLON=KIDIA,KFDIA
      PRDELP(JLON,JLEV)=PRDELP(JLON,JLEV)-YDCST%RG*(PDIFTQ5(JLON,JLEV)&
       & -PDIFTQ5(JLON,JLEV-1))*PCVGQ(JLON,JLEV)  
      PDIFTQ(JLON,JLEV)=PDIFTQ(JLON,JLEV)&
       & -YDCST%RG*PRDELP5(JLON,JLEV)*PCVGQ(JLON,JLEV)  
      PDIFTQ(JLON,JLEV-1)=PDIFTQ(JLON,JLEV-1)&
       & +YDCST%RG*PRDELP5(JLON,JLEV)*PCVGQ(JLON,JLEV)  
      PCVGQ(JLON,JLEV)=PCVGQ(JLON,JLEV)/(1.0_JPRB+PGM(JLON)*TEQK)
    ENDDO
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     IV - COMPUTATION OF OROGRAPHIC GRAVITY WAVE DRAG
!     ------------------------------------------------------------------
IF ( LGWDSP ) THEN
  CALL ACDRAGLAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI5,PAPHIF5,PAPRS5,PAPRSF5,PDELP5,PRDELP5,&
   & PU5,PV5,PT5,PQ5,&
   & PAPHI,PAPHIF,PAPRS,PAPRSF,PDELP,PRDELP,&
   & PU,PV,PT,PQ,&
   & PGETRL,PVRLAN,PVRLDI,PTS,PQS,&
   & PSTRDU5,PSTRDV5,&
   & PSTRDU,PSTRDV,PTRAJGWD5)
ENDIF

!*
!     ------------------------------------------------------------------
!     III - COMPUTATION OF VERTICAL TURBULENT EXCHANGE COEFFICIENTS
!           AND SURFACE EXCHANGE COEFFICIENTS AND COMPUTATION OF
!           VERTICAL TURBULENT DIFFUSION
!     ------------------------------------------------------------------
IF ( LVDIFSP) THEN
  DO JLON=KIDIA, KFDIA

    ZNTS(JLON)=PTS1(JLON)

  ENDDO

  CALL ACDIFSPAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI5,PAPHIF5,PDELP5,PRDELP5,PT5,PQ5,&
   & PU5,PV5,PKTROV5,PKUROV5,PXTROV5,PXUROV5,&
   & PAPHI,PAPHIF,PDELP,PRDELP,PT,PQ,&
   & PU,PV,ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & PCDROV5,PCHROV5,PXDROV5,PXHROV5,&
   & ZCDROV,ZCHROV,ZXDROV,ZXHROV,&
   & PLSM,ZNEIJ,PQS,PTS,ZNTS,PTS1,PQS1,&
   & PDIFTQ5,PDIFTS5,PSTRTU5,PSTRTV5,&
   & PDIFTQ,PDIFTS,PSTRTU,PSTRTV,&
   & ZFCLL,ZFCLN,ZFCS,ZFEVL,ZFEVN,ZLHS)

  CALL ACTSECAD(YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PAPHI5,PAPHIF5,PAPRS5,PAPRSF5,PQ5,PT5,PU5,PV5,&
   & PAPHI,PAPHIF,PAPRS,PAPRSF,PQ,PT,PU,PV,&
   & PQS,PTS,ZGZ0,&
   & PKTROV5,PKUROV5,&
   & ZKTROV,ZKUROV,ZXTROV,ZXUROV,&
   & PCDROV5,PCHROV5,&
   & ZCDROV,ZCHROV,ZXDROV,ZXHROV)

ENDIF

!*
!     ------------------------------------------------------------------
!     I - SET VARIABLES TO ZERO IN THE CASE OF NON-CALLING
!         PARAMETRIZATIONS.
!     ------------------------------------------------------------------

PDIFCQ(:,:) = 0.0_JPRB
PDIFCS(:,:) = 0.0_JPRB
PDIFTQ(:,:) = 0.0_JPRB
PDIFTS(:,:) = 0.0_JPRB
PFPLCL(:,:) = 0.0_JPRB
PFPLCN(:,:) = 0.0_JPRB
PFPLSL(:,:) = 0.0_JPRB
PFPLSN(:,:) = 0.0_JPRB
PFCCQN(:,:) = 0.0_JPRB
PFCCQL(:,:) = 0.0_JPRB
PFCSQN(:,:) = 0.0_JPRB
PFCSQL(:,:) = 0.0_JPRB
PSTRCU(:,:) = 0.0_JPRB
PSTRCV(:,:) = 0.0_JPRB
PSTRDU(:,:) = 0.0_JPRB
PSTRDV(:,:) = 0.0_JPRB
PSTRTU(:,:) = 0.0_JPRB
PSTRTV(:,:) = 0.0_JPRB
PSTRMU(:,:) = 0.0_JPRB
PSTRMV(:,:) = 0.0_JPRB
PFRMH (:,:) = 0.0_JPRB

PFRSO(:,:,:) = 0.0_JPRB
PFRTH(:,:,:) = 0.0_JPRB

ZFCLL(:) = 0.0_JPRB
ZFCLN(:) = 0.0_JPRB
ZFCS(:)  = 0.0_JPRB
ZFEVL(:) = 0.0_JPRB
ZFEVN(:) = 0.0_JPRB
ZLHS(:)  = 0.0_JPRB

!*
!     ------------------------------------------------------------------
!     0. 8 - Computation of the fluxes of enthalpy and sensible heat
!            (the function of the fluxes of precipitations and
!             condensation)
!     ------------------------------------------------------------------

!      - If it is not liquid water then
!                   the flux of condensation = the flux of precipitation

IF (.NOT.LSTRASPN) THEN
  DO JLEV = KTDIA, KLEV
    DO JLON = KIDIA, KFDIA
      PFCCQL5(JLON,JLEV) = PFPLCL5(JLON,JLEV)
      PFCCQN5(JLON,JLEV) = PFPLCN5(JLON,JLEV)
      PFCSQL5(JLON,JLEV) = PFPLSL5(JLON,JLEV)
      PFCSQN5(JLON,JLEV) = PFPLSN5(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('APLPARSAD',1,ZHOOK_HANDLE)
END SUBROUTINE APLPARSAD
