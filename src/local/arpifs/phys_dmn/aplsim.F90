#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE APLSIM(YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, YDGEOMETRY, YDCPG_DIM, YDCPG_MISC, &
         & YDCPG_PHY0, YDMF_PHYS, YDCPG_DYN0, YDMF_PHYS_SURF, YDVARS, YDMODEL, PDTPHY, PTRAJ_PHYS)

!**** *APLSIM* METEO-FRANCE PHYSICS.

!     Purpose.
!     --------
!         Call METEO-FRANCE physics and physical tendencies.

!**   Interface.
!     ----------
!        *CALL* *APLSIM(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        PDTPHY    : timestep used in the physics.

! End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_DYN_TYPE, CPG_PHY_TYPE, &
                              & CPG_MISC_TYPE
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL
USE PARKIND1           , ONLY : JPIM     ,JPRB
USE YOMHOOK            , ONLY : LHOOK,   DR_HOOK

USE YOMCT0             , ONLY : LTWOTL
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TYPE, LPRTTRAJ
USE YOMLUN             , ONLY : NULOUT

USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE
USE MF_PHYS_NEXT_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_NEXT_STATE_TYPE
!     -------------------------------------------------------------------------

IMPLICIT NONE

TYPE (MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE
TYPE (MF_PHYS_NEXT_STATE_TYPE), INTENT(INOUT) :: YDMF_PHYS_NEXT_STATE
TYPE(GEOMETRY),                 INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),             INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_MISC_TYPE),            INTENT(INOUT) :: YDCPG_MISC
TYPE(CPG_PHY_TYPE),             INTENT(IN)    :: YDCPG_PHY0
TYPE(MF_PHYS_TYPE),             INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),             INTENT(IN)    :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),        INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),          INTENT(INOUT) :: YDVARS
TYPE(MODEL),                    INTENT(IN)    :: YDMODEL
REAL(KIND=JPRB),                INTENT(IN)    :: PDTPHY 
TYPE (TRAJ_PHYS_TYPE),          INTENT(INOUT), OPTIONAL :: PTRAJ_PHYS

!     ------------------------------------------------------------------
INTEGER(KIND=JPIM) :: IFIELDSS

INTEGER(KIND=JPIM) :: INSTEP_DEB,INSTEP_FIN
INTEGER(KIND=JPIM) :: JROF

!     --- UPPER AIR PHYSICAL TENDENCIES.
REAL(KIND=JPRB) :: ZTENDH(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)     ! Enthalpy tendency.
REAL(KIND=JPRB) :: ZTENDQ(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)     ! Moisture tendency.

!     --- RADIATION COEFFICIENTS FOR SIMPLIFIED PHYSICS IN GRID-POINT ---
REAL(KIND=JPRB) :: ZAC_HC(YDCPG_DIM%KFLEVG+1,YDCPG_DIM%KFLEVG+1)           ! horizontally-constant field for ZAC.

REAL(KIND=JPRB) :: ZCOR   (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB3C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB3N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB4C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB4N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB6C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB6N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT1C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT1N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT2C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT2N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT3C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT3N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT4C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT4N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT5C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT5N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZQS1   (YDCPG_DIM%KLON)

REAL (KIND=JPRB) :: ZRDG_CVGQ (YDCPG_DIM%KLON, 1:YDCPG_DIM%KFLEVG)
REAL (KIND=JPRB) :: ZRDG_MMU0 (YDCPG_DIM%KLON)
REAL (KIND=JPRB) :: ZRDG_MU0LU (YDCPG_DIM%KLON)
REAL (KIND=JPRB) :: ZRDG_MU0M (YDCPG_DIM%KLON)
REAL (KIND=JPRB) :: ZRDG_MU0N (YDCPG_DIM%KLON)
REAL (KIND=JPRB) :: ZRDG_MU0 (YDCPG_DIM%KLON)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "aplpars.intfb.h"
#include "aplpassh.intfb.h"
#include "cpphinp.intfb.h"
#include "cppsolan.intfb.h"
#include "cptendsm.intfb.h"
#include "cputqys.intfb.h"
#include "rdradcoef.intfb.h"
#include "wrphtrajm.intfb.h"
#include "mf_phys_transfer.intfb.h"
#include "mf_phys_cvv.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('APLSIM', 0, ZHOOK_HANDLE)
ASSOCIATE(  YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY, YDSIMPHL=>YDMODEL%YRML_PHY_MF%YRSIMPHL, YDMDDH=>YDMODEL%YRML_DIAG%YRMDDH, &
& YDRCOEF=>YDMODEL%YRML_PHY_RAD%YRRCOEF, YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY, YDLDDH=>YDMODEL%  YRML_DIAG%YRLDDH,         &
& YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2, YDSTOPH=>YDMODEL%YRML_PHY_STOCH%YRSTOPH)

ASSOCIATE(NTSSG=>YDDPHY%NTSSG, LTRAJPS=>YDSIMPHL%LTRAJPS, LRAYSP=>YDSIMPHL%LRAYSP, LEDR=>YDPHY%LEDR,               &
& LSDDH=>YDLDDH%LSDDH, HDSF=>YDMDDH%HDSF, LRCOEF =>YDRCOEF%LRCOEF, LTLADDIA=>YDRCOEF%LTLADDIA, NG3SR=>YDRCOEF%NG3SR&
& )

!     ------------------------------------------------------------------

!        1.    Preliminary calculations necessary
!              for all types of physics.
!              ------------------------------------


INSTEP_DEB=1
INSTEP_FIN=1

!=PARALLEL

IF (LRAYSP.AND.(YDCPG_DIM%KSTEP >= INSTEP_DEB .AND. YDCPG_DIM%KSTEP <= INSTEP_FIN)) THEN  
  CALL CPPSOLAN(YDMODEL%YRCST, YDCPG_DIM%KLON, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDVARS%GEOMETRY%GEMU%T0, YDVARS%GEOMETRY%GELAM%T0, &
  & ZRDG_MMU0)
  IF (.NOT.LSDDH) THEN
!-----------INITIALIZING THE WEIGHT VECTORS-------------
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDCPG_MISC%DHSF(JROF)=HDSF(JROF+YDCPG_DIM%KSTGLO-1)
    ENDDO
!-------------------------------------------------------
  ENDIF
ENDIF

CALL CPPHINP(YDGEOMETRY, YDMODEL, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDVARS%GEOMETRY%GEMU%T0, YDVARS%GEOMETRY%GELAM%T0,   &
& YDVARS%U%T0, YDVARS%V%T0, YDVARS%Q%T0, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%CVGQ%DL, YDVARS%CVGQ%DM, YDCPG_PHY0%XYB%RDELP, &
& YDCPG_DYN0%CTY%EVEL, YDVARS%CVGQ%T0, ZRDG_MU0, ZRDG_MU0LU, ZRDG_MU0M, ZRDG_MU0N, ZRDG_CVGQ)

!=END PARALLEL

! * In some cases, some pseudo-historic surface buffers (like z0) should
!   not be modified between the entrance and the output of APLSIM
!   (this is the case for example if LDCONFX=T).
!   For the time being, we must save:
!   - HV (group VV) : resistance to evapotranspiration
!   - Z0F (group VD): gravity * surface roughness length
!   - Z0H (group VV): gravity * roughness length for heat
!   - PBLH (group VH): PBL height
!   - SPSH (group VH):
!   - QSH (group VH):

!        2.9  Computation of evolution of T, u, v and Q.
!             ------------------------------------------

!       2.9b Prognostic convection etc.
!            --------------------------


! TRANSFER NOT ADVECTED VARIABLES INTO PGFLT1
CALL MF_PHYS_TRANSFER (YDCPG_DIM, YDVARS, YDMODEL)

!=PARALLEL

CALL MF_PHYS_CVV (YDCPG_DIM, YDVARS%CVV%T0, YDVARS%CVV%T1, YDMODEL)

!=END PARALLEL

!    -------------------------------------------------------------------

!*       3.    Simplified physics.
!              -------------------

!        3.1  Preliminary calculations necessary for simplified physics.
!             ----------------------------------------------------------


!=SKIP

  ! * read grid-point transmission coefficients for simplified physics.
IF (LRAYSP.AND.LRCOEF.AND.(YDCPG_DIM%KSTEP > 1.OR.LTLADDIA)) THEN
  IFIELDSS=NG3SR*YDCPG_DIM%KFLEVG
  CALL RDRADCOEF(YDGEOMETRY, YDRCOEF, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KSTGLO, IFIELDSS, &
  & ZCOR, ZRAB3C, ZRAB3N, ZRAB4C, ZRAB4N, ZRAB6C, ZRAB6N, ZRAT1C, ZRAT1N, ZRAT2C, ZRAT2N, ZRAT3C,   &
  & ZRAT3N, ZRAT4C, ZRAT4N, ZRAT5C, ZRAT5N, ZAC_HC)
ENDIF

!=END SKIP


!        3.2  Simplified physics.
!             -------------------

!=PARALLEL

CALL APLPASSH (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_PHY_MF%YRPHY1, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, &
& YDMF_PHYS_BASE_STATE%Q, YDMF_PHYS_SURF%GSP_SG%PF_T1, YDMF_PHYS_SURF%GSP_RR%PT_T1, YDMF_PHYS_SURF%GSP_RR%PW_T1,                  &
& YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_SURF%GSD_VF%PVEG, ZQS1)  

CALL APLPASSH (YDMODEL%YRCST, YDMODEL%YRML_PHY_MF%YRPHY, YDMODEL%YRML_PHY_MF%YRPHY1, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, &
& YDMF_PHYS_BASE_STATE%Q, YDMF_PHYS_BASE_STATE%YGSP_SG%F, YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDMF_PHYS_BASE_STATE%YGSP_RR%W,         &
& YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_SURF%GSD_VF%PVEG, YDCPG_MISC%QS)

!=END PARALLEL

!=PARALLEL

CALL APLPARS(YDGEOMETRY, YDRCOEF, YDMODEL%YRCST, YDMODEL%YRML_PHY_MF, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, 1, YDCPG_DIM%KFLEVG,           &
& NTSSG, YDCPG_DIM%KSTEP, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF,  &
& YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYDF, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP,               &
& YDMF_PHYS_BASE_STATE%U, YDMF_PHYS_BASE_STATE%V, YDMF_PHYS_BASE_STATE%T, YDMF_PHYS_BASE_STATE%Q, YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP, &
& ZRDG_CVGQ, YDMF_PHYS_BASE_STATE%YGSP_SG%F, YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDCPG_MISC%QS, YDMF_PHYS_SURF%GSP_RR%PT_T1,                &
& ZQS1, YDMF_PHYS_SURF%GSD_VF%PGETRL, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_SURF%GSD_VF%PZ0F, YDMF_PHYS_SURF%GSD_VF%PVRLAN,             &
& YDMF_PHYS_SURF%GSD_VF%PVRLDI, YDMF_PHYS_SURF%GSD_VF%PALBF, ZRDG_MU0, YDVARS%GEOMETRY%GM%T0, ZAC_HC,                                   &
& ZCOR, ZRAB3C, ZRAB3N, ZRAB4C, ZRAB4N, ZRAB6C, ZRAB6N, ZRAT1C, ZRAT1N, ZRAT2C, ZRAT2N, ZRAT3C,                                         &
& ZRAT3N, ZRAT4C, ZRAT4N, ZRAT5C, ZRAT5N, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%DIFTQ,                                &
& YDMF_PHYS%OUT%DIFTS, YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN, YDMF_PHYS%OUT%FCSQL, YDMF_PHYS%OUT%FCSQN,                              &
& YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, YDMF_PHYS%OUT%FPLSL, YDMF_PHYS%OUT%FPLSN, YDMF_PHYS%OUT%FRSO,                               &
& YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%STRCU, YDMF_PHYS%OUT%STRCV, YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV,                               &
& YDMF_PHYS%OUT%STRTU, YDMF_PHYS%OUT%STRTV, YDMF_PHYS%OUT%STRMU, YDMF_PHYS%OUT%STRMV, YDMF_PHYS%OUT%FRMH                                &
& )

!=END PARALLEL

!        3.3  Store the model trajectory at t-dt (leap-frog) or t (sl2tl).
!             ------------------------------------------------------------

!=SKIP

IF (LTRAJPS) THEN
  PTRAJ_PHYS%PQSSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDCPG_MISC%QS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
  PTRAJ_PHYS%PTSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA) =YDMF_PHYS_BASE_STATE%YGSP_RR%T(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
  PTRAJ_PHYS%PSNSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDMF_PHYS_BASE_STATE%YGSP_SG%F(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1)

  IF (.NOT. LTWOTL) THEN
    CALL WRPHTRAJM(YDGEOMETRY, YDSIMPHL, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, PTRAJ_PHYS, YDVARS%U%T9, YDVARS%V%T9, &
    & YDVARS%T%T9, YDVARS%Q%T9, YDVARS%L%T9, YDVARS%I%T9, YDVARS%SP%T9)  
  ENDIF

  IF (LPRTTRAJ.AND.PTRAJ_PHYS%LASTCHUNK) WRITE(NULOUT,*)'GREPTRAJ STORE TRAJ_PHYS in APLSIM'
ENDIF

!=END SKIP

!        3.4  Computation of tendencies T,u,v and Q.
!             --------------------------------------

!=PARALLEL
CALL CPTENDSM (YDMODEL%YRCST, YDPHY, YDCPG_DIM%KLON, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KFLEVG, YDMF_PHYS%OUT%DIFCQ,          &
& YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTS, YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN,              &
& YDMF_PHYS%OUT%FCSQL, YDMF_PHYS%OUT%FCSQN, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN, YDMF_PHYS%OUT%FPLSL,              &
& YDMF_PHYS%OUT%FPLSN, YDMF_PHYS%OUT%FRSO, YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%STRCU, YDMF_PHYS%OUT%STRCV,                &
& YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV, YDMF_PHYS%OUT%STRTU, YDMF_PHYS%OUT%STRTV, YDMF_PHYS%OUT%STRMU,              &
& YDMF_PHYS%OUT%STRMV, YDMF_PHYS%OUT%FRMH, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%RDELP, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, &
& YDMF_PHYS_BASE_STATE%U, YDMF_PHYS_BASE_STATE%V, YDMF_PHYS_BASE_STATE%T, YDMF_PHYS_BASE_STATE%Q, YDCPG_MISC%QS,        &
& YDMF_PHYS_BASE_STATE%YGSP_RR%T, YDVARS%GEOMETRY%OROG%T0, YDMF_PHYS%OUT%TENDU, YDMF_PHYS%OUT%TENDV,                    &
& ZTENDH, ZTENDQ, YDMF_PHYS%OUT%FHPCL, YDMF_PHYS%OUT%FHPCN, YDMF_PHYS%OUT%FHPSL, YDMF_PHYS%OUT%FHPSN,                   &
& YDMF_PHYS%OUT%FHSCL, YDMF_PHYS%OUT%FHSCN, YDMF_PHYS%OUT%FHSSL, YDMF_PHYS%OUT%FHSSN)  
!=END PARALLEL


!        3.5  Computation of evolution of T, u, v and Q.
!             ------------------------------------------

CALL CPUTQYS (YDGEOMETRY, YDCPG_DIM, YDMF_PHYS_BASE_STATE, YDMF_PHYS_NEXT_STATE, YDMF_PHYS, &
            & YDMODEL%YRCST, YDSTOPH, YDPHY2, PDTPHY, ZTENDH, ZTENDQ)

!     ------------------------------------------------------------------

!*       5.    Final calculations.
!              -------------------

!=PARALLEL
IF (LEDR) THEN
  YDMF_PHYS_SURF%GSD_DI%PXEDR(:,:)=YDMF_PHYS%OUT%EDR(:,:)
ENDIF
!=END PARALLEL

END ASSOCIATE

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('APLSIM', 1, ZHOOK_HANDLE)
END SUBROUTINE APLSIM
