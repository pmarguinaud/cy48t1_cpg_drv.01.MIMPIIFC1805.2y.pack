SUBROUTINE CUCALLN_MF &
 & (PPLDARE, PPLRG,    KSTEP,   YDTHF, YDCST, YDERAD,  YDML_PHY_SLIN,      YDML_PHY_EC,  YGFL,&
 & KIDIA,    KFDIA,    KLON,     KSMAX,   KLEV, PDX, KSPPN2D,&
 & LDLAND, LDSLPHY,&
 & PTSPHY,PVDIFTS,&
 & PTM1,     PQM1,     PUM1,     PVM1,    PLITOT,&
 & PVERVEL,  PQHFL,    PAHFS,   PAPHM1,&
 & PAP,      PAPH,     PGEO,     PGEOH, PGAW,&
 & PCUCONVCA,PGP2DSPP, &
 & PTENT,    PTENQ,    PTENU,    PTENV,   PARPRC,&
 & KTOPC,    KBASEC,   KTYPE,&
 & KCBOT,    KCTOP,    KBOTSC,   LDCUM,   LDSC,&
 & LDSHCV,&
 & PLCRIT_AER,&     
 & PLU,      PLUDE,    PLUDELI,  PSNDE,    PMFU,    PMFD,&
 & PDIFCQ,   PDIFCS,   PFHPCL,   PFHPCN,&
 & PFPLCL,   PFPLCN,   PLRAIN,   PRSUD,&
 & PSTRCU,   PSTRCV,   PFCQLF,   PFCQIF,&
 & PMFUDE_RATE ,       PMFDDE_RATE ,      PCAPE  ,PWMEAN,  PDISS,&
 & KTRAC,    PCM1,     PTENC,    PSCAV )  

!          *CUCALL* - MASTER ROUTINE - PROVIDES INTERFACE FOR:
!                     *CUMASTR* (CUMULUS PARAMETERIZATION)
!                     *CUCCDIA* (CUMULUS CLOUDS FOR RADIATION)
!                     *CUSTRAT* (PBL_STRATOCUMULUS)

!**   PURPOSE.
!     --------

!          *CUCALL* - INTERFACE FOR *CUMASTR*,*CUCCDIA* AND *CUSTRAT*:
!                     PROVIDES INPUT FOR CUMASTR, CUCCDIA AND CUSTRAT.
!                     RECEIVES UPDATED TENDENCIES, PRECIPITATION
!                     AND CONVECTIVE CLOUD PARAMETERS FOR RADIATION.

!**   INTERFACE.
!     ----------

!          *CUCALL* IS CALLED FROM *CALLPAR*

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----
!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KSMAX*        HORIZONTAL SPECTRAL TRUNCATION
!    *KLEV*         NUMBER OF LEVELS
!    *KSPPN2D*      Number of 2D patterns in SPP scheme
!    *KTRAC*        NUMBER OF CHEMICAL TRACERS

!     INPUT PARAMETERS (LOGICAL)

!    *LDLAND*       LAND SEA MASK (.TRUE. FOR LAND)

!     INPUT PARAMETERS (REAL)

!    *PTSPHY*       TIME STEP FOR THE PHYSICS                     S
!    *PTM1*         TEMPERATURE (T-1)                             K
!    *PQM1*         SPECIFIC HUMIDITY (T-1)                       KG/KG
!    *PUM1*         X-VELOCITY COMPONENT (T-1)                    M/S
!    *PVM1*         Y-VELOCITY COMPONENT (T-1)                    M/S
!    *PCM1*         CHEMICAL TRACERS (T-1)                        KG/KG
!    *PLITOT*       GRID MEAN LIQUID WATER + ICE CONTENT          KG/KG
!    *PVERVEL*      VERTICAL VELOCITY                             PA/S
!    *PQHFL*        MOISTURE FLUX (EXCEPT FROM SNOW EVAP.)     KG/(SM2)
!    *PAHFS*        SENSIBLE HEAT FLUX                            W/M2
!    *PAPHM1*       PRESSURE ON HALF LEVELS                       PA
!    *PAP*          PROVISIONAL PRESSURE ON FULL LEVELS           PA 
!    *PAPH*         PROVISIONAL PRESSURE ON HALF LEVELS           PA
!    *PGEO*         GEOPOTENTIAL                                  M2/S2
!    *PGEOH*        GEOPOTENTIAL ON HALF LEVELS                   M2/S2
!    *PGAW*       NORMALISED GAUSSIAN QUADRATURE WEIGHT / NUMBER OF LONGITUDE POINTS
!                           LOCAL SUB-AREA == 4*RPI*RA**2 * PGAW
!    *PLCRIT_AER*   CRITICAL LIQUID MMR FOR AUTOCONVERSION PROCESS KG/KG
!    *PCUCONVCA*    COUPLING TO CELL AUTOMATON OR 2D ADVECT FIELD ( )
!    *PGP2DSPP*     Standard stochastic variable (mean=0, SD=1)

!    UPDATED PARAMETERS (REAL):

!     tendency_cml    cumulative tendency used for final output
!     tendency_tmp    cumulative tendency used as input
!     tendency_loc    local tendency from convection

!    *PTENC*        TENDENCY OF CHEMICAL TRACERS                 1/S
!    *PARPRC*       ACCUMULATED PRECIPITATION AMMOUNT           KG/(M2*S)
!                   FOR RADIATION CALCULATION

!    UPDATED PARAMETERS (INTEGER):

!    *KTOPC*        UPDATED CONVECTIVE CLOUD TOP LEVEL FOR RADIATION
!    *KBASEC*       UPDATED CONVECTIVE CLOUD BASE LEVEL FOR RADIATION

!    OUTPUT PARAMETERS (INTEGER):

!    *KTYPE*        TYPE OF CONVECTION
!                       1 = PENETRATIVE CONVECTION
!                       2 = SHALLOW CONVECTION
!                       3 = MIDLEVEL CONVECTION 
!    *KCBOT*        CLOUD BASE LEVEL
!    *KCTOP*        CLOUD TOP LEVEL
!    *KBOTSC*       CLOUD BASE LEVEL FOR SC-CLOUDS

!    OUTPUT PARAMETERS (LOGICAL):

!    *LDCUM*        FLAG: .TRUE. FOR CONVECTIVE POINTS 
!    *LDSC*         FLAG: .TRUE. FOR SC-POINTS

!    OUTPUT PARAMETERS (REAL):

!    *PLU*          LIQUID WATER CONTENT IN UPDRAFTS            KG/KG
!    *PLUDE*        DETRAINED TOTAL CONDENSATE                  KG/(M2*S)
!    *PLUDELI*      DETRAINED LIQUID, ICE                       KG/(M2*S)
!    *PSNDE*        DETRAINED SNOW/RAIN                         KG/(M2*S)
!    *PMFU*         MASSFLUX UPDRAFTS                           KG/(M2*S)
!    *PMFD*         MASSFLUX DOWNDRAFTS                         KG/(M2*S)
!    *PDIFCQ*       CONVECTIVE FLUX OF SPECIFIC HUMIDITY        KG/(M2*S)
!    *PDIFCS*       CONVECTIVE FLUX OF HEAT                      J/(M2*S)
!    *PFHPCL*       ENTHALPY FLUX DUE TO CONVECTIVE RAIN         J/(M2*S)
!    *PFHPCN*       ENTHALPY FLUX DUE TO CONVECTIVE SNOW         J/(M2*S)
!    *PFPLCL*       CONVECTIVE RAIN FLUX                        KG/(M2*S)
!    *PFPLCN*       CONVECTIVE SNOW FLUX                        KG/(M2*S)
!    *PLRAIN*       RAIN+SNOW CONTENT IN UPDRAFTS                 KG/KG
!    *PRSUD*        CONVECT MEAN RAIN AND SNOW CONTENT IN COLUMN  KG/KG
!    *PSTRTU*       CONVECTIVE FLUX OF U-MOMEMTUM         (M/S)*KG/(M2*S)
!    *PSTRTV*       CONVECTIVE FLUX OF V-MOMEMTUM         (M/S)*KG/(M2*S)
!    *PFCQLF*       CONVECTIVE CONDENSATION FLUX LIQUID         KG/(M2*S)
!    *PFCQIF*       CONVECTIVE CONDENSATION FLUX ICE            KG/(M2*S)

!               Diagnostics:
!    *PMFUDE_RATE* UPDRAFT DETRAINMENT RATE                     KG/(M3*S)
!    *PMFDDE_RATE* DOWNDRAFT DETRAINMENT RATE                   KG/(M3*S)
!    *PCAPE*       MAXIMUM pseudoadiabat CAPE                   (J/KG) 
!    *PWMEAN*      VERTICALLY AVERAGED UPDRAUGHT VELOCITY        M/S
!    *PDISS*       ESTIMATE FOR DISSIPATION BT DEEP CONVCTION    J/m2

!     EXTERNALS.
!     ----------

!          CUMASTR
!          CUCCDIA
!          CUSTRAT

!     AUTHOR.
!     -------
!      M.TIEDTKE      E.C.M.W.F.     12/1989

!     MODIFICATIONS.
!     --------------
!      15-10-01 : FULLIMP mods D.Salmond 
!      11-02-04 : Enable tracer transport P. Bechtold
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      D.Salmond     22-Nov-2005 Mods for coarser/finer physics
!      F.Vana        18-May-2012 cleaning
!----------------------------------------------------------------------

USE MODEL_PHYSICS_ECMWF_MOD      , ONLY : MODEL_PHYSICS_ECMWF_TYPE
USE MODEL_PHYSICS_SIMPLINEAR_MOD , ONLY : MODEL_PHYSICS_SIMPLINEAR_TYPE
USE YOERAD                       , ONLY : TERAD
USE YOM_YGFL                     , ONLY : TYPE_GFLD
USE PARKIND1                     , ONLY : JPIM     ,JPRB
USE YOMHOOK                      , ONLY : LHOOK,   DR_HOOK

USE YOMCST                       , ONLY : TCST  
USE YOETHF                       , ONLY : TTHF 

IMPLICIT NONE


REAL(KIND=JPRB)   ,INTENT(IN)    :: PPLDARE
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPLRG
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP
TYPE(TTHF)                         ,INTENT(IN) :: YDTHF
TYPE(TCST)                         ,INTENT(IN) :: YDCST
TYPE(TERAD)                        ,INTENT(IN) :: YDERAD
TYPE(MODEL_PHYSICS_ECMWF_TYPE)     ,INTENT(IN) :: YDML_PHY_EC
TYPE(MODEL_PHYSICS_SIMPLINEAR_TYPE),INTENT(IN) :: YDML_PHY_SLIN
TYPE(TYPE_GFLD)   ,INTENT(IN)    :: YGFL
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSMAX 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSPPN2D
INTEGER(KIND=JPIM),INTENT(IN)    :: KTRAC 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
LOGICAL           ,INTENT(IN)    :: LDLAND(KLON) 
LOGICAL           ,INTENT(IN)    :: LDSLPHY 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSPHY 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVDIFTS
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLCRIT_AER(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCM1(KLON,KLEV,KTRAC) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLITOT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQHFL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAHFS(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHM1(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPH(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEO(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEOH(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGAW(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCUCONVCA(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGP2DSPP(KLON,KSPPN2D)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCAV(KTRAC) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDX(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENC(KLON,KLEV,KTRAC) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PARPRC(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KTOPC(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KBASEC(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KTYPE(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KCBOT(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KCTOP(KLON) 
INTEGER(KIND=JPIM),INTENT(OUT)   :: KBOTSC(KLON) 
LOGICAL           ,INTENT(OUT)   :: LDCUM(KLON) 
LOGICAL           ,INTENT(OUT)   :: LDSC(KLON) 
LOGICAL           ,INTENT(IN)    :: LDSHCV(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLUDE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLUDELI(KLON,KLEV,4) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSNDE(KLON,KLEV,2) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFD(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCQ(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDIFCS(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPCL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPCN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLCN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLRAIN(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRSUD(KLON,KLEV,2) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCU(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSTRCV(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCQLF(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFCQIF(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFUDE_RATE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFDDE_RATE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCAPE(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PWMEAN(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDISS(KLON,KLEV)

REAL(KIND=JPRB) ::     ZTP1(KLON,KLEV),        ZQP1(KLON,KLEV),&
 & ZUP1(KLON,KLEV),        ZVP1(KLON,KLEV),&
 & ZTU(KLON,KLEV),         ZQU(KLON,KLEV),&
 & ZQSAT(KLON,KLEV),       ZRAIN(KLON)         

REAL(KIND=JPRB) :: ZCP1(KLON,KLEV,KTRAC)

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZQEA(KLON,KLEV)&
 & ,  ZTEA(KLON,KLEV)&
 & ,  ZVEA(KLON,KLEV)&
 & ,  ZUEA(KLON,KLEV)&
 & ,  ZENTHD(KLON,KLEV),ZENTHS(KLON,KLEV)  
REAL(KIND=JPRB) :: ZCONDFLL(KLON),ZCONDFLN(KLON)

INTEGER(KIND=JPIM) :: IFLAG, JK, JL, JN

REAL(KIND=JPRB) :: ZCP, ZGDPH

LOGICAL :: LLTDKMF
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "cuccdia.intfb.h"
#include "cumastrn.intfb.h"
#include "satur.intfb.h"

!DIR$ VFUNCTION EXPHF
#include "fcttre.ycst.h"
#include "fcttrm.ycst.h"

!-----------------------------------------------------------------------

!*    0.1          STORE T,Q,X,Y TENDENCIES FOR FLUX COMPUTATIONS
!*                 ----------------------------------------------

IF (LHOOK) CALL DR_HOOK('CUCALLN_MF',0,ZHOOK_HANDLE)
ASSOCIATE(NJKT2=>YDML_PHY_EC%YRECUMF%NJKT2, &
 & LPHYLIN=>YDML_PHY_SLIN%YREPHLI%LPHYLIN, RLPTRC=>YDML_PHY_SLIN%YREPHLI%RLPTRC, &
 & LEPCLD=>YDML_PHY_EC%YREPHY%LEPCLD, LMFTRAC=>YDML_PHY_EC%YREPHY%LMFTRAC, &
 & LENCLD2=>YDML_PHY_SLIN%YRPHNC%LENCLD2)
! Setup of tendencies
DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZQEA(JL,JK)=PTENQ(JL,JK)
    ZTEA(JL,JK)=PTENT(JL,JK)
    ZVEA(JL,JK)=PTENV(JL,JK)
    ZUEA(JL,JK)=PTENU(JL,JK)
    ZENTHD(JL,JK)=0.0_JPRB
    ZENTHS(JL,JK)=0.0_JPRB
  ENDDO
ENDDO

!-----------------------------------------------------------------------

!*    1.           UPDATE PROGN. VALUES DEPENDING ON 
!                  TIME INTEGRATION AND CALCULATE QS
!*                 ---------------------------------

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZUP1(JL,JK)=PUM1(JL,JK)+PTENU(JL,JK)*PTSPHY
    ZVP1(JL,JK)=PVM1(JL,JK)+PTENV(JL,JK)*PTSPHY
    ZTP1(JL,JK)=PTM1(JL,JK)+PTENT(JL,JK)*PTSPHY
    ZQP1(JL,JK)=PQM1(JL,JK)+PTENQ(JL,JK)*PTSPHY
    ZQSAT(JL,JK)=ZQP1(JL,JK)
  ENDDO
ENDDO

IF ( LDSLPHY ) THEN
  IF(KTRAC>0 .AND. LMFTRAC) THEN
    DO JN=1,KTRAC
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
   ! attention: transport is for positive definit quantities only
        ! ZCP1(JL,JK,JN)=MAX(0._JPRB,PCM1(JL,JK,JN))
          ZCP1(JL,JK,JN)=PCM1(JL,JK,JN)
        ENDDO
      ENDDO
    ENDDO
  ENDIF
ELSE
  IF(KTRAC>0 .AND. LMFTRAC) THEN
    DO JN=1,KTRAC
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
   ! attention: transport is for positive definit quantities only
        ! ZCP1(JL,JK,JN)=MAX(0._JPRB,PCM1(JL,JK,JN)+PTENC(JL,JK,JN)*PTSPHY)
          ZCP1(JL,JK,JN)=PCM1(JL,JK,JN)+PTENC(JL,JK,JN)*PTSPHY
        ENDDO
      ENDDO
    ENDDO
  ENDIF
ENDIF

IFLAG=1
CALL SATUR (YDTHF, YDCST, KIDIA , KFDIA , KLON  , NJKT2 , KLEV,&
 & YDML_PHY_SLIN%YREPHLI%LPHYLIN, &
 & PAP   , ZTP1  , ZQSAT , IFLAG  )  

!-----------------------------------------------------------------------

!*    2.     CALL 'CUMASTR'(MASTER-ROUTINE FOR CUMULUS PARAMETERIZATION) 
!*           ----------------------------------------------------------- 

LLTDKMF = .TRUE.
CALL CUMASTRN &
 & (PPLDARE, PPLRG,    YDTHF,   YDCST,    YDML_PHY_SLIN,   YDML_PHY_EC,   YGFL, &
 & KIDIA,    KFDIA,    KLON,    KLEV, PDX, LLTDKMF, KSPPN2D, &
 & LDLAND,   PTSPHY,&
 & ZTP1,     ZQP1,     ZUP1,     ZVP1,     PLITOT,&
 & PVERVEL,  ZQSAT,    PQHFL,    PAHFS,&
 & PAP,      PAPH,     PGEO,     PGEOH, PGAW,&
 & PCUCONVCA,PGP2DSPP, &
 & PTENT,    PTENQ,    PTENU,    PTENV,&
 & LDCUM,    KTYPE,    KCBOT,    KCTOP,&
 & KBOTSC,   LDSC,&
 & LDSHCV,&
 & PLCRIT_AER,&
 & ZTU,      ZQU,      PLU,      PLUDE,  PLUDELI,   PSNDE,&
 & ZENTHD,   PFPLCL,   PFPLCN,   ZRAIN,  PLRAIN,    PRSUD,&
 & PMFU,     PMFD,&
 & PMFUDE_RATE,        PMFDDE_RATE,    PCAPE,  PWMEAN,  PDISS,&
 & KTRAC,    ZCP1,     PTENC,    PSCAV)  
!----------------------------------------------------------------------

!*    3.0       CALL 'CUCCDIA' TO UPDATE CLOUD PARAMETERS FOR RADIATION
!               -------------------------------------------------------

CALL CUCCDIA &
 & (YDERAD,  YDML_PHY_SLIN%YREPHLI,  YDML_PHY_EC%YREPHY,&
 & KIDIA,    KFDIA,    KLON,   KLEV,&
 & KSTEP,    KCBOT,    KCTOP,&
 & LDCUM,    ZQU,      PLU,      PMFU,    ZRAIN,&
 & PARPRC,   KTOPC,    KBASEC                   )  


!---------------------------------------------------------------------

!*    5.           FLUX COMPUTATIONS
!                  -----------------

DO JL=KIDIA,KFDIA
  PDIFCQ(JL,1)=0.0_JPRB
  PDIFCS(JL,1)=0.0_JPRB
  PSTRCU(JL,1)=0.0_JPRB
  PSTRCV(JL,1)=0.0_JPRB
  PFCQLF(JL,1)=0.0_JPRB
  PFCQIF(JL,1)=0.0_JPRB
  PFHPCL(JL,1)=0.0_JPRB
  PFHPCN(JL,1)=0.0_JPRB
  PDIFCS(JL,1)=0.0_JPRB
  PDIFCQ(JL,1)=0.0_JPRB
  ZCONDFLL(JL)=0.0_JPRB
  ZCONDFLN(JL)=0.0_JPRB
ENDDO

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA

    ZGDPH   = -YDCST%RG/(      PAPHM1(JL,JK+1)-PAPHM1(JL,JK) )
!...increment in dry static energy is converted to flux of d.s.e.
    ZCP=YDCST%RCPD*(1+YDTHF%RVTMP2*ZQP1(JL,JK))
    PDIFCS(JL,JK+1)=(PTENT(JL,JK)-ZTEA(JL,JK))/ZGDPH*ZCP+ PDIFCS(JL,JK)
!...increments in U,V
    PSTRCU(JL,JK+1)=(PTENU(JL,JK)-ZUEA(JL,JK))/ZGDPH + PSTRCU(JL,JK)
    PSTRCV(JL,JK+1)=(PTENV(JL,JK)-ZVEA(JL,JK))/ZGDPH + PSTRCV(JL,JK)

!...increment in Q
    PDIFCQ(JL,JK+1)=(PTENQ(JL,JK)-ZQEA(JL,JK))/ZGDPH + PDIFCQ(JL,JK)

  ENDDO
ENDDO

DO JK=1,KLEV
!DEC$ IVDEP
  DO JL=KIDIA,KFDIA
!... enthalpy flux due to precipitations

    PFHPCL(JL,JK+1)=-PFPLCL(JL,JK+1)*YDCST%RLVTT
    PFHPCN(JL,JK+1)=-PFPLCN(JL,JK+1)*YDCST%RLSTT
    ZCONDFLL(JL)=ZCONDFLL(JL) + PLUDELI(JL,JK,1)
    ZCONDFLN(JL)=ZCONDFLN(JL) + PLUDELI(JL,JK,2)
    PDIFCS(JL,JK+1)=PDIFCS(JL,JK+1)-PFHPCL(JL,JK+1)-PFHPCN(JL,JK+&
     & 1)&
     & + YDCST%RLVTT * ZCONDFLL(JL) + YDCST%RLSTT * ZCONDFLN(JL)  
    PDIFCQ(JL,JK+1)=PDIFCQ(JL,JK+1)-PFPLCL(JL,JK+1)-PFPLCN(JL,JK+&
     & 1)&
     & - ZCONDFLL(JL) - ZCONDFLN(JL)  
    PFCQLF(JL,JK+1)=ZCONDFLL(JL)
    PFCQIF(JL,JK+1)=ZCONDFLN(JL)
  ENDDO
ENDDO
!---------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CUCALLN_MF',1,ZHOOK_HANDLE)
END SUBROUTINE CUCALLN_MF
