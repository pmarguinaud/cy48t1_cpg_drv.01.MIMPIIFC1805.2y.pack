SUBROUTINE HL2FL ( KIDIA, KFDIA, KLON, KTDIAT, KLEV,&
          & PAPRS, PAPRSF, PXHL, KINI, PXFL)
!
!**** * HL2FL* - Passage des Half-level aux Full-level
!-----------------------------------------------------------------------
!     Auteur.
!     -------
!       30/06/2011, Eric BAZILE 

!   Modifications.
!   --------------
!
!   ARGUMENTS D'ENTREE.
!   -------------------
! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT.
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIAT     : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL)
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KINI       : INDICE DE DEPART DE LA DIMENSION VERTICALE DU TABLEAU HALF LEVEL
!              EN ENTREE general 0 (KINI:KLEV) pour acbl89, acturb,acevolet

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PXHL       : CHAMP A PASSER SUR LES FULL-LEVEL 

! - 2D (1:KLEV) .

! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.

! -   ARGUMENTS EN ENTREE/SORTIE.
!     ---------------------------
! - 2D (1:KLEV) .
! PXFL       : PXHL INTERPOLE SUR LES FL.

USE PARKIND1  ,ONLY : JPIM   ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIAT 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KINI 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PXHL(KLON,KINI:KLEV)

REAL(KIND=JPRB)   ,INTENT(OUT)  :: PXFL(KLON,KLEV)

REAL(KIND=JPRB) :: ZHOOK_HANDLE
INTEGER(KIND=JPIM) :: JLEV, JLON

!   CHECK RELIABILITY OF INPUT ARGUMENTS.

IF (LHOOK) CALL DR_HOOK('HL2FL',0,ZHOOK_HANDLE)

IF (KINI == 1) THEN
   DO JLEV = KTDIAT+1, KLEV
   DO JLON = KIDIA,KFDIA
      PXFL(JLON,JLEV)=((PXHL(JLON,JLEV)-PXHL(JLON,JLEV-1)) &
            & *PAPRSF(JLON,JLEV) &
            & + PXHL(JLON,JLEV-1)*PAPRS(JLON,JLEV) &
            & - PXHL(JLON,JLEV)*PAPRS(JLON,JLEV-1)) & 
            & / (PAPRS(JLON,JLEV)-PAPRS(JLON,JLEV-1))
   ENDDO
   ENDDO
   DO JLON = KIDIA,KFDIA
      PXFL(JLON,KTDIAT)= PXFL(JLON,KTDIAT+1)
   ENDDO
ELSE
   DO JLEV = KTDIAT, KLEV
   DO JLON = KIDIA,KFDIA
      PXFL(JLON,JLEV)=((PXHL(JLON,JLEV)-PXHL(JLON,JLEV-1)) &
            & *PAPRSF(JLON,JLEV) &
            & + PXHL(JLON,JLEV-1)*PAPRS(JLON,JLEV) &
            & - PXHL(JLON,JLEV)*PAPRS(JLON,JLEV-1)) & 
            & / (PAPRS(JLON,JLEV)-PAPRS(JLON,JLEV-1))
   ENDDO
   ENDDO
ENDIF

IF (LHOOK) CALL DR_HOOK('HL2FL',1,ZHOOK_HANDLE)
END SUBROUTINE HL2FL
