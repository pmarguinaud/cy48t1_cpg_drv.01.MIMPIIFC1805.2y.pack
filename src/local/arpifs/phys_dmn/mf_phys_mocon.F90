SUBROUTINE MF_PHYS_MOCON (YDCPG_BNDS, YDCPG_OPTS, PRDG_LCVQ, PMOC_CLPH, YDMF_PHYS, YDMF_PHYS_BASE_STATE)

!**** *MF_PHYS_MOCON*

!     Author. 
!     ------- 
!      Philippe Marguinaud *METEO-FRANCE*
!      Original : 28-04-2021

USE YOMHOOK, ONLY : LHOOK, DR_HOOK
USE PARKIND1, ONLY : JPIM, JPRB

USE CPG_OPTS_TYPE_MOD  , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE MF_PHYS_BASE_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_BASE_STATE_TYPE

IMPLICIT NONE

TYPE(CPG_BNDS_TYPE),           INTENT(IN)    :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),           INTENT(IN)    :: YDCPG_OPTS
REAL(KIND=JPRB),               INTENT(IN)    :: PRDG_LCVQ (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
INTEGER (KIND=JPIM),           INTENT(IN)    :: PMOC_CLPH (YDCPG_OPTS%KLON)
TYPE(MF_PHYS_TYPE),            INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JLON

REAL (KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('MF_PHYS_MOCON', 0, ZHOOK_HANDLE)

!=PARALLEL

DO JLEV=1,YDCPG_OPTS%KFLEVG
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    YDMF_PHYS%OUT%MOCON(JLON) = YDMF_PHYS%OUT%MOCON(JLON)+(PRDG_LCVQ(JLON,JLEV)-YDMF_PHYS_BASE_STATE%Q(JLON,JLEV)*&
     & YDMF_PHYS_BASE_STATE%DIV(JLON,JLEV))*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)&
     & *MAX(0,SIGN(1,JLEV-PMOC_CLPH(JLON)))  
  ENDDO
ENDDO

DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
  YDMF_PHYS%OUT%MOCON(JLON) = YDMF_PHYS%OUT%MOCON(JLON)/(YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD(JLON,YDCPG_OPTS%KFLEVG)-YDMF_PHYS_BASE_STATE%YCPG_PHY%PREHYD(JLON,PMOC_CLPH(JLON)-1))
ENDDO

!=END PARALLEL

IF (LHOOK) CALL DR_HOOK ('MF_PHYS_MOCON', 1, ZHOOK_HANDLE)

END SUBROUTINE MF_PHYS_MOCON
