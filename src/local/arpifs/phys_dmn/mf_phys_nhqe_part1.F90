SUBROUTINE MF_PHYS_NHQE_PART1 (YDGEOMETRY, YDCPG_BNDS, YDCPG_OPTS, PNHQ_TT0, PNHQ_TT0L, PNHQ_TT0M, PNHQ_TT9, YDVARS, YDMODEL, PGFL)

USE PARKIND1, ONLY : JPIM, JPRB

USE YOMCT0             , ONLY : LTWOTL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE CPG_OPTS_TYPE_MOD   , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE TYPE_MODEL         , ONLY : MODEL


IMPLICIT NONE

TYPE(GEOMETRY),     INTENT(IN)   :: YDGEOMETRY
TYPE(CPG_BNDS_TYPE),INTENT(IN)   :: YDCPG_BNDS
TYPE(CPG_OPTS_TYPE),INTENT(IN)   :: YDCPG_OPTS

REAL (KIND=JPRB),  INTENT (OUT)  :: PNHQ_TT0  (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL (KIND=JPRB),  INTENT (OUT)  :: PNHQ_TT0L (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL (KIND=JPRB),  INTENT (OUT)  :: PNHQ_TT0M (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
REAL (KIND=JPRB),  INTENT (OUT)  :: PNHQ_TT9  (YDCPG_OPTS%KLON, 1:YDCPG_OPTS%KFLEVG)
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(MODEL)       ,INTENT(IN)    :: YDMODEL


REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)

#include "gnhqe_conv_tempe.intfb.h"

INTEGER (KIND=JPIM) :: JLEV
INTEGER (KIND=JPIM) :: JROF

  ! Valid for NPDVAR=2 only.
  ! At instant t (with the derivatives):
  DO JLEV=1,YDCPG_OPTS%KFLEVG
    DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PNHQ_TT0(JROF,JLEV)=YDVARS%T%T0(JROF,JLEV)
      PNHQ_TT0L(JROF,JLEV)=YDVARS%T%DL(JROF,JLEV)
      PNHQ_TT0M(JROF,JLEV)=YDVARS%T%DM(JROF,JLEV)
    ENDDO
  ENDDO
  CALL GNHQE_CONV_TEMPE(YDGEOMETRY,.TRUE.,YDMODEL%YRML_GCONF%YGFL%NDIM,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,&
   & YDVARS%SPD%T0,YDVARS%SP%T0,YDVARS%T%T0,&
   & KGFLTYP=0,PGFL=PGFL,KDDER=2,PQCHAL=YDVARS%SPD%DL,PQCHAM=YDVARS%SPD%DM,&
   & PTL=YDVARS%T%DL,PTM=YDVARS%T%DM)
  ! At instant t-dt for leap-frog advections (without the derivatives):
  IF (.NOT.LTWOTL) THEN
    DO JLEV=1,YDCPG_OPTS%KFLEVG
      DO JROF=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PNHQ_TT9(JROF,JLEV)=YDVARS%T%T9(JROF,JLEV)
      ENDDO
    ENDDO
    CALL GNHQE_CONV_TEMPE(YDGEOMETRY,.TRUE.,YDMODEL%YRML_GCONF%YGFL%NDIM,YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA,&
     & YDVARS%SPD%T9,YDVARS%SP%T9,YDVARS%T%T9,&
     & KGFLTYP=9,PGFL=PGFL)
  ENDIF
  

END SUBROUTINE
