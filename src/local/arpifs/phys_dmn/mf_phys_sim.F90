#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE MF_PHYS_SIM(YDGEOMETRY, YDCPG_DIM, YDCPG_MISC, YDCPG_PHY0, YDCPG_PHY9, YDMF_PHYS, YDMF_PHYS_TMP, &
& YDCPG_DYN0, YDCPG_DYN9, YDMF_PHYS_SURF, YDVARS, YDGMV, YDSURF, YDCFU, YDXFU, &
& YDMODEL, LDCONFX, PDTPHY, &
& PGFL, PKOZO, PGP2DSDT, PB1, PB2, PGMVT1, PGFLT1, PGPAR, PTRAJ_PHYS, YDDDH,   &
& PFTCNS)

!**** *MF_PHYS_SIM* METEO-FRANCE PHYSICS.

!     Purpose.
!     --------
!         Call METEO-FRANCE physics and physical tendencies.

!**   Interface.
!     ----------
!        *CALL* *MF_PHYS_SIM(...)*

!        Explicit arguments :
!        --------------------

!     INPUT:
!     ------
!        KBL       : NPROMA-packets number
!        KGPCOMP   : total number of grid points in the domain
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        LDCONFX   : (see in CPG)
!        PDTPHY    : timestep used in the physics.
!        KIBL      : index into YRCSGEOM/YRGSGEOM types in YDGEOMETRY
!        POROGL,POROGM: components of grad(orography).
!        PGFL      : GFL at time t and t-dt.
!        PKOZO     : fields for photochemistery of ozon.
!        PGP2DSDT  : stochastic physics random pattern.

!     INPUT/OUTPUT:
!     -------------
!        PB1       : "SLB1"-buffer, used for interpolations in the SL scheme.
!        PB2       : "SLB2"-buffer.
!        PGFLT1    : GFL t+dt
!        PGPAR     : surface fields for AROME.
!        PGMU0     : COSINE OF SOLAR ZENITH ANGLE, APPROXIMATE ACTUAL VALUE
!                    linear T_e correction
!                    linear T_e correction

!     OUTPUT:
!     -------
!        PDHSF     : distribution of horizontal mean weights used for
!                    simplified radiation scheme.
!        ---------------------- output of aplpar ------------------------------
!        PFCQNG    : pseudo-flux of water to correct for Q<0.
!        PDIFCQLC to PFCNEGQSC:
!        ---------------------- end of output of aplpar -----------------------
!        PTENDU    : "U"-wind tendency due to physics.
!        PTENDV    : "V"-wind tendency due to physics.
!        PDIAGH    : Add Hail diagnostic PDIAGH (AROME)

!        Implicit arguments :
!        --------------------

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------

!     Author.
!     -------
!       2000-12-04: F. Bouyssel & J.M. Piriou

!     Modifications.
!     --------------
!       04-Mar-2009 A.Alias : call CPTEND/INITAPLPAR modified to add
!                         Humidity Mesopheric flux (ZFRMQ).
!                     and IVCLIA removed and call to CPNUDG modified as
!                         Nuding mask is now in SD_VF group
!                         call HL_APLPAR modified to add PFCQNG for acdifus
!                         call APL_AROME modified to add Sulfate/Volcano aerosols for radaer
!       K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!       2009-10-15 Y. Bouteloup : Store radiative cloud water and ice in GFL (YIRAD and YLRAD)
!       F. Vana   15-Oct-2009 : NSPLTHOI option
!       K.Yessad (Feb 2010): use YM_RADTC and RFORADTC
!       2010-03-26 Y. Bouteloup : Store radiative cloud water and ice in GFL (AROME case)
!       2010-04-26 Y. Bouteloup : Only one call to cputqy, cputqys and cputqy_arome
!            This need the use of ZTENDGFL as argument of cptend, cptend_new and apl_arome.
!       2010-05-11 F. Bouyssel : Use of PINDX, PINDY
!       2010-05-28 C. Geijo    : Fix error in IPTR array element referencing 
!       2010-06-21 O.Riviere/F. Bouyssel : Fix to have Ts evolving in Fa files with Surfex
!       Dec 2010 A.Alias   : ZMU0N added to call CPPHINP/APLPAR/APL_AROME/HL_APLPAR
!                            CALL to CPNUDG with or with LMSE (A.Voldoire)
!       K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!       L. Bengtsson-Sedlar & F. Vana 18-Feb-2011 : CA scheme for convection
!       F. Vana   22-Feb-2011 : 3D turbulence
!       2011-02-01 M. Mokhtari: Add LMDUST and PEXTT9 and PEXTT0 IN APLPAR
!                             (treatment of the desert aerosols) 
!       2011-03 A.Alias  : new argument to  for sunshine hours YSD_VD%YSUND 
!                      CPNUDG if LMSE=.T. or LMSE=.F. (bugfix)
!                      debug ozone GFL (IPO3) (D. St-Martin)
!                      Humidity Mesopheric flux (ZFRMQ) added in CPTEND_NEW
!       F.Bouyssel (26-03-2011): Fix to have Snow in hist file with surfex
!       2011-06: M. Jerczynski - some cleaning to meet norms
!       E. Bazile 2011-08-26 : Output for MUSC 1D with LFA files with WRITEPHYSIO
!         used previously for extracting profiles from 3D (now also available for AROME).
!       K. Yessad (Dec 2011): use YDOROG, YDGSGEOM and YDCSGEOM.
!       2011-11-21 JF Gueremy : dry convective adjustment (LAJUCV)
!       F. Vana  26-Jan-2012 : historic Qs for TOM's BBC.
!       F.Bouttier Jul 2012: stochastic physics for AROME
!       Z. SASSI  : 07-Mar-2013   INITIALIZING THE WEIGHT VECTORS PDHSF(NPROMA)
!       [DISTRIBUTION OF HORIZONTAL MEANS WEIGHTS]
!       F. Vana  28-Nov-2013 : Redesigned trajectory handling
!       T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!       2013-11, D. Degrauwe: Flexible interface CPTEND_FLEX.
!       2013-11, J. Masek: Passing intermittency arrays for ACRANEB2.
!       K. Yessad (July 2014): Move some variables.
!       2016-04, J. Masek: Passing sunshine duration to APL_AROME.
!       2016-09, M. Mokhtari & A. Ambar: replacement of ZEXT and ZEZDIAG by PGFL
!                                        in aplpar.F90 argument.
!       2016-10, P. Marguinaud : Port to single precision
!       K. Yessad (Dec 2016): Prune obsolete options.
!       K. Yessad (June 2017): Introduce NHQE model.
!       2017-09, J. Masek: Shifted dimensioning of PGMU0.
!       K. Yessad (Feb 2018): remove deep-layer formulations.
!       K. Yessad (Apr 2018): introduce key L_RDRY_VD (ensure consistent definition of "dver" everywhere).
!       2018-09, F. Duruisseau: add rconv and sconv in gfl for bayrad
!       2018-09, R. Brozkova: Passing of diagnostic hail, global normal
!         irradiance and mean radiant temperature from APLPAR.
!       2018-09, D. St-Martin : add NOGWD inputs in aplpar
!       2018-09, M. Michou : add ARPEGE-Climat chemistry call in aplpar  
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
!       2019-05, I. Etchevers : add visibilities and precipitation type
!   R. El Khatib 27-02-2019 memory bandwidth savings.
!   R. El Khatib 30-Oct-2018 IMAXDRAFT
!       2019-09, M. Hrastinski: Dataflow for TKE and TTE terms in ALARO DDH (PFTCNS).
!       2019-09, J. Masek: Modified call to APL_AROME (added argument NFRRC).
!       2019-12, Y. Bouteloup: Introduction of ZTENDU and ZTENDV for computation of ZDEC in cputqy
!                diferent from PTENDU and PTENDV in the case of the use of Tiedtke scheme to avoid double counting
!       2020-12, U. Andrae : Introduce SPP for HARMONIE-AROME
!       2021-01, R. Brozkova: ALARO graupel fix.
! End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE, MF_PHYS_TMP_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_DYN_TYPE, CPG_PHY_TYPE, &
                              & CPG_MISC_TYPE
USE CPG_DIM_TYPE_MOD   , ONLY : CPG_DIM_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE YOMGMV             , ONLY : TGMV
USE YOMCFU             , ONLY : TCFU
USE YOMXFU             , ONLY : TXFU
USE TYPE_MODEL         , ONLY : MODEL
USE PARKIND1           , ONLY : JPIM     ,JPRB
USE YOMHOOK            , ONLY : LHOOK,   DR_HOOK
USE SC2PRG_MOD         , ONLY : SC2PRG

USE YOMCT0             , ONLY : LSLAG, LTWOTL, LNHDYN, LAROME, LSFORCS, LNHQE,LCORWAT
USE YOMCVER            , ONLY : LVERTFE  ,LVFE_GWMPA 
USE YOMDYNA            , ONLY : LGWADV, L3DTURB, L_RDRY_VD
USE YOMNUD             , ONLY : NFNUDG   ,LNUDG 
USE YOMSNU             , ONLY : XPNUDG
USE MODULE_RADTC_MIX   , ONLY : YM_RADTC
USE YOMSCM             , ONLY : LGSCM
USE YOMCST             , ONLY : RG, RD
USE YOMCHET            , ONLY : GCHETN
USE INTDYN_MOD         , ONLY : YYTCTY0  ,YYTRCP0  ,YYTRCP9  ,YYTXYB0_PHY,YYTXYB9_PHY
USE YOMLSFORC          , ONLY : LMUSCLFA
USE YOMSPSDT           , ONLY : YSPPT
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TYPE, LPRTTRAJ
USE YOMLUN             , ONLY : NULOUT

USE DDH_MIX            , ONLY : TYP_DDH
USE SPP_MOD            , ONLY : YSPP_CONFIG,YSPP
USE MF_PHYS_STATE_TYPE_MOD &
                       , ONLY : MF_PHYS_STATE_TYPE
!     -------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_DIM_TYPE),INTENT(IN)    :: YDCPG_DIM
TYPE(CPG_MISC_TYPE),INTENT(INOUT):: YDCPG_MISC
TYPE(CPG_PHY_TYPE),INTENT(INOUT), TARGET :: YDCPG_PHY0
TYPE(CPG_PHY_TYPE),INTENT(INOUT), TARGET :: YDCPG_PHY9
TYPE(MF_PHYS_TYPE),INTENT(INOUT) :: YDMF_PHYS
TYPE(MF_PHYS_TMP_TYPE), INTENT(INOUT) :: YDMF_PHYS_TMP
TYPE(CPG_DYN_TYPE),INTENT(INOUT), TARGET :: YDCPG_DYN0
TYPE(CPG_DYN_TYPE),INTENT(INOUT), TARGET :: YDCPG_DYN9
TYPE(MF_PHYS_SURF_TYPE),INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(TGMV)        ,INTENT(INOUT) :: YDGMV
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(TCFU)        ,INTENT(INOUT) :: YDCFU
TYPE(TXFU)        ,INTENT(INOUT) :: YDXFU
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
LOGICAL           ,INTENT(IN)    :: LDCONFX
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDTPHY 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKOZO(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG*YDMODEL%YRML_PHY_G%YRDPHY%NVCLIS+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGP2DSDT(YDGEOMETRY%YRDIM%NPROMA,YSPPT%YGPSDT(1)%NG2D)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB1(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB2%NFLDSLB2)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG,YDGMV%YT1%NDIM) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGPAR(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_MF%YRPARAR%NGPAR+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFTCNS(YDGEOMETRY%YRDIM%NPROMA,0:YDCPG_DIM%KFLEVG,6)
TYPE (TRAJ_PHYS_TYPE), INTENT(INOUT) :: PTRAJ_PHYS
TYPE(TYP_DDH)     ,INTENT(INOUT) :: YDDDH

!     ------------------------------------------------------------------
INTEGER(KIND=JPIM) :: IFIELDSS
INTEGER(KIND=JPIM) :: IPQ,IPO3
INTEGER(KIND=JPIM) :: IPGFL(YDMODEL%YRML_GCONF%YGFL%NUMFLDS)

INTEGER(KIND=JPIM) :: INSTEP_DEB,INSTEP_FIN
INTEGER(KIND=JPIM) :: JLEV, JGFL
INTEGER(KIND=JPIM) :: JROF
INTEGER(KIND=JPIM) :: ISLB1U9  ,ISLB1V9  ,ISLB1T9  ,ISLB1GFL9, ISLB1VD9

!     --- UPPER AIR PHYSICAL TENDENCIES.
REAL(KIND=JPRB) :: ZTENDH(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)     ! Enthalpy tendency.
REAL(KIND=JPRB) :: ZTENDQ(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)     ! Moisture tendency.
REAL(KIND=JPRB) :: ZTENDPTKE(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)  ! Pseudo progn. TKE

! GFL tendencies for APL_AROME (assumes YDMODEL%YRML_GCONF%YGFL%NUMFLDS>=YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
! for now, use Jovi's trick :
REAL(KIND=JPRB) :: ZTENDGFL(YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NUMFLDS)   ! GFL tendencies

!     --- UPPER AIR PHYSICAL TENDENCIES FOR AROME.
!       (the previous one are not used in AROME)
REAL(KIND=JPRB) :: ZTENDT (YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG)        ! temperature tendency
REAL(KIND=JPRB) :: ZTENDD (YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG)        ! d  tendency
REAL(KIND=JPRB) :: ZTENDEXT(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)      ! GFL EXTRA tendency
REAL(KIND=JPRB) :: ZTENDEXT_DEP(YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)  ! GFL EXTRA tendency

REAL(KIND=JPRB) :: ZTENDU (YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG)    ! U tendency without deep convection contribution
REAL(KIND=JPRB) :: ZTENDV (YDGEOMETRY%YRDIM%NPROMA,YDCPG_DIM%KFLEVG)    ! V tendency without deep convection contribution

!     --- RADIATION COEFFICIENTS FOR SIMPLIFIED PHYSICS IN GRID-POINT ---
REAL(KIND=JPRB) :: ZAC(YDCPG_DIM%KLON,(YDCPG_DIM%KFLEVG+1)*(YDCPG_DIM%KFLEVG+1))   ! Curtis matrix.
REAL(KIND=JPRB) :: ZAC_HC(YDCPG_DIM%KFLEVG+1,YDCPG_DIM%KFLEVG+1)           ! horizontally-constant field for ZAC.

REAL(KIND=JPRB) :: ZCOR   (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB3C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB3N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB4C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB4N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB6C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAB6N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT1C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT1N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT2C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT2N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT3C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT3N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT4C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT4N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT5C (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZRAT5N (YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG)
REAL(KIND=JPRB) :: ZQS1   (YDCPG_DIM%KLON)

REAL(KIND=JPRB), POINTER :: ZP1CHEM0(:,:,:), ZP1CHEM9(:,:,:)
REAL(KIND=JPRB), POINTER :: ZP1EXT0(:,:,:), ZP1EXT9(:,:,:)
REAL(KIND=JPRB), POINTER :: ZP1LIMA0(:,:,:), ZP1LIMA9(:,:,:)
REAL(KIND=JPRB), POINTER :: ZP1EZDIAG(:,:)
REAL(KIND=JPRB), POINTER :: ZP1NOGW0(:,:), ZP1NOGW9(:,:), ZP2NOGW0(:,:), ZP2NOGW9(:,:)
REAL(KIND=JPRB), POINTER :: ZPTENDTKE1(:,:)

TYPE (MF_PHYS_STATE_TYPE) :: YLMF_PHYS_STATE
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "aplpars.intfb.h"
#include "aplpassh.intfb.h"
#include "cpphinp.intfb.h"
#include "cppsolan.intfb.h"
#include "cptendsm.intfb.h"
#include "cputqys.intfb.h"
#include "cp_ptrslb1.intfb.h"
#include "mf_phys_init.intfb.h"
#include "rdradcoef.intfb.h"
#include "wrphtrajm.intfb.h"
#include "mf_phys_nhqe_part1.intfb.h"
#include "mf_phys_nhqe_part2.intfb.h"
#include "mf_phys_transfer.intfb.h"
#include "mf_phys_precips.intfb.h"
#include "mf_phys_cvv.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('MF_PHYS_SIM',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM, YDDIMV=>YDGEOMETRY%YRDIMV, YDVAB=>YDGEOMETRY%YRVAB, YDCSGEOM=>   &
& YDGEOMETRY%YRCSGEOM(YDCPG_DIM%KBL), YDGSGEOM=>YDGEOMETRY%YRGSGEOM(YDCPG_DIM%KBL), YDOROG=>YDGEOMETRY%YROROG(YDCPG_DIM%KBL),  &
& YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY, YDPTRSLB1=>YDMODEL%YRML_DYN%YRPTRSLB1, YDPTRSLB2=>YDMODEL%      &
& YRML_DYN%YRPTRSLB2, YDTOPH=>YDMODEL%YRML_PHY_MF%YRTOPH, YDSIMPHL=>YDMODEL%YRML_PHY_MF%YRSIMPHL,   &
& YDRIP=>YDMODEL%YRML_GCONF%YRRIP, YDMDDH=>YDMODEL%YRML_DIAG%YRMDDH, YDRCOEF=>YDMODEL%YRML_PHY_RAD% &
& YRRCOEF, YDARPHY=>YDMODEL%YRML_PHY_MF%YRARPHY, YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY, YDLDDH=>YDMODEL%&
& YRML_DIAG%YRLDDH, YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2, YGFL=>YDMODEL%YRML_GCONF%YGFL, YDEPHY=>     &
& YDMODEL%YRML_PHY_EC%YREPHY, YDPARAR=>YDMODEL%YRML_PHY_MF%YRPARAR, YDPRECIPS=>YDMODEL%YRML_PHY_MF% &
& YRPHY%YRDPRECIPS, YDSTOPH=>YDMODEL%YRML_PHY_STOCH%YRSTOPH)

ASSOCIATE(MVTS=>YDPARAR%MVTS, CMF_UPDRAFT=>YDPARAR%CMF_UPDRAFT, TSPHY=>YDPHY2%TSPHY, NPROMA=>YDDIM%   &
& NPROMA, NTSSG=>YDDPHY%NTSSG, NVCLIS=>YDDPHY%NVCLIS, LMDUST=>YDARPHY%LMDUST, &
& LMSE=>YDARPHY%LMSE, LMFSHAL=>YDARPHY%LMFSHAL, YI=>YGFL%YI, YH=>YGFL%YH, YEZDIAG=>YGFL%YEZDIAG, YL   &
& =>YGFL%YL, YEXT=>YGFL%YEXT, YG=>YGFL%YG, YQ=>YGFL%YQ, YR=>YGFL%YR, YSCONV=>YGFL%YSCONV, YS=>YGFL%   &
& YS, YEFB3=>YGFL%YEFB3, YEFB2=>YGFL%YEFB2, YEFB1=>YGFL%YEFB1, YO3=>YGFL%YO3, YNOGW=>YGFL%YNOGW,      &
& LCHEM_ARPCLIM=>YDMODEL%YRML_CHEM%YRCHEM%LCHEM_ARPCLIM, YCHEM=>YGFL%YCHEM, NGFL_EXT=>YGFL%NGFL_EXT,  &
& YTKE=>YGFL%YTKE, YCOMP=>YGFL%YCOMP, YLCONV=>YGFL%YLCONV, YRCONV=>YGFL%YRCONV, YICONV=>YGFL%YICONV,  &
& YSP_SBD=>YDSURF%YSP_SBD, YSD_VVD=>YDSURF%YSD_VVD, NFLEVG=>YDDIMV%NFLEVG, NFLSA=>YDDIMV%NFLSA,       &
& NFLSUL=>YDDIMV%NFLSUL, LTRAJPS=>YDSIMPHL%LTRAJPS, LRAYSP=>YDSIMPHL%LRAYSP, &
& LNEBR=>YDPHY%LNEBR, LNEBN=>YDPHY%LNEBN, LMPHYS=>YDPHY%LMPHYS, LSTRAPRO=>YDPHY%LSTRAPRO, LPTKE=>     &
& YDPHY%LPTKE, NDPSFI=>YDPHY%NDPSFI, LOZONE=>YDPHY%LOZONE, L3MT=>YDPHY%L3MT, LGPCMT=>YDPHY%LGPCMT,    &
& LAJUCV=>YDPHY%LAJUCV, LCVPGY=>YDPHY%LCVPGY, LRRGUST=>YDPHY%LRRGUST, LEDR=>YDPHY%LEDR, NTAJUC=>      &
& YDTOPH%NTAJUC, NTPLUI=>YDTOPH%NTPLUI, NUMFLDS=>YGFL%NUMFLDS, LAGPHY=>YDEPHY%LAGPHY, &
& LDPRECIPS=>YDPHY%LDPRECIPS, LDPRECIPS2=>YDPHY%LDPRECIPS2, NDTPRECCUR=>YDPRECIPS%           &
& NDTPRECCUR, NDTPRECCUR2=>YDPRECIPS%NDTPRECCUR2, LSDDH=>YDLDDH%LSDDH, HDSF=>YDMDDH%HDSF, MSLB1SP9=>  &
& YDPTRSLB1%MSLB1SP9, MSLB2KAPPAH=>YDPTRSLB2%MSLB2KAPPAH, MSLB2KAPPAM=>YDPTRSLB2%MSLB2KAPPAM, LRCOEF  &
& =>YDRCOEF%LRCOEF, LTLADDIA=>YDRCOEF%LTLADDIA, NG3SR=>YDRCOEF%NG3SR, NLIMA=>YGFL%NLIMA, YLIMA=>YGFL  &
& %YLIMA)

CALL SC2PRG(1,YCHEM(:)%MP ,YDMODEL%YRML_GCONF%YGFL%NCHEM,   PGFL,ZP1CHEM0)    !  YDVARS%CHEM(1)%T0
CALL SC2PRG(1,YCHEM(:)%MP9,YDMODEL%YRML_GCONF%YGFL%NCHEM,   PGFL,ZP1CHEM9)    !  YDVARS%CHEM(1)%T9
CALL SC2PRG(1,YLIMA(:)%MP ,YDMODEL%YRML_GCONF%YGFL%NLIMA,   PGFL,ZP1LIMA0)    !  YDVARS%LIMA(1)%T0
CALL SC2PRG(1,YLIMA(:)%MP9,YDMODEL%YRML_GCONF%YGFL%NLIMA,   PGFL,ZP1LIMA9)    !  YDVARS%LIMA(1)%T9
CALL SC2PRG(1,YEXT(:)%MP  ,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT,PGFL,ZP1EXT0 )    !  YDVARS%EXT(1)%T0
CALL SC2PRG(1,YEXT(:)%MP9 ,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT,PGFL,ZP1EXT9 )    !  YDVARS%EXT(1)%T9
CALL SC2PRG(1,YEZDIAG(:)%MP,PGFL,ZP1EZDIAG)
CALL SC2PRG(1,YNOGW(:)%MP ,PGFL ,ZP1NOGW0)    !  YDVARS%NOGW(1)%T0
CALL SC2PRG(1,YNOGW(:)%MP9,PGFL ,ZP1NOGW9)    !  YDVARS%NOGW(1)%T9 
CALL SC2PRG(2,YNOGW(:)%MP ,PGFL ,ZP2NOGW0)    !  YDVARS%NOGW(2)%T0
CALL SC2PRG(2,YNOGW(:)%MP9,PGFL ,ZP2NOGW9)    !  YDVARS%NOGW(2)%T9 

CALL SC2PRG(YTKE%MP1  ,ZTENDGFL ,ZPTENDTKE1)

CALL YLMF_PHYS_STATE%INIT (LTWOTL, YDCPG_DYN0, YDCPG_DYN9, YDCPG_PHY0, YDCPG_PHY9, YDVARS, YDMF_PHYS_SURF, &
                  & P1EXT0=ZP1EXT0, P1EXT9=ZP1EXT9, P1CHEM0=ZP1CHEM0, P1CHEM9=ZP1CHEM9, &
                  & P1NOGW0=ZP1NOGW0, P1NOGW9=ZP1NOGW9, P2NOGW0=ZP2NOGW0, P2NOGW9=ZP2NOGW9, &
                  & P1LIMA0=ZP1LIMA0, P1LIMA9=ZP1LIMA9)

!     ------------------------------------------------------------------

!        1.    Preliminary calculations necessary
!              for all types of physics.
!              ------------------------------------


! In the NHQE model, MF_PHYS_SIM enters with Tt and grad(Tt), where Tt = T * exp(-(R/cp) log(pre/prehyd)).
! But calculations of MF_PHYS_SIM must use T and grad(T).
! So we do a conversion Tt -> T.
IF (LNHQE) THEN
  CALL MF_PHYS_NHQE_PART1 (YDGEOMETRY, YDCPG_DIM, YDMF_PHYS_TMP, YDVARS, YDMODEL, PGFL)
ENDIF

INSTEP_DEB=1
INSTEP_FIN=1

IF (LRAYSP.AND.(YDCPG_DIM%KSTEP >= INSTEP_DEB .AND. YDCPG_DIM%KSTEP <= INSTEP_FIN)) THEN  
  CALL CPPSOLAN(YDGEOMETRY%YRDIM,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDGSGEOM%GEMU,YDGSGEOM%GELAM,YDMF_PHYS_TMP%RDG%MMU0)
  IF (.NOT.LSDDH) THEN
!-----------INITIALIZING THE WEIGHT VECTORS-------------
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      YDCPG_MISC%DHSF(JROF)=HDSF(JROF+YDCPG_DIM%KSTGLO-1)
    ENDDO
!-------------------------------------------------------
  ENDIF
ENDIF

CALL CPPHINP(YDGEOMETRY, YDMODEL, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDGSGEOM%GEMU, YDGSGEOM%GELAM, YDVARS%U%T0, YDVARS%V% &
& T0, YDVARS%Q%T0, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%CVGQ%DL, YDVARS%CVGQ%DM, YDCPG_PHY0%XYB%RDELP, &
& YDCPG_DYN0%CTY%EVEL, YDVARS%CVGQ%T0, YDMF_PHYS_TMP%RDG%MU0, YDMF_PHYS_TMP%RDG%MU0LU, YDMF_PHYS_TMP%RDG%MU0M, YDMF_PHYS_TMP%RDG%MU0N, YDMF_PHYS_TMP%RDG%CVGQ)
YDMF_PHYS_TMP%RDG%LCVQ(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:YDCPG_DIM%KFLEVG)=YDMF_PHYS_TMP%RDG%CVGQ(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1:YDCPG_DIM%KFLEVG)

! * In some cases, some pseudo-historic surface buffers (like z0) should
!   not be modified between the entrance and the output of MF_PHYS_SIM
!   (this is the case for example if LDCONFX=T).
!   For the time being, we must save:
!   - HV (group VV) : resistance to evapotranspiration
!   - Z0F (group VD): gravity * surface roughness length
!   - Z0H (group VV): gravity * roughness length for heat
!   - PBLH (group VH): PBL height
!   - SPSH (group VH):
!   - QSH (group VH):

CALL MF_PHYS_INIT ( YGFL, YDARPHY, YDCPG_DIM%KIDIA, YDCPG_DIM%KFDIA, YDCPG_DIM%KLON, YDCPG_DIM%KFLEVG, NTSSG, YSP_SBD%NLEVS,&
  & YDMF_PHYS%OUT%DIFCQ , YDMF_PHYS%OUT%DIFCQN, YDMF_PHYS%OUT%DIFCQL, YDMF_PHYS%OUT%DIFCS , YDMF_PHYS%OUT%DIFTQ , YDMF_PHYS%OUT%DIFTQN, YDMF_PHYS%OUT%DIFTQL,&
  & YDMF_PHYS%OUT%DIFTS , YDMF_PHYS%OUT%FCCQL , YDMF_PHYS%OUT%FCCQN , YDMF_PHYS%OUT%FCSQL , YDMF_PHYS%OUT%FCSQN , YDMF_PHYS%OUT%FCQNG , YDMF_PHYS%OUT%FCQNNG,&
  & YDMF_PHYS%OUT%FCQLNG, YDMF_PHYS%OUT%FCQRNG, YDMF_PHYS%OUT%FCQSNG,YDMF_PHYS%OUT%FCQGNG,&
  & YDMF_PHYS%OUT%FPLCL , YDMF_PHYS%OUT%FPLCN , YDMF_PHYS%OUT%FPLCG , YDMF_PHYS%OUT%FPLCH, YDMF_PHYS%OUT%FPLSL , YDMF_PHYS%OUT%FPLSN ,YDMF_PHYS%OUT%FPLSG ,&
  & YDMF_PHYS%OUT%FPLSH, YDMF_PHYS%OUT%FRSO  , YDMF_PHYS%OUT%FRSOC ,&
  & YDMF_PHYS%OUT%FRTH  , YDMF_PHYS%OUT%FRTHC , YDMF_PHYS%OUT%STRCU , YDMF_PHYS%OUT%STRCV , YDMF_PHYS%OUT%STRDU , YDMF_PHYS%OUT%STRDV , YDMF_PHYS%OUT%STRTU ,&
  & YDMF_PHYS%OUT%STRTV , YDMF_PHYS%OUT%STRMU , YDMF_PHYS%OUT%STRMV , YDMF_PHYS%OUT%FRMH  , &
  & YDMF_PHYS%OUT%DIFCQLC,YDMF_PHYS%OUT%DIFCQIC,YDMF_PHYS%OUT%FIMCC,&
  & YDMF_PHYS%OUT%FEDQLC, YDMF_PHYS%OUT%FEDQIC, YDMF_PHYS%OUT%FEDQRC, YDMF_PHYS%OUT%FEDQSC, YDMF_PHYS%OUT%FCNEGQLC,YDMF_PHYS%OUT%FCNEGQIC,YDMF_PHYS%OUT%FCNEGQRC,YDMF_PHYS%OUT%FCNEGQSC,&
  & YDMF_PHYS%OUT%FCHOZ , &
  & YDCPG_MISC%NEB   , YDCPG_MISC%QICE  , YDCPG_MISC%QLI   , &
  & YDCPG_MISC%RH    , YDMF_PHYS%OUT%ALB , &
  & YDMF_PHYS%OUT%CT    , YDMF_PHYS%OUT%FCHSP , YDMF_PHYS%OUT%FCLL  , YDMF_PHYS%OUT%FCLN  ,&
  & YDMF_PHYS%OUT%FCS   , YDMF_PHYS%OUT%FEVL  , YDMF_PHYS%OUT%FEVN  , YDMF_PHYS%OUT%FEVV  , YDMF_PHYS%OUT%FLASH , YDMF_PHYS%OUT%FTR   , YDMF_PHYS%OUT%FLWSP ,&
  & YDMF_PHYS%OUT%FONTE , YDMF_PHYS%OUT%FGEL  , YDMF_PHYS%OUT%FGELS ,&
  & YDMF_PHYS%OUT%FRSGNI, YDMF_PHYS%OUT%FRSDNI, YDMF_PHYS%OUT%FRSODS, YDMF_PHYS%OUT%FRSOPS, YDMF_PHYS%OUT%FRSOPT, YDMF_PHYS%OUT%FRSOLU, YDMF_PHYS%OUT%FRTHDS,&
  & YDMF_PHYS%OUT%FPFPSL, YDMF_PHYS%OUT%FPFPSN,YDMF_PHYS%OUT%FPFPSG, YDMF_PHYS%OUT%FPFPCL, YDMF_PHYS%OUT%FPFPCN,YDMF_PHYS%OUT%FPEVPSL,YDMF_PHYS%OUT%FPEVPSN,YDMF_PHYS%OUT%FPEVPSG,YDMF_PHYS%OUT%FPEVPCL,&
  & YDMF_PHYS%OUT%FPEVPCN,YDMF_PHYS%OUT%FPEVPCG, &
  & YDMF_PHYS%OUT%GZ0   , YDMF_PHYS%OUT%GZ0H  , YDCPG_MISC%QS    , YDMF_PHYS%OUT%RUISL ,&
  & YDMF_PHYS%OUT%RUISP , YDMF_PHYS%OUT%RUISS , YDMF_PHYS%OUT%UCLS  , YDMF_PHYS%OUT%VCLS  , YDMF_PHYS%OUT%NUCLS , YDMF_PHYS%OUT%NVCLS , YDMF_PHYS%OUT%TCLS  , YDMF_PHYS%OUT%MRT,&
  & YDMF_PHYS%OUT%QCLS  , YDMF_PHYS%OUT%RHCLS , YDCPG_MISC%CLCT  , YDMF_PHYS%OUT%CLCH  , YDMF_PHYS%OUT%CLCM  , YDMF_PHYS%OUT%CLCL  , YDMF_PHYS%OUT%CLCC  ,&
  & YDMF_PHYS%OUT%CAPE  , YDMF_PHYS%OUT%CTOP  , YDMF_PHYS%OUT%CLPH  , YDMF_PHYS%OUT%VEIN  , YDMF_PHYS%OUT%UGST  , YDMF_PHYS%OUT%VGST  ,&
  & YDMF_PHYS%OUT%DIAGH , YDMF_PHYS%OUT%EDR, YDMF_PHYS%OUT%VISICLD, YDMF_PHYS%OUT%VISIHYD, YDMF_PHYS%OUT%MXCLWC, YDMF_PHYS%OUT%MOCON)

!*       2.    Complete physics.
!              -----------------

! Set GFL tendencies to 0

ZTENDGFL(:,:,:) = 0.0_JPRB

!        2.9  Computation of evolution of T, u, v and Q.
!             ------------------------------------------

! * Calculation of IPGFL, since the old pointers
!   MSLB1[X]9 (=MSLB1GFL9+IP[X]) do not exist any longer in PTRSLB1.

! usefull pointer for new version of cputqy

DO JGFL=1,NUMFLDS
  IF ((YCOMP(JGFL)%MP1 > 0) .AND. (YCOMP(JGFL)%MP_SL1 > 0)) THEN
     IPGFL(YCOMP(JGFL)%MP1) = (YCOMP(JGFL)%MP_SL1-1)*(YDCPG_DIM%KFLEVG+2*NFLSUL)
  ENDIF   
ENDDO  

!  ALARO does not respect the coding rules, tendency of pseudo-TKE is computed in APLPAR and not
!  in CPTEND_NEW. To use the new version of cputqy it is then necessary to write it in GFL tendencies array.
! This memory transfer is not necessary, please respect coding rules to avoid it.

! Not necessary for intflex: already done in aplpar2intflex

IF (LPTKE) THEN
  DO JLEV=1,YDCPG_DIM%KFLEVG
    DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
      ZPTENDTKE1(JROF,JLEV) = ZTENDPTKE(JROF,JLEV)
    ENDDO
  ENDDO    
ENDIF
! Extra-GFL
IF(LMDUST.AND.(NGFL_EXT/=0)) THEN
  DO JGFL=1, NGFL_EXT
    DO JLEV=1,YDCPG_DIM%KFLEVG
      DO JROF=YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA
        ZTENDGFL(JROF,JLEV,YEXT(JGFL)%MP1) = ZTENDEXT(JROF,JLEV,JGFL)+&! turbulent tendency
                                           & ZTENDEXT_DEP(JROF,JLEV,JGFL) ! moist tendency
      ENDDO
    ENDDO 
  ENDDO   
ENDIF


! ky: non-zero option not yet coded for the time being.
ZTENDD=0.0_JPRB

!       2.9b Prognostic convection etc.
!            --------------------------

! TRANSFER NOT ADVECTED VARIABLES INTO PGFLT1
CALL MF_PHYS_TRANSFER (YDCPG_DIM, YDVARS, YDMODEL)

CALL MF_PHYS_CVV (YDCPG_DIM, YDVARS, YDMODEL)

!    -------------------------------------------------------------------

!*       3.    Simplified physics.
!              -------------------

!        3.1  Preliminary calculations necessary for simplified physics.
!             ----------------------------------------------------------


  ! * read grid-point transmission coefficients for simplified physics.
IF (LRAYSP.AND.LRCOEF.AND.(YDCPG_DIM%KSTEP > 1.OR.LTLADDIA)) THEN
  IFIELDSS=NG3SR*YDCPG_DIM%KFLEVG
  CALL RDRADCOEF(YDGEOMETRY,YDRCOEF,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KSTGLO,IFIELDSS, &
& ZCOR  , ZRAB3C, ZRAB3N, ZRAB4C, ZRAB4N, ZRAB6C, ZRAB6N, ZRAT1C, ZRAT1N, ZRAT2C, ZRAT2N, ZRAT3C, &
& ZRAT3N, ZRAT4C, ZRAT4N, ZRAT5C, ZRAT5N, ZAC_HC)
ENDIF


!        3.2  Simplified physics.
!             -------------------

TSPHY = MAX(PDTPHY,1.0_JPRB)

CALL APLPASSH (YDPHY,YDMODEL%YRML_PHY_MF%YRPHY1,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG,&
 & YLMF_PHYS_STATE%YCPG_PHY%PREHYD,YLMF_PHYS_STATE%Q,&
 & YDMF_PHYS_SURF%GSP_SG%PF_T1,YDMF_PHYS_SURF%GSP_RR%PT_T1,YDMF_PHYS_SURF%GSP_RR%PW_T1,&
 & YDMF_PHYS_SURF%GSD_VF%PLSM,YDMF_PHYS_SURF%GSD_VF%PVEG,&
 & ZQS1)  

CALL APLPASSH (YDPHY,YDMODEL%YRML_PHY_MF%YRPHY1,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KLON,YDCPG_DIM%KFLEVG,&
 & YLMF_PHYS_STATE%YCPG_PHY%PREHYD,YLMF_PHYS_STATE%Q,&
 & YLMF_PHYS_STATE%YGSP_SG%F,YLMF_PHYS_STATE%YGSP_RR%T,YLMF_PHYS_STATE%YGSP_RR%W,&
 & YDMF_PHYS_SURF%GSD_VF%PLSM,YDMF_PHYS_SURF%GSD_VF%PVEG,&
 & YDCPG_MISC%QS)  

CALL APLPARS(YDGEOMETRY,YDRCOEF,YDMODEL%YRML_PHY_MF,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KLON,1,YDCPG_DIM%KFLEVG,NTSSG,YDCPG_DIM%KSTEP,&
 & YLMF_PHYS_STATE%YCPG_DYN%PHI,YLMF_PHYS_STATE%YCPG_PHY%PREHYD,YLMF_PHYS_STATE%YCPG_DYN%PHIF,&
 & YLMF_PHYS_STATE%YCPG_PHY%PREHYDF,YLMF_PHYS_STATE%YCPG_PHY%XYB%DELP,YLMF_PHYS_STATE%YCPG_PHY%XYB%RDELP,&
 & YLMF_PHYS_STATE%U,YLMF_PHYS_STATE%V,YLMF_PHYS_STATE%T,YLMF_PHYS_STATE%Q,YLMF_PHYS_STATE%YCPG_DYN%RCP%CP,YDMF_PHYS_TMP%RDG%CVGQ,&
 & YLMF_PHYS_STATE%YGSP_SG%F,YLMF_PHYS_STATE%YGSP_RR%T,YDCPG_MISC%QS,&
 & YDMF_PHYS_SURF%GSP_RR%PT_T1,ZQS1,&
 & YDMF_PHYS_SURF%GSD_VF%PGETRL,YDMF_PHYS_SURF%GSD_VF%PLSM,&
 & YDMF_PHYS_SURF%GSD_VF%PZ0F,YDMF_PHYS_SURF%GSD_VF%PVRLAN,YDMF_PHYS_SURF%GSD_VF%PVRLDI,&
 & YDMF_PHYS_SURF%GSD_VF%PALBF,YDMF_PHYS_TMP%RDG%MU0,YDGSGEOM%GM,&
 & ZAC_HC,ZCOR,ZRAB3C,ZRAB3N,ZRAB4C,ZRAB4N,ZRAB6C,ZRAB6N,&
 & ZRAT1C,ZRAT1N,ZRAT2C,ZRAT2N,ZRAT3C,ZRAT3N,ZRAT4C,ZRAT4N,ZRAT5C,ZRAT5N,&
 & YDMF_PHYS%OUT%DIFCQ,YDMF_PHYS%OUT%DIFCS,YDMF_PHYS%OUT%DIFTQ,YDMF_PHYS%OUT%DIFTS,&
 & YDMF_PHYS%OUT%FCCQL,YDMF_PHYS%OUT%FCCQN,YDMF_PHYS%OUT%FCSQL,YDMF_PHYS%OUT%FCSQN,&
 & YDMF_PHYS%OUT%FPLCL,YDMF_PHYS%OUT%FPLCN,YDMF_PHYS%OUT%FPLSL,YDMF_PHYS%OUT%FPLSN,YDMF_PHYS%OUT%FRSO,YDMF_PHYS%OUT%FRTH,&
 & YDMF_PHYS%OUT%STRCU,YDMF_PHYS%OUT%STRCV,YDMF_PHYS%OUT%STRDU,YDMF_PHYS%OUT%STRDV,YDMF_PHYS%OUT%STRTU,YDMF_PHYS%OUT%STRTV,&
 & YDMF_PHYS%OUT%STRMU,YDMF_PHYS%OUT%STRMV,YDMF_PHYS%OUT%FRMH)

!        3.3  Store the model trajectory at t-dt (leap-frog) or t (sl2tl).
!             ------------------------------------------------------------

IF (LTRAJPS) THEN
  PTRAJ_PHYS%PQSSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YDCPG_MISC%QS(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
  PTRAJ_PHYS%PTSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA) =YLMF_PHYS_STATE%YGSP_RR%T(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)
  PTRAJ_PHYS%PSNSMF(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA)=YLMF_PHYS_STATE%YGSP_SG%F(YDCPG_DIM%KIDIA:YDCPG_DIM%KFDIA,1)

  IF (.NOT. LTWOTL) THEN
    CALL WRPHTRAJM(YDGEOMETRY,YDSIMPHL,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,PTRAJ_PHYS,&
     & YDVARS%U%T9,YDVARS%V%T9,YDVARS%T%T9,&
     & YDVARS%Q%T9,YDVARS%L%T9,YDVARS%I%T9,YDVARS%SP%T9)  
  ENDIF

  IF (LPRTTRAJ.AND.PTRAJ_PHYS%LASTCHUNK) WRITE(NULOUT,*)'GREPTRAJ STORE TRAJ_PHYS in MF_PHYS_SIM'
ENDIF

!        3.4  Computation of tendencies T,u,v and Q.
!             --------------------------------------

TSPHY = MAX(PDTPHY,1.0_JPRB)

CALL CPTENDSM (YDPHY,YDCPG_DIM%KLON,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KFLEVG,&
 & YDMF_PHYS%OUT%DIFCQ,YDMF_PHYS%OUT%DIFCS,YDMF_PHYS%OUT%DIFTQ,YDMF_PHYS%OUT%DIFTS,&
 & YDMF_PHYS%OUT%FCCQL,YDMF_PHYS%OUT%FCCQN,YDMF_PHYS%OUT%FCSQL,YDMF_PHYS%OUT%FCSQN,&
 & YDMF_PHYS%OUT%FPLCL,YDMF_PHYS%OUT%FPLCN,YDMF_PHYS%OUT%FPLSL,YDMF_PHYS%OUT%FPLSN,&
 & YDMF_PHYS%OUT%FRSO,YDMF_PHYS%OUT%FRTH,&
 & YDMF_PHYS%OUT%STRCU,YDMF_PHYS%OUT%STRCV,YDMF_PHYS%OUT%STRDU,YDMF_PHYS%OUT%STRDV,&
 & YDMF_PHYS%OUT%STRTU,YDMF_PHYS%OUT%STRTV,&
 & YDMF_PHYS%OUT%STRMU,YDMF_PHYS%OUT%STRMV,YDMF_PHYS%OUT%FRMH,&
 & YLMF_PHYS_STATE%YCPG_PHY%XYB%RDELP,YLMF_PHYS_STATE%YCPG_DYN%PHIF,&
 & YLMF_PHYS_STATE%U,YLMF_PHYS_STATE%V,YLMF_PHYS_STATE%T,YLMF_PHYS_STATE%Q,&
 & YDCPG_MISC%QS,YLMF_PHYS_STATE%YGSP_RR%T,YDOROG%OROG,&
 & YDMF_PHYS%OUT%TENDU,YDMF_PHYS%OUT%TENDV,ZTENDH,ZTENDQ,&
 & YDMF_PHYS%OUT%FHPCL,YDMF_PHYS%OUT%FHPCN,YDMF_PHYS%OUT%FHPSL,YDMF_PHYS%OUT%FHPSN,&
 & YDMF_PHYS%OUT%FHSCL,YDMF_PHYS%OUT%FHSCN,YDMF_PHYS%OUT%FHSSL,YDMF_PHYS%OUT%FHSSN)  


!        3.5  Computation of evolution of T, u, v and Q.
!             ------------------------------------------

IF (LSLAG) CALL CP_PTRSLB1(YDMODEL%YRML_DYN%YRDYN,YDPTRSLB1,ISLB1U9,ISLB1V9,ISLB1T9,ISLB1VD9,ISLB1GFL9)
CALL CPUTQYS(YDGEOMETRY%YRDIMV,YDGMV,YGFL,YDPTRSLB1,&
 & YDSTOPH, YDPHY2, &
 & YDCPG_DIM%KLON,YDCPG_DIM%KIDIA,YDCPG_DIM%KFDIA,YDCPG_DIM%KFLEVG,PDTPHY,IPGFL,&
 & ISLB1U9,ISLB1V9,ISLB1T9,ISLB1GFL9,&
 & ZTENDH, ZTENDQ, YDMF_PHYS%OUT%TENDU, YDMF_PHYS%OUT%TENDV,&
 & YDMF_PHYS%FOR%U, YDMF_PHYS%FOR%V, YDMF_PHYS%FOR%T, YDMF_PHYS%FOR%Q, &
 & YLMF_PHYS_STATE%YCPG_DYN%RCP%CP,YLMF_PHYS_STATE%YCPG_PHY%XYB%DELP,&
 & YLMF_PHYS_STATE%T,YLMF_PHYS_STATE%U,YLMF_PHYS_STATE%V,&
 & PB1, PGMVT1, PGFLT1,&
 & YDMF_PHYS%OUT%FDIS)


!     ------------------------------------------------------------------

!*       5.    Final calculations.
!              -------------------

IF (LEDR) THEN
  YDMF_PHYS_SURF%GSD_DI%PXEDR(:,:)=YDMF_PHYS%OUT%EDR(:,:)
ENDIF

CALL MF_PHYS_PRECIPS (YDCPG_DIM, YDMF_PHYS_TMP, YDMF_PHYS_SURF, YDMODEL)

! Restore Tt and grad(Tt) for NHQE model.
IF (LNHQE) THEN
  CALL MF_PHYS_NHQE_PART2 (YDGEOMETRY, YDCPG_DIM, YDMF_PHYS_TMP, YDVARS)
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('MF_PHYS_SIM',1,ZHOOK_HANDLE)
END SUBROUTINE MF_PHYS_SIM
