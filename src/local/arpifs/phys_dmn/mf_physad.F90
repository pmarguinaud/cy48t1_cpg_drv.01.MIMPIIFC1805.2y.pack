SUBROUTINE MF_PHYSAD(YDGEOMETRY,YDGMV,YDGMV5,YDSURF5,&
 !------------------------------------------------------------
 ! -INPUT in TL and AD
 & YDMODEL,KST,KEND,KSTGLO,PDTPHY,&
 & KIBL,&
 !------------------------------------------------------------
 ! -INPUT in TL, INPUT/OUTPUT in AD
 & PGMV,PGFL,PRCP0,PXYB0,PHI0,PHIF0,PRE0,PRE0F,PCTY0,&
 & PRCP9,PXYB9,PHI9,PHIF9,PRE9,PRE9F,&
 !-----------------------------------------------------------
 ! -INPUT in TL and AD
 & PQS,PQS1,PSNS,PTS,PTS1,&
 !----------------------------------------------------------
 ! -INPUT/OUTPUT in TL and AD
 & PB1,PGMVT1,PGFLT1,&
 !-----------------------------------------------------------
 ! -INPUT for TRAJECTORY in TL and AD
 & PGMVT5,PGFLT5,PRCP5,PXYB5,PHI5,PHIF5,PRE5,PRE5F,PCTY5,&
 & PGMVT95,PGFLT95,PRCP95,PXYB95,PHI95,PHIF95,PRE95,PRE95F,&
 & PSD_VF5,PTRAJ_PHYS,PTRAJ_PHYS_TLAD)

!**** *MF_PHYSAD* - METEO-FRANCE physics: adjoint grid point calculations.

!     Purpose.
!     --------
!        METEO-FRANCE physics: adjoint grid point calculations.

!**   Interface.
!     ----------
!        *CALL* *MF_PHYSAD(...)*

!        Explicit arguments :
!        --------------------

!     INPUT in TL and AD:
!     -------------------
!        KST       : first element of work.
!        KEND      : last element of work.
!        KSTGLO    : global offset.
!        PDTPHY    : timestep used in the physics.
!        KIBL      : index into YRGSGEOM instance in YDGEOMETRY

!     INPUT in TL, INPUT/OUTPUT in AD:
!     --------------------------------
!        PGMV      : GMV at time t and t-dt.
!        PGFL      : GFL at t and t-dt.
!        PRCP0     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PXYB0     : contains pressure depth, "delta", "alpha" at time t.
!        PHI0      : geopotential height on interlayers at time t.
!        PHIF0     : geopotential height on layers at time t.
!        PRE0      : hydrostatic pressure "prehyd" on interlayers at time t.
!        PRE0F     : hydrostatic pressure "prehyd" on layers at time t.
!        PCTY0     : contains vertical velocities, vertical integral of divergence at t.
!        PRCP9     : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PXYB9     : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI9      : geopotential height on interlayers at time t-dt.
!        PHIF9     : geopotential height on layers at time t-dt.
!        PRE9      : hydrostatic pressure "prehyd" on interlayers at time t-dt.
!        PRE9F     : hydrostatic pressure "prehyd" on layers at time t-dt.

!     INPUT in TL and AD:
!     -------------------
!        PQS       : surface specific humidity at t or t-dt accord. to ltwotl.
!        PQS1      : surface specific humidity at t+dt.
!        PSNS      : mass of snow per unit surf at t or t-dt accord. to ltwotl.
!        PTS       : surface temperature at t or t-dt accord. to ltwotl.
!        PTS1      : surface temperature at t+dt.

!     INPUT/OUTPUT in TL and AD:
!     --------------------------
!        PB1       : "SLB1"-buffer, used for interpolations in the SL scheme.
!        PGMVT1    : GMV t+dt
!        PGFLT1    : GFL t+dt


!     INPUT FOR TRAJECTORY ("5" ARRAYS) in TL and AD:
!     -----------------------------------------------
!        PGMVT5    : GMV time t.
!        PGFLT5    : GFL at time t.
!        PRCP5     : contains "cp", "R" and "Kap=R/Cp" at t.
!        PXYB5     : contains pressure depth, "delta", "alpha" at t.
!        PHI5      : geopotential height on interlayers at time t.
!        PHIF5     : geopotential height on layers at time t.
!        PRE5      : hydrostatic pressure "prehyd" on interlayers at time t.
!        PRE5F     : hydrostatic pressure "prehyd" on layers at time t.
!        PCTY5     : contains vertical velocities, vertical integral of divergence at t.
!        PGMVT95   : GMV at time t-dt.
!        PGFLT95   : GFL at time t-dt.
!        PRCP95    : contains "cp", "R" and "Kap=R/Cp" at t-dt.
!        PXYB95    : contains pressure depth, "delta", "alpha" at t-dt.
!        PHI95     : geopotential height on interlayers at time t-dt.
!        PHIF95    : geopotential height on layers at time t-dt.
!        PRE95     : hydrostatic pressure "prehyd" on interlayers at time t-dt.
!        PRE95F    : hydrostatic pressure "prehyd" on layers at time t-dt.
!        PSD_VF5   : surface grid-point fields (VARSF).

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------

!     Author.
!     -------
!        2000-12-04: F. Bouyssel & J.M. Piriou

!     Modifications.
!     --------------
!      Modified :
!       K. Yessad (Dec 2008): remove dummy CDLOCK + cleanings
!       K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!       O.Riviere (Nov 09): adds additional arg in RDPHTRAJTM for LGWDSPNL 
!       Y. Bouteloup (Apr 2010) : Only one call to cputqysad
!       K.Yessad (Feb 2010): use YM_RADTC and RFORADTC
!       A.Alias   (Dec 2010): ZMU0N added to call CPPHINP
!       K. Yessad (Jan 2011): introduce INTDYN_MOD structures.
!       O.Riviere (Feb 11): Remove call to cpphinpad if LPROQCLDTL+add QL/QI
!       O.Riviere (June 2011): Retrieval of Ql/i/r/s from trajectory if needed
!       K. Yessad (Dec 2011): use YDOROG and YDGSGEOM.
!       T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!       F. Vana  28-Nov-2013 : Redesigned trajectory handling
!       K. Yessad (Dec 2016): Prune obsolete options.
!       E. Arbogast (Sep 2018): YDSURF comes from trajectory and is intent IN
!     ------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM     ,JPRB
USE YOMHOOK            , ONLY : LHOOK    ,DR_HOOK
USE SC2PRG_MOD         , ONLY : SC2PRG
USE YOMCT0             , ONLY : LTWOTL   ,LELAM
USE YOMCT3             , ONLY : NSTEP
USE MODULE_RADTC_MIX   , ONLY : YM_RADTC
USE INTDYN_MOD         , ONLY : YYTCTY5  ,YYTCTY0  ,YYTRCP5  ,YYTRCP95 ,&
 &                              YYTRCP0  ,YYTRCP9  ,YYTXYB5  ,YYTXYB95 ,YYTXYB0  ,YYTXYB9  ,YYTGMVT95,YYTGFLT95
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TLAD_TYPE, TRAJ_PHYS_TYPE, MSTEPTRAJW


!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)            ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)                ,INTENT(INOUT) :: YDGMV
TYPE(TGMV)                ,INTENT(INOUT) :: YDGMV5
TYPE(TSURF)               ,INTENT(IN)    :: YDSURF5
TYPE(MODEL)               ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM)        ,INTENT(IN)    :: KST 
INTEGER(KIND=JPIM)        ,INTENT(IN)    :: KEND 
INTEGER(KIND=JPIM)        ,INTENT(IN)    :: KSTGLO 
REAL(KIND=JPRB)           ,INTENT(IN)    :: PDTPHY
INTEGER(KIND=JPIM)        ,INTENT(IN)    :: KIBL
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRCP0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP0%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PXYB0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB0%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PHI0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PHIF0(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRE0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRE0F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PCTY0(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY0%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRCP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP9%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PXYB9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB9%NDIM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PHI9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PHIF9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRE9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PQS(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PQS1(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PSNS(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PTS(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PTS1(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PB1(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB1%NFLDSLB1)
REAL(KIND=JPRB)           ,INTENT(INOUT) :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%YT1%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PGMVT5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV5%YT5%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PGFLT5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM5)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRCP5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP5%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PXYB5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB5%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PHI5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PHIF5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRE5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRE5F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PCTY5(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG,YYTCTY5%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PGMVT95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTGMVT95%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PGFLT95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTGFLT95%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRCP95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTRCP95%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PXYB95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB95%NDIM)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PHI95(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PHIF95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRE95(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PRE95F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)           ,INTENT(IN)    :: PSD_VF5(YDGEOMETRY%YRDIM%NPROMA,YDSURF5%YSD_VFD%NDIM)
TYPE (TRAJ_PHYS_TYPE)     ,INTENT(IN)    :: PTRAJ_PHYS
TYPE (TRAJ_PHYS_TLAD_TYPE),INTENT(IN)    :: PTRAJ_PHYS_TLAD
!     ------------------------------------------------------------------
!     ==== COMPUTED BY CPPHINPAD ====
! ZMU0 : (NPROMM)           ; SOLAR ANGLE
! ZMU0M: (NPROMM)           ; SOLAR ANGLE (mean between radiation steps)
! ZCVGQ: (NPROMM,NFLEVG)    ; CONVERGENCE OF HUMIDITY
REAL(KIND=JPRB) :: ZMU0(YDGEOMETRY%YRDIM%NPROMM),ZCVGQ(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZMU0M(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB) :: ZMU0N(YDGEOMETRY%YRDIM%NPROMA) 

!     --- 1D DIAGNOSTIC FIELDS, MOON RADIATION.
! ZMU0LU   : (NPROMM)         ; LUNAR ANGLE.
REAL(KIND=JPRB) :: ZMU0LU(YDGEOMETRY%YRDIM%NPROMM)

! ZCVGQ5: (NPROMM,NFLEVG)   ; CONVERGENCE OF HUMIDITY

REAL(KIND=JPRB) :: ZCVGQ5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)

!     ==== FIELDS FOR SIMPL.PHYS.PARAM. ====

REAL(KIND=JPRB) :: ZDIFCQ(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZDIFCS(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDIFTQ(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZDIFTS(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFCCQN(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFCCQL(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFCSQN(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFCSQL(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFPLCL(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFPLCN(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFPLSL(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFPLSN(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFRSO (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)
REAL(KIND=JPRB) :: ZFRTH (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)
REAL(KIND=JPRB) :: ZSTRCU(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRCV(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRDU(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRDV(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRTU(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRTV(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRMU(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRMV(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFRMH (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFDIS (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZDIFCQ5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZDIFCS5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZDIFTQ5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZDIFTS5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFCCQN5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFCCQL5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFCSQN5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFCSQL5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFPLCL5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFPLCN5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFPLSL5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZFPLSN5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFRSO5 (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)
REAL(KIND=JPRB) :: ZFRTH5 (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)
REAL(KIND=JPRB) :: ZSTRCU5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRCV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRDU5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRDV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRTU5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRTV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRMU5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRMV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZFRMH5 (YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZKTROV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZKUROV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZXTROV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZXUROV5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZCDROV5(YDGEOMETRY%YRDIM%NPROMM),ZCHROV5(YDGEOMETRY%YRDIM%NPROMM),ZXDROV5(YDGEOMETRY%YRDIM%NPROMM),&
 & ZXHROV5(YDGEOMETRY%YRDIM%NPROMM)
REAL(KIND=JPRB) :: ZTRAJGWD5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG),ZTRIGCONV5(YDGEOMETRY%YRDIM%NPROMM)

REAL(KIND=JPRB) :: ZQR5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),ZQS5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQI5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),ZQL5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQLISCVPPKF(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZNEBSCVPPKF(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZTENDU(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDV(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENDH(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDQ(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENDQL(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDQI(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZTENDU5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDV5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENDH5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDQ5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENDQL5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZTENDQI5(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB) :: ZPOUB(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)

!     --- RADIATION COEFFICIENTS FOR SIMPLIFIED PHYSICS IN GRID-POINT ---
REAL(KIND=JPRB) :: ZAC(YDGEOMETRY%YRDIM%NPROMM,(YDGEOMETRY%YRDIMV%NFLEVG+1)*(YDGEOMETRY%YRDIMV%NFLEVG+1))   ! Curtis matrix.
REAL(KIND=JPRB) :: ZAC_HC(YDGEOMETRY%YRDIMV%NFLEVG+1,YDGEOMETRY%YRDIMV%NFLEVG+1)           ! horizontally-constant field for ZAC.
REAL(KIND=JPRB) :: ZRADTC(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG,YM_RADTC%NDIM) ! others than Curtis matrix.

INTEGER(KIND=JPIM) :: IFIELDSS, IPQ, IPL, IPI, JLEV, JROF, JSG
INTEGER(KIND=JPIM) :: ISLB1U9, ISLB1V9, ISLB1T9, ISLB1GFL9

REAL(KIND=JPRB), POINTER :: ZT0T(:,:), ZT0U(:,:), ZT0V(:,:), ZT9T(:,:)
REAL(KIND=JPRB), POINTER :: ZT9U(:,:), ZT9V(:,:)
REAL(KIND=JPRB), POINTER :: ZMCOR(:,:), ZMRAB3C(:,:), ZMRAB3N(:,:), ZMRAB4C(:,:)
REAL(KIND=JPRB), POINTER :: ZMRAB4N(:,:), ZMRAB6C(:,:), ZMRAB6N(:,:), ZMRAT1C(:,:)
REAL(KIND=JPRB), POINTER :: ZMRAT1N(:,:), ZMRAT2C(:,:), ZMRAT2N(:,:), ZMRAT3C(:,:)
REAL(KIND=JPRB), POINTER :: ZMRAT3N(:,:), ZMRAT4C(:,:), ZMRAT4N(:,:), ZMRAT5C(:,:)
REAL(KIND=JPRB), POINTER :: ZMRAT5N(:,:)
REAL(KIND=JPRB), POINTER :: ZPI(:,:), ZPI9(:,:), ZPL(:,:), ZPL9(:,:)
REAL(KIND=JPRB), POINTER :: ZPQ(:,:), ZPQ9(:,:), ZPQL(:,:), ZPQM(:,:)
REAL(KIND=JPRB), POINTER :: ZPI5(:,:), ZPL5(:,:), ZPQ5(:,:), ZPQ5L(:,:)
REAL(KIND=JPRB), POINTER :: ZPQ5M(:,:)

REAL(KIND=JPRB) :: ZNOTUSED1(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNOTUSED2(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNOTUSED3(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNOTUSED4(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZNOTUSED5(YDGEOMETRY%YRDIM%NPROMM,0:YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_PHY_G%YRDPHY%NTSSG+1)

INTEGER(KIND=JPIM)    :: ISTEP_NOT_USED, ISTGLO_NOT_USED


REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "aplparsad.intfb.h"
#include "aplparstl.intfb.h"
#include "cpphinp.intfb.h"
#include "cpphinpad.intfb.h"
#include "cptendsmad.intfb.h"
#include "cptendsmtl.intfb.h"
#include "cputqysad.intfb.h"
#include "rdphtrajtm.intfb.h"
#include "rdradcoef.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('MF_PHYSAD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 &  YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), YDOROG=>YDGEOMETRY%YROROG(KIBL), YDPTRSLB1=>YDMODEL%YRML_DYN%YRPTRSLB1, &
 & YDSIMPHL=>YDMODEL%YRML_PHY_MF%YRSIMPHL,YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,YDRCOEF=>YDMODEL%YRML_PHY_RAD%YRRCOEF, &
 & YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2,YGFL=>YDMODEL%YRML_GCONF%YGFL)

ASSOCIATE(NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NDIM5=>YGFL%NDIM5, YI=>YGFL%YI, &
 & YL=>YGFL%YL, YQ=>YGFL%YQ, &
 & NPROMA=>YDDIM%NPROMA, NPROMM=>YDDIM%NPROMM, &
 & NFLEVG=>YDDIMV%NFLEVG, NFLSUL=>YDDIMV%NFLSUL, &
 & NTSSG=>YDDPHY%NTSSG, &
 & NDIMGMV=>YDGMV%NDIMGMV, YT0=>YDGMV%YT0, YT1=>YDGMV%YT1, YT9=>YDGMV%YT9, &
 & YT5=>YDGMV5%YT5, &
 & MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9, MSLB1T9=>YDPTRSLB1%MSLB1T9, &
 & MSLB1U9=>YDPTRSLB1%MSLB1U9, MSLB1V9=>YDPTRSLB1%MSLB1V9, &
 & NFLDSLB1=>YDPTRSLB1%NFLDSLB1, &
 & LRCOEF=>YDRCOEF%LRCOEF, NG3SR=>YDRCOEF%NG3SR, &
 & YSD_VF=>YDSURF5%YSD_VF, YSD_VFD=>YDSURF5%YSD_VFD, &
 & TSPHY=>YDPHY2%TSPHY, LPROCLDTL=>YDSIMPHL%LPROCLDTL, &
 & LRAYSP=>YDSIMPHL%LRAYSP, LTRAJPST=>YDSIMPHL%LTRAJPST)

CALL SC2PRG(YI%MP5    ,PGFLT5   ,ZPI5)
CALL SC2PRG(YL%MP5    ,PGFLT5   ,ZPL5)
CALL SC2PRG(YQ%MP5    ,PGFLT5   ,ZPQ5)
CALL SC2PRG(YQ%MP5L   ,PGFLT5   ,ZPQ5L)
CALL SC2PRG(YQ%MP5M   ,PGFLT5   ,ZPQ5M)
CALL SC2PRG(YI%MP     ,PGFL     ,ZPI)
CALL SC2PRG(YI%MP9    ,PGFL     ,ZPI9)
CALL SC2PRG(YL%MP     ,PGFL     ,ZPL)
CALL SC2PRG(YL%MP9    ,PGFL     ,ZPL9)
CALL SC2PRG(YQ%MP     ,PGFL     ,ZPQ)
CALL SC2PRG(YQ%MP9    ,PGFL     ,ZPQ9)
CALL SC2PRG(YQ%MPL    ,PGFL     ,ZPQL)
CALL SC2PRG(YQ%MPM    ,PGFL     ,ZPQM)
CALL SC2PRG(YM_RADTC%MCOR,  ZRADTC   ,ZMCOR)
CALL SC2PRG(YM_RADTC%MRAB3C,ZRADTC   ,ZMRAB3C)
CALL SC2PRG(YM_RADTC%MRAB3N,ZRADTC   ,ZMRAB3N)
CALL SC2PRG(YM_RADTC%MRAB4C,ZRADTC   ,ZMRAB4C)
CALL SC2PRG(YM_RADTC%MRAB4N,ZRADTC   ,ZMRAB4N)
CALL SC2PRG(YM_RADTC%MRAB6C,ZRADTC   ,ZMRAB6C)
CALL SC2PRG(YM_RADTC%MRAB6N,ZRADTC   ,ZMRAB6N)
CALL SC2PRG(YM_RADTC%MRAT1C,ZRADTC   ,ZMRAT1C)
CALL SC2PRG(YM_RADTC%MRAT1N,ZRADTC   ,ZMRAT1N)
CALL SC2PRG(YM_RADTC%MRAT2C,ZRADTC   ,ZMRAT2C)
CALL SC2PRG(YM_RADTC%MRAT2N,ZRADTC   ,ZMRAT2N)
CALL SC2PRG(YM_RADTC%MRAT3C,ZRADTC   ,ZMRAT3C)
CALL SC2PRG(YM_RADTC%MRAT3N,ZRADTC   ,ZMRAT3N)
CALL SC2PRG(YM_RADTC%MRAT4C,ZRADTC   ,ZMRAT4C)
CALL SC2PRG(YM_RADTC%MRAT4N,ZRADTC   ,ZMRAT4N)
CALL SC2PRG(YM_RADTC%MRAT5C,ZRADTC   ,ZMRAT5C)
CALL SC2PRG(YM_RADTC%MRAT5N,ZRADTC   ,ZMRAT5N)
CALL SC2PRG(YT0%MT    ,PGMV    ,ZT0T)
CALL SC2PRG(YT0%MU    ,PGMV    ,ZT0U)
CALL SC2PRG(YT0%MV    ,PGMV    ,ZT0V)
CALL SC2PRG(YT9%MT    ,PGMV    ,ZT9T)
CALL SC2PRG(YT9%MU    ,PGMV    ,ZT9U)
CALL SC2PRG(YT9%MV    ,PGMV    ,ZT9V)
!     ------------------------------------------------------------------

!        1.0 Preliminary calculations.
!            -------------------------

TSPHY = MAX(PDTPHY,1.0_JPRB)

DO JLEV = 0, NFLEVG
  DO JROF = KST, KEND
    ZDIFCQ(JROF,JLEV) = 0.0_JPRB
    ZDIFCS(JROF,JLEV) = 0.0_JPRB
    ZDIFTQ(JROF,JLEV) = 0.0_JPRB
    ZDIFTS(JROF,JLEV) = 0.0_JPRB
    ZFPLCL(JROF,JLEV) = 0.0_JPRB
    ZFPLCN(JROF,JLEV) = 0.0_JPRB
    ZFPLSL(JROF,JLEV) = 0.0_JPRB
    ZFPLSN(JROF,JLEV) = 0.0_JPRB
    ZFCCQN(JROF,JLEV) = 0.0_JPRB
    ZFCCQL(JROF,JLEV) = 0.0_JPRB
    ZFCSQN(JROF,JLEV) = 0.0_JPRB
    ZFCSQL(JROF,JLEV) = 0.0_JPRB
    ZSTRCU(JROF,JLEV) = 0.0_JPRB
    ZSTRCV(JROF,JLEV) = 0.0_JPRB
    ZSTRDU(JROF,JLEV) = 0.0_JPRB
    ZSTRDV(JROF,JLEV) = 0.0_JPRB
    ZSTRTU(JROF,JLEV) = 0.0_JPRB
    ZSTRTV(JROF,JLEV) = 0.0_JPRB
    ZSTRMU(JROF,JLEV) = 0.0_JPRB
    ZSTRMV(JROF,JLEV) = 0.0_JPRB
    ZFRMH (JROF,JLEV) = 0.0_JPRB
    ZFDIS (JROF,JLEV) = 0.0_JPRB
  ENDDO
ENDDO

DO JSG  = 1, NTSSG+1
  DO JLEV = 0, NFLEVG
    DO JROF = KST, KEND
      ZFRSO(JROF,JLEV,JSG) = 0.0_JPRB
      ZFRTH(JROF,JLEV,JSG) = 0.0_JPRB
    ENDDO
  ENDDO
ENDDO

DO JLEV = 1, NFLEVG
  DO JROF = KST, KEND
    ZTENDU(JROF,JLEV) = 0.0_JPRB
    ZTENDQ(JROF,JLEV) = 0.0_JPRB
    ZTENDQL(JROF,JLEV) = 0.0_JPRB
    ZTENDQI(JROF,JLEV) = 0.0_JPRB
    ZTENDV(JROF,JLEV) = 0.0_JPRB
    ZTENDH(JROF,JLEV) = 0.0_JPRB
    ZCVGQ (JROF,JLEV) = 0.0_JPRB
  ENDDO
ENDDO

CALL CPPHINP(YDGEOMETRY,YDMODEL,KST,KEND,&
 & YDGSGEOM%GEMU,YDGSGEOM%GELAM,&
 & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),ZPQ5,ZPQ5L,ZPQ5M,ZPOUB,ZPOUB,&
 & PXYB5(1,1,YYTXYB5%M_RDELP),PCTY5(1,0,YYTCTY5%M_EVEL),ZPOUB,&
 & ZMU0,ZMU0LU,ZMU0M,ZMU0N,ZCVGQ5)  

IF (LRAYSP.AND.LRCOEF) THEN
  IFIELDSS=NG3SR*NFLEVG
  CALL RDRADCOEF(YDGEOMETRY,YDRCOEF,KST,KEND,KSTGLO,IFIELDSS,&
& ZMCOR  , ZMRAB3C, ZMRAB3N, ZMRAB4C, &
& ZMRAB4N, ZMRAB6C, ZMRAB6N, ZMRAT1C, &
& ZMRAT1N, ZMRAT2C, ZMRAT2N, ZMRAT3C, &
& ZMRAT3N, ZMRAT4C, ZMRAT4N, ZMRAT5C, &
& ZMRAT5N, ZAC_HC)
ENDIF

!     ------------------------------------------------------------------

!        1.4 Compute the trajectories for phys.fluxes and tendencies
!            --------------------------------------------------------

IF (LTRAJPST.AND.(.NOT.LELAM.OR.(LELAM.AND.(MSTEPTRAJW(NSTEP)>0)))) THEN
  CALL RDPHTRAJTM(YDGEOMETRY,YDSIMPHL,KST,KEND,PTRAJ_PHYS_TLAD,&
   & ZCVGQ5 ,&
   & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
   & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
   & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,ZFRSO5 ,ZFRTH5 ,&
   & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,ZSTRTU5,ZSTRTV5,&
   & ZSTRMU5,ZSTRMV5,ZFRMH5 ,&
   & ZKTROV5,ZKUROV5,ZXTROV5,ZXUROV5,&
   & ZCDROV5,ZCHROV5,ZXDROV5,ZXHROV5,ZTRIGCONV5,ZTRAJGWD5,&
   & ZQL5,ZQI5,ZQR5,ZQS5,ZQLISCVPPKF,ZNEBSCVPPKF,&
   & ZTENDU5,ZTENDV5,ZTENDH5,ZTENDQ5,ZTENDQL5,ZTENDQI5 )  
ELSE

  IF (LTWOTL) THEN

    ! For OOPS : Call trajectory calculation available in TL code in order to emulate LTRAJPST=T
    ! This replaces aplparsadt cptendsmat calls which are obsolete

    ISTEP_NOT_USED=-1
    ISTGLO_NOT_USED=-1
    ZNOTUSED1=0.0_JPRB
    ZNOTUSED2=0.0_JPRB
    ZNOTUSED3=0.0_JPRB
    ZNOTUSED4=0.0_JPRB
    ZNOTUSED5=0.0_JPRB

    CALL APLPARSTL(YDGEOMETRY,YDMODEL%YRML_PHY_MF,KST,KEND,ISTGLO_NOT_USED,NPROMA,1,NFLEVG,NTSSG,ISTEP_NOT_USED,&
     & PHI5,PRE5,PHIF5,PRE5F,PXYB5(1,1,YYTXYB5%M_DELP),PXYB5(1,1,YYTXYB5%M_RDELP),&
     & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),PGMVT5(1,1,YT5%MT),ZPQ5,&
     & ZPL5,ZPI5,PRCP5(1,1,YYTRCP5%M_CP),ZCVGQ5,&
     & ZNOTUSED1,ZNOTUSED1,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,&
     & ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,&
     & ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED3,&
     & PSNS,PTS,PQS,PTS1,PQS1,&
     & PSD_VF5(1,YSD_VF%YGETRL%MP),PSD_VF5(1,YSD_VF%YLSM%MP),&
     & PSD_VF5(1,YSD_VF%YZ0F%MP),PSD_VF5(1,YSD_VF%YVRLAN%MP),PSD_VF5(1,YSD_VF%YVRLDI%MP),&
     & PSD_VF5(1,YSD_VF%YALBF%MP),ZMU0,YDGSGEOM%GM,&
     & ZAC_HC,ZRADTC(1,1,YM_RADTC%MCOR),&
     & ZMRAB3C,ZMRAB3N,&
     & ZMRAB4C,ZMRAB4N,&
     & ZMRAB6C,ZMRAB6N,&
     & ZMRAT1C,ZMRAT1N,&
     & ZMRAT2C,ZMRAT2N,&
     & ZMRAT3C,ZMRAT3N,&
     & ZMRAT4C,ZMRAT4N,&
     & ZMRAT5C,ZMRAT5N,&
     & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
     & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
     & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,ZFRSO5,ZFRTH5,&
     & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,ZSTRTU5,ZSTRTV5,&
     & ZKTROV5,ZKUROV5,ZXTROV5,ZXUROV5,&
     & ZCDROV5,ZCHROV5,ZXDROV5,ZXHROV5,ZTRAJGWD5,&
     & ZQL5,ZQI5,ZQR5,ZQS5,ZQLISCVPPKF,ZNEBSCVPPKF,&
     & ZSTRMU5,ZSTRMV5,ZFRMH5,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED5,ZNOTUSED5,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZTRIGCONV5,PTRAJ_PHYS)

    CALL CPTENDSMTL(YDMODEL%YRML_PHY_MF%YRPHY,YDSIMPHL,NPROMA,KST,KEND,NFLEVG,&
     & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
     & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
     & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,&
     & ZFRSO5,ZFRTH5,&
     & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,&
     & ZSTRTU5,ZSTRTV5,&
     & ZSTRMU5,ZSTRMV5,ZFRMH5,&
     & PXYB5(1,1,YYTXYB5%M_RDELP),PHIF5,&
     & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),PGMVT5(1,1,YT5%MT),&
     & ZPQ5,ZPL5,ZPI5,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED5,ZNOTUSED5,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED4,ZNOTUSED4,ZNOTUSED4,&
     & ZNOTUSED2,ZNOTUSED2,&
     & ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,&
     & ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,&
     & PQS,PTS,YDOROG%OROG,&
     & ZTENDU5,ZTENDV5,ZTENDH5,ZTENDQ5,ZTENDQL5,ZTENDQI5,&
     & ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2,ZNOTUSED2)

  ELSE
    CALL ABOR1("MF_PHYSAD:  LTRAJPST=F && LTWOTL=F not implemented")
  ENDIF

ENDIF

!        1.3 Computation of evolution of T, u, v and Q
!            -----------------------------------------

! * Calculation of IPQ, since the old pointer
!   MSLB1[X]9 (=MSLB1GFL9+IP[X]) do not exist any longer in PTRSLB1.
!   (In the future, CPTENDSMAD and CPUTQYSAD must be
!   rewritten in order to treat the GFL globally and not
!   individually).

IPQ=(YQ%MP_SL1-1)*(NFLEVG+2*NFLSUL)
IPL=(YL%MP_SL1-1)*(NFLEVG+2*NFLSUL)
IPI=(YI%MP_SL1-1)*(NFLEVG+2*NFLSUL)

ISLB1U9  = MSLB1U9
ISLB1V9  = MSLB1V9
ISLB1T9  = MSLB1T9
ISLB1GFL9= MSLB1GFL9

CALL CPUTQYSAD(YDGEOMETRY%YRDIMV,YDGMV,YGFL,YDPTRSLB1,NPROMA,KST,KEND,NFLEVG,PDTPHY,IPQ,IPL,IPI,&
   & ISLB1U9,ISLB1V9,ISLB1T9,ISLB1GFL9,&
   & ZTENDH5,ZTENDQ5,ZTENDQL5,ZTENDQI5,ZTENDU5,ZTENDV5,&
   & PRCP95(1,1,YYTRCP95%M_CP),PXYB95(1,1,YYTXYB95%M_DELP),&
   & PGMVT95(1,1,YYTGMVT95%M_T),PGMVT95(1,1,YYTGMVT95%M_U),PGMVT95(1,1,YYTGMVT95%M_V),&
   & PRCP5(1,1,YYTRCP5%M_CP),PXYB5(1,1,YYTXYB5%M_DELP),PGMVT5(1,1,YT5%MT),PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),&
   & ZTENDH, ZTENDQ,ZTENDQL,ZTENDQI, ZTENDU, ZTENDV,&
   & PRCP9(1,1,YYTRCP9%M_CP),PXYB9(1,1,YYTXYB9%M_DELP),ZT9T,ZT9U,ZT9V,&
   & PRCP0(1,1,YYTRCP0%M_CP),PXYB0(1,1,YYTXYB0%M_DELP),ZT0T,ZT0U,ZT0V,&
   & PB1, PGMVT1, PGFLT1,&
   & ZFDIS)


!        1.2 Computation of tendencies T,u,v and Q
!            -------------------------------------

IF (LTWOTL) THEN

  CALL CPTENDSMAD (YDMODEL%YRML_PHY_MF%YRPHY,YDSIMPHL,NPROMA,KST,KEND,NFLEVG,&
   & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
   & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
   & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,&
   & ZFRSO5,ZFRTH5,&
   & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,&
   & ZSTRTU5,ZSTRTV5,&
   & ZSTRMU5,ZSTRMV5,ZFRMH5 ,&
   & PXYB5(1,1,YYTXYB5%M_RDELP),PHIF5,&
   & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),PGMVT5(1,1,YT5%MT),&
   & ZPQ5,ZPL5,ZPI5,&
   & ZDIFCQ,ZDIFCS,ZDIFTQ,ZDIFTS,&
   & ZFCCQL,ZFCCQN,ZFCSQL,ZFCSQN,&
   & ZFPLCL,ZFPLCN,ZFPLSL,ZFPLSN,&
   & ZFRSO,ZFRTH,&
   & ZSTRCU,ZSTRCV,ZSTRDU,ZSTRDV,&
   & ZSTRTU,ZSTRTV,&
   & ZSTRMU,ZSTRMV,ZFRMH,&
   & PXYB0(1,1,YYTXYB0%M_RDELP),PHIF0,&
   & ZT0U,ZT0V,ZT0T,&
   & ZPQ,ZPL,ZPI,&
   & PQS,PTS,YDOROG%OROG,&
   & ZTENDU5,ZTENDV5,ZTENDH5,ZTENDQ5,ZTENDQL5,ZTENDQI5,&
   & ZTENDU,ZTENDV,ZTENDH,ZTENDQ,ZTENDQL,ZTENDQI)  

ELSE

  CALL CPTENDSMAD (YDMODEL%YRML_PHY_MF%YRPHY,YDSIMPHL,NPROMA,KST,KEND,NFLEVG,&
   & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
   & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
   & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,&
   & ZFRSO5,ZFRTH5,&
   & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,&
   & ZSTRTU5,ZSTRTV5,&
   & ZSTRMU5,ZSTRMV5,ZFRMH5 ,&
   & PXYB95(1,1,YYTXYB95%M_RDELP),PHIF95,&
   & PGMVT95(1,1,YYTGMVT95%M_U),PGMVT95(1,1,YYTGMVT95%M_V),PGMVT95(1,1,YYTGMVT95%M_T),&
   & PGFLT95(1,1,YYTGFLT95%M_Q),PGFLT95(1,1,YYTGFLT95%M_L),PGFLT95(1,1,YYTGFLT95%M_I),&
   & ZDIFCQ,ZDIFCS,ZDIFTQ,ZDIFTS,&
   & ZFCCQL,ZFCCQN,ZFCSQL,ZFCSQN,&
   & ZFPLCL,ZFPLCN,ZFPLSL,ZFPLSN,&
   & ZFRSO,ZFRTH,&
   & ZSTRCU,ZSTRCV,ZSTRDU,ZSTRDV,&
   & ZSTRTU,ZSTRTV,&
   & ZSTRMU,ZSTRMV,ZFRMH,&
   & PXYB9(1,1,YYTXYB9%M_RDELP),PHIF9,&
   & ZT9U,ZT9V,ZT9T,&
   & ZPQ9,ZPL9,ZPI9,&
   & PQS,PTS,YDOROG%OROG,&
   & ZTENDU5,ZTENDV5,ZTENDH5,ZTENDQ5,ZTENDQL5,ZTENDQI5,&
   & ZTENDU,ZTENDV,ZTENDH,ZTENDQ,ZTENDQL,ZTENDQI)  

ENDIF

!        1.1 Call parametrization
!            --------------------

IF (LTWOTL) THEN

  CALL APLPARSAD(YDGEOMETRY,YDMODEL%YRML_PHY_MF,KST,KEND,NPROMA,1,NFLEVG,NTSSG,&
   & PHI5,PRE5,PHIF5,PRE5F,PXYB5(1,1,YYTXYB5%M_DELP),PXYB5(1,1,YYTXYB5%M_RDELP),&
   & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),PGMVT5(1,1,YT5%MT),&
   & ZPQ5,ZPL5,ZPI5,PRCP5(1,1,YYTRCP5%M_CP),ZCVGQ5,&
   & PHI0,PRE0,PHIF0,PRE0F,PXYB0(1,1,YYTXYB0%M_DELP),PXYB0(1,1,YYTXYB0%M_RDELP),&
   & ZT0U,ZT0V,ZT0T,&
   & ZPQ,ZPL,ZPI,PRCP0(1,1,YYTRCP0%M_CP),ZCVGQ,&
   & PSNS,PTS,PQS,PTS1,PQS1,&
   & PSD_VF5(1,YSD_VF%YGETRL%MP),PSD_VF5(1,YSD_VF%YLSM%MP),&
   & PSD_VF5(1,YSD_VF%YZ0F%MP),PSD_VF5(1,YSD_VF%YVRLAN%MP),PSD_VF5(1,YSD_VF%YVRLDI%MP),&
   & PSD_VF5(1,YSD_VF%YALBF%MP),ZMU0,YDGSGEOM%GM,&
   & ZAC_HC,ZMCOR,&
   & ZMRAB3C,ZMRAB3N,&
   & ZMRAB4C,ZMRAB4N,&
   & ZMRAB6C,ZMRAB6N,&
   & ZMRAT1C,ZMRAT1N,&
   & ZMRAT2C,ZMRAT2N,&
   & ZMRAT3C,ZMRAT3N,&
   & ZMRAT4C,ZMRAT4N,&
   & ZMRAT5C,ZMRAT5N,&
   & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
   & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
   & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,ZFRSO5,ZFRTH5,&
   & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,ZSTRTU5,ZSTRTV5,&
   & ZKTROV5,ZKUROV5,ZXTROV5,ZXUROV5,&
   & ZCDROV5,ZCHROV5,ZXDROV5,ZXHROV5,ZTRAJGWD5,&
   & ZQL5,ZQI5,ZQR5,ZQS5,ZQLISCVPPKF,ZNEBSCVPPKF,&
   & ZSTRMU5,ZSTRMV5,ZFRMH5 ,&
   & ZDIFCQ,ZDIFCS,ZDIFTQ,ZDIFTS,&
   & ZFCCQL,ZFCCQN,ZFCSQL,ZFCSQN,&
   & ZFPLCL,ZFPLCN,ZFPLSL,ZFPLSN,ZFRSO,ZFRTH,&
   & ZSTRCU,ZSTRCV,ZSTRDU,ZSTRDV,ZSTRTU,ZSTRTV,&
   & ZSTRMU,ZSTRMV,ZFRMH,ZTRIGCONV5)

ELSE

  CALL APLPARSAD(YDGEOMETRY,YDMODEL%YRML_PHY_MF,KST,KEND,NPROMA,1,NFLEVG,NTSSG,&
   & PHI95,PRE95,PHIF95,PRE95F,PXYB95(1,1,YYTXYB95%M_DELP),PXYB95(1,1,YYTXYB95%M_RDELP),&
   & PGMVT95(1,1,YYTGMVT95%M_U),PGMVT95(1,1,YYTGMVT95%M_V),PGMVT95(1,1,YYTGMVT95%M_T),&
   & PGFLT95(1,1,YYTGFLT95%M_Q),PGFLT95(1,1,YYTGFLT95%M_L),PGFLT95(1,1,YYTGFLT95%M_I),PRCP95(1,1,YYTRCP95%M_CP),ZCVGQ5,&
   & PHI9,PRE9,PHIF9,PRE9F,PXYB9(1,1,YYTXYB9%M_DELP),PXYB9(1,1,YYTXYB9%M_RDELP),&
   & ZT9U,ZT9V,ZT9T,&
   & ZPQ9,ZPL9,ZPI9,PRCP9(1,1,YYTRCP9%M_CP),ZCVGQ,&
   & PSNS,PTS,PQS,PTS1,PQS1,&
   & PSD_VF5(1,YSD_VF%YGETRL%MP),PSD_VF5(1,YSD_VF%YLSM%MP),&
   & PSD_VF5(1,YSD_VF%YZ0F%MP),PSD_VF5(1,YSD_VF%YVRLAN%MP),PSD_VF5(1,YSD_VF%YVRLDI%MP),&
   & PSD_VF5(1,YSD_VF%YALBF%MP),ZMU0,YDGSGEOM%GM,&
   & ZAC_HC,ZMCOR,&
   & ZMRAB3C,ZMRAB3N,&
   & ZMRAB4C,ZMRAB4N,&
   & ZMRAB6C,ZMRAB6N,&
   & ZMRAT1C,ZMRAT1N,&
   & ZMRAT2C,ZMRAT2N,&
   & ZMRAT3C,ZMRAT3N,&
   & ZMRAT4C,ZMRAT4N,&
   & ZMRAT5C,ZMRAT5N,&
   & ZDIFCQ5,ZDIFCS5,ZDIFTQ5,ZDIFTS5,&
   & ZFCCQL5,ZFCCQN5,ZFCSQL5,ZFCSQN5,&
   & ZFPLCL5,ZFPLCN5,ZFPLSL5,ZFPLSN5,ZFRSO5,ZFRTH5,&
   & ZSTRCU5,ZSTRCV5,ZSTRDU5,ZSTRDV5,ZSTRTU5,ZSTRTV5,&
   & ZKTROV5,ZKUROV5,ZXTROV5,ZXUROV5,&
   & ZCDROV5,ZCHROV5,ZXDROV5,ZXHROV5,ZTRAJGWD5,&
   & ZQL5,ZQI5,ZQR5,ZQS5,ZQLISCVPPKF,ZNEBSCVPPKF,&
   & ZSTRMU5,ZSTRMV5,ZFRMH5 ,&
   & ZDIFCQ,ZDIFCS,ZDIFTQ,ZDIFTS,&
   & ZFCCQL,ZFCCQN,ZFCSQL,ZFCSQN,&
   & ZFPLCL,ZFPLCN,ZFPLSL,ZFPLSN,ZFRSO,ZFRTH,&
   & ZSTRCU,ZSTRCV,ZSTRDU,ZSTRDV,ZSTRTU,ZSTRTV,&
   & ZSTRMU,ZSTRMV,ZFRMH,ZTRIGCONV5)

ENDIF

IF (.NOT.LPROCLDTL) THEN
  CALL CPPHINPAD(YDGEOMETRY,KST,KEND,&
   & PGMVT5(1,1,YT5%MU),PGMVT5(1,1,YT5%MV),ZPQ5,ZPQ5L,ZPQ5M,&
   & PXYB5(1,1,YYTXYB5%M_RDELP),PCTY5(1,0,YYTCTY5%M_EVEL),&
   & ZT0U,ZT0V,ZPQ,ZPQL,ZPQM,&
   & PXYB0(1,1,YYTXYB0%M_RDELP),PCTY0(1,0,YYTCTY0%M_EVEL),&
   & ZCVGQ5,ZCVGQ)  
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('MF_PHYSAD',1,ZHOOK_HANDLE)
END SUBROUTINE MF_PHYSAD
!   R. El Khatib 27-02-2019 Use pointer function SC2PRG to avoid bounds violation
