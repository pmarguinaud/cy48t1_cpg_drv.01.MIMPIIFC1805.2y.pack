SUBROUTINE PROFILECHET(YDGEOMETRY,&
 & YDCPG_MISC,YDMF_PHYS,YDCPG_DYN0,YDMF_PHYS_SURF,YDVARS,&
 & YDSURF,&
 & YDDPHY,YDRIP,YDML_PHY_MF,KLON,&
 & PGELAM, PGEMU,&
 & PGM, PMU0, POROG, PRCORI, PRATATH, PRATATX,&
 & PCVGQ, PLCVQ,&
 & PFPLCH, PFPLSH )

USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_DYN_TYPE, &
                              & CPG_MISC_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMRIP       , ONLY : TRIP
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1  ,ONLY : JPIM    ,JPRB
USE YOMHOOK   ,ONLY : LHOOK   ,DR_HOOK
USE YOMCHET   ,ONLY : GCHET   ,GCHETN
USE YOMDPHY   ,ONLY : TDPHY
USE YOMLUN    ,ONLY : NULOUT

IMPLICIT NONE

! **** *PROFILECHET* 

!---------------------------------------------------------------- 
!   Purpose
!   -------
!   Output profiles for SCM for points with physical tendencies exceding 
!   the threshold.

!----------------------------------------------------------------!
!   Author
!   ------
!   2004-03-01: T.Kovacic, J.M.Piriou, F.Bouyssel

!   Modifications
!   -------------
!   2004-10, F. Bouyssel : cleaning
!   2007-05, F. Bouyssel : add PFCLL, PFCLN
!   2011-06: M. Jerczynski - some cleaning to meet norms

!----------------------------------------------------------------

!-------------------------------------------------
!  Dummy variables 
!-------------------------------------------------

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(CPG_MISC_TYPE),INTENT(INOUT):: YDCPG_MISC
TYPE(MF_PHYS_TYPE),INTENT(INOUT) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),INTENT(INOUT) :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),INTENT(INOUT) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),INTENT(INOUT) :: YDVARS
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(TDPHY)       ,INTENT(INOUT) :: YDDPHY
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(INOUT):: YDML_PHY_MF
TYPE(TRIP)        ,INTENT(INOUT) :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA)


REAL(KIND=JPRB)   ,INTENT(IN)    :: PRCORI(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRATATH(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRATATX(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVGQ(YDGEOMETRY%YRDIM%NPROMM,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLCVQ(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLCH(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFPLSH(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
LOGICAL :: LLTEST
INTEGER(KIND=JPIM) :: I, J, IIND
INTEGER(KIND=JPIM), DIMENSION(GCHET%NPHTEXTH) :: JIND
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "writeprofile.intfb.h"
!-------------------------------------------------
! Check arguments.
!-------------------------------------------------

IF (LHOOK) CALL DR_HOOK('PROFILECHET', 0, ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, NPROMM=>YDDIM%NPROMM, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & NTSSG=>YDDPHY%NTSSG)
! ----------------------------------------------------
  
  ! deleting multiple points over the same geographical point
IIND = 1
JIND(IIND) = 1
DO I = 2, GCHET%NPHTEXTH
  LLTEST=.TRUE.
  DO J = 1, I-1
    IF(GCHET%APOINT(I)%NCHETLONG==GCHET%APOINT(J)%NCHETLONG) THEN
      LLTEST=.FALSE.
      EXIT
    ENDIF
  ENDDO
  IF (LLTEST) THEN
    IIND = IIND + 1
    JIND(IIND) = I
  ENDIF
ENDDO

GCHET%NPHTEXTH = IIND
DO I = 1, IIND
  GCHET%APOINT(I) = GCHET%APOINT(JIND(I))
ENDDO

IF (GCHET%NPHTEXTH>GCHETN%NPROFMAX) THEN
  WRITE(NULOUT,*) 'WARNING : MAX NUMBER OF PROFILES REACHED IN PROFILECHET!'
  GCHET%NPHTEXTH = GCHETN%NPROFMAX
ENDIF

DO J = 1, GCHET%NPHTEXTH
  CALL WRITEPROFILE(YDGEOMETRY, YDSURF, YDDPHY, YDRIP, YDML_PHY_MF, 'CHET.PROFILE.', GCHET%APOINT(J)%NCHETLONG,                &
  & GCHET%APOINT(J)%ALAT, GCHET%APOINT(J)%ALONG, 0._JPRB, PGELAM, PGEMU, PGM, PMU0, POROG, YDCPG_DYN0%OROGL,                   &
  & YDCPG_DYN0%OROGM, PRCORI, PRATATH, PRATATX, YDMF_PHYS_SURF%GSD_VF%PLSM, YDMF_PHYS_SURF%GSD_VV%PARG,                        &
  & YDMF_PHYS_SURF%GSD_VV%PSAB, YDMF_PHYS_SURF%GSD_VF%PALBF, YDMF_PHYS_SURF%GSD_VF%PALBSF, YDMF_PHYS_SURF%GSD_VF%PEMISF,       &
  & YDMF_PHYS_SURF%GSD_VV%PD2, YDMF_PHYS_SURF%GSD_VV%PIVEG, YDMF_PHYS_SURF%GSD_VV%PLAI, YDMF_PHYS%OUT%CT,                      &
  & YDMF_PHYS_SURF%GSD_VF%PZ0F, YDMF_PHYS_SURF%GSD_VV%PZ0H, YDMF_PHYS_SURF%GSD_VF%PZ0RLF, YDMF_PHYS_SURF%GSD_VF%PGETRL,        &
  & YDMF_PHYS_SURF%GSD_VF%PVRLAN, YDMF_PHYS_SURF%GSD_VF%PVRLDI, YDMF_PHYS_SURF%GSD_VV%PRSMIN, YDMF_PHYS_SURF%GSD_VF%PVEG,      &
  & YDMF_PHYS_SURF%GSD_VV%PHV, YDMF_PHYS_SURF%GSD_VA%PSEA, YDMF_PHYS_SURF%GSD_VA%PLAN, YDMF_PHYS_SURF%GSD_VA%PSOO,             &
  & YDMF_PHYS_SURF%GSD_VA%PDES, YDMF_PHYS_SURF%GSD_VC%PGROUP, YDVARS%SP%T0, YDVARS%SP%DL, YDVARS%SP%DM,                        &
  & YDVARS%T%T0, YDVARS%T%DL, YDVARS%T%DM, YDVARS%Q%T0, YDVARS%Q%DL, YDVARS%Q%DM, YDVARS%U%T0, YDVARS%V%T0,                    &
  & YDVARS%VOR%T0, YDVARS%DIV%T0, PCVGQ, PLCVQ, YDMF_PHYS_SURF%GSP_RR%PT_T9, YDMF_PHYS_SURF%GSP_SB%PT_T9,                      &
  & YDMF_PHYS_SURF%GSP_RR%PFC_T9, YDMF_PHYS_SURF%GSP_RR%PW_T9, YDMF_PHYS_SURF%GSP_RR%PIC_T9, YDMF_PHYS_SURF%GSP_SB%PQ_T9,      &
  & YDMF_PHYS_SURF%GSP_SB%PTL_T9, YDMF_PHYS_SURF%GSP_SG%PF_T9, YDMF_PHYS%OUT%DIFCQ, YDMF_PHYS%OUT%DIFCQN,                      &
  & YDMF_PHYS%OUT%DIFCQL, YDMF_PHYS%OUT%DIFCS, YDMF_PHYS%OUT%DIFTQ, YDMF_PHYS%OUT%DIFTQN, YDMF_PHYS%OUT%DIFTQL,                &
  & YDMF_PHYS%OUT%DIFTS, YDMF_PHYS%OUT%FCCQL, YDMF_PHYS%OUT%FCCQN, YDMF_PHYS%OUT%FCSQL, YDMF_PHYS%OUT%FCSQN,                   &
  & YDMF_PHYS%OUT%FCQNG, YDMF_PHYS%OUT%FCQNNG, YDMF_PHYS%OUT%FCQLNG, YDMF_PHYS%OUT%FPLCL, YDMF_PHYS%OUT%FPLCN,                 &
  & YDMF_PHYS%OUT%FPLSL, YDMF_PHYS%OUT%FPLSN, YDMF_PHYS%OUT%FRSO, YDMF_PHYS%OUT%FRTH, YDMF_PHYS%OUT%STRCU,                     &
  & YDMF_PHYS%OUT%STRCV, YDMF_PHYS%OUT%STRDU, YDMF_PHYS%OUT%STRDV, YDMF_PHYS%OUT%STRTU, YDMF_PHYS%OUT%STRTV,                   &
  & YDMF_PHYS%OUT%STRMU, YDMF_PHYS%OUT%STRMV, YDMF_PHYS%OUT%FRMH, YDMF_PHYS%OUT%FCHOZ, YDCPG_MISC%NEB,                         &
  & YDCPG_MISC%QICE, YDCPG_MISC%QLI, YDCPG_MISC%RH, YDMF_PHYS%OUT%FCS, YDMF_PHYS%OUT%FCLL, YDMF_PHYS%OUT%FCLN,                 &
  & YDMF_PHYS%OUT%FEVL, YDMF_PHYS%OUT%FEVN, YDMF_PHYS%OUT%FEVV, YDMF_PHYS%OUT%FTR, YDMF_PHYS%OUT%FLWSP,                        &
  & YDMF_PHYS%OUT%FONTE, YDMF_PHYS%OUT%FGEL, YDMF_PHYS%OUT%FGELS, YDMF_PHYS%OUT%FRSODS, YDMF_PHYS%OUT%FRSOPS,                  &
  & YDMF_PHYS%OUT%FRSOPT, YDMF_PHYS%OUT%FRTHDS, YDCPG_MISC%QS, YDMF_PHYS%OUT%RUISL, YDMF_PHYS%OUT%RUISP,                       &
  & YDMF_PHYS%OUT%RUISS, YDMF_PHYS%OUT%UCLS, YDMF_PHYS%OUT%VCLS, YDMF_PHYS%OUT%TCLS, YDMF_PHYS%OUT%QCLS,                       &
  & YDMF_PHYS%OUT%RHCLS, YDCPG_MISC%CLCT, YDMF_PHYS%OUT%CLCH, YDMF_PHYS%OUT%CLCM, YDMF_PHYS%OUT%CLCL,                          &
  & YDMF_PHYS%OUT%CLCC, PFPLCH, PFPLSH, YDMF_PHYS_SURF%GSD_VH%PTCCH, YDMF_PHYS_SURF%GSD_VH%PSCCH, YDMF_PHYS_SURF%GSD_VH%PBCCH, &
  & YDMF_PHYS_SURF%GSD_VH%PPBLH)
ENDDO
GCHET%NPHTEXTH = 0

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('PROFILECHET', 1, ZHOOK_HANDLE)

END SUBROUTINE PROFILECHET
