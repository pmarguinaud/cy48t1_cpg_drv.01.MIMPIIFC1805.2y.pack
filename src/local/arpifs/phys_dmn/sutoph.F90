!OPTIONS XOPT(NOEVAL)
SUBROUTINE SUTOPH(YDSTA,YDDIMV,YDEPHY,YDPHY,YDTOPH,KULOUT)

!**** *SUTOPH*   - Initialize common YOMTOPH top parameterization

!     Purpose.
!     --------
!           Initialize YOMTOPH, the common that contains the top pressure
!           and the first level of parameterization
!           it also contains mesospheric drag vertical profil

!**   Interface.
!     ----------
!        *CALL* *SUTOPH(...)

!        Explicit arguments :
!        --------------------
!        KULOUT : Logical unit for the output

!        Implicit arguments :
!        --------------------
!        COMMON YOMTOPH, YOMSTA

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        Documentation ARPEGE

!     Author.
!     -------
!      A. Lasserre-Bigorry
!      Original : 91-06-10

!     Modifications.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      F.Bouyssel 04-11-22 : NTCOET,ETCOET
!      P. Marquet 05-09-12 : NTAJUC
!      M. Deque   05-09-12 : default RCLX
!      M. Deque   05-09-12 : default TPSCLIM
!      E. Bazile and I. Beau 2011-01-18 : NTRELAX (T,Q,U) for 1D model MUSC
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      Y. Bouteloup 2013-11-04 : New formulation of the drag meso for Qv
!     ------------------------------------------------------------------

USE YOMSTA   , ONLY : TSTA
USE YOMDIMV  , ONLY : TDIMV
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK    ,DR_HOOK
USE YOMLUN   , ONLY : NULNAM
USE YOMCT0   , ONLY : LECMWF
USE YOMTOPH  , ONLY : TTOPH
USE YOMPHY   , ONLY : TPHY
USE YOEPHY   , ONLY : TEPHY

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(TSTA)        , INTENT(IN)          :: YDSTA
TYPE(TDIMV)       , INTENT(IN)          :: YDDIMV
TYPE(TEPHY)        ,INTENT(INOUT)       :: YDEPHY
TYPE(TPHY)         ,INTENT(INOUT)       :: YDPHY
TYPE(TTOPH)        ,INTENT(INOUT),TARGET:: YDTOPH
INTEGER(KIND=JPIM), INTENT(IN)          :: KULOUT 

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JLEV

REAL(KIND=JPRB) :: PAP, PAPX, ZMEST, ZMESU, ZMESQ

REAL(KIND=JPRB) :: PMESQF
REAL(KIND=JPRB) :: PMESTF
REAL(KIND=JPRB) :: PMESUF
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

REAL(KIND=JPRB) , POINTER ::  ETCOET
REAL(KIND=JPRB) , POINTER ::  ETOZON
INTEGER(KIND=JPIM) , POINTER ::  NTAJUC
REAL(KIND=JPRB) , POINTER ::  ETRELAXU
REAL(KIND=JPRB) , POINTER ::  XDRMTK
REAL(KIND=JPRB) , POINTER ::  RCLX
REAL(KIND=JPRB) , POINTER ::  ETCVIM
REAL(KIND=JPRB) , POINTER ::  ETDRAG
REAL(KIND=JPRB) , POINTER ::  XDRMUK
REAL(KIND=JPRB) , POINTER ::  RFMESOQ
REAL(KIND=JPRB) , POINTER ::  XDRMQK
REAL(KIND=JPRB) , POINTER ::  ETRELAXQ
REAL(KIND=JPRB) , POINTER ::  ETRADI
REAL(KIND=JPRB) , POINTER ::  ETDIFU
REAL(KIND=JPRB) , POINTER ::  ETNEBU
REAL(KIND=JPRB) , POINTER ::  XDRMQP
REAL(KIND=JPRB) , POINTER ::  ETDRME
REAL(KIND=JPRB) , POINTER ::  ETCOEF
REAL(KIND=JPRB) , POINTER ::  XDRMUP
REAL(KIND=JPRB) , POINTER ::  ETRELAXT
REAL(KIND=JPRB) , POINTER ::  XDRMQX
REAL(KIND=JPRB) , POINTER ::  ETQSAT
REAL(KIND=JPRB) , POINTER ::  ETAJUC
REAL(KIND=JPRB) , POINTER ::  XDRMUX
REAL(KIND=JPRB) , POINTER ::  ETPLUI
REAL(KIND=JPRB) , POINTER ::  ET850
REAL(KIND=JPRB) , POINTER ::  ET950
REAL(KIND=JPRB) , POINTER ::  XDRMTX
REAL(KIND=JPRB) , POINTER ::  XDRMTP
REAL(KIND=JPRB) , POINTER ::  TPSCLIM
#include "namtoph.nam.h"

!     ------------------------------------------------------------------

!*    Mesospheric drag shape function

!     PMESUF(PAP,PAPX) = MAX( (PAPX-PAP)/MAX(PAP**1.5,1.E-10),0. )
PMESUF(PAP,PAPX) = MAX( (PAPX-PAP)/MAX(PAP,1.E-10_JPRB),0.0_JPRB )
PMESTF(PAP,PAPX) = MAX( (PAPX-PAP)/MAX(PAP,1.E-10_JPRB),0.0_JPRB )
PMESQF(PAP,PAPX) = MAX( (PAPX-PAP)/MAX(PAPX,1.E-10_JPRB),0.0_JPRB )
!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "posnam.intfb.h"
#include "seapre.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUTOPH',0,ZHOOK_HANDLE)
ASSOCIATE(NFLEVG=>YDDIMV%NFLEVG, &
 & NTRELAXQ=>YDTOPH%NTRELAXQ, NTRADI=>YDTOPH%NTRADI, &
 & NTRELAXT=>YDTOPH%NTRELAXT, NTRELAXU=>YDTOPH%NTRELAXU, &
 & NT850=>YDTOPH%NT850, NT950=>YDTOPH%NT950, &
 & NTPLUI=>YDTOPH%NTPLUI, NTCVIM=>YDTOPH%NTCVIM, NTNEBU=>YDTOPH%NTNEBU, &
 & NTCOET=>YDTOPH%NTCOET, NTDRAG=>YDTOPH%NTDRAG, NTDIFU=>YDTOPH%NTDIFU, &
 & RMESOQ=>YDTOPH%RMESOQ, RMESOT=>YDTOPH%RMESOT, RMESOU=>YDTOPH%RMESOU, &
 & NTOZON=>YDTOPH%NTOZON, NTQSAT=>YDTOPH%NTQSAT, NTCOEF=>YDTOPH%NTCOEF, &
 & NTDRME=>YDTOPH%NTDRME, &
 & LRRMES=>YDPHY%LRRMES, LRAYFM=>YDPHY%LRAYFM, LRAY=>YDPHY%LRAY, &
 & LRAYFM15=>YDPHY%LRAYFM15, LAGPHY=>YDEPHY%LAGPHY, &
 & STPRE=>YDSTA%STPRE)
XDRMTK => YDTOPH%XDRMTK
ETCOEF => YDTOPH%ETCOEF
ETNEBU => YDTOPH%ETNEBU
XDRMTX => YDTOPH%XDRMTX
ETCOET => YDTOPH%ETCOET
XDRMTP => YDTOPH%XDRMTP
ETRELAXT => YDTOPH%ETRELAXT
RCLX => YDTOPH%RCLX
ETCVIM => YDTOPH%ETCVIM
ETAJUC => YDTOPH%ETAJUC
RFMESOQ => YDTOPH%RFMESOQ
ETQSAT => YDTOPH%ETQSAT
ETRELAXQ => YDTOPH%ETRELAXQ
XDRMQP => YDTOPH%XDRMQP
ETRELAXU => YDTOPH%ETRELAXU
XDRMUK => YDTOPH%XDRMUK
XDRMQX => YDTOPH%XDRMQX
ETDRAG => YDTOPH%ETDRAG
XDRMUX => YDTOPH%XDRMUX
XDRMQK => YDTOPH%XDRMQK
ETRADI => YDTOPH%ETRADI
ETDRME => YDTOPH%ETDRME
XDRMUP => YDTOPH%XDRMUP
ETOZON => YDTOPH%ETOZON
ETPLUI => YDTOPH%ETPLUI
ET850 => YDTOPH%ET850
ET950 => YDTOPH%ET950
NTAJUC => YDTOPH%NTAJUC
TPSCLIM => YDTOPH%TPSCLIM
ETDIFU => YDTOPH%ETDIFU
!     ------------------------------------------------------------------

!*       1.    Set default values.
!              -------------------

!        1.1 Set implicit default values

ETQSAT=0._JPRB
ETDIFU=0._JPRB
ETCOEF=0._JPRB
ETDRAG=0._JPRB
ETCVIM=0._JPRB
ETPLUI=0._JPRB
ET850=85000._JPRB
ET950=95000._JPRB
ETRADI=0._JPRB
ETNEBU=0._JPRB
ETOZON=0._JPRB
ETDRME=0._JPRB
ETCOET=0._JPRB
ETAJUC=0._JPRB
ETRELAXT=0._JPRB
ETRELAXQ=0._JPRB
ETRELAXU=0._JPRB
NTQSAT=1
NTDIFU=1
NTCOEF=1
NTDRAG=1
NTCVIM=1
NTPLUI=1
NTRADI=1
NTNEBU=1
NTOZON=1
NTDRME=1
NTCOET=1
NTAJUC=1
NTRELAXT=NFLEVG
NTRELAXQ=NFLEVG
NTRELAXU=NFLEVG

XDRMUK=0._JPRB
XDRMUX=0._JPRB
XDRMUP=0._JPRB
XDRMTK=0._JPRB
XDRMTX=0._JPRB
XDRMTP=0._JPRB
XDRMQK=0._JPRB
XDRMQP=0._JPRB
XDRMQX=0._JPRB

RFMESOQ=3.725E-06_JPRB
RCLX=0.0_JPRB
TPSCLIM=197._JPRB

!        1.2 Modify default values according to LECMWF

IF (LECMWF) THEN
ELSE
ENDIF

!     ------------------------------------------------------------------

!*       2.    Modify default values.
!              ----------------------

CALL POSNAM(NULNAM,'NAMTOPH')
READ(NULNAM,NAMTOPH)

!*       2.1  Search corresponding level, to pressure in NAMTOPH
!             for each parameterization

IF(ETQSAT /= 0.0_JPRB) CALL SEAPRE (ETQSAT,NTQSAT,STPRE,NFLEVG)
IF(ETDIFU /= 0.0_JPRB) CALL SEAPRE (ETDIFU,NTDIFU,STPRE,NFLEVG)
IF(ETCOEF /= 0.0_JPRB) CALL SEAPRE (ETCOEF,NTCOEF,STPRE,NFLEVG)
IF(ETDRAG /= 0.0_JPRB) CALL SEAPRE (ETDRAG,NTDRAG,STPRE,NFLEVG)
IF(ETCVIM /= 0.0_JPRB) CALL SEAPRE (ETCVIM,NTCVIM,STPRE,NFLEVG)
IF(ETPLUI /= 0.0_JPRB) CALL SEAPRE (ETPLUI,NTPLUI,STPRE,NFLEVG)
IF(ET850 /= 0.0_JPRB) CALL SEAPRE (ET850,NT850,STPRE,NFLEVG)
IF(ET950 /= 0.0_JPRB) CALL SEAPRE (ET950,NT950,STPRE,NFLEVG)
IF(ETRADI /= 0.0_JPRB) THEN
  IF (LRAY) THEN
    CALL SEAPRE (ETRADI,NTRADI,STPRE,NFLEVG)
  ENDIF
  IF (LRAYFM.OR.LRAYFM15) THEN
    ETRADI=0._JPRB
    NTRADI=1
  ENDIF
ENDIF
IF(ETNEBU /= 0.0_JPRB) CALL SEAPRE (ETNEBU,NTNEBU,STPRE,NFLEVG)
IF(ETOZON /= 0.0_JPRB) CALL SEAPRE (ETOZON,NTOZON,STPRE,NFLEVG)
IF(ETDRME /= 0.0_JPRB) CALL SEAPRE (ETDRME,NTDRME,STPRE,NFLEVG)
IF(ETCOET /= 0.0_JPRB) CALL SEAPRE (ETCOET,NTCOET,STPRE,NFLEVG)
IF(ETAJUC /= 0.0_JPRB) CALL SEAPRE (ETAJUC,NTAJUC,STPRE,NFLEVG)
IF(ETRELAXT /= 0.0_JPRB) CALL SEAPRE (ETRELAXT,NTRELAXT,STPRE,NFLEVG)
IF(ETRELAXQ /= 0.0_JPRB) CALL SEAPRE (ETRELAXQ,NTRELAXQ,STPRE,NFLEVG)
IF(ETRELAXU /= 0.0_JPRB) CALL SEAPRE (ETRELAXU,NTRELAXU,STPRE,NFLEVG)
!     ------------------------------------------------------------------

!*       3.    Print final values.
!              -------------------

WRITE(UNIT=KULOUT,FMT='('' COMMON YOMTOPH '')')
WRITE(UNIT=KULOUT,FMT='('' ETQSAT = '',E10.4,'' NTQSAT = '',I10&
 & ,'' ETDIFU = '',E10.4,'' NTDIFU = '',I10&
 & ,/,'' ETCOEF = '',E10.4,'' NTCOEF = '',I10&
 & ,'' ETDRAG = '',E10.4,'' NTDRAG = '',I10&
 & ,/,'' ETCVIM = '',E10.4,'' NTCVIM = '',I10&
 & ,'' ETPLUI = '',E10.4,'' NTPLUI = '',I10&
 & ,/,'' ETRADI = '',E10.4,'' NTRADI = '',I10&
 & ,'' ETNEBU = '',E10.4,'' NTNEBU = '',I10&
 & ,/,'' ETOZON = '',E10.4,'' NTOZON = '',I10&
 & ,'' ETDRME = '',E10.4,'' NTDRME = '',I10&
 & ,/,'' ETCOET = '',E10.4,'' NTCOET = '',I10&
 & ,/,'' ETAJUC = '',E10.4,'' NTAJUC = '',I10&
 & ,/,'' ETRELAXT = '',E10.4,'' NTRELAXT = '',I10&
 & ,/,'' ETRELAXQ = '',E10.4,'' NTRELAXQ = '',I10&
 & ,/,'' ETRELAXU = '',E10.4,'' NTRELAXU = '',I10&
 & ,/,'' XDRMUK = '',E10.4,'' XDRMUP = '',E10.4&
 & ,'' XDRMUX = '',E10.4,'' XDRMTK = '',E10.4&
 & ,'' XDRMTP = '',E10.4,'' XDRMTX = '',E10.4&
 & ,'' XDRMQK = '',E11.4,'' XDRMQP = '',E11.4&
 & ,/,'' RFMESOQ= '',E11.4,'' RCLX   = '',E11.4&
 & )')&
 & ETQSAT,NTQSAT,ETDIFU,NTDIFU&
 & ,ETCOEF,NTCOEF,ETDRAG,NTDRAG&
 & ,ETCVIM,NTCVIM,ETPLUI,NTPLUI&
 & ,ETRADI,NTRADI,ETNEBU,NTNEBU&
 & ,ETOZON,NTOZON,ETDRME,NTDRME&
 & ,ETCOET,NTCOET&
 & ,ETAJUC,NTAJUC&
 & ,ETRELAXT,NTRELAXT&
 & ,ETRELAXQ,NTRELAXQ&
 & ,ETRELAXU,NTRELAXU&
 & ,XDRMUK,XDRMUP,XDRMUX,XDRMTK,XDRMTP,XDRMTX&
 & ,XDRMQK,XDRMQP,RFMESOQ,RCLX
WRITE(UNIT=KULOUT,FMT='(100(A,E10.4))') 'ET850 = ',ET850,' ET950 = ',ET950

!     VERIFICATION OF CONSISTENCY BETWEEN PHYSICAL PARAMETERIZATION

IF (ETCOEF > ETDIFU.OR.ETCOEF > ETDRAG)THEN
  WRITE(UNIT=KULOUT,FMT='('' ETCOEF TOO LOW '')')
  CALL ABOR1('SUTOPH')
ENDIF
IF (ETQSAT > ETNEBU.OR.ETQSAT > ETPLUI.OR.ETQSAT > ETCVIM)THEN
  WRITE(UNIT=KULOUT,FMT='('' ETQSAT TOO LOW '')')
  CALL ABOR1('SUTOPH')
ENDIF
IF (ETCVIM > ETNEBU)THEN
  WRITE(UNIT=KULOUT,FMT='('' ETCVIM TOO LOW '')')
  CALL ABOR1('SUTOPH')
ENDIF

!     ------------------------------------------------------------------

!*       4.    INITIALIZE MESOSPHERIC DRAG FOR U,V,T and Qv
!              -----------------------------------------

IF (LRRMES.AND..NOT.LAGPHY) THEN
  WRITE (UNIT=KULOUT,FMT='('' PROFIL VERTICAL DE DRAG MESO'',/&
   & ,'' LEV'',T15,''VITESSE'',T45,''TEMPERATURE''&
   & , T65, ''HUMIDITE'' )')  
  DO JLEV=1,NFLEVG
    RMESOU(JLEV)=XDRMUK*PMESUF(STPRE(JLEV),XDRMUP)
    RMESOT(JLEV)=XDRMTK*PMESTF(STPRE(JLEV),XDRMTP)
    RMESOQ(JLEV)=XDRMQK*PMESQF(STPRE(JLEV),XDRMQP)
    IF (XDRMUX /= 0.0_JPRB) RMESOU(JLEV)=MIN(RMESOU(JLEV),XDRMUX)
    IF (XDRMTX /= 0.0_JPRB) RMESOT(JLEV)=MIN(RMESOT(JLEV),XDRMTX)
    IF (XDRMQX /= 0.0_JPRB) RMESOQ(JLEV)=MIN(RMESOQ(JLEV),XDRMQX)
    ZMESU=1.0_JPRB/MAX(1.E-8_JPRB,RMESOU(JLEV)*3600._JPRB*24._JPRB)
    ZMEST=1.0_JPRB/MAX(1.E-8_JPRB,RMESOT(JLEV)*3600._JPRB*24._JPRB)
    ZMESQ=1.0_JPRB/MAX(1.E-8_JPRB,RMESOQ(JLEV)*3600._JPRB*24._JPRB)
    WRITE (UNIT=KULOUT,FMT='(I3,T10,E9.3,T20,G9.3,T40,E9.3,T50&
     & ,G9.3, T70,E9.3, T80,G9.3)') JLEV,RMESOU(JLEV),ZMESU,&
     & RMESOT(JLEV),ZMEST,&
     & RMESOQ(JLEV),ZMESQ 
  ENDDO
  DO JLEV=1,NFLEVG
    YDTOPH%RUREL(JLEV)=0.0_JPRB
    YDTOPH%RVREL(JLEV)=0.0_JPRB
    YDTOPH%RTREL(JLEV)=YDSTA%STTEM(JLEV)
    YDTOPH%RQREL(JLEV)=RFMESOQ
  ENDDO
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUTOPH',1,ZHOOK_HANDLE)
END SUBROUTINE SUTOPH
