SUBROUTINE VDFHGHTHL (YDVDF,YDEPHLI,YDECUMF,YDEPHY,YDPARAR,KSTEP, KIDIA , KFDIA  , KLON  , KLEV   , KDRAFT, PTMST,&
                   & PUM1, PVM1, PTM1    , PQM1  , PLM1   , PIM1  , PAM1,&
                   & PAPHM1  , PAPM1 , PGEOM1 , PGEOH , &
                   & PKHFL   , PKQFL , PKUFL  , PKVFL, &
! OUTPUT
                   & PMFLX  , &        
                   & PTHLUH  , PQTUH   , PTHTVUH, & 
                   & PQCUH, PQIUH, PUUH, PVUH, &
                   & PGP2DSPP, KGFL_EZDIAG, PEZDIAG_POS, &
                   & PTENDQVUP, PTENDTUP, PSURFPREP, PSURFSNOW, &
                   & PUPGENL, PUPGENN, PCLFR, &
                   & ZLENGTH_M, &
                   & ZLENGTH_H, PTKE)
                     
!     ------------------------------------------------------------------

!**   *ARO_VDFHGHT* - INTERFACE TO VDFHGHT to be called from 
!                                          the main physics interface of Arome APL_AROME


!     PURPOSE
!     -------

!     PREPARE VARIABLE AND CALL VDFHGHTN

!     INTERFACE
!     ---------

!     *VDFHGHTN* IS CALLED BY *VDFMAIN*

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----
!     INPUT PARAMETERS (INTEGER):

!     *KIDIA*        START POINT
!     *KFDIA*        END POINT
!     *KLEV*         NUMBER OF LEVELS
!     *KSTEP*        TIME STEP
!     *KLON*         NUMBER OF GRID POINTS PER PACKET
!     *KDRAFT*       NUMBER OF EXPLICITLY MODELED DRAFTS - CURRENTLY 3:
!                    1: test parcel
!                    2: rising dry thermals which stop at cloud base or inversion
!                    3: rising dry thermals which become cloudy
!                    (4: downdrafts .. to be done?)

!     INPUT PARAMETERS (REAL):

!     *PTMST*        DOUBLE TIME STEP (SINGLE AT 1TH STEP)        S
!     *PTM1*         TEMPERATURE AT T-1                           K
!     *PUM1*         X_VELOCITY COMPONENT AT T-1                  M/S
!     *PVM1*         V-VELOCITY COMPONENT AT T-1                  M/S
!     *PQM1*         SPECIFIC HUMUDITY AT T-1                     KG/KG
!     *PLM1*         SPECIFIC CLOUD LIQUID WATER AT T-1           KG/KG
!     *PIM1*         SPECIFIC CLOUD ICE AT T-1                    KG/KG
!     *PAM1*         CLOUD FRACTION AT T-1                        KG/KG
!     *PAPHM1*       PRESSURE AT HALF LEVEL AT T-1                PA
!     *PAPM1*        PRESSURE AT FULL LEVEL AT T-1                PA
!     *PGEOM1*       GEOPOTENTIAL AT T-1                          M2/S2
!     *PGEOH*        GEOPOTENTIAL AT HALF LEVEL                   M2/S2
 
!     *PKHFL*        SURFACE KINEMATIC HEAT FLUX                  K*M/S
!     *PKQFL*        SURFACE KINEMATIC MOISTURE FLUX              M/S
!     *PKUFL*        SURFACE KINEMATIC ZONAL WIND FLUX     
!     *PKVFL*        SURFACE KINEMATIC MERIDIONAL WIND FLUX

!     OUTPUT PARAMETERS (REAL):
!     *PMFLX*        PBL MASS FLUX                                M/S
!     *PTHLUH*       UPDRAFT GENERALIZED LIQUID STATIC ENERGY (SLG)
!                    AT HALF LEVEL                                M2/S2
!     *PQTUH*        UPDRAFT SPECIFIC TOTAL WATER AT HALF LEVEL   KG/KG
!     *PTHTVUH*      UPDRAFT virt pot. temp. (for TKE scheme) AT HALF LEVEL K 
!     *PQCUH*        UPDRAFT SPECIFIC LIQUID WATER AT HALF LEVEL   KG/KG 
!     *PQIUH*        UPDRAFT SPECIFIC ICE WATER AT HALF LEVEL   KG/KG 
!     *PQRUH*        UPDRAFT SPECIFIC RAIN WATER AT HALF LEVEL KG/KG 
!     *PQSUH*        UPDRAFT SPECIFIC SNOW AT HALF LEVEL KG/KG 
!     *PUUH*         UPDRAFT X-MOMENTUM
!     *PVUH*         UPDRAFT Y-MOMENTUM
!     *PTENDTUP*     TENDENCIES FOR NON-CONSERVED AROME
!     *PTENDQVUP*    VARIABLES DUE TO UPDRAFT PRECIPITATION (AND ITS EVAPORATION)
!     *PSURFPREP*   
!     *PSURFSNOW*                   

!     METHOD
!     ------

!     SEE DOCUMENTATION

!     A.P. SIEBESMA and S. MALARDEL   May 2007   
!     Wim de Rooy                     August 2008
!                include tendencies due to updraft rain/snow, and export
!                surface precipitation/snow
!     Wim de Rooy Modifications for LHARATU July 2015
!     2011-06: M. Jerczynski - some cleaning to meet norms
!     2020-12  U. Andrae : Introduce SPP for HARMONIE-AROME

!     ------------------------------------------------------------------

USE YOECUMF  , ONLY : TECUMF
USE YOEPHLI  , ONLY : TEPHLI
USE YOEPHY   , ONLY : TEPHY
USE YOMPARAR , ONLY : TPARAR
USE YOEVDF   , ONLY : TVDF
USE PARKIND1  ,ONLY : JPIM     ,JPRB

USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RD       ,RCPD     ,RLVTT    ,&
                    & RLSTT    ,RATM
USE SPP_MOD, ONLY : YSPP

IMPLICIT NONE


!*         0.1    GLOBAL VARIABLES

TYPE(TVDF)        ,INTENT(IN)    :: YDVDF
TYPE(TECUMF)      ,INTENT(INOUT) :: YDECUMF
TYPE(TEPHLI)      ,INTENT(INOUT) :: YDEPHLI
TYPE(TEPHY)       ,INTENT(INOUT) :: YDEPHY
TYPE(TPARAR)      ,INTENT(INOUT) :: YDPARAR
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KDRAFT
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTMST 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PIM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHM1(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEOM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEOH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKHFL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKQFL(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKUFL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKVFL(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMFLX(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTHLUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQTUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTHTVUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQCUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQIUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUUH(KLON,0:KLEV,KDRAFT) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVUH(KLON,0:KLEV,KDRAFT)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCLFR(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGP2DSPP(KLON,YSPP%N2D)
INTEGER(KIND=JPIM),INTENT(IN)    :: KGFL_EZDIAG
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEZDIAG_POS(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDQVUP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSURFPREP(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSURFSNOW(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUPGENL(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUPGENN(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENDTUP(KLON,KLEV)
! variables RACMO turbulence (=LHARATU)
REAL(KIND=JPRB)   ,INTENT(OUT) :: ZLENGTH_M(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT) :: ZLENGTH_H(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTKE(KLON,KLEV) 

!*         0.2    LOCAL VARIABLES

REAL(KIND=JPRB)      :: ZPFPLVN(KLON,0:KLEV) 
REAL(KIND=JPRB)      :: ZPFPLVL(KLON,0:KLEV) 
REAL(KIND=JPRB)      :: ZKMFL(KLON)
REAL(KIND=JPRB)      :: ZSLGUH(KLON,0:KLEV,KDRAFT),ZFRACB(KLON,KDRAFT), &
  & ZZPTOP(KLON,KDRAFT),ZZPLCL(KLON,KDRAFT)
INTEGER(KIND=JPIM)   :: IPLCL(KLON,KDRAFT), IPTOP(KLON,KDRAFT), IPLZB(KLON,KDRAFT)
REAL(KIND=JPRB)      :: ZRICUI(KLON), ZBIR(KLON)

LOGICAL              :: LLODNODECP(KLON), LLODRUNDRY(KLON) 
INTEGER(KIND=JPIM)   :: IPBLTYPE(KLON) 

REAL(KIND=JPRB)       :: ZRSCP,ZINVATM
REAL(KIND=JPRB)       :: ZINVDZRHO              !   -1/(dz*rho)

REAL(KIND=JPRB)       :: ZHOOK_HANDLE
INTEGER(KIND=JPIM)    :: JLON,JLEV,JDRAFT

#include "vdfhghtnhl.intfb.h"

IF (LHOOK) CALL DR_HOOK('VDFHGHTHL',0,ZHOOK_HANDLE)
ASSOCIATE(LTOTPREC=>YDPARAR%LTOTPREC)

! Compute surface momentum flux
ZKMFL(KIDIA:KFDIA) = SQRT(PKUFL(KIDIA:KFDIA)**2 + PKVFL(KIDIA:KFDIA)**2)

! Non used input arguments
ZBIR = 0.
LLODNODECP = .FALSE.
LLODRUNDRY = .FALSE.

PMFLX=0._JPRB
ZSLGUH=0._JPRB
PQTUH=0._JPRB
PTHLUH=0._JPRB
ZPFPLVL=0._JPRB
ZPFPLVN=0._JPRB
PSURFPREP=0._JPRB
PSURFSNOW=0._JPRB
PUPGENL=0._JPRB
PUPGENN=0._JPRB
PTENDQVUP=0._JPRB
PTENDTUP=0._JPRB
!
PQCUH=0._JPRB
PQIUH=0._JPRB

CALL VDFHGHTNHL(YDVDF,YDEPHLI,YDECUMF,YDEPHY,YDPARAR,KIDIA,KFDIA,KLON,KLEV,KDRAFT, &
 & PTMST,KSTEP,PUM1,PVM1,PTM1,PQM1,PLM1,PIM1,PAM1, &
 & PAPHM1,PAPM1,PGEOM1,PGEOH,ZKMFL,PKHFL,PKQFL,PMFLX, &
 & PUUH,PVUH,ZSLGUH,PQTUH,PTHTVUH,ZFRACB,ZZPTOP,IPTOP,ZZPLCL,IPLCL,IPLZB,ZRICUI,&
 & ZPFPLVL,ZPFPLVN,PCLFR,ZBIR,LLODNODECP,LLODRUNDRY,IPBLTYPE, &
 & PGP2DSPP,KGFL_EZDIAG,PEZDIAG_POS,ZLENGTH_M,ZLENGTH_H,PTKE)


! Convert SLGUH to THLUH
!!!! The transformation from sl to thetal is approximate because in MesoNH we use
!!!!  cp moist and the latente corresponding the T of the env,
!!!!  but in VDFHGHT we use dry cp and constant latent heat.
ZRSCP=RD/RCPD
ZINVATM=1/RATM

DO JDRAFT=2,KDRAFT
  DO JLEV=0,KLEV
    DO JLON=KIDIA,KFDIA
      PTHLUH(JLON,JLEV,JDRAFT) = MAX(0._JPRB,(ZSLGUH (JLON,JLEV,JDRAFT)&
 &     -  PGEOH(JLON,JLEV)) )  / ((MAX(PAPHM1(JLON,JLEV),1._JPRB)*ZINVATM)**ZRSCP) / RCPD 
    ENDDO
  ENDDO
ENDDO

!  Calculate tendencies in qv, and T due to vertical flux
!  divergence of updraft precipitation (snow and rain), i.e. ZPFPLVL and ZPFPLVN

DO JLEV=2,KLEV
  DO JLON=KIDIA,KFDIA
    ZINVDZRHO=RG/(PAPHM1(JLON,JLEV)-PAPHM1(JLON,JLEV-1))
   
    IF(LTOTPREC)THEN
     PUPGENL(JLON,JLEV) = (ZPFPLVL(JLON,JLEV) - ZPFPLVL(JLON,JLEV-1))*ZINVDZRHO
     PUPGENN(JLON,JLEV) = (ZPFPLVN(JLON,JLEV) - ZPFPLVN(JLON,JLEV-1))*ZINVDZRHO
    ENDIF

    PTENDQVUP(JLON,JLEV)= - (ZPFPLVL(JLON,JLEV) - ZPFPLVL(JLON,JLEV-1))*ZINVDZRHO&
 &                        - (ZPFPLVN(JLON,JLEV) - ZPFPLVN(JLON,JLEV-1))*ZINVDZRHO
    PTENDTUP(JLON,JLEV)=  (ZPFPLVL(JLON,JLEV) - ZPFPLVL(JLON,JLEV-1))*ZINVDZRHO&
 &                       *RLVTT/RCPD&
 &                       +  (ZPFPLVN(JLON,JLEV) - ZPFPLVN(JLON,JLEV-1))*ZINVDZRHO&
 &                       *RLSTT/RCPD  
  ENDDO
ENDDO
 
DO JLON=KIDIA,KFDIA
  PSURFPREP(JLON)=ZPFPLVL(JLON,KLEV)
  PSURFSNOW(JLON)=ZPFPLVN(JLON,KLEV)
ENDDO

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('VDFHGHTHL',1,ZHOOK_HANDLE)
END SUBROUTINE VDFHGHTHL
