SUBROUTINE AER_DIAGGLOMAP &
 &( YDMODEL, KIDIA  , KFDIA  , KLON   , KTDIA  , KLEV , KTILES , &
 &  PGLOMAP_DIAGS , PAEROP, PAERSRC, PQP   , PTP, PRSF1, &
 &  PODTO  , PODSS  , PODDU  , PODOM  , PODBC, PODSU ,PODNI, PODAM, &
 &  PAEPM1 , PAEPM25, PAEPM10, PGFL, PAERODDF, PDRYDIAM, PWETDIAM,&
 &  PDRYVOL, PWETVOL,PAERO_WVL_DIAG)




!**** *AER_DIAGGLOMAPS* - ROUTINE DEALING WITH THE ARCHIVING OF GLOMAP'S OUTPUT

!      S.REMY based on AER_PHY3.F90, 12/2014
!      S.Rémy 06/2016, add Aitken mode
!      S.Rémy 07/2016, Add relative impact of water on optical properties
!      S.Rémy 07/2016, separate Acc. and coarse mode from Aitken mode
!      computations


!**   INTERFACE.
!     ----------
!          *AER_DIAGGLOMAP* IS CALLED FROM *AER_PHY3_LAYER*.

! INPUTS:
! -------

! OUTPUTS:
! --------

!     EXTERNALS.
!     ----------
!          *PSURF%PSD_VD(AOD)*

!     MODIFICATIONS.
!     -------------

!     SWITCHES.
!     --------

!     MODEL PARAMETERS
!     ----------------

!-----------------------------------------------------------------------

USE TYPE_MODEL , ONLY : MODEL
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

USE YOEAEROP  ,ONLY : ALF_SU, ALF_OM, ALF_SS, ALF_NI, ALF_AM
USE YOE_AERODIAG , ONLY : JPAERO_WVL_AOD, JPAERO_WVL_AODABS
USE YOMCST   , ONLY : YRCST
USE YOETHF   , ONLY : YRTHF



IMPLICIT NONE

TYPE(MODEL)       ,INTENT(IN) :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA, KFDIA, KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA, KLEV
INTEGER(KIND=JPIM),INTENT(IN) :: KTILES

REAL(KIND=JPRB),INTENT(IN)    :: PGLOMAP_DIAGS(KLON,MAX(KLEV,60),60)
REAL(KIND=JPRB),INTENT(IN)    :: PTP(KLON,KLEV),PQP(KLON,KLEV), PRSF1(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PAEROP(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB),INTENT(IN)    :: PAERSRC(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)

! Outputs

REAL(KIND=JPRB),INTENT(OUT)   :: PODTO(KLON), PODSS(KLON), PODDU(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PODOM(KLON), PODBC(KLON), PODSU(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PODNI(KLON), PODAM(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PAEPM1(KLON),PAEPM25(KLON),PAEPM10(KLON)
REAL(KIND=JPRB),INTENT(INOUT) :: PGFL(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB),INTENT(OUT)   :: PAERODDF(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO,8)
REAL(KIND=JPRB),INTENT(OUT)   :: PDRYDIAM(KLON,KLEV,7)
REAL(KIND=JPRB),INTENT(OUT)   :: PWETDIAM(KLON,KLEV,7)
REAL(KIND=JPRB),INTENT(OUT)   :: PDRYVOL(KLON,KLEV,7)
REAL(KIND=JPRB),INTENT(OUT)   :: PWETVOL(KLON,KLEV,7)
REAL(KIND=JPRB),INTENT(OUT)   :: PAERO_WVL_DIAG(KLON,YDMODEL%YRML_GCONF%YGFL%NAERO_WVL_DIAG,&
 & YDMODEL%YRML_GCONF%YGFL%NAERO_WVL_DIAG_TYPES)


!*   0.5    LOCAL VARIABLES
!           ---------------

INTEGER(KIND=JPIM) :: JAER, JK, JL, JTAB
INTEGER(KIND=JPIM) :: IRH(KLON,KLEV), IFLAG

REAL(KIND=JPRB) :: ZEXTTOT_ACCI(KLON),ZEXTTOT_ACCS(KLON)
REAL(KIND=JPRB) :: ZEXTTOT_COAI(KLON),ZEXTTOT_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOT_AITI(KLON),ZEXTTOT_AITS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTDU_ACCS(KLON),ZEXTTOTDU_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTDU_ACCI(KLON),ZEXTTOTDU_COAI(KLON)
REAL(KIND=JPRB) :: ZEXTTOTSS_ACCS(KLON),ZEXTTOTSS_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTSU_ACCS(KLON),ZEXTTOTSU_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTSU_AITS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTNI_ACCS(KLON),ZEXTTOTNI_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTNI_AITS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTAM_ACCS(KLON),ZEXTTOTAM_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTAM_AITS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTBC_ACCS(KLON),ZEXTTOTBC_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTOC_ACCS(KLON),ZEXTTOTOC_COAS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTOC_AITI(KLON),ZEXTTOTOC_AITS(KLON)
REAL(KIND=JPRB) :: ZEXTTOTBC_AITI(KLON),ZEXTTOTBC_AITS(KLON)


REAL(KIND=JPRB) :: ZAEROU1(KLON,KLEV), ZAEROU2(KLON,KLEV), ZAEROU3(KLON,KLEV), ZAEROU4(KLON,KLEV)
REAL(KIND=JPRB) :: ZQSAT(KLON,KLEV), ZRHCL(KLON,KLEV)
REAL(KIND=JPRB) :: ZAEROU5(KLON,KLEV), ZAEROU6(KLON,KLEV), ZAEROU7(KLON,KLEV), ZAEROU8(KLON,KLEV)
REAL(KIND=JPRB) :: ZAEROU9(KLON,KLEV),ZAEROU10(KLON,KLEV),ZAEROU11(KLON,KLEV),ZAEROU12(KLON,KLEV)
REAL(KIND=JPRB) :: ZAEROU13(KLON,KLEV),ZAEROU14(KLON,KLEV),ZAEROU15(KLON,KLEV),ZAEROU16(KLON,KLEV),ZAEROU17(KLON,KLEV)


REAL(KIND=JPRB) :: ZHOOK_HANDLE
!-----------------------------------------------------------------------
#include "satur.intfb.h"

IF (LHOOK) CALL DR_HOOK('AER_DIAGGLOMAP',0,ZHOOK_HANDLE)
ASSOCIATE(YDCOMPO=>YDMODEL%YRML_CHEM%YRCOMPO,YDEPHLI=>YDMODEL%YRML_PHY_SLIN%YREPHLI, &
 & YDEAERATM=>YDMODEL%YRML_PHY_RAD%YREAERATM, YDEAERSNK=>YDMODEL%YRML_PHY_AER%YREAERSNK, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL)

ASSOCIATE(NACTAERO=>YGFL%NACTAERO, NAERO=>YGFL%NAERO, &
 & RRHTAB=>YDEAERSNK%RRHTAB, &
 & LAERNITRATE=>YDCOMPO%LAERNITRATE, &
 & LAERDUSTSOURCE=>YDEAERATM%LAERDUSTSOURCE, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, &
 & NAERO_WVL_DIAG=>YGFL%NAERO_WVL_DIAG, &
 & NAERO_WVL_DIAG_TYPES=>YGFL%NAERO_WVL_DIAG_TYPES, &
 & NTYPAER=>YDEAERATM%NTYPAER, NAEROUT=>YGFL%NAEROUT, YAEROUT=>YGFL%YAEROUT)




!*         0.     AOD storage
!                 ----------------------------------------

! Extinction
ZEXTTOT_ACCI(:)=0._JPRB
ZEXTTOT_ACCS(:)=0._JPRB
ZEXTTOT_COAI(:)=0._JPRB
ZEXTTOT_COAS(:)=0._JPRB
ZEXTTOT_AITI(:)=0._JPRB
ZEXTTOT_AITS(:)=0._JPRB
ZEXTTOTSS_COAS(:)=0._JPRB
ZEXTTOTSS_ACCS(:)=0._JPRB
ZEXTTOTDU_COAS(:)=0._JPRB
ZEXTTOTDU_ACCS(:)=0._JPRB
ZEXTTOTDU_COAI(:)=0._JPRB
ZEXTTOTDU_ACCI(:)=0._JPRB
ZEXTTOTBC_COAS(:)=0._JPRB
ZEXTTOTBC_ACCS(:)=0._JPRB
ZEXTTOTSU_COAS(:)=0._JPRB
ZEXTTOTSU_ACCS(:)=0._JPRB
ZEXTTOTSU_AITS(:)=0._JPRB
ZEXTTOTNI_COAS(:)=0._JPRB
ZEXTTOTNI_ACCS(:)=0._JPRB
ZEXTTOTNI_AITS(:)=0._JPRB
ZEXTTOTAM_COAS(:)=0._JPRB
ZEXTTOTAM_ACCS(:)=0._JPRB
ZEXTTOTAM_AITS(:)=0._JPRB
ZEXTTOTOC_COAS(:)=0._JPRB
ZEXTTOTOC_ACCS(:)=0._JPRB
ZEXTTOTBC_AITS(:)=0._JPRB
ZEXTTOTOC_AITS(:)=0._JPRB
ZEXTTOTBC_AITI(:)=0._JPRB
ZEXTTOTOC_AITI(:)=0._JPRB

!*         1.     RH computation
!                 ----------------------------------------
IFLAG=2
CALL SATUR (YRTHF, YRCST, KIDIA , KFDIA , KLON  , KTDIA , KLEV, LPHYLIN, &
  & PRSF1, PTP    , ZQSAT , IFLAG)

IRH(:,:)=1

!  Compute total vertical burden for each of soluble and insoluble modes (Acc and Coarse) and for each species
DO JL=KIDIA,KFDIA
  DO JK=1,KLEV
    ZRHCL(JL,JK)=PQP(JL,JK)/(MAX(1.E-30_JPRB, ZQSAT(JL,JK)))
    DO JTAB=1,12
      IF (ZRHCL(JL,JK)*100._JPRB > RRHTAB(JTAB)) THEN
         IRH(JL,JK)=JTAB
      ENDIF
    ENDDO

    ! Mass weighted by the extinction ratio dep on RH
    IF (LAERNITRATE) THEN
      ZEXTTOT_AITI(JL)=ZEXTTOT_AITI(JL)+PAEROP(JL,JK,27)+PAEROP(JL,JK,28)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)

      ZEXTTOT_AITS(JL)=ZEXTTOT_AITS(JL)+PAEROP(JL,JK,5)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+&
              & PAEROP(JL,JK,8)+PAEROP(JL,JK,9)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)+ &
              & PAEROP(JL,JK,6)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)+PAEROP(JL,JK,7)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)

      ZEXTTOT_ACCS(JL)=ZEXTTOT_ACCS(JL)+PAEROP(JL,JK,11)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+ &
               & PAEROP(JL,JK,14)+PAEROP(JL,JK,15)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)+ &
               & PAEROP(JL,JK,16)*ALF_SS(IRH(JL,JK),1,1)/ALF_SS(1,1,1)+PAEROP(JL,JK,17) + &
               & PAEROP(JL,JK,12)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)+PAEROP(JL,JK,13)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)

      ZEXTTOT_COAS(JL)=ZEXTTOT_COAS(JL)+PAEROP(JL,JK,19)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+ &
               & PAEROP(JL,JK,22)+PAEROP(JL,JK,23)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)+ &
               & PAEROP(JL,JK,24)*ALF_SS(IRH(JL,JK),1,2)/ALF_SS(1,1,2)+PAEROP(JL,JK,25) + &
               & PAEROP(JL,JK,20)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)+PAEROP(JL,JK,21)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)

      ZEXTTOT_ACCI(JL)=ZEXTTOT_ACCI(JL)+PAEROP(JL,JK,30)
      ZEXTTOT_COAI(JL)=ZEXTTOT_COAI(JL)+PAEROP(JL,JK,32)

      ZEXTTOTSS_ACCS(JL)=ZEXTTOTSS_ACCS(JL)+PAEROP(JL,JK,16)*ALF_SS(IRH(JL,JK),1,1)/ALF_SS(1,1,1)
      ZEXTTOTSS_COAS(JL)=ZEXTTOTSS_COAS(JL)+PAEROP(JL,JK,24)*ALF_SS(IRH(JL,JK),1,2)/ALF_SS(1,1,2)
      ZEXTTOTDU_ACCS(JL)=ZEXTTOTDU_ACCS(JL)+PAEROP(JL,JK,17)
      ZEXTTOTDU_COAS(JL)=ZEXTTOTDU_COAS(JL)+PAEROP(JL,JK,25)
      ZEXTTOTDU_ACCI(JL)=ZEXTTOTDU_ACCI(JL)+PAEROP(JL,JK,30)
      ZEXTTOTDU_COAI(JL)=ZEXTTOTDU_COAI(JL)+PAEROP(JL,JK,32)
      ZEXTTOTSU_ACCS(JL)=ZEXTTOTSU_ACCS(JL)+PAEROP(JL,JK,11)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTSU_COAS(JL)=ZEXTTOTSU_COAS(JL)+PAEROP(JL,JK,19)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTSU_AITS(JL)=ZEXTTOTSU_AITS(JL)+PAEROP(JL,JK,5)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTBC_ACCS(JL)=ZEXTTOTBC_ACCS(JL)+PAEROP(JL,JK,14)
      ZEXTTOTBC_AITS(JL)=ZEXTTOTBC_AITS(JL)+PAEROP(JL,JK,8)
      ZEXTTOTBC_AITI(JL)=ZEXTTOTBC_AITI(JL)+PAEROP(JL,JK,27)
      ZEXTTOTOC_AITI(JL)=ZEXTTOTOC_AITI(JL)+PAEROP(JL,JK,28)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTBC_COAS(JL)=ZEXTTOTBC_COAS(JL)+PAEROP(JL,JK,22)
      ZEXTTOTOC_ACCS(JL)=ZEXTTOTOC_ACCS(JL)+PAEROP(JL,JK,15)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTOC_COAS(JL)=ZEXTTOTOC_COAS(JL)+PAEROP(JL,JK,23)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTOC_AITS(JL)=ZEXTTOTOC_AITS(JL)+PAEROP(JL,JK,9)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTNI_ACCS(JL)=ZEXTTOTNI_ACCS(JL)+PAEROP(JL,JK,12)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)
      ZEXTTOTNI_COAS(JL)=ZEXTTOTNI_COAS(JL)+PAEROP(JL,JK,20)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)
      ZEXTTOTNI_AITS(JL)=ZEXTTOTNI_AITS(JL)+PAEROP(JL,JK,6)*ALF_NI(IRH(JL,JK),1,1)/ALF_NI(1,1,1)
      ZEXTTOTAM_ACCS(JL)=ZEXTTOTAM_ACCS(JL)+PAEROP(JL,JK,13)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)
      ZEXTTOTAM_COAS(JL)=ZEXTTOTAM_COAS(JL)+PAEROP(JL,JK,21)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)
      ZEXTTOTAM_AITS(JL)=ZEXTTOTAM_AITS(JL)+PAEROP(JL,JK,7)*ALF_AM(IRH(JL,JK),1)/ALF_AM(1,1)

    ELSE
      ZEXTTOT_AITI(JL)=ZEXTTOT_AITI(JL)+PAEROP(JL,JK,21)+PAEROP(JL,JK,22)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)

      ZEXTTOT_AITS(JL)=ZEXTTOT_AITS(JL)+PAEROP(JL,JK,5)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+ &
         & PAEROP(JL,JK,6)+PAEROP(JL,JK,7)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)

      ZEXTTOT_ACCS(JL)=ZEXTTOT_ACCS(JL)+PAEROP(JL,JK,9)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+ &
         & PAEROP(JL,JK,10)+PAEROP(JL,JK,11)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)+ &
         & PAEROP(JL,JK,12)*ALF_SS(IRH(JL,JK),1,1)/ALF_SS(1,1,1)+PAEROP(JL,JK,13)

      ZEXTTOT_COAS(JL)=ZEXTTOT_COAS(JL)+PAEROP(JL,JK,15)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)+ &
         & PAEROP(JL,JK,16)+PAEROP(JL,JK,17)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)+ &
         & PAEROP(JL,JK,18)*ALF_SS(IRH(JL,JK),1,2)/ALF_SS(1,1,2)+PAEROP(JL,JK,19)

      ZEXTTOT_ACCI(JL)=ZEXTTOT_ACCI(JL)+PAEROP(JL,JK,24)
      ZEXTTOT_COAI(JL)=ZEXTTOT_COAI(JL)+PAEROP(JL,JK,26)

      ZEXTTOTSS_ACCS(JL)=ZEXTTOTSS_ACCS(JL)+PAEROP(JL,JK,12)*ALF_SS(IRH(JL,JK),1,1)/ALF_SS(1,1,1)
      ZEXTTOTSS_COAS(JL)=ZEXTTOTSS_COAS(JL)+PAEROP(JL,JK,18)*ALF_SS(IRH(JL,JK),1,2)/ALF_SS(1,1,2)
      ZEXTTOTDU_ACCS(JL)=ZEXTTOTDU_ACCS(JL)+PAEROP(JL,JK,13)
      ZEXTTOTDU_COAS(JL)=ZEXTTOTDU_COAS(JL)+PAEROP(JL,JK,19)
      ZEXTTOTDU_ACCI(JL)=ZEXTTOTDU_ACCI(JL)+PAEROP(JL,JK,24)
      ZEXTTOTDU_COAI(JL)=ZEXTTOTDU_COAI(JL)+PAEROP(JL,JK,26)
      ZEXTTOTSU_ACCS(JL)=ZEXTTOTSU_ACCS(JL)+PAEROP(JL,JK,9)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTSU_COAS(JL)=ZEXTTOTSU_COAS(JL)+PAEROP(JL,JK,15)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTSU_AITS(JL)=ZEXTTOTSU_AITS(JL)+PAEROP(JL,JK,5)*ALF_SU(IRH(JL,JK),1)/ALF_SU(1,1)
      ZEXTTOTBC_ACCS(JL)=ZEXTTOTBC_ACCS(JL)+PAEROP(JL,JK,10)
      ZEXTTOTBC_AITS(JL)=ZEXTTOTBC_AITS(JL)+PAEROP(JL,JK,6)
      ZEXTTOTBC_AITI(JL)=ZEXTTOTBC_AITI(JL)+PAEROP(JL,JK,21)
      ZEXTTOTBC_COAS(JL)=ZEXTTOTBC_COAS(JL)+PAEROP(JL,JK,16)
      ZEXTTOTOC_ACCS(JL)=ZEXTTOTOC_ACCS(JL)+PAEROP(JL,JK,11)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTOC_COAS(JL)=ZEXTTOTOC_COAS(JL)+PAEROP(JL,JK,17)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTOC_AITS(JL)=ZEXTTOTOC_AITS(JL)+PAEROP(JL,JK,7)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
      ZEXTTOTOC_AITI(JL)=ZEXTTOTOC_AITI(JL)+PAEROP(JL,JK,22)*ALF_OM(IRH(JL,JK),1)/ALF_OM(1,1)
    ENDIF
  ENDDO
ENDDO

PODSS(:)=0._JPRB
PODSU(:)=0._JPRB
PODOM(:)=0._JPRB
PODBC(:)=0._JPRB
PODDU(:)=0._JPRB
PODNI(:)=0._JPRB
PODAM(:)=0._JPRB
! Add accumulation solube mode contribution
WHERE (ZEXTTOT_ACCS(:) > 1.E-15_JPRB)
  PODSS(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTSS_ACCS(:)/ZEXTTOT_ACCS(:))
  PODSU(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTSU_ACCS(:)/ZEXTTOT_ACCS(:))

  PODBC(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTBC_ACCS(:)/ZEXTTOT_ACCS(:))
  PODDU(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTDU_ACCS(:)/ZEXTTOT_ACCS(:))

  PODOM(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTOC_ACCS(:)/ZEXTTOT_ACCS(:))
  PODNI(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTNI_ACCS(:)/ZEXTTOT_ACCS(:))
  PODAM(:)=(PGLOMAP_DIAGS(:,9,2)*ZEXTTOTAM_ACCS(:)/ZEXTTOT_ACCS(:))
ENDWHERE
! Add coarse solube mode contribution
WHERE (ZEXTTOT_COAS(:) > 1.E-15_JPRB)
  PODSS(:)=PODSS(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTSS_COAS(:)/ZEXTTOT_COAS(:))
  PODSU(:)=PODSU(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTSU_COAS(:)/ZEXTTOT_COAS(:))

  PODBC(:)=PODBC(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTBC_COAS(:)/ZEXTTOT_COAS(:))
  PODDU(:)=PODDU(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTDU_COAS(:)/ZEXTTOT_COAS(:))

  PODOM(:)=PODOM(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTOC_COAS(:)/ZEXTTOT_COAS(:))
  PODNI(:)=PODNI(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTNI_COAS(:)/ZEXTTOT_COAS(:))
  PODAM(:)=PODAM(:)+(PGLOMAP_DIAGS(:,15,2)*ZEXTTOTAM_COAS(:)/ZEXTTOT_COAS(:))
ENDWHERE
! Add Aitken solube mode contribution
WHERE (ZEXTTOT_AITS(:) > 1.E-20_JPRB)
  PODSU(:)=PODSU(:)+(PGLOMAP_DIAGS(:,3,2)*ZEXTTOTSU_AITS(:)/ZEXTTOT_AITS(:))

  PODBC(:)=PODBC(:)+(PGLOMAP_DIAGS(:,3,2)*ZEXTTOTBC_AITS(:)/ZEXTTOT_AITS(:))

  PODOM(:)=PODOM(:)+(PGLOMAP_DIAGS(:,3,2)*ZEXTTOTOC_AITS(:)/ZEXTTOT_AITS(:))
  PODNI(:)=PODNI(:)+(PGLOMAP_DIAGS(:,3,2)*ZEXTTOTNI_AITS(:)/ZEXTTOT_AITS(:))
  PODAM(:)=PODAM(:)+(PGLOMAP_DIAGS(:,3,2)*ZEXTTOTAM_AITS(:)/ZEXTTOT_AITS(:))
ENDWHERE
! Add Aitken insolube mode contribution
WHERE (ZEXTTOT_AITI(:) > 1.E-20_JPRB)

  PODBC(:)=PODBC(:)+(PGLOMAP_DIAGS(:,21,2)*ZEXTTOTBC_AITI(:)/ZEXTTOT_AITI(:))

  PODOM(:)=PODOM(:)+(PGLOMAP_DIAGS(:,21,2)*ZEXTTOTOC_AITI(:)/ZEXTTOT_AITI(:))
ENDWHERE

!  Dust
! Add Accumulation insolube mode contribution
WHERE (ZEXTTOT_ACCI(:) > 1.E-15_JPRB)
   PODDU(:)=PODDU(:)+(PGLOMAP_DIAGS(:,27,2)*ZEXTTOTDU_ACCI(:)/ZEXTTOT_ACCI(:))
ENDWHERE
! Add Coarse insolube mode contribution
WHERE (ZEXTTOT_COAI(:) > 1.E-15_JPRB)
   PODDU(:)=PODDU(:)+(PGLOMAP_DIAGS(:,33,2)*ZEXTTOTDU_COAI(:)/ZEXTTOT_COAI(:))
ENDWHERE

! Total aerosol
PODTO(KIDIA:KFDIA)=PGLOMAP_DIAGS(KIDIA:KFDIA,3,2)+PGLOMAP_DIAGS(KIDIA:KFDIA,21,2)+PGLOMAP_DIAGS(KIDIA:KFDIA,9,2) &
 & +PGLOMAP_DIAGS(KIDIA:KFDIA,27,2)+PGLOMAP_DIAGS(KIDIA:KFDIA, 15,2)+PGLOMAP_DIAGS(KIDIA:KFDIA,33,2)


! Initialize arrays for diagnostics
ZAEROU1(:,:) = 0.0_JPRB
ZAEROU2(:,:) = 0.0_JPRB
ZAEROU3(:,:) = 0.0_JPRB
ZAEROU4(:,:) = 0.0_JPRB
ZAEROU5(:,:) = 0.0_JPRB
ZAEROU6(:,:) = 0.0_JPRB
ZAEROU7(:,:) = 0.0_JPRB
ZAEROU8(:,:) = 0.0_JPRB
ZAEROU9(:,:) =0._JPRB
ZAEROU10(:,:) =0._JPRB
ZAEROU11(:,:) =0._JPRB
ZAEROU12(:,:) =0._JPRB
ZAEROU13(:,:) =0._JPRB
ZAEROU14(:,:) =0._JPRB
ZAEROU15(:,:) =0._JPRB
ZAEROU16(:,:) =0._JPRB
ZAEROU17(:,:) =0._JPRB

DO JL=KIDIA,KFDIA

IF (NAEROUT >= 1) THEN
  !-- the AOD  is stored in AEROU 12 13 14 for soluble and insoluble

    ! 550 nm
    ! Aitken mode soluble
    IF (KLEV >= 1) ZAEROU1(JL,1)=PGLOMAP_DIAGS(JL,3,2)
    ! Acc mode soluble
    IF (KLEV >= 2) ZAEROU1(JL,2)=PGLOMAP_DIAGS(JL, 9,2)
    ! Coarse mode soluble
    IF (KLEV >= 3) ZAEROU1(JL,3)=PGLOMAP_DIAGS(JL, 15,2)
    ! Aitken mode insoluble
    IF (KLEV >= 4) ZAEROU1(JL,4)=PGLOMAP_DIAGS(JL,21,2)
    ! Acc mode insoluble
    IF (KLEV >= 5) ZAEROU1(JL,5)=PGLOMAP_DIAGS(JL, 27,2)
    ! Coarse mode insoluble
    IF (KLEV >= 6) ZAEROU1(JL,6)=PGLOMAP_DIAGS(JL, 33,2)


    ! 440 nm
    ! Aitken mode soluble
    IF (KLEV >= 7) ZAEROU1(JL,7)=PGLOMAP_DIAGS(JL,2,2)
    ! Acc mode soluble
    IF (KLEV >= 8) ZAEROU1(JL,8)=PGLOMAP_DIAGS(JL, 8,2)
    ! Coarse mode soluble
    IF (KLEV >= 9) ZAEROU1(JL,9)=PGLOMAP_DIAGS(JL, 14,2)
    ! Aitken mode insoluble
    IF (KLEV >= 10) ZAEROU1(JL,10)=PGLOMAP_DIAGS(JL,20,2)
    ! Acc mode insoluble
    IF (KLEV >= 11) ZAEROU1(JL,11)=PGLOMAP_DIAGS(JL, 26,2)
    ! Coarse mode insoluble
    IF (KLEV >= 12) ZAEROU1(JL,12)=PGLOMAP_DIAGS(JL, 32,2)

    ! 380 nm
    ! Aitken mode soluble
    IF (KLEV >= 13) ZAEROU1(JL,13)=PGLOMAP_DIAGS(JL,1,2)
    ! Acc mode soluble
    IF (KLEV >= 14) ZAEROU1(JL,14)=PGLOMAP_DIAGS(JL, 7,2)
    ! Coarse mode soluble
    IF (KLEV >= 15) ZAEROU1(JL,15)=PGLOMAP_DIAGS(JL, 13,2)
    ! Aitken mode insoluble
    IF (KLEV >= 16) ZAEROU1(JL,16)=PGLOMAP_DIAGS(JL,19,2)
    ! Acc mode insoluble
    IF (KLEV >= 17) ZAEROU1(JL,17)=PGLOMAP_DIAGS(JL, 25,2)
    ! Coarse mode insoluble
    IF (KLEV >= 18) ZAEROU1(JL,18)=PGLOMAP_DIAGS(JL, 31,2)

    ! 670 nm
    ! Aitken mode soluble
    IF (KLEV >= 19) ZAEROU1(JL,19)=PGLOMAP_DIAGS(JL,4,2)
    ! Acc mode soluble
    IF (KLEV >= 20) ZAEROU1(JL,20)=PGLOMAP_DIAGS(JL, 10,2)
    ! Coarse mode soluble
    IF (KLEV >= 21) ZAEROU1(JL,21)=PGLOMAP_DIAGS(JL, 16,2)
    ! Aitken mode insoluble
    IF (KLEV >= 22) ZAEROU1(JL,22)=PGLOMAP_DIAGS(JL,22,2)
    ! Acc mode insoluble
    IF (KLEV >= 23) ZAEROU1(JL,23)=PGLOMAP_DIAGS(JL, 28,2)
    ! Coarse mode insoluble
    IF (KLEV >= 24) ZAEROU1(JL,24)=PGLOMAP_DIAGS(JL, 34,2)

    ! 870 nm
    ! Aitken mode soluble
    IF (KLEV >= 25) ZAEROU1(JL,25)=PGLOMAP_DIAGS(JL,5,2)
    ! Acc mode soluble
    IF (KLEV >= 26) ZAEROU1(JL,26)=PGLOMAP_DIAGS(JL, 11,2)
    ! Coarse mode soluble
    IF (KLEV >= 27) ZAEROU1(JL,27)=PGLOMAP_DIAGS(JL, 17,2)
    ! Aitken mode insoluble
    IF (KLEV >= 28) ZAEROU1(JL,28)=PGLOMAP_DIAGS(JL,23,2)
    ! Acc mode insoluble
    IF (KLEV >= 29) ZAEROU1(JL,29)=PGLOMAP_DIAGS(JL, 29,2)
    ! Coarse mode insoluble
    IF (KLEV >= 30) ZAEROU1(JL,30)=PGLOMAP_DIAGS(JL, 35,2)

    ! 1020 nm
    ! Aitken mode soluble
    IF (KLEV >= 31) ZAEROU1(JL,31)=PGLOMAP_DIAGS(JL,6,2)
    ! Acc mode soluble
    IF (KLEV >= 32) ZAEROU1(JL,32)=PGLOMAP_DIAGS(JL, 12,2)
    ! Coarse mode soluble
    IF (KLEV >= 33) ZAEROU1(JL,33)=PGLOMAP_DIAGS(JL, 18,2)
    ! Aitken mode insoluble
    IF (KLEV >= 34) ZAEROU1(JL,34)=PGLOMAP_DIAGS(JL,24,2)
    ! Acc mode insoluble
    IF (KLEV >= 35) ZAEROU1(JL,35)=PGLOMAP_DIAGS(JL, 30,2)
    ! Coarse mode insoluble
    IF (KLEV >= 36) ZAEROU1(JL,36)=PGLOMAP_DIAGS(JL, 36,2)


!-- the absorption AOD  is stored in AEROU 1 2 3 also

    ! 550 nm
    ! Aitken mode soluble + insoluble
    IF (KLEV >= 37) ZAEROU1(JL,37)=PGLOMAP_DIAGS(JL,3,1)+PGLOMAP_DIAGS(JL, 21,1)
    ! Acc mode soluble + insoluble
    IF (KLEV >= 38) ZAEROU1(JL,38)=PGLOMAP_DIAGS(JL, 9,1)+PGLOMAP_DIAGS(JL, 27,1)
    ! Coarse mode soluble + insoluble
    IF (KLEV >= 39) ZAEROU1(JL,39)=PGLOMAP_DIAGS(JL, 15,1)+PGLOMAP_DIAGS(JL, 33,1)

    ! Aitken mode soluble
    IF (KLEV >= 40) ZAEROU1(JL,40)=PGLOMAP_DIAGS(JL,3,2)
    ! Acc mode soluble
    IF (KLEV >= 41) ZAEROU1(JL,41)=PGLOMAP_DIAGS(JL, 9,2)
    ! Coarse mode soluble
    IF (KLEV >= 42) ZAEROU1(JL,42)=PGLOMAP_DIAGS(JL, 15,2)
    ! Aitken mode insoluble
    IF (KLEV >= 43) ZAEROU1(JL,43)=PGLOMAP_DIAGS(JL,21,2)
    ! Acc mode insoluble
    IF (KLEV >= 44) ZAEROU1(JL,44)=PGLOMAP_DIAGS(JL, 27,2)
    ! Coarse mode insoluble
    IF (KLEV >= 45) ZAEROU1(JL,45)=PGLOMAP_DIAGS(JL, 33,2)
  ENDIF


  IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_AOD) THEN
    IF (NAERO_WVL_DIAG >= 1) THEN
      PAERO_WVL_DIAG(JL,1,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,3,2)+PGLOMAP_DIAGS(JL, 9,2)+PGLOMAP_DIAGS(JL, 15,2)+ &
      & PGLOMAP_DIAGS(JL,21,2)+PGLOMAP_DIAGS(JL, 27,2)+PGLOMAP_DIAGS(JL, 33,2)
    ENDIF
    IF (NAERO_WVL_DIAG >= 2) THEN
      PAERO_WVL_DIAG(JL,2,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,1,2)+PGLOMAP_DIAGS(JL, 7,2)+PGLOMAP_DIAGS(JL, 13,2)+ &
      & PGLOMAP_DIAGS(JL,19,2)+PGLOMAP_DIAGS(JL, 25,2)+PGLOMAP_DIAGS(JL, 31,2)
    ENDIF
    IF (NAERO_WVL_DIAG >= 3) THEN
      PAERO_WVL_DIAG(JL,3,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,2,2)+PGLOMAP_DIAGS(JL, 8,2)+PGLOMAP_DIAGS(JL, 14,2)+ &
      & PGLOMAP_DIAGS(JL,20,2)+PGLOMAP_DIAGS(JL, 26,2)+PGLOMAP_DIAGS(JL, 32,2)
    ENDIF
    IF (NAERO_WVL_DIAG >= 4) THEN
      PAERO_WVL_DIAG(JL,4,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,4,2)+PGLOMAP_DIAGS(JL, 10,2)+PGLOMAP_DIAGS(JL, 16,2)+ &
      & PGLOMAP_DIAGS(JL,22,2)+PGLOMAP_DIAGS(JL, 28,2)+PGLOMAP_DIAGS(JL, 34,2)
    ENDIF
    IF (NAERO_WVL_DIAG >= 5) THEN
      PAERO_WVL_DIAG(JL,5,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,5,2)+PGLOMAP_DIAGS(JL, 11,2)+PGLOMAP_DIAGS(JL, 17,2)+ &
      & PGLOMAP_DIAGS(JL,23,2)+PGLOMAP_DIAGS(JL, 29,2)+PGLOMAP_DIAGS(JL, 35,2)
    ENDIF
    IF (NAERO_WVL_DIAG >= 6) THEN
      PAERO_WVL_DIAG(JL,6,JPAERO_WVL_AOD) = PGLOMAP_DIAGS(JL,6,2)+PGLOMAP_DIAGS(JL, 12,2)+PGLOMAP_DIAGS(JL, 18,2)+ &
      & PGLOMAP_DIAGS(JL,24,2)+PGLOMAP_DIAGS(JL, 30,2)+PGLOMAP_DIAGS(JL, 36,2)
    ENDIF
  ENDIF
  IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_AODABS) THEN
    IF (NAERO_WVL_DIAG >= 1) THEN
      PAERO_WVL_DIAG(JL,1,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,3,1)+PGLOMAP_DIAGS(JL, 9,1)+PGLOMAP_DIAGS(JL, 15,1)+ &
      & PGLOMAP_DIAGS(JL,21,1)+PGLOMAP_DIAGS(JL, 27,1)+PGLOMAP_DIAGS(JL, 33,1)
    ENDIF
    IF (NAERO_WVL_DIAG >= 2) THEN
      PAERO_WVL_DIAG(JL,2,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,1,1)+PGLOMAP_DIAGS(JL, 7,1)+PGLOMAP_DIAGS(JL, 13,1)+ &
      & PGLOMAP_DIAGS(JL,19,1)+PGLOMAP_DIAGS(JL, 25,1)+PGLOMAP_DIAGS(JL, 31,1)
    ENDIF
    IF (NAERO_WVL_DIAG >= 3) THEN
      PAERO_WVL_DIAG(JL,3,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,2,1)+PGLOMAP_DIAGS(JL, 8,1)+PGLOMAP_DIAGS(JL, 14,1)+ &
      & PGLOMAP_DIAGS(JL,20,1)+PGLOMAP_DIAGS(JL, 26,1)+PGLOMAP_DIAGS(JL, 32,1)
    ENDIF
    IF (NAERO_WVL_DIAG >= 4) THEN
      PAERO_WVL_DIAG(JL,4,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,4,1)+PGLOMAP_DIAGS(JL, 10,1)+PGLOMAP_DIAGS(JL, 16,1)+ &
      & PGLOMAP_DIAGS(JL,22,1)+PGLOMAP_DIAGS(JL, 28,1)+PGLOMAP_DIAGS(JL, 34,1)
    ENDIF
    IF (NAERO_WVL_DIAG >= 5) THEN
      PAERO_WVL_DIAG(JL,5,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,5,1)+PGLOMAP_DIAGS(JL, 11,1)+PGLOMAP_DIAGS(JL, 17,1)+ &
      & PGLOMAP_DIAGS(JL,23,1)+PGLOMAP_DIAGS(JL, 29,1)+PGLOMAP_DIAGS(JL, 35,1)
    ENDIF
    IF (NAERO_WVL_DIAG >= 6) THEN
      PAERO_WVL_DIAG(JL,6,JPAERO_WVL_AODABS) = PGLOMAP_DIAGS(JL,6,1)+PGLOMAP_DIAGS(JL, 12,1)+PGLOMAP_DIAGS(JL, 18,1)+ &
      & PGLOMAP_DIAGS(JL,24,1)+PGLOMAP_DIAGS(JL, 30,1)+PGLOMAP_DIAGS(JL, 36,1)
    ENDIF
  ENDIF


  IF (NAEROUT >= 2) ZAEROU2(JL,1:MIN(NACTAERO,KLEV))=PAERSRC(JL,1:MIN(NACTAERO,KLEV))

  ! sinks

  DO JK=1,KLEV

    IF (NAEROUT >= 3) ZAEROU3(JL,JK)=PGLOMAP_DIAGS(JL, JK,4)
    IF (NAEROUT >= 4) ZAEROU4(JL,JK)=PGLOMAP_DIAGS(JL, JK,5)
    IF (NAEROUT >= 5) ZAEROU5(JL,JK)=PGLOMAP_DIAGS(JL, JK,6)
    IF (NAEROUT >= 6) ZAEROU6(JL,JK)=PGLOMAP_DIAGS(JL, JK,7)
    IF (NAEROUT >= 7) ZAEROU7(JL,JK)=PGLOMAP_DIAGS(JL, JK,8)
    IF (NAEROUT >= 8) ZAEROU8(JL,JK)=PGLOMAP_DIAGS(JL, JK,9)
    IF (NAEROUT >= 9) ZAEROU9(JL,JK)=PGLOMAP_DIAGS(JL, JK,10)
    IF (NAEROUT >= 10) ZAEROU10(JL,JK)=PGLOMAP_DIAGS(JL, JK,11)
    IF (NAEROUT >= 11) ZAEROU11(JL,JK)=PGLOMAP_DIAGS(JL, JK,12)
    IF (NAEROUT >= 12) ZAEROU12(JL,JK)=PGLOMAP_DIAGS(JL, JK,13)
    IF (NAEROUT >= 13) ZAEROU13(JL,JK)=PGLOMAP_DIAGS(JL, JK,14)
    IF (NAEROUT >= 14) ZAEROU14(JL,JK)=PGLOMAP_DIAGS(JL, JK,15)
    IF (NAEROUT >= 15) ZAEROU15(JL,JK)=PGLOMAP_DIAGS(JL, JK,16)
    IF (NAEROUT >= 16) ZAEROU16(JL,JK)=PGLOMAP_DIAGS(JL, JK,17)
    IF (NAEROUT >= 17) ZAEROU17(JL,JK)=PGLOMAP_DIAGS(JL, JK,18)

  ENDDO ! loop over KLEV for budgets

ENDDO ! loop over KFDIA

!------------------------------------------------------------------------------
!*         1.5     STORE DRY AND WET DIAMETER, EFFECTIVE RADIUS
!                  -----------------------------------------------

PDRYDIAM(KIDIA:KFDIA,:,1:7)=PGLOMAP_DIAGS(KIDIA:KFDIA,1:KLEV,19:25)
PWETDIAM(KIDIA:KFDIA,:,1:7)=PGLOMAP_DIAGS(KIDIA:KFDIA,1:KLEV,26:32)
PDRYVOL(KIDIA:KFDIA,:,1:7) =PGLOMAP_DIAGS(KIDIA:KFDIA,1:KLEV,33:39)
PWETVOL(KIDIA:KFDIA,:,1:7) =PGLOMAP_DIAGS(KIDIA:KFDIA,1:KLEV,40:46)


!------------------------------------------------------------------------------
!*         2     SET DIAGNOSTICS FIELDS TO VALUES COMPUTED ABOVE
!                  -----------------------------------------------
!-- but only for forecast configuration


IF(NAEROUT > 0) THEN
  IF (NAEROUT >= 1) PGFL(KIDIA:KFDIA,:,YAEROUT(1)%MP) = ZAEROU1(KIDIA:KFDIA,:)
  IF (NAEROUT >= 2) PGFL(KIDIA:KFDIA,:,YAEROUT(2)%MP) = ZAEROU2(KIDIA:KFDIA,:)
  IF (NAEROUT >= 3) PGFL(KIDIA:KFDIA,:,YAEROUT(3)%MP) = ZAEROU3(KIDIA:KFDIA,:)
  IF (NAEROUT >= 4) PGFL(KIDIA:KFDIA,:,YAEROUT(4)%MP) = ZAEROU4(KIDIA:KFDIA,:)
  IF (NAEROUT >= 5) PGFL(KIDIA:KFDIA,:,YAEROUT(5)%MP) = ZAEROU5(KIDIA:KFDIA,:)
  IF (NAEROUT >= 6) PGFL(KIDIA:KFDIA,:,YAEROUT(6)%MP) = ZAEROU6(KIDIA:KFDIA,:)
  IF (NAEROUT >= 7) PGFL(KIDIA:KFDIA,:,YAEROUT(7)%MP) = ZAEROU7(KIDIA:KFDIA,:)
  IF (NAEROUT >= 8) PGFL(KIDIA:KFDIA,:,YAEROUT(8)%MP) = ZAEROU8(KIDIA:KFDIA,:)
  IF (NAEROUT >= 9) PGFL(KIDIA:KFDIA,:,YAEROUT(9)%MP) = ZAEROU9(KIDIA:KFDIA,:)
  IF (NAEROUT >= 10) PGFL(KIDIA:KFDIA,:,YAEROUT(10)%MP) = ZAEROU10(KIDIA:KFDIA,:)
  IF (NAEROUT >= 11) PGFL(KIDIA:KFDIA,:,YAEROUT(11)%MP) = ZAEROU11(KIDIA:KFDIA,:)
  IF (NAEROUT >= 12) PGFL(KIDIA:KFDIA,:,YAEROUT(12)%MP) = ZAEROU1(KIDIA:KFDIA,:)
  IF (NAEROUT >= 13) PGFL(KIDIA:KFDIA,:,YAEROUT(13)%MP) = ZAEROU13(KIDIA:KFDIA,:)
  IF (NAEROUT >= 14) PGFL(KIDIA:KFDIA,:,YAEROUT(14)%MP) = ZAEROU14(KIDIA:KFDIA,:)
  IF (NAEROUT >= 15) PGFL(KIDIA:KFDIA,:,YAEROUT(15)%MP) = ZAEROU15(KIDIA:KFDIA,:)
  IF (NAEROUT >= 16) PGFL(KIDIA:KFDIA,:,YAEROUT(16)%MP) = ZAEROU16(KIDIA:KFDIA,:)
  IF (NAEROUT >= 17) PGFL(KIDIA:KFDIA,:,YAEROUT(17)%MP) = ZAEROU17(KIDIA:KFDIA,:)
  ! additional diagnostics
  IF (NAEROUT >= 18) PGFL(KIDIA:KFDIA,:,YAEROUT(18)%MP) = PDRYDIAM(KIDIA:KFDIA,:,1)
  IF (NAEROUT >= 19) PGFL(KIDIA:KFDIA,:,YAEROUT(19)%MP) = PDRYDIAM(KIDIA:KFDIA,:,2)
  IF (NAEROUT >= 20) PGFL(KIDIA:KFDIA,:,YAEROUT(20)%MP) = PDRYDIAM(KIDIA:KFDIA,:,3)
  IF (NAEROUT >= 21) PGFL(KIDIA:KFDIA,:,YAEROUT(21)%MP) = PDRYDIAM(KIDIA:KFDIA,:,4)
  IF (NAEROUT >= 22) PGFL(KIDIA:KFDIA,:,YAEROUT(22)%MP) = PDRYDIAM(KIDIA:KFDIA,:,5)
  IF (NAEROUT >= 23) PGFL(KIDIA:KFDIA,:,YAEROUT(23)%MP) = PDRYDIAM(KIDIA:KFDIA,:,6)
  IF (NAEROUT >= 24) PGFL(KIDIA:KFDIA,:,YAEROUT(24)%MP) = PDRYDIAM(KIDIA:KFDIA,:,7)
  IF (NAEROUT >= 25) PGFL(KIDIA:KFDIA,:,YAEROUT(25)%MP) = PWETDIAM(KIDIA:KFDIA,:,1)
  IF (NAEROUT >= 26) PGFL(KIDIA:KFDIA,:,YAEROUT(26)%MP) = PWETDIAM(KIDIA:KFDIA,:,2)
  IF (NAEROUT >= 27) PGFL(KIDIA:KFDIA,:,YAEROUT(27)%MP) = PWETDIAM(KIDIA:KFDIA,:,3)
  IF (NAEROUT >= 28) PGFL(KIDIA:KFDIA,:,YAEROUT(28)%MP) = PWETDIAM(KIDIA:KFDIA,:,4)
  IF (NAEROUT >= 29) PGFL(KIDIA:KFDIA,:,YAEROUT(29)%MP) = PWETDIAM(KIDIA:KFDIA,:,5)
  IF (NAEROUT >= 30) PGFL(KIDIA:KFDIA,:,YAEROUT(30)%MP) = PWETDIAM(KIDIA:KFDIA,:,6)
  IF (NAEROUT >= 31) PGFL(KIDIA:KFDIA,:,YAEROUT(31)%MP) = PWETDIAM(KIDIA:KFDIA,:,7)
ENDIF


!------------------------------------------------------------------------------
!*         7.       STORE ALL AEROSOL VERTICALLY INTEGRATED FLUXES (TODO)
!                   ----------------------------------------------

DO JAER=1,NACTAERO
  DO JL=KIDIA,KFDIA
    PAERODDF(JL,JAER,1)=0._JPRB
    PAERODDF(JL,JAER,2)=0._JPRB
    PAERODDF(JL,JAER,3)=0._JPRB
    PAERODDF(JL,JAER,4)=0._JPRB
    PAERODDF(JL,JAER,5)=0._JPRB
    PAERODDF(JL,JAER,6)=0._JPRB
    PAERODDF(JL,JAER,7)=0._JPRB
  ENDDO
ENDDO

!-----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('AER_DIAGGLOMAP',1,ZHOOK_HANDLE)
END SUBROUTINE AER_DIAGGLOMAP

