SUBROUTINE AER_PHY3 &
 &( YDMODEL,KIDIA  , KFDIA  , KLON   , KTDIA  , KLEV , KTILES ,   &
 &  KFLDX, KLEVX,  KTRAC  , KAERO , KCHEM, &
 &  PRS1   , PRSF1  , PAEROP , PCAERO , PCEN  , PAPHIF , &
 &  PFPLCL , PFPLCN , PFPLSL , PFPLSN , PGELAT, PGELAM, &
 &  PAP    , PIP    , PLP    ,   PRP  ,    PSP  , PCOVPTOT, PLU ,&
 &  PFCCQL, PFCCQN, PFCSQL, PFCSQN, &
 &  PO3P  ,PQP   , PTP    , PTHP   , PTENC  , PCFLX  , &
 &  PAERDDP, PAERSDM, PAERSRC, PAERWS , PAERGUST     , PAERUST, PAERMAP, &
 &  PCLAERS, PPRAERS, PCHEM2AER, &  
 &  PALBD  , PTAUAER, PFRTI  , PLSM   , PSNS , PWND  , PWS1   , PAERFLX, PAERLIF, &
 &  PAERODDF, PFRAC_CA1, PFRAC_CA2, PTSPHY , PGFL   , &
 &  PODSS  , PODDU  , PODOM  , PODBC, PODSU , PODNI, PODAM, PODSOA, PODVFA , PODVSU , &
 &  PODTOACC , PAEPM1 , PAEPM25, PAEPM10, PAERAOT, PAERLISI, &
 &  PEXTRA, PAERO_WVL_DIAG  )

!**** *AER_PHY3* - ROUTINE DEALING WITH THE FINAL AEROSOL COMPUTATIONS:
!                  SCAVENGING, MASS DIAGNOSTICS, AEROSOL-PERTURBED RADIANCES

!      JJ.MORCRETTE , ECMWF


!**   INTERFACE.
!     ----------
!          *AER_PHY3* IS CALLED FROM *CALLPAR*.

! INPUTS:
! -------

! OUTPUTS:
! --------

!     EXTERNALS.
!     ----------
!          *AER_CGROWTH*, *AER_SO2SO4*, *AER_SCAVIN*, *AER_SCAVBC*, 
!          *AER_BDGTMSS*, *AER_SIXS*, *AER_NO3NH4*

!     MODIFICATIONS.
!     -------------
!          Original: JJ.Morcrette, 20060220  
!          JJMorcrette 20080402 New diagnostics
!          JJMorcrette 20080508 Gas-to-particle conversion  (from Hunneus and Boucher)
!          PBechtold   20091007 Reorganize for Scavenging
!          JJMorcrette 20130730 15-variable aerosol model
!          JJMorcrette 20131001 cleaning for 40R1
!          VHuijnen    20141001 Including optical properties output required for photolysis
!          K. Yessad (July 2014): Move some variables.
!          S. Rémy   (Jun 2016): add dry matter for sea-salt
!          S. Rémy   (Sep 2016): add nitrates
!          V. Huijnen (Aug 2017): moved computation of optical prop's for photolysis to tm5_macc_aerosol.F90

!     SWITCHES.
!     --------

!     MODEL PARAMETERS
!     ----------------

!-----------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMCST       , ONLY : RD, RG, RPI, RMSO2, RMSO4, YRCST
USE YOETHF       , ONLY : YRTHF
USE YOESRTCOP    , ONLY : RSASWA, RSASWB, RSFUA0, RSFUA1
USE YOMCT3       , ONLY : NSTEP

USE YOMCHEM      , ONLY : IEXTR_WD, IEXTR_CH, IEXTR_NG, IEXTR_CHTR
USE YOE_AERODIAG , ONLY : NPAERODIAG, NPAERAOT, NPAERLISI_VAR, NPAERLISI_WVL, &
  &                       JPAERODIAG_SRC, JPAERODIAG_DDP, JPAERODIAG_SDM, JPAERODIAG_WDL, &
  &                       JPAERODIAG_WDC, JPAERODIAG_NGT, JPAERODIAG_OD, &
  &                       JPAERLISI_EXT, JPAERLISI_BSTOA, JPAERLISI_BSGND, &
  &                       JPAERLISI_MOLBSC, JPAERLISI_AERBSC, &
  &                       JPAERLISI_355, JPAERLISI_532, JPAERLISI_1064, &
  &                       JPAERO_WVL_AOD, JPAERO_WVL_AODABS, JPAERO_WVL_AODFM, &
  &                       JPAERO_WVL_SSA, JPAERO_WVL_ASSIMETRY
   
!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL)       ,INTENT(INOUT):: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)   :: KIDIA, KFDIA, KLON
INTEGER(KIND=JPIM),INTENT(IN)   :: KTDIA, KLEV, KFLDX, KLEVX 
INTEGER(KIND=JPIM),INTENT(IN)   :: KTILES
INTEGER(KIND=JPIM),INTENT(IN)   :: KTRAC
INTEGER(KIND=JPIM),INTENT(IN)   :: KAERO(YDMODEL%YRML_GCONF%YGFL%NAERO)
INTEGER(KIND=JPIM),INTENT(IN)   :: KCHEM(YDMODEL%YRML_GCONF%YGFL%NCHEM)

REAL(KIND=JPRB),INTENT(IN)    :: PRSF1(KLON,KLEV), PRS1(KLON,0:KLEV), PAPHIF(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PAP(KLON,KLEV)  , PIP(KLON,KLEV)  , PLP(KLON,KLEV)  , PLU(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PFCCQL(KLON,KLEV),PFCCQN(KLON,KLEV),PFCSQL(KLON,KLEV), PFCSQN(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PRP(KLON,KLEV) , PSP(KLON,KLEV), PCOVPTOT(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PAEROP(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NAERO)
REAL(KIND=JPRB),INTENT(IN)    :: PCAERO(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB),INTENT(IN)    :: PCEN(KLON,KLEV,KTRAC), PCFLX(KLON,KTRAC)
REAL(KIND=JPRB),INTENT(IN)    :: PO3P(KLON,KLEV), PQP(KLON,KLEV), PTP(KLON,KLEV), PTHP(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PFPLCL(KLON,0:KLEV),PFPLCN(KLON,0:KLEV),PFPLSL(KLON,0:KLEV),PFPLSN(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)    :: PGELAT(KLON)    , PGELAM(KLON) 
REAL(KIND=JPRB),INTENT(IN)    :: PALBD(KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW), PFRTI(KLON,KTILES)
REAL(KIND=JPRB),INTENT(IN)    :: PAERDDP(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO), PAERSDM(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB),INTENT(IN)    :: PAERWS(KLON), PAERGUST(KLON), PAERUST(KLON), PAERMAP(KLON,5)
REAL(KIND=JPRB),INTENT(IN)    :: PAERSRC(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB),INTENT(IN)    :: PCHEM2AER(KLON,KLEV,4)
REAL(KIND=JPRB),INTENT(IN)    :: PAERFLX(KLON,YDMODEL%YRML_PHY_RAD%YREAERATM%NTYPAER(2))
REAL(KIND=JPRB),INTENT(IN)    :: PAERLIF(KLON,YDMODEL%YRML_PHY_RAD%YREAERATM%NTYPAER(2))
REAL(KIND=JPRB),INTENT(IN)    :: PCLAERS(KLON)
REAL(KIND=JPRB),INTENT(IN)    :: PLSM(KLON)  , PSNS(KLON)    , PWND(KLON)   , PWS1(KLON)
REAL(KIND=JPRB),INTENT(IN)    :: PFRAC_CA1(KLON),PFRAC_CA2(KLON)
REAL(KIND=JPRB),INTENT(IN)    :: PTSPHY

REAL(KIND=JPRB),INTENT(INOUT) :: PTENC(KLON,KLEV,KTRAC)
REAL(KIND=JPRB),INTENT(OUT)   :: PODSS(KLON), PODDU(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PODOM(KLON), PODBC(KLON), PODSU(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PODNI(KLON), PODAM(KLON), PODSOA(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PODVFA(KLON),PODVSU(KLON)
REAL(KIND=JPRB),INTENT(INOUT) :: PODTOACC(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PAEPM1(KLON),PAEPM25(KLON),PAEPM10(KLON)
REAL(KIND=JPRB),INTENT(OUT)   :: PAERODDF(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO,NPAERODIAG)
REAL(KIND=JPRB),INTENT(OUT)   :: PTAUAER(KLON,KLEV,7)
REAL(KIND=JPRB),INTENT(INOUT) :: PEXTRA(KLON,KLEVX,KFLDX)
REAL(KIND=JPRB),INTENT(INOUT) :: PGFL(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM), PPRAERS(KLON)

REAL(KIND=JPRB),INTENT(OUT)   :: PAERAOT(KLON,KLEV,NPAERAOT)
REAL(KIND=JPRB),INTENT(OUT)   :: PAERLISI(KLON,KLEV,NPAERLISI_WVL,NPAERLISI_VAR)
REAL(KIND=JPRB),INTENT(OUT)   :: PAERO_WVL_DIAG(KLON,YDMODEL%YRML_GCONF%YGFL%NAERO_WVL_DIAG,&
 &                                              YDMODEL%YRML_GCONF%YGFL%NAERO_WVL_DIAG_TYPES)


!*   0.5    LOCAL VARIABLES
!           ---------------

INTEGER(KIND=JPIM) :: JAER, JK, JL, JMMD, JSW, JWAVL, JT, IKLEVTROP(KLON)
INTEGER(KIND=JPIM) :: IAER, IBINDD
INTEGER(KIND=JPIM) :: IFLAG, IWHAT, INWAVL, ITWAVL(20)
INTEGER(KIND=JPIM) :: INBSS, INBDU, INBOM, INBBC, INBSU, INBFA, INBVO
INTEGER(KIND=JPIM) :: ISBC1, ISBC2, ISOM1, ISOM2, ISSO2, ISSO4, ISDUST,ISNO3,ISNH4,INH3_C,IHNO3_C,ISNH3_C,ISHNO3_C,ISN,IS
INTEGER(KIND=JPIM) :: ISNX(2), ISX(2) 
INTEGER(KIND=JPIM) :: IWHERE

REAL(KIND=JPRB) :: ZAEROK(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NAERO), ZTAEROK(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NAERO), &
 &  ZTAERO0(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
REAL(KIND=JPRB) :: ZFAERO(KLON,YDMODEL%YRML_GCONF%YGFL%NACTAERO)
!REAL(KIND=JPRB) :: ZAEROM(KLON,KLEV,NACTAERO)

REAL(KIND=JPRB) :: ZAER(KLON,KLEV) , ZAERNEG(KLON,KLEV+1),ZBETAB(KLON,KLEV), ZBETAI(KLON,KLEV)
REAL(KIND=JPRB) :: ZAP(KLON,KLEV)  , ZCLDWAT(KLON,KLEV), ZDUM(KLON,KLEV)
REAL(KIND=JPRB) :: ZWATER(KLON,KLEV), ZICE(KLON,KLEV)
REAL(KIND=JPRB) :: ZDP(KLON,KLEV)  , ZDZ(KLON,KLEV) 

REAL(KIND=JPRB) :: ZBCPHI(KLON,KLEV), ZBCPHO(KLON,KLEV), ZOMPHI(KLON,KLEV) , ZOMPHO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTBCPHI(KLON,KLEV),ZTBCPHO(KLON,KLEV),ZTOMPHI(KLON,KLEV), ZTOMPHO(KLON,KLEV)
REAL(KIND=JPRB) :: ZITBCPHO(KLON,KLEV),ZITOMPHO(KLON,KLEV),ZITSO2(KLON,KLEV)
REAL(KIND=JPRB) :: ZFSO2(KLON)  , ZFSO4(KLON)
  
REAL(KIND=JPRB) :: ZQSAT(KLON,KLEV), ZRHCL(KLON,KLEV), ZRHO(KLON,KLEV)
REAL(KIND=JPRB) :: ZTAER(KLON,KLEV), ZTAERI(KLON,KLEV)
REAL(KIND=JPRB) :: ZTAERO(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NAERO), ZTENV(KLON)
REAL(KIND=JPRB) :: ZNH3OK(KLON,KLEV)
REAL(KIND=JPRB) :: ZHNO3OK(KLON,KLEV)
REAL(KIND=JPRB) :: ZITNO3_1(KLON,KLEV)
REAL(KIND=JPRB) :: ZSO4(KLON,KLEV)
REAL(KIND=JPRB) :: ZSO2(KLON,KLEV), ZTSO2(KLON,KLEV)  , ZTSO4(KLON,KLEV)
REAL(KIND=JPRB) :: ZNO3_1(KLON,KLEV),ZNO3_2(KLON,KLEV), ZNH4(KLON,KLEV)
REAL(KIND=JPRB) :: ZDUST1(KLON,KLEV), ZDUST2(KLON,KLEV), ZDUST3(KLON,KLEV)
REAL(KIND=JPRB) :: ZTDUST1(KLON,KLEV), ZTDUST2(KLON,KLEV), ZTDUST3(KLON,KLEV)
REAL(KIND=JPRB) :: ZSS1(KLON,KLEV), ZSS2(KLON,KLEV), ZSS3(KLON,KLEV)
REAL(KIND=JPRB) :: ZTSS1(KLON,KLEV), ZTSS2(KLON,KLEV), ZTSS3(KLON,KLEV)
REAL(KIND=JPRB) :: ZTNO3_1(KLON,KLEV), ZTNO3_2(KLON,KLEV), ZTNH4(KLON,KLEV)
REAL(KIND=JPRB) :: ZTNH3(KLON,KLEV), ZTHNO3(KLON,KLEV)
REAL(KIND=JPRB) :: ZFNO3_1(KLON), ZFNO3_2(KLON), ZFNH4(KLON)
REAL(KIND=JPRB) :: ZTENC0(KLON,KLEV)



!REAL(KIND=JPRB) :: ZTYPDDP(KLON)   , ZTYPSDM(KLON)    , ZTYPSCC(KLON)    , ZTYPSCL(KLON)
!REAL(KIND=JPRB) :: ZTYPNGT(KLON)   , ZTYPMSS(KLON)    , ZTYPSRC(KLON)    , ZTYPTAU(KLON)

REAL(KIND=JPRB), ALLOCATABLE :: ZAEREXT(:,:,:), ZAEREXTD(:,:,:), ZAERMSS(:,:,:) ,&
  & ZAERMSST(:,:)  , ZAERTAU(:,:,:), ZAERTAUT(:,:,:), ZAERTAUI(:,:),&
  & ZAERTAUB(:,:,:), ZAERTAUA(:,:)  , ZAERTAUF(:,:) , ZAERSCC(:,:)   , ZAERSCL(:,:) ,ZAERSRC(:,:), &
  & ZAERNGT(:,:)   , ZAEROMGI(:,:)  , ZAERASYI(:,:) , ZAERABST(:,:)
REAL(KIND=JPRB) :: ZLISIM(KLON,YDMODEL%YRML_PHY_AER%YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZLISIA(KLON,YDMODEL%YRML_PHY_AER%YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZLISIS(KLON,YDMODEL%YRML_PHY_AER%YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZLISIT(KLON,YDMODEL%YRML_PHY_AER%YREAERLID%NWLID,0:KLEV)
REAL(KIND=JPRB) :: ZCONDEN(KLON), ZRAYL(KLON), ZSIGAER(KLON,6), ZEXTRH(KLON,3,3), ZABSRH(KLON,3,3)

REAL(KIND=JPRB) :: ZCLWAT, ZDEGRAD, ZEPSCOV, ZEPSWAT, ZRWSAT, ZRWPWP
REAL(KIND=JPRB) :: Z1CLD , ZDENSVIS,ZQIWP  , ZQLWP  , ZQRWP  , ZQSWP
REAL(KIND=JPRB) :: ZDESIC, ZRELRA , ZLIQRAI, ZSNOICE, ZCFLIRA, ZCFSNIC, ZVISCON, ZVISRAY
REAL(KIND=JPRB) :: ZNS   , ZRANGE , ZSIGAIR, ZVISICL, ZVISIPR, ZVISCAE, ZVISPAE 

REAL(KIND=JPRB) :: ZAEROU7(KLON,KLEV), ZQLI(KLON,KLEV)

REAL(KIND=JPRB), ALLOCATABLE :: ZAEROU16(:,:)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "surf_inq.h"

#include "satur.intfb.h"
#include "aer_cgrowth.intfb.h"
#include "aer_so2so4.intfb.h"
#include "aer_so2so4_v2.intfb.h"
#include "aer_no3nh4.intfb.h"
#include "troplev.intfb.h"
#include "aer_vso2so4.intfb.h"
#include "aer_scavin.intfb.h"
#include "aer_scavbc.intfb.h"
#include "aer_negat.intfb.h"
#include "aer_bdgtmss.intfb.h"
#include "chem_inext.intfb.h"
!#include "aer_sixs.intfb.h"
#include "aerc_scav.intfb.h"
#include "aerc_scavl19.intfb.h"
#include "chem_negat.intfb.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('AER_PHY3',0,ZHOOK_HANDLE)
ASSOCIATE(YDCOMPO=>YDMODEL%YRML_CHEM%YRCOMPO,YDEAERSNK=>YDMODEL%YRML_PHY_AER%YREAERSNK, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP,YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD, &
 & YDEAERLID=>YDMODEL%YRML_PHY_AER%YREAERLID,YDEAERSRC=>YDMODEL%YRML_PHY_AER%YREAERSRC, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL, YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY, &
 & YDEAERATM=>YDMODEL%YRML_PHY_RAD%YREAERATM, YDEPHLI=>YDMODEL%YRML_PHY_SLIN%YREPHLI)
ASSOCIATE(NACTAERO=>YGFL%NACTAERO, NAERO=>YGFL%NAERO, NCHEM=>YGFL%NCHEM, &
 & NDIM=>YGFL%NDIM, NAEROUT=>YGFL%NAEROUT, YAEROUT=>YGFL%YAEROUT, YR=>YGFL%YR, YS=>YGFL%YS, &
 & LCHEM_DIA=>YDCOMPO%LCHEM_DIA, YCHEM=>YGFL%YCHEM, &
 & LAERAOT=>YGFL%LAERAOT, LAERLISI=>YGFL%LAERLISI, LAERCHEM=>YGFL%LAERCHEM, &
 & LAER6SDIA=>YDEAERATM%LAER6SDIA, LAERCLIMG=>YDEAERATM%LAERCLIMG, &
 & LAERCLIMZ=>YDEAERATM%LAERCLIMZ, LAERGTOP=>YDEAERATM%LAERGTOP, &
 & LAERHYGRO=>YDEAERATM%LAERHYGRO, &
 & YAEROCLIM=>YGFL%YAEROCLIM, &
 & LAERNGAT=>YDEAERATM%LAERNGAT, &
 & LAERNITRATE => YDCOMPO%LAERNITRATE, &
 & NAERSCAV=>YDEAERATM%NAERSCAV, LAERVOL=>YDEAERATM%LAERVOL, &
 & NXT3DAER=>YDEAERATM%NXT3DAER, &
 & NWLID=>YDEAERLID%NWLID, &
 & NDDUST=>YDEAERSRC%NDDUST, NTYPAER=>YDEAERATM%NTYPAER, &
 & YSURF=>YDEPHY%YSURF, &
 & NSO4SCHEME => YDEAERSRC%NSO4SCHEME, &
 & RRHTAB=>YDEAERSNK%RRHTAB, &
 & LAERVISI=>YDERAD%LAERVISI, NTSW=>YDERAD%NTSW, RNS=>YDERAD%RNS, &
 & RSIGAIR=>YDERAD%RSIGAIR, &
 & NSTART=>YDRIP%NSTART, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, &
 & NAERO_WVL_DIAG=>YGFL%NAERO_WVL_DIAG, &
 & NAERO_WVL_DIAG_TYPES=>YGFL%NAERO_WVL_DIAG_TYPES, &
 & YAERO_DESC=>YDEAERATM%YAERO_DESC)
!     ------------------------------------------------------------------

!*         0.     PROGNOSTIC AEROSOLS - FINAL COMPUTATIONS
!                 ----------------------------------------


ZEPSCOV=1.E-03_JPRB
ZEPSWAT=1.E-18_JPRB
ZDEGRAD=180._JPRB/RPI
CALL SURF_INQ(YSURF,PRWSAT=ZRWSAT)
CALL SURF_INQ(YSURF,PRWPWP=ZRWPWP)
IBINDD=NTYPAER(2)
INWAVL=20


!*         0.1    SWITCHING ON POSSIBLE DEBUG PRINTS AND ALLOCATING MEMORY
!                 --------------------------------------------------------

ALLOCATE( ZAERMSS(KLON,KLEV,NACTAERO) )
ALLOCATE( ZAERMSST(KLON,NACTAERO) )
ALLOCATE( ZAERTAU(KLON,KLEV,NACTAERO) )
ALLOCATE( ZAERTAUT(KLON,NACTAERO,INWAVL)  )
ALLOCATE( ZAERTAUI(KLON,INWAVL) )
ALLOCATE( ZAEROMGI(KLON,INWAVL) )
ALLOCATE( ZAERASYI(KLON,INWAVL) )
ALLOCATE( ZAERTAUB(KLON,NACTAERO,INWAVL)  )
ALLOCATE( ZAERTAUA(KLON,INWAVL) )
ALLOCATE( ZAERTAUF(KLON,INWAVL) )
ALLOCATE( ZAERSCC(KLON,NACTAERO) )
ALLOCATE( ZAERSCL(KLON,NACTAERO) )
ALLOCATE( ZAERSRC(KLON,NACTAERO) )
ALLOCATE( ZAERNGT(KLON,NAERO) )
ALLOCATE( ZAEREXT(KLON,KLEV,INWAVL) )
ALLOCATE( ZAEREXTD(KLON,KLEV,INWAVL) )
ALLOCATE( ZAERABST(KLON,KLEV) )

ZAERMSS(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=0._JPRB
ZAERMSST(KIDIA:KFDIA,1:NACTAERO)      =0._JPRB
ZAERTAU(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=0._JPRB
ZAERTAUT(KIDIA:KFDIA,1:NACTAERO,1:INWAVL) =0._JPRB
ZAERTAUI(KIDIA:KFDIA,1:INWAVL)            =0._JPRB
ZAEROMGI(KIDIA:KFDIA,1:INWAVL)            =0._JPRB
ZAERASYI(KIDIA:KFDIA,1:INWAVL)            =0._JPRB
ZAERTAUB(KIDIA:KFDIA,1:NACTAERO,1:INWAVL) =0._JPRB
ZAERTAUA(KIDIA:KFDIA,1:INWAVL)            =0._JPRB
ZAERTAUF(KIDIA:KFDIA,1:INWAVL)            =0._JPRB
ZAERSCC(KIDIA:KFDIA,1:NACTAERO)       =0._JPRB
ZAERSCL(KIDIA:KFDIA,1:NACTAERO)       =0._JPRB

ZAERNGT(KIDIA:KFDIA,1:NAERO)          =0._JPRB
ZAEREXT(KIDIA:KFDIA,1:KLEV,1:INWAVL)  =0._JPRB
ZAEREXTD(KIDIA:KFDIA,1:KLEV,1:INWAVL) =0._JPRB
ZAERABST(KIDIA:KFDIA,1:KLEV) = 0._JPRB

ZLISIM(:,:,:) =0._JPRB
ZLISIA(:,:,:) =0._JPRB
ZLISIS(:,:,:) =0._JPRB
ZLISIT(:,:,:) =0._JPRB
ZSIGAER(:,:)  =0._JPRB

ZTAERO(:,:,:)=0._JPRB 

ZAERSRC(KIDIA:KFDIA,1:NACTAERO)=PAERSRC(KIDIA:KFDIA,1:NACTAERO) 
! computation of tropopause level 
CALL TROPLEV(KLON,KIDIA,KFDIA,KLEV,.FALSE.,PTP,PQP,PRSF1,IKLEVTROP)

IF (LAERLISI .AND. NAEROUT > 0) THEN
  ALLOCATE( ZAEROU16(KLON,KLEV) )
  ZAEROU16(:,:) =0._JPRB
ENDIF


DO JAER=1,NAERO
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZAEROK(JL,JK,JAER) =PCEN (JL,JK,KAERO(JAER))
      ZTAEROK(JL,JK,JAER)=PTENC(JL,JK,KAERO(JAER))
    ENDDO
  ENDDO
ENDDO

ZNH3OK(:,:)=0.0_JPRB
ZNH4(:,:)=0.0_JPRB
ZNO3_1(:,:)=0.0_JPRB
ZNO3_2(:,:)=0.0_JPRB
ZHNO3OK(:,:)=0.0_JPRB

ZTNH4(:,:)=0.0_JPRB
ZTNH3(:,:)=0.0_JPRB
ZTHNO3(:,:)=0.0_JPRB
ZTNO3_1(:,:)=0.0_JPRB
ZTNO3_2(:,:)=0.0_JPRB
! identify index for HNO3 in chemistry, and store initial tendency
IF (LAERNITRATE .AND. NCHEM >0) THEN
 DO JT=1,NCHEM
  IF ((TRIM(YCHEM(JT)%CNAME) == 'NH3' )) THEN
    ISNH3_C=KCHEM(JT)
    INH3_C=JT
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZNH3OK(JL,JK) = MAX(0._JPRB,PCEN(JL,JK,KCHEM(JT))  )
        !ZNH3OK(JL,JK) = MAX(0._JPRB,PCEN(JL,JK,KCHEM(JT)) + PTENC(JL,JK,KCHEM(JT))*PTSPHY )
      ENDDO
    ENDDO
  ENDIF
 ENDDO
 DO JT=1,NCHEM
  IF ((TRIM(YCHEM(JT)%CNAME) == 'HNO3' )) THEN
  ISHNO3_C=KCHEM(JT)
  IHNO3_C=JT
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZHNO3OK(JL,JK) = MAX(0._JPRB,PCEN (JL,JK,KCHEM(JT)))
        !ZHNO3OK(JL,JK) = MAX(0._JPRB,PCEN(JL,JK,KCHEM(JT)) + PTENC(JL,JK,KCHEM(JT))*PTSPHY )
      ENDDO
    ENDDO
  ENDIF
 ENDDO
ENDIF

IFLAG=2
CALL SATUR (YRTHF, YRCST, KIDIA , KFDIA , KLON  , KTDIA , KLEV, LPHYLIN, &
  & PRSF1, PTP    , ZQSAT , IFLAG)  

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZRHO(JL,JK)=PRSF1(JL,JK)/(RD*PTP(JL,JK))
    ZDP(JL,JK)= PRS1(JL,JK) - PRS1(JL,JK-1)
    ZDZ(JL,JK)= ZDP(JL,JK) / (ZRHO(JL,JK)*RG)  
    ZRHCL(JL,JK)=PQP(JL,JK)/(MAX(1.E-30_JPRB, ZQSAT(JL,JK)))
  ENDDO
ENDDO


!-------------------------------------------------------------------------------
!** First batch of processes: Hygroscopicity on OM and BC, and Gas-to-particle 
!   conversion. As these processes are applied separately on individual aerosol 
!   types: successive addition of tendencies is done "outside"

!*         1.     HYGROSCOPY (APPLIED TO BC AND OM ONLY)
!                 --------------------------------------
IF (LCHEM_DIA) THEN
   ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO) =  ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
   IF (LAERNITRATE .AND. NCHEM>0) ZTENC0(KIDIA:KFDIA,1:KLEV) = 0._JPRB
ENDIF 

IF (LAERHYGRO .AND. NTYPAER(3) /= 0) THEN
  ISOM1=NTYPAER(1)+NTYPAER(2)+1
  ISOM2=ISOM1+1

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZOMPHI(JL,JK)=PAEROP(JL,JK,ISOM1)
      ZOMPHO(JL,JK)=PAEROP(JL,JK,ISOM2)
      ZITOMPHO(JL,JK)=ZTAEROK(JL,JK,ISOM2)
    ENDDO
  ENDDO

  CALL AER_CGROWTH(YDEAERATM,KIDIA,KFDIA,KLON,KLEV,ZOMPHO,ZITOMPHO,PTSPHY,ZTOMPHI,ZTOMPHO)

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTAEROK(JL,JK,ISOM1)=ZTAEROK(JL,JK,ISOM1)+ZTOMPHI(JL,JK)
      ZTAEROK(JL,JK,ISOM2)=ZTAEROK(JL,JK,ISOM2)+ZTOMPHO(JL,JK)
    ENDDO
  ENDDO
ENDIF

IF (LAERHYGRO .AND. NTYPAER(4) /= 0) THEN
  ISBC1=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+1
  ISBC2=ISBC1+1

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZBCPHI(JL,JK)=PAEROP(JL,JK,ISBC1)
      ZBCPHO(JL,JK)=PAEROP(JL,JK,ISBC2)
      ZITBCPHO(JL,JK)=ZTAEROK(JL,JK,ISBC2)
    ENDDO
  ENDDO

  CALL AER_CGROWTH(YDEAERATM,KIDIA,KFDIA,KLON,KLEV,ZBCPHO,ZITBCPHO,PTSPHY,ZTBCPHI,ZTBCPHO)

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTAEROK(JL,JK,ISBC1)=ZTAEROK(JL,JK,ISBC1)+ZTBCPHI(JL,JK)
      ZTAEROK(JL,JK,ISBC2)=ZTAEROK(JL,JK,ISBC2)+ZTBCPHO(JL,JK)
    ENDDO
  ENDDO
ENDIF


!*         2.     GAS-TO-PARTICLE CONVERSION (SO2 -> SO4)
!*         2.1    (SO2 -> SO4)
!                 ---------------------------------------

IF (NTYPAER(5) /= 0) THEN
  ISSO4=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+1
  IF (LAERGTOP) THEN
    IF (LAERCHEM) THEN
      ! Use conversion calculated in chemistry scheme
      ZFSO4(:) = 0._JPRB
      ZFSO2(:) = 0._JPRB
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTSO4(JL,JK) = PCHEM2AER(JL,JK,1)
          ZTSO2(JL,JK) = - PCHEM2AER(JL,JK,1) * (RMSO2/RMSO4)
          ZFSO4(JL) = ZFSO4(JL) + ZTSO4(JL,JK)*(ZDP(JL,JK)/RG)
          ZFSO2(JL) = ZFSO2(JL) + ZTSO2(JL,JK)*(ZDP(JL,JK)/RG)
          ZTAEROK(JL,JK,ISSO4)=ZTAEROK(JL,JK,ISSO4)+ZTSO4(JL,JK)
       ENDDO
      ENDDO

      ! SO4 Source
      DO JL=KIDIA,KFDIA
        ZAERSRC(JL,ISSO4)=ZAERSRC(JL,ISSO4)-ZFSO4(JL)
      ENDDO
    ELSE
      ISSO2=ISSO4+1
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
!          ZSO2(JL,JK)=ZAEROK(JL,JK,ISSO2)
          ZSO2(JL,JK)=PAEROP(JL,JK,ISSO2)
          ZITSO2(JL,JK)=ZTAEROK(JL,JK,ISSO2)
          ZQLI(JL,JK)=PLP(JL,JK)+PIP(JL,JK)
        ENDDO
      ENDDO

      SELECT CASE (NSO4SCHEME)
        CASE(1)
          CALL AER_SO2SO4 (YDRIP, YDEAERSNK, KIDIA , KFDIA, KLON, KLEV, &
          & ZSO2 , ZITSO2, PGELAT, PGELAM, PTSPHY, ZRHCL, PTP, &
          & ZTSO2, ZTSO4, ZFSO2,ZFSO4,ZDP )

        CASE(2)
          CALL AER_SO2SO4_V2 ( YDRIP, &
          &  KIDIA , KFDIA , KLON  , KLEV  ,         &
          &  PTSPHY, PTP   , PRSF1 , PAP  , ZQLI  , PGELAT, PGELAM, &
          &  ZSO2  , ZITSO2, PGFL(:,:,YAEROCLIM(1)%MP), &
          &  PGFL(:,:,YAEROCLIM(2)%MP), PGFL(:,:,YAEROCLIM(3)%MP) ,&
          &  ZTSO2 , ZTSO4, ZFSO2, ZFSO4, ZDP )

        CASE DEFAULT
          CALL ABOR1('ABORT: IN AER_PHY3, NSO4SCHEME MUST BE 1 OR 2')
      END SELECT

      ! SO4 Source
      DO JL=KIDIA,KFDIA
        ZAERSRC(JL,ISSO4)=ZAERSRC(JL,ISSO4)-ZFSO4(JL)
      ENDDO

      ! SO2 sink, added to scavenging (null)
      DO JL=KIDIA,KFDIA
        ! This used to go in the SO4 scavenging field rather than SO2,
        ! which appears wrong!
        ZAERSCL(JL,ISSO2)=ZAERSCL(JL,ISSO2)-ZFSO2(JL)
      ENDDO

      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTAEROK(JL,JK,ISSO2)=ZTAEROK(JL,JK,ISSO2)+ZTSO2(JL,JK)
          ZTAEROK(JL,JK,ISSO4)=ZTAEROK(JL,JK,ISSO4)+ZTSO4(JL,JK)
       ENDDO
      ENDDO
    ENDIF
  ENDIF
ENDIF

IF (LAERVOL) THEN
  ISSO4=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)+NTYPAER(6)+NTYPAER(7)+NTYPAER(8)+NTYPAER(9)+1
  ISSO2=ISSO4+1
  IF (LAERGTOP) THEN
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
!        ZSO2(JL,JK)=ZAEROK(JL,JK,ISSO2)
        ZSO2(JL,JK)=PAEROP(JL,JK,ISSO2)
        ZITSO2(JL,JK)=ZTAEROK(JL,JK,ISSO2)
      ENDDO
    ENDDO

    SELECT CASE (NSO4SCHEME)
      CASE(1)
        CALL AER_VSO2SO4 (YDEAERSNK, KIDIA, KFDIA, KLON, KLEV, &
          & ZSO2, ZITSO2, PGELAT, PTSPHY, ZTSO2, ZTSO4 )

      CASE(2)
        CALL AER_SO2SO4_V2 ( YDRIP, &
          &  KIDIA , KFDIA , KLON  , KLEV  ,         &
          &  PTSPHY, PTP   , PRSF1 , PAP  , ZQLI  , PGELAT, PGELAM, &
          &  ZSO2  , ZITSO2, PGFL(:,:,YAEROCLIM(1)%MP), &
          &  PGFL(:,:,YAEROCLIM(2)%MP), PGFL(:,:,YAEROCLIM(3)%MP) ,&
          &  ZTSO2 , ZTSO4, ZFSO2, ZFSO4, ZDP )
      CASE DEFAULT
        CALL ABOR1('ABORT: IN AER_PHY3, NSO4SCHEME MUST BE 1 OR 2')
      END SELECT
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAEROK(JL,JK,ISSO2)=ZTAEROK(JL,JK,ISSO2)+ZTSO2(JL,JK)
        ZTAEROK(JL,JK,ISSO4)=ZTAEROK(JL,JK,ISSO4)+ZTSO4(JL,JK)
     ENDDO
    ENDDO
  ENDIF
ENDIF

! computation of tropopause level 
CALL TROPLEV(KLON,KIDIA,KFDIA,KLEV,.FALSE.,PTP,PQP,PRSF1,IKLEVTROP)


!*         2.2    (NO3 and NH4 formation from HNO3, NH3 + Het. uptake)
!                 ---------------------------------------

IF (LAERNITRATE) THEN
  ISDUST=NTYPAER(1)+1
  ISNO3=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)+1
  ISNH4=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)+NTYPAER(6)+1

  DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZSS1(JL,JK)=ZAEROK(JL,JK,1)
        ZSS2(JL,JK)=ZAEROK(JL,JK,2)
        ZSS3(JL,JK)=ZAEROK(JL,JK,3)
        ZTSS1(JL,JK)=ZTAEROK(JL,JK,1)
        ZTSS2(JL,JK)=ZTAEROK(JL,JK,2)
        ZTSS3(JL,JK)=ZTAEROK(JL,JK,3)
        ZDUST1(JL,JK)=ZAEROK(JL,JK,ISDUST)
        ZDUST2(JL,JK)=ZAEROK(JL,JK,ISDUST+1)
        ZDUST3(JL,JK)=ZAEROK(JL,JK,ISDUST+2)
        ZTDUST1(JL,JK)=ZTAEROK(JL,JK,ISDUST)
        ZTDUST2(JL,JK)=ZTAEROK(JL,JK,ISDUST+1)
        ZTDUST3(JL,JK)=ZTAEROK(JL,JK,ISDUST+2)
        ZSO4(JL,JK)=ZAEROK(JL,JK,ISSO4)
        ZNO3_1(JL,JK)=ZAEROK(JL,JK,ISNO3)
        ZNO3_2(JL,JK)=ZAEROK(JL,JK,ISNO3+1)
        ZNH4(JL,JK)=ZAEROK(JL,JK,ISNH4)
        ZITNO3_1(JL,JK)=ZTAEROK(JL,JK,ISNO3)
    ENDDO
  ENDDO

  IF (NCHEM >0) THEN
  ! Add chemical conversion within cloud droplets (see tm5_wetchem)
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTNH4(JL,JK) = PCHEM2AER(JL,JK,4)
        ZTAEROK(JL,JK,ISNH4)=ZTAEROK(JL,JK,ISNH4)+ZTNH4(JL,JK)
        ZAERSRC(JL,ISNH4)=ZAERSRC(JL,ISNH4)+ZTNH4(JL,JK)*(ZDP(JL,JK)/RG)
      ENDDO
    ENDDO
  ENDIF


  CALL AER_NO3NH4 (YDEAERSNK,YDEAERATM,KIDIA , KFDIA, KLON, KLEV, IKLEVTROP, &
      & ZSO4 , ZNO3_1, ZNO3_2, ZNH4, ZNH3OK, ZHNO3OK, ZDUST1, ZDUST2, ZDUST3,&
      & ZTDUST1, ZTDUST2, ZTDUST3, ZSS1,ZSS2,ZSS3,ZTSS1,ZTSS2,ZTSS3,&
      & ZRHO, ZRHCL, PTP, ZDP, PTSPHY,PFRAC_CA1, PFRAC_CA2, &
      & ZTNO3_1,ZTNO3_2, ZTNH4, ZTHNO3, ZTNH3, ZFNO3_1,ZFNO3_2,ZFNH4)

   DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTAEROK(JL,JK,ISNO3)=ZTAEROK(JL,JK,ISNO3)+ZTNO3_1(JL,JK)
      ZTAEROK(JL,JK,ISNO3+1)=ZTAEROK(JL,JK,ISNO3+1)+ZTNO3_2(JL,JK)
      ZTAEROK(JL,JK,ISNH4)=ZTAEROK(JL,JK,ISNH4)+ZTNH4(JL,JK)
      ZTAEROK(JL,JK,1)=ZTSS1(JL,JK)
      ZTAEROK(JL,JK,2)=ZTSS2(JL,JK)
      ZTAEROK(JL,JK,3)=ZTSS3(JL,JK)
      ZTAEROK(JL,JK,ISDUST)=ZTDUST1(JL,JK)
      ZTAEROK(JL,JK,ISDUST+1)=ZTDUST2(JL,JK)
      ZTAEROK(JL,JK,ISDUST+2)=ZTDUST3(JL,JK)
      ! Update NH3 and HNO3 (chemical species)
      IF (NCHEM > 0) THEN
         PTENC(JL,JK,ISHNO3_C)  =  PTENC(JL,JK,ISHNO3_C)+ ZTHNO3(JL,JK)
         PTENC(JL,JK,ISNH3_C)  =  PTENC(JL,JK,ISNH3_C)+ ZTNH3(JL,JK)
      ENDIF
    ENDDO
  ENDDO
  ! NO3/NH4 Source
  DO JL=KIDIA,KFDIA
      ZAERSRC(JL,ISNO3)=ZAERSRC(JL,ISNO3)-ZFNO3_1(JL)
      ZAERSRC(JL,ISNO3+1)=ZAERSRC(JL,ISNO3+1)-ZFNO3_2(JL)
      ZAERSRC(JL,ISNH4)=ZAERSRC(JL,ISNH4)-ZFNH4(JL)
  ENDDO

  ! collect chemistry tendencies 
  IF (LCHEM_DIA) THEN
    IF (LAERNITRATE .AND. NCHEM>0) THEN
      ! Also account for change in HNO3
      CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , 1, 1,  &
       &    ZDP, PTSPHY, ZTHNO3,ZTENC0,PEXTRA(:,ISHNO3_C,IEXTR_CH))
      ! Also account for change in HNO3, troposphere
      CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , 1, 1,  &
       &    ZDP, PTSPHY,ZTHNO3,ZTENC0,PEXTRA(:,ISHNO3_C,IEXTR_CHTR),KTOP=IKLEVTROP)
      ! Also account for change in NH3 
      CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , 1, 1,  &
       &    ZDP, PTSPHY, ZTNH3,ZTENC0,PEXTRA(:,ISNH3_C,IEXTR_CH))
      ! Also account for change in NH3, troposphere
      CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , 1, 1,  &
       &    ZDP, PTSPHY,ZTNH3,ZTENC0,PEXTRA(:,ISNH3_C,IEXTR_CHTR),KTOP=IKLEVTROP)
    ENDIF
  ENDIF

ENDIF


! collect chemistry tendencies 
IF (LCHEM_DIA) THEN
  CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , NACTAERO, NACTAERO ,  & 
   &    ZDP, PTSPHY, ZTAEROK ,ZTAERO0, PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_CH))
  !For troposphere only:
  CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , NACTAERO, NACTAERO , &
   &    ZDP, PTSPHY, ZTAEROK ,ZTAERO0,PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_CHTR),KTOP=IKLEVTROP)
ENDIF

!-------------------------------------------------------------------------------
!** Second batch of processes: Scavenging in- and below-cloud by large-scale, 
!   then convective precipitation. As these processes are applied 
!   systematically on all aerosol types: successive addition of tendencies is 
!   done "inside" the AER_SCAVIN and AER_SCAVBC routines

!*         3.     SCAVENGING OF PROGNOSTIC AEROSOLS FROM PRECIPITATION
!                 ----------------------------------------------------

IF (NAERSCAV==1) THEN

  IF (LCHEM_DIA) THEN
     ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO) =  ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ENDIF 
     
  DO JAER=1,NACTAERO
    ! Skip species if no scavenging configured
    IF (YAERO_DESC(JAER)%RSCAVIN == 0._JPRB .AND. &
     &  YAERO_DESC(JAER)%RSCAVBCR == 0._JPRB .AND. &
     &  YAERO_DESC(JAER)%RSCAVBCS == 0._JPRB) CYCLE

!* USING CURRENT LARGE-SCALE PRECIPITATION FLUXES

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZBETAI(JL,JK)=0._JPRB
        ZBETAB(JL,JK)=0._JPRB
        ZAER(JL,JK)  = PAEROP(JL,JK,JAER)
        ZTAERI(JL,JK)= ZTAEROK(JL,JK,JAER)
        ZTAER(JL,JK) =0._JPRB

        ZCLWAT = MAX(0._JPRB, PLP(JL,JK)+PIP(JL,JK))
        IF (ZCLWAT > ZEPSWAT .AND. PAP(JL,JK) > ZEPSCOV) THEN
          ZAP(JL,JK)    =PAP(JL,JK)
          ZCLDWAT(JL,JK)=ZCLWAT / PAP(JL,JK)
          ZWATER(JL,JK)= PLP(JL,JK) / PAP(JL,JK)
          ZICE(JL,JK)= PIP(JL,JK) / PAP(JL,JK)
        ELSE
          ZAP(JL,JK)    =0.0_JPRB
          ZCLDWAT(JL,JK)=0.0_JPRB
          ZWATER(JL,JK)= 0.0_JPRB
          ZICE(JL,JK)= 0.0_JPRB
        ENDIF
      ENDDO
    ENDDO
    ZTENV(:)=0._JPRB

!-- both SCAVIN and SCAVBC are entered with kg kg-1

!    IF (NSTEP == YRRIP%NSTART) WRITE(NULOUT,FMT='(" aer_phy3 LS scav JAER ",I4)') JAER

    CALL AER_SCAVIN &
      &( YDEAERSNK, &
      &  KIDIA, KFDIA, KLON , KLEV  , YAERO_DESC(JAER)%RSCAVIN    , NSTART, NSTEP  , &
      &  ZDP  , ZDZ  , ZRHO , PTP, PFPLSL, PFPLSN, ZAP   , ZCLDWAT, ZWATER, ZICE,ZAER  , ZTAERI, &
      &  PTSPHY, &
      &  ZTAER, ZTENV )

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAERI(JL,JK)=ZTAER(JL,JK)
        ZTAER(JL,JK)=0._JPRB
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZAERSCL(JL,JAER)=ZTENV(JL)
    ENDDO

    CALL AER_SCAVBC &
      &( YDEAERSNK, KIDIA , KFDIA  , KLON  , KLEV, &
      &  YAERO_DESC(JAER)%RSCAVBCR, YAERO_DESC(JAER)%RSCAVBCS, &
      &  NSTEP , ZDP, &
      &  PFPLSL, PFPLSN , ZAER  , ZTAERI, PTSPHY, &
      &  ZTAER,ZTENV )

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAERO(JL,JK,JAER)=ZTAER(JL,JK)
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZAERSCL(JL,JAER)=ZAERSCL(JL,JAER)+ZTENV(JL)
    ENDDO

!* USING CURRENT CONVECTIVE PRECIPITATION FLUXES

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZBETAI(JL,JK)=0._JPRB
        ZBETAB(JL,JK)=0._JPRB
        ZTAERI(JL,JK)=ZTAERO(JL,JK,JAER)
        ZTAER(JL,JK) =0._JPRB

        ZCLWAT = MAX(0._JPRB, PLU(JL,JK))
        IF (ZCLWAT > ZEPSWAT) THEN
          ZAP(JL,JK)    =0.05_JPRB
          ZAP(JL,JK)    =PAP(JL,JK)
          ZCLDWAT(JL,JK)=ZCLWAT
          ZWATER(JL,JK)=ZCLDWAT(JL,JK)
          ZICE(JL,JK)=0.0_JPRB
          IF (PTP(JL,JK) < 238.15_JPRB) THEN
             ZWATER(JL,JK)=0._JPRB
             ZICE(JL,JK)=ZCLDWAT(JL,JK)
          ELSEIF (PTP(JL,JK) < 273.15_JPRB) THEN
             ZWATER(JL,JK)=(PTP(JL,JK)-238.15_JPRB)/35._JPRB*ZCLDWAT(JL,JK)
             ZICE(JL,JK)=ZCLDWAT(JL,JK)-ZWATER(JL,JK)
          ENDIF
        ELSE
          ZAP(JL,JK)    =0.0_JPRB
          ZCLDWAT(JL,JK)=0.0_JPRB
          ZWATER(JL,JK)=0.0_JPRB
          ZICE(JL,JK)=0.0_JPRB
        ENDIF
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZTENV(JL)=0._JPRB
    ENDDO

!    IF (NSTEP == YRRIP%NSTART) WRITE(NULOUT,FMT='(" aer_phy3 CV scav JAER ",I4)') JAER

    CALL AER_SCAVIN &
      &( YDEAERSNK   , KIDIA, KFDIA, KLON , KLEV  , YAERO_DESC(JAER)%RSCAVIN, NSTART, NSTEP  , &
      &  ZDP  , ZDZ  , ZRHO , PTP, PFPLCL, PFPLCN, ZAP   , ZCLDWAT, ZWATER, ZICE,ZAER  , ZTAERI, &
      &  PTSPHY, &
      &  ZTAER, ZTENV )

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAERI(JL,JK)=ZTAER(JL,JK)
        ZTAER(JL,JK)=0._JPRB
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZAERSCC(JL,JAER)=ZTENV(JL)
    ENDDO

    CALL AER_SCAVBC &
      &( YDEAERSNK, KIDIA , KFDIA  , KLON  , KLEV, &
      &  YAERO_DESC(JAER)%RSCAVBCR, YAERO_DESC(JAER)%RSCAVBCS, &
      &  NSTEP , ZDP, &
      &  PFPLCL, PFPLCN , ZAER  , ZTAERI, PTSPHY, &
      &  ZTAER,ZTENV )

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAEROK(JL,JK,JAER)=ZTAER(JL,JK)
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZAERSCC(JL,JAER)=ZAERSCC(JL,JAER)+ZTENV(JL)
    ENDDO

  !ENDIF

  ENDDO ! JAER

! collect wet dep tendencies

  IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , NACTAERO, NACTAERO ,  & 
    &    ZDP, PTSPHY, ZTAEROK ,ZTAERO0, PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_WD))
  ENDIF

ELSE
  ZAERSCL(:,:)=0._JPRB
  ZAERSCC(:,:)=0._JPRB
ENDIF

IF (NAERSCAV==2) THEN
  ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ZFAERO(:,:)=0._JPRB
! CHEM_SCAV updates tendency 
!* convective precip 
  ZDUM(:,:)=0._JPRB
  CALL  AERC_SCAV &
    & ( YDMODEL, KIDIA , KFDIA  , KLON , KLEV , NACTAERO ,NACTAERO,  NSTEP, PTSPHY, &
    &   PRSF1, ZDP, PTP, PFPLCL, PFPLCN, PFCCQL, PFCCQN,PAP, PLU, ZDUM, PRP, PSP,  ZAEROK, ZTAERO0,  &
    &   ZTAEROK,ZFAERO)

    DO JL=KIDIA,KFDIA
      ZAERSCC(JL,1:NACTAERO)=ZFAERO(JL,1:NACTAERO)
    ENDDO
! write to extra fields for diagnostic  
  IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV, NACTAERO, NACTAERO, ZDP, PTSPHY, ZTAEROK, ZTAERO0, &
    & PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_WD))
  ENDIF
  ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ZFAERO(:,:)=0._JPRB
! * Grid-scale precip 
  CALL  AERC_SCAV &
    & ( YDMODEL, KIDIA , KFDIA  , KLON , KLEV , NACTAERO ,NACTAERO , NSTEP, PTSPHY, &
    &   PRSF1, ZDP, PTP, PFPLSL, PFPLSN,  PFCSQL, PFCSQN, PAP, PLP, PIP, PRP, PSP,  ZAEROK,  ZTAERO0,  &
    &   ZTAEROK, ZFAERO, PPRCOV=PCOVPTOT )

    DO JL=KIDIA,KFDIA
      ZAERSCL(JL,1:NACTAERO)=ZFAERO(JL,1:NACTAERO)
    ENDDO
! write flux to extra fields for diagnostic of wet deposition  
  IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV, NACTAERO , NACTAERO, ZDP, PTSPHY, ZTAEROK, ZTAERO0, &
    & PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_WD) )
  ENDIF
ENDIF  

IF (NAERSCAV==3 ) THEN
   ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ZFAERO(:,:)=0._JPRB
! CHEM_SCAV updates tendency
!* convective precip
  ZDUM(:,:)=0._JPRB
  CALL  AERC_SCAVL19 &
    & ( YDMODEL, KIDIA , KFDIA  , KLON , KLEV , NACTAERO ,NACTAERO,  NSTEP,PTSPHY, &
    &   PRSF1, ZDP, PTP, PFPLCL, PFPLCN, PAP, PLU, ZDUM, PRP, PSP,  ZAEROK, ZTAERO0,  &
    &   ZTAEROK,ZFAERO)

    DO JL=KIDIA,KFDIA
      ZAERSCC(JL,1:NACTAERO)=ZFAERO(JL,1:NACTAERO)
    ENDDO
! write to extra fields for diagnostic
  IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV, NACTAERO, NACTAERO, ZDP, PTSPHY, ZTAEROK, ZTAERO0, &
    & PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_WD))
  ENDIF
  ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO)=ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ZFAERO(:,:)=0._JPRB
! * Grid-scale precip
  CALL  AERC_SCAVL19 &
    & ( YDMODEL, KIDIA , KFDIA  , KLON , KLEV , NACTAERO ,NACTAERO , NSTEP,PTSPHY, &
    &   PRSF1, ZDP, PTP, PFPLSL, PFPLSN,  PAP, PLP, PIP, PRP, PSP,  ZAEROK, ZTAERO0,  &
    &   ZTAEROK, ZFAERO, PPRCOV=PCOVPTOT)

    DO JL=KIDIA,KFDIA
      ZAERSCL(JL,1:NACTAERO)=ZFAERO(JL,1:NACTAERO)
    ENDDO
! write flux to extra fields for diagnostic of wet deposition
  IF (LCHEM_DIA) THEN
    CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV, NACTAERO , NACTAERO, ZDP,PTSPHY, ZTAEROK, ZTAERO0, &
    & PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_WD) )
  ENDIF
ENDIF


!-- put back all tendencies in the field for GEMS tendencies

PTENC(KIDIA:KFDIA,1:KLEV,KAERO(1):KAERO(NAERO)) = ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NAERO)
     

!*         4.    ELIMINATION OF NEGATIVE PROGNOSTIC AEROSOL CONCENTRATIONS
!                 ---------------------------------------------------------

IF (LAERNGAT) THEN

  IF (LCHEM_DIA) THEN
     ZTAERO0(KIDIA:KFDIA,1:KLEV,1:NACTAERO) =  ZTAEROK(KIDIA:KFDIA,1:KLEV,1:NACTAERO)
  ENDIF 

  DO JAER=1,NAERO

      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZAER(JL,JK) = PAEROP(JL,JK,JAER)
          ZTAER(JL,JK)= PTENC(JL,JK,KAERO(JAER))
        ENDDO
      ENDDO

    CALL AER_NEGAT &
      & ( YDEAERATM, KIDIA   , KFDIA, KLON , KLEV, &
      &   PTSPHY  , &
      &   ZAER    , ZTAER, PRS1, &
      &   ZAERNEG )  

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTAERO(JL,JK,JAER)=ZTAER(JL,JK)
      ENDDO
    ENDDO
    DO JL=KIDIA,KFDIA
      ZAERNGT(JL,JAER)=ZAERNEG(JL,KLEV+1)
    ENDDO

  ENDDO

! collect neg fix tendencies
IF (LCHEM_DIA) THEN
  CALL CHEM_INEXT( KIDIA , KFDIA  , KLON , KLEV , NACTAERO, NACTAERO ,  & 
   &    ZDP, PTSPHY, ZTAEROK ,ZTAERO0, PEXTRA(:,NCHEM+1:NCHEM+NACTAERO,IEXTR_NG))
ENDIF

  PTENC(KIDIA:KFDIA,1:KLEV,KAERO(1):KAERO(NAERO)) = ZTAERO(KIDIA:KFDIA,1:KLEV,1:NAERO)

ELSE
  ZAERNGT(:,:)=0._JPRB  
ENDIF

! negative fixer for HNO3 and NH3
IF (LAERNITRATE .AND. NCHEM > 0 ) THEN
  ISNX=(/ISHNO3_C,ISNH3_C/)
  ISX=(/IHNO3_C,INH3_C/)
  DO JT = 1,2 
    ISN=ISNX(JT)  
    IS=ISX(JT)
    IF ( YCHEM(IS)%LNEGFIX ) THEN
      ZAER(KIDIA:KFDIA,1:KLEV)=PGFL(KIDIA:KFDIA,1:KLEV,YCHEM(IS)%MP9_PH) 
      ZTAERI(KIDIA:KFDIA,1:KLEV)=PTENC(KIDIA:KFDIA,1:KLEV,ISN)
      CALL CHEM_NEGAT&
  &  ( KIDIA   , KFDIA , KLON , KLEV, PTSPHY, ZAER, ZTAERI, PRS1, ZAERNEG, ZTAER)
      IF (LCHEM_DIA) THEN
        PEXTRA(KIDIA:KFDIA,IS,IEXTR_NG ) =  PEXTRA(KIDIA:KFDIA,IS,IEXTR_NG ) - ZAERNEG(KIDIA:KFDIA,KLEV) * PTSPHY 
      ENDIF
      ! adapted tendency back in tendency array
      PTENC(KIDIA:KFDIA,1:KLEV,ISN) = ZTAER(KIDIA:KFDIA,1:KLEV)
    ENDIF 
  ENDDO  
ENDIF 

!*         5.      VERTICALLY INTEGRATED MASS AND TOTAL OPTICAL DEPTH
!                  -------------------------------------------------- 

INWAVL = 20
ITWAVL( 1)= 9   ! 550 nm
ITWAVL( 2)= 1   ! 340 nm
ITWAVL( 3)= 2   ! 355 nm 
ITWAVL( 4)= 3   ! 380 nm
ITWAVL( 5)= 4   ! 400 nm
ITWAVL( 6)= 5   ! 440 nm
ITWAVL( 7)= 6   ! 469 nm
ITWAVL( 8)= 7   ! 500 nm
ITWAVL( 9)= 8   ! 532 nm 
ITWAVL(10)=10   ! 645 nm
ITWAVL(11)=11   ! 670 nm
ITWAVL(12)=12   ! 800 nm
ITWAVL(13)=13   ! 858 nm                  
ITWAVL(14)=14   ! 865 nm
ITWAVL(15)=15   ! 1020 nm
ITWAVL(16)=16   ! 1064 nm
ITWAVL(17)=17   ! 1240 nm
ITWAVL(18)=18   ! 1640 nm
ITWAVL(19)=19   ! 2130 nm
ITWAVL(20)=20   ! 10 microns

IWHAT=0
IF (LAERCLIMG .OR. LAERCLIMZ) THEN
! BEN
  IWHAT=2
  INWAVL=1
ENDIF

!-- Caution: if prognostic aerosols are on, PAEROP is in kg kg-1 and  ZCAERO is in kg cm-3 
! optical thicknesses can be computed over the 17 following wavelengths:

!-- version INWAVL channels
! (340)355(380)400 440 469 500 532 550 645 670 800 858 865(1020)1064 1240 1640 2130 
!                  ---             ---     ---         --- ----      ---- ---- MODIS
!              ***                             ***                             SEVIRI 
!  ---  ---        xxx     ---             xxx         xxx xxxx                AERONET
!      OOO                     000                               000           EARLINET/CALIPSO

CALL AER_BDGTMSS &
  & ( YDEAERATM,YDMODEL%YRML_PHY_AER,YDCOMPO,YGFL, &
  &   KIDIA, KFDIA , KLON  , KLEV, NACTAERO, IWHAT, INWAVL, ITWAVL, NSTART, NSTEP, &
  &   PRS1 , PAEROP(:,:,1:NACTAERO), PCAERO, ZDZ , PO3P    , PQP  , ZRHCL , ZRHO  , PTP   , PTHP , PAPHIF,&
  &   PODSS , PODDU , PODOM, PODBC  , PODSU, PODNI, PODAM, PODSOA, PODVFA, PODVSU, &
  &   PAEPM1, PAEPM25,PAEPM10,&
  &   ZAERMSS, ZAERMSST, ZAERTAU , ZAERTAUT, ZAERTAUI, ZAERTAUB, ZAERTAUA, ZAERTAUF, PAERAOT, &
  &   ZAEROMGI,ZAERASYI, ZAEREXT , ZAEREXTD, ZAERABST, ZEXTRH, ZABSRH, ZSIGAER, &
  &   ZLISIM, ZLISIA, ZLISIS, ZLISIT  )

IF (LAERLISI) THEN
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
!-- the total extinction coefficient at 355, 532 and 1064 nm is archived through PAERLISI
      PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_EXT)=ZAEREXT(JL,JK,3)
      PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_EXT)=ZAEREXT(JL,JK,9)
      PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_EXT)=ZAEREXT(JL,JK,16)

!-- the extinction coefficients for dust only are archived through PAERLISI, but not currently used
      !PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_EXTD)=ZAEREXTD(JL,JK,3)
      !PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_EXTD)=ZAEREXTD(JL,JK,6)
      !PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_EXTD)=ZAEREXTD(JL,JK,9)

!-- the unattenuated molecular backscatter is archived through PAERLISI
      PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_MOLBSC)=ZLISIM(JL,1,JK)
      PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_MOLBSC)=ZLISIM(JL,2,JK)
      PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_MOLBSC)=ZLISIM(JL,3,JK)

!-- the unattenuated aerosol backscatter is archived through PAERLISI
      PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_AERBSC)=ZLISIA(JL,1,JK)
      PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_AERBSC)=ZLISIA(JL,2,JK)
      PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_AERBSC)=ZLISIA(JL,3,JK)

!-- the backscatter seen from TOA is archived through PAERLISI
      PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_BSTOA)=ZLISIT(JL,1,JK)
      PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_BSTOA)=ZLISIT(JL,2,JK)
      PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_BSTOA)=ZLISIT(JL,3,JK)

!-- the backscatter seen from surface is archived through PAERLISI
      PAERLISI(JL,JK,JPAERLISI_355,JPAERLISI_BSGND)=ZLISIS(JL,1,JK-1)
      PAERLISI(JL,JK,JPAERLISI_532,JPAERLISI_BSGND)=ZLISIS(JL,2,JK-1)
      PAERLISI(JL,JK,JPAERLISI_1064,JPAERLISI_BSGND)=ZLISIS(JL,3,JK-1)

      IF (NAEROUT > 0) THEN
!-- absorption profile is archived through AEROUT
        ZAEROU16(JL,JK)=ZAERABST(JL,JK)
      ENDIF
    ENDDO
  ENDDO
ENDIF


!*         6.      STORE VARIOUS AEROSOL DIAGNOSTICS AND SURFACE FIELDS
!                  ---------------------------------------------------- 

PTAUAER(KIDIA:KFDIA,1:KLEV,1:7)=0._JPRB

INBSS=NTYPAER(1)
INBDU=NTYPAER(1)+NTYPAER(2)
INBOM=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)
INBBC=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)
INBSU=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)
INBFA=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)+NTYPAER(6)
INBVO=NTYPAER(1)+NTYPAER(2)+NTYPAER(3)+NTYPAER(4)+NTYPAER(5)+NTYPAER(6)+NTYPAER(7)

!-- sea salt
IF (NTYPAER(1) /= 0) THEN
  DO IAER=1,INBSS 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,1)=PTAUAER(JL,JK,1)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- dust
IF (NTYPAER(2) /= 0) THEN
  DO IAER=INBSS+1,INBDU 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,2)=PTAUAER(JL,JK,2)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- organic carbon
IF (NTYPAER(3) /= 0) THEN
  DO IAER=INBDU+1,INBOM 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,3)=PTAUAER(JL,JK,3)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- black carbon
IF (NTYPAER(4) /= 0) THEN
  DO IAER=INBOM+1,INBBC 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,4)=PTAUAER(JL,JK,4)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- tropospheric sulfates
IF (NTYPAER(5) /= 0) THEN
  DO IAER=INBBC+1,INBSU 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,5)=PTAUAER(JL,JK,5)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- flying ash 
IF (NTYPAER(6) /= 0) THEN
  DO IAER=INBSU+1,INBFA
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,6)=PTAUAER(JL,JK,6)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF
!-- volcanic sulfates
IF (NTYPAER(7) /= 0) THEN
  DO IAER=INBFA+1,INBVO 
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTAUAER(JL,JK,7)=PTAUAER(JL,JK,7)+ZAERTAU(JL,JK,IAER)
      ENDDO
    ENDDO
  ENDDO
ENDIF

! Initialize arrays for diagnostics
ZAEROU7(:,:) = 0.0_JPRB

!------------------------------------------------------------------------------
!*         6.1     STORE IN PAERO_WVL_DIAG AND PODTOACC
!                  ------------------------------------

DO JWAVL=1,MIN(INWAVL,NAERO_WVL_DIAG)
  DO JL=KIDIA,KFDIA
    IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_AOD) THEN
      PAERO_WVL_DIAG(JL,JWAVL,JPAERO_WVL_AOD) = ZAERTAUI(JL,JWAVL)
    ENDIF
    IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_AODABS) THEN
      PAERO_WVL_DIAG(JL,JWAVL,JPAERO_WVL_AODABS) = ZAERTAUA(JL,JWAVL)
    ENDIF
    IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_AODFM) THEN
      PAERO_WVL_DIAG(JL,JWAVL,JPAERO_WVL_AODFM) = ZAERTAUF(JL,JWAVL)
    ENDIF
    IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_SSA) THEN
      PAERO_WVL_DIAG(JL,JWAVL,JPAERO_WVL_SSA) = ZAEROMGI(JL,JWAVL)
    ENDIF
    IF (NAERO_WVL_DIAG_TYPES >= JPAERO_WVL_ASSIMETRY) THEN
      PAERO_WVL_DIAG(JL,JWAVL,JPAERO_WVL_ASSIMETRY) = ZAERASYI(JL,JWAVL)
    ENDIF
  ENDDO
ENDDO

DO JL=KIDIA,KFDIA
!-- total optical depth, accumulated 
  PODTOACC(JL)=PODTOACC(JL) + ZAERTAUI(JL,1) * PTSPHY
ENDDO

!------------------------------------------------------------------------------
!*         6.2     STORE IN AEROUT-7
!                  -----------------

!-- in another 3D extra field, store various 2D diagnostics (aer_7) (NB: Instantaneous)

IWHERE=0
DO JL=KIDIA,KFDIA
  ZAEROU7(JL,IWHERE+1) = PAERWS(JL)
  ZAEROU7(JL,IWHERE+2) = PAERGUST(JL)
  ZAEROU7(JL,IWHERE+3) = PAERUST(JL)
  ZAEROU7(JL,IWHERE+4) = PALBD(JL,1)
  ZAEROU7(JL,IWHERE+5) = PFRTI(JL,8)
  ZAEROU7(JL,IWHERE+6) = PWS1(JL)
  ZAEROU7(JL,IWHERE+7) = PAERMAP(JL,5)
  ZAEROU7(JL,IWHERE+8) = PAERMAP(JL,2)
  ZAEROU7(JL,IWHERE+9) = PWND(JL)
ENDDO
IWHERE=IWHERE+9

IF (IBINDD == 3) THEN
! levels 10 to 12 of aer_7 are filled with LTS  (NB: Instantaneous)
! levels 13 to 15 of aer_7 are filled with the surface dust flux corresponding to the NDDUST emission type
  DO JMMD=1,IBINDD
    DO JL=KIDIA,KFDIA
      ZAEROU7(JL,IWHERE+JMMD) = PAERLIF(JL,JMMD)
      ZAEROU7(JL,IWHERE+IBINDD+JMMD) = ZAEROU7(JL,IWHERE+IBINDD+JMMD) + PAERFLX(JL,JMMD) * PTSPHY
    ENDDO
  ENDDO
  IWHERE=IWHERE+2*IBINDD
ENDIF

! Start the fields that used to be in AEROUT-9 at a well-defined index beyond
! the old duplicate per-bin AODs, unless either we have an excessive number of
! dust diagnostics, or are pressed for levels
IF (IWHERE < 28 .AND. KLEV >= 60) IWHERE=28

IF (LAERVISI) THEN
!-- VISIBILITY CALCULATIONS
!-- formulation from Hinkley (1976) Vm = (3.91/Sigma) * (0.55/Lambda)^1.3
!   is here used to get visibility at 0.55 um, where Sigma is the total extinction coefficient in km-1
!   0.012 km-1 is the approximate contribution from Rayleigh (could be improved)
!   ZSIGAER is the extinction coefficient in km-1 from the aerosols (computed in aer_bdgtmss.F90)
!   ZCONDEN is the extinction coefficient, sum of the effect of rain and snow 

  ZRANGE = 3.912023_JPRB
  ZRELRA=1000._JPRB
  ZDESIC=1000._JPRB

! values for 0.55 um (in fact 0.6250 - 0.4415 um)
  JSW=10                
  DO JL=KIDIA,KFDIA
    ZDENSVIS=ZRHO(JL,KLEV)
! All ..WP in kg kg-1
    ZQIWP = 0._JPRB
    ZQLWP = 0._JPRB
    IF (PAP(JL,KLEV) > 0.001_JPRB) THEN
      Z1CLD   = 1._JPRB/PAP(JL,KLEV)
      ZQIWP   = MAX(0._JPRB, PIP(JL,KLEV)*Z1CLD)
      ZQLWP   = MAX(0._JPRB, PLP(JL,KLEV)*Z1CLD)
    ENDIF
    ZQRWP   = MAX(0._JPRB, PGFL(JL,KLEV,YR%MP))
    ZQSWP   = MAX(0._JPRB, PGFL(JL,KLEV,YS%MP))

! ice and snow together
    ZSNOICE = ZQIWP+ZQSWP
    ZCFSNIC = RSFUA0(JSW) + RSFUA1(JSW) / ZDESIC
! liquid and rain together
    ZLIQRAI = ZQLWP+ZQRWP
    ZCFLIRA = RSASWA(JSW) + RSASWB(JSW) / ZRELRA
! cloud+precipitation
    ZCONDEN(JL) = (ZSNOICE * ZCFSNIC + ZLIQRAI * ZCFLIRA) * ZDENSVIS * 1.E+06_JPRB

! Molecular density in first layer
    ZNS = RNS*273.15_JPRB/PTP(JL,KLEV)
! Rayleigh scattering cross section (cm2 molec-1)
    ZSIGAIR = (RSIGAIR/ZNS)/ZNS ! not RSIGAIR/(ZNS*ZNS) because intermediate value busts single precision
! Rayleigh scattering coefficient (km-1)
    ZRAYL(JL) = 1.E+05_JPRB * ZSIGAIR * ZNS * PRSF1(JL,KLEV) / 101325._JPRB
!-- to follow the Met Office (Clark et al., 2008, QJ, 134, 1801; Claxton, 2008, QJ, 134, 1527) 
    ZRAYL(JL) = 0.03912023_JPRB

! "total" visibility
    ZVISICL = ZRANGE / (ZRAYL(JL) + PCLAERS(JL) + ZCONDEN(JL))
    ZVISIPR = ZRANGE / (ZRAYL(JL) + ZSIGAER(JL,1) + ZCONDEN(JL))
    ZVISCAE = ZRANGE / (ZRAYL(JL) + PCLAERS(JL))
    ZVISPAE = ZRANGE / (ZRAYL(JL) + ZSIGAER(JL,1))
    ZVISCON = ZRANGE / (ZRAYL(JL) + ZCONDEN(JL))
    ZVISRAY = ZRANGE /  ZRAYL(JL)

    IF (KLEV >= IWHERE+1) ZAEROU7(JL,IWHERE+1)=ZVISICL
    IF (KLEV >= IWHERE+2) ZAEROU7(JL,IWHERE+2)=ZVISIPR
    IF (KLEV >= IWHERE+3) ZAEROU7(JL,IWHERE+3)=ZVISCAE
    IF (KLEV >= IWHERE+4) ZAEROU7(JL,IWHERE+4)=ZVISPAE
    IF (KLEV >= IWHERE+5) ZAEROU7(JL,IWHERE+5)=ZVISCON
    IF (KLEV >= IWHERE+6) ZAEROU7(JL,IWHERE+6)=ZVISRAY
    IF (KLEV >= IWHERE+7) ZAEROU7(JL,IWHERE+7)=PCLAERS(JL)
    IF (KLEV >= IWHERE+8) ZAEROU7(JL,IWHERE+8)=ZSIGAER(JL,1)
    IF (KLEV >= IWHERE+9) ZAEROU7(JL,IWHERE+9)=ZCONDEN(JL)
    PPRAERS(JL)=ZSIGAER(JL,1)
    DO JK=1,5
      IF (KLEV >= IWHERE+JK+9) ZAEROU7(JL,IWHERE+JK+9)=ZRANGE / (ZRAYL(JL) + ZSIGAER(JL,JK) + ZCONDEN(JL))
    ENDDO
  ENDDO
ENDIF
IWHERE=IWHERE+14

DO JL=KIDIA,KFDIA
!-- total optical depth, accumulated 
  PODTOACC(JL)=PODTOACC(JL) + ZAERTAUI(JL,1) * PTSPHY

!-- near surface extinction coefficient at (550,400,670) nm for RH=50%
  IF (KLEV >= IWHERE+ 1) ZAEROU7(JL,IWHERE+ 1)=ZEXTRH(JL,3,1)
  IF (KLEV >= IWHERE+ 2) ZAEROU7(JL,IWHERE+ 2)=ZEXTRH(JL,3,2)
  IF (KLEV >= IWHERE+ 3) ZAEROU7(JL,IWHERE+ 3)=ZEXTRH(JL,3,3)
!-- near surface absorption coefficient at (550,400,670) nm for RH=50%
  IF (KLEV >= IWHERE+ 4) ZAEROU7(JL,IWHERE+ 4)=ZABSRH(JL,3,1)
  IF (KLEV >= IWHERE+ 5) ZAEROU7(JL,IWHERE+ 5)=ZABSRH(JL,3,2)
  IF (KLEV >= IWHERE+ 6) ZAEROU7(JL,IWHERE+ 6)=ZABSRH(JL,3,3)
!-- near surface extinction coefficient at (550,400,670) nm for RH=40%
  IF (KLEV >= IWHERE+ 7) ZAEROU7(JL,IWHERE+ 7)=ZEXTRH(JL,2,1)
  IF (KLEV >= IWHERE+ 8) ZAEROU7(JL,IWHERE+ 8)=ZEXTRH(JL,2,2)
  IF (KLEV >= IWHERE+ 9) ZAEROU7(JL,IWHERE+ 9)=ZEXTRH(JL,2,3)
!-- near surface absorption coefficient at (550,400,670) nm for RH=40%
  IF (KLEV >= IWHERE+10) ZAEROU7(JL,IWHERE+10)=ZABSRH(JL,2,1)
  IF (KLEV >= IWHERE+11) ZAEROU7(JL,IWHERE+11)=ZABSRH(JL,2,2)
  IF (KLEV >= IWHERE+12) ZAEROU7(JL,IWHERE+12)=ZABSRH(JL,2,3)
!-- near surface extinction coefficient at (550,400,670) nm for RH=0%
  IF (KLEV >= IWHERE+13) ZAEROU7(JL,IWHERE+13)=ZEXTRH(JL,1,1)
  IF (KLEV >= IWHERE+14) ZAEROU7(JL,IWHERE+14)=ZEXTRH(JL,1,2)
  IF (KLEV >= IWHERE+15) ZAEROU7(JL,IWHERE+15)=ZEXTRH(JL,1,3)
!-- near surface absorption coefficient at (550,400,670) nm for RH=0%
  IF (KLEV >= IWHERE+16) ZAEROU7(JL,IWHERE+16)=ZABSRH(JL,1,1)
  IF (KLEV >= IWHERE+17) ZAEROU7(JL,IWHERE+17)=ZABSRH(JL,1,2)
  IF (KLEV >= IWHERE+18) ZAEROU7(JL,IWHERE+18)=ZABSRH(JL,1,3)
ENDDO
IWHERE=IWHERE+18

!------------------------------------------------------------------------------
!*         6.6     SET DIAGNOSTICS FIELDS TO VALUES COMPUTED ABOVE
!                  -----------------------------------------------
!-- but only for forecast configuration


IF(NAEROUT > 0) THEN
  PGFL(KIDIA:KFDIA,:,YAEROUT(1)%MP) = ZAEROU7(KIDIA:KFDIA,:)
  IF(LAERLISI .AND. NAEROUT >= 2) THEN
    PGFL(KIDIA:KFDIA,:,YAEROUT(2)%MP) = ZAEROU16(KIDIA:KFDIA,:)
  ENDIF
ENDIF


!------------------------------------------------------------------------------
!*         7.       STORE ALL AEROSOL VERTICALLY INTEGRATED FLUXES
!                   ----------------------------------------------

DO JAER=1,NACTAERO
  DO JL=KIDIA,KFDIA
    PAERODDF(JL,JAER,JPAERODIAG_SRC)=ZAERSRC(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_DDP)=PAERDDP(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_SDM)=PAERSDM(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_WDL)=ZAERSCL(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_WDC)=ZAERSCC(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_NGT)=ZAERNGT(JL,JAER)
    PAERODDF(JL,JAER,JPAERODIAG_OD)=ZAERTAUT(JL,JAER,1)

    ! This is calculated instead in POSTPHY, for consistency
    ! with the GHG and CHEM total column masses.
    ! PAERODDF(JL,JAER,JPAERODIAG_MSS)=ZAERMSST(JL,JAER)
  ENDDO
ENDDO

!------------------------------------------------------------------------------
!*         8.       RADIANCE DIAGNOSTICS WITH 6S SW RT CODE
!                   ---------------------------------------

IF (LAER6SDIA) THEN
!  CALL AER_SIXS
ENDIF

!------------------------------------------------------------------------------
!*         9.       RELEASE LOCAL MEMORY
!                   --------------------
 
DEALLOCATE( ZAERMSS )
DEALLOCATE( ZAERMSST )
DEALLOCATE( ZAERTAU )
DEALLOCATE( ZAERTAUT )
DEALLOCATE( ZAERTAUI ) 
DEALLOCATE( ZAEROMGI )
DEALLOCATE( ZAERASYI )
DEALLOCATE( ZAERTAUB )
DEALLOCATE( ZAERTAUA ) 
DEALLOCATE( ZAERTAUF ) 
DEALLOCATE( ZAERSCC )
DEALLOCATE( ZAERSCL )
DEALLOCATE( ZAERSRC )
DEALLOCATE( ZAERNGT )
DEALLOCATE( ZAEREXT )
DEALLOCATE( ZAEREXTD )
DEALLOCATE( ZAERABST )

IF (LAERLISI .AND. NAEROUT > 0) THEN
  DEALLOCATE( ZAEROU16 )
ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('AER_PHY3',1,ZHOOK_HANDLE)
END SUBROUTINE AER_PHY3

