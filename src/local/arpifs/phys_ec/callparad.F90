#ifdef RS6K        
@PROCESS NOCHECK
#endif
SUBROUTINE CALLPARAD(YDGEOMETRY,YDSURF,YDMODEL,KIDIA,KFDIA,KLON,KSMAX,KLEVS,KTILES,&
 & KSTGLO,&
 & KTDIA,KLEV,KSTART,KIBL,KSTEP,LDRAIN1D,&
 !-----------------------------------------------------------------------
 ! - INPUT (TRAJECTORY) .
 & PT5    , PQ5, PGFL5,&
 & PU5    , PV5    , PVERVEL5,&
 & PRS15  , PRSF15,&
 & PAPRS5 , PAPRSF5, PDELP5,&
 & PAPHI5 , PAPHIF5,&
 & PEMTD5 , PTRSO5 , PMU0,   PMU0M, PGAW,&
 & PSNS5  , PTSA5  , PWL5,   PTL5,&
 & PWSA5  , PTIA5,&
 & PASN5  , PRSN5  , PTSN5,&
 & PHSTD  , PHSDFOR, PGAMMA,&
 & PTHETA , PSIG,&
 & PLSM   , PALBF  ,&
 & PALCOEFF, &
 & PEMISF,&
 & PCVL   , PCVH   , PTVL  , PTVH ,&
 & PLAILC , PLAIHC ,&
 & PSOTY , PCI , PSST,&
 & PGZ0MCL  ,PLZ0HCL,&
 & PSD_VF5, PGZ0M5 , PLZ0H5,&
 & PUCFL5, PVCFL5, PTCFL5, PDCFL5, PQCFL5, PU10N5, PV10N5,&
 ! - UPDATED (TRAJECTORY)
 & PTENT5 , PTENQ5, PTENGFL5,&
 & PTENU5 , PTENV5,&
 & PNTOP  , PNBAS  , PACPR5,&
 & PUSTRTI5, PVSTRTI5, PAHFSTI5,PEVAPTI5 ,PTSKTI5,&
 !====================================================================     
 ! - INPUT .
 & PT     , PQ     , PGFL,&
 & PU     , PV     , PVERVEL,&
 & PL     , PI     , PA     ,&
 & PRS1   , PRSF1,&
 & PAPRS  , PAPRSF , PDELP,&
 & PAPHI  , PAPHIF,&
 & PTSA   , PTL,&
 & PWSA   , PTIA  , PTSN, PII0,&
 ! - UPDATED
 & PTENT  , PTENQ, PTENGFL,&
 & PTENU  , PTENV,&
 & PACPR  , PACCPR,  PACCPR3,&
 & PUSTRTI, PVSTRTI, PAHFSTI,PEVAPTI ,PTSKTI,&
 & PHYS_MWAVE,&
 ! - OUTPUT
 & PDIFTQ , PDIFTS , PSTRTU , PSTRTV,&
 & PGZ0M  , PLZ0H,&
 & PUCFL, PVCFL, PTCFL, PDCFL, PQCFL, PU10N, PV10N,&
 & PTSAE1 , PTLE1, PTIAE1, PTSNE1,&
 & PGPHIST, PTRAJ_PHYS_TLAD,YDACV)

!**** *CALLPARAD * - CALL ECMWF PHYSICS (Adjoint)

!     PURPOSE.
!     --------
!     - CALL THE SUBROUTINES OF THE E.C.M.W.F. PHYSICS PACKAGE.

!**   Interface.
!     ----------
!        *CALL* *CALLPARAD*

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------

! - DIMENSIONS ETC.

! KIDIA   : START OF HORIZONTAL LOOP
! KFDIA   : START OF HORIZONTAL LOOP
! KLON    : HORIZONTAL DIMENSION
! KSMAX   : HORIZONTAL SPECTRAL TRUNCATION
! KLEVS   : NUMBER OF LEVELS IN SOIL
! KTILES  : NUMBER OF vegetation (surface cover) types
! KTDIA   : START OF THE VERTICAL LOOP IN THE PHYSICS
!            (IF SOME LEVELS ARE SKIPPED AT THE TOP OF THE MODEL).
! KLEV    : END OF VERTICAL LOOP AND VERTICAL DIMENSION
! KSTART  : FIRST STEP OF MODEL
! KSTEP   : CURRENT TIME STEP
! KIBL    : INDEX INTO GRID POINT GEOMETRY STORED IN YDGEOMETRY

!-----------------------------------------------------------------------

!  Trajectory   Perturbation  Description
!-----------------------------------------------------------------------

! - 2D (0:KLEV)

! PAPHI5        PAPHI         GEOPOTENTIAL ON HALF-LEVELS
! PAPRS5        PAPRS         PRESSURE ON HALF-LEVELS
! PRS15         PRS1          PROVISIONAL T+DT PRESSURE ON HALF LEVELS
! PEMTD5        ---           DOWNWARD LONGWAVE EMISSIVITY
! PTRSO5        ---           SHORTWAVE TRANSMISSIVITY

! - 2D (1:KLEV)

! PAPHIF5      PAPHIF         GEOPOTENTIAL ON FULL LEVELS
! PAPRSF5      PAPRSF         PRESSURE ON FULL LEVELS
! PDELP5       PDELP          LAYER THICKNESS IN PRESSURE UNITS
! PT5          PT             TEMPERATURE
! PQ5          PQ             SPECIFIC HUMIDITY OF WATER VAPOUR
! PU5          PU             X-COMPONENT OF WIND
! PV5          PV             Y-COMPONENT OF WIND
! PVERVEL5     PVERVEL        VERTICAL VELOCITY
! PRSF15       PRSF1          PROVISIONAL T+DT PRESSURE ON FULL LEVELS
! PGFL5        PGFL           GFL FIELDS

! - 1D (PROGNOSTIC QUANTITIES)

! PSNS5        PSNS           MASS OF SNOW PER UNIT SURFACE
! PTSA5        PTSA           MULTI-LAYER SOIL TEMPERATURE
! PWSA5        PWSA           MULTI-LAYER SOIL WETNESS
! PTIA5        PTIA           MULTI-LAYER ICE TEMPERATURE
! PTSN5        PTSN           SNOW TEMPERATURE
! PWL5         PWL            SKIN RESERVOIR WATER CONTENT
! PTL5         PTL            SKIN (BRIGHTNESS) TEMPERATURE
! PASN5        ---            SNOW ALBEDO
! PRSN5        ---            SNOW DENSITY

! - 0D 

! ----         PII0           SOLAR CONSTANT

!-----------------------------------------------------------------------

! -   UPDATED ARGUMENTS.
!     --------------------

!  Trajectory   Perturbation  Description
!-----------------------------------------------------------------------

! - 2D (1:KLEV)

! PTENT5        PTENT         TENDENCY OF TEMPERATURE
! PTENQ5        PTENQ         TENDENCY OF HUMIDITY
! PTENU5        PTENU         TENDENCY OF U-COMP. OF WIND
! PTENV5        PTENV         TENDENCY OF U-COMP. OF WIND
! PTENGFL5      PTENGFL       TENDENCY OF ALL GFL FIELDS
! PNTOP         ---           CONVECTIVE CLOUD TOP INDEX
!                             (TO BE CONVERTED TO INTEGER)
! PNBAS         ---           CONVECTIVE CLOUD BASE INDEX
!                             (TO BE CONVERTED TO INTEGER)
! PACPR5        PACPR         CONVECTIVE RAINFALL ACCUMULATED BETWEEN TWO
!                             (FULL) RADIATION TIME STEPS
! PUSTRTI5      PUSTRTI       E-W (INSTANTANEOUS) SURFACE STRESS FOR EACH TILE
! PVSTRTI5      PVSTRTI       N-S (INSTANTANEOUS) SURFACE STRESS FOR EACH TILE
! PAHFSTI5      PAHFSTI       (INSTANTANEOUS) SURFACE SENSIBLE HEAT FLUX
!                             FOR EACH TILE
! PEVAPTI5      PEVAPTI       (INSTANTANEOUS) EVAPORATION FOR EACH TILE
! PTSKTI5       PTSKTI        SKIN TEMPERATURE FOR EACH TILE

!-----------------------------------------------------------------------

! -   OUTPUT ARGUMENTS.
!     --------------------

!  Trajectory   Perturbation  Description
!-----------------------------------------------------------------------

! - 2D (0:KLEV) FLUXES

! ---           PDIFTQ        TURBULENT FLUX (INC. Q NEGATIVE) OF SPEC. HUMIDITY
! ---           PDIFTS        TURBULENT FLUX OF ENTHALPY (OR DRY STATIC ENERGY)
! ---           PSTRTU        TURBULENT FLUX OF MOMENTUM "U"
! ---           PSTRTV        TURBULENT FLUX OF MOMENTUM "V"

! - 1D (TENDENCIES FROM SURFACE SCHEME)

! ---           PTLE1         OF SKIN TEMPERATURE
! ---           PTSNE1        OF SNOW TEMPERATURE
! ---           PTSAE1        OF MULTI-LAYER SOIL TEMPERATURE
! ---           PTIAE1        OF MULTI-LAYER SEA ICE TEMPERATURE

! - 1D (DIAGNOSTIC) .

! PGZ0M5        PGZ0M         G * ROUGHNESS LENGTH (CURRENT).
! PLZ0H5        PLZ0H         LOGARITHM OF ROUGHNESS LENGTH FOR HEAT (CURRENT)

!-----------------------------------------------------------------------

!     Externals.  
!     ----------

!     SURFRAD   -
!     SATUR     -
!     CLOUD     -
!     RADHEAT   -
!     VDFMAINS  -
!     GWDRAG    -
!     CUCALLN2  -
!     COND      - 
!     CLOUDST   -
!     CONDAD    - 
!     GWDRAG_WMSS -
!     SURFTSTPS -
!     SURFTSTPSAD -
!     GWDRAG_WMSSAD -
!     CLOUDSTAD -
!     CUCALLN2AD - 
!     GWDRAGAD  -
!     VDFMAINSAD -
!     RADHEATAD -
!     CLOUDAD   -
!     SATURAD   -

!     Method. See documentaion.
!     -------

!     AUTHOR.
!     -------
!      ORIGINAL 93-10-04 M.HAMRUD/P.VITERBO (FROM APLPAR)
!     J.F. MAHFOUF        E.C.M.W.F.          13/12/95

!     MODIFICATIONS.
!     --------------
!     M.Janiskova 06-O999 Adapted for sw radiation
!                 02-T000 Adapted for new lw radiation
!     M.Janiskova T000-01-21 Reading the trajectory for phys.tendecies from file
!                            insted of re-computing them
!        D.Salmond (15-10-01) FULLIMP mods
!              2002-10-31 M. Janiskova (new cloud scheme - cloudst)
!      Modified 2003-12-15 P. Lopez: separate call to simplified convection scheme.
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!     P. Viterbo 2004-05-24 Change surface units
!     M. Ko"hler 2004-12-03 Adoptation to moist advection-diffusion PBL
!        D.Salmond     22-Nov-2005 Mods for coarser/finer physics
!     P. Lopez + P. Bechtold 2006-02-11  Add Tracers
!     JJMorcrette 20060625 MODIS albedo
!     G. Balsamo 20070115 Soil type
!     A. Geer     25-04-2008  Rainy 4D-Var for multiple sensors
!     G. Balsamo  10-10-2008  Lake tile
!     A. Geer     01-10-2008  Rainy 4D-Var: tidying, orography, compatability
!     M. Janiskova 2008-04-10 Frequency of calling LW radiation
!     H. Hersbach   04-Dec-2009 10-m neutral wind
!     M.Janiskova 18-09-2009  Call to non-orographic gwd
!     S. Boussetta/G.Balsamo 05-2009 Added variable LAI
!     M.Janiskova 18-05-2010  Modified use of saved trajectory for LEMWAVE
!                             to avoid re-computation 
!     A. Geer    07-Apr-2010 cloud / precip fraction for all-sky
!      A. Alias    12-2010     Modification of call to RADHEAT
!     L. Magnusson 28-09-2010 For NEMO-LIM
!     A. Geer    21-Sep_2010 All-sky AMSU-A
!     P. Lopez   07-Oct-2010 Added obs operator for ground-based radar composites (GBRAD)
!     J. Hague:  21-Mar-2011 : YGOM Derived type added
!     L. Jones   18-Oct-2011 : Tidied MACC variables in call list
!     A. Geer    11-Jan-2012 Removed all-sky operator (MWAVE) - it's in HOP now.
!     P. Lopez   16-Aug-2012 Added obs operator for rain gauge assimilation (RAINGG).
!     F. Vana    15-Mar-2013 Few changes to comply with the new NL model dataflow.
!     F. Vana    28-Nov-2013 : Redesigned trajectory handling.
!     M.Janiskova 10-Jul-2013 Perturbation of top layer surface fields
!     F. Vana    Oct-2013  Fix for simpl. convection interface
!     T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!     K. Yessad (July 2014): Move some variables.
!     P. Lopez   Sept 2015 : Added lightning parameterization.
!     P. Lopez   30-May-2017 : Moved ground-based radars and rain-gauges from model to obs part.
!     E.Dutra/G.Arduini Jan 2018: Fix to snow var dimensions in call to SURFBC for snowML
!-----------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF
USE PARKIND1           , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK

USE YOMCST             , ONLY : RG, RLVTT, RLSTT, RTT, RCPD, RDAY, YRCST
USE YOETHF             , ONLY : R2ES, R3LES, R3IES, R4LES, R4IES, R5LES, R5IES, R5ALVCP, R5ALSCP,&
 &                              RALVDCP, RALSDCP, RTWAT, RTICE, RTICECU, RTWAT_RTICE_R, RTWAT_RTICECU_R  , YRTHF

USE YOMSPHYHIST        , ONLY : SPHYS_HIST_TYPE
USE YOMRIP0            , ONLY : NINDAT, NSSSSS
USE YOMGBRAD           , ONLY : LEGBRAD_ACTIVE
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TLAD_TYPE
USE YOE_PHYS_MWAVE     , ONLY : N_PHYS_MWAVE
USE TYPE_ACV           , ONLY : ACV_CONTAINER

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON
INTEGER(KIND=JPIM),INTENT(IN)    :: KSMAX
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEVS 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTILES 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTART
INTEGER(KIND=JPIM),INTENT(IN)    :: KIBL
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTEP
LOGICAL           ,INTENT(IN)    :: LDRAIN1D
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL5(KLON,KLEV)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGFL5(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRS15(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRSF15(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS5(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHI5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEMTD5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTRSO5(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0M(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGAW(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSNS5(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSA5(KLON,KLEVS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWL5(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTL5(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PWSA5(KLON,KLEVS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTIA5(KLON,KLEVS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PASN5(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRSN5(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSN5(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHSTD(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHSDFOR(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGAMMA(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTHETA(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSIG(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLSM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALCOEFF(:,:) ! KLON, NALBEDOCOEFFS
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEMISF(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCVH(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTVL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTVH(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLAILC(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLAIHC(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSOTY(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCI(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSST(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGZ0MCL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLZ0HCL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSD_VF5(KLON,YDSURF%YSD_VFD%NDIM5)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0M5(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLZ0H5(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENV5(KLON,KLEV)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENGFL5(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM1)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNTOP(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNBAS(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PACPR5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUSTRTI5(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVSTRTI5(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAHFSTI5(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEVAPTI5(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSKTI5(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PHYS_MWAVE(KLON,KLEV,N_PHYS_MWAVE)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUCFL5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVCFL5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTCFL5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDCFL5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQCFL5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU10N5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV10N5(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PI(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PA(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVERVEL(KLON,KLEV)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRS1(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PRSF1(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPRS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPRSF(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPHI(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSA(KLON,KLEVS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTL(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTIA(KLON,KLEVS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PWSA(KLON,KLEVS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PII0
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENV(KLON,KLEV)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENGFL(KLON,KLEV,YDMODEL%YRML_GCONF%YGFL%NDIM1)
!*GEMSuncomm
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PACPR(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PACCPR(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PACCPR3(KLON,YDMODEL%YRML_GCONF%YRRIP%NSTOP)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUSTRTI(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVSTRTI(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAHFSTI(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PEVAPTI(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSKTI(KLON,KTILES) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTQ(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDIFTS(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRTU(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSTRTV(KLON,0:KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUCFL(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVCFL(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTCFL(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PDCFL(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQCFL(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PU10N(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PV10N(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGZ0M(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLZ0H(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTLE1(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSNE1(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTSAE1(KLON,KLEVS)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTIAE1(KLON,KLEVS)
! Inter timestep memory
TYPE (SPHYS_HIST_TYPE),INTENT(INOUT) :: PGPHIST
! Precomputed trajectory from TL
TYPE (TRAJ_PHYS_TLAD_TYPE), INTENT(IN) :: PTRAJ_PHYS_TLAD
! for parameter optimization.
TYPE(ACV_CONTAINER),INTENT(INOUT),OPTIONAL :: YDACV
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZTPROC5(KLON,KLEV), ZQPROC5(KLON,KLEV)
REAL(KIND=JPRB) :: ZUPROC5(KLON,KLEV), ZVPROC5(KLON,KLEV)
REAL(KIND=JPRB) :: ZTPROC (KLON,KLEV), ZQPROC (KLON,KLEV)
REAL(KIND=JPRB) :: ZUPROC (KLON,KLEV), ZVPROC (KLON,KLEV)

REAL(KIND=JPRB) :: Z_PVDISG5(KLON), Z_PUSTRG5(KLON),Z_PVSTRG5(KLON)

REAL(KIND=JPRB) :: Z_PVDIS (KLON)
REAL(KIND=JPRB) :: Z_PVDISG(KLON), Z_PUSTRG(KLON),Z_PVSTRG(KLON)

REAL(KIND=JPRB) :: Z_PDIFTQ5(KLON,0:KLEV),Z_PDIFTS5(KLON,0:KLEV)

REAL(KIND=JPRB) :: Z_PACPR15(KLON)
REAL(KIND=JPRB) :: Z_PALB5(KLON),Z_PEMIS5(KLON)

REAL(KIND=JPRB) :: Z_PFTLHEV(KLON),Z_PFTLHSB(KLON),&
 & Z_PFWSB(KLON)

REAL(KIND=JPRB) :: Z_PSNSE15(KLON),Z_PWLE15(KLON),Z_PTLE15(KLON)
REAL(KIND=JPRB) :: Z_PTSNE15(KLON),Z_PTIAE15(KLON,KLEVS)
REAL(KIND=JPRB) :: Z_PTSAE15(KLON,KLEVS),Z_PWSAE15(KLON,KLEVS)

REAL(KIND=JPRB) :: Z_PSNSE1(KLON),Z_PWLE1(KLON)
REAL(KIND=JPRB) :: Z_PWSAE1(KLON,KLEVS)

REAL(KIND=JPRB) :: Z_PDIFCQ5(KLON,0:KLEV),&
 & Z_PDIFCS5(KLON,0:KLEV),&
 & Z_PFCQNG5(KLON,0:KLEV),Z_PFPLCL5(KLON,0:KLEV),&
 & Z_PFPLCN5(KLON,0:KLEV),&
 & Z_PFPLSL5(KLON,0:KLEV),Z_PFPLSN5(KLON,0:KLEV),&
 & Z_PFHPCL5(KLON,0:KLEV),&
 & Z_PFHPCN5(KLON,0:KLEV),Z_PFHPSL5(KLON,0:KLEV),&
 & Z_PFHPSN5(KLON,0:KLEV),&
 & Z_PFRSO5 (KLON,0:KLEV),Z_PFRTH5 (KLON,0:KLEV),&
 & Z_PSRSWDCS5(KLON),Z_PSRSWD5(KLON),&
 & Z_PFRSOD5(KLON)       ,Z_PFRTHD5(KLON),&
 & Z_PSTRCU5(KLON,0:KLEV),&
 & Z_PSTRCV5(KLON,0:KLEV),Z_PSTRDU5(KLON,0:KLEV),&
 & Z_PSTRDV5(KLON,0:KLEV),&
 & Z_PSTRTU5(KLON,0:KLEV),Z_PSTRTV5(KLON,0:KLEV)  
REAL(KIND=JPRB) :: Z_PFCCQN5(KLON,0:KLEV), Z_PFCCQL5(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZFLXWGWU5(KLON,0:KLEV), ZFLXWGWV5(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTENU5(KLON,KLEV), ZTENV5(KLON,KLEV), ZTFIELD5(KLON,KLEV)

REAL(KIND=JPRB) :: Z_PDIFCQ(KLON,0:KLEV),&
 & Z_PDIFCS(KLON,0:KLEV),&
 & Z_PFCQNG(KLON,0:KLEV),Z_PFPLCL(KLON,0:KLEV),Z_PFPLCN(KLON,0:KLEV),&
 & Z_PFPLSL(KLON,0:KLEV),Z_PFPLSN(KLON,0:KLEV),Z_PFHPCL(KLON,0:KLEV),&
 & Z_PFHPCN(KLON,0:KLEV),Z_PFHPSL(KLON,0:KLEV),Z_PFHPSN(KLON,0:KLEV),&
 & Z_PFRSO (KLON,0:KLEV),Z_PFRTH (KLON,0:KLEV),Z_PSRSWD(KLON),&
 & Z_PFRSOD(KLON)       ,Z_PFRTHD(KLON),&
 & Z_PSTRCU(KLON,0:KLEV),&
 & Z_PSTRCV(KLON,0:KLEV),Z_PSTRDU(KLON,0:KLEV),Z_PSTRDV(KLON,0:KLEV)  
REAL(KIND=JPRB) :: Z_PFCCQN(KLON,0:KLEV), Z_PFCCQL(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZFLXWGWU(KLON,0:KLEV), ZFLXWGWV(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTENU(KLON,KLEV), ZTENV(KLON,KLEV), ZTFIELD(KLON,KLEV)
REAL(KIND=JPRB) :: ZCAPE(KLON)

REAL(KIND=JPRB) :: Z_PNEB5(KLON,KLEV), Z_PQLI5(KLON,KLEV),&
 & Z_PQSAT5(KLON,KLEV),Z_PQICE5(KLON,KLEV),Z_PTCC5 (KLON)  
REAL(KIND=JPRB) :: Z_PFRSOC5(KLON,0:1),Z_PFRTHC5(KLON,0:1)

REAL(KIND=JPRB) :: Z_PNEB(KLON,KLEV), Z_PQLI(KLON,KLEV),&
 & Z_PQSAT(KLON,KLEV),Z_PQICE(KLON,KLEV),Z_PTCC  (KLON)  
REAL(KIND=JPRB) :: Z_PFRSOC(KLON,0:1),Z_PFRTHC(KLON,0:1)

REAL(KIND=JPRB) :: Z_PEMTD (KLON,0:KLEV),Z_PTRSO (KLON,0:KLEV)

REAL(KIND=JPRB) :: ZDUMMY(KLON,KLEV)

REAL(KIND=JPRB) :: Z_ALBD5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW), Z_ALBP5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: Z_TRSODIR5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW), Z_TRSODIF5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: Z_SFSWDIR5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW), Z_SFSWDIF5 (KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)

REAL(KIND=JPRB) :: ZRII0

INTEGER(KIND=JPIM) :: ITOPC(KLON), IBASC(KLON) 
INTEGER(KIND=JPIM) :: ITOPC1(KLON), IBASC1(KLON)
INTEGER(KIND=JPIM) :: ITYPE(KLON), ICBOT(KLON), ICTOP(KLON), IBOTSC(KLON)
INTEGER(KIND=JPIM) :: ID0, IM0, IY0, IDINCR,ISEC,IDD,IMM,IYY,ILMON(12)

LOGICAL :: LLLAND(KLON),LLSICE(KLON),LLNH(KLON),LLCUM(KLON)
LOGICAL :: LLSC(KLON)
LOGICAL :: LLLAKE(KLON)
LOGICAL :: LLOCN_KPP(KLON)
!--to store variable LAI
REAL(KIND=JPRB) :: ZLAIL(KLON),ZLAIH(KLON)
REAL(KIND=JPRB) :: ZLAILI(KLON),ZLAIHI(KLON)

REAL(KIND=JPRB) :: ZALBD5(KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW), ZALBP5(KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW)
REAL(KIND=JPRB) :: ZAZ0H5(KLON)  , ZAZ0M5 (KLON) , ZHSTD(KLON), ZHSDFOR(KLON)
REAL(KIND=JPRB) :: ZSNM1M5(KLON) , ZRSNM1M5(KLON) 
REAL(KIND=JPRB) :: ZWLM1M5(KLON)
REAL(KIND=JPRB) :: ZWLMX5 (KLON)
REAL(KIND=JPRB) :: ZGEOM15(KLON,KLEV), ZGEOMH5(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZLU5(KLON,KLEV)   , ZLUDE5(KLON,KLEV), ZMFU5(KLON,KLEV)
REAL(KIND=JPRB) :: ZMFD5(KLON,KLEV)  
REAL(KIND=JPRB) :: ZPSTRTU5(KLON,0:KLEV),ZPSTRTV5(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTRSOD5(KLON)
REAL(KIND=JPRB) :: ZCEMTR5(KLON,0:1) , ZCTRSO5(KLON,0:1)
REAL(KIND=JPRB) :: ZEMIR5(KLON),       ZEMIW5(KLON)
REAL(KIND=JPRB) :: ZSPECTRALEMISS5(KLON,2)
REAL(KIND=JPRB) :: ZQRAIN5(KLON,KLEV), ZRAINT5(KLON,KLEV)
REAL(KIND=JPRB) :: ZSUDU5(KLON)
REAL(KIND=JPRB) :: ZDSRP5(KLON), ZISUND5(KLON)
REAL(KIND=JPRB) :: ZPA5(KLON,KLEV), ZPL5(KLON,KLEV), ZPI5(KLON,KLEV)
REAL(KIND=JPRB) :: ZPA55(KLON,KLEV), ZPL55(KLON,KLEV), ZPI55(KLON,KLEV)
REAL(KIND=JPRB) :: ZCOVPTOT5(KLON,KLEV)

! ARDU SNOWML
REAL(KIND=JPRB) :: ZSNM1M5ML(KLON,1) , ZRSNM1M5ML(KLON,1)

!---- added for convenience                    
REAL(KIND=JPRB) :: ZTHT5(KLON,KLEV+1)

REAL(KIND=JPRB) :: ZALBD(KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW), ZALBP(KLON,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW)
REAL(KIND=JPRB) :: ZAZ0H(KLON)  , ZAZ0M (KLON)
REAL(KIND=JPRB) :: ZCTLWP(KLON)
REAL(KIND=JPRB) :: ZSNM1M(KLON)
REAL(KIND=JPRB) :: ZWLM1M(KLON)
REAL(KIND=JPRB) :: ZGEOM1(KLON,KLEV), ZGEOMH(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZLU(KLON,KLEV)   , ZLUDE(KLON,KLEV), ZMFU(KLON,KLEV)
REAL(KIND=JPRB) :: ZMFD(KLON,KLEV)  , ZHRSW(KLON,KLEV), ZHRLW(KLON,KLEV)
REAL(KIND=JPRB) :: ZTRSOD(KLON)
REAL(KIND=JPRB) :: ZT(KLON,KLEV)
REAL(KIND=JPRB) :: ZQRAIN(KLON,KLEV), ZRAINT(KLON,KLEV)
REAL(KIND=JPRB) :: ZCEMTR(KLON,0:1) , ZCTRSO(KLON,0:1)
REAL(KIND=JPRB) :: ZEMIR(KLON),       ZEMIW(KLON)
REAL(KIND=JPRB) :: ZCHAR(KLON)
REAL(KIND=JPRB) :: ZTSKRAD5(KLON)
REAL(KIND=JPRB) :: ZTSKRAD(KLON)
REAL(KIND=JPRB) :: ZPA(KLON,KLEV), ZPL(KLON,KLEV), ZPI(KLON,KLEV)

!---- added for convenience                    
REAL(KIND=JPRB) :: ZTHT(KLON,KLEV+1), ZEMIT(KLON)
REAL(KIND=JPRB) :: ZCCC5(KLON) , ZHCC5(KLON)  , ZLCC5(KLON)  , ZMCC5(KLON)

REAL(KIND=JPRB) :: ZFRTI(KLON,KTILES),ZALBTI5(KLON,KTILES),ZFRSOTI5(KLON,KTILES)
REAL(KIND=JPRB) :: ZALBTI(KLON,KTILES),ZFRSOTI(KLON,KTILES)
INTEGER(KIND=JPIM) :: ITVL(KLON),ITVH(KLON),ISOTY(KLON)
REAL(KIND=JPRB) ::    ZCVL(KLON),ZCVH(KLON)

REAL(KIND=JPRB) ::    ZCCNL(KLON),ZCCNO(KLON),ZEVAPSNW(KLON)
REAL(KIND=JPRB) ::    ZWND5(KLON),ZCCNL5(KLON),ZCCNO5(KLON),ZEVAPSNW5(KLON)

REAL(KIND=JPRB) :: ZLRCH4(KLON,KLEV)

REAL(KIND=JPRB) :: ZCOVPTOT(KLON,KLEV)

!          EXTRA LOCAL STORAGE FOR TRAJECTORY
!          ----- ----- ------- --- ----------

REAL(KIND=JPRB) :: ZTLE55   (KLON) , ZAZ0M55(KLON),&
 & ZAZ0H55  (KLON) , ZLZ0H55(KLON)   
REAL(KIND=JPRB) :: ZTENT5(KLON,KLEV),ZTENQ5(KLON,KLEV)
REAL(KIND=JPRB) :: ZTTENT5(KLON,KLEV),ZTTENQ5(KLON,KLEV),&
 & ZTTENU5(KLON,KLEV),ZTTENV5(KLON,KLEV)
REAL(KIND=JPRB) :: ZTENT55A (KLON,KLEV),ZTENQ55A (KLON,KLEV),&
 & ZTENU55A (KLON,KLEV),ZTENV55A (KLON,KLEV)     
REAL(KIND=JPRB) :: ZTENT55B (KLON,KLEV),ZTENQ55B (KLON,KLEV),&
 & ZTENU55B (KLON,KLEV),ZTENV55B (KLON,KLEV)     
REAL(KIND=JPRB) :: ZTENT55C (KLON,KLEV),ZTENQ55C (KLON,KLEV),&
 & ZTENU55C (KLON,KLEV),ZTENV55C (KLON,KLEV)     
REAL(KIND=JPRB) :: ZTENT55D (KLON,KLEV),ZTENQ55D (KLON,KLEV),&
 & ZTENU55D (KLON,KLEV),ZTENV55D (KLON,KLEV)     
REAL(KIND=JPRB) :: ZTENT55E (KLON,KLEV),ZTENQ55E (KLON,KLEV),&
 & ZTENU55E (KLON,KLEV),ZTENV55E (KLON,KLEV)     
REAL(KIND=JPRB) :: ZTENT55F (KLON,KLEV),ZTENQ55F (KLON,KLEV),&
 & ZTENU55F (KLON,KLEV),ZTENV55F (KLON,KLEV)  
REAL(KIND=JPRB) :: ZTENT55G (KLON,KLEV),&
 & ZTENU55G (KLON,KLEV),ZTENV55G (KLON,KLEV)
REAL(KIND=JPRB) :: ZUSTRTI55(KLON,KTILES),ZVSTRTI55(KLON,KTILES)
REAL(KIND=JPRB) :: ZAHFSTI55(KLON,KTILES),ZEVAPTI55(KLON,KTILES),&
 & ZTSKTI55(KLON,KTILES)  
REAL(KIND=JPRB) :: ZAHFSTI5(KLON,KTILES),ZEVAPTI5(KLON,KTILES)
REAL(KIND=JPRB) :: ZPEMTD5(KLON,0:KLEV)

 REAL(KIND=JPRB) :: ZTENTX5 (KLON,KLEV),ZTENQX5 (KLON,KLEV),&
 & ZTENUX5 (KLON,KLEV),ZTENVX5 (KLON,KLEV)  
REAL(KIND=JPRB) :: ZTENTVD5 (KLON,KLEV),ZTENQVD5 (KLON,KLEV),&
 & ZTENUVD5 (KLON,KLEV),ZTENVVD5 (KLON,KLEV) 
REAL(KIND=JPRB) :: ZTENTGW5 (KLON,KLEV),&
 & ZTENUGW5 (KLON,KLEV),ZTENVGW5 (KLON,KLEV)  
REAL(KIND=JPRB) :: ZTENTCO5 (KLON,KLEV),ZTENQCO5 (KLON,KLEV),&
 & ZTENUCO5 (KLON,KLEV),ZTENVCO5 (KLON,KLEV) 
REAL(KIND=JPRB) :: ZTENTCL5 (KLON,KLEV)
REAL(KIND=JPRB) :: ZTENUWM5 (KLON,KLEV),ZTENVWM5 (KLON,KLEV)

INTEGER(KIND=JPIM) :: IFLAG, JK, JL, JSW, JTILE, ITRC
INTEGER(KIND=JPIM) :: ISTEP, IFREQWMS, ISTEPWMS

LOGICAL :: LLSLPHY

REAL(KIND=JPRB) :: ZCARDI,ZRCHAR,ZRCPD,ZTSGWD

CHARACTER (LEN = 1) ::  CL_CDCONF

!Dummy arrays for convection
REAL(KIND=JPRB) :: ZMFUDE_RATE5(KLON,KLEV)     ! UD detrainmnet rate
REAL(KIND=JPRB) :: ZMFDDE_RATE5(KLON,KLEV)     ! DD detrainmnet rate
REAL(KIND=JPRB) :: ZCAPE5(KLON)                ! CAPE (J/kg)

! FLAKE: Lake model (Local for PASSIVE introduction)
REAL(KIND=JPRB) :: ZCLAKE5(KLON)
REAL(KIND=JPRB) :: ZHLICE5(KLON) 
REAL(KIND=JPRB) :: ZTLICE5 (KLON)
REAL(KIND=JPRB) :: ZTLWML5 (KLON)

! Dummies for radiation output
REAL(KIND=JPRB) :: ZSWTOA(KLON)
REAL(KIND=JPRB) :: ZFRSOPS(KLON)
REAL(KIND=JPRB) :: ZFRSOFS(KLON)
!----------------------------------------------------------------------------
! For convective/diffusive tracer Transport:

INTEGER(KIND=JPIM)           :: ITRAC
INTEGER(KIND=JPIM)           :: IAERO(YDMODEL%YRML_GCONF%YGFL%NAERO) ! Index array for aerosol tracers
INTEGER(KIND=JPIM)           :: ICHEM(YDMODEL%YRML_GCONF%YGFL%NCHEM) ! Index array for chemistry
REAL(KIND=JPRB), POINTER :: ZCEN5(:,:,:),ZTENC5(:,:,:),ZCEN(:,:,:) ,ZTENC (:,:,:),&
                          & ZTENC5_SKF(:,:,:), ZSCAV5(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZTENC55D(:,:,:),ZTENC55B(:,:,:)
REAL(KIND=JPRB), POINTER :: ZCFLX5(:,:), ZCFLX(:,:)
!!REAL(KIND=JPRB), ALLOCATABLE :: ZTENCX5(:,:,:)
!!REAL(KIND=JPRB), ALLOCATABLE :: ZTENCVD5(:,:,:)
INTEGER(KIND=JPIM)           :: IMNTH

! NEMO/LIM2
REAL(KIND=JPRB) :: ZALBICE(KLON)
REAL(KIND=JPRB) :: ZTHKICE5(KLON)
REAL(KIND=JPRB) :: ZSNTICE5(KLON)

! Local arrays for lightning parameterization
REAL(KIND=JPRB) :: ZLIGH_TOT5(KLON), ZLIGH_CTG5(KLON)
REAL(KIND=JPRB) :: ZWMFU5(KLON), ZCTOPH5(KLON), ZPRECMX5(KLON)
REAL(KIND=JPRB) :: ZCDEPTH5(KLON), ZICE5(KLON)

REAL(KIND=JPRB) :: ZLIGH_TOT (KLON), ZLIGH_CTG (KLON)
REAL(KIND=JPRB) :: ZWMFU(KLON), ZCTOPH(KLON), ZPRECMX(KLON)
REAL(KIND=JPRB) :: ZCDEPTH(KLON), ZICE(KLON)

!Dummy arrays for vdfmain
LOGICAL :: LLSHCV(KLON)

!Dummy array for culight
LOGICAL :: LLLINOX(KLON)

LOGICAL :: LLCLDCOVER

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!----------------------------------------------------------------------------

#include "surfrad.h"
#include "surfbc.h"
#include "surf_inq.h"
#include "surftstps.h"
#include "surftstpsad.h"

#include "abor1.intfb.h"
#include "cldpp.intfb.h"
#include "cldppad.intfb.h"
#include "ccloud.intfb.h"
#include "cloudad.intfb.h"
#include "cloudst.intfb.h"
#include "cloudstad.intfb.h"
#include "cond.intfb.h"
#include "condad.intfb.h"
#include "cucalln2.intfb.h"
#include "cucalln2ad.intfb.h"
#include "culight.intfb.h"
#include "culightad.intfb.h"
#include "gems_init.intfb.h"
#include "gems_init_ad.intfb.h"
#include "gems_tend.intfb.h"
#include "gems_tend_ad.intfb.h"
#include "gwdrags.intfb.h"
#include "gwdragad.intfb.h"
!#include "gwdrag_wmss.intfb.h"
#include "gwdrag_wmssad.intfb.h"
#include "radheat.intfb.h"
#include "radheatad.intfb.h"
#include "radinaad.intfb.h"
#include "rdphtrajt.intfb.h"
#include "satur.intfb.h"
#include "saturad.intfb.h"
#include "updcal.intfb.h"
#include "vdfmains.intfb.h"
#include "vdfmainsad.intfb.h"
#include "sualb2si.intfb.h"
#include "icestatenemo.intfb.h"

!     ------------------------------------------------------------------
#include "fcttre.func.h"
#include "fcttim.func.h"

!-----------------------------------------------------------------------

!*                  ----  TRAJECTORY COMPUTATIONS  ----

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CALLPARAD',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, &
 &  YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), YDEGWWMS=>YDMODEL%YRML_PHY_EC%YREGWWMS,YDPHNC=>YDMODEL%YRML_PHY_SLIN%YRPHNC, &
 & YDSRFTLAD=>YDMODEL%YRML_PHY_SLIN%YRSRFTLAD,YDMCC=>YDMODEL%YRML_AOC%YRMCC,YDRIP=>YDMODEL%YRML_GCONF%YRRIP, &
 & YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD,YDEGWDWMS=>YDMODEL%YRML_PHY_SLIN%YREGWDWMS,YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2, &
 & YDELWRAD=>YDMODEL%YRML_PHY_RAD%YRELWRAD,YDRADIATION=>YDMODEL%YRML_PHY_RAD%YRADIATION, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY,YDERDI=>YDMODEL%YRML_PHY_RAD%YRERDI, &
 & YDNCL=>YDMODEL%YRML_PHY_SLIN%YRNCL,YDVDF=>YDMODEL%YRML_PHY_G%YRVDF, &
 & YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY)

ASSOCIATE(NAERO=>YGFL%NAERO, NCHEM=>YGFL%NCHEM, NCHEM_DV=>YGFL%NCHEM_DV, &
 & NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NGEMS=>YGFL%NGEMS, &
 & NGHG=>YGFL%NGHG, YGHG=>YGFL%YGHG, YLRCH4=>YGFL%YLRCH4, &
 & LREGWWMS=>YDEGWDWMS%LREGWWMS, &
 & GTPHYGWWMS=>YDEGWWMS%GTPHYGWWMS, &
 & NLWFR=>YDELWRAD%NLWFR, &
 & LEGBRAD=>YDEPHY%LEGBRAD, LERAINGG=>YDEPHY%LERAINGG, &
 & LEMWAVE=>YDEPHY%LEMWAVE, LESICE=>YDEPHY%LESICE, NPRACCL=>YDEPHY%NPRACCL, &
 & YSURF=>YDEPHY%YSURF, &
 & NMODE=>YDERAD%NMODE, NSW=>YDERAD%NSW, NTSW=>YDERAD%NTSW, &
 & RCCNLND=>YDERAD%RCCNLND, RCCNSEA=>YDERAD%RCCNSEA, &
 & RCARDI=>YDERDI%RCARDI, &
 & LNEMOLIMALB=>YDMCC%LNEMOLIMALB, LNEMOLIMTHK=>YDMCC%LNEMOLIMTHK, &
 & LNCLIN=>YDNCL%LNCLIN, &
 & LECOND2=>YDPHNC%LECOND2, LECUMF2=>YDPHNC%LECUMF2, LEDCLD2=>YDPHNC%LEDCLD2, &
 & LEGWDG2=>YDPHNC%LEGWDG2, LEGWWMS2=>YDPHNC%LEGWWMS2, LENCLD2=>YDPHNC%LENCLD2, &
 & LEQNGT2=>YDPHNC%LEQNGT2, LERADFL2=>YDPHNC%LERADFL2, LERADI2=>YDPHNC%LERADI2, &
 & LERADLW2=>YDPHNC%LERADLW2, LERADN2=>YDPHNC%LERADN2, LERADS2=>YDPHNC%LERADS2, &
 & LERADSW2=>YDPHNC%LERADSW2, LESURF2=>YDPHNC%LESURF2, &
 & LETRAJPT=>YDPHNC%LETRAJPT, LEVDIF2=>YDPHNC%LEVDIF2, &
 & LTRACLNPH=>YDPHNC%LTRACLNPH, &
 & NSTOP=>YDRIP%NSTOP, &
 & RSTATI=>YDRIP%RSTATI, &
 & LREGSF=>YDSRFTLAD%LREGSF, &
 & YSD_VF=>YDSURF%YSD_VF, YSD_VFD=>YDSURF%YSD_VFD, &
 & TSPHY=>YDPHY2%TSPHY, LELIGHT=>YDEPHY%LELIGHT,RVDIFTS=>YDVDF%RVDIFTS, &
 & NCSNEC=>YDDPHY%NCSNEC)
!-----------------------------------------------------------------------

!*         1.     INITIAL COMPUTATIONS.
!                 ---------------------

! Set up constants required
ZRCPD=1.0_JPRB/RCPD

! Initialize Ocean and Lake variable for passive introduction  
LLOCN_KPP(:)=.FALSE.
LLLAKE(:)=.FALSE.
ZCLAKE5(:)=0.0_JPRB
ZHLICE5(:)=0.0_JPRB
ZTLICE5(:)=0.0_JPRB
ZTLWML5(:)=0.0_JPRB

ITRAC=0
IF (NGEMS > 0 .AND. LTRACLNPH) THEN
  JL=0
  JK=0
  CALL GEMS_INIT(YDSURF, YDMODEL%YRML_CHEM,YDEPHY,YGFL,YDPHY2, YDRIP, KIDIA, KFDIA, KLEV, KLON, ITRAC, YDGSGEOM%GELAM, YDGSGEOM%GELAT, & 
      & PSD_VF5, IAERO, ICHEM, ZLRCH4, PGFL5,&
      & PTENGFL5, ZCEN5, ZTENC5, ZTENC5_SKF, ZCFLX5, ZSCAV5) 

ELSE
! ALLOCATE arrays so they can be passed to VDFOUTER etc. in the non-GEMS case
   ALLOCATE(ZCEN5(1,1,1))
   ALLOCATE(ZTENC5(1,1,1))
   ALLOCATE(ZCFLX5(1,1))
   ALLOCATE(ZSCAV5(1))
   ALLOCATE(ZTENC5_SKF(1,1,1))
ENDIF

IF (LTRACLNPH .AND. NGEMS > 0) THEN
  ALLOCATE( ZTENC55B(KLON,KLEV,ITRAC) )
  ALLOCATE( ZTENC55D(KLON,KLEV,ITRAC) )  
  ZTENC55B(:,:,:)  =0.0_JPRB
  ZTENC55D(:,:,:) =0.0_JPRB
ELSE
  ALLOCATE( ZTENC55B(1,1,1) )
  ALLOCATE( ZTENC55D(1,1,1) )
ENDIF

CL_CDCONF='A'

CALL SURF_INQ(YSURF,PRCHAR=ZRCHAR)

ZCARDI = 0.0_JPRB
ZCHAR(:)=ZRCHAR
ZQRAIN5 (:,:) = 0.0_JPRB
ZRAINT5 (:,:) = 0.0_JPRB
LLSLPHY=.FALSE.

ZPA55(:,:) = 0.0_JPRB
ZPL55(:,:) = 0.0_JPRB
ZPI55(:,:) = 0.0_JPRB
ZDUMMY(:,:)= 0.0_JPRB

!*       1.1   FIRST TIME-STEP
IF (KSTEP == KSTART) THEN
  DO JL=KIDIA,KFDIA
    PNTOP(JL)=1.0_JPRB
    PNBAS(JL)=1.0_JPRB
    PACPR5(JL)=0.0_JPRB
    PGZ0M5(JL)=PGZ0MCL(JL)
    PLZ0H5(JL)=PLZ0HCL(JL)
  ENDDO
ENDIF

!*       1.2   CHANGE UNITS/SCALINGS

DO JL=KIDIA,KFDIA
  ZAZ0M5(JL)  = PGZ0M5(JL) /RG
  ZAZ0H5(JL)  = EXP(PLZ0H5(JL))
  ZLZ0H55(JL) = PLZ0H5(JL)
  ZSNM1M5(JL) = PSNS5(JL)
  ZRSNM1M5(JL)= PRSN5(JL)
  ! ARDU REQUIRED FOR SURFBC WHERE SNOW IS 2D
  ZSNM1M5ML(JL,1) = PSNS5(JL)
  ZRSNM1M5ML(JL,1)= PRSN5(JL)
  ZWLM1M5(JL) = PWL5 (JL)
  IF (PLSM(JL) > 0.5_JPRB) THEN
    ZHSTD (JL) = PHSTD(JL) /RG
    ZHSDFOR(JL) = PHSDFOR(JL)  
  ELSE
    ZHSTD (JL) = 0._JPRB
    PSIG  (JL) = 0._JPRB
    ZHSDFOR(JL) = 0._JPRB 
  ENDIF
ENDDO

!*      1.3   GLOBAL AUXILLIARY VARIABLES

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZGEOM15(JL,JK)  = PAPHIF5(JL,JK)-PAPHI5(JL,KLEV)
  ENDDO
ENDDO
DO JK=0,KLEV
  DO JL=KIDIA,KFDIA
    ZGEOMH5(JL,JK)=PAPHI5(JL,JK)-PAPHI5(JL,KLEV)
  ENDDO
ENDDO

!*      1.3a   AUXILLIARY VARIABLES FOR VDF, SRF AND SURFRAD

CALL SURFBC (YDSURF=YSURF,KIDIA=KIDIA,KFDIA=KFDIA,KLON=KLON,KTILES=KTILES,KLEVSN=1,&
 & PTVL=PTVL,PTVH=PTVH,PSOTY=PSOTY,PSDOR=ZHSTD,PCVLC=PCVL,PCVHC=PCVH,&
 & PLAILC=PLAILC,PLAIHC=PLAIHC,PLAILI=ZLAILI,PLAIHI=ZLAIHI,&
 & PLSM=PLSM,PCI=PCI,PCLAKE=ZCLAKE5,PHLICE=ZHLICE5,&
 & PGEMU=YDGSGEOM%GEMU,PSNM1M=ZSNM1M5ML,PWLM1M=ZWLM1M5,PRSNM1M=ZRSNM1M5ML,&
 & LDLAND=LLLAND,LDSICE=LLSICE,LDLAKE=LLLAKE,LDNH=LLNH,LDOCN_KPP=LLOCN_KPP,&
 & KTVL=ITVL,KTVH=ITVH,KSOTY=ISOTY,&
 & PCVL=ZCVL,PCVH=ZCVH,PLAIL=ZLAIL,PLAIH=ZLAIH,&
 & PWLMX=ZWLMX5,PFRTI=ZFRTI)

! ARDU: ASSIGN TO THE 1D-FIELDS USED AFTERWARDS
  DO JL=KIDIA,KFDIA
    ZSNM1M5(JL) = ZSNM1M5ML(JL,1)
    ZRSNM1M5(JL)= ZRSNM1M5ML(JL,1)
  ENDDO
  
DO JL=KIDIA,KFDIA
  ITOPC(JL)=INT(PNTOP(JL)+0.01_JPRB)
  IBASC(JL)=INT(PNBAS(JL)+0.01_JPRB)
ENDDO

!*       1.4  SET SURFACE TENDENCIES TO ZERO
DO JK=1,KLEVS
  DO JL=KIDIA,KFDIA
    Z_PTSAE15(JL,JK)=0.0_JPRB
    Z_PWSAE15(JL,JK)=0.0_JPRB
    Z_PTIAE15(JL,JK)=0.0_JPRB
  ENDDO
ENDDO
DO JL=KIDIA,KFDIA
  Z_PTLE15(JL)=0.0_JPRB
  Z_PWLE15(JL)=0.0_JPRB
  Z_PSNSE15(JL)=0.0_JPRB
  Z_PTSNE15(JL)=0.0_JPRB
ENDDO

!*       1.5  SET FLUXES TO ZERO

! radiation

IF (.NOT. LERADI2) THEN
  Z_PFRSOC5(:,:) = 0.0_JPRB
  Z_PFRTHC5(:,:) = 0.0_JPRB
  Z_PFRSOD5(:)   = 0.0_JPRB
  Z_PFRTHD5(:)   = 0.0_JPRB
  Z_PFRSO5 (:,:) = 0.0_JPRB
! Set surface solar flux to a minimum value for stability in vertical diffusion
  Z_PFRSO5 (:,KLEV) = 1.0E-15_JPRB
  Z_PFRTH5 (:,:) = 0.0_JPRB
ENDIF

! vertical diffusion

IF (.NOT.LEVDIF2) THEN
  Z_PDIFTS5(:,:) = 0.0_JPRB
  Z_PDIFTQ5(:,:) = 0.0_JPRB
  Z_PSTRTU5(:,:) = 0.0_JPRB
  Z_PSTRTV5(:,:) = 0.0_JPRB
  Z_PDIFTS5(:,:) = 0.0_JPRB
  Z_PDIFTQ5(:,:) = 0.0_JPRB
  ZEVAPSNW5(:) = 0.0_JPRB
ENDIF

!gravity wave drag

IF (.NOT. LEGWDG2) THEN
  Z_PUSTRG5(:)   = 0.0_JPRB
  Z_PVSTRG5(:)   = 0.0_JPRB
  Z_PVDISG5(:)   = 0.0_JPRB
  Z_PSTRDU5(:,:) = 0.0_JPRB
  Z_PSTRDV5(:,:) = 0.0_JPRB
ENDIF

!convection

IF (LECUMF2) THEN
  ZPSTRTU5(:,:) = 0.0_JPRB
  ZPSTRTV5(:,:) = 0.0_JPRB
  ZMFUDE_RATE5(:,:) = 0.0_JPRB
  ZMFDDE_RATE5(:,:) = 0.0_JPRB
ENDIF
IF (.NOT.LECUMF2) THEN
  Z_PDIFCS5(:,:) = 0.0_JPRB
  Z_PDIFCQ5(:,:) = 0.0_JPRB
  Z_PFHPCL5(:,:) = 0.0_JPRB
  Z_PFHPCN5(:,:) = 0.0_JPRB
  Z_PFPLCL5(:,:) = 0.0_JPRB
  Z_PFPLCN5(:,:) = 0.0_JPRB
  Z_PSTRCU5(:,:) = 0.0_JPRB
  Z_PSTRCV5(:,:) = 0.0_JPRB
  Z_PFCCQL5(:,:) = 0.0_JPRB
  Z_PFCCQN5(:,:) = 0.0_JPRB
  ZLUDE5 (:,:) = 0.0_JPRB
  ZLU5   (:,:) = 0.0_JPRB
ENDIF

!large scale precipitation

IF (.NOT.LECOND2 .AND. .NOT.LENCLD2) THEN
  Z_PFHPSL5(:,:) = 0.0_JPRB
  Z_PFHPSN5(:,:) = 0.0_JPRB
  Z_PFPLSL5(:,:) = 0.0_JPRB
  Z_PFPLSN5(:,:) = 0.0_JPRB
  ZCOVPTOT5(:,:) = 0.0_JPRB
ENDIF

!cloudiness

IF (.NOT.LEDCLD2 .AND. .NOT.LENCLD2) THEN
  Z_PNEB5(:,:)  = 0.0_JPRB
  Z_PQLI5(:,:)  = 0.0_JPRB
  Z_PQICE5(:,:) = 0.0_JPRB
  Z_PTCC5(:)    = 0.0_JPRB
ENDIF

IF (.NOT.LEGWWMS2) THEN
  ZFLXWGWU5(:,:) = 0.0_JPRB
  ZFLXWGWV5(:,:) = 0.0_JPRB
ENDIF

!*       1.6  SET SURFACE SOLAR TRANSMISSIVITY

Z_PSRSWDCS5(:) = 0.0_JPRB
Z_PSRSWD5(:) = 0.0_JPRB
DO JL=KIDIA,KFDIA
  ZSUDU5 (JL) = Z_PSRSWDCS5(JL)
  ZTRSOD5(JL) = Z_PSRSWD5(JL)
ENDDO

!*       1.7 SET PHYSICAL TENDENCIES

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZTENTX5(JL,JK) = PTENT5(JL,JK)
    ZTENQX5(JL,JK) = PTENQ5(JL,JK)
    ZTENUX5(JL,JK) = PTENU5(JL,JK)
    ZTENVX5(JL,JK) = PTENV5(JL,JK)
  ENDDO
ENDDO

!*       1.8 READ TRAJECTORY FOR PHYS.TENDENCIES, RADIATION,
!            NEW CLOUD SCHEME AND NON-OROGRAPHIC GRAVITY WAVE

IF ((LERADN2.OR.LERADSW2) .OR. LENCLD2 .OR. LEGWWMS2 .OR. LESURF2&
 & .OR. LETRAJPT) THEN
  CALL RDPHTRAJT(YDGEOMETRY,YDPHNC,YDEPHY,YDMODEL%YRML_PHY_G%YRDPHY,KIDIA,KFDIA, PTRAJ_PHYS_TLAD, &
   & ZTENTVD5,ZTENQVD5,ZTENUVD5,ZTENVVD5,&
   & ZTENTGW5,ZTENUGW5,ZTENVGW5,&
   & ZTENTCO5,ZTENQCO5,ZTENUCO5,ZTENVCO5,&
   & ZTENTCL5,ZTENUWM5,ZTENVWM5,&
   & Z_PDIFTQ5 ,Z_PDIFTS5 ,ZPEMTD5 ,PTRSO5,&
   & ZPA55, ZPL55, ZPI55,&
   & ZLUDE5, ZLU5,&
   & Z_PFPLCL5, Z_PFPLCN5,PUCFL5,PVCFL5,PTCFL5,PQCFL5,&
   & ZFRSOTI5 , ZAHFSTI5 , ZEVAPTI5,&
   & ZEVAPSNW5,Z_PFPLSL5(:,KLEV),Z_PFPLSN5(:,KLEV),&
   & Z_PFRTH5(:,KLEV) )  
ENDIF

!     ------------------------------------------------------------------

!*         2.     RADIATION TRANSFER
!                 ------------------

IF ( LERADI2 ) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55A(JL,JK) = ZTENTX5(JL,JK)
      ZTENQ55A(JL,JK) = ZTENQX5(JL,JK)
      ZTENU55A(JL,JK) = ZTENUX5(JL,JK)
      ZTENV55A(JL,JK) = ZTENVX5(JL,JK)
!     IF(LETRAJPT)ZTENTX5(JL,JK) = ZTENTRA5(JL,JK)
    ENDDO
  ENDDO

!*         2.1  FULL RADIATION COMPUTATIONS

!*         2.2 SURFACE BOUNDARY CONDITIONS
  IF (LERADS2) THEN

    DO JL=KIDIA,KFDIA
      ZWND5(JL)=SQRT(PU5(JL,KLEV)*PU5(JL,KLEV)+PV5(JL,KLEV)*PV5(JL,KLEV))
    ENDDO  

    IY0=NCCAA(NINDAT)
    IM0=NMM(NINDAT)
    ID0=NDD(NINDAT)
    IDINCR=(NSSSSS+NINT(RSTATI))/NINT(RDAY)
    ISEC=MOD(NSSSSS+NINT(RSTATI),NINT(RDAY))
    CALL UPDCAL(ID0,IM0,IY0,IDINCR,IDD,IMM,IYY,ILMON,-1)
    ZALBICE(KIDIA:KFDIA) = 0.0_JPRB
    IF (LNEMOLIMALB) THEN 
       CALL ICESTATENEMO(YDMCC,KSTGLO,KIDIA,KFDIA,PALBICE=ZALBICE(KIDIA:KFDIA))
    ENDIF

    ! This is unsatisfactory: SURFRAD is called requesting the albedo
    ! in two spectral intervals, yet it has been configured for six.
    ! This means that the near-IR albedos ZALBD5(:,2) and ZALBP5(:,2)
    ! are incorrect, although they are then overwritten by correct
    ! values through the call to SUALB2SI.  The broadband tile albedos
    ! ZALBTI5 are also incorrect, but unfortunately these are not
    ! corrected. The emissivities from SURFRAD should be fine.  This
    ! should be fixed, ideally using a single routine to compute
    ! everything.  Robin Hogan, 8 May 2019.
    CALL SURFRAD(YDSURF=YSURF,KDD=IDD,KMM=IMM,KMON=ILMON,KSECO=ISEC,&
     & KIDIA=KIDIA,KFDIA=KFDIA,KLON=KLON,KTILES=KTILES,KSW=NSW,KLW=2,&
     & LDNH=LLNH,&
     & PALBF=PALBF,PALBICEF=ZALBICE,PTVH=PTVH,&
     & PALCOEFF=PALCOEFF, &
     & PASN=PASN5,PMU0=PMU0M,PTS=PTL5,PWND=ZWND5,&
     & PWS1=PWSA5(:,1),KSOTY=ISOTY,PFRTI=ZFRTI,PHLICE=ZHLICE5,PTLICE=ZTLICE5,&
     & PALBD=ZALBD5(:,1:NSW),PALBP=ZALBP5(:,1:NSW),PALB=Z_PALB5,&
     & PSPECTRALEMISS=ZSPECTRALEMISS5,PEMIT=Z_PEMIS5,&
     & PALBTI=ZALBTI5,PCCNL=ZCCNL5,PCCNO=ZCCNO5,LNEMOLIMALB=LNEMOLIMALB )
    ZEMIR5(KIDIA:KFDIA) = ZSPECTRALEMISS5(KIDIA:KFDIA,1)
    ZEMIW5(KIDIA:KFDIA) = ZSPECTRALEMISS5(KIDIA:KFDIA,2)
    IMNTH=12
    IF (LERADSW2) THEN
      CALL SUALB2SI (YDEPHY, KIDIA, KFDIA, KLON, IMNTH, KTILES,&
       & IDD, IMM, ILMON, ISEC, NSW,&
       & LLNH,&
       & PALBF , PALCOEFF, &
       & PASN5,&
       & ZFRTI , PMU0M  ,&
       & ZALBD5, ZALBP5&
       & )
    ENDIF
  ELSE
    DO JSW=1,NTSW
      ZALBD5(KIDIA:KFDIA,JSW)=PALBF(KIDIA:KFDIA)
      ZALBP5(KIDIA:KFDIA,JSW)=PALBF(KIDIA:KFDIA)
    ENDDO
    DO JL=KIDIA,KFDIA
      Z_PALB5(JL)=PALBF(JL)
      Z_PEMIS5(JL)=PEMISF(JL)
      ZEMIR5(JL)=PEMISF(JL)
      ZEMIW5(JL)=PEMISF(JL)
      ZCCNL5(JL)=RCCNLND
      ZCCNO5(JL)=RCCNSEA
    ENDDO
    DO JK=1,KTILES
      DO JL=KIDIA,KFDIA
        ZALBTI5(JL,JK)=PALBF(JL)
      ENDDO
    ENDDO
  ENDIF

  IFLAG=2
  CALL SATUR  ( YRTHF, YRCST, KIDIA  , KFDIA  , KLON  , KTDIA , KLEV, YDMODEL%YRML_PHY_SLIN%YREPHLI%LPHYLIN, &
   & PAPRSF5, PT5    , Z_PQSAT5, IFLAG)   

!*         2.3 CLOUD
  IF (LEDCLD2 .AND. .NOT. LENCLD2) THEN

    ITOPC1(:) = ITOPC(:)
    IBASC1(:) = IBASC(:)
    Z_PACPR15(:) = PACPR5(:)

    CALL  CCLOUD (YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YRECLD, KIDIA  , KFDIA  , KLON  , KLEV,&
     & IBASC  , ITOPC,&
     & PAPRSF5, PACPR5,&
     & PQ5    , Z_PQSAT5 , PT5    , PVERVEL5,&
     & ZCCC5  , ZHCC5  , ZLCC5  , ZMCC5,&
     & Z_PNEB5  , Z_PTCC5  , Z_PQICE5 , Z_PQLI5   )  
  ELSEIF (LENCLD2) THEN
    IF (KSTEP == KSTART .AND. .NOT.LNCLIN) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZPA55(JL,JK) = 0.0_JPRB
          ZPL55(JL,JK) = 0.0_JPRB
          ZPI55(JL,JK) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF
    ! The only part of CLDPP relevant here is the numerics 
    ! security checks
    LLCLDCOVER=.FALSE.
    CALL  CLDPP (YDRADIATION,&
     &  YDERAD,YDERDI,YDMODEL%YRML_PHY_EC%YRECLD,YDRIP, KIDIA , KFDIA , KLON   , KLEV,&
     &  PAPRS5 , PAPRSF5 ,ZPA55  , ZPL55   , ZPI55, PT5,&
! Following line should be YDGSGEOM%GELAT,YDGSGEOM%GELAM, but not available in this 
! routine and is not relevant here anyway. 
! ***************************************************************************
!GM GELAT and GELAM are now available - somebody should make the correction
!   and remove these comments
! ***************************************************************************
     &  YDGSGEOM%GESLO, YDGSGEOM%GESLO,&
     &  LLCLDCOVER , ZCCC5  , ZHCC5  , ZLCC5   , ZMCC5  , Z_PTCC5,&
     & Z_PNEB5  , Z_PQICE5 , Z_PQLI5 )  
  ENDIF

  IF ((LERADN2 .AND. LERADFL2) .OR. (LERADN2 .AND. LERADLW2)) THEN
    DO JK=0,KLEV
      DO JL=KIDIA,KFDIA
        PEMTD5(JL,JK) = ZPEMTD5(JL,JK)
      ENDDO
    ENDDO
  ENDIF

  ZTSKRAD5(KIDIA:KFDIA)=PTL5(KIDIA:KFDIA)

  ZCEMTR5(:,:) = 0.0_JPRB
  ZCTRSO5(:,:) = 0.0_JPRB

  CALL RADHEAT (&
   &   YDERAD,YDERDI,YDMODEL%YRML_PHY_MF, &
   &   KIDIA  , KFDIA  , KLON   , KLEV,&
   &   PAPRS5 ,&
   &   Z_PEMIS5 , PEMTD5 , PMU0,&
   &   PQ5,&
   &   PTENT5 , PTRSO5 , ZTRSOD5, PTL5  , TSPHY,&
   &   Z_TRSODIR5, Z_TRSODIF5, Z_ALBD5 , Z_ALBP5,&
   &   Z_PFRSO5 , Z_PFRTH5 , Z_PFRSOD5, Z_PFRTHD5,&
   &   ZCEMTR5, ZCTRSO5, Z_PFRSOC5, Z_PFRTHC5,&
   &   ZSUDU5 , ZISUND5, ZDSRP5,&
   &   Z_SFSWDIR5,Z_SFSWDIF5,&
   & ZFRSOPS, ZFRSOFS, ZSWTOA )  
   
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENTX5(JL,JK) = PTENT5(JL,JK)
    ENDDO
  ENDDO

ELSE
  DO JK=1,KTILES
    DO JL=KIDIA,KFDIA
      ZALBTI5(JL,JK)=PALBF(JL)
    ENDDO
  ENDDO    
  DO JL=KIDIA,KFDIA
    Z_PEMIS5(JL)=PEMISF(JL)
  ENDDO
  ZTSKRAD5(KIDIA:KFDIA)=PTL5(KIDIA:KFDIA)
ENDIF

IF (LETRAJPT) THEN

! -----------------------------------
! using stored variables from TL     |
! -----------------------------------

!     ------------------------------------------------------------------

!*         3.A    VERTICAL EXCHANGE OF U,V,T,Q BY TURBULENCE
!                 ------------------------------------------

  DO JL=KIDIA,KFDIA
    ZTLE55(JL)=Z_PTLE15(JL)
    ZAZ0M55(JL)=ZAZ0M5(JL)
    ZAZ0H55(JL)=ZAZ0H5(JL)
  ENDDO

  DO JK=1,KTILES
    DO JL=KIDIA,KFDIA
      ZUSTRTI55(JL,JK)=PUSTRTI5(JL,JK)
      ZVSTRTI55(JL,JK)=PVSTRTI5(JL,JK)
      ZAHFSTI55(JL,JK)=PAHFSTI5(JL,JK)
      ZEVAPTI55(JL,JK)=PEVAPTI5(JL,JK)
      ZTSKTI55(JL,JK)=PTSKTI5(JL,JK)
    ENDDO
  ENDDO

  IF ( LEVDIF2 ) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55B(JL,JK) = ZTENTX5(JL,JK)
        ZTENQ55B(JL,JK) = ZTENQX5(JL,JK)
        ZTENU55B(JL,JK) = ZTENUX5(JL,JK)
        ZTENV55B(JL,JK) = ZTENVX5(JL,JK)

        ZTENTX5(JL,JK) = ZTENTVD5(JL,JK)
        ZTENQX5(JL,JK) = ZTENQVD5(JL,JK)
        ZTENUX5(JL,JK) = ZTENUVD5(JL,JK)
        ZTENVX5(JL,JK) = ZTENVVD5(JL,JK)
      ENDDO
    ENDDO

!LOP Tracer tendencies (not used for the moment)
!    DO ITRC=1,ITRAC
!      DO JK=1,KLEV
!        DO JL=KIDIA,KFDIA
!          ZTENC55B(JL,JK,ITRC) = ZTENCX5(JL,JK,ITRC)
!          ZTENCX5(JL,JK,ITRC) = ZTENCVD5(JL,JK,ITRC)
!        ENDDO
!      ENDDO
!    ENDDO

  ENDIF

!     ------------------------------------------------------------------

!*         4.A     GRAVITY WAVE DRAG PARAMETRIZATION
!                  ---------------------------------

  IF ( LEGWDG2 ) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55C(JL,JK) = ZTENTX5(JL,JK)
        ZTENQ55C(JL,JK) = ZTENQX5(JL,JK)
        ZTENU55C(JL,JK) = ZTENUX5(JL,JK)
        ZTENV55C(JL,JK) = ZTENVX5(JL,JK)

        ZTENTX5(JL,JK) = ZTENTGW5(JL,JK)
        ZTENUX5(JL,JK) = ZTENUGW5(JL,JK)
        ZTENVX5(JL,JK) = ZTENVGW5(JL,JK)
      ENDDO
    ENDDO

  ENDIF

!     ------------------------------------------------------------------

!*         5.A    CONVECTION PARAMETRIZATION
!                 --------------------------

  IF (LECUMF2) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55D(JL,JK) = ZTENTX5(JL,JK)
        ZTENQ55D(JL,JK) = ZTENQX5(JL,JK)
        ZTENU55D(JL,JK) = ZTENUX5(JL,JK)
        ZTENV55D(JL,JK) = ZTENVX5(JL,JK)

        ZTENTX5(JL,JK) = ZTENTCO5(JL,JK)
        ZTENQX5(JL,JK) = ZTENQCO5(JL,JK)
        ZTENUX5(JL,JK) = ZTENUCO5(JL,JK)
        ZTENVX5(JL,JK) = ZTENVCO5(JL,JK)

        ZTENT55F(JL,JK) = ZTENTX5(JL,JK)
        ZTENQ55F(JL,JK) = ZTENQX5(JL,JK)
        ZTENU55F(JL,JK) = ZTENUX5(JL,JK)
        ZTENV55F(JL,JK) = ZTENVX5(JL,JK)
      ENDDO
    ENDDO

!LOP Tracer tendencies (not used for the moment)
!    DO ITRC=1,ITRAC
!      DO JK=1,KLEV
!        DO JL=KIDIA,KFDIA
!          ZTENC55D(JL,JK,ITRC) = ZTENCX5(JL,JK,ITRC)
!        ENDDO
!      ENDDO
!    ENDDO

  ENDIF

!    ------------------------------------------------------------------

!*         6.A    LARGE SCALE WATER PHASE CHANGES
!                 -------------------------------

  IF ( LECOND2 .OR. LENCLD2 ) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55E(JL,JK) = ZTENTX5(JL,JK)
        ZTENQ55E(JL,JK) = ZTENQX5(JL,JK)
        ZTENU55E(JL,JK) = ZTENUX5(JL,JK)
        ZTENV55E(JL,JK) = ZTENVX5(JL,JK)
      ENDDO
    ENDDO
    IF (LEMWAVE) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENT5(JL,JK) = ZTENTX5(JL,JK)
          ZTENQ5(JL,JK) = ZTENQX5(JL,JK)
        ENDDO
      ENDDO
      CALL CLOUDST (&
       & YDMODEL%YRML_PHY_SLIN%YREPHLI,YDPHNC,YDMODEL%YRML_PHY_EC%YRECLD,YDMODEL%YRML_PHY_EC%YRECLDP, &
       & KIDIA  , KFDIA , KLON , KTDIA , KLEV, LDRAIN1D,&
       & TSPHY  ,&
       & PAPRS5 , PAPRSF5, PQ5  , Z_PQSAT5  , PT5    ,&
       & ZLUDE5 , ZLU5   ,&
       & ZTPROC5, ZTENT5 , ZQPROC5 , ZTENQ5 ,&
       & ZPA5   , ZPI5   , ZPL5 , Z_PFPLSL5 , Z_PFPLSN5,&
       & Z_PFHPSL5, Z_PFHPSN5, ZCOVPTOT5 )  
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENT5(JL,JK) = ZTENT5(JL,JK) + ZTPROC5(JL,JK)
          ZTENQ5(JL,JK) = ZTENQ5(JL,JK) + ZQPROC5(JL,JK)
        ENDDO
      ENDDO

      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENTX5(JL,JK) = ZTENT5(JL,JK)
        ENDDO
      ENDDO
    ELSE
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
        ZTENTX5(JL,JK) = ZTENTCL5(JL,JK) 
      ENDDO
    ENDDO
  ENDIF
  ENDIF

!     ------------------------------------------------------------------

!          7.A   WARNER-McINTYRE-SCINOCCA NON-OROGRAPHIC GRAVITY WAVE SCHEME
!                -----------------------------------------------------------

  IF ( LEGWWMS2 ) THEN

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55G(JL,JK) = ZTENTX5(JL,JK) 
        ZTENU55G(JL,JK) = ZTENUX5(JL,JK)
        ZTENU55G(JL,JK) = ZTENUX5(JL,JK)

        ZTENU5(JL,JK) = ZTENUWM5(JL,JK)
        ZTENV5(JL,JK) = ZTENVWM5(JL,JK)
      ENDDO
    ENDDO
  ENDIF

ELSE

! ------------------------------------------------------------
! re-computation of NL routines to get necessary trajectory  |
! ------------------------------------------------------------

!     ------------------------------------------------------------------

!*         3.     VERTICAL EXCHANGE OF U,V,T,Q BY TURBULENCE
!                 ------------------------------------------

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55B(JL,JK) = PTENT5(JL,JK)
      ZTENQ55B(JL,JK) = PTENQ5(JL,JK)
      ZTENU55B(JL,JK) = PTENU5(JL,JK)
      ZTENV55B(JL,JK) = PTENV5(JL,JK)
    ENDDO
  ENDDO
  IF (NGEMS > 0) THEN
    DO ITRC=1,ITRAC
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENC55B(JL,JK,ITRC) = ZTENC5(JL,JK,ITRC)  
        ENDDO
      ENDDO
    ENDDO
  ENDIF  

!     ***  LOCAL STORAGE OF UPDATED ARRAYS ***

  DO JL=KIDIA,KFDIA
    ZAZ0M55(JL)=ZAZ0M5(JL)
    ZAZ0H55(JL)=ZAZ0H5(JL)
    ZTLE55(JL)=Z_PTLE15(JL)
  ENDDO

  DO JK=1,KTILES
    DO JL=KIDIA,KFDIA
      ZUSTRTI55(JL,JK)=PUSTRTI5(JL,JK)
      ZVSTRTI55(JL,JK)=PVSTRTI5(JL,JK)
      ZAHFSTI55(JL,JK)=PAHFSTI5(JL,JK)
      ZEVAPTI55(JL,JK)=PEVAPTI5(JL,JK)
      ZTSKTI55(JL,JK)=PTSKTI5(JL,JK)
    ENDDO
  ENDDO

  IF ( LEVDIF2 ) THEN
    
    ZTHKICE5 = 0.0_JPRB
    ZSNTICE5 = 0.0_JPRB
    IF (LNEMOLIMTHK) THEN
       CALL ICESTATENEMO(YDMCC,KSTGLO,KIDIA,KFDIA,PTHKICE=ZTHKICE5(KIDIA:KFDIA),PSNTICE=ZSNTICE5(KIDIA:KFDIA))
    ENDIF

    CALL VDFMAINS (YDMODEL, KIDIA,KFDIA,KLON,KLEV,KLEVS,KTILES,&
     & ITRAC , TSPHY  , ITVL  , ITVH  , ZCVL ,ZCVH ,&
     & ZLAIL , ZLAIH  , PSNS5 , PRSN5 , ZHSDFOR,&
     & PU5   , PV5    , PT5   , PQ5   , ZCEN5, PAPRS5, ZGEOM15, ZGEOMH5,&
     & PTL5  , PTSA5  , PWSA5,&
     & Z_PFRSO5(1,KLEV) , Z_PFRTH5(1,KLEV), Z_PEMIS5,&
     & ZTHKICE5, ZSNTICE5,&
     & PTSN5 ,PTIA5(1,1),&
     ! & ZHLICE5 , ZTLICE5 , ZTLWML5 , & !lake passive
     & PSST  , ISOTY  , ZFRTI , ZALBTI5 , ZWLMX5,&
     & ZCHAR ,ZTSKRAD5, ZCFLX5,&
     ! OUTPUT (TRAJECTORY)
     & ZAZ0M5 , ZAZ0H5,&
     & PUCFL5, PVCFL5, PTCFL5, PDCFL5, PQCFL5, PU10N5, PV10N5,&
     & ZFRSOTI5, ZEVAPSNW5,&
     ! INPUT TENDENDCIES
     & PTENT5 , PTENQ5 , PTENU5 , PTENV5 ,&
     ! OUTPUT TENDENDCIES
     & ZTPROC5, ZQPROC5, ZUPROC5, ZVPROC5, ZTENC5, Z_PTLE15,&
     !-UPDATED FIELDS FOR TILES (TRAJECTORY)
     & PUSTRTI5 ,PVSTRTI5 ,PAHFSTI5,PEVAPTI5,PTSKTI5,&
     ! FLUX OUTPUTS (TRAJECTORY)
     & Z_PDIFTS5, Z_PDIFTQ5, Z_PSTRTU5 , Z_PSTRTV5 )

    PTENT5(KIDIA:KFDIA,1:KLEV)=PTENT5(KIDIA:KFDIA,1:KLEV)+ZTPROC5(KIDIA:KFDIA,1:KLEV)
    PTENQ5(KIDIA:KFDIA,1:KLEV)=PTENQ5(KIDIA:KFDIA,1:KLEV)+ZQPROC5(KIDIA:KFDIA,1:KLEV)
    PTENU5(KIDIA:KFDIA,1:KLEV)=PTENU5(KIDIA:KFDIA,1:KLEV)+ZUPROC5(KIDIA:KFDIA,1:KLEV)
    PTENV5(KIDIA:KFDIA,1:KLEV)=PTENV5(KIDIA:KFDIA,1:KLEV)+ZVPROC5(KIDIA:KFDIA,1:KLEV)

!  Storage for surface scheme
    DO JK=1,KTILES
      DO JL=KIDIA,KFDIA
        ZAHFSTI5(JL,JK)=PAHFSTI5(JL,JK)
        ZEVAPTI5(JL,JK)=PEVAPTI5(JL,JK)
      ENDDO
    ENDDO

  ELSE
!*   NECESSARY COMPUTATIONS IF SUBROUTINE IS BY-PASSED.
    DO JTILE=1,KTILES
      DO JL=KIDIA,KFDIA
        ZFRSOTI5(JL,JTILE)=Z_PFRSO5(JL,KLEV)
      ENDDO
    ENDDO  

  ENDIF

! do not turn off shallow convection

  DO JL=KIDIA,KFDIA
    LLSHCV(JL) = .TRUE.
  ENDDO

!     ------------------------------------------------------------------

!*         4.     GRAVITY WAVE DRAG PARAMETRIZATION
!                  ---------------------------------

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55C(JL,JK) = PTENT5(JL,JK)
      ZTENQ55C(JL,JK) = PTENQ5(JL,JK)
      ZTENU55C(JL,JK) = PTENU5(JL,JK)
      ZTENV55C(JL,JK) = PTENV5(JL,JK)
    ENDDO
  ENDDO

  IF ( LEGWDG2 ) THEN

!*         4.1     CALL GWD

    CALL GWDRAGS (&
     &  YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YREGWD, &
     &  KIDIA   , KFDIA , KLON , KLEV,&
     &  TSPHY   , KSTEP  , KSTART,&
     &  PAPRS5  , PAPRSF5, ZGEOM15,&
     &  PT5   , PU5 , PV5,&
     &  ZHSTD  , PGAMMA , PTHETA, PSIG,&
     ! OUTPUTS
     &  Z_PUSTRG5  , Z_PVSTRG5 , Z_PVDISG5, PTENV5 , PTENU5 ,PTENT5,&
     ! FLUX OUTPUTS
     & Z_PSTRDU5  , Z_PSTRDV5                        )  

  ENDIF

!     ------------------------------------------------------------------

!*         5.     CONVECTION PARAMETRIZATION
!                 --------------------------

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55D(JL,JK) = PTENT5(JL,JK)
      ZTENQ55D(JL,JK) = PTENQ5(JL,JK)
      ZTENU55D(JL,JK) = PTENU5(JL,JK)
      ZTENV55D(JL,JK) = PTENV5(JL,JK)
    ENDDO
  ENDDO
  IF (NGEMS > 0) THEN
    DO ITRC=1,ITRAC
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENC55D(JL,JK,ITRC) = ZTENC5(JL,JK,ITRC)  
        ENDDO
      ENDDO  
    ENDDO
  ENDIF
  
  IF (LECUMF2) THEN

!*         5.1    CALL CUCALLN2

! SIMPLIFIED MASS FLUX CONVECTION SCHEME (P. Lopez)

      ZTTENT5(KIDIA:KFDIA,1:KLEV)=PTENT5(KIDIA:KFDIA,1:KLEV)
      ZTTENQ5(KIDIA:KFDIA,1:KLEV)=PTENQ5(KIDIA:KFDIA,1:KLEV)
      ZTTENU5(KIDIA:KFDIA,1:KLEV)=PTENU5(KIDIA:KFDIA,1:KLEV)
      ZTTENV5(KIDIA:KFDIA,1:KLEV)=PTENV5(KIDIA:KFDIA,1:KLEV)
      CALL CUCALLN2 (&
       &  YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST, YDERAD,YDMODEL%YRML_PHY_SLIN,YDMODEL%YRML_PHY_EC, &
       &  KIDIA   , KFDIA , KLON  , KLEV,&
       &  LLLAND,LLSLPHY,LDRAIN1D,&
       &  TSPHY,RVDIFTS,&
       &  PT5     , PQ5   ,  PU5    , PV5,&
       &  PVERVEL5, Z_PDIFTQ5, Z_PDIFTS5, PAPRS5,&
       &  PRSF15  , PRS15  , ZGEOM15, ZGEOMH5, PGAW,&
       &  ZTPROC5 , ZTTENT5, PTENT5 , ZQPROC5, ZTTENQ5, PTENQ5,&
       &  ZUPROC5 , ZTTENU5, PTENU5 , ZVPROC5, ZTTENV5, PTENV5 , PACPR5,&
       &  ITOPC   , IBASC  , ITYPE,&
       &  ICBOT   , ICTOP  , IBOTSC , LLCUM  , LLSC,&
       &  ZLU5    , ZLUDE5 , ZMFU5  , ZMFD5,&
       &  Z_PDIFCQ5 , Z_PDIFCS5, Z_PFHPCL5, Z_PFHPCN5,&
       &  Z_PFPLCL5 , Z_PFPLCN5, Z_PSTRCU5, Z_PSTRCV5,&
       &  Z_PFCCQL5 , Z_PFCCQN5,&
       &  ZMFUDE_RATE5 ,    ZMFDDE_RATE5,   ZCAPE5,&
       &  ITRAC   , ZCEN5   , ZTENC5, ZSCAV5 )

      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          PTENT5(JL,JK) = PTENT5(JL,JK) + ZTPROC5(JL,JK)
          PTENQ5(JL,JK) = PTENQ5(JL,JK) + ZQPROC5(JL,JK)
          PTENU5(JL,JK) = PTENU5(JL,JK) + ZUPROC5(JL,JK)
          PTENV5(JL,JK) = PTENV5(JL,JK) + ZVPROC5(JL,JK)
        ENDDO
      ENDDO

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        ZTENT55F(JL,JK) = PTENT5(JL,JK)
        ZTENQ55F(JL,JK) = PTENQ5(JL,JK)
        ZTENU55F(JL,JK) = PTENU5(JL,JK)
        ZTENV55F(JL,JK) = PTENV5(JL,JK)
      ENDDO
    ENDDO

    ! CALCULATE TOTAL AND CLOUD-TO-GROUND LIGHTNING.
    IF (LELIGHT) THEN 
  
      CALL CULIGHT ( YDMODEL%YRML_PHY_EC%YRTHF, YDMODEL%YRCST, YDEPHY,YGFL, KIDIA, KFDIA, KLON, KLEV, PGAW, YDGSGEOM%GELAT,&
                &    PRSF15, PRS15, ZGEOMH5, ZGEOM15, LLLAND,&
                &    PT5, ZLU5, ZMFU5, ZCAPE5,&
                &    Z_PFPLCL5, Z_PFPLCN5,&
                &    LLCUM, ICBOT, ICTOP,&
                ! Outputs
                &    LLLINOX, ZLIGH_TOT5, ZLIGH_CTG5, ZCTOPH5,&
                &    ZPRECMX5, ZICE5, ZCDEPTH5, ZWMFU5)
      
    ENDIF

  ENDIF

!     ------------------------------------------------------------------

!*         6.     LARGE SCALE WATER PHASE CHANGES
!                 -------------------------------

!     ***  LOCAL STORAGE OF UPDATED TENDENCIES ***

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55E(JL,JK) = PTENT5(JL,JK)
      ZTENQ55E(JL,JK) = PTENQ5(JL,JK)
      ZTENU55E(JL,JK) = PTENU5(JL,JK)
      ZTENV55E(JL,JK) = PTENV5(JL,JK)
    ENDDO
  ENDDO

  IF ( LECOND2 .AND. .NOT. LENCLD2 ) THEN

!*         6.1    CALL COND

    CALL COND (&
     &  YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YRECND, KIDIA , KFDIA , KLON  , KLEV ,&
     &  TSPHY,&
     &  PT5    , PQ5    , PRS15,   PRSF15,&
     &  ZTPROC5, PTENT5, ZQPROC5, PTENQ5,&
     & Z_PFHPSL5, Z_PFHPSN5, Z_PFPLSL5, Z_PFPLSN5   )  

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTENT5(JL,JK) = PTENT5(JL,JK) + ZTPROC5(JL,JK)
        PTENQ5(JL,JK) = PTENQ5(JL,JK) + ZQPROC5(JL,JK)
      ENDDO
    ENDDO

  ELSEIF( LENCLD2 ) THEN

!*         6.2     CALL CLOUDST

    CALL CLOUDST (&
     & YDMODEL%YRML_PHY_SLIN%YREPHLI,YDPHNC,YDMODEL%YRML_PHY_EC%YRECLD,YDMODEL%YRML_PHY_EC%YRECLDP, &
     & KIDIA  , KFDIA , KLON , KTDIA , KLEV, LDRAIN1D,&
     & TSPHY  ,&
     & PAPRS5 , PAPRSF5, PQ5  , Z_PQSAT5  , PT5    ,&
     & ZLUDE5 , ZLU5   ,&
     & ZTPROC5, PTENT5 , ZQPROC5, PTENQ5 ,&
     & ZPA5   , ZPI5   , ZPL5 , Z_PFPLSL5 , Z_PFPLSN5,&
     & Z_PFHPSL5, Z_PFHPSN5, ZCOVPTOT5 )  
    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTENT5(JL,JK) = PTENT5(JL,JK) + ZTPROC5(JL,JK)
        PTENQ5(JL,JK) = PTENQ5(JL,JK) + ZQPROC5(JL,JK)
      ENDDO
    ENDDO
  ENDIF

!*        7.  WARNER-McINTYRE-SCINOCCA NON-OROGRAPHIC GRAVITY WAVE SCHEME
!             -----------------------------------------------------------

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENT55G(JL,JK) = PTENT5(JL,JK)
      ZTENU55G(JL,JK) = PTENU5(JL,JK)
      ZTENV55G(JL,JK) = PTENV5(JL,JK)

      ZTENU5(JL,JK) = ZTENUWM5(JL,JK)
      ZTENV5(JL,JK) = ZTENVWM5(JL,JK)
    ENDDO
  ENDDO

  IF ( LEGWWMS2 ) THEN
!    ZTSGWD=GTPHYGWWMS
!    ZTSGWD=TSPHY
!    IF (MOD(KSTEP*TSPHY,ZTSGWD)==0.0_JPRB.OR.KSTEP==KSTART) THEN
!      CALL GWDRAG_WMSS(KIDIA , KFDIA , KLON   , KLEV   , ZTSGWD , &
!       & PT5 , PU5 , PV5 , PAPRSF5  , PAPRS5, PAPHIF5, &
!       & YDGSGEOM%GELAT, &
!       & ZTENU5 , ZTENV5 , ZFLXWGWU5, ZFLXWGWV5)
!    ENDIF

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PTENU5(JL,JK) = PTENU5(JL,JK)+ZTENU5(JL,JK)
        PTENV5(JL,JK) = PTENV5(JL,JK)+ZTENV5(JL,JK)

        ZTFIELD5(JL,JK) = (PU5(JL,JK)*ZTENU5(JL,JK)&
         & + PV5(JL,JK)*ZTENV5(JL,JK))*ZRCPD
        PTENT5(JL,JK) = PTENT5(JL,JK)-ZTFIELD5(JL,JK)
      ENDDO
    ENDDO

  ENDIF

!     ------------------------------------------------------------------

!*        10.      COMPUTATION OF NEW SURFACE VALUES
!                  ---------------------------------

  IF ( LESURF2) THEN

!*        10.1   CALL SURFTSTP

    CALL SURFTSTPS(YDSURF=YSURF,KIDIA=KIDIA,KFDIA=KFDIA,KLON=KLON,KLEVS=KLEVS,&
     & KTILES=KTILES, KSOTY=ISOTY,&
     & PTSPHY=TSPHY , PFRTI=ZFRTI,&
     & PAHFSTI=PAHFSTI5, PEVAPTI=PEVAPTI5, PSSRFLTI=ZFRSOTI5,&
     & LDLAND=LLLAND,  LDSICE=LLSICE, LDSI=LESICE, LDNH=LLNH,&
     & PSNM1M=ZSNM1M5  ,PTSNM1M=PTSN5,PRSNM1M=PRSN5,&
     & PTSAM1M=PTSA5, PTIAM1M=PTIA5,&
     & PWLM1M=ZWLM1M5  ,PWSAM1M=PWSA5,&
     & PHLICEM1M=ZTLICE5,&
     & PRSFC=Z_PFPLCL5(:,KLEV) ,PRSFL=Z_PFPLSL5(:,KLEV),&
     & PSLRFL=Z_PFRTH5(:,KLEV) ,&
     & PSSFC=Z_PFPLCN5(:,KLEV) ,PSSFL=Z_PFPLSN5(:,KLEV),&
     & PCVL=ZCVL    ,PCVH=ZCVH   ,PWLMX=ZWLMX5   ,PEVAPSNW=ZEVAPSNW5,&
!-TENDENCIES OUTPUT
     & PTSNE1=Z_PTSNE15 , PTSAE1=Z_PTSAE15 , PTIAE1=Z_PTIAE15)

  ENDIF

ENDIF

!     ------------------------------------------------------------------

!*         8.     ELIMINATION OF NEGATIVE SPECIFIC HUMIDITIES
!                 -------------------------------------------

IF ( LEQNGT2 ) THEN

  CALL ABOR1 ('AD OF NEGATIVE HUMIDITIES NOT CODED')

ELSE

!*         8.3     NECESSARY COMPUTATIONS IF SUBROUTINE IS BY-PASSED.
  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA
      Z_PFCQNG5(JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*         9.     TRACER TRANSPORT TENDENCIES BACK INTO EXTRA FIELDS
!                 --------------------------------------------------
 
IF (NGEMS > 0 .AND. LTRACLNPH) THEN
  CALL GEMS_TEND(YGFL, KIDIA, KFDIA, KLEV, KLON, PTENGFL5, ZTENC5)
ENDIF


!     ------------------------------------------------------------------

!*              11.     END OF E.C.M.W.F. PHYSICS
!                      -------------------------

!*              11.1    UNIT CHANGES/SCALINGS
DO JL=KIDIA,KFDIA
  PGZ0M5 (JL)=ZAZ0M5(JL)*RG
  PLZ0H5 (JL)=LOG(ZAZ0H5(JL))
ENDDO

!-----------------------------------------------------------------------

!*                    ----  ADJOINT COMPUTATIONS  ----

!-----------------------------------------------------------------------

!*         7.     INITIALISATIONS
!                 ---------------

ZFRSOTI(:,:) = 0.0_JPRB
ZEVAPSNW(:) = 0.0_JPRB

Z_PUSTRG (:)=0.0_JPRB
Z_PVSTRG (:)=0.0_JPRB

ZALBD(:,:)   = 0.0_JPRB
ZALBP(:,:)   = 0.0_JPRB
ZALBTI(:,:)  = 0.0_JPRB
ZTSKRAD(:)   = 0.0_JPRB
ZEMIR(:)     = 0.0_JPRB
ZEMIW(:)     = 0.0_JPRB
ZAZ0H(:)     = 0.0_JPRB
ZAZ0M(:)     = 0.0_JPRB
ZCTLWP(:)    = 0.0_JPRB
ZSNM1M(:)    = 0.0_JPRB
ZWLM1M(:)    = 0.0_JPRB
ZGEOM1(:,:)  = 0.0_JPRB
ZGEOMH(:,:)  = 0.0_JPRB
ZLU(:,:)     = 0.0_JPRB
ZLUDE(:,:)   = 0.0_JPRB
ZMFU(:,:)    = 0.0_JPRB
ZMFD(:,:)    = 0.0_JPRB
ZHRSW(:,:)   = 0.0_JPRB
ZHRLW(:,:)   = 0.0_JPRB
ZTRSOD(:)    = 0.0_JPRB
ZT(:,:)      = 0.0_JPRB
ZTHT(:,:)    = 0.0_JPRB
ZEMIT(:)     = 0.0_JPRB

ZPA(:,:)     = 0.0_JPRB
ZPL(:,:)     = 0.0_JPRB
ZPI(:,:)     = 0.0_JPRB
ZQRAIN(:,:)  = 0.0_JPRB
ZRAINT(:,:)  = 0.0_JPRB
ZCTRSO(:,:)  = 0.0_JPRB
ZCEMTR(:,:)  = 0.0_JPRB
Z_PQICE(:,:)   = 0.0_JPRB
Z_PQLI(:,:)    = 0.0_JPRB
Z_PNEB(:,:)    = 0.0_JPRB
Z_PTCC(:)      = 0.0_JPRB
Z_PEMTD(:,:)   = 0.0_JPRB

Z_PFRSO(:,:)   = 0.0_JPRB
Z_PFRTH(:,:)   = 0.0_JPRB
Z_PQSAT(:,:)   = 0.0_JPRB

Z_PTRSO(:,:) = 0.0_JPRB
Z_PFRSOD(:)  = 0.0_JPRB
Z_PFRTHD(:)  = 0.0_JPRB

Z_PFPLSN (:,:) = 0.0_JPRB
Z_PFPLCN (:,:) = 0.0_JPRB
Z_PFPLSL (:,:) = 0.0_JPRB
Z_PFPLCL (:,:) = 0.0_JPRB
Z_PFHPSL (:,:) = 0.0_JPRB
Z_PFHPSN (:,:) = 0.0_JPRB
Z_PFHPCL (:,:) = 0.0_JPRB
Z_PFHPCN (:,:) = 0.0_JPRB
ZCOVPTOT (:,:) = 0.0_JPRB

Z_ALBD5  (:,:) = 0.0_JPRB
Z_ALBP5  (:,:) = 0.0_JPRB
Z_TRSODIR5(:,:)= 0.0_JPRB
Z_TRSODIF5(:,:)= 0.0_JPRB
Z_SFSWDIR5(:,:) = 0.0_JPRB
Z_SFSWDIF5(:,:) = 0.0_JPRB

ZTENU   (:,:) = 0.0_JPRB
ZTENV   (:,:) = 0.0_JPRB
ZTFIELD (:,:) = 0.0_JPRB
ZFLXWGWU(:,:) = 0.0_JPRB
ZFLXWGWV(:,:) = 0.0_JPRB

ZTPROC(:,:)  = 0.0_JPRB
ZQPROC(:,:)  = 0.0_JPRB
ZUPROC(:,:)  = 0.0_JPRB
ZVPROC(:,:)  = 0.0_JPRB

ZCAPE(:) = 0.0_JPRB
ZLIGH_TOT(:) = 0.0_JPRB

!    REMARK :

!    Prognostic variables of the surface scheme need to be initialised
!    to zero as long as the TL/AD versions are not available

Z_PWSAE1(:,:)  = 0.0_JPRB
Z_PWLE1(:)     = 0.0_JPRB
Z_PSNSE1(:)    = 0.0_JPRB

!*      AD-OBSERVATION OPERATOR FOR RADAR RAIN ASSIMILATION

IF (LEGBRAD_ACTIVE) THEN

! Update precipitation accumulation
  DO JL = KIDIA, KFDIA
     Z_PFPLSN (JL,KLEV) = Z_PFPLSN (JL,KLEV) + TSPHY * PACCPR (JL)
     Z_PFPLCN (JL,KLEV) = Z_PFPLCN (JL,KLEV) + TSPHY * PACCPR (JL)
     Z_PFPLSL (JL,KLEV) = Z_PFPLSL (JL,KLEV) + TSPHY * PACCPR (JL)
     Z_PFPLCL (JL,KLEV) = Z_PFPLCL (JL,KLEV) + TSPHY * PACCPR (JL)
  ENDDO

! Reset precipitation accumulation to zero at first time step 
! of accumulation period
  IF (MOD(KSTEP*NINT(TSPHY),NPRACCL) == 0) THEN
    DO JL = KIDIA, KFDIA
      PACCPR (JL) = 0.0_JPRB
    ENDDO
  ENDIF

ENDIF

!*      AD-OBSERVATION OPERATOR FOR RAIN GAUGE ASSIMILATION

IF (LERAINGG) THEN

! Store precipitation amount at current time step (mm).
  DO JL = KIDIA, KFDIA
    Z_PFPLSN (JL,KLEV) = Z_PFPLSN (JL,KLEV) + TSPHY * PACCPR3 (JL,KSTEP)
    Z_PFPLCN (JL,KLEV) = Z_PFPLCN (JL,KLEV) + TSPHY * PACCPR3 (JL,KSTEP)
    Z_PFPLSL (JL,KLEV) = Z_PFPLSL (JL,KLEV) + TSPHY * PACCPR3 (JL,KSTEP)
    Z_PFPLCL (JL,KLEV) = Z_PFPLCL (JL,KLEV) + TSPHY * PACCPR3 (JL,KSTEP)
    PACCPR3 (JL,KSTEP) = 0.0_JPRB
  ENDDO

ENDIF

! Diagnostic fields required by AD observation operator for all-sky observations
IF(YDMODEL%YRML_PHY_EC%YREPHY%LEMWAVE) THEN

  DO JK = KLEV, 1, -1
   DO JL = KIDIA, KFDIA
    
      Z_PFPLSL (JL,JK-1) = Z_PFPLSL (JL,JK-1) + 0.5_JPRB * PHYS_MWAVE(JL,JK,4)
      Z_PFPLSL (JL,JK  ) = Z_PFPLSL (JL,JK  ) + 0.5_JPRB * PHYS_MWAVE(JL,JK,4)
      Z_PFPLSN (JL,JK-1) = Z_PFPLSN (JL,JK-1) + 0.5_JPRB * PHYS_MWAVE(JL,JK,5)
      Z_PFPLSN (JL,JK  ) = Z_PFPLSN (JL,JK  ) + 0.5_JPRB * PHYS_MWAVE(JL,JK,5)
      Z_PFPLCL (JL,JK-1) = Z_PFPLCL (JL,JK-1) + 0.5_JPRB * PHYS_MWAVE(JL,JK,6)
      Z_PFPLCL (JL,JK  ) = Z_PFPLCL (JL,JK  ) + 0.5_JPRB * PHYS_MWAVE(JL,JK,6)
      Z_PFPLCN (JL,JK-1) = Z_PFPLCN (JL,JK-1) + 0.5_JPRB * PHYS_MWAVE(JL,JK,7)
      Z_PFPLCN (JL,JK  ) = Z_PFPLCN (JL,JK  ) + 0.5_JPRB * PHYS_MWAVE(JL,JK,7)
      IF (LENCLD2) THEN 
        ZPL (JL,JK) = ZPL (JL,JK) + PHYS_MWAVE(JL,JK,1)
        ZPI (JL,JK) = ZPI (JL,JK) + PHYS_MWAVE(JL,JK,2)
        ZPA (JL,JK) = ZPA (JL,JK) + PHYS_MWAVE(JL,JK,3)
        ZCOVPTOT (JL,JK) = ZCOVPTOT (JL,JK) + PHYS_MWAVE(JL,JK,8)
      ENDIF
      PHYS_MWAVE(JL,JK,:) = 0.0_JPRB 

    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*              10.     END OF E.C.M.W.F. PHYSICS
!                      -------------------------

!*              10.1    UNIT CHANGES/SCALINGS
DO JL=KIDIA,KFDIA
  ZAZ0H  (JL)=ZAZ0H(JL) + PLZ0H(JL)/ZAZ0H55(JL)
  PLZ0H  (JL)=0.0_JPRB
  ZAZ0M  (JL)=ZAZ0M(JL) + PGZ0M(JL)*RG
  PGZ0M  (JL)=0.0_JPRB
ENDDO
!     ------------------------------------------------------------------

!*        10.      COMPUTATION OF NEW SURFACE VALUES
!                  ---------------------------------

IF ( LESURF2) THEN

!*        10.1   CALL SURFTSTPSAD

  CALL SURFTSTPSAD(YDSURF=YSURF,KIDIA=KIDIA,KFDIA=KFDIA,KLON=KLON,KLEVS=KLEVS,KTILES=KTILES,&
     & KSOTY=ISOTY,&
     & PTSPHY=TSPHY , PFRTI=ZFRTI,&
!-trajectory
     & LDLAND=LLLAND,  LDSICE=LLSICE, LDSI=LESICE, LDNH=LLNH, LDREGSF=LREGSF,&
     & PAHFSTI5=ZAHFSTI5, PEVAPTI5=ZEVAPTI5, PSSRFLTI5=ZFRSOTI5,&
     & PSNM1M5=ZSNM1M5  ,PTSNM1M5=PTSN5,PRSNM1M=PRSN5,&
     & PTSAM1M5=PTSA5, PTIAM1M5=PTIA5,&
     & PWLM1M5=ZWLM1M5  ,PWSAM1M5=PWSA5,&
     & PHLICEM1M5=ZTLICE5,&
     & PRSFC5=Z_PFPLCL5(:,KLEV)   ,PRSFL5=Z_PFPLSL5(:,KLEV),&
     & PSLRFL5=Z_PFRTH5(:,KLEV)   ,&
     & PSSFC5=Z_PFPLCN5(:,KLEV)   ,PSSFL5=Z_PFPLSN5(:,KLEV),&
     & PCVL=ZCVL    ,PCVH=ZCVH   ,PWLMX5=ZWLMX5   ,PEVAPSNW5=ZEVAPSNW5,&
!-TENDENCIES OUTPUT
     & PTSNE15=Z_PTSNE15 , PTSAE15=Z_PTSAE15 , PTIAE15=Z_PTIAE15,&
!-perturbations
     & PAHFSTI=PAHFSTI, PEVAPTI=PEVAPTI, PSSRFLTI=ZFRSOTI,&
     & PSNM1M=ZSNM1M  ,PTSNM1M=PTSN,&
     & PTSAM1M=PTSA, PTIAM1M=PTIA,&
     & PWLM1M=ZWLM1M  ,PWSAM1M=PWSA,&
     & PRSFC=Z_PFPLCL(:,KLEV)   ,PRSFL=Z_PFPLSL(:,KLEV),&
     & PSLRFL=Z_PFRTH(:,KLEV)   ,&
     & PSSFC=Z_PFPLCN(:,KLEV)  ,PSSFL=Z_PFPLSN(:,KLEV),&
     & PEVAPSNW=ZEVAPSNW,&
!-TENDENCIES OUTPUT
     & PTSNE1=PTSNE1 , PTSAE1=PTSAE1 , PTIAE1=PTIAE1)

ENDIF

!*         9.      TRACER TRANSPORT TENDENCIES BACK INTO EXTRA FIELDS
!                  --------------------------------------------------

!    Initialize Tracers Adjoint Variables

IF (NGEMS > 0 .AND. LTRACLNPH) THEN
  CALL GEMS_INIT_AD( YGFL, KIDIA, KFDIA, KLEV, KLON, ITRAC,&
    & PTENGFL, ZCEN, ZTENC, ZCFLX )
ELSE
! ALLOCATE arrays so they can be passed to VDFOUTER etc. in the non-GEMS case
   ALLOCATE(ZCEN(1,1,1))
   ALLOCATE(ZTENC(1,1,1))
   ALLOCATE(ZCFLX(1,1))
ENDIF

!     ------------------------------------------------------------------

!*         8.     ELIMINATION OF NEGATIVE SPECIFIC HUMIDITIES
!                 -------------------------------------------

IF ( LEQNGT2 ) THEN

  CALL ABOR1 ('AD OF NEGATIVE HUMIDITIES NOT CODED')

ELSE

!*         8.3     NECESSARY COMPUTATIONS IF SUBROUTINE IS BY-PASSED.
  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA
      Z_PFCQNG (JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

!     ------------------------------------------------------------------

!*         7.   WARNER-McINTYRE-SCINOCCA NON-OROGRAPHIC GRAVITY WAVE SCHEME
!               -----------------------------------------------------------

IF ( LEGWWMS2 ) THEN
 
  ZTSGWD=GTPHYGWWMS
!  ZTSGWD=TSPHY

  IFREQWMS = INT(ZTSGWD/TSPHY)
  ISTEPWMS = MOD(KSTEP,IFREQWMS)

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTFIELD(JL,JK) = ZTFIELD(JL,JK)-PTENT(JL,JK)
      ZTENU(JL,JK) = ZTENU(JL,JK)+ZRCPD*PU5(JL,JK)*ZTFIELD(JL,JK)
      PU(JL,JK) = PU(JL,JK)+ZRCPD*ZTENU5(JL,JK)*ZTFIELD(JL,JK)
      ZTENV(JL,JK) = ZTENV(JL,JK)+ZRCPD*PV5(JL,JK)*ZTFIELD(JL,JK)
      PV(JL,JK) = PV(JL,JK)+ZRCPD*ZTENV5(JL,JK)*ZTFIELD(JL,JK)
      ZTFIELD(JL,JK) = 0.0_JPRB

      ZTENV(JL,JK) = ZTENV(JL,JK)+PTENV(JL,JK)
      ZTENU(JL,JK) = ZTENU(JL,JK)+PTENU(JL,JK)

      IF (LREGWWMS) THEN
      IF (PAPRSF5(JL,JK) < 50.0_JPRB .AND. ABS(PV5(JL,JK)) > 30.0_JPRB) THEN
        ZTENU (JL,JK) = 0.0_JPRB
      ENDIF
      ENDIF

    ENDDO
  ENDDO

  IF (ISTEPWMS == 0. .OR. KSTEP==KSTART .OR. KSTEP >= (NSTOP-1)) THEN
!  IF (MOD(KSTEP*TSPHY,ZTSGWD)==0.0_JPRB.OR.KSTEP==KSTART) THEN

    IF(KSTEP /= NSTOP-1) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZTENU(JL,JK) = ZTENU(JL,JK)+PGPHIST%TGWDWMSU(JL,JK)
          PGPHIST%TGWDWMSU(JL,JK) = 0.0_JPRB
          ZTENV(JL,JK) = ZTENV(JL,JK)+PGPHIST%TGWDWMSV(JL,JK)
          PGPHIST%TGWDWMSV(JL,JK) = 0.0_JPRB 
        ENDDO
      ENDDO
    ELSE
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          PGPHIST%TGWDWMSU(JL,JK) = 0.0_JPRB
          PGPHIST%TGWDWMSV(JL,JK) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF
        
    CALL GWDRAG_WMSSAD(YDEGWDWMS,YDMODEL%YRML_PHY_EC%YREGWD,YDEGWWMS, KIDIA , KFDIA , KLON   , KLEV   , ZTSGWD ,&
     & PT5 , PU5 , PV5 , PAPRSF5  , PAPRS5, PAPHIF5,&
     & YDGSGEOM%GELAT  , PGAW, &
     & ZTENU55G  , ZTENV55G , ZFLXWGWU5, ZFLXWGWV5,&
     & PT  , PU  , PV  , PAPRSF   , PAPRS , PAPHIF ,&
     & ZTENU  , ZTENV  , ZFLXWGWU , ZFLXWGWV )

  ELSE

    DO JK=1,KLEV
      DO JL=KIDIA,KFDIA
        PGPHIST%TGWDWMSU(JL,JK) = PGPHIST%TGWDWMSU(JL,JK)+ZTENU(JL,JK)
        ZTENU(JL,JK) = 0.0_JPRB 
        PGPHIST%TGWDWMSV(JL,JK) = PGPHIST%TGWDWMSV(JL,JK)+ZTENV(JL,JK)
        ZTENV(JL,JK) = 0.0_JPRB
      ENDDO
    ENDDO
  ENDIF

ELSE

  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA      
      ZFLXWGWU (JL,JK) = 0.0_JPRB
      ZFLXWGWV (JL,JK) = 0.0_JPRB
    ENDDO
  ENDDO
ENDIF


!*         6.     LARGE SCALE WATER PHASE CHANGES
!                 -------------------------------

!*    SET UP INPUT FLUXES TO ZERO

IF ( LECOND2 .AND. .NOT. LENCLD2 ) THEN

!*         6.1    CALL CONDAD

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTPROC(JL,JK)=ZTPROC(JL,JK)+PTENT(JL,JK)
      ZQPROC(JL,JK)=ZQPROC(JL,JK)+PTENQ(JL,JK)
    ENDDO
  ENDDO

  CALL CONDAD (&
   & YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YRECND, &
   &  KIDIA , KFDIA , KLON  , KLEV,&
   &  TSPHY,&
   &  PT5    , PQ5    , PRS15,   PRSF15,&
   &  ZTPROC5,ZTENT55E, ZQPROC5, ZTENQ55E,&
   &  Z_PFHPSL5, Z_PFHPSN5, Z_PFPLSL5, Z_PFPLSN5,&
   &  PT    , PQ    , PRS1,   PRSF1,&
   &  ZTPROC, PTENT, ZQPROC,  PTENQ,&
   & Z_PFHPSL, Z_PFHPSN, Z_PFPLSL, Z_PFPLSN          )  

ELSEIF (LENCLD2) THEN

!*         6.2     CALL CLOUDSTAD

  IF (KSTEP >= (NSTOP-1)) THEN
    PA(:,:)  = 0.0_JPRB
    PL(:,:)  = 0.0_JPRB
    PI(:,:)  = 0.0_JPRB
  ELSE
    DO JK=KLEV,1,-1
      DO JL=KIDIA,KFDIA
        ZPI (JL,JK) = ZPI (JL,JK) + PI(JL,JK)
        PI(JL,JK) = 0.0_JPRB
        ZPL (JL,JK) = ZPL (JL,JK) + PL(JL,JK)
        PL(JL,JK) = 0.0_JPRB
        ZPA (JL,JK) = ZPA (JL,JK) + PA(JL,JK)
        PA(JL,JK) = 0.0_JPRB
      ENDDO
    ENDDO
  ENDIF

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTPROC(JL,JK)=ZTPROC(JL,JK)+PTENT(JL,JK)
      ZQPROC(JL,JK)=ZQPROC(JL,JK)+PTENQ(JL,JK)
    ENDDO
  ENDDO

  CALL CLOUDSTAD (&
   & YDMODEL%YRML_PHY_SLIN,YDMODEL%YRML_PHY_EC%YRECLD,YDMODEL%YRML_PHY_EC%YRECLDP, &
   & KIDIA  , KFDIA , KLON , KTDIA , KLEV, LDRAIN1D,&
   & TSPHY  ,&
   & PAPRS5 , PAPRSF5, PQ5  , Z_PQSAT5  , PT5    ,&
   & ZLUDE5 , ZLU5   ,&
   & ZTPROC5,ZTENT55E ,ZQPROC5, ZTENQ55E ,&
   & ZPA5   , ZPI5   , ZPL5 , Z_PFPLSL5 , Z_PFPLSN5,&
   & Z_PFHPSL5, Z_PFHPSN5, ZCOVPTOT5,&
   & PAPRS  , PAPRSF , PQ   , Z_PQSAT   , PT     ,&
   & ZLUDE  , ZLU    ,&
   & ZTPROC , PTENT  , ZQPROC , PTENQ  ,&
   & ZPA    , ZPI    , ZPL  ,Z_PFPLSL   , Z_PFPLSN ,&
   & Z_PFHPSL , Z_PFHPSN, ZCOVPTOT )  

ENDIF

!     ------------------------------------------------------------------

!*         5.     CONVECTION PARAMETRIZATION
!                 --------------------------

IF (LECUMF2) THEN

  ! CALCULATE TOTAL AND CLOUD-TO-GROUND LIGHTNING.
  IF (LELIGHT) THEN 

!LLL Initialize lightning flash rate here for sensitivity computations.
!    DO JL=KIDIA,KFDIA
!      ZLIGH_TOT(JL) = ???
!    ENDDO
!LLL

    CALL CULIGHTAD ( YDEPHY, KIDIA, KFDIA, KLON, KLEV, PGAW, YDGSGEOM%GELAT,&
              ! Trajectory variables.
              &    PRSF15, PRS15, ZGEOMH5, ZGEOM15, LLLAND,&
              &    PT5, ZLU5, ZMFU5, ZCAPE5,&
              &    Z_PFPLCL5, Z_PFPLCN5,&
              &    LLCUM, ICBOT, ICTOP,&
              &    ZLIGH_TOT5, ZLIGH_CTG5, ZCTOPH5,&
              &    ZPRECMX5, ZICE5, ZCDEPTH5, ZWMFU5, &
              ! Perturbations.
              &    PRSF1, PRS1, ZGEOMH, ZGEOM1, &
              &    PT, ZLU, ZMFU, ZCAPE, &
              &    Z_PFPLCL, Z_PFPLCN, &
              &    ZLIGH_TOT, ZLIGH_CTG, ZCTOPH, &
              &    ZPRECMX, ZICE, ZCDEPTH, ZWMFU)
    
  ENDIF

! SIMPLIFIED MASS FLUX CONVECTION SCHEME (P. Lopez)

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTPROC (JL,JK) = ZTPROC (JL,JK) + PTENT (JL,JK)
      ZQPROC (JL,JK) = ZQPROC (JL,JK) + PTENQ (JL,JK)
      ZUPROC (JL,JK) = ZUPROC (JL,JK) + PTENU (JL,JK)
      ZVPROC (JL,JK) = ZVPROC (JL,JK) + PTENV (JL,JK)
    ENDDO
  ENDDO

  CALL CUCALLN2AD (&
   & YDMODEL%YRML_PHY_EC%YRTHF,YDMODEL%YRCST,YDMODEL%YRML_PHY_SLIN,YDMODEL%YRML_PHY_EC, &
   & KIDIA,    KFDIA,    KLON,   KLEV,&
   & LLLAND,   LDRAIN1D,&
   & TSPHY,&
   & PT5,      PQ5,      PU5,      PV5,&
   & PVERVEL5, Z_PDIFTQ5,  Z_PDIFTS5,  PAPRS5,&
   & PRSF15,   PRS15,    ZGEOM15,  ZGEOMH5, PGAW,&
   & ZTPROC5 , ZTENT55D, ZQPROC5 , ZTENQ55D,&
   & ZUPROC5 , ZTENU55D, ZVPROC5 , ZTENV55D,&
   & ZTENT55F, ZTENQ55F, ZTENU55F, ZTENV55F,&
   & ITYPE,&
   & ICBOT,    ICTOP,    IBOTSC,   LLCUM,   LLSC,&
   & ZLU5,     ZLUDE5,   ZMFU5,    ZMFD5,&
   & Z_PFPLCL5,  Z_PFPLCN5,&
   & ITRAC,    ZCEN5,    ZTENC55D, ZSCAV5,&
   & PT,       PQ,       PU,       PV,&
   & PVERVEL,  PDIFTQ,   PDIFTS,   PAPRS,&
   & PRSF1,    PRS1,     ZGEOM1,   ZGEOMH,&
   & ZTPROC,   PTENT,    ZQPROC,   PTENQ,&
   & ZUPROC,   PTENU,    ZVPROC,   PTENV,&
   & ZLU,      ZLUDE,    ZMFU,     ZMFD,&
   & Z_PDIFCQ, Z_PDIFCS, Z_PFHPCL, Z_PFHPCN,&
   & Z_PFPLCL, Z_PFPLCN, Z_PSTRCU, Z_PSTRCV,  Z_PFCCQL ,Z_PFCCQN,&
   & ZCAPE,&
   & ZCEN, ZTENC )  

!*         5.2    SET CONVECTIVE CLOUD PARAMETERS FOR NEXT TIME-STEP

  DO JL=KIDIA,KFDIA
    PNTOP(JL)=REAL ( ITOPC(JL),JPRB)  + 0.01_JPRB
    PNBAS(JL)=REAL ( IBASC(JL),JPRB)  + 0.01_JPRB
  ENDDO

ELSE

!*         5.3     NECESSARY COMPUTATIONS IF SUBROUTINE IS BY-PASSED.
  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA
      Z_PDIFCS (JL,JK)=0.0_JPRB
      Z_PDIFCQ (JL,JK)=0.0_JPRB
      Z_PFHPCL (JL,JK)=0.0_JPRB
      Z_PFHPCN (JL,JK)=0.0_JPRB
      Z_PFPLCL (JL,JK)=0.0_JPRB
      Z_PFPLCN (JL,JK)=0.0_JPRB
      Z_PSTRCU (JL,JK)=0.0_JPRB
      Z_PSTRCV (JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF


!*         4.     GRAVITY WAVE DRAG PARAMETRIZATION
!                  ---------------------------------

!*         4.1     SETUP INPUT FLUXES TO ZERO.

!Z_PUSTRG (:)=0.0_JPRB
!Z_PVSTRG (:)=0.0_JPRB
Z_PVDISG (:)=0.0_JPRB

Z_PSTRDU (:,:)=0.0_JPRB
Z_PSTRDV (:,:)=0.0_JPRB

IF ( LEGWDG2 ) THEN

!*         4.2     CALL GWDAD

  CALL GWDRAGAD (&
   & YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YREGWD, &
   &  KIDIA    , KFDIA  , KLON  , KLEV,&
   &  TSPHY    , KSTEP   , KSTART,&
   &  PAPRS5   , PAPRSF5 , ZGEOM15, PT5    , PU5,&
   &  PV5      , ZHSTD   , PGAMMA,  PTHETA , PSIG,&
   &  PAPRS    , PAPRSF  , ZGEOM1 , PT     , PU,&
   &  PV,&
   ! OUTPUTS
   &  Z_PUSTRG5  , Z_PVSTRG5 , Z_PVDISG5,&
   &  ZTENV55C , ZTENU55C, ZTENT55C,&
   &  Z_PUSTRG   , Z_PVSTRG  , Z_PVDISG , PTENV  , PTENU  , PTENT,&
   ! FLUX OUTPUTS
   &  Z_PSTRDU5  , Z_PSTRDV5,&
   & Z_PSTRDU   , Z_PSTRDV                                )  

ENDIF

!     ------------------------------------------------------------------

!*         3.     VERTICAL EXCHANGE OF U,V,T,Q BY TURBULENCE
!                 ------------------------------------------

!*         3.1     SETUP INPUT FLUXES TO ZERO.

DO JL=KIDIA,KFDIA
  Z_PVDIS (JL)=0.0_JPRB
  Z_PFTLHEV (JL)=0.0_JPRB
  Z_PFTLHSB (JL)=0.0_JPRB
  Z_PFWSB (JL)=0.0_JPRB
ENDDO

IF ( LEVDIF2 ) THEN

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTPROC (JL,JK) = ZTPROC (JL,JK) + PTENT (JL,JK)
      ZQPROC (JL,JK) = ZQPROC (JL,JK) + PTENQ (JL,JK)
      ZUPROC (JL,JK) = ZUPROC (JL,JK) + PTENU (JL,JK)
      ZVPROC (JL,JK) = ZVPROC (JL,JK) + PTENV (JL,JK)
    ENDDO
  ENDDO

  CALL VDFMAINSAD (YDVDF,YDMCC,YDMODEL%YRML_PHY_SLIN,YDEPHY,YDRIP, KIDIA,KFDIA,KLON,KLEV,KLEVS,KTILES,& 
   & ITRAC , TSPHY  , RVDIFTS ,ITVL  , ITVH  , ZCVL ,ZCVH ,&
   & ZLAIL, ZLAIH   , PSNS5 , PRSN5 , ZHSDFOR,&
   & PU5   , PV5    , PT5   , PQ5   , ZCEN5, PAPRS5, ZGEOM15, ZGEOMH5,&
   & PTL5  , PTSA5  , PWSA5,&
   & Z_PFRSO5(1,KLEV), Z_PFRTH5(1,KLEV), Z_PEMIS5,&
   & ZTHKICE5, ZSNTICE5,&
   & PTSN5 ,PTIA5(1,1),&
   ! & ZHLICE5 , ZTLICE5 , ZTLWML5 , & !lake passive
   & PSST  , ISOTY  , ZFRTI , ZALBTI5 , ZWLMX5,&
   & ZCHAR ,ZTSKRAD5,ZCFLX5,&
   ! OUTPUT (TRAJECTORY)
   & ZAZ0M55 , ZAZ0H55,&
   & PUCFL5, PVCFL5, PTCFL5, PDCFL5, PQCFL5, PU10N5, PV10N5,&
   & ZFRSOTI5, ZEVAPSNW5,&
   ! INPUT TENDENCIES (TRAJECTORY)           
   & ZTENT55B , ZTENQ55B , ZTENU55B , ZTENV55B ,&
   ! OUTPUT TENDENDCIES
   & ZTPROC5 , ZQPROC5 , ZUPROC5 , ZVPROC5 , ZTENC55B, ZTLE55,&
   !-UPDATED FIELDS FOR TILES (TRAJECTORY)
   & ZUSTRTI55 ,ZVSTRTI55 ,ZAHFSTI55,ZEVAPTI55,ZTSKTI55,&
   ! FLUX OUTPUTS (TRAJECTORY)
   & Z_PDIFTS5, Z_PDIFTQ5, Z_PSTRTU5 , Z_PSTRTV5,&
   !=========  
   & PU    , PV    , PT    , PQ   , ZCEN, PAPRS , ZGEOM1, ZGEOMH,&
   & PTL   , PTSA  , PWSA,&
   & Z_PFRSO(1,KLEV) , Z_PFRTH(1,KLEV),&
   & PTSN  , PTIA(1,1),&
   & ZALBTI ,ZTSKRAD,ZCFLX,&
   ! OUTPUT           
   & ZAZ0M , ZAZ0H,&
   & PUCFL, PVCFL, PTCFL, PDCFL, PQCFL, PU10N, PV10N,&
   & ZFRSOTI, ZEVAPSNW,&
   ! INPUT TENDENCIES           
   & PTENT  , PTENQ , PTENU , PTENV ,&
   ! OUTPUT TENDENCIES           
   & ZTPROC  , ZQPROC , ZUPROC , ZVPROC , ZTENC, PTLE1,&
   !-UPDATED FIELDS FOR TILES 
   & PUSTRTI , PVSTRTI , PAHFSTI , PEVAPTI , PTSKTI,&
   ! FLUX OUTPUTS 
   & PDIFTS , PDIFTQ  , PSTRTU  , PSTRTV  )  
     
ELSE
  DO JTILE=KTILES,1,-1
    DO JL=KIDIA,KFDIA
      Z_PFRSO(JL,KLEV) = Z_PFRSO(JL,KLEV)+ZFRSOTI(JL,JTILE)
      ZFRSOTI(JL,JTILE) = 0.0_JPRB
    ENDDO
  ENDDO
  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA
      PDIFTQ (JL,JK)=0.0_JPRB
      PDIFTS (JL,JK)=0.0_JPRB
      PSTRTU (JL,JK)=0.0_JPRB
      PSTRTV (JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
  PUCFL(:) = 0.0_JPRB
  PVCFL(:) = 0.0_JPRB
  PTCFL(:) = 0.0_JPRB
  PDCFL(:) = 0.0_JPRB
  PQCFL(:) = 0.0_JPRB
  PU10N(:) = 0.0_JPRB
  PV10N(:) = 0.0_JPRB
ENDIF


!     ------------------------------------------------------------------

!*         2.     RADIATION TRANSFER
!                 ------------------

ZRII0 =0.0_JPRB

IF ( LERADI2 ) THEN

!*         2.1  FULL RADIATION COMPUTATIONS
!*         2.5  RADIATIVE FLUXES AT EVERY TIME-STEP

  CALL RADHEATAD (&
   &   YDERDI,YDMODEL%YRML_PHY_SLIN%YREPHLI,YDPHNC,YDMODEL%YRML_PHY_MF%YRPHY3,  &
   &   KIDIA   , KFDIA   , KLON    , KLEV,&
   &   PAPRS5  , PAPRSF5,&
   &   Z_PEMIS5  , PEMTD5  , PMU0,&
   &   PQ5,&
   &   ZTENT55A , PT5     , PTRSO5  , ZTRSOD5 , PTL5,&
   &   Z_PNEB5  , Z_PQLI5 , Z_PQICE5,&
   &   Z_PFRSO5  ,Z_PFRTH5  , Z_PFRSOD5 , Z_PFRTHD5,&
   &   PAPRS   , PAPRSF,&
   &   Z_PEMTD,&
   &   PQ,&
   &   PTENT   , PT      , Z_PTRSO   , ZTRSOD  , PTL,&
   &   PII0,&
   &   Z_PFRSO   , Z_PFRTH   , Z_PFRSOD  , Z_PFRTHD )  

!*         2.4  CALL RADIATION AT EVERY GRID POINT
  PTL(KIDIA:KFDIA)=PTL(KIDIA:KFDIA)+ZTSKRAD(KIDIA:KFDIA)
  ZTSKRAD(KIDIA:KFDIA)=0.0_JPRB

  IF (LERADSW2 .OR. LERADN2) THEN

    IF (LERADN2 .AND. LERADLW2) THEN
      ISTEP = MOD(KSTEP,NLWFR)
      IF (ISTEP /= 0 .AND. KSTEP /= 0 .AND. KSTEP < (NSTOP-1)) THEN
        DO JK=0,KLEV
          DO JL=KIDIA,KFDIA
            PGPHIST%RADLWM (JL,JK) = PGPHIST%RADLWM (JL,JK)+Z_PEMTD(JL,JK)
            Z_PEMTD(JL,JK) = 0.0_JPRB
          ENDDO
        ENDDO
      ELSEIF(KSTEP /= NSTOP-1) THEN
        DO JK=0,KLEV
          DO JL=KIDIA,KFDIA
            Z_PEMTD(JL,JK) = Z_PEMTD(JL,JK)+PGPHIST%RADLWM(JL,JK)
            PGPHIST%RADLWM(JL,JK) = 0.0_JPRB
          ENDDO
        ENDDO      
      ELSE
        DO JK=0,KLEV
          DO JL=KIDIA,KFDIA
            PGPHIST%RADLWM(JL,JK) = 0.0_JPRB
          ENDDO
        ENDDO      
      ENDIF
    ENDIF

    CALL RADINAAD (&
     & YDMODEL, &
     &  KIDIA , KFDIA , KLON , KLEV   , NMODE,&
     &  ZALBD5, ZALBP5, PAPRS5 , PAPRSF5,&
     &  ZCCNL5, ZCCNO5, RCARDI , Z_PNEB5,&
     &  YDGSGEOM%GELAM, YDGSGEOM%GECLO, YDGSGEOM%GESLO,&
     &  PDELP5, ZEMIR5, ZEMIW5,&
     &  YDGSGEOM%GEMU , PMU0  ,  PQ5  , Z_PQSAT5,&
     &  Z_PQICE5, Z_PQLI5,&
     &  PLSM  , PT5   , PTL5,&
     &  ZTHT5 ,&
     &  ZALBD , ZALBP  , PAPRS  , PAPRSF ,&
     &  ZCCNL , ZCCNO  , ZCARDI , Z_PNEB ,&
     &  PDELP , ZEMIR  , ZEMIW  ,&
     &  PQ    , Z_PQSAT,&
     &  Z_PQICE , Z_PQLI,&
     &  PT    , PTL    ,&
     &  PII0,&
     &  Z_PEMTD , Z_PTRSO,&
     & ZTHT  , ZTRSOD)  
  ENDIF

!*         2.3 CLOUD
  IF (LEDCLD2 .AND. .NOT. LENCLD2) THEN
    CALL CLOUDAD ( YDMODEL%YRML_PHY_SLIN%YREPHLI,YDMODEL%YRML_PHY_EC%YRECLD, KIDIA    , KFDIA , KLON   , KLEV,&
     & IBASC1  , ITOPC1  ,&
     & PQ5     , Z_PQSAT5, PT5     , Z_PACPR15,&
     & Z_PNEB5 , Z_PTCC5 , Z_PQICE5, Z_PQLI5,&
     & PQ      , Z_PQSAT , PT      , PACPR  ,&
     & Z_PNEB  , Z_PTCC  , Z_PQICE , Z_PQLI  )  
  ELSEIF (LENCLD2) THEN
    CALL  CLDPPAD (&
     & YDERDI, &
     &  KIDIA , KFDIA    , KLON    , KLEV,&
     &  ZPA55   , ZPL55    , ZPI55   ,&
     &  Z_PNEB5 , Z_PQICE5 , Z_PQLI5 ,&
     &  ZPA     , ZPL      , ZPI     ,&
     & Z_PNEB  , Z_PQICE  , Z_PQLI  )      
    
    IF (KSTEP == KSTART) THEN
      DO JK=1,KLEV
        DO JL=KIDIA,KFDIA
          ZPA  (JL,JK) = 0.0_JPRB
          ZPL  (JL,JK) = 0.0_JPRB
          ZPI  (JL,JK) = 0.0_JPRB
        ENDDO
      ENDDO
    ELSE
      DO JK=KLEV,1,-1
        DO JL=KIDIA,KFDIA
          PI(JL,JK) = PI(JL,JK) + ZPI(JL,JK)  
          ZPI  (JL,JK) = 0.0_JPRB
          PL(JL,JK) = PL(JL,JK) + ZPL(JL,JK)  
          ZPL  (JL,JK) = 0.0_JPRB
          PA(JL,JK) = PA(JL,JK) + ZPA(JL,JK)
          ZPA  (JL,JK) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF

  ELSE
    Z_PNEB  (:,:)=0.0_JPRB
    Z_PQLI  (:,:)=0.0_JPRB
    Z_PQICE (:,:)=0.0_JPRB
    Z_PTCC  (:)  =0.0_JPRB
  ENDIF

  CALL SATURAD (KIDIA , KFDIA , KLON  , KTDIA , KLEV,&
   & PAPRSF5, PT5    , Z_PQSAT5,&
   & PAPRSF , PT     , Z_PQSAT  )  

ELSE
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      Z_PNEB (JL,JK)=0.0_JPRB
      Z_PQLI (JL,JK)=0.0_JPRB
      Z_PQICE (JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
  DO JL=KIDIA,KFDIA
    Z_PFRSOC (JL,0)=0.0_JPRB
    Z_PFRTHC (JL,0)=0.0_JPRB
    Z_PFRSOC (JL,1)=0.0_JPRB
    Z_PFRTHC (JL,1)=0.0_JPRB
    Z_PFRSOD (JL)=0.0_JPRB
    Z_PFRTHD (JL)=0.0_JPRB
  ENDDO
  DO JK=0,KLEV
    DO JL=KIDIA,KFDIA
      Z_PFRSO (JL,JK)=0.0_JPRB
      Z_PFRTH (JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
  PTL(KIDIA:KFDIA)=PTL(KIDIA:KFDIA)+ZTSKRAD(KIDIA:KFDIA)
  ZTSKRAD(KIDIA:KFDIA)=0.0_JPRB
ENDIF

!*         1.     INITIAL COMPUTATIONS.
!                 ---------------------

!*       1.6  SET SURFACE SOLAR TRANSMISSIVITY
Z_PSRSWD (:) = 0.0_JPRB
DO JL=KIDIA,KFDIA
  Z_PSRSWD(JL) = Z_PSRSWD(JL) + ZTRSOD (JL)
  ZTRSOD (JL) = 0.0_JPRB
ENDDO

!*       1.5  SET FLUXES TO ZERO
DO JK=0,KLEV
  DO JL=KIDIA,KFDIA
    Z_PFCCQL (JL,JK) = 0.0_JPRB
    Z_PFCCQN (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!*       1.4  SET SURFACE TENDENCIES TO ZERO
DO JK=1,KLEVS
  DO JL=KIDIA,KFDIA
    PTSAE1 (JL,JK)=0.0_JPRB
    Z_PWSAE1 (JL,JK)=0.0_JPRB
    PTIAE1 (JL,JK)  =0.0_JPRB
  ENDDO
ENDDO
DO JL=KIDIA,KFDIA
  PTLE1 (JL)=0.0_JPRB
  Z_PWLE1 (JL)=0.0_JPRB
  Z_PSNSE1 (JL)=0.0_JPRB
  PTSNE1(JL)   =0.0_JPRB
ENDDO

!*      1.3   GLOBAL AUXILLIARY VARIABLES

DO JK=0,KLEV
  DO JL=KIDIA,KFDIA
    PAPHI (JL,JK) = PAPHI (JL,JK) + ZGEOMH (JL,JK)
    ZGEOMH (JL,JK)=0.0_JPRB
  ENDDO
ENDDO
DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    PAPHIF (JL,JK) = PAPHIF (JL,JK) + ZGEOM1 (JL,JK)
    ZGEOM1 (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!*       1.2   CHANGE UNITS/SCALINGS
DO JL=KIDIA,KFDIA
  PLZ0H(JL) = PLZ0H(JL) + ZAZ0H (JL)*EXP(ZLZ0H55(JL))
  ZAZ0H (JL)  = 0.0_JPRB
  PGZ0M (JL) = PGZ0M (JL) + ZAZ0M (JL)/RG
  ZAZ0M (JL)  = 0.0_JPRB
ENDDO

!*       1.1   FIRST TIME-STEP
IF (KSTEP == KSTART) THEN
  DO JL=KIDIA,KFDIA
    PACCPR (JL)=0.0_JPRB
    PACPR (JL)=0.0_JPRB
    PGZ0M (JL)=0.0_JPRB
    PLZ0H (JL)=0.0_JPRB
  ENDDO
ENDIF

IF (NGEMS > 0 .AND. LTRACLNPH) THEN
  CALL GEMS_TEND_AD( YGFL, YDPHY2, KIDIA, KFDIA, KLEV, KLON,&
    & ZLRCH4, PGFL, PTENGFL, ZCEN, ZTENC )
ENDIF

DEALLOCATE( ZTENC55D )
DEALLOCATE( ZTENC55B )

DEALLOCATE(ZCEN5,ZTENC5,ZCFLX5,ZSCAV5,ZTENC5_SKF)
DEALLOCATE(ZCEN,ZTENC,ZCFLX)

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CALLPARAD',1,ZHOOK_HANDLE)
END SUBROUTINE CALLPARAD
