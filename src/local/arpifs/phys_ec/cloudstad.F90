SUBROUTINE CLOUDSTAD ( YDML_PHY_SLIN,YDECLD,YDECLDP,KIDIA , KFDIA , KLON , KTDIA , KLEV, LDRAIN1D,&
 & PTSPHY,&
 & PAPHP15 , PAPP15  , PQM15   , PQS5   , PTM15,&
 & PLUDE5  , PLU5,&
 & PTENT5  , PGTENT5 , PTENQ5 , PGTENQ5 ,&
 & PCLC5   , PQIWP5  , PQLWP5 ,PFPLSL5 , PFPLSN5,&
 & PFHPSL5 , PFHPSN5, PCOVPTOT5, &
 & PAPHP1 ,  PAPP1   , PQM1   , PQS   , PTM1,&
 & PLUDE  , PLU,&
 & PTENT  , PGTENT , PTENQ , PGTENQ,&
 & PCLC   , PQIWP  , PQLWP ,PFPLSL , PFPLSN,&
 & PFHPSL , PFHPSN, PCOVPTOT )  

!**** *CLOUDSTTL*  - COMPUTES CLOUD COVER AND LIQUID WATER PATH
!                    AND LARGE-SCALE CONDENSATION
!                    (adjoint)

!     PURPOSE.
!     --------
!         THIS ROUTINE COMPUTES CLOUD AMOUNTS WHICH ARE REQUIRED BY THE
!     RADIATION SCHEME FOR COMPUTATION OF THE FLUXES AND HE PHYSICAL
!     TENDENCIES OF THE TWO PROGNOSTIC VARIABLES T AND Q DUE TO
!     THE WATER PHASE CHANGES

!**   INTERFACE.
!     ----------
!              *CLOUDSTAD* IS CALLED FROM *CALLPARAD*

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----

!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KTDIA*        START OF THE VERTICAL LOOP
!    *KLEV*         NUMBER OF LEVELS

!    INPUT PARAMETERS (REAL):

!    *PTSPHY*       TIME STEP FOR THE PHYSICS                     S
!    *PQM15*        SPECIFIC HUMIDITY AT T-1                  (Trajectory)
!    *PQS5*         SATURATION SPECIFIC HUMIDITY              (Trajectory)
!    *PTM15*        TEMPERATURE AT T-1                        (Trajectory)
!    *PAPP15*       PROVISIONAL PRESSURE ON FULL LEVELS       (Trajectory)
!    *PAPHP15*      PRESSURE AT HALF LEVELS AT T-1            (Trajectory)
!    *PLUDE5*       DETRAINED LIQUID WATER                    (Trajectory)
!    *PLU5*         LIQUID WATER CONTENT IN UPDRAFTS          (Trajectory)
!    *PQM1*         SPECIFIC HUMIDITY AT T-1                     KG/KG
!    *PQS*          SATURATION SPECIFIC HUMIDITY                 KG/KG
!    *PTM1*         TEMPERATURE AT T-1                             K
!    *PAPHP1*       PRESSURE AT HALF LEVELS AT T-1                PA   
!    *PAPP1*        PROVISIONAL PRESSURE ON FULL LEVELS           PA
!    *PLUDE*        DETRAINED LIQUID WATER                       KG/(M3*S)
!    *PLU*          LIQUID WATER CONTENT IN UPDRAFTS             KG/KG

!    UPDATED PARAMETERS (REAL):
!    *PTENT5*       TEMPERATURE TENDENCY                      (Trajectory)
!    *PTENQ5*       MOISTURE TENDENCY                         (Trajectory)
!    *PTENT*        TEMPERATURE TENDENCY                          K/S
!    *PTENQ*        MOISTURE TENDENCY                            KG/(KG S)

!    OUTPUT PARAMETERS (REAL):

!    *PCLC5*        CLOUD COVER OF INDIVIDUAL LAYER
!    *PQIWP5*       ICE WATER CONTENT                         (Trajectory)    
!    *PQLWP5*       LIQUID WATER CONTENT                      (Trajectory)
!    *PFHPSL5*      ENTHALPY FLUX DUE TO LARGE SCALE RAIN     (Trajectory) 
!    *PFHPSN5*      ENTHALPY FLUX DUE TO LARGE SCALE SNOW     (Trajectory) 
!    *PFPLSL5*      LARGE SCALE RAIN FLUX                     (Trajectory)
!    *PFPLSN5*      LARGE SCALE SNOW FLUX                     (Trajectory)  
!    *PCLC*         CLOUD COVER OF INDIVIDUAL LAYER
!    *PQIWP*        ICE WATER CONTENT                             KG/KG
!    *PQLWP*        LIQUID WATER CONTENT                          KG/KG
!    *PFHPSL*       ENTHALPY FLUX DUE TO LARGE SCALE RAIN         J/(M2*S)
!    *PFHPSN*       ENTHALPY FLUX DUE TO LARGE SCALE SNOW         J/(M2*S)
!    *PFPLSL*       LARGE SCALE RAIN FLUX                        KG/(M2*S)
!    *PFPLSN*       LARGE SCALE SNOW FLUX                        KG/(M2*S)

!    *PCOVPTOT*     PRECIPITATION FRACTION IN EACH LAYER

!     METHOD.
!     -------

!     EXTERNALS.
!     ----------

!          NONE

!     REFERENCE.
!     ----------

!     AUTHOR.
!     -------
!     2002-10-07  M. Janiskova

!     MODIFICATIONS.
!     --------------
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Janiskova   13-Apr-2005 modified regularization
!        P.Lopez       21-Mar-2007 modified regularization of PCLC
!        P.Lopez       22-Oct-2007 Retuning of autoconversion coefficient
!        A.Geer        01-Oct-2008 LDRAIN1D name change to reflect usage
!        A. Geer       13-Apr-2010 Output 2D precip fraction for all-sky radiances
!        M. Janiskova  11-Mar-2011 Cleaning and simplification of AG modification

!-----------------------------------------------------------------------

USE MODEL_PHYSICS_SIMPLINEAR_MOD , ONLY : MODEL_PHYSICS_SIMPLINEAR_TYPE
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RETV, RG  ,RCPD     ,&
 &                    RLVTT    ,RLSTT    ,RLMLT    ,RTT  , YRCST
USE YOETHF   , ONLY : R2ES     ,R3LES    ,R3IES    ,R4LES    ,&
 &                    R4IES    ,R5LES    ,R5IES    ,R5ALVCP  ,R5ALSCP  ,&
 &                    RALVDCP  ,RALSDCP  ,RTWAT    ,RTICE    ,RTICECU   ,&
 &                    RTWAT_RTICE_R      ,RTWAT_RTICECU_R    ,RVTMP2 , YRTHF
USE YOECLD   , ONLY : TECLD
USE YOECLDP  , ONLY : TECLDP

IMPLICIT NONE

TYPE(TECLD)       ,INTENT(INOUT) :: YDECLD
TYPE(TECLDP)      ,INTENT(INOUT) :: YDECLDP
TYPE(MODEL_PHYSICS_SIMPLINEAR_TYPE),INTENT(INOUT):: YDML_PHY_SLIN
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
LOGICAL           ,INTENT(IN)    :: LDRAIN1D
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSPHY 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHP15(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPP15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM15(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLUDE5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLU5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENQ5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCLC5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQIWP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQLWP5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSL5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSN5(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOVPTOT5(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPHP1(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPP1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQS(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLUDE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLU(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCLC(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQIWP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQLWP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFPLSN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFHPSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PFHPSN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCOVPTOT(KLON,KLEV) 
!     -----------------------------------------------------------------

!*       0.1   ARGUMENTS.
!              ----------

!     -----------------------------------------------------------------

!*       0.2   LOCAL ARRAYS.
!              -------------

INTEGER(KIND=JPIM) :: JK, JL

!=======================================================================
!     TUNABLE CONSTANTS (to be moved to include files later)
!=======================================================================

! zscal is a scale factor that linearly reduces the variance between 
! qv=qv-crit and qv-qsat
! 0 = No scaling
! 1 = full scaling (i.e. variance=0 when qv=qsat)

REAL(KIND=JPRB) :: ZSCAL=0.9_JPRB

!=======================================================================

REAL(KIND=JPRB) :: ZTP15(KLON,KLEV), ZQP15(KLON,KLEV)
REAL(KIND=JPRB) :: ZLUDE5(KLON,KLEV), ZQLWC5(KLON,KLEV)
REAL(KIND=JPRB) :: ZDP5(KLON,KLEV)
REAL(KIND=JPRB) :: ZLSDCP5(KLON,KLEV), ZLFDCP5(KLON,KLEV), ZLVDCP5(KLON,KLEV)
REAL(KIND=JPRB) :: ZFWAT5(KLON,KLEV), ZFWATR15(KLON,KLEV), ZFWATR25(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFREEZE15(KLON,KLEV), ZRFREEZE35(KLON,KLEV)

REAL(KIND=JPRB) :: ZTP1(KLON,KLEV), ZQP1(KLON,KLEV)
REAL(KIND=JPRB) :: ZLUDE(KLON,KLEV), ZQLWC(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFL(KLON), ZSFL(KLON), ZDP(KLON,KLEV), ZDR(KLON)
REAL(KIND=JPRB) :: ZLSDCP(KLON,KLEV), ZLFDCP(KLON,KLEV), ZLVDCP(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN(KLON), ZSFLN(KLON),ZGDP(KLON)
REAL(KIND=JPRB) :: ZFWAT(KLON,KLEV), ZDQDT(KLON), ZDTDT(KLON)
REAL(KIND=JPRB) :: ZRFREEZE(KLON,KLEV)
REAL(KIND=JPRB) :: ZQCRIT(KLON), ZCOVPTOT(KLON), ZCOVPCLR(KLON)
REAL(KIND=JPRB) :: ZDQSDTEMP(KLON), ZCORQS(KLON), ZDTGDP(KLON)
REAL(KIND=JPRB) :: ZQOLD(KLON),ZPP(KLON),ZDQ(KLON), ZQLIM(KLON)

REAL(KIND=JPRB) :: ZCRH(KLEV), ZSCALM(KLEV)

REAL(KIND=JPRB) :: ZZZ5(KLON,KLEV), ZCLD5(KLON,KLEV)
REAL(KIND=JPRB) :: ZLNEW5(KLON,KLEV), ZPR5(KLON,KLEV), ZD5(KLON,KLEV)
REAL(KIND=JPRB) :: ZCONS5(KLON,KLEV), ZSNMLT5(KLON,KLEV), ZZ2S5(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN5(KLON,KLEV), ZSFLN5(KLON,KLEV),ZGDP5(KLON,KLEV)
REAL(KIND=JPRB) :: ZDQDT5(KLON,KLEV), ZDTDT5(KLON,KLEV), ZQCRIT5(KLON,KLEV)
REAL(KIND=JPRB) :: ZDR15(KLON,KLEV), ZDR25(KLON,KLEV)
REAL(KIND=JPRB) :: ZTP25(KLON,KLEV), ZTP35(KLON,KLEV),ZQP25(KLON,KLEV)
REAL(KIND=JPRB) :: ZTPB5(KLON,KLEV), ZQPB5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQLWC15(KLON,KLEV), ZQLWC25(KLON,KLEV), ZCLC5(KLON,KLEV)

! check dimension

REAL(KIND=JPRB) :: ZOEALFAW5(KLON,KLEV), ZALFAW5(KLON,KLEV)
REAL(KIND=JPRB) :: ZCOVPTOT15(KLON,KLEV), ZFOEEW5 (KLON,KLEV)
REAL(KIND=JPRB) :: ZCOVPCLR5(KLON,KLEV), ZCOVPCLR15(KLON,KLEV)
REAL(KIND=JPRB) :: ZESDP5(KLON,KLEV), ZESDP15(KLON,KLEV), ZFAC5(KLON,KLEV)
REAL(KIND=JPRB) :: ZFACW5(KLON,KLEV), ZFACI5(KLON,KLEV), ZCOR5(KLON,KLEV)
REAL(KIND=JPRB) :: ZDQSDTEMP5(KLON,KLEV), ZCORQS5(KLON,KLEV)
REAL(KIND=JPRB) :: ZPRTOT5(KLON,KLEV), ZQE5 (KLON,KLEV), ZB5(KLON,KLEV)
REAL(KIND=JPRB) :: ZPRECLR5(KLON,KLEV), ZPRECLR15(KLON,KLEV) 
REAL(KIND=JPRB) :: ZBETA5(KLON,KLEV), ZDTGDP5(KLON,KLEV)  
REAL(KIND=JPRB) :: ZDPR5(KLON,KLEV), ZDPR15(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN15(KLON,KLEV), ZSFLN15(KLON,KLEV)
REAL(KIND=JPRB) :: ZRFLN25(KLON,KLEV), ZSFLN25(KLON,KLEV)
REAL(KIND=JPRB) :: ZDPRSFL5(KLON,KLEV), ZDPSSFL5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQOLD5(KLON,KLEV), ZPP5(KLON)
REAL(KIND=JPRB) :: ZDQ5(KLON,KLEV), ZQLIM5(KLON,KLEV)
REAL(KIND=JPRB) :: ZQPD5(KLON,KLEV), ZQCD5(KLON,KLEV)
REAL(KIND=JPRB) :: ZSQRT5(KLON,KLEV)

REAL(KIND=JPRB) :: ZRFL5(KLON,KLEV+1), ZSFL5(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZCOVPTOT5(KLON,0:KLEV)

REAL(KIND=JPRB) :: ZRN5, ZSN5, ZXX5, ZOEALFA5, ZRFREEZE25

REAL(KIND=JPRB) :: ZOEALFA, ZCLD, ZLNEW, ZPR, ZLCRIT, ZD, ZZ2S
REAL(KIND=JPRB) :: ZCONS, ZSNMLT, ZZZ, ZRN, ZSN
REAL(KIND=JPRB) :: ZOEALFAW, ZALFAW, ZFAC, ZFACW, ZFACI, ZESDP, ZCOR
REAL(KIND=JPRB) :: ZFOEEW, ZPRTOT, ZDPR, ZDPRSFL, ZDPSSFL, ZPRECLR
REAL(KIND=JPRB) :: ZQE, ZBETA, ZB, ZQPD, ZQCD, ZFWATR, ZRFREEZE2

REAL(KIND=JPRB) :: ZCKCODT, ZCONS2, ZCONS3, ZMELTP2, Z3ES, Z4ES, ZQMAX
REAL(KIND=JPRB) :: ZEPS1,ZEPS2,ZHUCOE,ZHUTIL,ZCKCODTA

REAL(KIND=JPRB) :: ZYYY, ZGAMMA

INTEGER(KIND=JPIM) :: INPCLO1,INPCLO2,IK,ICALL

LOGICAL :: LLO1, LLO2, LLFLAG(KLON)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "cuadjtqs.intfb.h"
#include "cuadjtqsad.intfb.h"

!     ------------------------------------------------------------------
#include "fcttre.func.h"
!     ------------------------------------------------------------------

!*         1.     SET-UP INPUT QUANTITIES
!                 -----------------------

!*         1.1    Set-up tunning parameters

IF (LHOOK) CALL DR_HOOK('CLOUDSTAD',0,ZHOOK_HANDLE)
ASSOCIATE(CETA=>YDECLD%CETA, &
 & RCLCRIT=>YDECLDP%RCLCRIT, RKCONV=>YDECLDP%RKCONV, RLMIN=>YDECLDP%RLMIN, &
 & RPECONS=>YDECLDP%RPECONS, &
 & RLPTRC=>YDML_PHY_SLIN%YREPHLI%RLPTRC, &
 & LREGCL=>YDML_PHY_SLIN%YRNCL%LREGCL, &
 & LEVAPLS2=>YDML_PHY_SLIN%YRPHNC%LEVAPLS2)
ZHUCOE=0.7_JPRB
ZHUTIL=0.9_JPRB

INPCLO1=1
INPCLO2=1

! set up constants required

!LLL ZCKCODT=3._JPRB*RKCONV*PTSPHY
ZCKCODT=2.0_JPRB*RKCONV*PTSPHY
ZCKCODTA=ZCKCODT/100._JPRB
ZCONS2 =1.0_JPRB/(PTSPHY*RG)
ZCONS3 =2.5E6_JPRB/1005._JPRB
ZMELTP2=RTT+2.0_JPRB

ZQMAX=0.5_JPRB
ZEPS1=1.E-12_JPRB
ZEPS2=1.E-10_JPRB

ZGAMMA=0.1_JPRB/SQRT(1.0_JPRB-0.95_JPRB)

!     --------------------------------------------------------------------

!*         2.1    COMPUTE CRITICAL RELATIVE HUMIDITY AND RELATIVE HUMIDITY
!                 --------------------------------------------------------

DO JK=1,KLEV

! Critical relative humidity

  ZCRH(JK) = 0.85_JPRB-MAX( ZHUCOE*CETA(JK)**INPCLO1*&
   & (1.0_JPRB-CETA(JK))**INPCLO2*&
   & (1.85_JPRB+SQRT(ZHUTIL)*(CETA(JK)-0.5_JPRB)),ZEPS1)  
  ZSCALM(JK)=ZSCAL*MAX((CETA(JK)-0.2_JPRB),ZEPS1)**(0.2_JPRB)

  DO JL=KIDIA,KFDIA

! thermodynamic constants

    ZDP5(JL,JK) = PAPHP15(JL,JK+1)-PAPHP15(JL,JK)
    ZZZ5(JL,JK) =1.0_JPRB/(RCPD+RCPD*RVTMP2*PQM15(JL,JK))
    ZLFDCP5(JL,JK) = RLMLT*ZZZ5(JL,JK)
    ZLSDCP5(JL,JK) = RLSTT*ZZZ5(JL,JK)
    ZLVDCP5(JL,JK) = RLVTT*ZZZ5(JL,JK)
    LLFLAG(JL)=.TRUE.
  ENDDO
ENDDO

! first guess values for T and q

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZTP15(JL,JK) = PTM15(JL,JK) + PTSPHY*PGTENT5(JL,JK)
    ZQP15(JL,JK) = PQM15(JL,JK) + PTSPHY*PGTENQ5(JL,JK)
    ZTP25(JL,JK) = ZTP15(JL,JK)
    ZTP35(JL,JK) = ZTP15(JL,JK)
    ZQP25(JL,JK) = ZQP15(JL,JK)
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!*         2.2    INITIALIZATION OF CLOUD AND PRECIPITATION ARRAYS
!                 ------------------------------------------------

!       Clear cloud and freezing arrays

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PCLC5(JL,JK) = 0.0_JPRB
    PQIWP5(JL,JK) = 0.0_JPRB
    PQLWP5(JL,JK) = 0.0_JPRB
    ZQLWC5(JL,JK) = 0.0_JPRB
    ZRFREEZE15(JL,JK)=0.0_JPRB
    ZRFREEZE35(JL,JK)=0.0_JPRB
    PCOVPTOT5(JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!       Set to zero precipitation fluxes at the top

DO JL=KIDIA,KFDIA
  ZRFL5(JL,1) = 0.0_JPRB
  ZSFL5(JL,1) = 0.0_JPRB
  PFPLSL5(JL,1) = 0.0_JPRB
  PFPLSN5(JL,1) = 0.0_JPRB
  ZCOVPTOT5(JL,0) = 0.0_JPRB
  ZCOVPCLR5(JL,1) = 0.0_JPRB
ENDDO

!     ------------------------------------------------------------------

!*        3. COMPUTE LAYER CLOUD AMOUNTS
!            ---------------------------

! Large loop over KLEV 
! Calculates 
!   1. diagnostic CC and QL
!   2. Convective CC and QL
!   3. Rainfall 

DO JK=KTDIA,KLEV

!       3.1   INITIALIZATION

  DO JL=KIDIA,KFDIA

    ZDR15(JL,JK) = 0.0_JPRB
    ZDR25(JL,JK) = 0.0_JPRB

!-----------------------------------
! calculate dqs/dT correction factor
!-----------------------------------

    ZOEALFAW5(JL,JK) = 0.545_JPRB &
     & * (TANH(0.17_JPRB*(ZTP25(JL,JK)-RLPTRC))+1.0_JPRB)  
    IF (ZTP25(JL,JK) < RTT) THEN
      ZALFAW5(JL,JK) = ZOEALFAW5(JL,JK)
      Z3ES=R3IES
      Z4ES=R4IES
    ELSE
      ZALFAW5(JL,JK) = 1.0_JPRB
      Z3ES=R3LES
      Z4ES=R4LES
    ENDIF
    ZFOEEW5(JL,JK) = R2ES*EXP(Z3ES*(ZTP25(JL,JK)-RTT)&
     & / (ZTP25(JL,JK)-Z4ES))  
    ZESDP15(JL,JK) = ZFOEEW5(JL,JK)/PAPP15(JL,JK)
    ZESDP5(JL,JK)  = ZESDP15(JL,JK)
    IF (ZESDP15(JL,JK) > ZQMAX) THEN
      ZESDP5(JL,JK) = ZQMAX
    ENDIF
    
    ZFACW5(JL,JK) = R5LES/((ZTP25(JL,JK)-R4LES)**2)
    ZFACI5(JL,JK) = R5IES/((ZTP25(JL,JK)-R4IES)**2)
    ZFAC5 (JL,JK) = ZALFAW5(JL,JK)*ZFACW5(JL,JK) &
     & + (1.0_JPRB-ZALFAW5(JL,JK))*ZFACI5(JL,JK)  
    ZCOR5 (JL,JK) = 1.0_JPRB/(1.0_JPRB-RETV*ZESDP5(JL,JK))
    ZDQSDTEMP5(JL,JK) = ZFAC5(JL,JK)*ZCOR5(JL,JK)*PQS5(JL,JK)
    ZCORQS5(JL,JK) = 1.0_JPRB+ZCONS3*ZDQSDTEMP5(JL,JK)

! use clipped state

    IF (ZQP25(JL,JK) > PQS5(JL,JK)) THEN
      ZQLIM5(JL,JK) = PQS5(JL,JK)
    ELSE
      ZQLIM5(JL,JK) = ZQP25(JL,JK)
    ENDIF

! set up critical value of humidity

    ZQCRIT5(JL,JK) = ZCRH(JK)*PQS5(JL,JK)
  ENDDO

! Replace BETA distribution with simple UNIFORM from Letreut & Li (90)

  DO JL=KIDIA,KFDIA
    IF (ZQP25(JL,JK) <= ZQCRIT5(JL,JK)) THEN
      ZCLC5(JL,JK) = 0.0_JPRB
      ZQLWC15(JL,JK) = 0.0_JPRB
    ELSEIF (ZQP25(JL,JK) >= PQS5(JL,JK)) THEN
      ZCLC5(JL,JK) = 1.0_JPRB
      ZQLWC15(JL,JK) = (1.0_JPRB-ZSCALM(JK))*(PQS5(JL,JK)-ZQCRIT5(JL,JK))
    ELSE
      ZQPD5(JL,JK) = PQS5(JL,JK)-ZQP25(JL,JK)
      ZQCD5(JL,JK) = PQS5(JL,JK)-ZQCRIT5(JL,JK)
      ZSQRT5(JL,JK) = SQRT(ZQPD5(JL,JK)/(ZQCD5(JL,JK)-ZSCALM(JK) &
       & *(ZQP25(JL,JK)-ZQCRIT5(JL,JK))))    
      ZCLC5(JL,JK) = 1.0_JPRB-ZSQRT5(JL,JK)
      ZQLWC15(JL,JK) = (ZSCALM(JK)*ZQPD5(JL,JK) &
       & + (1.0_JPRB-ZSCALM(JK))*ZQCD5(JL,JK)) &
       & * ZCLC5(JL,JK)**2  
    ENDIF
  ENDDO

! Add convective component

!DEC$ IVDEP
  DO JL=KIDIA,KFDIA

    ZGDP5(JL,JK) = RG/(PAPHP15(JL,JK+1)-PAPHP15(JL,JK))

    ZLUDE5(JL,JK) = PLUDE5(JL,JK)*PTSPHY*ZGDP5(JL,JK)
    IF (JK<KLEV) THEN
      LLO1=ZLUDE5(JL,JK)>=RLMIN.AND.PLU5(JL,JK+1)>=ZEPS2
    ELSE
      LLO1=.FALSE.
    ENDIF
    IF (LLO1) THEN
      PCLC5(JL,JK) = ZCLC5(JL,JK) &
       & + (1.0_JPRB-ZCLC5(JL,JK)) &
       & * (1.0_JPRB-EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1)))  
      ZQLWC25(JL,JK) = ZQLWC15(JL,JK)+ZLUDE5(JL,JK)
    ELSE
      PCLC5(JL,JK) = ZCLC5(JL,JK)
      ZQLWC25(JL,JK) = ZQLWC15(JL,JK)
    ENDIF
  ENDDO

! Calculate precipitation overlap. 
! Simple form based on Maximum Overlap.
  DO JL=KIDIA,KFDIA
    IF (PCLC5(JL,JK) > ZCOVPTOT5(JL,JK-1)) THEN ! total rain frac
      ZCOVPTOT15(JL,JK) = PCLC5(JL,JK)
    ELSE
      ZCOVPTOT15(JL,JK) = ZCOVPTOT5(JL,JK-1)
    ENDIF
    ZCOVPTOT5(JL,JK) = ZCOVPTOT15(JL,JK)

    ZCOVPCLR15(JL,JK) = ZCOVPTOT5(JL,JK)-PCLC5(JL,JK)
    ZCOVPCLR5(JL,JK) = ZCOVPCLR15(JL,JK)
    IF (ZCOVPCLR15(JL,JK) < 0.0_JPRB) THEN
      ZCOVPCLR5(JL,JK) = 0.0_JPRB
    ENDIF
  ENDDO

!*         3.3    CALCULATE PRECIPITATION

!    Melting of incoming snow

  DO JL=KIDIA,KFDIA
    IF (ZSFL5(JL,JK) /= 0.0_JPRB) THEN
      ZCONS5(JL,JK) = ZCONS2*ZDP5(JL,JK)/ZLFDCP5(JL,JK)
      IF ((ZTP25(JL,JK)-ZMELTP2) > 0.0_JPRB) THEN
        ZZ2S5(JL,JK) = ZCONS5(JL,JK)*(ZTP25(JL,JK)-ZMELTP2)
      ELSE
        ZZ2S5(JL,JK) = 0.0_JPRB
      ENDIF
      IF (ZSFL5(JL,JK) <= ZZ2S5(JL,JK)) THEN
        ZSNMLT5(JL,JK) = ZSFL5(JL,JK)
      ELSE
        ZSNMLT5(JL,JK) = ZZ2S5(JL,JK)
      ENDIF
      ZRFLN5(JL,JK) = ZRFL5(JL,JK)+ZSNMLT5(JL,JK)
      ZSFLN5(JL,JK) = ZSFL5(JL,JK)-ZSNMLT5(JL,JK)
      ZTP15(JL,JK) = ZTP25(JL,JK)-ZSNMLT5(JL,JK)/ZCONS5(JL,JK)
    ELSE
      ZRFLN5(JL,JK) = ZRFL5(JL,JK)
      ZSFLN5(JL,JK) = ZSFL5(JL,JK)
    ENDIF
  ENDDO

!    Diagnostic calculation of rain production

!DEC$ IVDEP
  DO JL=KIDIA,KFDIA
    IF (PCLC5(JL,JK) > ZEPS2) THEN
      IF (LEVAPLS2 .OR. LDRAIN1D) THEN
        ZLCRIT = RCLCRIT
      ELSE
        ZLCRIT = RCLCRIT*2._JPRB
      ENDIF
      ZCLD5(JL,JK) = ZQLWC25(JL,JK)/PCLC5(JL,JK)    ! in-cloud liquid 
      ZD5(JL,JK) = ZCKCODT*(1.0_JPRB-EXP(-(ZCLD5(JL,JK)/ZLCRIT)**2))
      ZLNEW5(JL,JK) = PCLC5(JL,JK)*ZCLD5(JL,JK)*EXP(-ZD5(JL,JK))
      ZPR5(JL,JK) = ZQLWC25(JL,JK)-ZLNEW5(JL,JK)
      ZQLWC5(JL,JK) = ZQLWC25(JL,JK)-ZPR5(JL,JK)
    ELSE
      ZPR5(JL,JK) = 0.0_JPRB
      ZQLWC5(JL,JK) = ZQLWC25(JL,JK)
    ENDIF

!    New precipitation

    ZDR15(JL,JK) = ZDR15(JL,JK)+ZCONS2*ZDP5(JL,JK)*ZPR5(JL,JK)

    ZOEALFA5 = 0.545_JPRB &
     & *(TANH(0.17_JPRB*(ZTP15(JL,JK)-RLPTRC))+1.0_JPRB)  
    IF (ZTP15(JL,JK) < RTT) THEN
      ZFWAT5(JL,JK) = ZOEALFA5
    ELSE
      ZFWAT5(JL,JK) =1.0_JPRB
    ENDIF

! Rain fraction (different from cloud liquid water fraction!)
    IF (ZTP15(JL,JK) < RTT) THEN
      ZRFREEZE15(JL,JK)=ZFWAT5(JL,JK)*ZDR15(JL,JK)
      ZFWATR15(JL,JK)=0.0_JPRB
    ELSE
      ZFWATR15(JL,JK)=1.0_JPRB
    ENDIF 

    ZRN5 = ZFWATR15(JL,JK)*ZDR15(JL,JK)
    ZSN5 = (1.0_JPRB-ZFWATR15(JL,JK))*ZDR15(JL,JK)
    ZRFLN5(JL,JK) = ZRFLN5(JL,JK)+ZRN5
    ZSFLN5(JL,JK) = ZSFLN5(JL,JK)+ZSN5

    ZRFLN15(JL,JK) = ZRFLN5(JL,JK)
    ZSFLN15(JL,JK) = ZSFLN5(JL,JK)
    ZRFLN25(JL,JK) = ZRFLN5(JL,JK)
    ZSFLN25(JL,JK) = ZSFLN5(JL,JK)

!   Precip evaporation

    ZPRTOT5(JL,JK) = ZRFLN5(JL,JK)+ZSFLN5(JL,JK)
    LLO2=ZPRTOT5(JL,JK)>ZEPS2 .AND. ZCOVPCLR5(JL,JK)>ZEPS2 &
     & .AND. (LEVAPLS2 .OR. LDRAIN1D)

    IF (LLO2) THEN
      ZPRECLR15(JL,JK) = ZPRTOT5(JL,JK)*ZCOVPCLR5(JL,JK) &
       & / ZCOVPTOT15(JL,JK)

!     This is the humidity in the moistest zcovpclr region

      ZQE5(JL,JK) = PQS5(JL,JK)-(PQS5(JL,JK)-ZQLIM5(JL,JK)) &
       & * ZCOVPCLR5(JL,JK) / (1.0_JPRB-PCLC5(JL,JK))**2  
      ZBETA5(JL,JK) = RG*RPECONS*(SQRT(PAPP15(JL,JK) &
       & / PAPHP15(JL,KLEV+1))/5.09E-3_JPRB*ZPRECLR15(JL,JK) &
       & / ZCOVPCLR5(JL,JK))**0.5777_JPRB  

!     implicit solution:

      ZB5(JL,JK) = PTSPHY*ZBETA5(JL,JK)*(PQS5(JL,JK)-ZQE5(JL,JK)) &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))  

!     exact solution:

!     ZEXP5(JL,JK) = EXP(-ZBETA5(JL,JK)*ZCORQS5(JL,JK)*PTSPHY)
!     ZB5(JL,JK) = (PQS5(JL,JK)-ZQE5(JL,JK))*(_ONE_-ZEXP5(JL,JK)) &
!      &         / ZCORQS5(JL,JK)

      ZDTGDP5(JL,JK) =PTSPHY*RG/(PAPHP15(JL,JK+1)-PAPHP15(JL,JK))

      ZDPR15(JL,JK) = ZCOVPCLR5(JL,JK)*ZB5(JL,JK)/ZDTGDP5(JL,JK)
      ZDPR5 (JL,JK) = ZDPR15(JL,JK)
      IF (ZDPR15(JL,JK) > ZPRECLR15(JL,JK)) THEN
        ZDPR5(JL,JK) = ZPRECLR15(JL,JK)
      ENDIF

! take away from clr sky flux
      ZPRECLR5(JL,JK) = ZPRECLR15(JL,JK)-ZDPR5 (JL,JK)
      IF (ZPRECLR5(JL,JK) <= 0.0_JPRB) THEN !reset
        ZCOVPTOT5(JL,JK) = PCLC5(JL,JK)
      ENDIF
      PCOVPTOT5(JL,JK) = ZCOVPTOT5(JL,JK)

! warm proportion

      ZDPRSFL5(JL,JK) = ZDPR5(JL,JK)*ZRFLN25(JL,JK)/ZPRTOT5(JL,JK)     
      ZRFLN15(JL,JK) = ZRFLN15(JL,JK)-ZDPRSFL5(JL,JK)
     
! ice proportion

      ZDPSSFL5(JL,JK) = ZDPR5(JL,JK)*ZSFLN25(JL,JK)/ZPRTOT5(JL,JK)       
      ZSFLN15(JL,JK) = ZSFLN15(JL,JK)-ZDPSSFL5(JL,JK)
    ENDIF

!   Partitioning for cloud liquid and ice water content

    PQLWP5(JL,JK) = ZFWAT5(JL,JK)*ZQLWC5(JL,JK)
    PQIWP5(JL,JK) = (1.0_JPRB-ZFWAT5(JL,JK))*ZQLWC5(JL,JK)
  ENDDO

!   Incrementation of T and Q and fluxes' swap

  DO JL=KIDIA,KFDIA
    ZDQDT5(JL,JK) = -((ZRFLN15(JL,JK)-ZRFL5(JL,JK)) &
     & + (ZSFLN15(JL,JK)-ZSFL5(JL,JK)))*(RG/ZDP5(JL,JK)) &
     & + PLUDE5(JL,JK)*ZGDP5(JL,JK)  
    ZDTDT5(JL,JK) = (ZLVDCP5(JL,JK)*(ZRFLN15(JL,JK)-ZRFL5(JL,JK)) &
     & + ZLSDCP5(JL,JK) &
     & * (ZSFLN15(JL,JK)-ZSFL5(JL,JK)))*(RG/ZDP5(JL,JK)) &
     & - PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK)) & 
     & + (ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE15(JL,JK)*ZGDP5(JL,JK)

! first guess T and Q
    ZTP35(JL,JK) = ZTP15(JL,JK) + PTSPHY*ZDTDT5(JL,JK)
    ZQP15(JL,JK) = ZQP25(JL,JK) + PTSPHY*ZDQDT5(JL,JK)
    ZTPB5(JL,JK) = ZTP35(JL,JK)
    ZQPB5(JL,JK) = ZQP15(JL,JK)
    
    ZPP5(JL) = PAPP15(JL,JK)
    ZQOLD5(JL,JK) = ZQP15(JL,JK)
  ENDDO

! clipping of final qv

  IK=JK
  ICALL=0
  CALL CUADJTQS  ( YRTHF, YRCST, KIDIA, KFDIA, KLON, KLEV, IK,&
   & ZPP5 , ZTP35 , ZQP15, LLFLAG, ICALL  )  
  
  DO JL=KIDIA,KFDIA
    IF ((ZQOLD5(JL,JK)-ZQP15(JL,JK)) >= 0.0_JPRB) THEN
      ZDQ5(JL,JK) = ZQOLD5(JL,JK)-ZQP15(JL,JK)
    ELSE
      ZDQ5(JL,JK) = 0.0_JPRB
    ENDIF
    ZDR25(JL,JK) = ZCONS2*ZDP5(JL,JK)*ZDQ5(JL,JK)    

    IF (ZTP35(JL,JK) < RTT) THEN
      ZRFREEZE25=ZFWAT5(JL,JK)*ZDR25(JL,JK)
      ZFWATR25(JL,JK)=0.0_JPRB
    ELSE
      ZRFREEZE25=0.0_JPRB
      ZFWATR25(JL,JK)=1.0_JPRB
    ENDIF 

    ZRN5 = ZFWATR25(JL,JK)*ZDR25(JL,JK)
    ZSN5 = (1.0_JPRB-ZFWATR25(JL,JK))*ZDR25(JL,JK) 

    ZRFLN5(JL,JK) = ZRFLN15(JL,JK)+ZRN5
    ZSFLN5(JL,JK) = ZSFLN15(JL,JK)+ZSN5
    ZRFREEZE35(JL,JK)=ZRFREEZE15(JL,JK)+ZRFREEZE25
  ENDDO
    
  DO JL=KIDIA,KFDIA
    ZDQDT5(JL,JK) = -((ZRFLN5(JL,JK)-ZRFL5(JL,JK)) &
     & + (ZSFLN5(JL,JK)-ZSFL5(JL,JK)))*(RG/ZDP5(JL,JK))  &
     & + PLUDE5(JL,JK)*ZGDP5(JL,JK)  
    ZDTDT5(JL,JK) = (ZLVDCP5(JL,JK)*(ZRFLN5(JL,JK)-ZRFL5(JL,JK)) &
     & + ZLSDCP5(JL,JK) &
     & * (ZSFLN5(JL,JK)-ZSFL5(JL,JK)))*(RG/ZDP5(JL,JK)) &
     & - PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK)) & 
     & + (ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK))*ZRFREEZE35(JL,JK)*ZGDP5(JL,JK)  

    PTENQ5(JL,JK) = ZDQDT5(JL,JK) 
    PTENT5(JL,JK) = ZDTDT5(JL,JK) 

    PFPLSL5(JL,JK+1) = ZRFLN5(JL,JK)
    PFPLSN5(JL,JK+1) = ZSFLN5(JL,JK)
  ENDDO

  DO JL=KIDIA,KFDIA
    ZRFL5(JL,JK+1) = ZRFLN5(JL,JK)
    ZSFL5(JL,JK+1) = ZSFLN5(JL,JK)
  ENDDO

ENDDO  !jk

!*     ENTHALPY FLUXES DUE TO PRECIPITATION
!      ------------------------------------

DO JK=1,KLEV+1
  DO JL=KIDIA,KFDIA
    PFHPSL5(JL,JK) = -PFPLSL5(JL,JK)*RLVTT
    PFHPSN5(JL,JK) = -PFPLSN5(JL,JK)*RLSTT
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!                 ----- ADJOINT COMPUTATION ------

!     ------------------------------------------------------------------

!*                        INITIALIZATIONS
!                         ---------------

ZRFL(:)     = 0.0_JPRB
ZSFL(:)     = 0.0_JPRB
ZDR(:)      = 0.0_JPRB
ZDQ(:)      = 0.0_JPRB
ZQOLD(:)    = 0.0_JPRB
ZPP(:)      = 0.0_JPRB
ZQCRIT(:)   = 0.0_JPRB
ZGDP(:)     = 0.0_JPRB
ZRFLN(:)    = 0.0_JPRB
ZSFLN(:)    = 0.0_JPRB
ZDQDT(:)    = 0.0_JPRB
ZDTDT(:)    = 0.0_JPRB
ZDTGDP(:)   = 0.0_JPRB
ZCOVPTOT(:) = 0.0_JPRB
ZCOVPCLR(:) = 0.0_JPRB
ZDQSDTEMP(:) = 0.0_JPRB
ZCORQS(:)   = 0.0_JPRB
ZQLIM(:)    = 0.0_JPRB

ZDP(:,:)    = 0.0_JPRB
ZLFDCP(:,:) = 0.0_JPRB
ZLSDCP(:,:) = 0.0_JPRB
ZLVDCP(:,:) = 0.0_JPRB
ZTP1(:,:)   = 0.0_JPRB
ZQP1(:,:)   = 0.0_JPRB
ZQLWC(:,:)  = 0.0_JPRB
ZLUDE(:,:)  = 0.0_JPRB
ZFWAT(:,:)  = 0.0_JPRB
ZRFREEZE(:,:) = 0.0_JPRB

!*     ENTHALPY FLUXES DUE TO PRECIPITATION
!      ------------------------------------

DO JK=KLEV+1,1,-1
  DO JL=KIDIA,KFDIA
    PFPLSN(JL,JK)=PFPLSN(JL,JK)-PFHPSN(JL,JK)*RLSTT
    PFHPSN(JL,JK)=0.0_JPRB
    PFPLSL(JL,JK)=PFPLSL(JL,JK)-PFHPSL(JL,JK)*RLVTT
    PFHPSL(JL,JK)=0.0_JPRB
  ENDDO
ENDDO

!     ------------------------------------------------------------------

!*        3. COMPUTE LAYER CLOUD AMOUNTS
!            ---------------------------

! Large loop over KLEV 
! Calculates 
!   1. diagnostic CC and QL
!   2. Convective CC and QL
!   3. Rainfall 

DO JK=KLEV,KTDIA,-1

!*         3.3    CALCULATE PRECIPITATION

!   Incrementation of T and Q and fluxes' swap

  DO JL=KIDIA,KFDIA
    ZSFLN(JL) = ZSFLN(JL)+ZSFL(JL)
    ZSFL (JL) = 0.0_JPRB
    ZRFLN(JL) = ZRFLN(JL)+ZRFL(JL)
    ZRFL (JL) = 0.0_JPRB
  ENDDO

  DO JL=KIDIA,KFDIA
    ZSFLN(JL) = ZSFLN(JL)+PFPLSN(JL,JK+1)
    PFPLSN(JL,JK+1) = 0.0_JPRB
    ZRFLN(JL) = ZRFLN(JL)+PFPLSL(JL,JK+1)
    PFPLSL(JL,JK+1) = 0.0_JPRB
    
    ZDTDT(JL) = ZDTDT(JL)+PTENT (JL,JK)
    ZDQDT(JL) = ZDQDT(JL)+PTENQ (JL,JK)
    PTENT (JL,JK) = 0.0_JPRB
    PTENQ (JL,JK) = 0.0_JPRB

    ZDP(JL,JK) = ZDP(JL,JK)-ZDTDT(JL) &
     & * (ZLVDCP5(JL,JK)*(ZRFLN5(JL,JK)-ZRFL5(JL,JK)) &
     & + ZLSDCP5(JL,JK)*(ZSFLN5(JL,JK)-ZSFL5(JL,JK))) &
     & * RG/ZDP5(JL,JK)**2  
    ZRFLN(JL) = ZRFLN(JL)+ZDTDT(JL)*ZLVDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZRFL(JL) = ZRFL (JL)-ZDTDT(JL)*ZLVDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZSFLN(JL) = ZSFLN(JL)+ZDTDT(JL)*ZLSDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZSFL(JL) = ZSFL(JL)-ZDTDT(JL)*ZLSDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK) &
     & + ZDTDT(JL)*(ZRFLN5(JL,JK)-ZRFL5(JL,JK)) &
     & * RG/ZDP5(JL,JK)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK) &
     & +ZDTDT(JL)*(ZSFLN5(JL,JK)-ZSFL5(JL,JK)) &
     & * RG/ZDP5(JL,JK)  
    PLUDE(JL,JK) = PLUDE(JL,JK) &
     & - ZGDP5(JL,JK)*(ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK))*ZDTDT(JL)  
    ZGDP(JL) = ZGDP(JL)-PLUDE5(JL,JK)*(ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK))*ZDTDT (JL)  
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * ZFWAT5(JL,JK)*ZDTDT (JL)  
    ZFWAT(JL,JK) = ZFWAT(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (ZLVDCP5(JL,JK)-ZLSDCP5(JL,JK))*ZDTDT (JL)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (1.0_JPRB-ZFWAT5(JL,JK))*ZDTDT (JL)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK)+ZRFREEZE35(JL,JK)*ZGDP5(JL,JK)*ZDTDT (JL)
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK)-ZRFREEZE35(JL,JK)*ZGDP5(JL,JK)*ZDTDT (JL)
    ZRFREEZE (JL,JK) = ZRFREEZE (JL,JK)+(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK)) &
                   & * ZGDP5(JL,JK)*ZDTDT (JL)
    ZGDP (JL) = ZGDP (JL)+(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK)) &
            & * ZRFREEZE35(JL,JK)*ZDTDT (JL)
    ZDTDT(JL)=0.0_JPRB

    ZDP(JL,JK) = ZDP(JL,JK)+ZDQDT(JL)*((ZRFLN5(JL,JK)-ZRFL5(JL,JK)) &
     & + (ZSFLN5(JL,JK)-ZSFL5(JL,JK)))*RG/ZDP5(JL,JK)**2  
    ZRFLN(JL)=ZRFLN(JL)-ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZRFL(JL)=ZRFL(JL)+ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZSFLN(JL)=ZSFLN(JL)-ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZSFL(JL)=ZSFL(JL)+ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZGDP(JL) = ZGDP(JL)+PLUDE5(JL,JK)*ZDQDT (JL)
    PLUDE(JL,JK) = PLUDE(JL,JK)+ZGDP5(JL,JK)*ZDQDT (JL)
    ZDQDT(JL)=0.0_JPRB
  ENDDO

! clipping of final qv

  DO JL=KIDIA,KFDIA
    ZFWATR = 0.0_JPRB
    ZSN = 0.0_JPRB
    ZRN = 0.0_JPRB

    ZRFREEZE2 = ZRFREEZE (JL,JK)

    ZSN = ZSN + ZSFLN (JL)
    ZRN = ZRN + ZRFLN (JL)

    ZDR(JL) = ZDR(JL)+(1.0_JPRB-ZFWATR25(JL,JK))*ZSN
    ZFWATR = ZFWATR-ZDR25(JL,JK)*ZSN
    
    ZDR(JL) = ZDR(JL)+ZFWATR25(JL,JK)*ZRN
    ZFWATR = ZFWATR+ZDR25(JL,JK)*ZRN

! Update rain fraction and freezing.
! Note: impact of new temperature ZTP1 on ZFWAT is neglected here.
    IF (ZTP35(JL,JK) < RTT) THEN
      ZFWATR=0.0_JPRB

      ZFWAT (JL,JK) = ZFWAT (JL,JK)+ ZDR25(JL,JK)*ZRFREEZE2
      ZDR (JL) = ZDR (JL) + ZFWAT5(JL,JK)*ZRFREEZE2
    ELSE
      ZFWATR=0.0_JPRB
    ENDIF 

    ZDQ(JL) = ZDQ(JL)+ZCONS2*ZDP5(JL,JK)*ZDR (JL)
    ZDP(JL,JK) = ZDP(JL,JK)+ZCONS2*ZDQ5(JL,JK)*ZDR (JL)
    ZDR (JL) = 0.0_JPRB

    IF ((ZQOLD5(JL,JK)-ZQP15(JL,JK)) >= 0.0_JPRB) THEN
      ZQOLD (JL) = ZQOLD (JL)+ZDQ (JL)
      ZQP1 (JL,JK) = ZQP1 (JL,JK)-ZDQ (JL)
    ENDIF
    ZDQ (JL) = 0.0_JPRB

    ZPP5(JL) = PAPP15(JL,JK)
  ENDDO
    
  IK=JK
  ICALL=0
  CALL CUADJTQSAD ( KIDIA, KFDIA, KLON, KLEV, IK,&
   & ZPP5 , ZTPB5 , ZQPB5,&
   & ZPP  , ZTP1  , ZQP1 , LLFLAG, ICALL  )  

  DO JL=KIDIA,KFDIA    

! first guess T and Q

    ZQP1 (JL,JK) = ZQP1 (JL,JK)+ZQOLD (JL)
    ZQOLD (JL) = 0.0_JPRB
    PAPP1 (JL,JK) = PAPP1 (JL,JK)+ZPP (JL)
    ZPP (JL) = 0.0_JPRB

    ZDQDT (JL) = ZDQDT (JL)+PTSPHY*ZQP1 (JL,JK)
    ZDTDT (JL) = ZDTDT (JL)+PTSPHY*ZTP1 (JL,JK)

!   Incrementation of T and Q 

    ZDP(JL,JK) = ZDP(JL,JK)-ZDTDT(JL) &
     & * (ZLVDCP5(JL,JK)*(ZRFLN15(JL,JK)-ZRFL5(JL,JK)) &
     & + ZLSDCP5(JL,JK)*(ZSFLN15(JL,JK)-ZSFL5(JL,JK))) &
     & * RG/ZDP5(JL,JK)**2  
    ZRFLN(JL) = ZRFLN(JL)+ZDTDT(JL)*ZLVDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZRFL(JL) = ZRFL (JL)-ZDTDT(JL)*ZLVDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZSFLN(JL) = ZSFLN(JL)+ZDTDT(JL)*ZLSDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZSFL(JL) = ZSFL(JL)-ZDTDT(JL)*ZLSDCP5(JL,JK)*RG/ZDP5(JL,JK)
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK) &
     & + ZDTDT(JL)*(ZRFLN15(JL,JK)-ZRFL5(JL,JK)) &
     & * RG/ZDP5(JL,JK)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK) &
     & +ZDTDT(JL)*(ZSFLN15(JL,JK)-ZSFL5(JL,JK)) &
     & * RG/ZDP5(JL,JK)  
    PLUDE(JL,JK) = PLUDE(JL,JK) &
     & - ZGDP5(JL,JK)*(ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK))*ZDTDT(JL)  
    ZGDP(JL) = ZGDP(JL)-PLUDE5(JL,JK)*(ZFWAT5(JL,JK)*ZLVDCP5(JL,JK) &
     & + (1.0_JPRB-ZFWAT5(JL,JK))*ZLSDCP5(JL,JK))*ZDTDT (JL)  
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * ZFWAT5(JL,JK)*ZDTDT (JL)  
    ZFWAT(JL,JK) = ZFWAT(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (ZLVDCP5(JL,JK)-ZLSDCP5(JL,JK))*ZDTDT (JL)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK)-PLUDE5(JL,JK)*ZGDP5(JL,JK) &
     & * (1.0_JPRB-ZFWAT5(JL,JK))*ZDTDT (JL)  
    ZLSDCP(JL,JK) = ZLSDCP(JL,JK)+ZRFREEZE15(JL,JK)*ZGDP5(JL,JK)*ZDTDT (JL)
    ZLVDCP(JL,JK) = ZLVDCP(JL,JK)-ZRFREEZE15(JL,JK)*ZGDP5(JL,JK)*ZDTDT (JL)
    ZRFREEZE (JL,JK) = ZRFREEZE (JL,JK)+(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK)) &
                   & * ZGDP5(JL,JK)*ZDTDT (JL)
    ZGDP (JL) = ZGDP (JL)+(ZLSDCP5(JL,JK)-ZLVDCP5(JL,JK)) &
            & * ZRFREEZE15(JL,JK)*ZDTDT (JL)
    ZDTDT(JL)=0.0_JPRB    

    ZDP(JL,JK) = ZDP(JL,JK)+ZDQDT(JL)*((ZRFLN15(JL,JK)-ZRFL5(JL,JK)) &
     & + (ZSFLN15(JL,JK)-ZSFL5(JL,JK)))*RG/ZDP5(JL,JK)**2  
    ZRFLN(JL)=ZRFLN(JL)-ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZRFL(JL)=ZRFL(JL)+ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZSFLN(JL)=ZSFLN(JL)-ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZSFL(JL)=ZSFL(JL)+ZDQDT(JL)*RG/ZDP5(JL,JK)
    ZGDP(JL) = ZGDP(JL)+PLUDE5(JL,JK)*ZDQDT (JL)
    PLUDE(JL,JK) = PLUDE(JL,JK)+ZGDP5(JL,JK)*ZDQDT (JL)
    ZDQDT(JL)=0.0_JPRB
  ENDDO

!DEC$ IVDEP
  DO JL=KIDIA,KFDIA
    ZSN     = 0.0_JPRB
    ZRN     = 0.0_JPRB
    ZOEALFA = 0.0_JPRB
    ZPR     = 0.0_JPRB
    ZLNEW   = 0.0_JPRB
    ZCLD    = 0.0_JPRB
    ZD      = 0.0_JPRB

    ZPRTOT  = 0.0_JPRB
    ZPRECLR = 0.0_JPRB
    ZQE     = 0.0_JPRB
    ZBETA   = 0.0_JPRB
    ZB      = 0.0_JPRB
    ZDPR    = 0.0_JPRB
    ZDPRSFL = 0.0_JPRB
    ZDPSSFL = 0.0_JPRB
    ZFWATR  = 0.0_JPRB

!   Partitioning for cloud liquid and ice water content

    ZFWAT(JL,JK) = ZFWAT(JL,JK)-ZQLWC5(JL,JK)*PQIWP (JL,JK)
    ZQLWC(JL,JK) = ZQLWC(JL,JK)+(1.0_JPRB-ZFWAT5(JL,JK))*PQIWP (JL,JK)
    PQIWP (JL,JK) = 0.0_JPRB
    ZFWAT(JL,JK) = ZFWAT(JL,JK)+ZQLWC5(JL,JK)*PQLWP (JL,JK)
    ZQLWC(JL,JK) = ZQLWC(JL,JK)+ZFWAT5(JL,JK)*PQLWP (JL,JK)
    PQLWP (JL,JK) = 0.0_JPRB

!   Precip evaporation

    LLO2=ZPRTOT5(JL,JK)>ZEPS2 .AND. ZCOVPCLR5(JL,JK)>ZEPS2 &
     & .AND. (LEVAPLS2 .OR. LDRAIN1D)

    IF (LLO2) THEN

! ice proportion
      ZDPSSFL = ZDPSSFL-ZSFLN (JL)
      ZSFLN(JL) = ZSFLN(JL)+ZDPR5(JL,JK)*ZDPSSFL/ZPRTOT5(JL,JK)
      ZDPR = ZDPR+ZSFLN25(JL,JK)*ZDPSSFL/ZPRTOT5(JL,JK)
      ZPRTOT = ZPRTOT-ZDPR5(JL,JK)*ZSFLN25(JL,JK)*ZDPSSFL &
       & / ZPRTOT5(JL,JK)**2  

! warm proportion
      ZDPRSFL = ZDPRSFL-ZRFLN (JL)
      ZRFLN(JL) = ZRFLN(JL)+ZDPR5(JL,JK)*ZDPRSFL/ZPRTOT5(JL,JK)
      ZDPR = ZDPR+ZRFLN25(JL,JK)*ZDPRSFL/ZPRTOT5(JL,JK)
      ZPRTOT = ZPRTOT-ZDPR5(JL,JK)*ZRFLN25(JL,JK)*ZDPRSFL &
       & / ZPRTOT5(JL,JK)**2  

! take away from clr sky flux
      ZCOVPTOT(JL) = ZCOVPTOT(JL)+PCOVPTOT(JL,JK)
      PCOVPTOT(JL,JK) = 0.0_JPRB
      IF (ZPRECLR5(JL,JK) <= 0.0_JPRB) THEN !reset
        PCLC(JL,JK) = PCLC(JL,JK)+ZCOVPTOT (JL)
        ZCOVPTOT (JL) = 0.0_JPRB
      ENDIF
      ZDPR = ZDPR-ZPRECLR

      IF (ZDPR15(JL,JK) > ZPRECLR15(JL,JK)) THEN
        ZPRECLR = ZPRECLR+ZDPR
        ZDPR = 0.0_JPRB
      ENDIF

      ZB = ZB+ZCOVPCLR5(JL,JK)*ZDPR/ZDTGDP5(JL,JK)
      ZCOVPCLR(JL) = ZCOVPCLR(JL)+ZB5(JL,JK)*ZDPR/ZDTGDP5(JL,JK)
      ZDTGDP(JL) = ZDTGDP(JL)-ZCOVPCLR5(JL,JK)*ZB5(JL,JK)*ZDPR &
       & / ZDTGDP5(JL,JK)**2  
      
      PAPHP1(JL,JK+1) = PAPHP1(JL,JK+1)-PTSPHY*RG*ZDTGDP (JL) &
       & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
      PAPHP1(JL,JK) = PAPHP1(JL,JK)+PTSPHY*RG*ZDTGDP (JL) &
       & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
      ZDTGDP (JL) = 0.0_JPRB

!     exact solution:

!      PQS(JL,JK) = PQS(JL,JK)-ZEXP5(JL,JK)*ZB/ZCORQS5(JL,JK)
!      ZQE = ZQE+ZEXP5(JL,JK)*ZB/ZCORQS5(JL,JK)
!      ZCORQS(JL) = ZCORQS(JL)+ZEXP5(JL,JK)*(PQS5(JL,JK)-ZQE5(JL,JK)) &
!       &         * PTSPHY * ZBETA5(JL,JK)*ZB/ZCORQS5(JL,JK)
!      ZBETA = ZBETA+ZEXP5(JL,JK)*(PQS5(JL,JK)-ZQE5(JL,JK))*PTSPHY*ZB
!      ZCORQS(JL) = ZCORQS(JL)+ZEXP5(JL,JK)*(PQS5(JL,JK)-ZQE5(JL,JK)) &
!       &         * ZB / ZCORQS5(JL,JK)**2
!      PQS(JL,JK) = PQS(JL,JK)+ZB/ZCORQS5(JL,JK)
!      ZQE = ZQE-ZB/ZCORQS5(JL,JK)
!      ZCORQS(JL) = ZCORQS(JL)-(PQS5(JL,JK)-ZQE5(JL,JK))*ZB &
!       &         / ZCORQS5(JL,JK)**2

!     implicit solution:

      ZBETA = ZBETA+PTSPHY*(PQS5(JL,JK)-ZQE5(JL,JK))*ZB &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))  
      PQS(JL,JK) = PQS(JL,JK)+PTSPHY*ZBETA5(JL,JK)*ZB &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))  
      ZQE = ZQE-PTSPHY*ZBETA5(JL,JK)*ZB &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))  
      ZCORQS(JL) = ZCORQS(JL)-(PTSPHY**2)*ZBETA5(JL,JK) &
       & * (PQS5(JL,JK)-ZQE5(JL,JK))*ZBETA5(JL,JK)*ZB &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))**2  
      ZBETA = ZBETA-(PTSPHY**2)*ZBETA5(JL,JK) &
       & * (PQS5(JL,JK)-ZQE5(JL,JK))*ZCORQS5(JL,JK)*ZB &
       & / (1.0_JPRB+ZBETA5(JL,JK)*PTSPHY*ZCORQS5(JL,JK))**2  

!     This is the humidity in the moistest zcovpclr region

      ZXX5 = 0.5777_JPRB*(RG*RPECONS/5.09E-3_JPRB) &
       & * (5.09E-3_JPRB*ZCOVPCLR5(JL,JK)/(ZPRECLR15(JL,JK) &
       & * SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1))))**0.4223_JPRB  
      ZPRECLR = ZPRECLR+ZXX5*SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1)) &
       & * ZBETA/ZCOVPCLR5(JL,JK)  
      PAPP1(JL,JK) = PAPP1(JL,JK)+(ZXX5*0.5_JPRB*ZPRECLR15(JL,JK)*ZBETA &
       & / SQRT(PAPP15(JL,JK)*PAPHP15(JL,KLEV+1))) &
       & / ZCOVPCLR5(JL,JK)  
      PAPHP1(JL,KLEV+1) = PAPHP1(JL,KLEV+1) &
       & -(ZXX5*0.5_JPRB*ZPRECLR15(JL,JK) &
       & * SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1)) &
       & * ZBETA/PAPHP15(JL,KLEV+1)) &
       & / ZCOVPCLR5(JL,JK)  
      ZCOVPCLR(JL) = ZCOVPCLR(JL)-ZXX5*ZPRECLR15(JL,JK) &
       & * SQRT(PAPP15(JL,JK)/PAPHP15(JL,KLEV+1)) &
       & * ZBETA/ZCOVPCLR5(JL,JK)**2  

      PQS(JL,JK) = PQS(JL,JK)+ZQE
      ZCOVPCLR(JL) = ZCOVPCLR(JL)-(PQS5(JL,JK)-ZQLIM5(JL,JK))*ZQE &
       & / (1.0_JPRB-PCLC5(JL,JK))**2  
      PQS(JL,JK) = PQS(JL,JK)-ZCOVPCLR5(JL,JK)*ZQE &
       & / (1.0_JPRB-PCLC5(JL,JK))**2  
      ZQLIM(JL) = ZQLIM(JL)+ZCOVPCLR5(JL,JK)*ZQE &
       & / (1.0_JPRB-PCLC5(JL,JK))**2  
      PCLC(JL,JK) = PCLC(JL,JK)-2.0_JPRB*(PQS5(JL,JK)-ZQLIM5(JL,JK)) &
       & * ZCOVPCLR5(JL,JK)*ZQE &
       & / (1.0_JPRB-PCLC5(JL,JK))**3  

      ZCOVPCLR(JL) = ZCOVPCLR(JL)+ZPRTOT5(JL,JK)*ZPRECLR &
       & / ZCOVPTOT15(JL,JK)
      ZPRTOT = ZPRTOT+ZCOVPCLR5(JL,JK)*ZPRECLR &
       & / ZCOVPTOT15(JL,JK)
      ZCOVPTOT(JL) = ZCOVPTOT(JL)-ZPRTOT5(JL,JK)*ZCOVPCLR5(JL,JK) &
       & * ZPRECLR / ZCOVPTOT15(JL,JK)**2
    ENDIF

!    New precipitation

    ZRFLN (JL) = ZRFLN (JL)+ZPRTOT
    ZSFLN (JL) = ZSFLN (JL)+ZPRTOT

    ZSN = ZSN+ZSFLN(JL)
    ZRN = ZRN+ZRFLN (JL)

    ZFWATR = ZFWATR-ZDR15(JL,JK)*ZSN
    ZDR(JL) = ZDR(JL)+(1.0_JPRB-ZFWATR15(JL,JK))*ZSN
    ZFWATR = ZFWATR+ZDR15(JL,JK)*ZRN
    ZDR(JL) = ZDR(JL)+ZFWATR15(JL,JK)*ZRN

! Update rain fraction and freezing.
! Note: impact of new temperature ZTP1 on ZFWAT is neglected here.
    IF (ZTP15(JL,JK) < RTT) THEN
      ZFWATR=0.0_JPRB

      ZFWAT (JL,JK) = ZFWAT (JL,JK)+ ZDR15(JL,JK)*ZRFREEZE (JL,JK)
      ZDR (JL) = ZDR (JL) + ZFWAT5(JL,JK)*ZRFREEZE (JL,JK)
      ZRFREEZE (JL,JK)=0.0_JPRB
    ELSE
      ZFWATR=0.0_JPRB
    ENDIF 

    IF (ZTP15(JL,JK) < RTT) THEN
      ZOEALFA = ZOEALFA+ZFWAT (JL,JK)
    ENDIF
    ZFWAT (JL,JK) = 0.0_JPRB

    ZTP1(JL,JK) = ZTP1(JL,JK)+0.545_JPRB*0.17_JPRB*ZOEALFA &
     & / COSH(0.17_JPRB*(ZTP15(JL,JK)-RLPTRC))**2  

    ZPR = ZPR+ZCONS2*ZDP5(JL,JK)*ZDR (JL)
    ZDP(JL,JK) = ZDP(JL,JK)+ZCONS2*ZPR5(JL,JK)*ZDR (JL)

!    Diagnostic calculation of rain production

    IF (PCLC5(JL,JK) > ZEPS2) THEN
      ZPR = ZPR-ZQLWC (JL,JK)
      ZQLWC (JL,JK) = ZQLWC (JL,JK)+ZPR
      ZLNEW = ZLNEW-ZPR

      PCLC(JL,JK) = PCLC(JL,JK)+ZCLD5(JL,JK)*EXP(-ZD5(JL,JK))*ZLNEW
      ZCLD = ZCLD+PCLC5(JL,JK)*EXP(-ZD5(JL,JK))*ZLNEW
      ZD = ZD-PCLC5(JL,JK)*ZCLD5(JL,JK)*EXP(-ZD5(JL,JK))*ZLNEW

!    regularization of ZD
      IF (LREGCL) THEN
        ZCLD = ZCLD+ (2.*ZCKCODTA/ZLCRIT**2) &
         & * EXP(-(ZCLD5(JL,JK)/ZLCRIT)**2)*ZCLD5(JL,JK)*ZD  
      ELSE
        ZCLD = ZCLD+ (2.*ZCKCODT/ZLCRIT**2) &
         & * EXP(-(ZCLD5(JL,JK)/ZLCRIT)**2)*ZCLD5(JL,JK)*ZD  
      ENDIF
!    end of regularization

      ZQLWC (JL,JK) = ZQLWC (JL,JK)+ZCLD/PCLC5(JL,JK)
      PCLC(JL,JK) = PCLC(JL,JK)-ZQLWC25(JL,JK)*ZCLD/PCLC5(JL,JK)**2
    ENDIF
  ENDDO

!    Melting of incoming snow

  DO JL=KIDIA,KFDIA
    ZSNMLT = 0.0_JPRB
    ZCONS  = 0.0_JPRB
    ZZ2S   = 0.0_JPRB

    IF (ZSFL5(JL,JK) /= 0.0_JPRB) THEN
      ZSNMLT = ZSNMLT-ZTP1 (JL,JK)/ZCONS5(JL,JK)
      ZCONS = ZCONS+(ZTP1 (JL,JK)*ZSNMLT5(JL,JK))/ZCONS5(JL,JK)**2
      
      ZSFL (JL) = ZSFL (JL)+ZSFLN (JL)
      ZSNMLT = ZSNMLT-ZSFLN (JL)
      ZSFLN (JL) = 0.0_JPRB

      ZRFL (JL) = ZRFL (JL)+ZRFLN (JL)
      ZSNMLT = ZSNMLT+ZRFLN (JL)
      ZRFLN (JL) = 0.0_JPRB
      
      IF (ZSFL5(JL,JK) <= ZZ2S5(JL,JK)) THEN
        ZSFL (JL) = ZSFL (JL)+ZSNMLT
      ELSE
        ZZ2S = ZZ2S+ZSNMLT
      ENDIF
        
      IF ((ZTP25(JL,JK)-ZMELTP2) > 0.0_JPRB) THEN
        ZTP1 (JL,JK) = ZTP1 (JL,JK)+ZCONS5(JL,JK)*ZZ2S
        ZCONS = ZCONS+(ZTP25(JL,JK)-ZMELTP2)*ZZ2S
      ENDIF

      ZDP(JL,JK) = ZDP(JL,JK)+ZCONS2*ZCONS/ZLFDCP5(JL,JK)
      ZLFDCP(JL,JK) = ZLFDCP(JL,JK) &
       & - ZCONS2*ZDP5(JL,JK)*ZCONS/ZLFDCP5(JL,JK)**2  
    ELSE
      ZSFL (JL) = ZSFL (JL)+ZSFLN (JL)
      ZSFLN (JL) = 0.0_JPRB

      ZRFL (JL) = ZRFL (JL)+ZRFLN (JL)
      ZRFLN (JL) = 0.0_JPRB
    ENDIF
  ENDDO

!       3.1   INITIALIZATION

! Calculate precipitation overlap. 
! Simple form based on Maximum Overlap.

  DO JL=KIDIA,KFDIA
    IF (ZCOVPCLR15(JL,JK) < 0.0_JPRB) THEN
      ZCOVPCLR(JL) = 0.0_JPRB
    ENDIF
    ZCOVPTOT(JL) = ZCOVPTOT(JL)+ZCOVPCLR(JL)
    PCLC(JL,JK) = PCLC(JL,JK)-ZCOVPCLR(JL)
    ZCOVPCLR(JL) = 0.0_JPRB

    IF (PCLC5(JL,JK) > ZCOVPTOT5(JL,JK-1)) THEN ! total rain frac
      PCLC(JL,JK) = PCLC(JL,JK)+ZCOVPTOT(JL)
      ZCOVPTOT(JL) = 0.0_JPRB
    ENDIF
  ENDDO

! Add convective component

!DEC$ IVDEP
  DO JL=KIDIA,KFDIA
    IF (JK<KLEV) THEN
      LLO1=ZLUDE5(JL,JK)>=RLMIN.AND.PLU5(JL,JK+1)>=ZEPS2
    ELSE
      LLO1=.FALSE.
    ENDIF
    IF (LLO1) THEN
      ZLUDE (JL,JK) = ZLUDE (JL,JK)+ZQLWC (JL,JK)
      ZLUDE(JL,JK) = ZLUDE(JL,JK) &
       & + ((1.0_JPRB-ZCLC5(JL,JK))/PLU5(JL,JK+1)) &
       & * EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))*PCLC (JL,JK)  
      PLU(JL,JK+1) = PLU(JL,JK+1) &
       & -((1.0_JPRB-ZCLC5(JL,JK))*ZLUDE5(JL,JK)/PLU5(JL,JK+1)**2) &
       & * EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))*PCLC (JL,JK)  
!      PCLC (JL,JK)=-PCLC(JL,JK) &
!       &          *(_ONE_-EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1)))
      PCLC (JL,JK)=PCLC(JL,JK) &
       & *(1.-(1.-EXP(-ZLUDE5(JL,JK)/PLU5(JL,JK+1))))  
    ENDIF
 
    PLUDE(JL,JK) = PLUDE(JL,JK)+PTSPHY*ZGDP5(JL,JK)*ZLUDE (JL,JK)
    ZGDP(JL) = ZGDP(JL)+PTSPHY*PLUDE5(JL,JK)*ZLUDE (JL,JK)
    ZLUDE (JL,JK) = 0.0_JPRB

    PAPHP1(JL,JK+1) = PAPHP1(JL,JK+1)-RG*ZGDP (JL) &
     & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
    PAPHP1(JL,JK) = PAPHP1(JL,JK)+RG*ZGDP (JL) &
     & / (PAPHP15(JL,JK+1)-PAPHP15(JL,JK))**2  
    ZGDP (JL) = 0.0_JPRB
  ENDDO

! Replace BETA distribution with simple UNIFORM from Letreut & Li (90)

  DO JL=KIDIA,KFDIA
    ZQPD = 0.0_JPRB
    ZQCD = 0.0_JPRB
    IF (ZQP25(JL,JK) >= PQS5(JL,JK)) THEN
      PQS(JL,JK) =  PQS(JL,JK)+(1.0_JPRB-ZSCALM(JK))*ZQLWC (JL,JK)
      ZQCRIT(JL) = ZQCRIT(JL)-(1.0_JPRB-ZSCALM(JK))*ZQLWC (JL,JK)
    ELSE
      IF (ZQP25(JL,JK) > ZQCRIT5(JL,JK)) THEN
        ZQPD = ZQPD+ZSCALM(JK)*ZQLWC (JL,JK)*ZCLC5(JL,JK)**2
        ZQCD = ZQCD+(1.0_JPRB-ZSCALM(JK))*ZQLWC (JL,JK)*ZCLC5(JL,JK)**2
        PCLC(JL,JK) = PCLC(JL,JK)+(ZSCALM(JK)*ZQPD5(JL,JK) &
         & + (1.0_JPRB-ZSCALM(JK))*ZQCD5(JL,JK)) &
         & * 2.0_JPRB*ZCLC5(JL,JK)*ZQLWC (JL,JK)  

! regularization of cloud fraction
        IF (LREGCL) THEN
          IF (ZCLC5 (JL,JK)>0.2_JPRB .AND. ZCLC5 (JL,JK)<0.7_JPRB) THEN
            ZYYY = -1.2_JPRB*ZCLC5(JL,JK)+0.94_JPRB
            PCLC (JL,JK) = ZYYY * PCLC (JL,JK)
          ELSEIF (ZCLC5 (JL,JK)>=0.7_JPRB .AND. ZCLC5 (JL,JK)<0.95_JPRB) THEN
            PCLC (JL,JK) = 0.1_JPRB * PCLC (JL,JK)
          ELSEIF (ZCLC5 (JL,JK)>=0.95_JPRB) THEN
            ZYYY = ZGAMMA * SQRT(1.0_JPRB - ZCLC5(JL,JK))
            PCLC (JL,JK) = ZYYY * PCLC (JL,JK)
          ENDIF
        ENDIF
! end of regularization

        ZQPD = ZQPD - (0.5_JPRB/ZSQRT5(JL,JK))*PCLC(JL,JK) &
         & / (ZQCD5(JL,JK)-ZSCALM(JK)*(ZQP25(JL,JK)-ZQCRIT5(JL,JK)))  
        ZQCD = ZQCD + (0.5_JPRB/ZSQRT5(JL,JK))*(ZQPD5(JL,JK)*PCLC(JL,JK)) &
         & / (ZQCD5(JL,JK)-ZSCALM(JK)*(ZQP25(JL,JK)-ZQCRIT5(JL,JK)))**2  
        ZQP1(JL,JK) = ZQP1(JL,JK) - (0.5_JPRB/ZSQRT5(JL,JK)) &
         & * (ZQPD5(JL,JK)*ZSCALM(JK)*PCLC(JL,JK)) &
         & / (ZQCD5(JL,JK)-ZSCALM(JK)*(ZQP25(JL,JK)-ZQCRIT5(JL,JK)))**2  
        ZQCRIT(JL) = ZQCRIT(JL) + (0.5_JPRB/ZSQRT5(JL,JK)) &
         & * (ZQPD5(JL,JK)*ZSCALM(JK)*PCLC(JL,JK)) &
         & / (ZQCD5(JL,JK)-ZSCALM(JK)*(ZQP25(JL,JK)-ZQCRIT5(JL,JK)))**2  

        PQS(JL,JK) = PQS(JL,JK)+ZQCD
        ZQCRIT(JL) = ZQCRIT(JL)-ZQCD
        PQS(JL,JK) = PQS(JL,JK)+ZQPD
        ZQP1(JL,JK) = ZQP1(JL,JK)-ZQPD
      ENDIF
    ENDIF
    ZQLWC (JL,JK) = 0.0_JPRB
    PCLC (JL,JK) = 0.0_JPRB
  ENDDO

!DEC$ IVDEP
  DO JL=KIDIA,KFDIA
    ZOEALFAW = 0.0_JPRB
    ZALFAW   = 0.0_JPRB
    ZFOEEW   = 0.0_JPRB
    ZESDP    = 0.0_JPRB
    ZFACW    = 0.0_JPRB
    ZFACI    = 0.0_JPRB
    ZFAC     = 0.0_JPRB
    ZCOR     = 0.0_JPRB

! set up critical value of humidity

    PQS (JL,JK) = PQS (JL,JK)+ZCRH(JK)*ZQCRIT (JL)
    ZQCRIT (JL) = 0.0_JPRB

! use clipped state

    IF (ZQP25(JL,JK) > PQS5(JL,JK)) THEN
      PQS (JL,JK) = PQS (JL,JK)+ZQLIM (JL)
    ELSE
      ZQP1(JL,JK) = ZQP1(JL,JK)+ZQLIM (JL)
    ENDIF
    ZQLIM (JL) = 0.0_JPRB

!-----------------------------------
! calculate dqs/dT correction factor
!-----------------------------------

    ZDQSDTEMP(JL) = ZDQSDTEMP(JL)+ZCONS3*ZCORQS(JL)
    ZCORQS(JL) = 0.0_JPRB

    PQS(JL,JK) = PQS(JL,JK)+ZFAC5(JL,JK)*ZCOR5(JL,JK)*ZDQSDTEMP(JL)
    ZCOR = ZCOR+ZFAC5(JL,JK)*PQS5(JL,JK)*ZDQSDTEMP(JL)
    ZFAC = ZFAC+ZCOR5(JL,JK)*PQS5(JL,JK)*ZDQSDTEMP(JL)
    ZDQSDTEMP(JL) = 0.0_JPRB

    ZESDP = ZESDP+RETV*ZCOR/(1.0_JPRB-RETV*ZESDP5(JL,JK))**2

    ZFACW = ZFACW+ZALFAW5(JL,JK)*ZFAC
    ZALFAW = ZALFAW+ZFACW5(JL,JK)*ZFAC
    ZFACI = ZFACI+(1.0_JPRB-ZALFAW5(JL,JK))*ZFAC
    ZALFAW = ZALFAW-ZFACI5(JL,JK)*ZFAC

    ZTP1(JL,JK) = ZTP1(JL,JK)-2.0_JPRB*R5IES*ZFACI &
     & / (ZTP25(JL,JK)-R4IES)**3  
    ZTP1(JL,JK) = ZTP1(JL,JK)-2.0_JPRB*R5LES*ZFACW &
     & / (ZTP25(JL,JK)-R4LES)**3  

    IF (ZESDP15(JL,JK) > ZQMAX) THEN
      ZESDP  = 0.0_JPRB
    ENDIF
    ZFOEEW = ZFOEEW+ZESDP/PAPP15(JL,JK)
    PAPP1(JL,JK) = PAPP1(JL,JK)-ZESDP*ZFOEEW5(JL,JK) &
     & / PAPP15(JL,JK)**2  
    
    IF (ZTP25(JL,JK) < RTT) THEN
      Z3ES=R3IES
      Z4ES=R4IES
    ELSE
      Z3ES=R3LES
      Z4ES=R4LES
    ENDIF
    ZTP1(JL,JK) = ZTP1(JL,JK)+ Z3ES*(RTT-Z4ES)*ZFOEEW &
     & * ZFOEEW5(JL,JK)/(ZTP25(JL,JK)-Z4ES)**2  
    
    IF (ZTP25(JL,JK) < RTT) THEN
      ZOEALFAW = ZOEALFAW+ZALFAW
    ENDIF

    ZTP1(JL,JK) = ZTP1(JL,JK)+0.545_JPRB*0.17_JPRB*ZOEALFAW &
     & / COSH(0.17_JPRB*(ZTP25(JL,JK)-RLPTRC))**2  

    ZDR(JL) = 0.0_JPRB      
  ENDDO

ENDDO  !jk

!     ------------------------------------------------------------------

!*         2.2    INITIALIZATION OF CLOUD AND PRECIPITATION ARRAYS
!                 ------------------------------------------------

!       Set to zero precipitation fluxes at the top

DO JL=KIDIA,KFDIA
  PFPLSN (JL,1) = 0.0_JPRB
  PFPLSL (JL,1) = 0.0_JPRB
  ZSFL (JL) = 0.0_JPRB
  ZRFL (JL) = 0.0_JPRB
ENDDO

!       Clear cloud and freezing arrays

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PCOVPTOT (JL,JK) = 0.0_JPRB
    ZRFREEZE (JL,JK) = 0.0_JPRB
    ZQLWC (JL,JK) = 0.0_JPRB
    PQLWP (JL,JK) = 0.0_JPRB
    PQIWP (JL,JK) = 0.0_JPRB
    PCLC (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!     --------------------------------------------------------------------

!*         2.1    COMPUTE CRITICAL RELATIVE HUMIDITY AND RELATIVE HUMIDITY
!                 --------------------------------------------------------

! first guess values for T and q

DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    PQM1 (JL,JK) = PQM1 (JL,JK)+ZQP1 (JL,JK)
    PGTENQ(JL,JK)= PGTENQ(JL,JK)+PTSPHY*ZQP1 (JL,JK)
    ZQP1 (JL,JK) = 0.0_JPRB

    PTM1 (JL,JK) = PTM1 (JL,JK)+ZTP1 (JL,JK)
    PGTENT(JL,JK)= PGTENT(JL,JK)+PTSPHY*ZTP1 (JL,JK)
    ZTP1 (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

! thermodynamic constants

DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    ZZZ = 0.0_JPRB

    ZZZ = ZZZ+RLVTT*ZLVDCP (JL,JK)
    ZLVDCP (JL,JK) = 0.0_JPRB
    ZZZ = ZZZ+RLSTT*ZLSDCP (JL,JK)
    ZLSDCP (JL,JK) = 0.0_JPRB
    ZZZ = ZZZ+RLMLT*ZLFDCP (JL,JK)
    ZLFDCP (JL,JK) = 0.0_JPRB

    PQM1(JL,JK) = PQM1(JL,JK)-ZZZ*RCPD*RVTMP2/(RCPD+RCPD*RVTMP2*PQM15(JL,JK))**2
    PAPHP1(JL,JK+1) = PAPHP1(JL,JK+1)+ZDP (JL,JK)
    PAPHP1(JL,JK) = PAPHP1(JL,JK)-ZDP (JL,JK)
    ZDP (JL,JK) = 0.0_JPRB
  ENDDO
ENDDO

!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CLOUDSTAD',1,ZHOOK_HANDLE)
END SUBROUTINE CLOUDSTAD
