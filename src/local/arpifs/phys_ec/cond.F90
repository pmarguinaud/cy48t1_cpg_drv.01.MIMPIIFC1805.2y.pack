SUBROUTINE COND &
 & ( YDEPHLI,YDECND,KIDIA , KFDIA , KLON,  KLEV,&
 & PTSPHY,&
 & PTM1  , PQM1,   PAPHP1, PAPP1,&
 & PTENT, PGTENT, PTENQ, PGTENQ,&
 & PFHPSL, PFHPSN, PFPLSL, PFPLSN                      )  

!**** *COND* - COMPUTES LARGE-SCALE WATER PHASE CHANGES.

!     PURPOSE.
!     --------

!          THIS ROUTINE COMPUTES THE PHYSICAL TENDENCIES OF THE TWO
!     PROGNOSTIC VARIABLES T AND Q DUE TO THE WATER PHASE CHANGES:
!     CONDENSATION (ASSUMED TO BE TOTAL FOR ANY SUPERSATURATION) AS SNOW
!     OR LIQUID WATER DEPENDING ON TEMPERATURE, EVAPORATION OF FALLING
!     PRECIPITATIONS IN UNSATURATED LAYERS AND MELTING OF
!     THE FALLING SNOW. THE WHOLE COMPUTATION IS AN INSTANTANEOUS
!     ADAPTATION PROCESS ON THE T+1 VALUES. RAIN AND SNOWFALL SURFACE
!     FLUXES, LATER TO BE USED FOR SOIL PROCESS ARE STORED.

!**   INTERFACE.
!     ----------

!          *COND* IS CALLED FROM *CALLPAR*.
!          THE ROUTINE TAKES ITS INPUT FROM THE LONG-TERM STORAGE:
!     T AND Q AT T+1 AND P ON LEVEL BOUNDARIES AT T+1 FROM WHICH THE P
!     FOR THE PROGNOSTIC LEVELS (NECESSARY FOR THE SATURATION MIXING
!     RATIO COMPUTATIONS) HAVE BEEN COMPUTED ASSUMING A 0.5/0.5 LINEAR
!     INTERPOLATION. IT RETURNS ITS OUTPUT TO THE SAME SPACE: TENDENCIES
!     OF T AND Q AND PRECIPITATION FLUXES FOR RAIN AND SNOW.

!     METHOD.
!     -------

!          THE ATMOSPHERIC COLUMN IS SCANNED FROM TOP TO BOTTOM
!     OBVIOUSLY. THE POSSIBLE MELTING OF INCOMING SNOW IN THE LAYER
!     IS ENVISAGED FIRST (NO ACTION IN THE UPMOST LAYER) AND OCCURS
!     AS SOON AS THE TEMPERATURE IS LARGER THAN 2 CELSIUS. THE
!     MELTING IS LIMITED BY THE SNOW AMOUNT EVIDENTLY AND THE
!     INDUCED COOLING WHICH SHOULD MAINTAIN THE TEMPERATURE ABOVE
!     THE 2 CELSIUS THRESHOLD BUT IRRESPECTIVELY OF ANY SATURATION
!     CRITERIA AT THIS STAGE.
!         THEN WITH REFERENCE TO THE NEW TEMPERATURE
!     AFTER MELTING A TEST OF THE
!     RELATIVE HUMIDITY IS FIRST PERFORMED. IF THERE IS SUPERSATURATION
!     TEMPERATURE AND RELATIVE HUMIDITY ARE MODIFIED TO BRING THE LAYER
!     BACK TO A STATE OF QUASI EXACT SATURATION (ESTIMATED FROM A FIRST
!     ORDER *TAYLOR DEVELOPMENT OF QS(T) FROM THE INITIAL TEMPERATURE).
!     THE AMOUNT OF PRECIPITATIONS OBTAINED IS ASSIGNED TO THE SNOW OR
!     RAIN FLUX DEPENDING ON THE LOCAL TEMPERATURE RELATIVE TO THE
!     FREEZING POINT OF WATER. IF THERE IS UNDERSATURATION THE
!     REMAINING PRECIPITATION (SNOW + RAIN) EVAPORATES
!     ACROSS THE LAYER:THE RATE IS SUCH AS TO EFFECTIVELY MOISTEN THE
!     SUB CLOUD-LAYERS.
!          AT THE END THE SURFACE VALUES FOR RAIN AND SNOW FALL ARE
!     PREPARED.

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----
!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KLEV*         NUMBER OF LEVELS

!     INPUT PARAMETERS (LOGICAL)

!     INPUT PARAMETERS (REAL)

!    *PTSPHY*       TIME STEP FOR THE PHYSICS                      S
!    *PTM1*         TEMPERATURE (T-1)                              K
!    *PQM1*         SPECIFIC HUMIDITY (T-1)                      KG/KG
!    *PAPHP1*         PROVISIONAL PRESSURE ON HALF LEVELS         PA
!    *PAPP1*          PROVISIONAL PRESSURE ON FULL LEVELS         PA

!    UPDATED PARAMETERS (REAL):

!    *PTENT*        TEMPERATURE TENDENCY                          K/S
!    *PGTENT*       TEMPERATURE TENDENCY DEFINING THE UPDATED STATE  K/S
!    *PTENQ*        MOISTURE TENDENCY                            KG/(KG S)
!    *PGTENQ*       MOISTURE TENDENCY DEFINING THE UPDATED STATE KG/(KG S)

!    OUTPUT PARAMETERS (REAL):

!    *PFHPSL*       ENTHALPY FLUX DUE TO LARGE SCALE RAIN         J/(M2*S)
!    *PFHPSN*       ENTHALPY FLUX DUE TO LARGE SCALE SNOW         J/(M2*S)
!    *PFPLSL*       LARGE SCALE RAIN FLUX                        KG/(M2*S)
!    *PFPLSN*       LARGE SCALE SNOW FLUX                        KG/(M2*S)

!     EXTERNALS.
!     ----------

!          *CUADJTQ*   ADJUST T AND Q DUE TO CONDENSATION

!     REFERENCE.
!     ----------

!          SEE LARGE SCALE PHASE CHANGES' PART OF THE MODEL'S
!     DOCUMENTATION FOR DETAILS ABOUT THE MATHEMATICS OF THIS ROUTINE.

!     AUTHOR.
!     -------

!     MODIFICATIONS
!     -------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      M.Ko"hler     03-Dec-2004 cpmoist using RVTMP2
!      F. Vana       18-May-2012 cleaning, unifying SL phys and non-SL phys
!     ------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,R        ,RCPD     ,&
 & RLVTT    ,RLSTT    ,RLMLT    ,RTT  , YRCST
USE YOETHF   , ONLY : R2ES     ,R3LES    ,R3IES    ,R4LES    ,&
 & R4IES    ,R5LES    ,R5IES    ,R5ALVCP  ,R5ALSCP  ,&
 & RALVDCP  ,RALSDCP  ,RTWAT    ,RTICE   ,RTICECU   ,&
 & RTWAT_RTICE_R      ,RTWAT_RTICECU_R   ,RVTMP2  , YRTHF
USE YOECND   , ONLY : TECND
USE YOEPHLI  , ONLY : TEPHLI

IMPLICIT NONE

TYPE(TECND)       ,INTENT(INOUT) :: YDECND
TYPE(TEPHLI)      ,INTENT(INOUT) :: YDEPHLI
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSPHY 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHP1(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPP1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGTENQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFHPSN(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFPLSN(KLON,KLEV+1) 

LOGICAL ::  LLFLAG(KLON)
REAL(KIND=JPRB) :: ZRFL(KLON),                 ZSFL(KLON),&
 & ZTP1(KLON),                 ZQP1(KLON),&
 & ZLVDCP(KLON),               ZLSDCP(KLON),&
 & ZDP(KLON),                  ZDR(KLON),                  ZLFDCP(KLON),&
 & ZRFLN(KLON),                ZSFLN(KLON),&
 & ZFLN(KLON),                 &
 & ZDTDT(KLON),                ZDQDT(KLON)  
REAL(KIND=JPRB) :: ZPP(KLON)     , ZQOLD(KLON)
REAL(KIND=JPRB) :: ZTT(KLON,KLEV), ZQQ(KLON,KLEV)

INTEGER(KIND=JPIM) :: ICALL, IK, JK, JL

REAL(KIND=JPRB) :: Z4S, ZALFAW, ZCEVAP, ZCONS, &
 & ZCONS2, ZMELTP2, ZRN, ZSN, ZSNMLT, ZTMST, &
 & ZZZ  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "cuadjtq.intfb.h"
#include "cuadjtqs.intfb.h"

#include "fcttre.func.h"
!     ------------------------------------------------------------------

!*    Computational constants
!     -----------------------

IF (LHOOK) CALL DR_HOOK('COND',0,ZHOOK_HANDLE)
ASSOCIATE(REPFLM=>YDECND%REPFLM, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, RLPEVAP=>YDEPHLI%RLPEVAP, RLPTRC=>YDEPHLI%RLPTRC)
ZTMST=PTSPHY
ZCONS2 =1.0_JPRB/(ZTMST*RG)
ZMELTP2=RTT+2.0_JPRB

!     Fraction of rainfall evaporation in subsaturated layers
!     -------------------------------------------------------

IF (LPHYLIN) THEN
  ZCEVAP=RLPEVAP
ELSE
  ZCEVAP=0.95_JPRB
ENDIF

!     ------------------------------------------------------------------

!*         2.     TOP BOUNDARY CONDITION AND START OF VERTICAL LOOP.
!                 --- -------- --------- --- ----- -- -------- -----

!*         2.1     SET TO ZERO PRECIPITATION FLUXES AT THE TOP.

DO JL=KIDIA,KFDIA
  ZRFL(JL)=0.0_JPRB
  ZSFL(JL)=0.0_JPRB
  PFPLSL(JL,1)=0.0_JPRB
  PFPLSN(JL,1)=0.0_JPRB
ENDDO

!*         2.2     VERTICAL LOOP.

DO JK=1,KLEV

!     ------------------------------------------------------------------

!*         3.     COMPUTATION OF THE CONDENSATION RATES.
!                 ----------- -- --- ------------ ------

!*         3.1     T AND Q PROVISIONAL VALUES AT T+1 AND EFFECTIVES L/CP
!*                 FOR VAPORIZATION AND SUBLIMATION.

  DO JL=KIDIA,KFDIA
    ZTP1(JL)=PTM1(JL,JK)+ZTMST*PGTENT(JL,JK)
    ZQP1(JL)=PQM1(JL,JK)+ZTMST*PGTENQ(JL,JK)
  ENDDO

  DO JL=KIDIA,KFDIA
    ZDP(JL)=PAPHP1(JL,JK+1)-PAPHP1(JL,JK)
    ZZZ=1.0_JPRB/(RCPD+RCPD*RVTMP2*ZQP1(JL))
    ZLFDCP(JL)=RLMLT*ZZZ
    ZLVDCP(JL)=RLVTT*ZZZ
    ZLSDCP(JL)=RLSTT*ZZZ
    ZPP(JL)=PAPP1(JL,JK)
    ZTT(JL,JK)=ZTP1(JL)
    ZQQ(JL,JK)=ZQP1(JL)
    ZQOLD(JL)=ZQP1(JL)
    LLFLAG(JL)=.TRUE.
  ENDDO

!*         3.2    MELTING OF INCOMING SNOW

  DO JL=KIDIA,KFDIA
    IF (ZSFL(JL) /= 0.0_JPRB) THEN
      ZCONS=ZCONS2*ZDP(JL)/ZLFDCP(JL)
      ZSNMLT=MIN(ZSFL(JL),ZCONS*MAX(0.0_JPRB,(ZTP1(JL)-ZMELTP2)))
      ZRFLN(JL)=ZRFL(JL)+ZSNMLT
      ZSFLN(JL)=ZSFL(JL)-ZSNMLT
      ZTP1(JL)=ZTP1(JL)-ZSNMLT/ZCONS
    ELSE
      ZRFLN(JL)=ZRFL(JL)
      ZSFLN(JL)=ZSFL(JL)
    ENDIF
  ENDDO

!*              3.3    NEW PRECIPITATION FLUXES AFTER CONDENSATION
!*                     AND TOTAL FLUXES

  IK=JK
  ICALL=0
  IF (LPHYLIN) THEN
    CALL CUADJTQS ( YRTHF, YRCST, KIDIA, KFDIA, KLON, KLEV, IK,&
     & ZPP  , ZTT  , ZQQ , LLFLAG, ICALL  )  
  ELSE
    CALL CUADJTQ(YRTHF, YRCST, YDEPHLI,KIDIA,KFDIA,KLON,KLEV,IK,ZPP,ZTT,ZQQ,LLFLAG,ICALL)
  ENDIF

  DO JL=KIDIA,KFDIA
    ZDR(JL)=ZCONS2*ZDP(JL)*(ZQOLD(JL)-ZQQ(JL,JK))
    IF (LPHYLIN) THEN
      ZALFAW=0.545_JPRB*(1.0_JPRB+TANH(0.17_JPRB*(ZTP1(JL)-RLPTRC)))
    ELSE
      ZALFAW=FOEALFA(ZTP1(JL))
    ENDIF
    ZRN=    ZALFAW *MAX(0.0_JPRB,ZDR(JL))
    ZSN=(1.0_JPRB-ZALFAW)*MAX(0.0_JPRB,ZDR(JL))

!          NEW PRECIPITATION FLUXES

    ZRFLN(JL)=ZRFLN(JL)+ZRN
    ZSFLN(JL)=ZSFLN(JL)+ZSN

!          TOTAL FLUXES

    ZFLN(JL)=ZRFLN(JL)+ZSFLN(JL)

  ENDDO

!*         3.4     AT TOP LAYER OR IF THERE ARE NO PRECIPITATION AVOID
!*                 EVAPORATION COMPUTATION

!     ------------------------------------------------------------------

!*         4.     COMPUTATION OF EVAPORATION
!*                ----------- -- -----------

  DO JL=KIDIA,KFDIA
    IF (JK > 1.AND.ZFLN(JL) /= 0.0_JPRB) THEN
      ZFLN(JL)=MAX(0.0_JPRB,ZFLN(JL)+MIN(0.0_JPRB,ZDR(JL)*ZCEVAP))
      Z4S=MAX(ZRFLN(JL)+ZSFLN(JL),REPFLM)
      ZRFLN(JL)=ZRFLN(JL)*ZFLN(JL)/Z4S
      ZSFLN(JL)=ZSFLN(JL)*ZFLN(JL)/Z4S
    ENDIF
  ENDDO

!     ------------------------------------------------------------------

!*         6.     INCREMENTATION OF T AND Q TENDENCIES AND FLUXES' SWAP.
!                 -------------- -- - --- - ---------- --- ------- -----

!*         6.2     MODIFICATION OF THE T AND Q TENDENCIES.

  DO JL=KIDIA,KFDIA
    ZDQDT(JL)=-((ZRFLN(JL)-ZRFL(JL))+(ZSFLN(JL)-ZSFL(JL)))*(RG/ZDP(JL))
    PTENQ(JL,JK)=ZDQDT(JL)
    ZDTDT(JL)=(ZLVDCP(JL)*(ZRFLN(JL)-ZRFL(JL))+ZLSDCP(JL)&
     & *(ZSFLN(JL)-ZSFL(JL)))*(RG/ZDP(JL))  
    PTENT(JL,JK)=ZDTDT(JL)
    PFPLSL(JL,JK+1)=ZRFLN(JL)
    PFPLSN(JL,JK+1)=ZSFLN(JL)
  ENDDO

!*         6.4     SWAP OF THE FLUXES AND END OF THE VERTICAL LOOP.

  DO JL=KIDIA,KFDIA
    ZRFL(JL)=ZRFLN(JL)
    ZSFL(JL)=ZSFLN(JL)
  ENDDO

ENDDO

!     ------------------------------------------------------------------

!*         7.     ENTHALPY FLUXES DUE TO PRECIPITATION
!                 ------------------------------------

DO JK=1,KLEV+1
  DO JL=KIDIA,KFDIA
    PFHPSL(JL,JK)=-PFPLSL(JL,JK)*RLVTT
    PFHPSN(JL,JK)=-PFPLSN(JL,JK)*RLSTT
  ENDDO
ENDDO

!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('COND',1,ZHOOK_HANDLE)
END SUBROUTINE COND
