#ifdef RS6K
@PROCESS HOT NOSTRICT
#endif
SUBROUTINE CUADJTQ &
 & (YDTHF, YDCST, YDEPHLI,KIDIA,    KFDIA,    KLON,    KLEV,     KK,&
 &  PSP,      PT,       PQ,      LDFLAG,   KCALL,   LDOFLAG)  

!          PURPOSE.
!          --------
!          TO PRODUCE T,Q AND L VALUES FOR CLOUD ASCENT

!          INTERFACE
!          ---------
!          THIS ROUTINE IS CALLED FROM SUBROUTINES:
!              *COND*     (T AND Q AT CONDENSATION LEVEL)
!              *CUBASE*   (T AND Q AT CONDENSATION LEVEL)
!              *CUASC*    (T AND Q AT CLOUD LEVELS)
!              *CUINI*    (ENVIRONMENTAL T AND QS VALUES AT HALF LEVELS)
!              *CUSTRAT*  (T AND Q AT CONDENSATION LEVEL)
!          INPUT ARE UNADJUSTED T AND Q VALUES,
!          IT RETURNS ADJUSTED VALUES OF T AND Q

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----
!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KLEV*         NUMBER OF LEVELS
!    *KK*           LEVEL
!    *KCALL*        DEFINES CALCULATION AS
!                      KCALL=0  ENV. T AND QS IN*CUINI*
!                      KCALL=1  CONDENSATION IN UPDRAFTS  (E.G. CUBASE, CUASC)
!                      KCALL=2  EVAPORATION IN DOWNDRAFTS (E.G. CUDLFS,CUDDRAF)

!     INPUT PARAMETERS (LOGICAL):

!    *LDLAND*       LAND-SEA MASK (.TRUE. FOR LAND POINTS)

!     INPUT PARAMETERS (REAL):

!    *PSP*          PRESSURE                                        PA

!     UPDATED PARAMETERS (REAL):

!    *PT*           TEMPERATURE                                     K
!    *PQ*           SPECIFIC HUMIDITY                             KG/KG

!          EXTERNALS   
!          ---------
!          3 LOOKUP TABLES ( TLUCUA, TLUCUB, TLUCUC )
!          FOR CONDENSATION CALCULATIONS.
!          THE TABLES ARE INITIALISED IN *SUPHEC*.

!     AUTHOR.
!     -------
!      M.TIEDTKE         E.C.M.W.F.     12/89

!     MODIFICATIONS.
!     --------------
!      J.HAGUE               03-01-13   MASS Vector Functions
!      J.HAGUE               03-07-07   More MASS V.F.
!      M.Hamrud              01-Oct-2003 CY28 Cleaning
!      J.Hague & D.Salmond   22-Nov-2005 Optimisations 
!----------------------------------------------------------------------

USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST
USE YOETHF   , ONLY : TTHF  
USE YOEPHLI  , ONLY : TEPHLI

IMPLICIT NONE

TYPE(TTHF)        ,INTENT(IN)    :: YDTHF
TYPE(TCST)        ,INTENT(IN)    :: YDCST
TYPE(TEPHLI)      ,INTENT(IN)    :: YDEPHLI
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KK 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSP(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQ(KLON,KLEV) 
LOGICAL           ,INTENT(IN)    :: LDFLAG(KLON) 
INTEGER(KIND=JPIM),INTENT(IN)    :: KCALL 
LOGICAL  ,OPTIONAL,INTENT(IN)    :: LDOFLAG(KLON) 

INTEGER(KIND=JPIM) :: JL

REAL(KIND=JPRB) :: Z1S, Z2S, ZCOND,ZCOND1, ZCOR, ZFOEEWI, ZFOEEWL,&
 & ZOEALFA, ZQMAX, ZQSAT, ZTARG, ZQP
REAL(KIND=JPRB) :: ZL, ZI, ZF

LOGICAL :: LLFLAG(KLON)

#include "abor1.intfb.h"

!DIR$ VFUNCTION EXPHF
#include "fcttre.ycst.h"

!     STATEMENT FUNCTIONS

REAL(KIND=JPRB) :: MINJ, MAXJ, X, Y
REAL(KIND=JPRB) :: ZHOOK_HANDLE

MINJ(X,Y) = Y - 0.5_JPRB*(ABS(X-Y)-(X-Y))
MAXJ(X,Y) = Y + 0.5_JPRB*(ABS(X-Y)+(X-Y))

!----------------------------------------------------------------------

!     1.           DEFINE CONSTANTS
!                  ----------------

#ifdef DO_DR_HOOK_CUADJTQ
IF (LHOOK) CALL DR_HOOK('CUADJTQ',0,ZHOOK_HANDLE)
#endif
ASSOCIATE(LPHYLIN=>YDEPHLI%LPHYLIN, RLPAL1=>YDEPHLI%RLPAL1, &
 & RLPAL2=>YDEPHLI%RLPAL2, RLPTRC=>YDEPHLI%RLPTRC)

ZQMAX=0.5_JPRB

!*********************************************
IF (.NOT.LPHYLIN) THEN
!*********************************************                 

!     2.           CALCULATE CONDENSATION AND ADJUST T AND Q ACCORDINGLY
!                  -----------------------------------------------------

  IF (KCALL == 1 .OR. KCALL == 6 ) THEN

!   mixed phase saturation

      LLFLAG(KIDIA:KFDIA) = LDFLAG(KIDIA:KFDIA)
      IF(KCALL == 6) THEN
        IF(PRESENT(LDOFLAG)) THEN
          LLFLAG(KIDIA:KFDIA) = LLFLAG(KIDIA:KFDIA) .AND. .NOT.LDOFLAG(KIDIA:KFDIA)
        ELSE
          CALL ABOR1("CUADJTQ: LDOFLAG has to be present when KCALL==6")
        ENDIF
      ENDIF

      DO JL=KIDIA,KFDIA
      IF (LLFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)
        ZL=1.0_JPRB/(PT(JL,KK)-YDTHF%R4LES)
        ZI=1.0_JPRB/(PT(JL,KK)-YDTHF%R4IES)
!       ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP
        ZQSAT=YDTHF%R2ES *(FOEALFCU(PT(JL,KK))*EXP(YDTHF%R3LES*(PT(JL,KK)-YDCST%RTT)*ZL)+&
          &(1.0_JPRB-FOEALFCU(PT(JL,KK)))*EXP(YDTHF%R3IES*(PT(JL,KK)-YDCST%RTT)*ZI))
        ZQSAT=ZQSAT*ZQP
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB-YDCST%RETV*ZQSAT
        ZF=FOEALFCU(PT(JL,KK))*YDTHF%R5ALVCP*ZL**2 +&
          &(1.0_JPRB-FOEALFCU(PT(JL,KK)))*YDTHF%R5ALSCP*ZI**2
        ZCOND=(PQ(JL,KK)*ZCOR**2-ZQSAT*ZCOR)/(ZCOR**2+ZQSAT*ZF)
        ZCOND=MAX(ZCOND,0.0_JPRB)
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND
        PQ(JL,KK)=PQ(JL,KK)-ZCOND
        ZL=1.0_JPRB/(PT(JL,KK)-YDTHF%R4LES)
        ZI=1.0_JPRB/(PT(JL,KK)-YDTHF%R4IES)
!       ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP
        ZQSAT=YDTHF%R2ES *(FOEALFCU(PT(JL,KK))*EXP(YDTHF%R3LES*(PT(JL,KK)-YDCST%RTT)*ZL)+&
          &(1.0_JPRB-FOEALFCU(PT(JL,KK)))*EXP(YDTHF%R3IES*(PT(JL,KK)-YDCST%RTT)*ZI))
        ZQSAT=ZQSAT*ZQP
        ZQSAT=MINJ(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB-YDCST%RETV*ZQSAT
        ZF=FOEALFCU(PT(JL,KK))*YDTHF%R5ALVCP*ZL**2 +&
          &(1.0_JPRB-FOEALFCU(PT(JL,KK)))*YDTHF%R5ALSCP*ZI**2
        ZCOND1=(PQ(JL,KK)*ZCOR**2-ZQSAT*ZCOR)/(ZCOR**2+ZQSAT*ZF)
        IF(ZCOND ==  0.0_JPRB)ZCOND1=0.0_JPRB
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDIF
      ENDDO

      IF(KCALL==6) THEN
  
        DO JL=KIDIA,KFDIA
        IF(LDFLAG(JL).AND.LDOFLAG(JL)) THEN
          ZQP    =1.0_JPRB/PSP(JL)
          ZL=1.0_JPRB/(PT(JL,KK)-YDTHF%R4LES)
          ZQSAT=YDTHF%R2ES *EXP(YDTHF%R3LES*(PT(JL,KK)-YDCST%RTT)*ZL)
          ZQSAT=ZQSAT*ZQP
          ZQSAT=MIN(0.5_JPRB,ZQSAT)
          ZCOR=1.0_JPRB-YDCST%RETV*ZQSAT
          ZF=YDTHF%R5ALVCP*ZL**2
          ZCOND=(PQ(JL,KK)*ZCOR**2-ZQSAT*ZCOR)/(ZCOR**2+ZQSAT*ZF)
          ZCOND=MAX(ZCOND,0.0_JPRB)
          PT(JL,KK)=PT(JL,KK)+YDTHF%RALVDCP*ZCOND
          PQ(JL,KK)=PQ(JL,KK)-ZCOND
          ZL=1.0_JPRB/(PT(JL,KK)-YDTHF%R4LES)
          ZQSAT=YDTHF%R2ES *EXP(YDTHF%R3LES*(PT(JL,KK)-YDCST%RTT)*ZL)
          ZQSAT=ZQSAT*ZQP
          ZQSAT=MINJ(0.5_JPRB,ZQSAT)
          ZCOR=1.0_JPRB-YDCST%RETV*ZQSAT
          ZF=YDTHF%R5ALVCP*ZL**2
          ZCOND1=(PQ(JL,KK)*ZCOR**2-ZQSAT*ZCOR)/(ZCOR**2+ZQSAT*ZF)
          IF(ZCOND ==  0.0_JPRB)ZCOND1=0.0_JPRB
          PT(JL,KK)=PT(JL,KK)+YDTHF%RALVDCP*ZCOND1
          PQ(JL,KK)=PQ(JL,KK)-ZCOND1
        ENDIF
        ENDDO
  
      ENDIF

  ENDIF

  IF(KCALL == 2) THEN

!DIR$    IVDEP
!OCL NOVREC
    DO JL=KIDIA,KFDIA
      IF(LDFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)
        ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP    
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEMCU(PT(JL,KK)))
        ZCOND=MIN(ZCOND,0.0_JPRB)
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND
        PQ(JL,KK)=PQ(JL,KK)-ZCOND
        ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP    
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEMCU(PT(JL,KK)))
        IF(ZCOND == 0.0_JPRB)ZCOND1=MIN(ZCOND1,0.0_JPRB)
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDIF
    ENDDO

  ENDIF

  IF(KCALL == 0) THEN

!DIR$    IVDEP
!OCL NOVREC

!DIR$ LOOP_INFO EST_TRIPS(16)
    DO JL=KIDIA,KFDIA
      ZQP    =1.0_JPRB/PSP(JL)
      ZQSAT=FOEEWM(PT(JL,KK))*ZQP    
      ZQSAT=MIN(0.5_JPRB,ZQSAT)
      ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
      ZQSAT=ZQSAT*ZCOR
      ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
      PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND1
      PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ZQSAT=FOEEWM(PT(JL,KK))*ZQP    
      ZQSAT=MIN(0.5_JPRB,ZQSAT)
      ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
      ZQSAT=ZQSAT*ZCOR
      ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
      PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND1
      PQ(JL,KK)=PQ(JL,KK)-ZCOND1
    ENDDO

  ENDIF

  IF(KCALL == 4 )THEN

    DO JL=KIDIA,KFDIA
      IF(LDFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)
        ZQSAT=FOEEWM(PT(JL,KK))*ZQP
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND
        PQ(JL,KK)=PQ(JL,KK)-ZCOND
        ZQSAT=FOEEWM(PT(JL,KK))*ZQP
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDIF
    ENDDO
      
  ENDIF 

  IF(KCALL == 5) THEN  ! Same as 4 but with LDFLAG all true

!OCL NOVREC
!DIR$    IVDEP
!DIR$ LOOP_INFO EST_TRIPS(16)
      DO JL=KIDIA,KFDIA
        ZQP    =1.0_JPRB/PSP(JL)
        ZQSAT=FOEEWM(PT(JL,KK))*ZQP    
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND
        PQ(JL,KK)=PQ(JL,KK)-ZCOND
        ZQSAT=FOEEWM(PT(JL,KK))*ZQP    
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEM(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPM(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDDO
  ENDIF 

  IF(KCALL == 3) THEN 
!DIR$ LOOP_INFO EST_TRIPS(16)
      DO JL=KIDIA,KFDIA
        ZQP    =1.0_JPRB/PSP(JL)
        ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEMCU(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
        ZQSAT=FOEEWMCU(PT(JL,KK))*ZQP
        ZQSAT=MIN(0.5_JPRB,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*FOEDEMCU(PT(JL,KK)))
        PT(JL,KK)=PT(JL,KK)+FOELDCPMCU(PT(JL,KK))*ZCOND1
        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDDO

  ENDIF
!*********************************************
ELSE
!*********************************************                 

!     2.           CALCULATE CONDENSATION AND ADJUST T AND Q ACCORDINGLY
!                  -----------------------------------------------------

  IF (KCALL == 1 ) THEN

!DIR$    IVDEP
!OCL NOVREC
!DIR$ LOOP_INFO EST_TRIPS(16)
    DO JL=KIDIA,KFDIA
      IF(LDFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)
        ZTARG=PT(JL,KK)
        ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
        ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
        ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
        ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
        Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
        ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)

        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR

        Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
         & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)

        ZCOND=MAX(ZCOND,0.0_JPRB)

        IF(ZCOND /= 0.0_JPRB) THEN

          PT(JL,KK)=PT(JL,KK)+&
           & (ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND  
          PQ(JL,KK)=PQ(JL,KK)-ZCOND
          ZTARG=PT(JL,KK)
          ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
          ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
          ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
          ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
          Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
          ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)
  
          ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
          ZQSAT=ZQSAT*ZCOR
  
          Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
           & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
          ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)
  
          PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND1
  
          PQ(JL,KK)=PQ(JL,KK)-ZCOND1
        ENDIF
      ENDIF
    ENDDO

  ENDIF

  IF(KCALL == 2) THEN

!DIR$    IVDEP
!OCL NOVREC
    DO JL=KIDIA,KFDIA
      IF(LDFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)

        ZTARG=PT(JL,KK)
        ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
        ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
        ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
        ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
        Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
        ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)

        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR

        Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
         & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)

        ZCOND=MIN(ZCOND,0.0_JPRB)

        IF(ZCOND /= 0.0_JPRB) THEN

          PT(JL,KK)=PT(JL,KK)+&
           & (ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND  
          PQ(JL,KK)=PQ(JL,KK)-ZCOND
          ZTARG=PT(JL,KK)
          ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
          ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
          ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
          ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
          Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
          ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)
  
          ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
          ZQSAT=ZQSAT*ZCOR
  
          Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
           & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
          ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)
  
          PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND1
  
          PQ(JL,KK)=PQ(JL,KK)-ZCOND1
        ENDIF
      ENDIF
    ENDDO

  ENDIF

  IF(KCALL == 0) THEN

!DIR$    IVDEP
!OCL NOVREC
    DO JL=KIDIA,KFDIA
      ZQP    =1.0_JPRB/PSP(JL)

      ZTARG=PT(JL,KK)
      ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
      ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
      ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
      ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
      Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
      ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)

      ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
      ZQSAT=ZQSAT*ZCOR

      Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
       & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
      ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)

      PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND1

      PQ(JL,KK)=PQ(JL,KK)-ZCOND1

      ZTARG=PT(JL,KK)
      ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
      ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
      ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
      ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
      Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
      ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)

      ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
      ZQSAT=ZQSAT*ZCOR

      Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
       & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
      ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)

      PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND1

      PQ(JL,KK)=PQ(JL,KK)-ZCOND1
    ENDDO

  ENDIF

  IF(KCALL == 4) THEN

!DIR$    IVDEP
!OCL NOVREC
    DO JL=KIDIA,KFDIA
      IF(LDFLAG(JL)) THEN
        ZQP    =1.0_JPRB/PSP(JL)

        ZTARG=PT(JL,KK)
        ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
        ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
        ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
        ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
        Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
        ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)
        
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        
        Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
         & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
        ZCOND=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)
        
        PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND
        
        PQ(JL,KK)=PQ(JL,KK)-ZCOND
        
        ZTARG=PT(JL,KK)
        ZOEALFA=0.5_JPRB*(TANH(RLPAL1*(ZTARG-RLPTRC))+1.0_JPRB)
        ZFOEEWL=YDTHF%R2ES*EXP(YDTHF%R3LES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4LES))
        ZFOEEWI=YDTHF%R2ES*EXP(YDTHF%R3IES*(ZTARG-YDCST%RTT)/(ZTARG-YDTHF%R4IES))
        ZQSAT=ZQP    *(ZOEALFA*ZFOEEWL+(1.0_JPRB-ZOEALFA)*ZFOEEWI)
        Z1S=TANH(RLPAL2*(ZQSAT-ZQMAX))
        ZQSAT=0.5_JPRB*((1.0_JPRB-Z1S)*ZQSAT+(1.0_JPRB+Z1S)*ZQMAX)
        
        ZQSAT=MIN(ZQMAX,ZQSAT)
        ZCOR=1.0_JPRB/(1.0_JPRB-YDCST%RETV  *ZQSAT)
        ZQSAT=ZQSAT*ZCOR
        
        Z2S=    ZOEALFA *YDTHF%R5ALVCP*(1.0_JPRB/(ZTARG-YDTHF%R4LES)**2)+&
         & (1.0_JPRB-ZOEALFA)*YDTHF%R5ALSCP*(1.0_JPRB/(ZTARG-YDTHF%R4IES)**2)  
        ZCOND1=(PQ(JL,KK)-ZQSAT)/(1.0_JPRB+ZQSAT*ZCOR*Z2S)
        
        PT(JL,KK)=PT(JL,KK)+(ZOEALFA*YDTHF%RALVDCP+(1.0_JPRB-ZOEALFA)*YDTHF%RALSDCP)*ZCOND1

        PQ(JL,KK)=PQ(JL,KK)-ZCOND1
      ENDIF
    ENDDO

  ENDIF

!*********************************************
ENDIF
!*********************************************                 

END ASSOCIATE
#ifdef DO_DR_HOOK_CUADJTQ
IF (LHOOK) CALL DR_HOOK('CUADJTQ',1,ZHOOK_HANDLE)
#endif
END SUBROUTINE CUADJTQ
