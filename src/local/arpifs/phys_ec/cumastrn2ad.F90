SUBROUTINE CUMASTRN2AD &
 & (  YDTHF, YDCST, YDML_PHY_SLIN,   YDML_PHY_EC,&
 & KIDIA,    KFDIA,    KLON,    KLEV,&
 & LDLAND,   LDRAIN1D, PTSPHY,&
 & PTEN5,    PQEN5,    PUEN5,    PVEN5,&
 & PVERVEL5, PQSEN5,   PQHFL5,   PAHFS5,&
 & PAP5,     PAPH5,    PGEO5,    PGEOH5, PGAW,&
 & PTENT5,   PTENQ5,   PTENU5,   PTENV5,&
 & LDCUM,    KTYPE,    KCBOT,    KCTOP,&
 & KBOTSC,   LDSC,&
 & PTU5,     PQU5,     PLU5,     PLUDE5,&
 & PENTH5,   PMFLXR5,  PMFLXS5,  PRAIN5,&
 & PMFU5,    PMFD5,&
 & KTRAC,    PCEN5,    PTENC5,   PSCAV5,&
 & PTEN,     PQEN,     PUEN,     PVEN,&
 & PVERVEL,  PQSEN,    PQHFL,    PAHFS,&
 & PAP,      PAPH,     PGEO,     PGEOH,&
 & PTENT,    PTENQ,    PTENU,    PTENV,&
 & PTU,      PQU,      PLU,      PLUDE,&
 & PENTH,    PMFLXR,   PMFLXS,   PRAIN,&
 & PMFU,     PMFD,     PCAPE,&
 & PCEN,     PTENC )       

!**** *CUMASTRN2AD*  MASTER ROUTINE FOR SIMPLIFIED CUMULUS MASSFLUX-SCHEME
!                   (Adjoint)

!     P. LOPEZ   E.C.M.W.F      15/12/2003

!     Adapted from

!     M.TIEDTKE      E.C.M.W.F.     1986/1987/1989
!     D.GREGORY      E.C.M.W.F.     1996

!     PURPOSE
!     -------

!          THIS ROUTINE COMPUTES THE PHYSICAL TENDENCIES OF THE
!     PROGNOSTIC VARIABLES T,Q,U AND V DUE TO CONVECTIVE PROCESSES.
!     PROCESSES CONSIDERED ARE: CONVECTIVE FLUXES, FORMATION OF
!     PRECIPITATION, EVAPORATION OF FALLING RAIN BELOW CLOUD BASE,
!     SATURATED CUMULUS DOWNDRAFTS.

!**   INTERFACE.
!     ----------

!          *CUMASTRN2AD* IS CALLED FROM *CUCALLN2AD*
!     THE ROUTINE TAKES ITS INPUT FROM THE LONG-TERM STORAGE
!     T,Q,U,V,PHI AND P AND MOISTURE TENDENCIES.
!     IT RETURNS ITS OUTPUT TO THE SAME SPACE
!      1.MODIFIED TENDENCIES OF MODEL VARIABLES
!      2.RATES OF CONVECTIVE PRECIPITATION
!        (USED IN SUBROUTINE SURF)
!      3.CLOUD BASE, CLOUD TOP AND PRECIP FOR RADIATION
!        (USED IN SUBROUTINE CLOUD)

!     METHOD
!     -------

!     PARAMETERIZATION IS DONE USING A MASSFLUX-SCHEME.
!        (1) DEFINE CONSTANTS AND PARAMETERS
!        (2) SPECIFY VALUES (T,Q,QS...) AT HALF LEVELS AND
!            INITIALIZE UPDRAFT- AND DOWNDRAFT-VALUES IN 'CUININ2'
!        (3) CALCULATE CLOUD BASE IN 'CUBASEN2'
!            AND SPECIFY CLOUD BASE MASSFLUX FROM PBL MOISTURE BUDGET
!        (4) DO CLOUD ASCENT IN 'CUASCN2' IN ABSENCE OF DOWNDRAFTS
!        (5) DO DOWNDRAFT CALCULATIONS IN CUDDRAFN2
!        (6) SCALING OF MASSFLUX CONSIDERING THE CAPE CLOSURE
!        (7) DO FINAL ADJUSMENTS TO CONVECTIVE FLUXES IN 'CUFLXN2',
!            DO EVAPORATION IN SUBCLOUD LAYER
!        (8) CALCULATE INCREMENTS OF T AND Q IN 'CUDTDQN2'
!        (9) CALCULATE INCREMENTS OF U AND V IN 'CUDUDV2'

!     PARAMETER     DESCRIPTION                                   UNITS
!     ---------     -----------                                   -----
!     INPUT PARAMETERS (INTEGER):

!    *KIDIA*        START POINT
!    *KFDIA*        END POINT
!    *KLON*         NUMBER OF GRID POINTS PER PACKET
!    *KLEV*         NUMBER OF LEVELS
!    *KTRAC*        NUMBER OF TRACERS

!     INPUT PARAMETERS (LOGICAL)

!    *LDLAND*       LAND SEA MASK (.TRUE. FOR LAND)

!     INPUT PARAMETERS (REAL)

!    *PTSPHY*       TIME STEP FOR THE PHYSICS                       S
!    *PTEN5*        PROVISIONAL ENVIRONMENT TEMPERATURE (T+1)    (Trajectory)
!    *PQEN5*        PROVISIONAL ENVIRONMENT SPEC. HUMIDITY (T+1) (Trajectory)
!    *PUEN5*        PROVISIONAL ENVIRONMENT U-VELOCITY (T+1)     (Trajectory)
!    *PVEN5*        PROVISIONAL ENVIRONMENT V-VELOCITY (T+1)     (Trajectory)
!    *PCEN5*        CHEMICAL TRACERS                             (Trajectory) 
!    *PVERVEL5*     VERTICAL VELOCITY                            (Trajectory)
!    *PQSEN5*       ENVIRONMENT SPEC. SATURATION HUMIDITY (T+1)  (Trajectory)
!    *PQHFL5*       MOISTURE FLUX (EXCEPT FROM SNOW EVAP.)       (Trajectory) 
!    *PAHFS5*       SENSIBLE HEAT FLUX                           (Trajectory)
!    *PAP5*         PROVISIONAL PRESSURE ON FULL LEVELS          (Trajectory)
!    *PAPH5*        PROVISIONAL PRESSURE ON HALF LEVELS          (Trajectory)
!    *PGEO5*        GEOPOTENTIAL                                 (Trajectory) 
!    *PGEOH5*       GEOPOTENTIAL ON HALF LEVELS                  (Trajectory) 
!    *PSCAV5*       SCAVENGING COEFFICIENTS                      (Trajectory) 

!    *PTEN*         PROVISIONAL ENVIRONMENT TEMPERATURE (T+1)       K
!    *PQEN*         PROVISIONAL ENVIRONMENT SPEC. HUMIDITY (T+1)  KG/KG
!    *PUEN*         PROVISIONAL ENVIRONMENT U-VELOCITY (T+1)       M/S
!    *PVEN*         PROVISIONAL ENVIRONMENT V-VELOCITY (T+1)       M/S
!    *PCEN*         CHEMICAL TRACERS                           
!    *PVERVEL*      VERTICAL VELOCITY                             PA/S
!    *PQSEN*        ENVIRONMENT SPEC. SATURATION HUMIDITY (T+1)   KG/KG
!    *PQHFL*        MOISTURE FLUX (EXCEPT FROM SNOW EVAP.)        KG/(SM2)
!    *PAHFS*        SENSIBLE HEAT FLUX                            W/M2
!    *PAP*          PROVISIONAL PRESSURE ON FULL LEVELS             PA
!    *PAPH*         PROVISIONAL PRESSURE ON HALF LEVELS             PA
!    *PGEO*         GEOPOTENTIAL                                  M2/S2
!    *PGEOH*        GEOPOTENTIAL ON HALF LEVELS                   M2/S2

!    UPDATED PARAMETERS (REAL):

!    *PTENT5*       TEMPERATURE TENDENCY                         (Trajectory)     
!    *PTENQ5*       MOISTURE TENDENCY                            (Trajectory)  
!    *PTENU5*       TENDENCY OF U-COMP. OF WIND                  (Trajectory)  
!    *PTENV5*       TENDENCY OF V-COMP. OF WIND                  (Trajectory)  
!    *PTENC5*       TRACER TENDENCY                              (Trajectory)

!    *PTENT*        TEMPERATURE TENDENCY                           K/S
!    *PTENQ*        MOISTURE TENDENCY                             KG/(KG S)
!    *PTENU*        TENDENCY OF U-COMP. OF WIND                    M/S2
!    *PTENV*        TENDENCY OF V-COMP. OF WIND                    M/S2
!    *PTENC*        TRACER TENDENCY                                1/S

!    OUTPUT PARAMETERS (LOGICAL):

!    *LDCUM*        FLAG: .TRUE. FOR CONVECTIVE POINTS 
!    *LDSC*         FLAG: .TRUE. FOR SC-POINTS

!    OUTPUT PARAMETERS (INTEGER):

!    *KTYPE*        TYPE OF CONVECTION
!                       1 = PENETRATIVE CONVECTION
!                       2 = SHALLOW CONVECTION
!                       3 = MIDLEVEL CONVECTION
!    *KCBOT*        CLOUD BASE LEVEL
!    *KCTOP*        CLOUD TOP LEVEL
!    *KBOTSC*       CLOUD BASE LEVEL FOR SC-CLOUDS

!    OUTPUT PARAMETERS (REAL):

!    *PTU5*         TEMPERATURE IN UPDRAFTS                      (Trajectory)    
!    *PQU5*         SPEC. HUMIDITY IN UPDRAFTS                   (Trajectory)   
!    *PLU5*         LIQUID WATER CONTENT IN UPDRAFTS             (Trajectory)   
!    *PLUDE5*       DETRAINED LIQUID WATER                       (Trajectory)    
!    *PENTH5*       INCREMENT OF DRY STATIC ENERGY               (Trajectory)    
!    *PMFLXR5*      CONVECTIVE RAIN FLUX                         (Trajectory)    
!    *PMFLXS5*      CONVECTIVE SNOW FLUX                         (Trajectory)    
!    *PRAIN5*       TOTAL PRECIP. PRODUCED IN CONV. UPDRAFTS     (Trajectory)    
!                   (NO EVAPORATION IN DOWNDRAFTS)
!    *PMFU*         MASSFLUX UPDRAFTS                            (Trajectory)
!    *PMFD*         MASSFLUX DOWNDRAFTS                          (Trajectory)

!    *PTU*          TEMPERATURE IN UPDRAFTS                         K
!    *PQU*          SPEC. HUMIDITY IN UPDRAFTS                    KG/KG
!    *PLU*          LIQUID WATER CONTENT IN UPDRAFTS              KG/KG
!    *PLUDE*        DETRAINED LIQUID WATER                        KG/(M3*S)
!    *PENTH*        INCREMENT OF DRY STATIC ENERGY                 J/(KG*S)
!    *PMFLXR*       CONVECTIVE RAIN FLUX                          KG/(M2*S)
!    *PMFLXS*       CONVECTIVE SNOW FLUX                          KG/(M2*S)
!    *PRAIN*        TOTAL PRECIP. PRODUCED IN CONV. UPDRAFTS      KG/(M2*S)
!                   (NO EVAPORATION IN DOWNDRAFTS)
!    *PMFU*         MASSFLUX UPDRAFTS                             KG/(M2*S)
!    *PMFD*         MASSFLUX DOWNDRAFTS                           KG/(M2*S)
!    *PCAPE*        CONVECTVE AVAILABLE POTENTIAL ENERGY           J/KG

!     EXTERNALS.
!     ----------

!     CUININ2AD:   INITIALIZES VALUES AT VERTICAL GRID USED IN CU-PARAMETR.
!     CUBASEN2AD:  CLOUD BASE CALCULATION FOR PENETR.AND SHALLOW CONVECTION
!     CUASCN2AD:   CLOUD ASCENT FOR ENTRAINING PLUME
!     CUDDRAFN2AD: DOES MOIST DESCENT FOR CUMULUS DOWNDRAFTS
!     CUFLX2AD:    FINAL ADJUSTMENTS TO CONVECTIVE FLUXES (ALSO IN PBL)
!     CUDQDTN2AD:  UPDATES TENDENCIES FOR T AND Q
!     CUDUDV2AD:   UPDATES TENDENCIES FOR U AND V

!     SWITCHES.
!     --------

!     LMFDD2=.TRUE.    CUMULUS DOWNDRAFTS SWITCHED ON
!     LMFDUDV2=.TRUE.  CUMULUS FRICTION SWITCHED ON

!     MODEL PARAMETERS (DEFINED IN SUBROUTINE SUCUMF2)
!     ------------------------------------------------
!     RMFDEPS    FRACTIONAL MASSFLUX FOR DOWNDRAFTS AT LFS

!     REFERENCE.
!     ----------

!          PAPER ON MASSFLUX SCHEME (TIEDTKE,1989)
!          PAPER ON LINEARIZED CONVECTION SCHEME (LOPEZ and MOREAU, 2005)

!     MODIFICATIONS
!     -------------
!     M.Hamrud     01-Oct-2003 CY28 Cleaning
!     P.Lopez      01-Oct-2005 New version of linearized convection
!     P.Lopez      14-02-2006  Added transport of tracers        
!     P.Lopez      11-01-2007  Modified use of LDRAIN switch       
!     P.Lopez      19-Oct-2007 Revised version to improve match to Tiedtke scheme
!     A.Geer       01-OCt-2008 LDRAIN1D name change to reflect usage
!     P.Bechtold   07-10-2009  Added KE dissipation
!     P.Lopez      13-Aug-2015 Revised mass flux limiter after Peter Bechtold's mods.
!     P.Lopez      15-Oct-2015 CAPE coming from CUPDRA
!     P.Lopez      18-Jul-2016 Added downdraught term in shallow convection closure.

!----------------------------------------------------------------------

USE MODEL_PHYSICS_ECMWF_MOD      , ONLY : MODEL_PHYSICS_ECMWF_TYPE
USE MODEL_PHYSICS_SIMPLINEAR_MOD , ONLY : MODEL_PHYSICS_SIMPLINEAR_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : TCST
USE YOETHF   , ONLY : TTHF          

IMPLICIT NONE

TYPE(TTHF)                         ,INTENT(IN)   :: YDTHF
TYPE(TCST)                         ,INTENT(IN)   :: YDCST
TYPE(MODEL_PHYSICS_ECMWF_TYPE)     ,INTENT(INOUT):: YDML_PHY_EC
TYPE(MODEL_PHYSICS_SIMPLINEAR_TYPE),INTENT(INOUT):: YDML_PHY_SLIN
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM),INTENT(IN)    :: KTRAC
LOGICAL           ,INTENT(IN)    :: LDLAND(KLON)
LOGICAL           ,INTENT(IN)    :: LDRAIN1D
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTSPHY
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTEN5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQEN5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUEN5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVEN5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCEN5(KLON,KLEV,KTRAC) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSCAV5(KTRAC) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVERVEL5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQSEN5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQHFL5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAHFS5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAP5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPH5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEO5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEOH5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGAW(KLON)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENV5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENC5(KLON,KLEV,KTRAC)
LOGICAL           ,INTENT(OUT)   :: LDCUM(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KTYPE(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KCBOT(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KCTOP(KLON)
INTEGER(KIND=JPIM),INTENT(OUT)   :: KBOTSC(KLON)
LOGICAL           ,INTENT(OUT)   :: LDSC(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLUDE5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PENTH5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFLXR5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFLXS5(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRAIN5(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFU5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMFD5(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTEN(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQEN(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PUEN(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVEN(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCEN(KLON,KLEV,KTRAC) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PVERVEL(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQSEN(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQHFL(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAHFS(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAP(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAPH(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEO(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGEOH(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENT(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENQ(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENV(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTENC(KLON,KLEV,KTRAC) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLUDE(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PENTH(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMFLXR(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMFLXS(KLON,KLEV+1)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRAIN(KLON) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMFU(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMFD(KLON,KLEV)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PCAPE(KLON) 

! Input/output trajectory arrays

INTEGER(KIND=JPIM) ::  ILAB(KLON,KLEV),    IDTOP(KLON),&
                     & ICTOP0(KLON),       ILWMIN(KLON),&
                     & ISTUP(KLON)  

LOGICAL ::  LLDCUM(KLON), LLDDRAF(KLON), LLDDRAF3(KLON), LLDDRAF4(KLON)

! Local trajectory arrays

REAL(KIND=JPRB) ::   ZTENH5(KLON,KLEV),      ZQENH5(KLON,KLEV),&
                   & ZQSENH5(KLON,KLEV),     ZLGLAC5(KLON,KLEV),&
                   & ZTD5(KLON,KLEV),        ZQD5(KLON,KLEV),&
                   & ZMFUS5(KLON,KLEV),      ZMFDS5(KLON,KLEV),&
                   & ZMFUQ5(KLON,KLEV),      ZMFDQ5(KLON,KLEV),&
                   & ZDMFUP5(KLON,KLEV),     ZDMFDP5(KLON,KLEV),&
                   & ZDMFUPC5(KLON,KLEV),    ZDMFDPC5(KLON,KLEV),&
                   & ZMFUL5(KLON,KLEV),      ZRFL5(KLON),&
                   & ZUU5(KLON,KLEV),        ZVU5(KLON,KLEV),&
                   & ZUD5(KLON,KLEV),        ZVD5(KLON,KLEV)  
REAL(KIND=JPRB) ::   ZMFUB5(KLON),  ZMFUB25(KLON), ZMFUB35(KLON),&
                   & ZMFUB45(KLON), ZMFMAX5(KLON)  
REAL(KIND=JPRB) ::   ZDPMEL5(KLON,KLEV), ZWUBASE5(KLON)
REAL(KIND=JPRB) ::   ZCAPE5(KLON), ZHEAT5(KLON)
REAL(KIND=JPRB) ::   ZCAPPBL5(KLON), ZCAPDCYCL5(KLON), ZTAU5(KLON)

! Arrays for intermediate storage of trajectory

LOGICAL ::     LL_LDCUM1(KLON), LL_LDCUM2(KLON)
INTEGER(KIND=JPIM) :: I_KCBOT1(KLON), I_KCBOT2(KLON)
INTEGER(KIND=JPIM) :: I_KCTOP1(KLON)

REAL(KIND=JPRB) ::   ZMFU15(KLON,KLEV),      ZMFD15(KLON,KLEV),&
                   & ZDMFUP15(KLON,KLEV),    ZDMFDP15(KLON,KLEV),&
                   & ZRFL15(KLON),           ZQSEN15(KLON,KLEV),&
                   & ZQSEN25(KLON,KLEV),     ZTENT15(KLON,KLEV),&
                   & ZTENQ15(KLON,KLEV),     ZTENU15(KLON,KLEV),&
                   & ZTENV15(KLON,KLEV),     ZLGLAC15(KLON,KLEV),&
                   & ZLGLAC25(KLON,KLEV),    ZLUDE15(KLON,KLEV),&
                   & ZTENT25(KLON,KLEV),     ZTENQ25(KLON,KLEV)
                   
REAL(KIND=JPRB) ::   ZMFU25(KLON,KLEV),      ZMFD25(KLON,KLEV),&
                   & ZMFUS25(KLON,KLEV),     ZMFDS25(KLON,KLEV),&
                   & ZMFUQ25(KLON,KLEV),     ZMFDQ25(KLON,KLEV),&
                   & ZDMFUP25(KLON,KLEV),    ZDMFDP25(KLON,KLEV),&
                   & ZMFUL25(KLON,KLEV),     ZLUDE25(KLON,KLEV),&
                   & ZZBUO5(KLON,KLEV),      ZBUOH5(KLON,KLEV),&
                   & ZDS5(KLON,KLEV),        ZQPRCV5(KLON,KLEV),&
                   & ZTENH25(KLON,KLEV),     ZQENH25(KLON,KLEV),&   
                   & ZTENH35(KLON,KLEV),     ZQENH35(KLON,KLEV)

REAL(KIND=JPRB) ::   ZMFD35(KLON,KLEV),  ZMFDS35(KLON,KLEV),&
                   & ZMFDQ35(KLON,KLEV), ZDMFDP35(KLON,KLEV)

REAL(KIND=JPRB) ::   ZRESCAL5(KLON)
REAL(KIND=JPRB) ::   ZDHPBL5(KLON)
REAL(KIND=JPRB) ::   ZTENDH5(KLON,KLEV)
REAL(KIND=JPRB) ::   ZTENU5(KLON,KLEV),   ZTENV5(KLON,KLEV)
REAL(KIND=JPRB) ::   ZTU15(KLON,KLEV),    ZQU15(KLON,KLEV)
REAL(KIND=JPRB) ::   ZTU25(KLON,KLEV),    ZQU25(KLON,KLEV)
REAL(KIND=JPRB) ::   ZTD25(KLON,KLEV),    ZQD25(KLON,KLEV)
REAL(KIND=JPRB) ::   ZLU15(KLON,KLEV)
REAL(KIND=JPRB) ::   ZUU15(KLON,KLEV),    ZVU15(KLON,KLEV)
REAL(KIND=JPRB) ::   ZUD15(KLON,KLEV),    ZVD15(KLON,KLEV)
REAL(KIND=JPRB) ::   ZMFUS45(KLON,KLEV),  ZMFUQ45(KLON,KLEV)
REAL(KIND=JPRB) ::   ZMFDS45(KLON,KLEV),  ZMFDQ45(KLON,KLEV)
REAL(KIND=JPRB) ::   ZERATEU5(KLON,KLEV), ZERATED5(KLON,KLEV)
REAL(KIND=JPRB) ::   ZERATEU (KLON,KLEV), ZERATED (KLON,KLEV)

! Arrays for adjoint variables

! Input
REAL(KIND=JPRB) ::   ZTENH(KLON,KLEV),       ZQENH(KLON,KLEV),&
                   & ZQSENH(KLON,KLEV),      ZLGLAC(KLON,KLEV),&
                   & ZTD(KLON,KLEV),         ZQD(KLON,KLEV),&
                   & ZMFUS(KLON,KLEV),       ZMFDS(KLON,KLEV),&
                   & ZMFUQ(KLON,KLEV),       ZMFDQ(KLON,KLEV),&
                   & ZDMFUP(KLON,KLEV),      ZDMFDP(KLON,KLEV),&
                   & ZDMFUPC(KLON,KLEV),     ZDMFDPC(KLON,KLEV),&
                   & ZMFUL(KLON,KLEV),       ZDPMEL(KLON,KLEV),&
                   & ZUU(KLON,KLEV),         ZVU(KLON,KLEV),&
                   & ZUD(KLON,KLEV),         ZVD(KLON,KLEV),&
                   & ZBUOH(KLON,KLEV),       ZQPRCV(KLON,KLEV),&
                   & ZTENU(KLON,KLEV),       ZTENV(KLON,KLEV)

  
REAL(KIND=JPRB) ::   ZMFUB(KLON),  ZRFL(KLON),  ZWUBASE(KLON)
REAL(KIND=JPRB) ::   ZCAPE(KLON),  ZHEAT(KLON), ZDHPBL(KLON)
REAL(KIND=JPRB) ::   ZMFUDE_RATE5  (KLON,KLEV), ZMFDDE_RATE5  (KLON,KLEV)
REAL(KIND=JPRB) ::   ZMFUDE_RATE15 (KLON,KLEV), ZMFDDE_RATE15 (KLON,KLEV)
REAL(KIND=JPRB) ::   ZMFDDE_RATE35 (KLON,KLEV)
REAL(KIND=JPRB) ::   ZMFUDE_RATE   (KLON,KLEV), ZMFDDE_RATE   (KLON,KLEV)
REAL(KIND=JPRB) ::   ZCAPPBL (KLON), ZCAPDCYCL (KLON), ZTAU (KLON)
REAL(KIND=JPRB) ::   ZKHVFL5(KLON)
REAL(KIND=JPRB) ::   ZKHVFL (KLON)
!LOP REAL(KIND=JPRB) ::   ZRESCAL(KLON)

INTEGER(KIND=JPIM) :: IKB, IKD, ITOPM2, JK, JL, IK

REAL(KIND=JPRB) :: ZCONS2, ZPBMPT5, ZMFMAX25, ZMFMAX35, ZMFMAX45, ZMFMAX55
REAL(KIND=JPRB) :: ZCONS, ZCONS1, ZCONS3, ZCONS4, ZEPS
REAL(KIND=JPRB) :: ZWM
REAL(KIND=JPRB) :: ZDZ5, ZDZ15, ZDZ25, ZDZ255
REAL(KIND=JPRB) :: ZTAUPBL5, ZDUTEN5
REAL(KIND=JPRB) :: ZTAURES5, ZDX5, ZDHEAT5

REAL(KIND=JPRB) :: ZDS, ZDZ, ZZBUO
REAL(KIND=JPRB) :: ZFACT2, ZFACT3, ZFACT4, ZFACT5
REAL(KIND=JPRB) :: ZFACT6(KLON), ZFACT7, ZFACT8, ZFACT9(KLON), ZFACT10, ZFACT11
REAL(KIND=JPRB) :: ZFACT12, ZFACT13
REAL(KIND=JPRB) :: ZQUMQE5, ZDH5, ZDH25
REAL(KIND=JPRB) :: ZQUMQE,  ZDH, ZDH2, ZTENDH, ZMFMAX, ZMFA
REAL(KIND=JPRB) :: ZRMFCMIN, ZRCPD
REAL(KIND=JPRB) :: ZTAUPBL, ZDUTEN
REAL(KIND=JPRB) :: ZEPS25(KLON), ZEPS2

INTEGER(KIND=JPIM) :: I_KTYPE1(KLON)
LOGICAL :: LLO1(KLON), LLO2(KLON), LLO3(KLON), LLO4(KLON)

!     LOCAL 3D dummy arrays for convection
! scaling factor and momentum massflux
REAL(KIND=JPRB) :: ZMFS5(KLON,KLEV), ZMFUUS5(KLON,KLEV), ZMFDUS5(KLON,KLEV), ZMFS25(KLON)
REAL(KIND=JPRB) :: ZMFS35(KLON,KLEV), ZMFS45(KLON), ZMFS55(KLON,KLEV), ZMFS65(KLON)
REAL(KIND=JPRB) :: ZMFS (KLON), ZMFUUS (KLON,KLEV), ZMFDUS (KLON,KLEV)
REAL(KIND=JPRB) :: ZMFUUB5(KLON), ZMFUVB5(KLON)
REAL(KIND=JPRB) :: ZMFUUB (KLON), ZMFUVB (KLON)
REAL(KIND=JPRB) :: ZWMEAN5(KLON)
REAL(KIND=JPRB) :: ZWMEAN (KLON)
REAL(KIND=JPRB) :: ZWM5(KLON)
REAL(KIND=JPRB) :: ZMFA15(KLON,KLEV), ZMFA25(KLON,KLEV), ZMFA35(KLON,KLEV)

! Allocatable arrays for Tracers
REAL(KIND=JPRB) ::   ZSCAV5(KTRAC)
REAL(KIND=JPRB), DIMENSION(:,:)  ,ALLOCATABLE  :: ZMFUUC5, ZMFDUC5, ZMFUDR5, ZMFDDR5,&
                                                 &ZMFUUC,  ZMFDUC,  ZMFUDR,  ZMFDDR
REAL(KIND=JPRB), DIMENSION(:,:,:),ALLOCATABLE  :: ZTENC55

LOGICAL :: LLCVOPT
! arrays for optimization (trajectory for adjoint saved in CUBASEN2)
REAL(KIND=JPRB)    :: ZTU55    (KLON,KLEV)   
REAL(KIND=JPRB)    :: ZQU55    (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZLU55    (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZBUOH55  (KLON,KLEV)
REAL(KIND=JPRB)    :: ZWU2H55  (KLON,KLEV)
REAL(KIND=JPRB)    :: ZSUH55   (KLON,KLEV)
REAL(KIND=JPRB)    :: ZWU2H155 (KLON)
REAL(KIND=JPRB)    :: ZTVENH55 (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZTVUH55  (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZSUH255  (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZQU255   (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZFACT355 (KLON)    
REAL(KIND=JPRB)    :: ZFACT455 (KLON)    
REAL(KIND=JPRB)    :: ZFACT555 (KLON)    
REAL(KIND=JPRB)    :: ZWS55    (KLON)    
REAL(KIND=JPRB)    :: ZKHVFL55 (KLON)    
REAL(KIND=JPRB)    :: ZUST55   (KLON)    
REAL(KIND=JPRB)    :: ZWUBASE55 (KLON)    
REAL(KIND=JPRB)    :: ZWORK155  (KLON,KLEV)    
REAL(KIND=JPRB)    :: ZWORK155T (KLON,3,KLEV)    
INTEGER(KIND=JPIM) :: ICBOT55   (KLON)
INTEGER(KIND=JPIM) :: ILAB55    (KLON,KLEV)
LOGICAL            :: LLCUM55   (KLON)
LOGICAL            :: LLTEST155 (KLON,KLEV)
LOGICAL            :: LLTEST25  (KLON,KLEV)
!LOP LOGICAL            :: LLTEST35  (KLON,KLEV)
LOGICAL            :: LLTEST45  (KLON,KLEV)
LOGICAL            :: LLGO_ON55 (KLON,KLEV)
LOGICAL            :: LLMIDTEST55 (KLON,KLEV)
LOGICAL            :: LLCAPETEST55 (KLON,KLEV)

REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "cuascn2.intfb.h"
#include "cuascn2ad.intfb.h"
#include "cubasen2.intfb.h"
#include "cubasen2ad.intfb.h"
#include "cuddrafn2.intfb.h"
#include "cuddrafn2ad.intfb.h"
#include "cudtdqn2.intfb.h"
#include "cudtdqn2ad.intfb.h"
#include "cududv2.intfb.h"
#include "cududv2ad.intfb.h"
#include "cuctracer.intfb.h"
#include "cuctracerad.intfb.h"
#include "cuflx2.intfb.h"
#include "cuflx2ad.intfb.h"
#include "cuinin2.intfb.h"
#include "cuinin2ad.intfb.h"

#include "fcttre.ycst.h"

!-----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('CUMASTRN2AD',0,ZHOOK_HANDLE)
ASSOCIATE(LREGCV=>YDML_PHY_SLIN%YRCUMFS%LREGCV, &
 & LMFDD2=>YDML_PHY_SLIN%YRECUMF2%LMFDD2, LMFDUDV2=>YDML_PHY_SLIN%YRECUMF2%LMFDUDV2, &
 & LMFUVDIS2=>YDML_PHY_SLIN%YRECUMF2%LMFUVDIS2, NJKT32=>YDML_PHY_SLIN%YRECUMF2%NJKT32, &
 & RCAPDCYCL2=>YDML_PHY_SLIN%YRECUMF2%RCAPDCYCL2, RDEPTHS2=>YDML_PHY_SLIN%YRECUMF2%RDEPTHS2, &
 & RMFCFL2=>YDML_PHY_SLIN%YRECUMF2%RMFCFL2, RMFCMIN2=>YDML_PHY_SLIN%YRECUMF2%RMFCMIN2, &
 & RMFLIA2=>YDML_PHY_SLIN%YRECUMF2%RMFLIA2, &
 & RMFSOLCT2=>YDML_PHY_SLIN%YRECUMF2%RMFSOLCT2, RMFSOLTQ2=>YDML_PHY_SLIN%YRECUMF2%RMFSOLTQ2, &
 & RMFSOLUV2=>YDML_PHY_SLIN%YRECUMF2%RMFSOLUV2, RTAU02=>YDML_PHY_SLIN%YRECUMF2%RTAU02, &
 & RTAU2=>YDML_PHY_SLIN%YRECUMF2%RTAU2, RUVPER2=>YDML_PHY_SLIN%YRECUMF2%RUVPER2, &
 & LMFSCAV=>YDML_PHY_EC%YREPHY%LMFSCAV, LMFTRAC=>YDML_PHY_EC%YREPHY%LMFTRAC)
LLCVOPT=.TRUE.

!              --------  TRAJECTORY COMPUTATIONS  --------

!-----------------------------------------------------------------------

!     1.           SPECIFY CONSTANTS AND PARAMETERS
!                  --------------------------------

ZEPS=1.0E-10_JPRB
ZRCPD=1.0_JPRB/YDCST%RCPD
ZCONS1=1.0E-06_JPRB
ZCONS=1.0_JPRB/(YDCST%RG*PTSPHY)
ZCONS2=RMFCFL2/(YDCST%RG*PTSPHY)
ZCONS3=YDCST%RCPD*ZCONS1
ZCONS4=YDCST%RLVTT*ZCONS1
ZRMFCMIN=1.0_JPRB/RMFCMIN2

!----------------------------------------------------------------------

!*    2.       INITIALIZE VALUES AT VERTICAL GRID POINTS IN 'CUININ2'
!              -----------------------------------------------------

ZQSEN15(:,:)=PQSEN5(:,:)
ZQSEN25(:,:)=PQSEN5(:,:)

CALL CUININ2 &
 & ( YDML_PHY_SLIN%YRECUMF2,YDML_PHY_SLIN%YREPHLI,&
 & KIDIA,    KFDIA,    KLON,     KLEV,&
 & LDRAIN1D,&
 & PTEN5,    PQEN5,    PQSEN5,   PUEN5,    PVEN5,&
 & PVERVEL5, PGEO5,    PAPH5,&
 & ILWMIN,   ILAB,&
 & ZTENH5,   ZQENH5,   ZQSENH5,  PGEOH5,&
 & PTU5,     PQU5,     ZTD5,     ZQD5,&
 & ZUU5,     ZVU5,     ZUD5,     ZVD5,&
 & PLU5    )  

!---------------------------------------------------------------------

!*    3.           CLOUD BASE CALCULATIONS
!                  -----------------------

CALL CUBASEN2 &
 & ( YDML_PHY_SLIN%YRECUMF2,YDML_PHY_SLIN%YREPHLI,YDML_PHY_EC%YRECLDP,&
 & KIDIA,     KFDIA,     KLON,      KLEV,&
 & LDRAIN1D,  LLCVOPT,&
 & ZTENH5,    ZQENH5,    PGEOH5,    PAP5,     PAPH5,&
 & PQHFL5,    PAHFS5,&
 & PTEN5,     PQEN5,     PQSEN5,    PGEO5,&
 & PTU5,      PQU5,      PLU5,&
 & ZBUOH5,    ZWUBASE5,  ZLGLAC5,   ZQPRCV5,  ZCAPE5,&
 & KTYPE,     ILAB,      LDCUM,     LDSC,&
 & KCBOT,     KBOTSC,    ICTOP0,    ISTUP,&
 & ZTU55,     ZQU55,     ZLU55,&
 & ZBUOH55,   ZWU2H55,   ZSUH55,    ZWU2H155,&
 & ZTVENH55,  ZTVUH55,   ZSUH255,   ZQU255,&
 & ZFACT355,  ZFACT455,  ZFACT555,  ZWS55,&   
 & ZKHVFL55,  ZUST55,    ZWUBASE55, ZWORK155,  ZWORK155T,&  
 & ICBOT55,   ILAB55,    LLCUM55,   LLTEST155,&
 & LLGO_ON55, LLMIDTEST55, LLCAPETEST55 ) 

  
! Store trajectory arrays for adjoint
I_KCBOT1(:)=KCBOT(:)
LL_LDCUM1(:)=LDCUM(:)

! CALCULATE COLUMN AND SUB CLOUD LAYER MOISTURE CONVERGENCE
! AND SUB CLOUD LAYER MOIST STATIC ENERGY CONVERGENCE
ZDHPBL5(:)=0.0_JPRB
ZCAPPBL5(:)=0.0_JPRB
DO JL=KIDIA,KFDIA
  ZKHVFL5(JL)= -PAHFS5(JL,KLEV+1) * ZRCPD - YDCST%RETV *  PTEN5(JL,KLEV) * PQHFL5(JL,KLEV+1)
ENDDO
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    IF(LDCUM(JL) .AND. JK >= KCBOT(JL)) THEN
      ZTENDH5(JL,JK)=YDCST%RLVTT*PTENQ5(JL,JK)+YDCST%RCPD*PTENT5(JL,JK)
      ZDZ5=PAPH5(JL,JK+1)-PAPH5(JL,JK)
      ZDHPBL5(JL)=ZDHPBL5(JL)+ZTENDH5(JL,JK) * ZDZ5 * ZCONS1
      ZTENT25(JL,JK)=PTENT5(JL,JK)
      ZTENQ25(JL,JK)=PTENQ5(JL,JK)
      ZCAPPBL5(JL)=ZCAPPBL5(JL)+(PTENT5(JL,JK)+YDCST%RETV*PTENQ5(JL,JK))*ZDZ5
    ENDIF
  ENDDO
ENDDO

!*                 SPECIFY INITIAL CLOUD TYPE
!*

DO JL=KIDIA,KFDIA
  IF (LDCUM(JL)) THEN
    IF (KTYPE(JL) /= 3) THEN
      ZPBMPT5=PAPH5(JL,KCBOT(JL))-PAPH5(JL,ICTOP0(JL))
      IF (ZPBMPT5 >= RDEPTHS2) THEN
        KTYPE(JL)=1
      ELSE
        KTYPE(JL)=2
      ENDIF
    ENDIF
  ENDIF
ENDDO

! Store trajectory arrays for adjoint
I_KCBOT2(:)=KCBOT(:)
LL_LDCUM2(:)=LDCUM(:)

!---------------------------------------------------------------------

!*       4.     SIMPLIFIED CONVECTION SCHEME
!*              COMPUTE VERTICAL PROFILE OF RATIO M(z)/Mbase
!               --------------------------------------------

!DIR$ IVDEP
!OCL NOVREC
DO JL=KIDIA,KFDIA
  IF (LDCUM(JL)) THEN
    ZMFUB45(JL)=1.0_JPRB
    KCTOP(JL)=ICTOP0(JL)
  ELSE
    ZMFUB45(JL)=0.0_JPRB
  ENDIF
ENDDO

! Store trajectory arrays for adjoint
I_KTYPE1(:)=KTYPE(:)
I_KCTOP1(:)=KCTOP(:)
ZTU15(:,:)=PTU5(:,:)
ZQU15(:,:)=PQU5(:,:)
ZLU15(:,:)=PLU5(:,:)
ZTENH25(:,:)=ZTENH5(:,:)
ZQENH25(:,:)=ZQENH5(:,:)
ZTENH35(:,:)=ZTENH5(:,:)
ZQENH35(:,:)=ZQENH5(:,:)
ZLGLAC25(:,:)=ZLGLAC5(:,:)

CALL CUASCN2 &
 & ( YDML_PHY_SLIN%YRECUMF2,YDML_PHY_SLIN%YREPHLI,&
 & KIDIA,    KFDIA,    KLON,    KLEV,&
 & LDRAIN1D, LDLAND,   PTEN5,   ZTENH5,  ZQENH5,&
 & PGEOH5,   PQEN5,    PQSEN5,&
 & PAPH5,&    
 & ZLGLAC5,  ZQPRCV5,&
 & ZBUOH5,   ZWUBASE5, LDCUM,   KTYPE,&
 & PTU5,     PQU5,     PLU5,&
 & PMFU5 ,   ZMFUB45,&
 & ZMFUS5,   ZMFUQ5,   ZMFUL5,  PLUDE5,  ZDMFUP5,&
 & KCBOT,    KCTOP,    ISTUP,&
 & ZMFUDE_RATE5, ZWMEAN5,  ZERATEU5) 
  
!*                 SPECIFY CLOUD TYPE
!*

DO JL=KIDIA,KFDIA
  IF (LDCUM(JL)) THEN
    IF (KTYPE(JL) /= 3) THEN
      ZPBMPT5=PAPH5(JL,KCBOT(JL))-PAPH5(JL,KCTOP(JL))
      IF (ZPBMPT5 >= RDEPTHS2) THEN
        KTYPE(JL)=1
      ELSE
        KTYPE(JL)=2
      ENDIF
    ENDIF
  ENDIF
ENDDO

DO JL=KIDIA,KFDIA
  ZRFL5(JL)=ZDMFUP5(JL,1)
ENDDO
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    ZRFL5(JL)=ZRFL5(JL)+ZDMFUP5(JL,JK)
  ENDDO
ENDDO

! Initializations 

LLDDRAF(:)=.FALSE.
IDTOP(:)=0
DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PMFD5(JL,JK)=0.0_JPRB
    ZMFDS5(JL,JK)=0.0_JPRB
    ZMFDQ5(JL,JK)=0.0_JPRB
    ZDMFDP5(JL,JK)=0.0_JPRB
    ZDPMEL5(JL,JK)=0.0_JPRB
    ZMFDDE_RATE5(JL,JK)=0.0_JPRB
    ZERATED5(JL,JK)=0.0_JPRB
  ENDDO
ENDDO
IF(LMFUVDIS2) THEN
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZTENU5(JL,JK)=PTENU5(JL,JK)
      ZTENV5(JL,JK)=PTENV5(JL,JK)
    ENDDO
  ENDDO
ENDIF

!-----------------------------------------------------------------------

!*    5.           CUMULUS DOWNDRAFT CALCULATIONS
!                  ------------------------------

IF(LMFDD2) THEN

!*                 DETERMINE LFS AND DOWNDRAFTS IN 'CUDDRAFN2'
!                  SIMPLIFIED SCHEME
!                  -------------------------------------------

  ZRFL15(:)=ZRFL5(:)

  CALL CUDDRAFN2 &
   & ( YDML_PHY_SLIN%YRECUMF2,     YDML_PHY_SLIN%YREPHLI, &
   & KIDIA,    KFDIA,    KLON,     KLEV,&
   & KCBOT,    KCTOP,    LDCUM,    LDRAIN1D,&
   & ZTENH5,   ZQENH5,&
   & PTEN5,    PQSEN5,   PGEO5,&
   & PGEOH5,   PAPH5,    PTU5,     PQU5,&
   & ZRFL5,    ZTD5,     ZQD5,     PMFU5,&
   & PMFD5,    ZMFDS5,   ZMFDQ5,   ZDMFDP5,&
   & IDTOP,    LLDDRAF,&
   & ZMFDDE_RATE5, ZERATED5 )  

ENDIF

!*    6.        SIMPLIFIED CONVECTION SCHEME:
!*              CALCULATE CLOUD BASE MASSFLUX FROM A
!*              CAPE CLOSURE FOR ALL TYPES OF CONVECTION,
!*              AND FROM THE PROFILE OF RATIO M(z)/Mbase
!*              WHICH WAS CALCULATED IN CUASCN2.
!*              RESCALE ALL MASS-FLUXES ACCORDINGLY.
!           ---------------------------------------------

! Store trajectory for adjoint
ZMFU15(:,:)=PMFU5(:,:)
ZDMFUP15(:,:)=ZDMFUP5(:,:)
ZMFD15(:,:)=PMFD5(:,:)
ZDMFDP15(:,:)=ZDMFDP5(:,:)
ZMFUDE_RATE15(:,:)=ZMFUDE_RATE5(:,:)
ZMFDDE_RATE15(:,:)=ZMFDDE_RATE5(:,:)

DO JL=KIDIA,KFDIA
  ZHEAT5(JL)=0.0_JPRB
  ZCAPE5(JL)=0.0_JPRB
ENDDO

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    IF(LDCUM(JL) .AND. KTYPE(JL) /= 2 .AND. &
      & JK <= KCBOT(JL).AND.JK > KCTOP(JL)) THEN
      ZDZ5=PGEOH5(JL,JK-1)-PGEOH5(JL,JK)
      ZFACT5=1.0_JPRB/ZTENH5(JL,JK)
      ZDS5(JL,JK)=(PTEN5(JL,JK-1)-PTEN5(JL,JK) &
               & + ZDZ5/YDCST%RCPD)*ZFACT5
      ZDHEAT5 = (ZDS5(JL,JK)+YDCST%RETV*(PQEN5(JL,JK-1)-PQEN5(JL,JK))) &
            & * (YDCST%RG*(PMFU5(JL,JK)+PMFD5(JL,JK)))
      LLTEST45(JL,JK) = (ZDHEAT5 > 0.0_JPRB)
      IF (LLTEST45(JL,JK)) ZHEAT5(JL) = ZHEAT5(JL) + ZDHEAT5
      ZZBUO5(JL,JK)=PTU5(JL,JK)*ZFACT5-1.0_JPRB
      ZDZ15=PAPH5(JL,JK)-PAPH5(JL,JK-1)
      ZCAPE5(JL)=ZCAPE5(JL) + &
       & ( ZZBUO5(JL,JK)+YDCST%RETV*(PQU5(JL,JK)-ZQENH5(JL,JK))&
       & - PLU5(JL,JK) ) * ZDZ15  
    ENDIF
  ENDDO
ENDDO

! Time scale and subcloud contribution to CAPE to be substracted 
! for better diurnal cycle over land.
DO JL=KIDIA,KFDIA
  ZCAPDCYCL5(JL)=0.0_JPRB
  IF(LDCUM(JL).AND.KTYPE(JL) /= 2) THEN
    IKD=ISTUP(JL)
    IKB=KCBOT(JL)
    IK=KCTOP(JL)
    IF (ZWMEAN5(JL) < 15.0_JPRB) THEN
      ZWM5(JL) = 2.0_JPRB + ZWMEAN5(JL)
    ELSE
      ZWM5(JL) = 17.0_JPRB
    ENDIF
    ZTAURES5=1._JPRB+RTAU2*800._JPRB*SQRT(4._JPRB*PGAW(JL)/YDCST%RPI)
    ! for the 10-1 km resolution range increase ZTAURES
    ZDX5=2.0_JPRB*YDCST%RA*SQRT(YDCST%RPI*PGAW(JL))
    IF(ZDX5 < 8.E3_JPRB) THEN
      ZDX5=MAX(ZDX5,1.E2_JPRB)
      ZTAURES5=1.0_JPRB+(LOG(10.E3_JPRB/ZDX5))**2
    ENDIF
    IF(ZDX5 > 125.E3_JPRB) ZTAURES5=MIN(3.0_JPRB,ZTAURES5)
    ZFACT7=ZTAURES5/(ZWM5(JL)*YDCST%RG)
    ZTAU5(JL)=(PGEOH5(JL,IK)-PGEOH5(JL,IKB))*ZFACT7
    LLO3(JL)=KTYPE(JL)==1.AND.(PAPH5(JL,KLEV+1)-PAPH5(JL,IKD))<50.E2_JPRB
    IF(LLO3(JL).AND.LDLAND(JL).AND.RCAPDCYCL2==1.0_JPRB) THEN
      ZDZ5=MIN(1.E4_JPRB,PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1))/YDCST%RG
      ZCAPDCYCL5(JL)=YDCST%RCPD*ZTAU5(JL)*MAX(0.0_JPRB,ZKHVFL5(JL))/ZDZ5
    ENDIF
    IF(LLO3(JL).AND.RCAPDCYCL2==2.0_JPRB) THEN
      IF(LDLAND(JL)) THEN
        ZCAPDCYCL5(JL)=ZCAPPBL5(JL)*ZTAU5(JL)/ZTAURES5
      ELSE
        ZFACT2=SQRT(0.5*(PUEN5(JL,IKB)**2+PVEN5(JL,IKB)**2+PUEN5(JL,NJKT32)**2+PVEN5(JL,NJKT32)**2))
        ZFACT2=MAX(ZFACT2,0.1_JPRB)  ! security for division in AD.
        ZDUTEN5=2.0_JPRB+ZFACT2
        ZTAUPBL5=MIN(1.E4_JPRB,PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1))/(YDCST%RG*ZDUTEN5)
        ZCAPDCYCL5(JL)=ZCAPPBL5(JL)*ZTAUPBL5
      ENDIF
    ENDIF
  ENDIF
ENDDO

! CFL rescaling for cloud base mass flux
ZRESCAL5(:)=0.0_JPRB
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    LLTEST25(JL,JK) = (LDCUM(JL) .AND. (JK >= KCTOP(JL) .AND. JK <= KCBOT(JL)))
    IF (LLTEST25(JL,JK)) THEN
      ZMFMAX5(JL)=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*ZCONS2
      ZMFMAX5(JL)=MIN(ZMFMAX5(JL),RMFLIA2)
      ZRESCAL5(JL)=MAX(ZRESCAL5(JL),PMFU5(JL,JK)/ZMFMAX5(JL))
    ENDIF
  ENDDO
ENDDO

DO JL=KIDIA,KFDIA
  IF(LDCUM(JL)) THEN
    ZMFMAX5(JL)=1.0_JPRB/ZRESCAL5(JL)
    IF (KTYPE(JL) /= 2) THEN
      ZCAPE5(JL)=ZCAPE5(JL)-ZCAPDCYCL5(JL)
      ZFACT8=1.0_JPRB/(ZHEAT5(JL)*ZTAU5(JL))
      ZMFUB25(JL)= ZCAPE5(JL)*ZFACT8
    ELSE
      IF (ZDHPBL5(JL) > 0.0_JPRB) THEN
        IKB=KCBOT(JL)

        LLO4(JL)=(PMFD5(JL,IKB) < 0.0_JPRB)
        IF (LLO4(JL)) THEN
          ZEPS25(JL)=-PMFD5(JL,IKB)
        ELSE
          ZEPS25(JL)=0.0_JPRB
        ENDIF
        ZQUMQE5=PQU5(JL,IKB)+PLU5(JL,IKB)-ZEPS25(JL)*ZQD5(JL,IKB)-(1.0_JPRB-ZEPS25(JL))*ZQENH5(JL,IKB)
        ZDH5 =ZCONS3*(PTU5(JL,IKB)  -ZEPS25(JL)*ZTD5(JL,IKB)  -(1.0_JPRB-ZEPS25(JL))*ZTENH5(JL,IKB))   + ZCONS4*ZQUMQE5
        ZDH25=ZCONS3*(PTU5(JL,IKB-1)-ZEPS25(JL)*ZTD5(JL,IKB-1)-(1.0_JPRB-ZEPS25(JL))*ZTENH5(JL,IKB-1)) + ZCONS4*ZQUMQE5
        ZDH5 = 0.5_JPRB * (ZDH5 + ZDH25)
        LLO2(JL) = (ZDH5 < 0.1_JPRB * ZCONS3)
        IF (LLO2(JL)) ZDH5 = 0.1_JPRB * ZCONS3
        ZDH5 = YDCST%RG * ZDH5

        ZFACT9(JL) = 1.0_JPRB / ZDH5
        ZMFUB25(JL) = ZDHPBL5(JL) * ZFACT9(JL)

        ZMFMAX55 = (PAPH5(JL,IKB) - PAPH5(JL,IKB-1)) * ZCONS2
        LLO1(JL) = (ZMFUB25(JL) > 0.9_JPRB*ZMFMAX55/RMFCFL2 .AND. ZDH5 < 0.5_JPRB*YDCST%RG*ZCONS3)
        IF(LLO1(JL)) THEN
          ZDH5=0.75_JPRB*ZCONS3*YDCST%RG
          ZFACT6(JL)=1.0_JPRB/ZDH5
          ZMFUB25(JL)=ZDHPBL5(JL)*ZFACT6(JL)
        ENDIF
      ELSE
        ZMFUB25(JL)=ZMFMAX5(JL)*0.1_JPRB
      ENDIF
!LOP Reduce cloud base mass flux for shallow convection
!LLL      ZMFUB25(JL)=ZMFUB25(JL)*0.3_JPRB
    ENDIF
    ZMFUB35(JL)=MAX(ZMFUB25(JL),0.001_JPRB)
    ZMFUB5(JL)=MIN(ZMFUB35(JL),ZMFMAX5(JL))
  ELSE
    ZMFUB5(JL)=0.0_JPRB
  ENDIF
ENDDO

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PMFU5(JL,JK)  = ZMFUB5(JL) * PMFU5(JL,JK)
    ZDMFUP5(JL,JK)= ZMFUB5(JL) * ZDMFUP5(JL,JK)
    PMFD5(JL,JK)  = ZMFUB5(JL) * PMFD5(JL,JK)
    ZDMFDP5(JL,JK)= ZMFUB5(JL) * ZDMFDP5(JL,JK)
    ZMFUDE_RATE5(JL,JK)= ZMFUB5(JL)*ZMFUDE_RATE5(JL,JK)
    ZMFDDE_RATE5(JL,JK)= ZMFUB5(JL)*ZMFDDE_RATE5(JL,JK)
  ENDDO
ENDDO

! Store trajectory for adjoint
ZMFU25(:,:)=PMFU5(:,:)
ZMFUS25(:,:)=ZMFUS5(:,:)
ZMFUQ25(:,:)=ZMFUQ5(:,:)
ZMFUL25(:,:)=ZMFUL5(:,:)
ZDMFUP25(:,:)=ZDMFUP5(:,:)
ZLUDE25(:,:)=PLUDE5(:,:)
ZMFD25(:,:)=PMFD5(:,:)
ZMFDS25(:,:)=ZMFDS5(:,:)
ZMFDQ25(:,:)=ZMFDQ5(:,:)
ZDMFDP25(:,:)=ZDMFDP5(:,:)
ZLGLAC15(:,:)=ZLGLAC5(:,:)

!-----------------------------------------------------------------------

!*   7.         DETERMINE FINAL CONVECTIVE FLUXES IN 'CUFLX2'
!               ---------------------------------------------

CALL CUFLX2 &
 & ( YDML_PHY_SLIN%YRECUMF2,YDML_PHY_SLIN%YREPHLI,&
 & KIDIA,    KFDIA,    KLON,     KLEV,&
 & LDRAIN1D, LDLAND,   PTSPHY,&
 & PTEN5,    PQEN5,    PQSEN5,   ZTENH5,   ZQENH5,&
 & PAPH5,    PAP5,     LDCUM,&
 & KCBOT,    KCTOP,    IDTOP,    ITOPM2,&
 & KTYPE,    LLDDRAF,&
 & PMFU5,    PMFD5,    ZMFUS5,   ZMFDS5,&
 & ZMFUQ5,   ZMFDQ5,   ZMFUL5,   PLUDE5,&
 & ZDMFUP5,  ZDMFDP5,  ZDPMEL5,  ZLGLAC5,&
 & PMFLXR5,  PMFLXS5,  PRAIN5 )  

!- rescale DD fluxes if total mass flux becomes negative
!- correct DD detrainment rates if entrainment becomes negative
!- correct UD detrainment rates if entrainment becomes negative
!- conservation correction for precip

! Storage of trajectory for adjoint
ZMFD35(:,:)=PMFD5(:,:)
ZMFDS35(:,:)=ZMFDS5(:,:)
ZMFDQ35(:,:)=ZMFDQ5(:,:)
ZDMFDP35(:,:)=ZDMFDP5(:,:)
ZMFDDE_RATE35(:,:)=ZMFDDE_RATE5(:,:)
LLDDRAF4(:)=LLDDRAF(:)

ZMFS55(:,:)=1.0_JPRB
ZMFS65(:)=1.0_JPRB
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    IF (LLDDRAF(JL) .AND. JK >= IDTOP(JL)-1) THEN
      ZMFMAX35=PMFU5(JL,JK)*0.60_JPRB
      IF(PMFD5(JL,JK)+ZMFMAX35+1.E-15_JPRB < 0.0_JPRB) THEN
        ZFACT10=1.0_JPRB/PMFD5(JL,JK)
        ZMFS55(JL,JK)=MIN(ZMFS55(JL,JK-1),-ZMFMAX35*ZFACT10)
      ELSE
        ZMFS55(JL,JK)=ZMFS55(JL,JK-1)
      ENDIF
      ZMFS65(JL)=ZMFS55(JL,JK)
    ENDIF
  ENDDO
ENDDO
ZMFUUB5(:)=0.0_JPRB
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    IF (ZMFS65(JL) < 1.0_JPRB .AND. JK >= IDTOP(JL)-1) THEN
      PMFD5(JL,JK)=PMFD5(JL,JK)*ZMFS65(JL)
      ZMFDS5(JL,JK)=ZMFDS5(JL,JK)*ZMFS65(JL)
      ZMFDQ5(JL,JK)=ZMFDQ5(JL,JK)*ZMFS65(JL)
      ZMFDDE_RATE5(JL,JK)=ZMFDDE_RATE5(JL,JK)*ZMFS65(JL)
      ZMFUUB5(JL)=ZMFUUB5(JL)-(1.0_JPRB-ZMFS65(JL))*ZDMFDP5(JL,JK)
      PMFLXR5(JL,JK+1)=PMFLXR5(JL,JK+1)+ZMFUUB5(JL)
      ZDMFDP5(JL,JK)=ZDMFDP5(JL,JK)*ZMFS65(JL)
    ENDIF
   ! ZDMFUPC5(JL,JK)=ZDMFUP5(JL,JK)
   ! ZDMFDPC5(JL,JK)=ZDMFDP5(JL,JK)
  ENDDO
ENDDO

!----------------------------------------------------------------------

!*    8.        UPDATE TENDENCIES FOR T AND Q IN SUBROUTINE CUDTDQN2
!               ----------------------------------------------------

! Store trajectory arrays for adjoint
ZTU25(:,:)=PTU5(:,:)
ZQU25(:,:)=PQU5(:,:)
ZTD25(:,:)=ZTD5(:,:)
ZQD25(:,:)=ZQD5(:,:)

IF (RMFSOLTQ2 > 0.0_JPRB) THEN
! derive draught properties for implicit
  DO JK=KLEV,2,-1
    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL)) THEN
        IF (JK > KCBOT(JL)) THEN
          IF (PMFU5(JL,JK) > 1.E-15_JPRB) THEN
            ZFACT11=1.0_JPRB/PMFU5(JL,JK)
            ZMFA15(JL,JK)=ZFACT11
          ELSE
            ZMFA15(JL,JK)=1.E+15_JPRB
          ENDIF
! Store trajectory arrays for adjoint
          ZMFUS45(JL,JK)=ZMFUS5(JL,JK)
          ZMFUQ45(JL,JK)=ZMFUQ5(JL,JK)
          PQU5(JL,JK)=ZQENH5(JL,JK)+ZMFUQ5(JL,JK)*ZMFA15(JL,JK)
          PTU5(JL,JK)=ZTENH5(JL,JK)+ZMFUS5(JL,JK)*ZMFA15(JL,JK)*ZRCPD
          ZMFUS5(JL,JK)=PMFU5(JL,JK)*(YDCST%RCPD*PTU5(JL,JK)+PGEOH5(JL,JK))
          ZMFUQ5(JL,JK)=PMFU5(JL,JK)*PQU5(JL,JK)
          IF (LLDDRAF(JL)) THEN
            IF (PMFD5(JL,JK) < -1.E-15_JPRB) THEN
              ZFACT11=1.0_JPRB/PMFD5(JL,JK)
              ZMFA25(JL,JK)=ZFACT11
            ELSE
              ZMFA25(JL,JK)=-1.E+15_JPRB
            ENDIF
! Store trajectory arrays for adjoint
            ZMFDS45(JL,JK)=ZMFDS5(JL,JK)
            ZMFDQ45(JL,JK)=ZMFDQ5(JL,JK)
            ZQD5(JL,JK)=ZQENH5(JL,JK)+ZMFDQ5(JL,JK)*ZMFA25(JL,JK)
            ZTD5(JL,JK)=ZTENH5(JL,JK)+ZMFDS5(JL,JK)*ZMFA25(JL,JK)*ZRCPD
            ZMFDQ5(JL,JK)=PMFD5(JL,JK)*ZQD5(JL,JK)
            ZMFDS5(JL,JK)=PMFD5(JL,JK)*(YDCST%RCPD*ZTD5(JL,JK)+PGEOH5(JL,JK))
          ENDIF
        ELSEIF (JK <= KCBOT(JL).AND.JK >= KCTOP(JL)) THEN
          ZMFUS5(JL,JK)=PMFU5(JL,JK)*(YDCST%RCPD*PTU5(JL,JK)+PGEOH5(JL,JK))
          ZMFUQ5(JL,JK)=PMFU5(JL,JK)*PQU5(JL,JK)
          ZMFDS5(JL,JK)=PMFD5(JL,JK)*(YDCST%RCPD*ZTD5(JL,JK)+PGEOH5(JL,JK))
          ZMFDQ5(JL,JK)=PMFD5(JL,JK)*ZQD5(JL,JK)
        ENDIF
      ENDIF
    ENDDO
  ENDDO
ENDIF

ZTENT15(:,:)=PTENT5(:,:)
ZTENQ15(:,:)=PTENQ5(:,:)
ZLUDE15(:,:)=PLUDE5(:,:)

CALL CUDTDQN2 &
 & ( YDML_PHY_SLIN,    YDML_PHY_EC%YREPHY,&
 & KIDIA,    KFDIA,    KLON,     KLEV,&
 & ITOPM2,   KCTOP,    IDTOP,&
 & LDRAIN1D,   LDCUM,    LLDDRAF,  PTSPHY,&
 & PAPH5,    PGEOH5,   PGEO5,&
 & PTEN5,    ZTENH5,   PQEN5,    ZQENH5,    PQSEN5,&
 & ZLGLAC5,  PLUDE5,   PMFU5,    PMFD5,&
 & ZMFUS5,   ZMFDS5,   ZMFUQ5,   ZMFDQ5,&
 & ZMFUL5,   ZDMFUP5,  ZDMFDP5,  ZDPMEL5,&
 & PTENT5,   PTENQ5,   PENTH5  )  

!----------------------------------------------------------------------

!*    9.0        COMPUTE MOMENTUM IN UPDRAUGHT AND DOWNDRAUGHT
!                ---------------------------------------------

IF(LMFDUDV2) THEN

  ZTENU15(:,:)=PTENU5(:,:)
  ZTENV15(:,:)=PTENV5(:,:)

! Updraught and downdraught wind computations
!--------------------------------------------

  DO JK=KLEV-1,2,-1
    IK=JK+1
    DO JL=KIDIA,KFDIA
      IF(LDCUM(JL)) THEN
        IF (JK == KCBOT(JL) .AND. KTYPE(JL) < 3) THEN
          IKB=ISTUP(JL)
          ZUU5(JL,JK)=PUEN5(JL,IKB-1)
          ZVU5(JL,JK)=PVEN5(JL,IKB-1)
        ELSEIF (JK == KCBOT(JL) .AND. KTYPE(JL) == 3) THEN
          ZUU5(JL,JK)=PUEN5(JL,JK-1)
          ZVU5(JL,JK)=PVEN5(JL,JK-1)
        ENDIF
        IF (JK < KCBOT(JL) .AND. JK >= KCTOP(JL)) THEN
          ZUU5(JL,JK)=ZUU5(JL,IK)*(1.0_JPRB-ZERATEU5(JL,JK)) &
                   & +PUEN5(JL,JK)*ZERATEU5(JL,JK)
          ZVU5(JL,JK)=ZVU5(JL,IK)*(1.0_JPRB-ZERATEU5(JL,JK)) &
                   & +PVEN5(JL,JK)*ZERATEU5(JL,JK)
        ENDIF
! Store trajectory for adjoint
        ZUU15(JL,JK)=ZUU5(JL,JK)
        ZVU15(JL,JK)=ZVU5(JL,JK)
      ENDIF
    ENDDO
  ENDDO
  DO JK=3,KLEV
    IK=JK-1
    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL) .AND. LLDDRAF(JL)) THEN
        IF (JK == IDTOP(JL)) THEN
          ZUD5(JL,JK)=0.5_JPRB*(ZUU5(JL,JK)+PUEN5(JL,IK))
          ZVD5(JL,JK)=0.5_JPRB*(ZVU5(JL,JK)+PVEN5(JL,IK))
        ELSEIF (JK > IDTOP(JL)) THEN
          ZUD5(JL,JK)=ZUD5(JL,IK)*(1.0_JPRB-ZERATED5(JL,JK)) &
                   & +PUEN5(JL,IK)*ZERATED5(JL,JK)
          ZVD5(JL,JK)=ZVD5(JL,IK)*(1.0_JPRB-ZERATED5(JL,JK)) &
                   & +PVEN5(JL,IK)*ZERATED5(JL,JK)
        ENDIF
! Store trajectory for adjoint
        ZUD15(JL,JK)=ZUD5(JL,JK)
        ZVD15(JL,JK)=ZVD5(JL,JK)
      ENDIF
    ENDDO
  ENDDO

!*    9.1        UPDATE TENDENCIES FOR U AND V IN SUBROUTINE CUDUDV2
!                ---------------------------------------------------

! Rescale mass fluxes for stability in Momentum
!----------------------------------------------

  DO JL=KIDIA,KFDIA
    ZMFS5(JL,:)=1.0_JPRB
    ZMFS25(JL)=1.0_JPRB
  ENDDO

  IF (RMFSOLUV2 <= 1.0_JPRB) THEN
    DO JK=2,KLEV
      DO JL=KIDIA,KFDIA
        IF (LDCUM(JL) .AND. JK >= KCTOP(JL)-1) THEN
          ZMFMAX25=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*ZCONS
          IF (PMFU5(JL,JK) > ZMFMAX25 .AND. JK >= KCTOP(JL)) THEN
            ZFACT10=1.0_JPRB/PMFU5(JL,JK)
            ZMFS5(JL,JK)=MIN(ZMFS5(JL,JK-1),ZMFMAX25*ZFACT10) 
          ELSE
            ZMFS5(JL,JK)=ZMFS5(JL,JK-1) 
          ENDIF 
          ZMFS25(JL)=ZMFS5(JL,JK)
        ENDIF
      ENDDO
    ENDDO
  ENDIF

  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      ZMFUUS5(JL,JK)=PMFU5(JL,JK)
      ZMFDUS5(JL,JK)=PMFD5(JL,JK)
      IF (LDCUM(JL) .AND. JK >= KCTOP(JL)-1) THEN
        ZMFUUS5(JL,JK)=PMFU5(JL,JK)*ZMFS25(JL)
        ZMFDUS5(JL,JK)=PMFD5(JL,JK)*ZMFS25(JL)
      ENDIF
    ENDDO
  ENDDO

! Recompute draught properties below for implicit
! based on linear flux profiles

  IF (RMFSOLUV2 > 0.0_JPRB) THEN

    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL)) THEN
        JK=KCBOT(JL)
        IK=JK-1
        ZMFUUB5(JL)=ZMFUUS5(JL,JK)*(ZUU5(JL,JK)-PUEN5(JL,IK))
        ZMFUVB5(JL)=ZMFUUS5(JL,JK)*(ZVU5(JL,JK)-PVEN5(JL,IK))
      ENDIF
    ENDDO

    DO JK=2,KLEV
      IK=JK-1
      DO JL=KIDIA,KFDIA
        IF (LDCUM(JL) .AND. JK > KCBOT(JL)) THEN
          IKB=KCBOT(JL)
          ZFACT12=1.0_JPRB/(PAPH5(JL,KLEV+1)-PAPH5(JL,IKB))
          ZDZ25=(PAPH5(JL,KLEV+1)-PAPH5(JL,JK))*ZFACT12
          IF (KTYPE(JL) == 3) THEN
            ZDZ25=ZDZ25*ZDZ25
          ENDIF
          IF (ZMFUUS5(JL,JK) > RMFCMIN2) THEN
            ZFACT13=1.0_JPRB/ZMFUUS5(JL,JK)
            ZMFA35(JL,JK)=ZFACT13
          ELSE
            ZMFA35(JL,JK)=ZRMFCMIN
          ENDIF
          ZUU5(JL,JK)=PUEN5(JL,IK)+ZMFUUB5(JL)*ZDZ25*ZMFA35(JL,JK)
          ZVU5(JL,JK)=PVEN5(JL,IK)+ZMFUVB5(JL)*ZDZ25*ZMFA35(JL,JK)
          ZMFDUS5(JL,JK)=ZMFDUS5(JL,IKB)*ZDZ25
          ZUD5(JL,JK)=PUEN5(JL,IK)+ZUD5(JL,IKB)-PUEN5(JL,IKB-1)
          ZVD5(JL,JK)=PVEN5(JL,IK)+ZVD5(JL,IKB)-PVEN5(JL,IKB-1)
        ENDIF
! Add UV perturb to correct wind bias
        IF (LDCUM(JL) .AND. JK >= KCTOP(JL)) THEN
          ZUU5(JL,JK)=ZUU5(JL,JK)-RUVPER2*SIGN(1.0_JPRB,ZUU5(JL,JK))
          ZVU5(JL,JK)=ZVU5(JL,JK)-RUVPER2*SIGN(1.0_JPRB,ZVU5(JL,JK))
        ENDIF
      ENDDO
    ENDDO

  ENDIF

!-------------------------------------------------------------------

  CALL CUDUDV2 &
   & ( YDML_PHY_SLIN%YRECUMF2, &
   &   KIDIA,    KFDIA,    KLON,     KLEV,&
   &   ITOPM2,   KTYPE,    KCBOT,    KCTOP,    LDCUM,    PTSPHY,&
   &   PAPH5,    PUEN5,    PVEN5,    ZMFUUS5,  ZMFDUS5,&
   &   ZUU5,     ZUD5,     ZVU5,     ZVD5,&
   &   PTENU5,   PTENV5    ) 

   IF(LMFUVDIS2) THEN
! add KE dissipation
   ENDIF


ENDIF

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    IF (.NOT.LDCUM(JL)) THEN
      PLUDE5(JL,JK)=0.0_JPRB
      PLU5(JL,JK)=0.0_JPRB
    ENDIF
  ENDDO
ENDDO

!----------------------------------------------------------------------

!*   10.0          CHEMICAL TRACER TRANSPORT
!                  -------------------------

IF ( LMFTRAC .AND. KTRAC > 0 ) THEN

  ALLOCATE(ZTENC55(KLON,KLEV,KTRAC))
  ZTENC55(:,:,:)=PTENC5(:,:,:)

  ALLOCATE(ZMFUUC5(KLON,KLEV))
  ALLOCATE(ZMFDUC5(KLON,KLEV))
  ALLOCATE(ZMFUDR5(KLON,KLEV))
  ALLOCATE(ZMFDDR5(KLON,KLEV))


! transport switched off for mid-level convection
  DO JL=KIDIA,KFDIA
    IF( LDCUM(JL) .AND. KTYPE(JL)/=3 .AND. KCBOT(JL)-KCTOP(JL)>=1 ) THEN
      LLDCUM(JL)=.TRUE.
      LLDDRAF3(JL)=LLDDRAF(JL)
    ELSE
      LLDCUM(JL)=.FALSE.
      LLDDRAF3(JL)=.FALSE.
    ENDIF
  ENDDO
  
! check and correct mass fluxes for CFL criterium

  ZMFS35(:,:)=1.0_JPRB
  ZMFS45(:)=1.0_JPRB

  IF(RMFSOLCT2 <= 3.0_JPRB) THEN
    DO JK=2,KLEV
      DO JL=KIDIA,KFDIA
        IF(LLDCUM(JL) .AND. JK >= KCTOP(JL)) THEN
          ZMFMAX45=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*0.8_JPRB*ZCONS
          IF(PMFU5(JL,JK) > ZMFMAX45) THEN
            ZMFS35(JL,JK)=MIN(ZMFS35(JL,JK-1),ZMFMAX45/PMFU5(JL,JK)) 
          ELSE
            ZMFS35(JL,JK)=ZMFS35(JL,JK-1)
          ENDIF 
          ZMFS45(JL)=ZMFS35(JL,JK)
        ENDIF
      ENDDO
    ENDDO
  ENDIF


  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
      IF(LLDCUM(JL) .AND. JK >= KCTOP(JL)-1) THEN
        ZMFUUC5(JL,JK)=PMFU5(JL,JK)*ZMFS45(JL)
        ZMFUDR5(JL,JK)=ZMFUDE_RATE5(JL,JK)*ZMFS45(JL)
        ZDMFUPC5(JL,JK)=(PMFLXR5(JL,JK)+PMFLXS5(JL,JK))*ZMFS45(JL)
        IKB=KCBOT(JL)
        IF(JK>IKB) ZDMFUPC5(JL,JK)=ZDMFUPC5(JL,IKB)
      ELSE
        ZMFUUC5(JL,JK)=0.0_JPRB
        ZMFUDR5(JL,JK)=0.0_JPRB
      ENDIF
      IF ( LLDDRAF3(JL) .AND. JK >= IDTOP(JL)-1) THEN
        ZMFDUC5(JL,JK)=PMFD5(JL,JK)*ZMFS45(JL)
        ZMFDDR5(JL,JK)=ZMFDDE_RATE5(JL,JK)*ZMFS45(JL)
        ZDMFDPC5(JL,JK)=0.0_JPRB
      ELSE
        ZMFDUC5(JL,JK)=0.0_JPRB
        ZMFDDR5(JL,JK)=0.0_JPRB
      ENDIF
    ENDDO
  ENDDO

  IF(LMFSCAV) THEN
    ZSCAV5(:)=PSCAV5(:)
  ELSE
    ZSCAV5(:)=0.0_JPRB
    ZDMFUPC5(:,:)=0.0_JPRB
  ENDIF

  CALL CUCTRACER &
   & ( YDCST,YDML_PHY_SLIN%YRCUMFS,YDML_PHY_SLIN%YRECUMF2,YDML_PHY_EC%YRECUMF,&
   &   KIDIA,    KFDIA,    KLON,     KLEV,     KTRAC,&
   &   KCTOP,    IDTOP,    KTYPE,&
   &   LLDCUM,   LLDDRAF3, PTSPHY,&
   &   PAPH5,    PAP5,  ZMFUUC5,  ZMFDUC5,  PMFU5,  PMFD5,  ZMFUDR5,  ZMFDDR5,&
   &   ZDMFUPC5, ZDMFDPC5,&
   &   PCEN5,    PTENC5,   ZSCAV5 )

ENDIF

!----------------------------------------------------------------------

!                --------  ADJOINT COMPUTATIONS  --------

!----------------------------------------------------------------------

!*                           INITIALISATIONS
!                            ---------------

PMFLXR5(:,:)=0.0_JPRB
PMFLXS5(:,:)=0.0_JPRB

ZTENH(:,:) = 0.0_JPRB
ZQENH(:,:) = 0.0_JPRB
ZQSENH(:,:) = 0.0_JPRB
ZTD(:,:) = 0.0_JPRB
ZQD(:,:) = 0.0_JPRB
ZMFUS(:,:) = 0.0_JPRB
ZMFDS(:,:) = 0.0_JPRB
ZMFUQ(:,:) = 0.0_JPRB
ZMFDQ(:,:) = 0.0_JPRB
ZDMFUP(:,:) = 0.0_JPRB
ZDMFDP(:,:) = 0.0_JPRB
ZDMFUPC(:,:) = 0.0_JPRB
ZDMFDPC(:,:) = 0.0_JPRB
ZMFUL(:,:) = 0.0_JPRB
ZRFL(:) = 0.0_JPRB
ZUU(:,:) = 0.0_JPRB
ZVU(:,:) = 0.0_JPRB
ZUD(:,:) = 0.0_JPRB
ZVD(:,:) = 0.0_JPRB
ZMFUB(:) = 0.0_JPRB
ZDPMEL(:,:) = 0.0_JPRB
ZLGLAC(:,:) = 0.0_JPRB
ZQPRCV(:,:) = 0.0_JPRB
ZBUOH(:,:) = 0.0_JPRB
ZWUBASE(:) = 0.0_JPRB
ZDHPBL(:) = 0.0_JPRB
ZMFUDE_RATE(:,:) = 0.0_JPRB
ZMFDDE_RATE(:,:) = 0.0_JPRB
ZWMEAN(:) = 0.0_JPRB
ZERATEU(:,:) = 0.0_JPRB
ZERATED(:,:) = 0.0_JPRB
ZKHVFL(:)= 0.0_JPRB
!----------------------------------------------------------------------

!*   10.0          CHEMICAL TRACER TRANSPORT
!                  -------------------------

IF ( LMFTRAC .AND. KTRAC > 0 ) THEN

  ALLOCATE(ZMFUUC(KLON,KLEV))
  ALLOCATE(ZMFDUC(KLON,KLEV))
  ALLOCATE(ZMFUDR(KLON,KLEV))
  ALLOCATE(ZMFDDR(KLON,KLEV))

  ZMFUUC(:,:)=0.0_JPRB
  ZMFDUC(:,:)=0.0_JPRB
  ZMFUDR(:,:)=0.0_JPRB
  ZMFDDR(:,:)=0.0_JPRB
  ZMFS(:)=0.0_JPRB
  ZDMFUPC(:,:)=0.0_JPRB
  ZDMFDPC(:,:)=0.0_JPRB

  CALL CUCTRACERAD &
   & ( YDML_PHY_SLIN%YRECUMF2,&
   &   KIDIA,    KFDIA,    KLON,     KLEV,     KTRAC,&
   &   KCTOP,    IDTOP,&
   &   LLDCUM,   LLDDRAF3, PTSPHY,&
   &   PAPH5,    ZMFUUC5,  ZMFDUC5,  ZMFUDR5,  ZMFDDR5,&
   &   ZDMFUPC5, ZDMFDPC5,&
   &   PCEN5,    ZTENC55,  ZSCAV5,&
   &   PAPH,     ZMFUUC,   ZMFDUC,   ZMFUDR,   ZMFDDR,&
   &   ZDMFUPC,  ZDMFDPC,&
   &   PCEN,     PTENC )

  IF(.NOT.LMFSCAV) THEN
    ZDMFUPC(:,:)=0.0_JPRB
  ENDIF

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA 
      IF (LLDDRAF3(JL) .AND. JK >= IDTOP(JL)-1) THEN
        ZMFDDE_RATE(JL,JK) = ZMFDDE_RATE(JL,JK) + ZMFS45(JL)*ZMFDDR(JL,JK)
        ZMFS(JL) = ZMFS(JL) + ZMFDDE_RATE5(JL,JK)*ZMFDDR(JL,JK) 

        PMFD(JL,JK) = PMFD(JL,JK) + ZMFS45(JL)  *ZMFDUC(JL,JK)
        ZMFS(JL)    = ZMFS(JL)    + PMFD5(JL,JK)*ZMFDUC(JL,JK)
      ENDIF  
      ZMFDDR(JL,JK)=0.0_JPRB
      ZMFDUC(JL,JK)=0.0_JPRB

      IF (LLDCUM(JL) .AND. JK >= KCTOP(JL)-1) THEN
        IKB=KCBOT(JL)
        IF (JK>IKB) THEN
           ZDMFUPC(JL,IKB)=ZDMFUPC(JL,IKB)+ZDMFUPC(JL,JK)
           ZDMFUPC(JL,JK)=0.0_JPRB
        ENDIF
        PMFLXR(JL,JK) = PMFLXR(JL,JK) + ZMFS45(JL)*ZDMFUPC(JL,JK)
        PMFLXS(JL,JK) = PMFLXS(JL,JK) + ZMFS45(JL)*ZDMFUPC(JL,JK)
        ZMFS(JL) = ZMFS(JL) + (PMFLXR5(JL,JK)+PMFLXS5(JL,JK))*ZDMFUPC(JL,JK) 

        ZMFUDE_RATE(JL,JK) = ZMFUDE_RATE(JL,JK) + ZMFS45(JL)*ZMFUDR(JL,JK)
        ZMFS(JL) = ZMFS(JL) + ZMFUDE_RATE5(JL,JK)*ZMFUDR(JL,JK) 

        PMFU(JL,JK) = PMFU(JL,JK) + ZMFS45(JL)  *ZMFUUC(JL,JK)
        ZMFS(JL)    = ZMFS(JL)    + PMFU5(JL,JK)*ZMFUUC(JL,JK)
      ENDIF
      ZMFUDR(JL,JK)=0.0_JPRB
      ZMFUUC(JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
 
  IF (RMFSOLCT2 <= 3.0_JPRB) THEN
    DO JK=KLEV,2,-1
      DO JL=KIDIA,KFDIA
        IF (LLDCUM(JL) .AND. JK >= KCTOP(JL)) THEN
          ZMFMAX45=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*0.8_JPRB*ZCONS
!LOP No perturbation of mass flux limiter
          IF(PMFU5(JL,JK) > ZMFMAX45) THEN
            IF (ZMFS35(JL,JK-1) > ZMFMAX45/PMFU5(JL,JK)) THEN
              PMFU(JL,JK)=PMFU(JL,JK)-ZMFS(JL)*ZMFMAX45/(PMFU5(JL,JK)*PMFU5(JL,JK))
              ZMFS(JL)=0.0_JPRB
            ENDIF
          ENDIF
        ENDIF
      ENDDO
    ENDDO
  ENDIF

  ZMFS (:)=0.0_JPRB

  DEALLOCATE(ZMFDDR)
  DEALLOCATE(ZMFUDR)
  DEALLOCATE(ZMFDUC)
  DEALLOCATE(ZMFUUC)

  DEALLOCATE(ZMFDDR5)
  DEALLOCATE(ZMFUDR5)
  DEALLOCATE(ZMFDUC5)
  DEALLOCATE(ZMFUUC5)

  DEALLOCATE(ZTENC55)

ENDIF
!----------------------------------------------------------------------

DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    IF (.NOT.LDCUM(JL)) THEN
      PLUDE(JL,JK)=0.0_JPRB
      PLU(JL,JK)=0.0_JPRB
    ENDIF
  ENDDO
ENDDO

!*    9.           UPDATE TENDENCIES FOR U AND V IN SUBROUTINE CUDUDV2AD
!                  -----------------------------------------------------

IF(LMFDUDV2) THEN

  ZMFUUS(:,:)=0.0_JPRB
  ZMFDUS(:,:)=0.0_JPRB
  ZMFS(:)=0.0_JPRB

!-------------------------------------------------------------------

  IF(LMFUVDIS2) THEN
! add KE dissipation
  ENDIF

  CALL CUDUDV2AD &
   & ( YDML_PHY_SLIN%YRECUMF2,&
   & KIDIA,    KFDIA,    KLON,     KLEV,&
   & ITOPM2,   KTYPE,    KCBOT,    KCTOP,    LDCUM,    PTSPHY,&
   & PAPH5,    PUEN5,    PVEN5,    ZMFUUS5,  ZMFDUS5,&
   & ZUU5,     ZUD5,     ZVU5,     ZVD5,&
   & ZTENU15,  ZTENV15,&
   & PAPH,     PUEN,     PVEN,     ZMFUUS,   ZMFDUS,&
   & ZUU,      ZUD,      ZVU,      ZVD,&
   & PTENU,    PTENV )  

! Recompute draught properties below for implicit
! based on linear flux profiles

  IF (RMFSOLUV2 > 0.0_JPRB) THEN

    ZMFUUB(:)=0.0_JPRB
    ZMFUVB(:)=0.0_JPRB

    DO JK=KLEV,2,-1
      IK=JK-1
      DO JL=KIDIA,KFDIA
        IF (LDCUM(JL) .AND. JK > KCBOT(JL)) THEN
          IKB=KCBOT(JL)
! Recompute some trajectory
          ZFACT12=1.0_JPRB/(PAPH5(JL,KLEV+1)-PAPH5(JL,IKB)) 
          ZDZ25=(PAPH5(JL,KLEV+1)-PAPH5(JL,JK))*ZFACT12
          ZDZ255=ZDZ25
          IF (KTYPE(JL) == 3) THEN
            ZDZ25=ZDZ25*ZDZ25
          ENDIF

          ZDZ=0.0_JPRB
          ZMFA=0.0_JPRB
      
          PVEN(JL,IK)    = PVEN(JL,IK)    + ZVD(JL,JK)
          ZVD(JL,IKB)    = ZVD(JL,IKB)    + ZVD(JL,JK)
          PVEN(JL,IKB-1) = PVEN(JL,IKB-1) - ZVD(JL,JK)
          ZVD(JL,JK)=0.0_JPRB

          PUEN(JL,IK)    = PUEN(JL,IK)    + ZUD(JL,JK)
          ZUD(JL,IKB)    = ZUD(JL,IKB)    + ZUD(JL,JK)
          PUEN(JL,IKB-1) = PUEN(JL,IKB-1) - ZUD(JL,JK)
          ZUD(JL,JK)=0.0_JPRB

          ZDZ = ZDZ + ZMFDUS(JL,JK)*ZMFDUS5(JL,IKB)
          ZMFDUS(JL,IKB) = ZMFDUS(JL,IKB) + ZMFDUS(JL,JK)*ZDZ25
          ZMFDUS(JL,JK)=0.0_JPRB

          PVEN(JL,IK) = PVEN(JL,IK) + ZVU(JL,JK)
          ZMFUVB(JL)  = ZMFUVB(JL)  + ZVU(JL,JK)*ZDZ25*ZMFA35(JL,JK)
          ZDZ         = ZDZ         + ZVU(JL,JK)*ZMFUVB5(JL)*ZMFA35(JL,JK)
          ZMFA        = ZMFA        + ZVU(JL,JK)*ZMFUVB5(JL)*ZDZ25
          ZVU(JL,JK)=0.0_JPRB

          PUEN(JL,IK) = PUEN(JL,IK) + ZUU(JL,JK)
          ZMFUUB(JL)  = ZMFUUB(JL)  + ZUU(JL,JK)*ZDZ25*ZMFA35(JL,JK)
          ZDZ         = ZDZ         + ZUU(JL,JK)*ZMFUUB5(JL)*ZMFA35(JL,JK)
          ZMFA        = ZMFA        + ZUU(JL,JK)*ZMFUUB5(JL)*ZDZ25
          ZUU(JL,JK)=0.0_JPRB

          IF (ZMFUUS5(JL,JK) > RMFCMIN2) THEN
            ZFACT13=1.0_JPRB/ZMFUUS5(JL,JK)
            ZMFUUS(JL,JK)=ZMFUUS(JL,JK)-ZMFA*ZFACT13*ZFACT13
          ENDIF
          ZMFA=0.0_JPRB

          IF (KTYPE(JL) == 3) THEN
            ZDZ=2.0_JPRB*ZDZ255*ZDZ
          ENDIF

          PAPH(JL,KLEV+1) = PAPH(JL,KLEV+1) + ZDZ*ZFACT12
          PAPH(JL,JK)     = PAPH(JL,JK)     - ZDZ*ZFACT12
          PAPH(JL,KLEV+1) = PAPH(JL,KLEV+1) - ZDZ*ZDZ25*ZFACT12
          PAPH(JL,IKB)    = PAPH(JL,IKB)    + ZDZ*ZDZ25*ZFACT12
          ZDZ=0.0_JPRB
        ENDIF
      ENDDO
    ENDDO

    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL)) THEN
        JK=KCBOT(JL)
        IK=JK-1

        ZVU(JL,JK)   = ZVU(JL,JK)   + ZMFUVB(JL)*ZMFUUS5(JL,JK)
        PVEN(JL,IK)  = PVEN(JL,IK)  - ZMFUVB(JL)*ZMFUUS5(JL,JK)
        ZMFUUS(JL,JK)= ZMFUUS(JL,JK)+ ZMFUVB(JL)*(ZVU15(JL,JK)-PVEN5(JL,IK))
        ZMFUVB(JL)=0.0_JPRB

        ZUU(JL,JK)   = ZUU(JL,JK)   + ZMFUUB(JL)*ZMFUUS5(JL,JK)
        PUEN(JL,IK)  = PUEN(JL,IK)  - ZMFUUB(JL)*ZMFUUS5(JL,JK)
        ZMFUUS(JL,JK)= ZMFUUS(JL,JK)+ ZMFUUB(JL)*(ZUU15(JL,JK)-PUEN5(JL,IK))
        ZMFUUB(JL)=0.0_JPRB  
      ENDIF
    ENDDO

  ENDIF

! Re-scale massfluxes of shallow convection for stability in momentum
!--------------------------------------------------------------------

  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL).AND.JK>=KCTOP(JL)-1) THEN
        PMFD (JL,JK)=PMFD (JL,JK)+ZMFS25(JL)  *ZMFDUS (JL,JK)
        ZMFS (JL)   =ZMFS (JL)   +PMFD5(JL,JK)*ZMFDUS (JL,JK)
        ZMFDUS(JL,JK)=0.0_JPRB

        PMFU (JL,JK)=PMFU (JL,JK)+ZMFS25(JL)  *ZMFUUS (JL,JK)
        ZMFS (JL)   =ZMFS (JL)   +PMFU5(JL,JK)*ZMFUUS (JL,JK)
        ZMFUUS(JL,JK)=0.0_JPRB
      ENDIF

      PMFD (JL,JK)=PMFD (JL,JK) + ZMFDUS (JL,JK)
      ZMFDUS(JL,JK)=0.0_JPRB

      PMFU (JL,JK)=PMFU (JL,JK) + ZMFUUS (JL,JK)
      ZMFUUS(JL,JK)=0.0_JPRB
    ENDDO
  ENDDO

  IF (RMFSOLUV2 <= 1.0_JPRB) THEN
    DO JK=KLEV,2,-1
      DO JL=KIDIA,KFDIA
        IF (LDCUM(JL) .AND. JK >= KCTOP(JL)-1) THEN
          ZMFMAX=0.0_JPRB
          ZMFMAX25=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*ZCONS
  
          IF (PMFU5(JL,JK) > ZMFMAX25 .AND. JK >= KCTOP(JL)) THEN
            ZFACT10=1.0_JPRB/PMFU5(JL,JK)
            IF (ZMFMAX25*ZFACT10 < ZMFS5(JL,JK-1)) THEN
              ZMFMAX=ZMFMAX+ZMFS(JL)*ZFACT10
              PMFU(JL,JK)=PMFU(JL,JK)-ZMFS(JL)*ZMFMAX25*ZFACT10**2
              ZMFS(JL)=0.0_JPRB
            ENDIF
          ENDIF
   
          PAPH(JL,JK  )=PAPH(JL,JK  )+ZMFMAX*ZCONS
          PAPH(JL,JK-1)=PAPH(JL,JK-1)-ZMFMAX*ZCONS
          ZMFMAX=0.0_JPRB
        ENDIF
      ENDDO
    ENDDO
  ENDIF

  ZMFS(:)=0.0_JPRB

! Updraft and downdraft wind computations
!----------------------------------------

  DO JK=KLEV,3,-1
    IK=JK-1
    DO JL=KIDIA,KFDIA
      IF(LDCUM(JL) .AND. LLDDRAF(JL)) THEN
        IF (JK == IDTOP(JL)) THEN
          PVEN(JL,IK)= PVEN(JL,IK) + ZVD(JL,JK)*0.5_JPRB
          ZVU(JL,JK) = ZVU(JL,JK)  + ZVD(JL,JK)*0.5_JPRB 
          ZVD(JL,JK) = 0.0_JPRB
          PUEN(JL,IK)= PUEN(JL,IK) + ZUD(JL,JK)*0.5_JPRB
          ZUU(JL,JK) = ZUU(JL,JK)  + ZUD(JL,JK)*0.5_JPRB 
          ZUD(JL,JK) = 0.0_JPRB
        ELSEIF (JK > IDTOP(JL)) THEN
          ZVD(JL,IK)    = ZVD(JL,IK)    + (1.0_JPRB-ZERATED5(JL,JK)) *ZVD(JL,JK)
          PVEN(JL,IK)   = PVEN(JL,IK)   + ZERATED5(JL,JK)            *ZVD(JL,JK)
          ZERATED(JL,JK)= ZERATED(JL,JK)+ (PVEN5(JL,IK)-ZVD15(JL,IK))*ZVD(JL,JK)
          ZVD(JL,JK) = 0.0_JPRB
          ZUD(JL,IK)    = ZUD(JL,IK)    + (1.0_JPRB-ZERATED5(JL,JK)) *ZUD(JL,JK)
          PUEN(JL,IK)   = PUEN(JL,IK)   + ZERATED5(JL,JK)            *ZUD(JL,JK)
          ZERATED(JL,JK)= ZERATED(JL,JK)+ (PUEN5(JL,IK)-ZUD15(JL,IK))*ZUD(JL,JK)
          ZUD(JL,JK) = 0.0_JPRB
        ENDIF
      ENDIF
    ENDDO
  ENDDO
  DO JK=2,KLEV-1
    IK=JK+1
    DO JL=KIDIA,KFDIA
      IF(LDCUM(JL)) THEN
        IF (JK < KCBOT(JL) .AND. JK >= KCTOP(JL)) THEN
          ZVU(JL,IK)    = ZVU(JL,IK)    + (1.0_JPRB-ZERATEU5(JL,JK)) *ZVU(JL,JK)
          PVEN(JL,JK)   = PVEN(JL,JK)   + ZERATEU5(JL,JK)            *ZVU(JL,JK)
          ZERATEU(JL,JK)= ZERATEU(JL,JK)+ (PVEN5(JL,JK)-ZVU15(JL,IK))*ZVU(JL,JK)
          ZVU(JL,JK) = 0.0_JPRB

          ZUU(JL,IK)    = ZUU(JL,IK)    + (1.0_JPRB-ZERATEU5(JL,JK)) *ZUU(JL,JK)
          PUEN(JL,JK)   = PUEN(JL,JK)   + ZERATEU5(JL,JK)            *ZUU(JL,JK)
          ZERATEU(JL,JK)= ZERATEU(JL,JK)+ (PUEN5(JL,JK)-ZUU15(JL,IK))*ZUU(JL,JK)
          ZUU(JL,JK) = 0.0_JPRB           
        ENDIF
        
        IF (JK == KCBOT(JL) .AND. KTYPE(JL) < 3) THEN
          IKB=ISTUP(JL)
          PVEN(JL,IKB-1) = PVEN(JL,IKB-1) + ZVU(JL,JK)
          ZVU (JL,JK) = 0.0_JPRB 
          PUEN(JL,IKB-1) = PUEN(JL,IKB-1) + ZUU(JL,JK)
          ZUU (JL,JK) = 0.0_JPRB 
        ELSEIF (JK == KCBOT(JL) .AND. KTYPE(JL) == 3) THEN
          PVEN(JL,JK-1) = PVEN(JL,JK-1) + ZVU (JL,JK)
          ZVU (JL,JK) = 0.0_JPRB 
          PUEN(JL,JK-1) = PUEN(JL,JK-1) + ZUU (JL,JK)
          ZUU (JL,JK) = 0.0_JPRB 
        ENDIF
      ENDIF
    ENDDO
  ENDDO

ENDIF

!----------------------------------------------------------------------

!*    8.           UPDATE TENDENCIES FOR T AND Q IN SUBROUTINE CUDTDQNAD
!                  -----------------------------------------------------

CALL CUDTDQN2AD &
 & ( YDML_PHY_SLIN,&
 & KIDIA,    KFDIA,    KLON,     KLEV,&
 & ITOPM2,   KCTOP,    IDTOP,&
 & LDRAIN1D, LDCUM,    LLDDRAF,  PTSPHY,&
 & PAPH5,    PGEOH5,   PGEO5,&
 & PTEN5,    ZTENH5,   PQEN5,    ZQENH5,   PQSEN5,&
 & ZLGLAC5,  ZLUDE15,  PMFU5,    PMFD5,&
 & ZMFUS5,   ZMFDS5,   ZMFUQ5,   ZMFDQ5,&
 & ZMFUL5,   ZDMFUP5,  ZDMFDP5,  ZDPMEL5,&
 & ZTENT15,  ZTENQ15,  PENTH5,&
 & PAPH,     PGEOH,    PGEO,&
 & PTEN,     ZTENH,    PQEN,     ZQENH,    PQSEN,&
 & ZLGLAC,   PLUDE,    PMFU,     PMFD,&
 & ZMFUS,    ZMFDS,    ZMFUQ,    ZMFDQ,&
 & ZMFUL,    ZDMFUP,   ZDMFDP,   ZDPMEL,&
 & PTENT,    PTENQ,    PENTH  )  

IF (RMFSOLTQ2 > 0.0_JPRB) THEN
! derive draught properties for implicit
  DO JK=2,KLEV
    DO JL=KIDIA,KFDIA
      IF (LDCUM(JL)) THEN
        IF (JK > KCBOT(JL)) THEN
          ZMFA=0.0_JPRB

          IF (LLDDRAF(JL)) THEN

            PMFD(JL,JK)  = PMFD(JL,JK)  + ZMFDS(JL,JK)&
                       & *(YDCST%RCPD*ZTD5(JL,JK)+PGEOH5(JL,JK))
            ZTD(JL,JK)   = ZTD(JL,JK)   + ZMFDS(JL,JK)*PMFD5(JL,JK)*YDCST%RCPD
            PGEOH(JL,JK) = PGEOH(JL,JK) + ZMFDS(JL,JK)*PMFD5(JL,JK)
            ZMFDS(JL,JK)=0.0_JPRB

            PMFD(JL,JK) = PMFD(JL,JK) + ZMFDQ(JL,JK)*ZQD5(JL,JK)
            ZQD (JL,JK) = ZQD (JL,JK) + ZMFDQ(JL,JK)*PMFD5(JL,JK)
            ZMFDQ(JL,JK)=0.0_JPRB

            ZTENH(JL,JK) = ZTENH(JL,JK) + ZTD(JL,JK)
            ZMFA         = ZMFA         + ZTD(JL,JK)*ZMFDS45(JL,JK)*ZRCPD
            ZMFDS(JL,JK) = ZMFDS(JL,JK) + ZTD(JL,JK)*ZMFA25(JL,JK)*ZRCPD
            ZTD(JL,JK)=0.0_JPRB

            ZQENH(JL,JK) = ZQENH(JL,JK) + ZQD(JL,JK)
            ZMFA         = ZMFA         + ZQD(JL,JK)*ZMFDQ45(JL,JK)
            ZMFDQ(JL,JK) = ZMFDQ(JL,JK) + ZQD(JL,JK)*ZMFA25(JL,JK)
            ZQD(JL,JK)=0.0_JPRB
            
            IF (PMFD5(JL,JK) < -1.E-15_JPRB) THEN
              ZFACT11=1.0_JPRB/PMFD5(JL,JK)
              PMFD(JL,JK) = PMFD(JL,JK) - ZMFA*ZFACT11*ZFACT11
            ENDIF
            ZMFA=0.0_JPRB
          ENDIF

          PMFU(JL,JK) = PMFU(JL,JK) + ZMFUQ(JL,JK)*PQU5(JL,JK)
          PQU (JL,JK) = PQU (JL,JK) + ZMFUQ(JL,JK)*PMFU5(JL,JK)
          ZMFUQ(JL,JK)=0.0_JPRB

          PMFU(JL,JK)  = PMFU(JL,JK)  + ZMFUS(JL,JK)&
                     & *(YDCST%RCPD*PTU5(JL,JK)+PGEOH5(JL,JK))
          PTU(JL,JK)   = PTU(JL,JK)   + ZMFUS(JL,JK)*PMFU5(JL,JK)*YDCST%RCPD
          PGEOH(JL,JK) = PGEOH(JL,JK) + ZMFUS(JL,JK)*PMFU5(JL,JK)
          ZMFUS(JL,JK)=0.0_JPRB

          ZTENH(JL,JK) = ZTENH(JL,JK) + PTU(JL,JK)
          ZMFA         = ZMFA         + PTU(JL,JK)*ZMFUS45(JL,JK)*ZRCPD
          ZMFUS(JL,JK) = ZMFUS(JL,JK) + PTU(JL,JK)*ZMFA15(JL,JK)*ZRCPD
          PTU(JL,JK)=0.0_JPRB
          
          ZQENH(JL,JK) = ZQENH(JL,JK) + PQU(JL,JK)
          ZMFA         = ZMFA         + PQU(JL,JK)*ZMFUQ45(JL,JK)
          ZMFUQ(JL,JK) = ZMFUQ(JL,JK) + PQU(JL,JK)*ZMFA15(JL,JK)
          PQU(JL,JK)=0.0_JPRB

          IF (PMFU5(JL,JK) > 1.E-15_JPRB) THEN
            ZFACT11=1.0_JPRB/PMFU5(JL,JK)
            PMFU(JL,JK) = PMFU(JL,JK) - ZMFA*ZFACT11*ZFACT11
          ENDIF
          ZMFA=0.0_JPRB

        ELSEIF (JK <= KCBOT(JL) .AND. JK >= KCTOP(JL)) THEN  
        
          PMFD(JL,JK) = PMFD(JL,JK) + ZMFDQ(JL,JK)*ZQD5(JL,JK)
          ZQD (JL,JK) = ZQD (JL,JK) + ZMFDQ(JL,JK)*PMFD5(JL,JK)
          ZMFDQ(JL,JK)=0.0_JPRB
          
          PMFD(JL,JK)  = PMFD(JL,JK)  + ZMFDS(JL,JK)&
                     & *(YDCST%RCPD*ZTD5(JL,JK)+PGEOH5(JL,JK))
          ZTD(JL,JK)   = ZTD(JL,JK)   + ZMFDS(JL,JK)*PMFD5(JL,JK)*YDCST%RCPD
          PGEOH(JL,JK) = PGEOH(JL,JK) + ZMFDS(JL,JK)*PMFD5(JL,JK)
          ZMFDS(JL,JK)=0.0_JPRB

          PMFU(JL,JK) = PMFU(JL,JK) + ZMFUQ(JL,JK)*PQU5(JL,JK)
          PQU (JL,JK) = PQU (JL,JK) + ZMFUQ(JL,JK)*PMFU5(JL,JK)
          ZMFUQ(JL,JK)=0.0_JPRB

          PMFU(JL,JK)  = PMFU(JL,JK)  + ZMFUS(JL,JK)&
                     & *(YDCST%RCPD*PTU5(JL,JK)+PGEOH5(JL,JK))
          PTU(JL,JK)   = PTU(JL,JK)   + ZMFUS(JL,JK)*PMFU5(JL,JK)*YDCST%RCPD
          PGEOH(JL,JK) = PGEOH(JL,JK) + ZMFUS(JL,JK)*PMFU5(JL,JK)
          ZMFUS(JL,JK)=0.0_JPRB
        ENDIF
      ENDIF
    ENDDO
  ENDDO
ENDIF

!----------------------------------------------------------------------

!- rescale DD fluxes if total mass flux becomes negative
!- correct DD detrainment rates if entrainment becomes negative
!- correct UD detrainment rates if entrainment becomes negative
!- conservation correction for precip

ZMFUUB(:)=0.0_JPRB
ZMFS(:)=0.0_JPRB

DO JK=KLEV,2,-1
  DO JL=KIDIA,KFDIA
    IF (ZMFS65(JL) < 1.0_JPRB .AND. JK >= IDTOP(JL)-1) THEN
      ZMFS(JL)=ZMFS(JL)+ZDMFDP35(JL,JK)*ZDMFDP(JL,JK)
      ZDMFDP(JL,JK)=ZMFS65(JL)*ZDMFDP(JL,JK)
  
      ZMFUUB(JL)=ZMFUUB(JL)+PMFLXR(JL,JK+1)
  
      ZDMFDP(JL,JK)=ZDMFDP(JL,JK)-(1.0_JPRB-ZMFS65(JL))*ZMFUUB(JL)
      ZMFS(JL)=ZMFS(JL)+ZDMFDP35(JL,JK)*ZMFUUB(JL)
  
      ZMFS(JL)=ZMFS(JL)+ZMFDDE_RATE35(JL,JK)*ZMFDDE_RATE(JL,JK)
      ZMFDDE_RATE(JL,JK)=ZMFDDE_RATE (JL,JK)*ZMFS65(JL)
  
      ZMFS(JL)=ZMFS(JL)+ZMFDQ35(JL,JK)*ZMFDQ(JL,JK) 
      ZMFDQ(JL,JK)=ZMFDQ(JL,JK)*ZMFS65(JL)
  
      ZMFS(JL)=ZMFS(JL)+ZMFDS35(JL,JK)*ZMFDS(JL,JK) 
      ZMFDS(JL,JK)=ZMFDS(JL,JK)*ZMFS65(JL)
  
      ZMFS(JL)=ZMFS(JL)+ZMFD35(JL,JK)*PMFD(JL,JK) 
      PMFD(JL,JK)=PMFD(JL,JK)*ZMFS65(JL)
    ENDIF
  ENDDO
ENDDO

ZMFUUB(:)=0.0_JPRB

DO JK=KLEV,2,-1
  DO JL=KIDIA,KFDIA
    IF (LLDDRAF4(JL) .AND. JK >= IDTOP(JL)-1) THEN
      ZMFMAX=0.0_JPRB
      ZMFMAX35=PMFU5(JL,JK)*0.60_JPRB
      IF(ZMFD35(JL,JK)+ZMFMAX35+1.E-15_JPRB < 0.0_JPRB) THEN
        ZFACT10=1.0_JPRB/ZMFD35(JL,JK)
        IF (ZMFS55(JL,JK-1) > -ZMFMAX35*ZFACT10) THEN
          ZMFMAX=ZMFMAX-ZMFS(JL)*ZFACT10
          PMFD(JL,JK)=PMFD(JL,JK)+ZMFS(JL)*ZMFMAX35*ZFACT10**2
          ZMFS(JL)=0.0_JPRB
        ENDIF
      ENDIF
      PMFU(JL,JK)=PMFU(JL,JK)+ZMFMAX*0.60_JPRB
      ZMFMAX=0.0_JPRB
    ENDIF
  ENDDO
ENDDO

ZMFS(:)=0.0_JPRB


!*   7.      DETERMINE FINAL CONVECTIVE FLUXES IN 'CUFLX2AD'
!            -----------------------------------------------

CALL CUFLX2AD &
 & ( YDML_PHY_SLIN%YRECUMF2,YDML_PHY_SLIN%YREPHLI, &
 & KIDIA,    KFDIA,    KLON,    KLEV,&
 & PTSPHY,&
 & PTEN5,    PQEN5,    ZQSEN15,  ZTENH5,   ZQENH5,&
 & PAPH5,    PAP5,     LDLAND,   LDCUM, &
 & KCBOT,    KCTOP,    IDTOP,    ITOPM2,&
 & KTYPE,    LLDDRAF,&
 & ZMFU25,   ZMFD25,   ZMFUS25,  ZMFDS25,&
 & ZMFUQ25,  ZMFDQ25,  ZMFUL25,  ZLUDE25,&
 & ZDMFUP25, ZDMFDP25, ZDPMEL5,  ZLGLAC15,&
 & PMFLXR5,  PMFLXS5,  PRAIN5,&
 & PTEN,     PQEN,     PQSEN,    ZTENH,    ZQENH,&
 & PAPH,     PAP,&
 & PMFU,     PMFD,     ZMFUS,    ZMFDS,&
 & ZMFUQ,    ZMFDQ,    ZMFUL,    PLUDE,&
 & ZDMFUP,   ZDMFDP,   ZDPMEL,   ZLGLAC,&
 & PMFLXR,   PMFLXS,   PRAIN )  

!*    6.        SIMPLIFIED CONVECTION SCHEME:
!*              CALCULATE CLOUD BASE MASSFLUX FROM A
!*              CAPE CLOSURE FOR ALL TYPES OF CONVECTION,
!*              AND FROM THE PROFILE OF RATIO M(z)/Mbase
!*              WHICH WAS CALCULATED IN CUASCN2.
!*              RESCALE ALL MASS-FLUXES ACCORDINGLY.
!           ---------------------------------------------

DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    ZMFUB(JL)=ZMFUB(JL)+ZMFDDE_RATE(JL,JK)*ZMFDDE_RATE15(JL,JK)
    ZMFDDE_RATE(JL,JK)=ZMFDDE_RATE(JL,JK)*ZMFUB5(JL)

    ZMFUB(JL)=ZMFUB(JL)+ZMFUDE_RATE(JL,JK)*ZMFUDE_RATE15(JL,JK)
    ZMFUDE_RATE(JL,JK)=ZMFUDE_RATE(JL,JK)*ZMFUB5(JL)

    ZMFUB(JL)=ZMFUB(JL)+ZDMFDP(JL,JK)*ZDMFDP15(JL,JK)
    ZDMFDP(JL,JK)=ZDMFDP(JL,JK)*ZMFUB5(JL)

    ZMFUB(JL)=ZMFUB(JL)+PMFD(JL,JK)*ZMFD15(JL,JK)
    PMFD(JL,JK)=PMFD(JL,JK)*ZMFUB5(JL)

    ZMFUB(JL)=ZMFUB(JL)+ZDMFUP(JL,JK)*ZDMFUP15(JL,JK)
    ZDMFUP(JL,JK)=ZDMFUP(JL,JK)*ZMFUB5(JL)

    ZMFUB(JL)=ZMFUB(JL)+PMFU(JL,JK)*ZMFU15(JL,JK)
    PMFU(JL,JK)=PMFU(JL,JK)*ZMFUB5(JL)
  ENDDO
ENDDO

ZCAPE(:)=0.0_JPRB
ZHEAT(:)=0.0_JPRB
ZCAPDCYCL(:)=0.0_JPRB
ZCAPPBL(:)=0.0_JPRB
ZTAU(:)=0.0_JPRB
!LOP ZRESCAL(:)=0.0_JPRB

DO JL=KIDIA,KFDIA
  IF(LDCUM(JL)) THEN
!LOP    ZMFMAX=0.0_JPRB

! Reduce cloud base mass flux perturbation if required
    IF (LREGCV) THEN
      ZMFUB(JL)=ZMFUB(JL)*0.25_JPRB
    ENDIF

!LOP    IF (ZMFUB35(JL) > ZMFMAX5(JL)) THEN
!LOP      ZMFMAX = ZMFMAX + ZMFUB(JL)
!LOP      ZMFUB(JL)=0.0_JPRB
!LOP    ENDIF
    IF (ZMFUB35(JL) > ZMFMAX5(JL)) ZMFUB(JL)=0.0_JPRB
    IF (ZMFUB25(JL) < 0.001_JPRB) ZMFUB(JL)=0.0_JPRB

    IF (KTYPE(JL) /= 2) THEN
      ZFACT8=1.0_JPRB/(ZHEAT5(JL)*ZTAU5(JL))
      ZCAPE(JL) = ZCAPE(JL) + ZMFUB(JL)*ZFACT8
      ZHEAT(JL) = ZHEAT(JL) - ZMFUB(JL)*ZCAPE5(JL)/ZHEAT5(JL)*ZFACT8
      ZTAU(JL)  = ZTAU(JL)  - ZMFUB(JL)*ZCAPE5(JL)/ZTAU5(JL)*ZFACT8
      ZMFUB(JL)=0.0_JPRB

      ZCAPDCYCL(JL) = ZCAPDCYCL(JL) - ZCAPE(JL)
    ELSE
!LOP Reduce cloud base mass flux for shallow convection  
!LLL      ZMFUB(JL)=ZMFUB(JL)*0.3_JPRB 

      IF (ZDHPBL5(JL) > 0.0_JPRB) THEN
        IKB=KCBOT(JL)

        ZQUMQE=0.0_JPRB
        ZDH=0.0_JPRB
        ZDH2=0.0_JPRB
        ZEPS2 = 0.0_JPRB

        IF (LLO1(JL)) THEN
          ZDHPBL(JL) = ZDHPBL(JL) + ZMFUB(JL) * ZFACT6(JL)
          ZDH        = ZDH        - ZMFUB(JL) * ZDHPBL5(JL) * ZFACT6(JL)**2
          ZMFUB(JL) = 0.0_JPRB

          ZDH = 0.0_JPRB
        ENDIF

!LOP        IF (LREGCV) THEN
!LOP          IF (ZDHPBL5(JL)*ZFACT9**2 > 1.E+03_JPRB) &
!LOP             & ZMFUB(JL)=ZMFUB(JL)*0.2_JPRB
!LOP        ENDIF

        ZDHPBL(JL)=ZDHPBL(JL)+ZMFUB(JL)*ZFACT9(JL)
        ZDH       =ZDH       -ZMFUB(JL)*ZDHPBL5(JL)*ZFACT9(JL)**2
        ZMFUB(JL) = 0.0_JPRB

!LOP        IF (LREGCV) ZDH = ZDH * 0.1_JPRB

        ZDH  = YDCST%RG * ZDH

        IF (LLO2(JL)) ZDH = 0.0_JPRB
        
        ZDH2 = ZDH2 + 0.5_JPRB * ZDH
        ZDH  =        0.5_JPRB * ZDH

        PTU(JL,IKB-1)  = PTU(JL,IKB-1)  + ZCONS3 * ZDH2
        ZTENH(JL,IKB-1)= ZTENH(JL,IKB-1)- ZCONS3 * (1.0_JPRB - ZEPS25(JL)) * ZDH2
        ZQUMQE         = ZQUMQE         + ZCONS4 * ZDH2
        ZEPS2          = ZEPS2          - ZCONS3 * (ZTD25(JL,IKB-1) - ZTENH5(JL,IKB-1)) * ZDH2
        ZTD(JL,IKB-1)  = ZTD(JL,IKB-1)  - ZCONS3 * ZEPS25(JL) * ZDH2
        ZDH2 = 0.0_JPRB

        PTU(JL,IKB)  = PTU(JL,IKB)  + ZCONS3 * ZDH
        ZTENH(JL,IKB)= ZTENH(JL,IKB)- ZCONS3 * (1.0_JPRB-ZEPS25(JL)) * ZDH
        ZQUMQE       = ZQUMQE       + ZCONS4 * ZDH
        ZEPS2 = ZEPS2 -  ZCONS3 * (ZTD25(JL,IKB)-ZTENH5(JL,IKB)) * ZDH
        ZTD(JL,IKB) = ZTD(JL,IKB) - ZCONS3 * ZEPS25(JL) * ZDH
        ZDH = 0.0_JPRB

        PQU(JL,IKB)   = PQU(JL,IKB)   + ZQUMQE
        PLU(JL,IKB)   = PLU(JL,IKB)   + ZQUMQE
        ZEPS2 = ZEPS2 - (ZQD25(JL,IKB)-ZQENH5(JL,IKB)) * ZQUMQE
        ZQD(JL,IKB) = ZQD(JL,IKB) - ZEPS25(JL) * ZQUMQE
        ZQENH(JL,IKB) = ZQENH(JL,IKB) - (1.0_JPRB-ZEPS25(JL)) * ZQUMQE
        ZQUMQE = 0.0_JPRB

        IF (LLO4(JL)) PMFD(JL,IKB) = PMFD(JL,IKB) - ZEPS2
        ZEPS2 =0.0_JPRB
      ELSE
!LOP        ZMFMAX = ZMFMAX + ZMFUB(JL) * 0.1_JPRB
        ZMFUB(JL)=0.0_JPRB
      ENDIF
    ENDIF

!LOP    ZRESCAL(JL) = ZRESCAL(JL) - ZMFMAX / ZRESCAL5(JL)**2
!LOP    ZMFMAX=0.0_JPRB

  ELSE
    ZMFUB(JL)=0.0_JPRB
  ENDIF
ENDDO

! CFL rescaling for cloud base mass flux
!LOP DO JK=KLEV,2,-1
!LOP   DO JL=KIDIA,KFDIA
!LOP     IF (LLTEST25(JL,JK)) THEN
!LOP       ZMFMAX25=(PAPH5(JL,JK)-PAPH5(JL,JK-1))*ZCONS2*RMFLIC2+RMFLIA2
!LOP 
!LOP       ZMFMAX=0.0_JPRB
!LOP 
!LOP       IF (LLTEST35(JL,JK)) THEN
!LOP         PMFU(JL,JK) = PMFU(JL,JK) + ZRESCAL(JL) / ZMFMAX25
!LOP         ZMFMAX = ZMFMAX - ZRESCAL(JL) * ZMFU15(JL,JK) / ZMFMAX25**2
!LOP         ZRESCAL(JL)=0.0_JPRB
!LOP       ENDIF
!LOP 
!LOP       PAPH(JL,JK)   = PAPH(JL,JK)   + ZMFMAX * ZCONS2 * RMFLIC2
!LOP       PAPH(JL,JK-1) = PAPH(JL,JK-1) - ZMFMAX * ZCONS2 * RMFLIC2
!LOP       ZMFMAX=0.0_JPRB
!LOP     ENDIF
!LOP   ENDDO
!LOP ENDDO
!LOP ZRESCAL(:)=0.0_JPRB

! Time scale and subcloud contribution to CAPE to be substracted 
! for better diurnal cycle over land.
DO JL=KIDIA,KFDIA
  IF(LDCUM(JL).AND.KTYPE(JL) /= 2) THEN
    ZWM=0.0_JPRB 
    IKD=ISTUP(JL)
    IKB=KCBOT(JL)
    IK=KCTOP(JL)

    ZTAURES5=1._JPRB+RTAU2*800._JPRB*SQRT(4._JPRB*PGAW(JL)/YDCST%RPI)
    ! for the 10-1 km resolution range increase ZTAURES
    ZDX5=2.0_JPRB*YDCST%RA*SQRT(YDCST%RPI*PGAW(JL))
    IF(ZDX5 < 8.E3_JPRB) THEN
      ZDX5=MAX(ZDX5,1.E2_JPRB)
      ZTAURES5=1.0_JPRB+(LOG(10.E3_JPRB/ZDX5))**2
    ENDIF
    IF(ZDX5 > 125.E3_JPRB) ZTAURES5=MIN(3.0_JPRB,ZTAURES5)

    IF(LLO3(JL).AND.LDLAND(JL).AND.RCAPDCYCL2==1.0_JPRB) THEN
      ZDZ=0.0_JPRB 

      IF (ZKHVFL5(JL) > 0.0_JPRB) THEN
! Trajectory recomputation.
        ZDZ5=MIN(1.E4_JPRB,PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1))/YDCST%RG

        ZTAU(JL)   = ZTAU(JL)   + YDCST%RCPD * ZKHVFL5(JL) * ZCAPDCYCL(JL) / ZDZ5
        ZKHVFL(JL) = ZKHVFL(JL) + YDCST%RCPD * ZTAU5(JL)   * ZCAPDCYCL(JL) / ZDZ5
        ZDZ        = ZDZ        - YDCST%RCPD * ZTAU5(JL) * ZKHVFL5(JL) * ZCAPDCYCL(JL) / ZDZ5**2
      ENDIF

      IF ((PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1)) < 1.E4_JPRB) THEN
        PGEOH(JL,IKB)    = PGEOH(JL,IKB)    + ZDZ / YDCST%RG
        PGEOH(JL,KLEV+1) = PGEOH(JL,KLEV+1) - ZDZ / YDCST%RG
      ENDIF
      ZDZ = 0.0_JPRB   
    ENDIF
    IF(LLO3(JL).AND.RCAPDCYCL2==2.0_JPRB) THEN
      IF(LDLAND(JL)) THEN
        ZCAPPBL(JL) = ZCAPPBL(JL) + ZCAPDCYCL(JL)*ZTAU5(JL)/ZTAURES5
        ZTAU(JL)    = ZTAU(JL)    + ZCAPDCYCL(JL)*ZCAPPBL5(JL)/ZTAURES5
      ELSE
        ! A few trajectory recomputations
        ZFACT2=SQRT(0.5*(PUEN5(JL,IKB)**2+PVEN5(JL,IKB)**2+PUEN5(JL,NJKT32)**2+PVEN5(JL,NJKT32)**2))
        ZFACT2=MAX(ZFACT2,0.1_JPRB)  ! security for following divisions.
        ZDUTEN5=2.0_JPRB+ZFACT2
        ZTAUPBL5=MIN(1.E4_JPRB,PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1))/(YDCST%RG*ZDUTEN5)

        ZCAPPBL(JL) = ZCAPPBL(JL) + ZCAPDCYCL(JL)*ZTAUPBL5
        ZTAUPBL     = ZCAPDCYCL(JL)*ZCAPPBL5(JL)

        IF ((PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1)) < 1.E4_JPRB) THEN
          PGEOH(JL,IKB)    = PGEOH(JL,IKB)    + ZTAUPBL /(YDCST%RG*ZDUTEN5)
          PGEOH(JL,KLEV+1) = PGEOH(JL,KLEV+1) - ZTAUPBL /(YDCST%RG*ZDUTEN5)
          ZDUTEN = -ZTAUPBL *(PGEOH5(JL,IKB)-PGEOH5(JL,KLEV+1))/(YDCST%RG*ZDUTEN5**2)
        ELSE
          ZDUTEN = -ZTAUPBL *1.E4_JPRB/(YDCST%RG*ZDUTEN5**2)
        ENDIF
 
        IF (ZFACT2 > 0.1_JPRB) THEN
          PUEN(JL,IKB)    =  PUEN(JL,IKB)    +  ZDUTEN*PUEN5(JL,IKB)   *0.5_JPRB/ZFACT2 
          PVEN(JL,IKB)    =  PVEN(JL,IKB)    +  ZDUTEN*PVEN5(JL,IKB)   *0.5_JPRB/ZFACT2 
          PUEN(JL,NJKT32) =  PUEN(JL,NJKT32) +  ZDUTEN*PUEN5(JL,NJKT32)*0.5_JPRB/ZFACT2 
          PVEN(JL,NJKT32) =  PVEN(JL,NJKT32) +  ZDUTEN*PVEN5(JL,NJKT32)*0.5_JPRB/ZFACT2 
        ELSE
          ZDUTEN = 0.0_JPRB
        ENDIF

      ENDIF
    ENDIF
    ZCAPDCYCL(JL)=0.0_JPRB
    ZFACT7=ZTAURES5/(ZWM5(JL)*YDCST%RG)
    PGEOH(JL,IK ) = PGEOH(JL,IK ) + ZTAU(JL)*ZFACT7
    PGEOH(JL,IKB) = PGEOH(JL,IKB) - ZTAU(JL)*ZFACT7
    ZWM = ZWM - ZTAU(JL)*(PGEOH5(JL,IK)-PGEOH5(JL,IKB))/ZWM5(JL)*ZFACT7
    ZTAU(JL)=0.0_JPRB

    IF (ZWMEAN5(JL) < 15.0_JPRB) THEN
      ZWMEAN(JL) = ZWMEAN(JL) + ZWM
    ENDIF
    ZWM=0.0_JPRB    
  ENDIF 
  ZCAPDCYCL(JL)=0.0_JPRB
ENDDO

DO JK=KLEV,1,-1
  DO JL=KIDIA,KFDIA
    IF(LDCUM(JL) .AND. KTYPE(JL) /= 2 .AND. &
     & JK <= KCBOT(JL) .AND. JK > KCTOP(JL)) THEN
      ZZBUO=0.0_JPRB
      ZDS=0.0_JPRB
      ZDZ=0.0_JPRB
      ZDZ15=PAPH5(JL,JK)-PAPH5(JL,JK-1)

      ZFACT2=ZCAPE(JL)*ZDZ15
      ZZBUO       =ZZBUO       +ZFACT2
      PQU(JL,JK)  =PQU(JL,JK)  +YDCST%RETV*ZFACT2
      ZQENH(JL,JK)=ZQENH(JL,JK)-YDCST%RETV*ZFACT2
      PLU(JL,JK)  =PLU(JL,JK)  -ZFACT2
      ZDZ         =ZDZ         +ZCAPE(JL)*(ZZBUO5(JL,JK) &
       & +YDCST%RETV*(ZQU25(JL,JK)-ZQENH5(JL,JK))-PLU5(JL,JK))  

      PAPH(JL,JK)   = PAPH(JL,JK)   + ZDZ
      PAPH(JL,JK-1) = PAPH(JL,JK-1) - ZDZ  
      ZDZ=0.0_JPRB

      ZFACT5=1.0_JPRB/ZTENH5(JL,JK)
      PTU(JL,JK)=PTU(JL,JK)+ZZBUO*ZFACT5
      ZTENH(JL,JK)=ZTENH(JL,JK)-ZZBUO*ZTU25(JL,JK)*ZFACT5**2
      ZZBUO=0.0_JPRB

      IF (LLTEST45(JL,JK)) THEN
        ZFACT3=YDCST%RG*(ZMFU15(JL,JK)+ZMFD15(JL,JK))
        ZFACT4=YDCST%RG*(ZDS5(JL,JK)+YDCST%RETV*(PQEN5(JL,JK-1)-PQEN5(JL,JK)))  
        ZDS          =ZDS          +ZFACT3*ZHEAT(JL)
        PQEN(JL,JK-1)=PQEN(JL,JK-1)+YDCST%RETV*ZFACT3*ZHEAT(JL)
        PQEN(JL,JK  )=PQEN(JL,JK  )-YDCST%RETV*ZFACT3*ZHEAT(JL)
        PMFU(JL,JK)  =PMFU(JL,JK)  +ZFACT4*ZHEAT(JL)
        PMFD(JL,JK)  =PMFD(JL,JK)  +ZFACT4*ZHEAT(JL)
      ENDIF

      PTEN(JL,JK-1)=PTEN(JL,JK-1)+ZFACT5*ZDS
      PTEN(JL,JK  )=PTEN(JL,JK  )-ZFACT5*ZDS
      ZDZ          =ZDZ          +ZFACT5*ZDS/YDCST%RCPD
      ZTENH(JL,JK) =ZTENH(JL,JK) -ZFACT5*ZDS*ZDS5(JL,JK)
      ZDS=0.0_JPRB

      PGEOH(JL,JK-1)=PGEOH(JL,JK-1)+ZDZ
      PGEOH(JL,JK  )=PGEOH(JL,JK  )-ZDZ
      ZDZ=0.0_JPRB
    ENDIF
  ENDDO
ENDDO

DO JL=KIDIA,KFDIA
  ZCAPE(JL)=0.0_JPRB
  ZHEAT(JL)=0.0_JPRB
ENDDO

!*    5.           CUMULUS DOWNDRAFT CALCULATIONS
!                  ------------------------------

IF(LMFDD2) THEN

!*            DETERMINE LFS AND DOWNDRAFTS IN 'CUDDRAFN2TL'
!             SIMPLIFIED SCHEME
!             ---------------------------------------------

  PMFD5(:,:)=0.0_JPRB
  ZMFDS5(:,:)=0.0_JPRB
  ZMFDQ5(:,:)=0.0_JPRB
  ZDMFDP5(:,:)=0.0_JPRB

  CALL CUDDRAFN2AD &
   & ( YDML_PHY_SLIN,&
   & KIDIA,    KFDIA,    KLON,     KLEV,&
   & KCBOT,    KCTOP,    LDCUM,&
   & ZTENH5,   ZQENH5,&
   & PTEN5,    ZQSEN25,  PGEO5,&
   & PGEOH5,   PAPH5,    ZTU25,    ZQU25,&
   & ZRFL15,   ZTD5,     ZQD5,     ZMFU15,&
   & PMFD5,    ZMFDS5,   ZMFDQ5,   ZDMFDP5,&
   & IDTOP,    LLDDRAF,&
   & ZMFDDE_RATE5, ZERATED5,&
   & ZTENH,    ZQENH,    PGEO,&
   & PGEOH,    PAPH,     PTU,      PQU,&
   & ZRFL,     ZTD,      ZQD,      PMFU,&
   & PMFD,     ZMFDS,    ZMFDQ,    ZDMFDP,&
   & ZMFDDE_RATE, ZERATED )  

ENDIF

IF(LMFUVDIS2) THEN
  DO JK=KLEV,1,-1
    DO JL=KIDIA,KFDIA
      ZTENU(JL,JK)=0.0_JPRB
      ZTENV(JL,JK)=0.0_JPRB
      PTENU(JL,JK)=PTENU(JL,JK)+ZTENU(JL,JK)
      PTENV(JL,JK)=PTENV(JL,JK)+ZTENV(JL,JK)
      ZTENU(JL,JK)=0.0_JPRB
      ZTENV(JL,JK)=0.0_JPRB
    ENDDO
  ENDDO
ENDIF

! Initializations

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    PMFD (JL,JK)=0.0_JPRB
    ZMFDS (JL,JK)=0.0_JPRB
    ZMFDQ (JL,JK)=0.0_JPRB
    ZDMFDP (JL,JK)=0.0_JPRB
    ZDPMEL (JL,JK)=0.0_JPRB
    ZMFDDE_RATE (JL,JK)=0.0_JPRB
    ZERATED (JL,JK)=0.0_JPRB
  ENDDO
ENDDO

!*   4.         SIMPLIFIED CONVECTION SCHEME
!*              COMPUTE VERTICAL PROFILE OF RATIO M(z)/Mbase
!               --------------------------------------------

DO JK=KLEV,2,-1
  DO JL=KIDIA,KFDIA
    ZDMFUP(JL,JK)=ZDMFUP(JL,JK)+ZRFL(JL)
  ENDDO
ENDDO
DO JL=KIDIA,KFDIA
  ZDMFUP(JL,1)=ZDMFUP(JL,1)+ZRFL(JL)
  ZRFL(JL)=0.0_JPRB
ENDDO

CALL CUASCN2AD &
 & ( YDML_PHY_SLIN%YRCUMFS,YDML_PHY_SLIN%YRECUMF2,&
 & KIDIA,    KFDIA,     KLON,    KLEV,&
 & LDLAND,&
 & PTEN5,    ZTENH35,   ZQENH35,&
 & PGEOH5,   PQEN5,     ZQSEN25,&
 & PAPH5,    ZLGLAC25,  ZQPRCV5,&
 & ZBUOH5,   ZWUBASE5,  LL_LDCUM2, I_KTYPE1,&
 & ZTU15,    ZQU15,     ZLU15,&
 & PMFU5,    ZMFUB45,&
 & ZMFUS5,   ZMFUQ5,    ZMFUL5,    PLUDE5,   ZDMFUP5,&
 & I_KCBOT2, I_KCTOP1,  ISTUP,&
 & ZMFUDE_RATE5, ZWMEAN5, ZERATEU5,&
 & PTEN,     ZTENH,     ZQENH,&
 & PGEOH,    PQEN,      PQSEN,&
 & PAPH,     ZLGLAC,    ZQPRCV,&
 & ZBUOH,    ZWUBASE,&
 & PTU,      PQU,      PLU,&
 & PMFU,     ZMFUB,&
 & ZMFUS,    ZMFUQ,    ZMFUL,     PLUDE,    ZDMFUP,&
 & ZMFUDE_RATE, ZWMEAN, ZERATEU )  

ZMFUB(:)=0.0_JPRB

IF (LREGCV) THEN
  DO JL=KIDIA,KFDIA
    IF (LL_LDCUM1(JL)) THEN
      ZCAPPBL(JL) = ZCAPPBL(JL) * 0.2_JPRB * ABS(ZCAPPBL5(JL)) / (ABS(ZCAPPBL5(JL)) + 1.0_JPRB)
    ENDIF
  ENDDO
ENDIF

! CALCULATE COLUMN AND SUB CLOUD LAYER MOISTURE CONVERGENCE
! AND SUB CLOUD LAYER MOIST STATIC ENERGY CONVERGENCE
DO JK=KLEV,2,-1
  DO JL=KIDIA,KFDIA
    IF(LL_LDCUM1(JL) .AND. JK >= I_KCBOT1(JL)) THEN
      ZTENDH=0.0_JPRB
      ZDZ=0.0_JPRB
! Trajectory recomputations.
      ZDZ5=PAPH5(JL,JK+1)-PAPH5(JL,JK)

      PTENT(JL,JK) = PTENT(JL,JK) + ZCAPPBL(JL) * ZDZ5
      PTENQ(JL,JK) = PTENQ(JL,JK) + YDCST%RETV * ZCAPPBL(JL) * ZDZ5
      ZDZ = ZDZ + ZCAPPBL(JL) * (ZTENT25(JL,JK)+YDCST%RETV*ZTENQ25(JL,JK))

      ZTENDH = ZTENDH + ZDZ5 * ZCONS1 * ZDHPBL(JL)
      ZDZ    = ZDZ    + ZTENDH5(JL,JK) * ZCONS1 * ZDHPBL(JL)

      PAPH(JL,JK+1) = PAPH(JL,JK+1) + ZDZ
      PAPH(JL,JK)   = PAPH(JL,JK)   - ZDZ
      ZDZ = 0.0_JPRB

      PTENQ(JL,JK)=PTENQ(JL,JK)+YDCST%RLVTT*ZTENDH
      PTENT(JL,JK)=PTENT(JL,JK)+YDCST%RCPD *ZTENDH
      ZTENDH=0.0_JPRB
    ENDIF
  ENDDO
ENDDO
DO JL=KIDIA,KFDIA
  PAHFS(JL,KLEV+1) = PAHFS(JL,KLEV+1) - ZKHVFL(JL) * ZRCPD
  PTEN(JL,KLEV)    = PTEN(JL,KLEV)    - ZKHVFL(JL) * YDCST%RETV * PQHFL5(JL,KLEV+1)
  PQHFL(JL,KLEV+1) = PQHFL(JL,KLEV+1) - ZKHVFL(JL) * YDCST%RETV * PTEN5(JL,KLEV)
  ZKHVFL(JL) = 0.0_JPRB
ENDDO
ZCAPPBL (:)=0.0_JPRB
ZDHPBL (:)=0.0_JPRB

!*    3.           CLOUD BASE CALCULATIONS
!                  -----------------------

CALL CUBASEN2AD &
 & ( YDML_PHY_SLIN,YDML_PHY_EC%YRECLDP, &
 & KIDIA,     KFDIA,    KLON,     KLEV,&
 & LDRAIN1D,  LLCVOPT,&
 & ZTENH25,   ZQENH25,  PGEOH5,   PAP5,     PAPH5,&
 & PQHFL5,    PAHFS5,&
 & PTEN5,     PQEN5,    ZQSEN25,  PGEO5,&
 & PTU5,      PQU5,     PLU5,&
 & ZBUOH5,    ZWUBASE5, ZLGLAC5,  ZQPRCV5,  ZCAPE5,&
 & KTYPE,     ILAB,     LDCUM,    LDSC,&
 & KCBOT,     KBOTSC,   ICTOP0,   ISTUP,&
 & ZTU55,     ZQU55,    ZLU55,&
 & ZBUOH55,   ZWU2H55,  ZSUH55,   ZWU2H155,&
 & ZTVENH55,  ZTVUH55,  ZSUH255,  ZQU255,&
 & ZFACT355,  ZFACT455, ZFACT555, ZWS55,&   
 & ZKHVFL55,  ZUST55,   ZWUBASE55, ZWORK155, ZWORK155T,&
 & ICBOT55,   ILAB55,   LLCUM55,  LLTEST155,&
 & LLGO_ON55, LLMIDTEST55, LLCAPETEST55,&
 & ZTENH,     ZQENH,    PGEOH,    PAP,      PAPH,&
 & PQHFL,     PAHFS,&
 & PTEN,      PQEN,     PQSEN,    PGEO,&
 & PTU,       PQU,      PLU,&
 & ZBUOH,     ZWUBASE,  ZLGLAC,   ZQPRCV,   PCAPE )  

!*    2.       INITIALIZE VALUES AT VERTICAL GRID POINTS IN 'CUININ2AD'
!              --------------------------------------------------------

CALL CUININ2AD &
 & ( YDML_PHY_SLIN%YRECUMF2,&
 & KIDIA,    KFDIA,    KLON,     KLEV,&
 & PTEN5,    PQEN5,    ZQSEN25,  PUEN5,    PVEN5,&
 & PVERVEL5, PGEO5,    PAPH5,&
 & ILWMIN,   ILAB,&
 & ZTENH25,  ZQENH25,  ZQSENH5,  PGEOH5,&
 & PTU5,     PQU5,     ZTD5,     ZQD5,&
 & ZUU5,     ZVU5,     ZUD5,     ZVD5,&
 & PLU5,&
 & PTEN,     PQEN,     PQSEN,    PUEN,     PVEN,&
 & PVERVEL,  PGEO,     PAPH,&
 & ZTENH,    ZQENH,    ZQSENH,   PGEOH,&
 & PTU,      PQU,      ZTD,      ZQD,&
 & ZUU,      ZVU,      ZUD,      ZVD,&
 & PLU     )  

!----------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('CUMASTRN2AD',1,ZHOOK_HANDLE)
END SUBROUTINE CUMASTRN2AD
