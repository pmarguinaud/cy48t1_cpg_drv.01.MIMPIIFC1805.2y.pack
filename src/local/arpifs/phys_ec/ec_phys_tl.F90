#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE EC_PHYS_TL(YDGEOMETRY,YDGMV,YDSURF,YDMODEL,KBL,KNUMB,KSTGLO,KIBL,&
 & PB2,&
 & PGMV,PGMVS,PGFL,PGFL5,&
 & PGMVT1,PGMVT1S,PGFLT1,&
 & PSP_SB,PSP_SG,PSP_RR,&
 & PSD_VN,PSD_VD,YDTILEPROP,YDPHYSMWAVE,&
 & PFORCEU,PFORCEV,PFORCET,PFORCEQ,&
 & PII0,&
 & PTRAJ_PHYS,&
 & PGPHIST,PTRAJ_PHYS_TLAD,PTRAJ_SRFC,PTRAJ_CST,&
 & PTRAJ_RAINGG_TLAD,&
 & PHYS_MWAVE5,YDACV)

!**** *EC_PHYS_TL* - ECMWF Physics (Tangent linear)

!     Purpose.
!     --------
!           Interface to ECMWF Physics (Tangent linear)

!**   Interface.
!     ----------
!        *CALL* *EC_PHYS_TL(...)*

!        Explicit arguments :
!        --------------------
!        KNUMB     : number of elements of arrays for which computations
!                    are performed (used in message passing version)
!        KSTGLO    : Global offset for surface fields
!        KIBL      : index into grid point geometry in YDGEOMETRY

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!        Called by GP_MODEL_TL.

!     Reference.
!     ----------

!     Author.
!     -------
!        Deborah Salmond  *ECMWF*
!        K.YESSAD (04-JAN-2002): Pruning.

!     Modifications.
!     --------------
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!        M.Janiskova - 11-11-03 - perturbation of skin temperature for physics
!        M.Hamrud      15-Jan-2006  Revised GPRCP
!        JJMorcrette   20060511 MODIS albedo
!        M.Hamrud      01-Jul-2006 Revised surface fields
!        A. Geer       15-Apr-2008  Multiple sensors for rainy 4D-Var
!        A. Geer       01-Oct-2008 Rainy 4D-Var tidying
!        A. Geer       10-Jun-2008  Rainy 4D-Var passive mode and diagnostics
!        F. Karbou     16-Dec-2009  Surface emissivity estimation
!        H. Hersbach   04-Dec-2009 10-m neutral wind; move reset surface perts to scan2mtl
!        S. Boussetta/G.Balsamo      May 2009   (Add variable LAI) 
!        K. Yessad (Jan 2011): remove useless overdimension.
!        A. Geer       21-Sep-2010  All-sky AMSU-A
!        P. Lopez      07-Oct-2010 Added handling of ground-based radar precip data (GBRAD)       
!        L. Jones      18-Oct-2011 Tidied MACC variables in CALLPAR call
!        A. Geer       13-Jan-2012 Move all-sky operator (MWAVE) to hop
!        A. Geer       13-Jun-2012 Trajectory ambiguity means some GFL5 are filled by callpartl
!        P. Lopez      10-Aug-2012  Added rain gauge assimilation
!        F. Vana       28-Nov-2013 : Redesigned trajectory handling
!        M. Janiskova  13-Jul-2013 Perturbation of top layer surface fields if LESURF2
!        T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!        K. Yessad (July 2014): Move some variables.
!        R. Hogan      24-Apr-2015: Solar zenith angle computed in cos_sza
!        P. Lopez      30-May-2017 Moved ground-based radars and rain-gauges from model to obs part.
!        E. Dutra/G.Arduini Jan 2017: Snow multi-layer PSP_SG 4 dimensions. Remaining *tl only sees 1st layer
!        S. Massart   19-Feb-2019 Augmented control variable and solar constant optimisation
!     ------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF, GPPOPER, GPOPER
USE YOMGMV             , ONLY : TGMV
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCT0             , ONLY : LTWOTL
USE YOMCT3             , ONLY : NSTEP
USE YOMLUN             , ONLY : NULOUT
USE YOE_TILE_PROP      , ONLY : TETILEPROP
USE YOE_PHYS_MWAVE     , ONLY : TEPHYSMWAVE, N_PHYS_MWAVE
USE YOMLCZ             , ONLY : LFORCE
! USE YOMGBRAD           , ONLY : LEGBRAD_ACTIVE
USE YOMRAINGG          , ONLY : RACCPR3
USE INTDYN_MOD         , ONLY : YYTXYB
USE YOMTRAJ            , ONLY : TRAJ_PHYS_TYPE, TRAJ_PHYS_TLAD_TYPE,&
 &                              TRAJ_CST_TYPE, TRAJ_SRFC_TYPE, NGP5, NTRAJ_CST, TRAJ_RAINGG_TLAD_TYPE,&
 &                              LPRTTRAJ
USE YOMSPHYHIST        , ONLY : SPHYS_HIST_TYPE
USE TYPE_ACV           , ONLY : ACV_CONTAINER

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)              ,INTENT(IN)    :: YDGEOMETRY
TYPE(TGMV)                  ,INTENT(INOUT) :: YDGMV
TYPE(TSURF)                 ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)                 ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM)          ,INTENT(IN)    :: KBL 
INTEGER(KIND=JPIM)          ,INTENT(IN)    :: KNUMB 
INTEGER(KIND=JPIM)          ,INTENT(IN)    :: KSTGLO 
INTEGER(KIND=JPIM)          ,INTENT(IN)    :: KIBL 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PB2(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_DYN%YRPTRSLB2%NFLDSLB2) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PGMV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%NDIMGMV) 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PGMVS(YDGEOMETRY%YRDIM%NPROMA,YDGMV%NDIMGMVS) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PGFL5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
 & YDMODEL%YRML_GCONF%YGFL%NDIM5) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PGMVT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDGMV%YT1%NDIM) 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PGMVT1S(YDGEOMETRY%YRDIM%NPROMA,YDGMV%YT1%NDIMS) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PGFLT1(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG, &
 & YDMODEL%YRML_GCONF%YGFL%NDIM1) 
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PSP_SB(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS,YDSURF%YSP_SBD%NDIM)
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PSP_SG(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SGD%NLEVS,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PSP_RR(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PSD_VN(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VND%NDIM)
REAL(KIND=JPRB)             ,INTENT(INOUT) :: PSD_VD(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VDD%NDIM)
TYPE(TETILEPROP)            ,INTENT(INOUT) :: YDTILEPROP  
TYPE(TEPHYSMWAVE)           ,INTENT(INOUT) :: YDPHYSMWAVE  
REAL(KIND=JPRB)             ,INTENT(IN)    :: PFORCEU(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PFORCEV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PFORCET(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)             ,INTENT(IN)    :: PFORCEQ(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)             , INTENT(IN)   :: PII0
TYPE (TRAJ_PHYS_TYPE)       ,INTENT(IN)    :: PTRAJ_PHYS
TYPE (SPHYS_HIST_TYPE)      ,INTENT(INOUT) :: PGPHIST
TYPE (TRAJ_PHYS_TLAD_TYPE)  ,INTENT(INOUT) :: PTRAJ_PHYS_TLAD
TYPE (TRAJ_SRFC_TYPE)       ,INTENT(IN)    :: PTRAJ_SRFC
TYPE (TRAJ_CST_TYPE)        ,INTENT(IN)    :: PTRAJ_CST
TYPE (TRAJ_RAINGG_TLAD_TYPE),INTENT(INOUT) :: PTRAJ_RAINGG_TLAD
REAL(KIND=JPRB)    ,INTENT(INOUT),OPTIONAL :: PHYS_MWAVE5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,N_PHYS_MWAVE) 
TYPE(ACV_CONTAINER),INTENT(INOUT),OPTIONAL :: YDACV
!     ------------------------------------------------------------------
!     POINTER BLOCKS
REAL(KIND=JPRB) :: ZXYB95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB%NDIM)
REAL(KIND=JPRB) :: ZXYB15(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB%NDIM)
REAL(KIND=JPRB) :: ZXYB9 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB%NDIM)

! ZPRE9  : (NPROMA,0:NFLEVG)     ; HALF LEVEL PRESSURE
! ZPRE9F : (NPROMA,  NFLEVG)     ; FULL LEVEL PRESSURE

REAL(KIND=JPRB) :: ZPRE9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZPRE95  : (NPROMA,0:NFLEVG)     ; HALF LEVEL PRESSURE (TRAJECTORY)
! ZPREF95 : (NPROMA,  NFLEVG)     ; FULL LEVEL PRESSURE (TRAJECTORY)

REAL(KIND=JPRB) :: ZPRE95(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPREF95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZR9    : (NPROMA,NFLEVG)       ; R

REAL(KIND=JPRB) :: ZR9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZR95   : (NPROMA,NFLEVG)       ; R  (TRAJECTORY)

REAL(KIND=JPRB) :: ZR95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZPHI9  : (NPROMA,0:NFLEVG)     ; HALF LEVEL GEOPOTENTIAL
! ZPHIF9 : (NPROMA,  NFLEVG)     ; FULL LEVEL GEOPOTENTIAL

REAL(KIND=JPRB) :: ZPHI9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPHIF9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZPHI95  : (NPROMA,0:NFLEVG)    ; HALF LEVEL GEOPOTENTIAL (TRAJECTORY)
! ZPHIF95 : (NPROMA,  NFLEVG)    ; FULL LEVEL GEOPOTENTIAL (TRAJECTORY)

REAL(KIND=JPRB) :: ZPHI95(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 & ZPHIF95(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

! ZMU0  : (NPROMA)            ; SOLAR ANGLE
! ZMU0M : (NPROMA)            ; SOLAR ANGLE (between radiation steps)

REAL(KIND=JPRB) :: ZGAW(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZMU0(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZMU0M(YDGEOMETRY%YRDIM%NPROMA)

!     --- ECMWF EXTRA ARRAYS, TIME 1 ---

REAL(KIND=JPRB) :: ZPRS1 (YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZPRSF1  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZPRS15(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZPRSF15 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

!     --- TRAJECTORY VARIABLES FOR CALLPARTL ---

REAL(KIND=JPRB) :: ZTT95   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZQT95   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZUT95   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZVT95   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENT5  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENQ5  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENU5  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZTENV5  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZVVEL5  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSPT95  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZSPT15  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZEMTED5 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL(KIND=JPRB) :: ZTRSOL5 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG+1)
REAL(KIND=JPRB) :: ZWSA95  (YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTSA95  (YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTIA95  (YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZSNS95  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZWL95   (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTL95   (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZNTOP5  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZNBAS5  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZACPR5  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZACCPR5 (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZACCPR35(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZGZ0M5  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZLZ0H5  (YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZUSTRTI5 (YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_G%YRDPHY%NTILES)
REAL(KIND=JPRB) :: ZVSTRTI5 (YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_G%YRDPHY%NTILES)
REAL(KIND=JPRB) :: ZAHFSTI5 (YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_G%YRDPHY%NTILES)
REAL(KIND=JPRB) :: ZEVAPTI5 (YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_G%YRDPHY%NTILES)
REAL(KIND=JPRB) :: ZTSKTI5  (YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_G%YRDPHY%NTILES)
REAL(KIND=JPRB) :: ZGFLT95  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM)
REAL(KIND=JPRB) :: ZTENGFL5 (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1)

REAL(KIND=JPRB) :: ZSP_SG5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SGD%NLEVS,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB) :: ZSD_VF5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB) :: ZSD_VD5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VDD%NDIM)

!     --- FLUXES FROM PARAMETERISATIONS ---
REAL(KIND=JPRB) :: ZDIFTQ(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZDIFTS(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZSTRTU(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG),&
 &  ZSTRTV(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG)

!     --- PERTURBATION OF SURFACE FIELDS ---
REAL(KIND=JPRB) :: ZTSA9(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS), ZTSA1(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTIA9(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS), ZTIA1(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTSN9(YDGEOMETRY%YRDIM%NPROMA)  , ZTSN1(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTSKIN9(YDGEOMETRY%YRDIM%NPROMA), ZTSKIN1(YDGEOMETRY%YRDIM%NPROMA)

!     --- PERTURBATION OF SURFACE TENDENCIES ---
REAL(KIND=JPRB) :: ZTSAE1(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTIAE1(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS)
REAL(KIND=JPRB) :: ZTSNE1(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB) :: ZTLE1(YDGEOMETRY%YRDIM%NPROMA)

INTEGER(KIND=JPIM) :: IEND, IPCT,&
 & IST, ISTC, JFLD, JLEV, JROF, JGFL  

!* Working arrays for trajectory format conversion
REAL(KIND=JPRB)  :: ZTEMP(YDGEOMETRY%YRDIM%NPROMA,NGP5),ZTEMPC(YDGEOMETRY%YRDIM%NPROMA,NTRAJ_CST)

LOGICAL :: LLRAIN1D

REAL(KIND=JPRB) :: ZDTPHY, ZZPHY

!ARDU Working arrays for snowML
REAL(KIND=JPRB)    :: ZZSP_SG5_YA(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)    :: ZZSP_SG5_YR(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)    :: ZZSP_SG5_YT(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)    :: ZZPSP_SG(YDGEOMETRY%YRDIM%NPROMA)

REAL(KIND=JPRB)    :: ZTMP, ZTMP2
INTEGER(KIND=JPIM) :: JK

!!! Array with dynamics tendency of GFL
REAL(KIND=JPRB) :: ZGFL_DYN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM1)

REAL(KIND=JPRB) :: ZHOOK_HANDLE
 
!     ------------------------------------------------------------------

#include "callpartl.intfb.h"
#include "gpgeo.intfb.h"
#include "gpgeotl.intfb.h"
#include "gphpre.intfb.h"
#include "gphpretl.intfb.h"
#include "gpmktend.intfb.h"
#include "gprcp_qlirsg.intfb.h"
#include "gprcptl.intfb.h"
#include "get_traj_phys.intfb.h"
#include "cos_sza.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('EC_PHYS_TL',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDVAB=>YDGEOMETRY%YRVAB, &
 &  YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, YDGSGEOM=>YDGEOMETRY%YRGSGEOM(KIBL), &
 &  YDOROG=>YDGEOMETRY%YROROG(KIBL), YDERIP=>YDMODEL%YRML_PHY_RAD%YRERIP, &
 & YDPTRSLB2=>YDMODEL%YRML_DYN%YRPTRSLB2,YDPHY2=>YDMODEL%YRML_PHY_MF%YRPHY2,YDRIP=>YDMODEL%YRML_GCONF%YRRIP, &
 & YDSRFTLAD=>YDMODEL%YRML_PHY_SLIN%YRSRFTLAD,YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY,YGFL=>YDMODEL%YRML_GCONF%YGFL, &
 & YDDYN=>YDMODEL%YRML_DYN%YRDYN,YDPHNC=>YDMODEL%YRML_PHY_SLIN%YRPHNC,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY)

ASSOCIATE(NDIM=>YGFL%NDIM, NDIM1=>YGFL%NDIM1, NDIM5=>YGFL%NDIM5, &
 & NUMFLDS=>YGFL%NUMFLDS, YA=>YGFL%YA, YCOMP=>YGFL%YCOMP, &
 & YI=>YGFL%YI, YL=>YGFL%YL, YQ=>YGFL%YQ, &
 & NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & NTILES=>YDDPHY%NTILES, &
 & REPSP1=>YDDYN%REPSP1, &
 & NGPTOT=>YDGEM%NGPTOT, &
 & NDIMGMV=>YDGMV%NDIMGMV, NDIMGMVS=>YDGMV%NDIMGMVS, YPH9=>YDGMV%YPH9, &
 & YT1=>YDGMV%YT1, &
 & LESURF2=>YDPHNC%LESURF2, &
 & MSLB2VVEL=>YDPTRSLB2%MSLB2VVEL, NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & RCODECM=>YDERIP%RCODECM, RSIDECM=>YDERIP%RSIDECM, RCOVSRM=>YDERIP%RCOVSRM, &
 & RSIVSRM=>YDERIP%RSIVSRM, &
 & NSTART=>YDRIP%NSTART, NSTOP=>YDRIP%NSTOP, RCODEC=>YDRIP%RCODEC, &
 & RCOVSR=>YDRIP%RCOVSR, RSIDEC=>YDRIP%RSIDEC, RSIVSR=>YDRIP%RSIVSR, &
 & TDT=>YDRIP%TDT, &
 & GPTSKIN0=>YDSRFTLAD%GPTSKIN0, GPTSKIN9=>YDSRFTLAD%GPTSKIN9, &
 & NGSKIN=>YDSRFTLAD%NGSKIN, &
 & YSD_VD=>YDSURF%YSD_VD, YSD_VDD=>YDSURF%YSD_VDD, YSD_VF=>YDSURF%YSD_VF, &
 & YSD_VFD=>YDSURF%YSD_VFD, YSD_VN=>YDSURF%YSD_VN, YSD_VND=>YDSURF%YSD_VND, &
 & YSP_RR=>YDSURF%YSP_RR, YSP_RRD=>YDSURF%YSP_RRD, YSP_SB=>YDSURF%YSP_SB, &
 & YSP_SBD=>YDSURF%YSP_SBD, YSP_SG=>YDSURF%YSP_SG, YSP_SGD=>YDSURF%YSP_SGD, &
 & PHYS_MWAVE=>YDPHYSMWAVE%PHYS_MWAVE, &
 & RAHFSTI=>YDTILEPROP%RAHFSTI, REVAPTI=>YDTILEPROP%REVAPTI, &
 & RTSKTI=>YDTILEPROP%RTSKTI, RUSTRTI=>YDTILEPROP%RUSTRTI, &
 & RVSTRTI=>YDTILEPROP%RVSTRTI, TSPHY=>YDPHY2%TSPHY, LERAINGG=>YDEPHY%LERAINGG,&
 & NCSNEC=>YDDPHY%NCSNEC, LESNML=>YDEPHY%LESNML)
!     ------------------------------------------------------------------

!*       1.    Interface to global arrays.
!              ---------------------------

!*       1.1   INITIAL COMPUTATIONS

IPCT = 1
IST   = 1
IEND  = KNUMB
ISTC  = 1

LLRAIN1D = .FALSE.

!     ------------------------------------------------------------------

!*       2.   PREPARE FOR PHYSICS  COMPUTATIONS  (part I)
!             -------------------------------------------

!*       2.1   READ TRAJECTORY AT T-DT

!*  Read trajectory for physics
CALL GET_TRAJ_PHYS(YDGEOMETRY,YDSURF,YDDPHY,YGFL,IST,IEND,&
 & ZTT95,ZQT95,ZGFLT95,ZUT95,ZVT95,ZVVEL5,ZSPT95,ZSPT15,&
 & ZTENT5,ZTENQ5,ZTENGFL5,ZTENU5,ZTENV5,ZEMTED5,ZTRSOL5,ZWSA95,&
 & ZTSA95,ZSNS95,ZWL95,ZTL95,ZTIA95,ZNTOP5,ZNBAS5,&
 & ZACPR5,ZACCPR5,ZGZ0M5,ZLZ0H5,ZUSTRTI5,ZVSTRTI5,ZAHFSTI5,&
 & ZEVAPTI5,ZTSKTI5, PTRAJ_PHYS)

!*       2.2   SURFACE PRESSURE VARIABLES

DO JROF=IST,IEND
  ZPRE9 (JROF,NFLEVG)=PGMVS(JROF,YPH9%MSP)*EXP(ZSPT95(JROF))
  ZPRE95(JROF,NFLEVG)=EXP(ZSPT95(JROF))
ENDDO

!*       2.3   SURFACE FIELDS FOR PHYSICS

! Have to set these to zero as GPOPER does not know about IST:IEND
ZTEMP=0.0_JPRB
ZTEMPC=0.0_JPRB
ZTEMP(IST:IEND,:)=REAL(PTRAJ_SRFC%MIX_SRFC(IST:IEND,:),JPRB)
ZTEMPC(IST:IEND,:)= REAL(PTRAJ_CST%MIX_CST(IST:IEND,:),JPRB)
CALL GPOPER(YDGEOMETRY%YRDIM,YDDYN,'GETTRAJ',YDSURF,PSP_SG=ZSP_SG5,PSD_VF=ZSD_VF5,PSD_VD=ZSD_VD5,&
 & PFIELD=ZTEMP,PFIELD2=ZTEMPC)
IF (LPRTTRAJ.AND.PTRAJ_SRFC%LASTCHUNK) WRITE(NULOUT,*)'GREPTRAJ READ TRAJ_CST and TRAJ_SRFC in EC_PHYS_TL'


!        2.4  INITIALISATION OF SURFACE PERTURBATION

  IF (NSTEP == 0) THEN
    IF(.NOT. LTWOTL) THEN
      CALL GPPOPER(YDDYN,'SET9TO0',YDSURF,KBL=KBL)
    ENDIF
    DO JFLD=1,NTILES
      DO JROF=IST,IEND
        RUSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
        RVSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
        RAHFSTI(JROF,JFLD,KBL) = 0.0_JPRB
        REVAPTI(JROF,JFLD,KBL) = 0.0_JPRB
        RTSKTI (JROF,JFLD,KBL) = 0.0_JPRB
      ENDDO
    ENDDO
  ENDIF

  CALL GPPOPER(YDDYN,'SET1TO9',YDSURF,KBL=KBL)

  IF (LESURF2) THEN

!  Initialisation of perturbations for top layer surface fields

    IF (NSTEP == 0) THEN
      DO JFLD=1,NGSKIN
        DO JROF=IST,IEND
          GPTSKIN0(JROF,JFLD,KBL) = 0.0_JPRB
          GPTSKIN9(JROF,JFLD,KBL) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF

    DO JROF=IST,IEND
      ZTSA9(JROF,:) = 0.0_JPRB
      ZTIA9(JROF,:) = 0.0_JPRB

      ZTSA1(JROF,:) = 0.0_JPRB
      ZTIA1(JROF,:) = 0.0_JPRB
    ENDDO

    DO JROF=IST,IEND
      ZTSKIN9(JROF) = GPTSKIN9(JROF,1,KBL)
      ZTSN9  (JROF) = GPTSKIN9(JROF,2,KBL)
      ZTSA9(JROF,1) = GPTSKIN9(JROF,3,KBL)
      ZTIA9(JROF,1) = GPTSKIN9(JROF,4,KBL)

      ZTSKIN1(JROF) = GPTSKIN9(JROF,1,KBL)
      ZTSN1  (JROF) = GPTSKIN9(JROF,2,KBL)
      ZTSA1(JROF,1) = GPTSKIN9(JROF,3,KBL)
      ZTIA1(JROF,1) = GPTSKIN9(JROF,4,KBL)
    ENDDO
  ELSE

!  Initialisation of perturbations for top layer surface fields

    IF (NSTEP == 0) THEN
      DO JFLD=1,NGSKIN
        DO JROF=IST,IEND
          GPTSKIN0(JROF,JFLD,KBL) = 0.0_JPRB
          GPTSKIN9(JROF,JFLD,KBL) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF
    DO JROF=IST,IEND
      ZTSKIN9(JROF) = GPTSKIN9(JROF,1,KBL)
      ZTSKIN1(JROF) = GPTSKIN9(JROF,1,KBL)
    ENDDO


    DO JROF=IST,IEND
      ZTSA9(JROF,:) = 0.0_JPRB
      ZTIA9(JROF,:) = 0.0_JPRB
    ENDDO
  ENDIF

!     ------------------------------------------------------------------

!*       3.   PREPARE FOR PHYSICS  COMPUTATIONS  (part II)
!             --------------------------------------------

!*      3.1   Compute pressure

  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE95,PXYB=ZXYB95,PRESF=ZPREF95)

  CALL GPHPRETL(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRE9,ZPRE95,PXYB=ZXYB9,PXYB5=ZXYB95,PRESF=ZPRE9F)

!*      3.3   Compute R

  CALL GPRCP_QLIRSG  (NPROMA,IST,IEND,NFLEVG,PQ=ZQT95,PR=ZR95)
  CALL GPRCPTL(NPROMA,IST,IEND,NFLEVG,PQ=PGFL(1,1,YQ%MP9_PH),PR=ZR9)  

!*       3.4   Integrate Hydrostatics

  DO JROF=IST,IEND
    ZPHI9 (JROF,NFLEVG)=0.0_JPRB
    ZPHI95(JROF,NFLEVG) = YDOROG%OROG(JROF)
  ENDDO

  CALL GPGEO  (NPROMA,IST,IEND,NFLEVG,&
   & ZPHI95,ZPHIF95,ZTT95,ZR95,ZXYB95(1,1,YYTXYB%M_LNPR),ZXYB95(1,1,YYTXYB%M_ALPH),YDGEOMETRY%YRVERT_GEOM)  
  CALL GPGEOTL(NPROMA,IST,IEND,NFLEVG,&
   & ZPHI9,ZPHIF9,PGMV(1,1,YPH9%MT),ZR9,ZXYB9(1,1,YYTXYB%M_LNPR),ZXYB9(1,1,YYTXYB%M_ALPH),&
   & ZTT95,ZR95,ZXYB95(1,1,YYTXYB%M_LNPR),ZXYB95(1,1,YYTXYB%M_ALPH),YDGEOMETRY%YRVERT_GEOM)  

!*       3.5   Astronomy.

  DO JROF=IST,IEND
    ZGAW(JROF)=YDGSGEOM%GAW(JROF)
  ENDDO

  ! Solar zenith angle used each model timestep
  CALL COS_SZA(YDMODEL%YRML_PHY_RAD%YRERAD, YDERIP, YDRIP,&
             & IST, IEND, NPROMA, YDGSGEOM%GEMU,&
             & YDGSGEOM%GELAM, .FALSE., ZMU0)

  ! Solar zenith angle used by the radiation scheme each radiation
  ! timestep - strictly this shouldn't be recalculated every model
  ! timestep
  CALL COS_SZA(YDMODEL%YRML_PHY_RAD%YRERAD, YDERIP, YDRIP,&
             & IST, IEND, NPROMA, YDGSGEOM%GEMU,&
             & YDGSGEOM%GELAM, .TRUE., ZMU0M)


!*       3.7  ADIABATIC TENDENCIES AS INPUT FOR PHYSICS

  ZDTPHY=TDT

  CALL GPMKTEND(YDGEOMETRY,YGFL,IST,IEND,TSPHY,&
   & PGMVT1(1,1,YT1%MU),PGMVT1(1,1,YT1%MV) ,PGMVT1(1,1,YT1%MT),&
   & PGFLT1,&
   & PGMV(1,1,YPH9%MU),PGMV(1,1,YPH9%MV),PGMV(1,1,YPH9%MT),&
   & PGFL,ZGFL_DYN(1,1,1) )

  DO JROF=IST,IEND
    ZPRS1 (JROF,NFLEVG) = PGMVT1S(JROF,YT1%MSP)*EXP(ZSPT15(JROF))
    ZPRS15(JROF,NFLEVG) = EXP(ZSPT15(JROF))
  ENDDO

  CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRS15,PXYB=ZXYB15,PRESF=ZPRSF15)

  CALL GPHPRETL(NPROMA,NFLEVG,IST,IEND,YDVAB,ZPRS1,ZPRS15,PXYB5=ZXYB15,PRESF=ZPRSF1)
!     ------------------------------------------------------------------

!*       4.   CALL E.C.M.W.F. LINEAR PHYSICS
!             ------------------------------

! ARDU SNOWML compute aggregate snow var for density and mass, use for snow T 1st level
! ARDU PASS ONLY THE FIRST LAYER TO CALLPARTL
  DO JROF = IST,IEND
    IF ( LESNML .AND. NCSNEC > 1 ) THEN
      ZTMP =0._JPRB     ! total snow depth
      ZTMP2=0._JPRB     ! total snow mass
      DO JK=1,NCSNEC
        IF (ZSP_SG5(JROF,JK,YSP_SG%YF%MP) > 0._JPRB ) THEN
          ZTMP = ZTMP + (ZSP_SG5(JROF,JK,YSP_SG%YF%MP) / ZSP_SG5(JROF,JK,YSP_SG%YR%MP))
          ZTMP2= ZTMP2+ ZSP_SG5(JROF,JK,YSP_SG%YF%MP)
        ENDIF
      ENDDO
      IF (ZTMP2 > 0._JPRB .AND. ZTMP > 0._JPRB) THEN
        ZZSP_SG5_YR(JROF) = ZTMP2 / ZTMP ! average snow density
        ZZPSP_SG(JROF)    = SUM(PSP_SG(JROF,:,YSP_SG%YF%MP9),DIM=1) ! total snow mass
      ELSE
        ZZSP_SG5_YR(JROF) = ZSP_SG5(JROF,1,YSP_SG%YR%MP)  
        ZZPSP_SG(JROF)    = PSP_SG(JROF,1,YSP_SG%YF%MP9 )
      ENDIF
      ZZSP_SG5_YT(JROF) = ZSP_SG5(JROF,1,YSP_SG%YT%MP)
      ZZSP_SG5_YA(JROF) = ZSP_SG5(JROF,1,YSP_SG%YA%MP)
    ELSE
      ZZSP_SG5_YA(JROF) = ZSP_SG5(JROF,1,YSP_SG%YA%MP)
      ZZSP_SG5_YR(JROF) = ZSP_SG5(JROF,1,YSP_SG%YR%MP)
      ZZSP_SG5_YT(JROF) = ZSP_SG5(JROF,1,YSP_SG%YT%MP)
      ZZPSP_SG(JROF)    = PSP_SG(JROF,1,YSP_SG%YF%MP9 )
    ENDIF
  ENDDO


!*       4.1  CALL PHYSICS

  CALL CALLPARTL(YDGEOMETRY,YDSURF,YDMODEL,IST,IEND,NPROMA,YSP_SBD%NLEVS,NTILES,&
   & KSTGLO,&
   & 1,NFLEVG,NSTART,KIBL,NSTEP,LLRAIN1D,&
   ! - INPUT (TRAJECTORY)
   & ZTT95    ,ZQT95   ,ZGFLT95,&
   & ZUT95    ,ZVT95   ,ZVVEL5,&
   & ZPRS15   ,ZPRSF15,&
   & ZPRE95   ,ZPREF95 ,ZXYB95(1,1,YYTXYB%M_DELP),&
   & ZPHI95   ,ZPHIF95,&
   & ZEMTED5  ,ZTRSOL5 ,ZMU0    ,ZMU0M, ZGAW,&
   & ZSNS95   ,ZTSA95  ,ZWL95   ,ZTL95,&
   & ZWSA95   ,ZTIA95,&
   & ZZSP_SG5_YA, ZZSP_SG5_YR,&
   & ZZSP_SG5_YT,&
   & ZSD_VF5(1,YSD_VF%YGETRL%MP),ZSD_VF5(1,YSD_VF%YSDFOR%MP),&
   & ZSD_VF5(1,YSD_VF%YVRLAN%MP),&
   & ZSD_VF5(1,YSD_VF%YVRLDI%MP),ZSD_VF5(1,YSD_VF%YSIG%MP),&
   & ZSD_VF5(1,YSD_VF%YLSM%MP), ZSD_VF5(1,YSD_VF%YALBF%MP),&
   & ZSD_VF5(:,YSD_VF%IALSTART:YSD_VF%IALEND),& ! Albedo coefficients
   & ZSD_VF5(1,YSD_VF%YEMISF%MP),&
   & ZSD_VF5(1,YSD_VF%YCVL%MP), ZSD_VF5(1,YSD_VF%YCVH%MP) ,&
   & ZSD_VF5(1,YSD_VF%YTVL%MP),ZSD_VF5(1,YSD_VF%YTVH%MP),&
   & ZSD_VF5(1,YSD_VF%YLAIL%MP),ZSD_VF5(1,YSD_VF%YLAIH%MP),&
   & ZSD_VF5(1,YSD_VF%YSOTY%MP),&
   & ZSD_VF5(1,YSD_VF%YCI%MP), ZSD_VF5(1,YSD_VF%YSST%MP),&
   & ZSD_VF5(1,YSD_VF%YZ0F%MP), ZSD_VF5(1,YSD_VF%YLZ0H%MP),&
   & ZSD_VF5, ZGZ0M5, ZLZ0H5,&
   & ZSD_VD5(1,YSD_VD%Y10U%MP),ZSD_VD5(1,YSD_VD%Y10V%MP),ZSD_VD5(1,YSD_VD%Y2T%MP),&
   & ZSD_VD5(1,YSD_VD%Y2D%MP),ZSD_VD5(1,YSD_VD%Y2Q%MP),ZSD_VD5(1,YSD_VD%Y10NU%MP),ZSD_VD5(1,YSD_VD%Y10NV%MP),&
   ! - UPDATED (TRAJECTORY)
   & ZTENT5   ,ZTENQ5,  ZTENGFL5,&
   & ZTENU5   ,ZTENV5,&
   & ZNTOP5   ,ZNBAS5  , ZACPR5, ZACCPR5, ZACCPR35,&
   & ZUSTRTI5 ,ZVSTRTI5 ,ZAHFSTI5, ZEVAPTI5, ZTSKTI5,&
   ! - INPUT
   & PGMV(1,1,YPH9%MT), PGFL(1,1,YQ%MP9_PH), PGFL, PGFL5,&
   & PGMV(1,1,YPH9%MU), PGMV(1,1,YPH9%MV), PB2(1,MSLB2VVEL),&
   & PGFL(1,1,YL%MP9_PH) , PGFL(1,1,YI%MP9_PH) , PGFL(1,1,YA%MP9_PH),&
   & ZPRS1    ,ZPRSF1,&
   & ZPRE9    ,ZPRE9F  , ZXYB9(1,1,YYTXYB%M_DELP),&
   & ZPHI9    ,ZPHIF9,&
   & ZZPSP_SG ,ZTSA9,PSP_RR(1,YSP_RR%YW%MP9),ZTSKIN9,&
   & PSP_SB(1,1,YSP_SB%YQ%MP9 ),ZTIA9, ZTSN9, PII0, &
   ! - UPDATED
   & PGMVT1(1,1,YT1%MT) ,PGFLT1(1,1,YQ%MP1), PGFLT1,&
   & PGMVT1(1,1,YT1%MU) ,PGMVT1(1,1,YT1%MV),&
   & PSD_VN(1,YSD_VN%YACPR%MP), PSD_VN(1,YSD_VN%YACCPR%MP), RACCPR3(1,1,KBL),&
 !- T-star Tiles
   & RUSTRTI(1,1,KBL),RVSTRTI(1,1,KBL),RAHFSTI(1,1,KBL),&
   & REVAPTI(1,1,KBL),RTSKTI(1,1,KBL),&
   & PHYS_MWAVE(1,1,1,KBL), &
   ! - OUTPUT
   & ZDIFTQ ,ZDIFTS,ZSTRTU  ,ZSTRTV,&
   & PSD_VD(1,YSD_VD%YZ0F%MP)  ,PSD_VD(1,YSD_VD%YLZ0H%MP),&
   & PSD_VD(1,YSD_VD%Y10U%MP), PSD_VD(1,YSD_VD%Y10V%MP), PSD_VD(1,YSD_VD%Y2T%MP), &
   & PSD_VD(1,YSD_VD%Y2D%MP), PSD_VD(1,YSD_VD%Y2Q%MP), PSD_VD(1,YSD_VD%Y10NU%MP), PSD_VD(1,YSD_VD%Y10NV%MP),&
   & ZTSAE1,ZTLE1,ZTIAE1,ZTSNE1,PTRAJ_PHYS,PGPHIST,PTRAJ_PHYS_TLAD,&
   & PHYS_MWAVE5=PHYS_MWAVE5,YDACV=YDACV)


  !*  Ouput from rain radar forward operator.
! IF (LEGBRAD_ACTIVE) THEN
!   DO JROF = IST, IEND
!     ! Store end-of-accumulation precipitation from TL trajectory.
!     PSD_VN(JROF,YSD_VN%YACCPR5%MP) = ZACCPR5(JROF)
!   ENDDO
! ENDIF

  !*  Ouput from rain gauge forward operator.
  IF (LERAINGG) THEN
    ! Store precipitation amount at current time step (TL trajectory for use in adjoint).
    ! First do allocation when needed.
    IF (.NOT.ASSOCIATED(PTRAJ_RAINGG_TLAD%RR)) ALLOCATE (PTRAJ_RAINGG_TLAD%RR(NPROMA,NSTOP))
    DO JROF = IST, IEND
      PTRAJ_RAINGG_TLAD%RR (JROF,NSTEP) = ZACCPR35(JROF)
    ENDDO
    IF (LPRTTRAJ .AND. PTRAJ_RAINGG_TLAD%LASTCHUNK) &
      & WRITE(NULOUT,*)'GREPTRAJ STORE TRAJ_RAINGG_TLAD in EC_PHYS_TL'
  ENDIF

!*         4.1a ADD FORCING

  IF(LFORCE) THEN
    DO JLEV=1,NFLEVG
      DO JROF=IST,IEND
        PGMVT1(JROF,JLEV,YT1%MU)=PGMVT1(JROF,JLEV,YT1%MU)+&
         & PFORCEU(JROF,JLEV)  
        PGMVT1(JROF,JLEV,YT1%MV)=PGMVT1(JROF,JLEV,YT1%MV)+&
         & PFORCEV(JROF,JLEV)  
        PGMVT1(JROF,JLEV,YT1%MT)=PGMVT1(JROF,JLEV,YT1%MT)+&
         & PFORCET(JROF,JLEV)  
        PGFLT1(JROF,JLEV,YQ%MP1)=PGFLT1(JROF,JLEV,YQ%MP1)+&
         & PFORCEQ(JROF,JLEV)  
      ENDDO
    ENDDO
  ENDIF

!*       4.2  UPDATE TEMPERATURE,  WIND, GFL

  DO JLEV=1,NFLEVG
    DO JROF=IST,IEND
      PGMVT1(JROF,JLEV,YT1%MU)=PGMV(JROF,JLEV,YPH9%MU)+&
       & ZDTPHY*PGMVT1(JROF,JLEV,YT1%MU)  
      PGMVT1(JROF,JLEV,YT1%MV)=PGMV(JROF,JLEV,YPH9%MV)+&
       & ZDTPHY*PGMVT1(JROF,JLEV,YT1%MV)  
      PGMVT1(JROF,JLEV,YT1%MT)=PGMV(JROF,JLEV,YPH9%MT)+&
       & ZDTPHY*PGMVT1(JROF,JLEV,YT1%MT)  
    ENDDO
  ENDDO
  DO JGFL=1,NUMFLDS
    IF(YCOMP(JGFL)%LT1) THEN
      DO JLEV=1,NFLEVG
        DO JROF=IST,IEND
          PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)=PGFL(JROF,JLEV,YCOMP(JGFL)%MP9_PH)+&
           & ZDTPHY*PGFLT1(JROF,JLEV,YCOMP(JGFL)%MP1)  
        ENDDO
      ENDDO
    ENDIF
  ENDDO

!*       4.4  COMPUTE NEW T+DT FOR SURFACE FIELDS

  IF (LESURF2) THEN

    IF (NSTEP < (NSTOP-1)) THEN
      DO JROF=IST,IEND
        ZTSKIN1(JROF)=ZTSKIN1(JROF)+ZDTPHY*ZTLE1(JROF)
        ZTSN1  (JROF)   = ZTSN1  (JROF)  +ZDTPHY*ZTSNE1(JROF)
        ZTSA1  (JROF,1) = ZTSA1  (JROF,1)+ZDTPHY*ZTSAE1(JROF,1)
        ZTIA1  (JROF,1) = ZTIA1  (JROF,1)+ZDTPHY*ZTIAE1(JROF,1)
      ENDDO

      IF (LTWOTL) THEN
        DO JROF=IST,IEND
          GPTSKIN0(JROF,1,KBL) = ZTSKIN1(JROF)
          GPTSKIN0(JROF,2,KBL) = ZTSN1  (JROF)
          GPTSKIN0(JROF,3,KBL) = ZTSA1  (JROF,1)
          GPTSKIN0(JROF,4,KBL) = ZTIA1  (JROF,1)
          GPTSKIN9(JROF,1,KBL) = GPTSKIN0(JROF,1,KBL)
          GPTSKIN9(JROF,2,KBL) = GPTSKIN0(JROF,2,KBL)
          GPTSKIN9(JROF,3,KBL) = GPTSKIN0(JROF,3,KBL)
          GPTSKIN9(JROF,4,KBL) = GPTSKIN0(JROF,4,KBL)
        ENDDO
      ELSE
        DO JROF=IST,IEND
          ZZPHY = 1.0_JPRB-REPSP1
          GPTSKIN9(JROF,1,KBL) = REPSP1*ZTSKIN1(JROF)+ZZPHY*GPTSKIN0(JROF,1,KBL)
          GPTSKIN9(JROF,2,KBL) = REPSP1*ZTSN1  (JROF)+ZZPHY*GPTSKIN0(JROF,2,KBL)
          GPTSKIN9(JROF,3,KBL) = REPSP1*ZTSA1(JROF,1)+ZZPHY*GPTSKIN0(JROF,3,KBL)
          GPTSKIN9(JROF,4,KBL) = REPSP1*ZTIA1(JROF,1)+ZZPHY*GPTSKIN0(JROF,4,KBL)
          GPTSKIN0(JROF,1,KBL) = ZTSKIN1(JROF)
          GPTSKIN0(JROF,2,KBL) = ZTSN1  (JROF)
          GPTSKIN0(JROF,3,KBL) = ZTSA1  (JROF,1)
          GPTSKIN0(JROF,4,KBL) = ZTIA1  (JROF,1)
        ENDDO
      ENDIF
    ELSE
      DO JFLD=1,NGSKIN
        DO JROF=IST,IEND
          GPTSKIN9(JROF,JFLD,KBL) = 0.0_JPRB
          GPTSKIN0(JROF,JFLD,KBL) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF
  ELSE

    IF (NSTEP < (NSTOP-1)) THEN
      DO JROF=IST,IEND
        ZTSKIN1(JROF)=ZTSKIN1(JROF)+ZDTPHY*ZTLE1(JROF)
        GPTSKIN9(JROF,1,KBL) = REPSP1*ZTSKIN1(JROF)&
                         & + (1.0_JPRB-REPSP1)*GPTSKIN0(JROF,1,KBL)
        GPTSKIN0(JROF,1,KBL) = ZTSKIN1(JROF)
      ENDDO
    ELSE
      DO JROF=IST,IEND
        GPTSKIN9(JROF,1,KBL) = 0.0_JPRB
        GPTSKIN0(JROF,1,KBL) = 0.0_JPRB
      ENDDO
    ENDIF
  ENDIF

!*       4.5  COMPUTE DIAGNOSTIC QUANTITIES

 IF (LESURF2) THEN
    IF (NSTEP >= (NSTOP-1)) THEN
      DO JFLD=1,NTILES
        DO JROF=IST,IEND
          RUSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
          RVSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
          RAHFSTI(JROF,JFLD,KBL) = 0.0_JPRB
          REVAPTI(JROF,JFLD,KBL) = 0.0_JPRB
          RTSKTI (JROF,JFLD,KBL) = 0.0_JPRB
        ENDDO
      ENDDO
    ENDIF
  ELSE
    DO JFLD=1,NTILES
      DO JROF=IST,IEND
        RUSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
        RVSTRTI(JROF,JFLD,KBL) = 0.0_JPRB
        RAHFSTI(JROF,JFLD,KBL) = 0.0_JPRB
        REVAPTI(JROF,JFLD,KBL) = 0.0_JPRB
        RTSKTI (JROF,JFLD,KBL) = 0.0_JPRB
      ENDDO
    ENDDO
  ENDIF

  PSD_VD(:,YSD_VD%YZ0F%MP)    = 0.0_JPRB
  PSD_VD(:,YSD_VD%YLZ0H%MP)   = 0.0_JPRB

!*    5.7   SURFACE FIELDS TIMESTEPPING

  IF (LTWOTL) THEN
    CALL GPPOPER(YDDYN,'SET0TO1',YDSURF,PSP_SB=PSP_SB,PSP_SG=PSP_SG,PSP_RR=PSP_RR)
  ELSE
    CALL GPPOPER(YDDYN,'PHTFILT',YDSURF,PSP_SB=PSP_SB,PSP_SG=PSP_SG,PSP_RR=PSP_RR)
  ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('EC_PHYS_TL',1,ZHOOK_HANDLE)
END SUBROUTINE EC_PHYS_TL
