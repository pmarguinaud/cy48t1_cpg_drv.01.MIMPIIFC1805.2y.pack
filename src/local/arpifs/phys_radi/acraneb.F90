!OPTIONS XOPT(NOEVAL)
SUBROUTINE ACRANEB ( YDRIP,YDML_PHY_MF,KIDIA,KFDIA,KLON,KTDIA,KLEV,KJN,&
 !-----------------------------------------------------------------------
 ! - INPUT  2D .
 &PAPHIF,PAPRS,PAPRSF,PCP,PR,PDELP,PNEB,PQ,PQCO2,PQICE,&
 &PQLI,PQO3,PT,&
 ! - INPUT  1D .
 &PALB,PALBDIR,PEMIS,PMU0,PGEMU,PGELAM,PMU0LU,PTS,&
 ! - OUTPUT 2D .
 &PFRSO,PFRTH,&
 ! - OUTPUT 1D .
 &PFRSODS,PFRSOPS,PFRSOLU,PFRTHDS,&
 ! - CONSTANTES .
 &PDAER,PMAK,PMAN)

!**** *ACRANEB * - FLUX RADIATIFS ET DIAGNOSTICS DE SURFACE ASSOCIES .

!     Sujet.
!     ------
!     - ROUTINE DE CALCUL ACTIF .
!       CALCUL DES FLUX RADIATIFS ET DIAGNOSTICS DE SURFACE ASSOCIES .
!     - COMPUTATION OF RADIATIVE FLUXES AND ASSOCIATED SURFACE
!       DIAGNOSTICS.

!**   Interface.
!     ----------
!        *CALL* *ACRANEB*

!-----------------------------------------------------------------------
! WARNING: THE ENGLISH VERSION OF VARIABLES' NAMES IS TO BE READ IN THE
!          "APLPAR" CODE.
!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE.
!     -------------------

! - NOM DES PARAMETRES DE DIMENSIONNEMENT DE LA PHYSIQUE.

! KIDIA      : INDICE DE DEPART DES BOUCLES VECTORISEES SUR L'HORIZONT..
! KFDIA      : INDICE DE FIN DES BOUCLES VECTORISEES SUR L'HORIZONTALE.
! KLON       : DIMENSION HORIZONTALE DES TABLEAUX.
! KTDIA      : INDICE DE DEPART DES BOUCLES VERTICALES (1 EN GENERAL).
! KLEV       : DIMENSION VERTICALE DES TABLEAUX "FULL LEVEL".
! KJN        : NOMBRE MAXI DE PLAGES JOUR/NUIT DANS LE VECTEUR.

! - NOM DES VARIABLES DE LA PHYSIQUE (PAR ORDRE ALPHABETIQUE DANS CHAQUE
!   CATEGORIE).

! - 2D (0:KLEV) .

! PAPRS      : PRESSION AUX DEMI-NIVEAUX.
! PQO3       : CONTENU SPECIFIQUE LOCAL EN OZONE.
!        (0) : CONTENU MOYEN AU DESSUS DU MODELE.

! - 2D (1:KLEV) .

! PAPHIF     : GEOPOTENTIEL AUX NIVEAUX DES COUCHES.
! PAPRSF     : PRESSION AUX NIVEAUX DES COUCHES.
! PCP        : CHALEUR MASSIQUE A PRESSION CONSTANTE.
! PR         : CONSTANTE DES GAZ POUR L'AIR.
! PDELP      : EPAISSEUR EN PRESSION DE LA COUCHE.
! PNEB       : NEBULOSITE PARTIELLE "RADIATIVE".
! PQ         : HUMIDITE SPECIFIQUE DE LA VAPEUR D'EAU.
! PQCO2      : CONTENU SPECIFIQUE LOCAL EN CO2.
! PQICE      : HUMIDITE SPECIFIQUE SOLIDE "RADIATIVE".
! PQLI       : HUMIDITE SPECIFIQUE LIQUIDE "RADIATIVE".
! PT         : TEMPERATURE.

! - 1D (PROGNOSTIQUE) .

! PALB       : DIFFUSE SURFACE ALBEDO.
! PALBDIR    : DIRECT (PARALLEL) SURFACE ALBEDO.
! PEMIS      : SURFACE EMISSIVITY.
! PTS        : SURFACE TEMPERATURE.

! - 1D (DIAGNOSTIQUE) .

! PMU0       : COSINUS LOCAL INSTANTANE DE L'ANGLE ZENITHAL SOLAIRE.
! PMU0LU     : COSINUS LOCAL INSTANTANE DE L'ANGLE ZENITHAL LUNAIRE.

! - CONSTANTES .

! PDAER(KLEV,6): EPAISSEUR OPTIQUE DES AEROSOLS DANS LA COUCHE.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE.
!     --------------------

! - 2D (0:KLEV) .

! PFRSO      : FLUX DE RAYONNEMENT SOLAIRE.
! PFRTH      : FLUX DE RAYONNEMENT THERMIQUE.

! - 1D (DIAGNOSTIQUE) .

! PFRSODS    : FLUX SOLAIRE DIFFUS VERS LE BAS EN SURFACE.
! PFRSOPS    : FLUX SOLAIRE PARALLELE EN SURFACE.
! PFRSOLU    : FLUX DE RAYONNEMENT LUNAIRE DESCENDANT EN SURFACE.
! PFRTHDS    : FLUX THERMIQUE VERS LE BAS EN SURFACE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES.
!     ---------------------

!-----------------------------------------------------------------------

!     Externes.
!     ---------
!       AC_CLOUD_MODEL
!       FRASOLU
!       POSCT

!     Methode.
!     --------

!     Auteur.
!     -------
!      89-12, J.F. Geleyn.

!     Modifications.
!     --------------
!      2003-02  New EWS code from JFG and change definition of PQLI and PQICE:
!               they become grid-size values from J.M. Piriou. (Introduced by Y.Bouteloup in pre-cy26)
!      2003-12  New Prox code from JFG (Introduced by Y.Bouteloup in CY26T1 op5)
!      06-Feb-2006 J. Masek       Introduction of cloud model.
!      22-Oct-2008 T. Kral        New gaseous transmission functions.
!      K. Yessad (Jul 2009): remove CDLOCK + some cleanings
!      2013-11, J. Masek: Changes connected to introduction of ACRANEB2.
!                         ACRANEB remains frozen version of unmodularized
!                         original radiation scheme.
!      2016-10, P. Marguinaud : Port to single precision
! End Modifications
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB     ,JPRD
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMRIP   , ONLY : TRIP
USE YOMCST   , ONLY : RSIGMA   ,RPI      ,RG       ,RKAPPA

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TRIP)        ,INTENT(IN)    :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KJN
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPHIF(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDELP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNEB(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQCO2(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQICE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLI(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQO3(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALB(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBDIR(KLON)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PEMIS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAM(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0LU(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSO(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRTH(KLON,0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSODS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSOPS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRSOLU(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRTHDS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDAER(KLON,KLEV,6) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMAK(0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMAN(0:KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCP(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PR(KLON,KLEV) 

!-----------------------------------------------------------------------

REAL(KIND=JPRB) :: ZBB(KLON,0:KLEV),ZB1(KLON,KLEV),ZB2(KLON,KLEV)&
    &,ZB3(KLON,KLEV),ZB4(KLON,KLEV),ZDEOS(KLON,KLEV)&
    &,ZDEOT(KLON,KLEV),ZFDC(KLON,0:KLEV),ZFDN(KLON,0:KLEV)&
    &,ZFMC(KLON,0:KLEV),ZFMN(KLON,0:KLEV),ZFPC(KLON,0:KLEV)&
    &,ZFPN(KLON,0:KLEV),ZTU1(KLON,KLEV),ZTU2(KLON,KLEV)&
    &,ZTU3(KLON,KLEV),ZTU4(KLON,KLEV),ZTU5(KLON,KLEV)&
    &,ZTU6(KLON,KLEV),ZTU7(KLON,KLEV),ZTU8(KLON,KLEV)&
    &,ZTU9(KLON,KLEV),ZUEOS(KLON,KLEV),ZUEOT(KLON,KLEV)&
    &,ZZFDC(KLON,0:KLEV),ZZFDN(KLON,0:KLEV),ZZFMC(KLON,0:KLEV)&
    &,ZZFMN(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZQLI(KLON,KLEV),ZQICE(KLON,KLEV)    
REAL(KIND=JPRB) :: ZUUEOT(KLON,KLEV),ZFRTH(KLON,0:KLEV),ZZFRTH(KLON,0:KLEV)&
    &,ZZZFDC(KLON,0:KLEV),ZZZFDN(KLON,0:KLEV),ZZZFMC(KLON,0:KLEV)&
    &,ZZZFMN(KLON,0:KLEV),ZEOLT(KLON,KLEV),ZGEPC(KLON,KLEV)&
    &,ZLEPC(KLON,KLEV),ZDA4G(KLON,KLEV),ZDA4L(KLON,KLEV)&
    &,ZDAMP(KLON,KLEV)
REAL(KIND=JPRB) :: ZTDC(KLON,0:KLEV),ZTDN(KLON,0:KLEV),ZTMC(KLON,0:KLEV)&
    &,ZTMN(KLON,0:KLEV),ZZTDC(KLON,0:KLEV),ZZTDN(KLON,0:KLEV)&
    &,ZZTMC(KLON,0:KLEV),ZZTMN(KLON,0:KLEV),ZZZTDC(KLON,0:KLEV)&
    &,ZZZTDN(KLON,0:KLEV),ZZZTMC(KLON,0:KLEV),ZZZTMN(KLON,0:KLEV)&
    &,ZMIXP(KLON,0:KLEV),ZZTRTH(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZAL1(KLON),ZAL2(KLON),ZA1C(KLON),ZA1N(KLON),ZA2C(KLON)&
    &,ZA2N(KLON),ZA3C(KLON),ZA3N(KLON),ZA4C(KLON),ZA4N(KLON)&
    &,ZA5C(KLON),ZA5N(KLON),ZBE1(KLON),ZBE2(KLON),ZCTH(KLON)&
    &,ZDE1(KLON),ZDE2(KLON),ZDM0I(KLON),ZEOS(KLON),ZEOSO(KLON)&
    &,ZEOSS(KLON),ZEOT(KLON),ZEOTO(KLON),ZEOTS(KLON)&
    &,ZDRB(KLON),ZDRS(KLON),ZGA1(KLON),ZGA2(KLON),ZMU0I(KLON)&
    &,ZNMNB(KLON),ZNMNH(KLON),ZNMXB(KLON),ZNMXH(KLON)&
    &,ZNSC(KLON),ZNSH(KLON),ZNSO(KLON),ZNSOR(KLON),ZNTC(KLON)&
    &,ZNTH(KLON),ZNTO(KLON),ZRSC(KLON),ZRSH(KLON),ZRSO(KLON)&
    &,ZRTC(KLON),ZRTH(KLON),ZRTO(KLON),ZTRB(KLON),ZTRS(KLON)&
    &,ZUETS(KLON),ZZTRB(KLON)&
    &,ZZTRS(KLON),ZMU0(KLON),ZII0(KLON),ZFSO(KLON),ZFRB(KLON)&
    &,ZFRS(KLON),ZZFRB(KLON),ZZFRS(KLON)&
    &,ZVSH(KLON),ZVSC(KLON),ZVSO(KLON),ZVTH(KLON),ZVTC(KLON),ZVTO(KLON)
REAL(KIND=JPRB) :: ZPNER(KLON,KLEV,0:KLEV),ZTRG(KLON,0:KLEV),ZTRGO(KLON,0:KLEV)&
    &,ZTRMUL(KLON,0:KLEV),ZFLUXD(KLON,0:KLEV),ZFLUXL(KLON,0:KLEV)&
    &,ZFLUXR(KLON,0:KLEV),ZRPROX(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZTRT(KLON,0:KLEV),ZTRW(KLON,0:KLEV),ZFLUXT(KLON,0:KLEV)&
    &,ZFLUXC(KLON,0:KLEV),ZFLUX1(KLON)&
    &,ZTRP(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZZA4C1(KLON,KLEV)&
    &,ZZA4C2(KLON,KLEV),ZZA4C3(KLON,KLEV),ZZA4C4(KLON,KLEV)&
    &,ZZA4C5(KLON,KLEV),ZZA5C1(KLON,KLEV),ZZA5C2(KLON,KLEV)&
    &,ZZA5C3(KLON,KLEV),ZZA5C4(KLON,KLEV),ZZA5C5(KLON,KLEV)&
    &,ZZA1C(KLON,KLEV),ZZA2C(KLON,KLEV),ZZA3C(KLON,KLEV)
REAL(KIND=JPRB) :: ZBSFSI(KLON,KLEV)
REAL(KIND=JPRB) :: ZBSFSN(KLON,KLEV)
REAL(KIND=JPRB) :: ZBSFTI(KLON,KLEV)
REAL(KIND=JPRB) :: ZBSFTN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEO1TI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEO1TN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEO2TI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEO2TN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEOASI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEOASN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEOATI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEOATN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEODSI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEODSN(KLON,KLEV)
REAL(KIND=JPRB) :: ZEODTI(KLON,KLEV)
REAL(KIND=JPRB) :: ZEODTN(KLON,KLEV)
REAL(KIND=JPRB) :: ZUSAI (KLON,KLEV)
REAL(KIND=JPRB) :: ZUSAN (KLON,KLEV)
REAL(KIND=JPRB) :: ZUSBI (KLON,KLEV)
REAL(KIND=JPRB) :: ZUSBN (KLON,KLEV)

INTEGER(KIND=JPIM) :: IFDIA(KJN),IIDIA(KJN)

REAL(KIND=JPRB) :: ZRT_VEC(KLON,KLEV)
REAL(KIND=JPRD) :: ZTDETA_VEC(KLON,KLEV)
REAL(KIND=JPRB) :: ZGAS2B(6),ZG4B(6)
REAL(KIND=JPRB) :: ZRHOZ0V(6)

INTEGER(KIND=JPIM) :: IAUCR, JAE, JGA, JLEV, JLEVE, ILEVL, JLON, JN

LOGICAL :: LLFDIA, LLIDIA, LLREWS, LLILEV

REAL(KIND=JPRB) :: ZAL, ZALP, ZARGLI, ZTRLI, ZDUC, ZDUH, ZDUO, ZEARRT,&
          &ZEO, ZEO1, ZEO1SA(KLON,KLEV), ZEO1SI, ZEO1SN, ZEO1TA(KLON,KLEV),&
          &ZEO2, ZEO2SA(KLON,KLEV), ZEO3SA(KLON,KLEV),ZEO4SA(KLON,KLEV),ZEO2SI, ZEO2SN,&
          &ZEO2TA(KLON,KLEV), ZEO3, ZEO4, ZEO5,&
          &ZEOSA(KLON,KLEV), ZEOSI, ZEOSN, ZEPS, ZEPS1, ZEPS2,&
          &ZEPS3, ZEPSAL, ZG1, ZG2, ZIEART, ZMD, ZMU0IC, ZMU0IN,&
          &ZP, ZQ, ZRHO, ZRT, ZT2, ZT2I, ZT3, ZT4I,&
          &ZTAU, ZTD1, ZTD2, ZTD3, ZTD4, ZTD5, ZTD6,&
          &ZTD7, ZTDS1, ZTDS2, ZTDS3, ZTI, ZTUS1,&
          &ZWSC, ZWSH, ZWSO, ZWTC, ZWTH, ZWTO, ZSOLLEV, ZT_DEB, ZT_FIN,&
          &ZLON_CT, ZLAT_CT, ZLON_LU, ZLAT_LU,&
          &ZEPSNEB, ZSECUR, ZEFFE2, ZTARE2, ZDEL1, ZDEL2, ZFTPP,&
          &ZTARSP,  ZUSI, ZUSN,&
          &ZGAMV, ZEPSV, ZIBV0, ZIZV0, ZVOIGT, ZBZV, ZAFVOI, ZIRHOV,&
          &ZVOISIM, ZVOIEMP,ZZEO2TA,ZZEO2SA,ZUSA
REAL(KIND=JPRD) :: ZTDETA
REAL(KIND=JPRB) :: ZDEBL,ZW
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "ac_cloud_model.intfb.h"
#include "frasolu.intfb.h"
#include "posct.intfb.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACRANEB',0,ZHOOK_HANDLE)
ASSOCIATE(TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & VDP=>YDML_PHY_MF%YRPHY3%VDP, GOLCB=>YDML_PHY_MF%YRPHY3%GOLCB, GOLCC=>YDML_PHY_MF%YRPHY3%GOLCC, &
 & USAA=>YDML_PHY_MF%YRPHY3%USAA, &
 & GOLCA=>YDML_PHY_MF%YRPHY3%GOLCA, EODSA=>YDML_PHY_MF%YRPHY3%EODSA, GCE4=>YDML_PHY_MF%YRPHY3%GCE4, &
 & RII0=>YDML_PHY_MF%YRPHY3%RII0, VNP=>YDML_PHY_MF%YRPHY3%VNP, BSFTA=>YDML_PHY_MF%YRPHY3%BSFTA, &
 & EARRT=>YDML_PHY_MF%YRPHY3%EARRT, &
 & GCA=>YDML_PHY_MF%YRPHY3%GCA, EOATA=>YDML_PHY_MF%YRPHY3%EOATA, GCC=>YDML_PHY_MF%YRPHY3%GCC, &
 & GCB=>YDML_PHY_MF%YRPHY3%GCB, &
 & EODTA=>YDML_PHY_MF%YRPHY3%EODTA, USBA=>YDML_PHY_MF%YRPHY3%USBA, GCD4=>YDML_PHY_MF%YRPHY3%GCD4, &
 & BSFSA=>YDML_PHY_MF%YRPHY3%BSFSA, EOASA=>YDML_PHY_MF%YRPHY3%EOASA, EORAY=>YDML_PHY_MF%YRPHY3%EORAY, &
 & RMIXD=>YDML_PHY_MF%YRPHY3%RMIXD, RMIXP0=>YDML_PHY_MF%YRPHY3%RMIXP0,&
 & LRTPP=>YDML_PHY_MF%YRPHY%LRTPP, LRAYLU=>YDML_PHY_MF%YRPHY%LRAYLU, LRAYPL=>YDML_PHY_MF%YRPHY%LRAYPL, &
 & LRMIX=>YDML_PHY_MF%YRPHY%LRMIX, LVFULL=>YDML_PHY_MF%YRPHY%LVFULL, LRPROX=>YDML_PHY_MF%YRPHY%LRPROX, &
 & LRNUMX=>YDML_PHY_MF%YRPHY%LRNUMX, NRAUTOEV=>YDML_PHY_MF%YRPHY%NRAUTOEV, NPHYREP=>YDML_PHY_MF%YRPHY%NPHYREP, &
 & NOIR=>YDML_PHY_MF%YRPHY%NOIR, LVOIGT=>YDML_PHY_MF%YRPHY%LVOIGT, LREWS=>YDML_PHY_MF%YRPHY%LREWS, &
 & LRSTAB=>YDML_PHY_MF%YRPHY%LRSTAB, LRTDL=>YDML_PHY_MF%YRPHY%LRTDL,RDECLU=>YDRIP%RDECLU, &
 & RIP0LU=>YDRIP%RIP0LU, RTIMTR=>YDRIP%RTIMTR, RTMOLT=>YDRIP%RTMOLT)
!-----------------------------------------------------------------------

!     ------------------------------------------------------------------
!     0 - SET QL AND QI DEFINITIONS TO THEIR VALUE INSIDE THE CLOUD.
!         CALL CLOUD MODEL TO COMPUTE SATURATED CLOUD OPTICAL PROPERTIES.

IF (JPRD == JPRB) THEN
  ZEPSNEB=1.E-10_JPRB
ELSE
  ZEPSNEB=1.E-04_JPRB
ENDIF

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZQICE(JLON,JLEV)=PQICE(JLON,JLEV)/MAX(ZEPSNEB,PNEB(JLON,JLEV))
    ZQLI (JLON,JLEV)=PQLI (JLON,JLEV)/MAX(ZEPSNEB,PNEB(JLON,JLEV))
    ZRT_VEC(JLON,JLEV)=SQRT(PT(JLON,JLEV))
    ZTDETA_VEC(JLON,JLEV)=REAL (ZRT_VEC(JLON,JLEV), JPRD)**49*EXP(REAL (GCE4, JPRD)/REAL (PT(JLON,JLEV), JPRD))
  ENDDO
ENDDO

CALL AC_CLOUD_MODEL( YDML_PHY_MF%YRPHY,YDML_PHY_MF%YRPHY3, &
 & KIDIA    ,KFDIA    ,KLON     ,KTDIA    ,KLEV     , &
 & ZEPSNEB  ,PDELP    ,PNEB     ,ZQICE    ,ZQLI     , &
 & PR       ,PAPRSF   ,PT       ,ZBSFSI   ,ZBSFSN   , &
 & ZBSFTI   ,ZBSFTN   ,ZEOASI   ,ZEOASN   ,ZEOATI   , &
 & ZEOATN   ,ZEODSI   ,ZEODSN   ,ZEODTI   ,ZEODTN   , &
 & ZUSAI    ,ZUSAN    ,ZUSBI    ,ZUSBN    )

ZRHOZ0V(1)=0.296_JPRB
ZRHOZ0V(2)=0.046_JPRB
ZRHOZ0V(3)=0.202_JPRB
ZRHOZ0V(4)=0.031_JPRB
ZRHOZ0V(5)=0.013_JPRB
ZRHOZ0V(6)=0.012_JPRB

ZGAMV=0.36_JPRB
ZEPSV=0.014_JPRB
ZIBV0=1._JPRB/0.74_JPRB
ZIZV0=1._JPRB/0.0027_JPRB

ZVOISIM=1.21_JPRB

ZT_DEB=-12369600.0_JPRB ! debut d'eclipse en secondes depuis le 1.1.2000 a 12 UTC.
ZT_FIN=-12348000.0_JPRB ! fin d'eclipse en secondes depuis le 1.1.2000 a 12 UTC.
IF(NOIR == -999 .OR. (NOIR ==1 .AND. (RTIMTR > ZT_DEB .AND. RTIMTR < ZT_FIN))) THEN

!-------------------------------------------------
! On est entre le 1999-08-11 08:00 UTC et le 1999-08-11 14:00 UTC.
! On applique une eclipse de Soleil dont la trajectoire
! du centre de totalite (CT dans la suite) est 
! celle fournie par le Bureau des Longitudes,
! et avec un coefficient d'eclipse partielle
! lie a l'angle au centre de la Lune entre CT et le point courant.
!-------------------------------------------------


!-------------------------------------------------
! Position du centre de totalite.
!-------------------------------------------------

  CALL POSCT(NOIR,RTIMTR,ZLON_CT,ZLAT_CT)
  IF(ZLON_CT /= 0._JPRB) THEN

!-------------------------------------------------
! L'heure courante est dans la zone tabulee par le BDL.
!-------------------------------------------------


!-------------------------------------------------
! Latitude et longitude du point 
! au dessus duquel la Lune est au zenith.
!-------------------------------------------------

    ZLON_LU=RPI-RTMOLT
    ZLAT_LU=RDECLU

!-------------------------------------------------
! Fraction de Soleil ZFSO vue en chaque point "JLON".
!-------------------------------------------------

    CALL FRASOLU(KIDIA,KFDIA,KLON,PGELAM,PGEMU, &
     & ZLON_LU,ZLAT_LU, &
     & ZLON_CT,ZLAT_CT, &
     & ZFSO)
  ELSE

!-------------------------------------------------
! L'heure courante est hors de la zone tabulee par le BDL.
!-------------------------------------------------

    ZFSO=1._JPRB
  ENDIF
ELSE
  ZFSO=1._JPRB
ENDIF

!*
!     ------------------------------------------------------------------
!     I - CALCUL DES PARAMETRES DERIVES, CONSTANTES DE SECURITE (POUR
!     L'EPAISSEUR EN PRESSION DE LA COUCHE "AU DESSUS DU SOMMET DU
!     MODELE", L'HUMIDITE SPECIFIQUE ET LA CORRECTION POUR EVITER UNE
!     SOLUTION ANALYTIQUE RESONNANTE ENTRE RAYONNEMENT PARALLELE ET
!     RAYONNEMENT DIFFUS) AINSI QU'ARGUMENT LIMITE POUR EVITER
!     "L'UNDERFLOW" DANS LES CALCULS DE TRANSMISSION.

!         COMPUTATION OF DERIVED PARAMETERS, SECURITY CONSTANTS (FOR THE
!     PRESSURE THICKNESS OF THE LAYER "ABOVE THE MODEL'S TOP, THE
!     SPECIFIC HUMIDITY AND THE CORRECTION TO AVOID A RESONNANT
!     ANALYTICAL SOLUTION BETWEEN PARALLEL AND SCATTERED RADIATION) AS
!     WELL AS A LIMIT ARGUMENT TO AVOID "UNDERFLOW" IN THE COMPUTATIONS
!     OF TRANSMISSIVITY.

ZSECUR=0._JPRB
IF (LRSTAB) THEN
  ZSECUR=4._JPRB*RSIGMA*RG*TSPHY
ENDIF

DO JGA=1,6
  ZGAS2B(JGA)=GCA(JGA)/(2._JPRB*GCB(JGA))
  ZG4B(JGA)=4._JPRB*GCB(JGA)
ENDDO

! ALPHA1, ALPHA2 FOR AEROSOLS
ZEO2TA=0._JPRB
ZEO2SA=0._JPRB
ZEO1TA=0._JPRB
ZEO1SA=0._JPRB
ZEOSA=0._JPRB

DO JAE=1,6
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZZEO2TA=2._JPRB*BSFTA(JAE)*EODTA(JAE)*PDAER(JLON,JLEV,JAE)
      ZZEO2SA=2._JPRB*BSFSA(JAE)*EODSA(JAE)*PDAER(JLON,JLEV,JAE)
      ZEO2TA(JLON,JLEV)=ZEO2TA(JLON,JLEV)+ZZEO2TA
      ZEO2SA(JLON,JLEV)=ZEO2SA(JLON,JLEV)+ZZEO2SA
      ZEO1TA(JLON,JLEV)=ZEO1TA(JLON,JLEV)+ZZEO2TA+2._JPRB*EOATA(JAE)&
       & *PDAER(JLON,JLEV,JAE)  
      ZEO1SA(JLON,JLEV)=ZEO1SA(JLON,JLEV)+ZZEO2SA+2._JPRB*EOASA(JAE)&
       & *PDAER(JLON,JLEV,JAE)
      ZEOSA(JLON,JLEV)=ZEOSA(JLON,JLEV)+(EODSA(JAE)+EOASA(JAE))&
       & *PDAER(JLON,JLEV,JAE)
    ENDDO
  ENDDO
ENDDO

! ALPHA1, ALPHA2 FOR ICE/LIQUID (THERMAL BAND)
DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZEO2TI(JLON,JLEV)=2._JPRB*ZBSFTI(JLON,JLEV)*ZEODTI(JLON,JLEV)
    ZEO2TN(JLON,JLEV)=2._JPRB*ZBSFTN(JLON,JLEV)*ZEODTN(JLON,JLEV)
    ZEO1TI(JLON,JLEV)=ZEO2TI(JLON,JLEV)+2._JPRB*ZEOATI(JLON,JLEV)
    ZEO1TN(JLON,JLEV)=ZEO2TN(JLON,JLEV)+2._JPRB*ZEOATN(JLON,JLEV)
  ENDDO
ENDDO

ZEPS1=1.E-03_JPRB
ZEPS2=1.E-09_JPRB
ZEPS3=1.E-12_JPRB
ZEPSAL=1.E-04_JPRB

ZARGLI=-250._JPRB
ZTRLI=EXP(ZARGLI)

IF(LRAYLU) THEN

! CALCULS SOLAIRES ET LUNAIRES.
! LORSQUE LE SOLEIL EST LEVE, ZMU0 ET ZII0 SONT CEUX DU SOLEIL. 
! SI SEULE LA LUNE EST LEVEE, ILS RECOIVENT CEUX DE LA LUNE.

! SOLAR AND LUNAR COMPUTATIONS.
! IF SUN IS UP, ZMU0 AND ZII0 ARE RELATIVE TO THE SUN.
! IF MOON IS UP AND SUN IS DOWN, ZMU0 AND ZII0 ARE RELATIVE TO THE MOON.

  DO JLON=KIDIA,KFDIA
    ZSOLLEV=0.5_JPRB*(1._JPRB-SIGN(1._JPRB,0._JPRB-PMU0(JLON)))
    ZMU0(JLON)=ZSOLLEV*PMU0(JLON)+(1._JPRB-ZSOLLEV)*PMU0LU(JLON)
    ZII0(JLON)=ZSOLLEV*RII0+(1._JPRB-ZSOLLEV)*RIP0LU
  ENDDO

ELSE

! CALCULS SOLAIRES SEULS.
! SOLAR COMPUTATIONS ONLY.

  DO JLON=KIDIA,KFDIA
    ZMU0(JLON)=PMU0(JLON)
    ZII0(JLON)=RII0
  ENDDO

ENDIF

DO JLON=KIDIA,KFDIA
  ZII0(JLON)=ZII0(JLON)*ZFSO(JLON)
ENDDO

!*
!     ------------------------------------------------------------------
!     II - CALCUL DES LIMITES JOUR/NUIT.

!          COMPUTATION OF DAY/NIGHT LIMITS.

! - TEMPORAIRE(S) 1D .

! IIDIA     : TABLEAU DES INDICES DE DEBUTS DE "PLAGES" SOLAIRES.
!           : ARRAY OF INDICES OF BEGINNING OF "DAYLIGHT" INTERVALS.
! IFDIA     : TABLEAU DES INDICES DE FINS DE "PLAGES" SOLAIRES.
!           : ARRAY OF INDICES OF END OF "DAYLIGHT" INTERVALS.

IF(.NOT.LRAYPL.OR.(NPHYREP /= 0 .AND. NPHYREP /= -4)) THEN

! PAS DE CALCUL DE "PLAGES" SOLAIRES.
! NO "DAYLIGHT" INTERVALS COMPUTATION.

  IAUCR=1
  IIDIA(1)=KIDIA
  IFDIA(1)=KFDIA

ELSE

! CALCUL DE "PLAGES" SOLAIRES.
! "DAYLIGHT" INTERVALS COMPUTATION.

  IAUCR=0
  LLIDIA=ZMU0(KIDIA) > 0._JPRB
  LLFDIA=.FALSE.

  IF (LLIDIA) THEN
    IAUCR=1
    IIDIA(1)=KIDIA
  ENDIF
  
  !     BOUCLE NON VECTORISEE.
  !     NON VECTORIZED LOOP.
  
  DO JLON=KIDIA,KFDIA
    LLFDIA=ZMU0(JLON) > 0._JPRB
  
    IF (LLFDIA.NEQV.LLIDIA) THEN
  
      IF (LLFDIA) THEN
        IAUCR=IAUCR+1
        IIDIA(IAUCR)=JLON
      ELSE
        IFDIA(IAUCR)=JLON-1
      ENDIF
  
      LLIDIA=LLFDIA
    ENDIF
  
  ENDDO
  
  IF (LLFDIA) THEN
    IFDIA(IAUCR)=KFDIA
  ENDIF

ENDIF

!*
!     ------------------------------------------------------------------
!     III - CALCUL DES EPAISSEURS OPTIQUES GASEUSES. LES CALCULS
!     DESCENDANTS SONT SYMETRIQUES ENTRE SOLAIRE ET THERMIQUE (AU TERME
!     D'ALLONGEMENT PRES) MAIS LES CALCULS MONTANTS (DIFFUS TOUS LES
!     DEUX) SONT POUR UN FLUX SOLAIRE REFLECHI ET UN FLUX THERMIQUE
!     CORRESPONDANT A L'ECHANGE AVEC LA SURFACE.

!           COMPUTATION OF GASEOUS OPTICAL DEPTHS. THE DESCENDING
!     CALCULATIONS ARE SYMETRICAL BETWEEN SOLAR AND THERMAL (EXCEPT FOR
!     THE DIFFUSIVITY FACTOR) BUT THE ASCENDING CALCULATIONS (DIFFUSE IN
!     BOTH CASES) ARE FOR A REFLECTED SOLAR FLUX AND FOR A THERMAL FLUX
!     CORRESPONDING TO THE EXCHANGE WITH THE SURFACE.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZDEOS     : EPAISSEUR OPTIQUE "GAZ" DESCENDANTE SOLAIRE.
!            : GASEOUS OPTICAL DEPTH SOLAR DESCENDING.
! ZDEOT     : EPAISSEUR OPTIQUE "GAZ" DESCENDANTE THERMIQUE.
!            : GASEOUS OPTICAL DEPTH THERMAL DESCENDING.
! ZUEOS     : EPAISSEUR OPTIQUE "GAZ" MONTANTE SOLAIRE.
!            : GASEOUS OPTICAL DEPTH SOLAR ASCENDING.
! ZUEOT     : EPAISSEUR OPTIQUE "GAZ" MONTANTE THERMIQUE.
!            : GASEOUS OPTICAL DEPTH THERMAL ASCENDING.

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.1   CALCULS PRELIMINAIRES.

!             PRELIMINARY CALCULATIONS.

!     GEOMETRIE DU RAYONNEMENT.
!     RADIATIVE GEOMETRY.

! - TEMPORAIRE(S) 1D .

! ZDM0I     : DEMI DE L'INVERSE DU COSINUS MODIFIE DE L'ANGLE ZENITHAL.
!            : HALF INVERSE OF THE MODIFIED COSINE OF THE ZENITH ANGLE.

ZMD=0.5_JPRB*EXP(0.5_JPRB)
ZEARRT=EARRT*(EARRT+2._JPRB)
ZIEART=1._JPRB/EARRT
DO JLON=KIDIA,KFDIA
  ZDM0I(JLON)=0.5_JPRB*(SQRT(ZMU0(JLON)*ZMU0(JLON)+ZEARRT)-ZMU0(JLON))*ZIEART
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.2   SOMMET DE L'ATMOSPHERE DU MODELE (OU VALEUR ARBITRAIREMENT
!     FAIBLE DE LA PRESSION).

!             TOP OF THE MODEL'S ATMOSPHERE (OR ARBITRARILY SMALL
!     PRESSURE VALUE).

! - TEMPORAIRE(S) 1D .

! ZNSH      : QUANTITE D'ABSORBANT NON REDUITE SOLAIRE H2O.
!            : UNREDUCED ABSORBER AMOUNT SOLAR H2O.
! ZNSC      : QUANTITE D'ABSORBANT NON REDUITE SOLAIRE CO2.
!            : UNREDUCED ABSORBER AMOUNT SOLAR CO2.
! ZNSO      : QUANTITE D'ABSORBANT NON REDUITE SOLAIRE O3.
!            : UNREDUCED ABSORBER AMOUNT SOLAR O3.
! ZNTH      : QUANTITE D'ABSORBANT NON REDUITE THERMIQUE H2O.
!            : UNREDUCED ABSORBER AMOUNT THERMAL H2O.
! ZNTC      : QUANTITE D'ABSORBANT NON REDUITE THERMIQUE CO2.
!            : UNREDUCED ABSORBER AMOUNT THERMAL CO2.
! ZNTO      : QUANTITE D'ABSORBANT NON REDUITE THERMIQUE O3.
!            : UNREDUCED ABSORBER AMOUNT THERMAL O3.
! ZRSH      : QUANTITE D'ABSORBANT REDUITE (*P) SOLAIRE H2O.
!            : REDUCED (*P) ABSORBER AMOUNT SOLAR H2O.
! ZRSC      : QUANTITE D'ABSORBANT REDUITE (*P) SOLAIRE CO2.
!            : REDUCED (*P) ABSORBER AMOUNT SOLAR CO2.
! ZRSO      : QUANTITE D'ABSORBANT REDUITE (*P) SOLAIRE O3.
!            : REDUCED (*P) ABSORBER AMOUNT SOLAR O3.
! ZRTH      : QUANTITE D'ABSORBANT REDUITE (*P) THERMIQUE H2O (BANDES).
!            : REDUCED (*P) ABSORBER AMOUNT THERMAL H2O (BANDS).
! ZCTH      : QUANTITE D'ABSORBANT (*P ET *ES) THERMIQUE H2O (CONTINU).
!            : REDUCED (*P AND *ES) ABSORBER AMOUNT THERMAL H2O (CONT.).
! ZRTC      : QUANTITE D'ABSORBANT REDUITE (*P) THERMIQUE CO2.
!            : REDUCED (*P) ABSORBER AMOUNT THERMAL CO2.
! ZRTO      : QUANTITE D'ABSORBANT REDUITE (*P) THERMIQUE O3.
!            : REDUCED (*P) ABSORBER AMOUNT THERMAL O3.
! ZNSOR     : QUANTITE DOUBLE D'OZONE AU DESSUS DU NIVEAU CONSIDERE.
!            : DOUBLED OZONE QUANTITY ABOVE THE CONSIDERED LEVEL.
! ZEOS      : RESULTAT DES CALCULS D'EPAISSEUR OPTIQUE SOLAIRE.
!            : RESULT OF THE SOLAR OPTICAL DEPTH COMPUTATIONS.
! ZEOT      : RESULTAT DES CALCULS D'EPAISSEUR OPTIQUE THERMIQUE.
!            : RESULT OF THE THERMAL OPTICAL DEPTH COMPUTATIONS.
! ZEOSS     : EPAISSEUR OPTIQUE SOLAIRE "AU DESSUS DU MODELE".
!            : SOLAR OPTICAL DEPTH "ABOVE THE MODEL'S TOP".
! ZEOTS     : EPAISSEUR OPTIQUE THERMIQUE "DOWN" "AU DESSUS DU MODELE".
!            : THERM. "DOWNWARDS" OPTICAL DEPTH "ABOVE THE MODEL'S TOP".

DO JLON=KIDIA,KFDIA
  ZNSOR(JLON)=2._JPRB*MAX(PAPRS(JLON,0),ZEPS1)*PQO3(JLON,0)
ENDDO
DO JLEV=1,KTDIA-1
  DO JLON=KIDIA,KFDIA
    ZNSOR(JLON)=ZNSOR(JLON)+2._JPRB*PDELP(JLON,JLEV)*PQO3(JLON,JLEV)
  ENDDO
ENDDO

! EXTRA LOOP FOR THE CORRECTION OF THE 'PROXIMITY EFFECT' OR THE
! INTERPOLATION BETWEEN MAXIMUM AND MINIMUM EBL EFFECTS

IF ((LRPROX.OR.LRMIX).AND.(.NOT.(NRAUTOEV == 1.OR.LRTDL))) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZIRHOV=(PR(JLON,JLEV)*PT(JLON,JLEV))/PAPRSF(JLON,JLEV)
      ZQ=MAX(ZEPS2,PQ(JLON,JLEV))
      !ZRT=SQRT(PT(JLON,JLEV))
      ZRT=ZRT_VEC(JLON,JLEV)
      ZTI=1._JPRB/PT(JLON,JLEV)
      ZT2=PT(JLON,JLEV)*PT(JLON,JLEV)
      ZT2I=ZTI*ZTI
      ZT4I=ZT2I*ZT2I
      !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,JLEV))
      ZTDETA=ZTDETA_VEC(JLON,JLEV)
      ZT3=ZT2*PT(JLON,JLEV)
      ZDUH=(2._JPRB*PDELP(JLON,JLEV))*ZQ
      ZDUC=(2._JPRB*PDELP(JLON,JLEV))*PQCO2(JLON,JLEV)
      ZDUO=(2._JPRB*PDELP(JLON,JLEV))*PQO3(JLON,JLEV)
      ZNTH(JLON)=ZDUH*ZTI
      ZNTC(JLON)=ZDUC
      ZNTO(JLON)=ZDUO*ZT2
      ZDUH=ZDUH*(ZMD*PAPRSF(JLON,JLEV))*ZRT
      ZDUC=ZDUC*(ZMD*PAPRSF(JLON,JLEV))*PT(JLON,JLEV)
      ZDUO=ZDUO*(ZMD*PAPRSF(JLON,JLEV))*ZRT
      ZRTH(JLON)=(ZDUH*ZT2I*ZRT)
      ZVTH(JLON)=(ZDUH*ZT2I*ZRT)*ZIRHOV
      ZCTH(JLON)=(ZDUH*ZT2I*ZRT)*ZT4I*(1._JPRB+GCD4*ZQ*ZTDETA)
      ZRTC(JLON)=ZDUC*PT(JLON,JLEV)
      ZVTC(JLON)=ZDUC*PT(JLON,JLEV)*ZIRHOV
      ZRTO(JLON)=ZDUO*ZT3*ZRT
      ZVTO(JLON)=ZDUO*ZT3*ZRT*ZIRHOV
      ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
      ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
      ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
      ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
      ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
      ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
      ZEOLT(JLON,JLEV)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)&
       &+ZWTH*(VNP(3,4)&
       &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
       &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
       &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
       &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
       &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
       &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
       &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
       &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
       &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

      ! GAS OVERLAP CORRECTION
      ZEOLT(JLON,JLEV)=ZEOLT(JLON,JLEV) &
       &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
       &/(ZRTC(JLON)+GOLCC(4))) &
       &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
       &/(ZRTO(JLON)+GOLCC(5))) &
       &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
       &/(ZRTO(JLON)+GOLCC(6)))
    ENDDO
  ENDDO
ENDIF

DO JLON=KIDIA,KFDIA
  ZP=MAX(ZEPS1,PAPRS(JLON,KTDIA-1))
  ZIRHOV=(PR(JLON,KTDIA)*PT(JLON,KTDIA))/ZP
  ZQ=MAX(ZEPS2,PQ(JLON,KTDIA))
  !ZRT=SQRT(PT(JLON,KTDIA))
  ZRT=ZRT_VEC(JLON,KTDIA)
  ZTI=1._JPRB/PT(JLON,KTDIA)
  ZT2=PT(JLON,KTDIA)*PT(JLON,KTDIA)
  ZT2I=ZTI*ZTI
  ZT4I=ZT2I*ZT2I
  !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,KTDIA))
  ZTDETA=ZTDETA_VEC(JLON,KTDIA)
  ZT3=ZT2*PT(JLON,KTDIA)
  ZNSH(JLON)=(2._JPRB*ZP)*ZQ
  ZNSC(JLON)=(2._JPRB*ZP)*PQCO2(JLON,KTDIA)
  ZNSO(JLON)=ZNSOR(JLON)
  ZNTH(JLON)=ZNSH(JLON)*ZTI
  ZNTC(JLON)=ZNSC(JLON)
  ZNTO(JLON)=ZNSO(JLON)*ZT2
  ZRSH(JLON)=ZNSH(JLON)*(ZMD*0.5_JPRB*ZP)*ZRT
  ZRSC(JLON)=ZNSC(JLON)*(ZMD*0.5_JPRB*ZP)*PT(JLON,KTDIA)
  ZRSO(JLON)=ZNSO(JLON)*(ZMD*0.5_JPRB*ZP)*ZRT
  ZRTH(JLON)=ZRSH(JLON)*ZT2I*ZRT
  ZVTH(JLON)=ZRSH(JLON)*ZT2I*ZRT*ZIRHOV
  ZCTH(JLON)=ZRTH(JLON)*ZT4I*(1._JPRB+GCD4*ZQ*ZTDETA)
  ZRTC(JLON)=ZRSC(JLON)*PT(JLON,KTDIA)
  ZVTC(JLON)=ZRSC(JLON)*PT(JLON,KTDIA)*ZIRHOV
  ZRTO(JLON)=ZRSO(JLON)*ZT3*ZRT
  ZVTO(JLON)=ZRSO(JLON)*ZT3*ZRT*ZIRHOV
  ZNSH(JLON)=ZNSH(JLON)*ZDM0I(JLON)
  ZNSC(JLON)=ZNSC(JLON)*ZDM0I(JLON)
  ZNSO(JLON)=ZNSO(JLON)*ZDM0I(JLON)
  ZRSH(JLON)=ZRSH(JLON)*(ZDM0I(JLON)/ZMD)
  ZVSH(JLON)=ZRSH(JLON)*ZIRHOV
  ZRSC(JLON)=ZRSC(JLON)*(ZDM0I(JLON)/ZMD)
  ZVSC(JLON)=ZRSC(JLON)*ZIRHOV
  ZRSO(JLON)=ZRSO(JLON)*(ZDM0I(JLON)/ZMD)
  ZVSO(JLON)=ZRSO(JLON)*ZIRHOV
ENDDO
DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    ZVOIGT=ZRHOZ0V(1)*ZVSH(JLON)/ZRSH(JLON)
    ZBZV=ZG4B(1)*ZNSH(JLON)/(ZRSH(JLON)/ZNSH(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWSH=(ZGAS2B(1)*(ZRSH(JLON)/ZNSH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(1)*ZRSH(JLON)
    ZVOIGT=ZRHOZ0V(2)*ZVSC(JLON)/ZRSC(JLON)
    ZBZV=ZG4B(2)*ZNSC(JLON)/(ZRSC(JLON)/ZNSC(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWSC=(ZGAS2B(2)*(ZRSC(JLON)/ZNSC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(2)*ZRSC(JLON)
    ZVOIGT=ZRHOZ0V(3)*ZVSO(JLON)/ZRSO(JLON)
    ZBZV=ZG4B(3)*ZNSO(JLON)/(ZRSO(JLON)/ZNSO(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWSO=(ZGAS2B(3)*(ZRSO(JLON)/ZNSO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(3)*ZRSO(JLON)
    ZEOS(JLON)=(ZWSH*(1._JPRB+ZWSH*(VNP(1,1)+ZWSH*(VNP(2,1)&
     &+ZWSH*(VNP(3,1)&
     &+ZWSH*(VNP(4,1)+ZWSH*VNP(5,1)))))))/(1._JPRB+ZWSH*(VDP(1,1)&
     &+ZWSH*(VDP(2,1)+ZWSH*(VDP(3,1)+ZWSH*(VDP(4,1)+ZWSH&
     &*VDP(5,1))))))+(ZWSC*(1._JPRB+ZWSC*(VNP(1,2)+ZWSC*(VNP(2,2)&
     &+ZWSC*(VNP(3,2)+ZWSC*(VNP(4,2)+ZWSC*VNP(5,2)))))))/(1._JPRB&
     &+ZWSC*(VDP(1,2)+ZWSC*(VDP(2,2)+ZWSC*(VDP(3,2)+ZWSC&
     &*(VDP(4,2)+ZWSC*VDP(5,2))))))+(ZWSO*(1._JPRB+ZWSO*(VNP(1,3)&
     &+ZWSO*(VNP(2,3)+ZWSO*VNP(3,3)))))/(1._JPRB+ZWSO*(VDP(1,3)&
     &+ZWSO*(VDP(2,3)+ZWSO*VDP(3,3))))
    ZEOSS(JLON)=ZEOS(JLON)/ZDM0I(JLON)

! EXTRA STORAGE FOR A POSSIBLE AUTO-EVALUATION CALCULATION

    IF (NRAUTOEV == 1) THEN
      ZTRP(JLON,KTDIA-1)=EXP(MAX(-ZEOS(JLON),ZARGLI))
    ENDIF

  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
  ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
  ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
  ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
  ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
  ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
  ZEOT(JLON)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)+ZWTH*(VNP(3,4)&
   &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
   &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
   &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
   &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
   &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
   &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
   &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
   &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
   &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

  ! GAS OVERLAP CORRECTION
  ZEOT(JLON)=ZEOT(JLON) &
   &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
   &/(ZRTC(JLON)+GOLCC(4))) &
   &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
   &/(ZRTO(JLON)+GOLCC(5))) &
   &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
   &/(ZRTO(JLON)+GOLCC(6)))

  ZEOTS(JLON)=ZEOT(JLON)

! EXTRA STORAGE FOR A POSSIBLE AUTO-EVALUATION CALCULATION

  IF (NRAUTOEV == 1.OR.LRTDL) THEN
    ZTRG(JLON,KTDIA-1)=EXP(MAX(-ZEOT(JLON),ZARGLI))
    ZTRT(JLON,KTDIA-1)=ZTRG(JLON,KTDIA-1)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.3   BOUCLE DESCENDANTE SUR LES NIVEAUX VERTICAUX.

!             DESCENDING VERTICAL LOOP ON LAYERS.

! - TEMPORAIRE(S) 1D .

! ZEOSO     : EPAISSEUR OPTIQUE SOLAIRE "OLD" (POUR CALCUL "NEW").
!            : "OLD" SOLAR OPTICAL DEPTH (FOR COMPUTING "NEW" ONE).
! ZEOTO     : EPAISSEUR OPTIQUE THERMIQUE "OLD" (POUR CALCUL "NEW").
!            : "OLD" THERMAL OPTICAL DEPTH (FOR COMPUTING "NEW" ONE).

DO JLEV=KTDIA,KLEV
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      ZEOSO(JLON)=ZEOS(JLON)
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZEOTO(JLON)=ZEOT(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZIRHOV=(PR(JLON,JLEV)*PT(JLON,JLEV))/PAPRSF(JLON,JLEV)
    ZQ=MAX(ZEPS2,PQ(JLON,JLEV))
    !ZRT=SQRT(PT(JLON,JLEV))
    ZRT=ZRT_VEC(JLON,JLEV)
    ZTI=1._JPRB/PT(JLON,JLEV)
    ZT2=PT(JLON,JLEV)*PT(JLON,JLEV)
    ZT2I=ZTI*ZTI
    ZT4I=ZT2I*ZT2I
    !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,JLEV))
    ZTDETA=ZTDETA_VEC(JLON,JLEV)
    ZT3=ZT2*PT(JLON,JLEV)
    ZDUH=(2._JPRB*PDELP(JLON,JLEV))*ZQ
    ZDUC=(2._JPRB*PDELP(JLON,JLEV))*PQCO2(JLON,JLEV)
    ZDUO=(2._JPRB*PDELP(JLON,JLEV))*PQO3(JLON,JLEV)
    ZNSH(JLON)=ZNSH(JLON)+ZDUH*ZDM0I(JLON)
    ZNSC(JLON)=ZNSC(JLON)+ZDUC*ZDM0I(JLON)
    ZNSO(JLON)=ZNSO(JLON)+ZDUO*ZDM0I(JLON)
    ZNTH(JLON)=ZNTH(JLON)+ZDUH*ZTI
    ZNTC(JLON)=ZNTC(JLON)+ZDUC
    ZNTO(JLON)=ZNTO(JLON)+ZDUO*ZT2
    ZDUH=ZDUH*(ZMD*PAPRSF(JLON,JLEV))*ZRT
    ZDUC=ZDUC*(ZMD*PAPRSF(JLON,JLEV))*PT(JLON,JLEV)
    ZDUO=ZDUO*(ZMD*PAPRSF(JLON,JLEV))*ZRT
    ZRSH(JLON)=ZRSH(JLON)+ZDUH*(ZDM0I(JLON)/ZMD)
    ZVSH(JLON)=ZVSH(JLON)+ZDUH*(ZDM0I(JLON)/ZMD)*ZIRHOV
    ZRSC(JLON)=ZRSC(JLON)+ZDUC*(ZDM0I(JLON)/ZMD)
    ZVSC(JLON)=ZVSC(JLON)+ZDUC*(ZDM0I(JLON)/ZMD)*ZIRHOV
    ZRSO(JLON)=ZRSO(JLON)+ZDUO*(ZDM0I(JLON)/ZMD)
    ZVSO(JLON)=ZVSO(JLON)+ZDUO*(ZDM0I(JLON)/ZMD)*ZIRHOV
    ZRTH(JLON)=ZRTH(JLON)+(ZDUH*ZT2I*ZRT)
    ZVTH(JLON)=ZVTH(JLON)+(ZDUH*ZT2I*ZRT)*ZIRHOV
    ZCTH(JLON)=ZCTH(JLON)+(ZDUH*ZT2I*ZRT)*ZT4I*(1._JPRB+GCD4*ZQ*ZTDETA)
    ZRTC(JLON)=ZRTC(JLON)+ZDUC*PT(JLON,JLEV)
    ZVTC(JLON)=ZVTC(JLON)+ZDUC*PT(JLON,JLEV)*ZIRHOV
    ZRTO(JLON)=ZRTO(JLON)+ZDUO*ZT3*ZRT
    ZVTO(JLON)=ZVTO(JLON)+ZDUO*ZT3*ZRT*ZIRHOV
  ENDDO
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      ZVOIGT=ZRHOZ0V(1)*ZVSH(JLON)/ZRSH(JLON)
      ZBZV=ZG4B(1)*ZNSH(JLON)/(ZRSH(JLON)/ZNSH(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSH=(ZGAS2B(1)*(ZRSH(JLON)/ZNSH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(1)*ZRSH(JLON)
      ZVOIGT=ZRHOZ0V(2)*ZVSC(JLON)/ZRSC(JLON)
      ZBZV=ZG4B(2)*ZNSC(JLON)/(ZRSC(JLON)/ZNSC(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSC=(ZGAS2B(2)*(ZRSC(JLON)/ZNSC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(2)*ZRSC(JLON)
      ZVOIGT=ZRHOZ0V(3)*ZVSO(JLON)/ZRSO(JLON)
      ZBZV=ZG4B(3)*ZNSO(JLON)/(ZRSO(JLON)/ZNSO(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSO=(ZGAS2B(3)*(ZRSO(JLON)/ZNSO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(3)*ZRSO(JLON)
      ZEOS(JLON)=(ZWSH*(1._JPRB+ZWSH*(VNP(1,1)+ZWSH*(VNP(2,1)&
       &+ZWSH*(VNP(3,1)&
       &+ZWSH*(VNP(4,1)+ZWSH*VNP(5,1)))))))/(1._JPRB+ZWSH*(VDP(1,1)&
       &+ZWSH*(VDP(2,1)+ZWSH*(VDP(3,1)+ZWSH*(VDP(4,1)+ZWSH&
       &*VDP(5,1))))))+(ZWSC*(1._JPRB+ZWSC*(VNP(1,2)+ZWSC*(VNP(2,2)&
       &+ZWSC*(VNP(3,2)+ZWSC*(VNP(4,2)+ZWSC*VNP(5,2)))))))/(1._JPRB&
       &+ZWSC*(VDP(1,2)+ZWSC*(VDP(2,2)+ZWSC*(VDP(3,2)+ZWSC&
       &*(VDP(4,2)+ZWSC*VDP(5,2))))))+(ZWSO*(1._JPRB+ZWSO*(VNP(1,3)&
       &+ZWSO*(VNP(2,3)+ZWSO*VNP(3,3)))))/(1._JPRB+ZWSO*(VDP(1,3)&
       &+ZWSO*(VDP(2,3)+ZWSO*VDP(3,3))))
      ZDEOS(JLON,JLEV)=(ZEOS(JLON)-ZEOSO(JLON))/ZDM0I(JLON)

      IF (NRAUTOEV == 1) THEN
        ZTRP(JLON,JLEV)=EXP(MAX(-ZEOS(JLON),ZARGLI))
      ENDIF

    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
    ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
    ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
    ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
    ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
    ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
    ZEOT(JLON)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)&
     &+ZWTH*(VNP(3,4)&
     &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
     &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
     &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
     &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
     &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
     &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
     &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
     &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
     &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

    ! GAS OVERLAP CORRECTION
    ZEOT(JLON)=ZEOT(JLON) &
     &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
     &/(ZRTC(JLON)+GOLCC(4))) &
     &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
     &/(ZRTO(JLON)+GOLCC(5))) &
     &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
     &/(ZRTO(JLON)+GOLCC(6)))
     
    ZDEOT(JLON,JLEV)=ZEOT(JLON)-ZEOTO(JLON)

! EXTRA STORAGE FOR A POSSIBLE AUTO-EVALUATION CALCULATION

    IF (NRAUTOEV == 1.OR.LRTDL) THEN
      ZTRG(JLON,JLEV)=EXP(MAX(-ZEOT(JLON),ZARGLI))
      ZTRT(JLON,JLEV)=ZTRG(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.4   CONDITION A LA SURFACE POUR LES CALCULS THERMIQUES.

!             SURFACE CONDITION FOR THE THERMAL CALCULATIONS.

DO JLON=KIDIA,KFDIA
  ZEOT(JLON)=0._JPRB
  ZNTH(JLON)=0._JPRB
  ZNTC(JLON)=0._JPRB
  ZNTO(JLON)=0._JPRB
  ZRTH(JLON)=0._JPRB
  ZVTH(JLON)=0._JPRB
  ZCTH(JLON)=0._JPRB
  ZRTC(JLON)=0._JPRB
  ZVTC(JLON)=0._JPRB
  ZRTO(JLON)=0._JPRB
  ZVTO(JLON)=0._JPRB
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.5   BOUCLE ASCENDANTE SUR LES NIVEAUX VERTICAUX.

!             ASCENDING VERTICAL LOOP ON LAYERS.

DO JLEV=KLEV,KTDIA,-1
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      ZEOSO(JLON)=ZEOS(JLON)
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZEOTO(JLON)=ZEOT(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZIRHOV=(PR(JLON,JLEV)*PT(JLON,JLEV))/PAPRSF(JLON,JLEV)
    ZQ=MAX(ZEPS2,PQ(JLON,JLEV))
    !ZRT=SQRT(PT(JLON,JLEV))
    ZRT=ZRT_VEC(JLON,JLEV)
    ZTI=1._JPRB/PT(JLON,JLEV)
    ZT2=PT(JLON,JLEV)*PT(JLON,JLEV)
    ZT2I=ZTI*ZTI
    ZT4I=ZT2I*ZT2I
    !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,JLEV))
    ZTDETA=ZTDETA_VEC(JLON,JLEV)
    ZT3=ZT2*PT(JLON,JLEV)
    ZDUH=(2._JPRB*PDELP(JLON,JLEV))*ZQ
    ZDUC=(2._JPRB*PDELP(JLON,JLEV))*PQCO2(JLON,JLEV)
    ZDUO=(2._JPRB*PDELP(JLON,JLEV))*PQO3(JLON,JLEV)
    ZNSH(JLON)=ZNSH(JLON)+ZDUH
    ZNSC(JLON)=ZNSC(JLON)+ZDUC
    ZNSO(JLON)=ZNSO(JLON)+ZDUO
    ZNTH(JLON)=ZNTH(JLON)+ZDUH*ZTI
    ZNTC(JLON)=ZNTC(JLON)+ZDUC
    ZNTO(JLON)=ZNTO(JLON)+ZDUO*ZT2
    ZDUH=ZDUH*(ZMD*PAPRSF(JLON,JLEV))*ZRT
    ZDUC=ZDUC*(ZMD*PAPRSF(JLON,JLEV))*PT(JLON,JLEV)
    ZDUO=ZDUO*(ZMD*PAPRSF(JLON,JLEV))*ZRT
    ZRSH(JLON)=ZRSH(JLON)+ZDUH
    ZVSH(JLON)=ZVSH(JLON)+ZDUH*ZIRHOV
    ZRSC(JLON)=ZRSC(JLON)+ZDUC
    ZVSC(JLON)=ZVSC(JLON)+ZDUC*ZIRHOV
    ZRSO(JLON)=ZRSO(JLON)+ZDUO
    ZVSO(JLON)=ZVSO(JLON)+ZDUO*ZIRHOV
    ZRTH(JLON)=ZRTH(JLON)+(ZDUH*ZT2I*ZRT)
    ZVTH(JLON)=ZVTH(JLON)+(ZDUH*ZT2I*ZRT)*ZIRHOV
    ZCTH(JLON)=ZCTH(JLON)+(ZDUH*ZT2I*ZRT)*ZT4I*(1._JPRB+GCD4*ZQ*ZTDETA)
    ZRTC(JLON)=ZRTC(JLON)+ZDUC*PT(JLON,JLEV)
    ZVTC(JLON)=ZVTC(JLON)+ZDUC*PT(JLON,JLEV)*ZIRHOV
    ZRTO(JLON)=ZRTO(JLON)+ZDUO*ZT3*ZRT
    ZVTO(JLON)=ZVTO(JLON)+ZDUO*ZT3*ZRT*ZIRHOV
  ENDDO
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      ZVOIGT=ZRHOZ0V(1)*ZVSH(JLON)/ZRSH(JLON)
      ZBZV=ZG4B(1)*ZNSH(JLON)/(ZRSH(JLON)/ZNSH(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSH=(ZGAS2B(1)*(ZRSH(JLON)/ZNSH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(1)*ZRSH(JLON)
      ZVOIGT=ZRHOZ0V(2)*ZVSC(JLON)/ZRSC(JLON)
      ZBZV=ZG4B(2)*ZNSC(JLON)/(ZRSC(JLON)/ZNSC(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSC=(ZGAS2B(2)*(ZRSC(JLON)/ZNSC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(2)*ZRSC(JLON)
      ZVOIGT=ZRHOZ0V(3)*ZVSO(JLON)/ZRSO(JLON)
      ZBZV=ZG4B(3)*ZNSO(JLON)/(ZRSO(JLON)/ZNSO(JLON))
      ZAFVOI=1._JPRB
      IF (LVOIGT) THEN
        IF (LVFULL) THEN
          ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
        ELSE
          ZVOIEMP=ZVOISIM
        ENDIF
        ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
         &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
         &*ZVOIEMP)
      ENDIF
      ZWSO=(ZGAS2B(3)*(ZRSO(JLON)/ZNSO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
       &*ZBZV)-1._JPRB)+GCC(3)*ZRSO(JLON)
      ZEOS(JLON)=(ZWSH*(1._JPRB+ZWSH*(VNP(1,1)+ZWSH*(VNP(2,1)&
       &+ZWSH*(VNP(3,1)&
       &+ZWSH*(VNP(4,1)+ZWSH*VNP(5,1)))))))/(1._JPRB+ZWSH*(VDP(1,1)&
       &+ZWSH*(VDP(2,1)+ZWSH*(VDP(3,1)+ZWSH*(VDP(4,1)+ZWSH&
       &*VDP(5,1))))))+(ZWSC*(1._JPRB+ZWSC*(VNP(1,2)+ZWSC*(VNP(2,2)&
       &+ZWSC*(VNP(3,2)+ZWSC*(VNP(4,2)+ZWSC*VNP(5,2)))))))/(1._JPRB&
       &+ZWSC*(VDP(1,2)+ZWSC*(VDP(2,2)+ZWSC*(VDP(3,2)+ZWSC&
       &*(VDP(4,2)+ZWSC*VDP(5,2))))))+(ZWSO*(1._JPRB+ZWSO*(VNP(1,3)&
       &+ZWSO*(VNP(2,3)+ZWSO*VNP(3,3)))))/(1._JPRB+ZWSO*(VDP(1,3)&
       &+ZWSO*(VDP(2,3)+ZWSO*VDP(3,3))))
      ZUEOS(JLON,JLEV)=ZEOS(JLON)-ZEOSO(JLON)
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
    ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
    ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
    ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
    ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
    ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
    ZAFVOI=1._JPRB
    IF (LVOIGT) THEN
      IF (LVFULL) THEN
        ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
      ELSE
        ZVOIEMP=ZVOISIM
      ENDIF
      ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
       &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
       &*ZVOIEMP)
    ENDIF
    ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
     &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
    ZEOT(JLON)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)&
     &+ZWTH*(VNP(3,4)&
     &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
     &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
     &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
     &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
     &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
     &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
     &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
     &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
     &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

    ! GAS OVERLAP CORRECTION
    ZEOT(JLON)=ZEOT(JLON) &
     &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
     &/(ZRTC(JLON)+GOLCC(4))) &
     &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
     &/(ZRTO(JLON)+GOLCC(5))) &
     &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
     &/(ZRTO(JLON)+GOLCC(6)))

    ZUEOT(JLON,JLEV)=MIN(ZEOT(JLON)-ZEOTO(JLON),ZDEOT(JLON,JLEV))
    ZUUEOT(JLON,JLEV)=ZEOT(JLON)-ZEOTO(JLON)

    IF (NRAUTOEV == 1) THEN
      ZTRW(JLON,JLEV)=EXP(MAX(-ZEOT(JLON),ZARGLI))
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     III.6   SOMMET DE L'ATMOSPHERE DU MODELE (OU VALEUR ARBITRAIREMENT
!     FAIBLE DE LA PRESSION) POUR LES CALCULS THERMIQUES D'ECHANGE ENTRE
!     COUCHES.

!             TOP OF THE MODEL'S ATMOSPHERE (OR ARBITRARILY SMALL
!     PRESSURE VALUE) FOR THERMAL "EXCHANGE BETWEEN LAYERS"
!     COMPUTATIONS.

! - TEMPORAIRE(S) 1D .

! ZUETS     : EPAISSEUR OPTIQUE THERMIQUE "UP" "AU DESSUS DU MODELE".
!            : "UPWARDS" THERMAL OPTICAL DEPTH ABOVE THE MODEL'S TOP.

DO JLON=KIDIA,KFDIA
  ZEOTO(JLON)=ZEOT(JLON)
ENDDO
DO JLON=KIDIA,KFDIA
  ZP=MAX(ZEPS1,PAPRS(JLON,KTDIA-1))
  ZIRHOV=(PR(JLON,KTDIA)*PT(JLON,KTDIA))/ZP
  ZQ=MAX(ZEPS2,PQ(JLON,KTDIA))
  !ZRT=SQRT(PT(JLON,KTDIA))
  ZRT=ZRT_VEC(JLON,KTDIA)
  ZTI=1._JPRB/PT(JLON,KTDIA)
  ZT2=PT(JLON,KTDIA)*PT(JLON,KTDIA)
  ZT2I=ZTI*ZTI
  ZT4I=ZT2I*ZT2I
  !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,KTDIA))
  ZTDETA=ZTDETA_VEC(JLON,KTDIA)
  ZT3=ZT2*PT(JLON,KTDIA)
  ZDUH=(2._JPRB*ZP)*ZQ
  ZDUC=(2._JPRB*ZP)*PQCO2(JLON,KTDIA)
  ZDUO=ZNSOR(JLON)
  ZNTH(JLON)=ZNTH(JLON)+ZDUH*ZTI
  ZNTC(JLON)=ZNTC(JLON)+ZDUC
  ZNTO(JLON)=ZNTO(JLON)+ZDUO*ZT2
  ZDUH=ZDUH*(ZMD*0.5_JPRB*ZP)*ZRT
  ZDUC=ZDUC*(ZMD*0.5_JPRB*ZP)*PT(JLON,KTDIA)
  ZDUO=ZDUO*(ZMD*0.5_JPRB*ZP)*ZRT
  ZRTH(JLON)=ZRTH(JLON)+(ZDUH*ZT2I*ZRT)
  ZVTH(JLON)=ZVTH(JLON)+(ZDUH*ZT2I*ZRT)*ZIRHOV
  ZCTH(JLON)=ZCTH(JLON)+(ZDUH*ZT2I*ZRT)*ZT4I*(1._JPRB+GCD4*ZQ*ZTDETA)
  ZRTC(JLON)=ZRTC(JLON)+ZDUC*PT(JLON,KTDIA)
  ZVTC(JLON)=ZVTC(JLON)+ZDUC*PT(JLON,KTDIA)*ZIRHOV
  ZRTO(JLON)=ZRTO(JLON)+ZDUO*ZT3*ZRT
  ZVTO(JLON)=ZVTO(JLON)+ZDUO*ZT3*ZRT*ZIRHOV
  ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
  ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
  ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
  ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
  ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
  ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
  ZAFVOI=1._JPRB
  IF (LVOIGT) THEN
    IF (LVFULL) THEN
      ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
    ELSE
      ZVOIEMP=ZVOISIM
    ENDIF
    ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
     &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
     &*ZVOIEMP)
  ENDIF
  ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
   &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
  ZEOT(JLON)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)+ZWTH*(VNP(3,4)&
   &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
   &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
   &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
   &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
   &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
   &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
   &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
   &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
   &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

  ! GAS OVERLAP CORRECTION
  ZEOT(JLON)=ZEOT(JLON) &
   &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
   &/(ZRTC(JLON)+GOLCC(4))) &
   &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
   &/(ZRTO(JLON)+GOLCC(5))) &
   &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
   &/(ZRTO(JLON)+GOLCC(6)))

  ZUETS(JLON)=ZEOT(JLON)-ZEOTO(JLON)

  IF (NRAUTOEV == 1) THEN
    ZTRW(JLON,KTDIA-1)=EXP(MAX(-ZEOT(JLON),ZARGLI))
  ENDIF

ENDDO

! ADDITIONAL DOUBLE VERTICAL LOOPS FOR THE EXACT AUTO-EVALUATION OF NET
! EXCHANGED POWER RATES IN A CASE WITH GASEOUS ABSORPTION ONLY

IF (NRAUTOEV == 1.OR.LRTDL) THEN

  DO JLEVE=KTDIA,KLEV

    ILEVL=JLEVE
    IF (NRAUTOEV == 1) THEN
      ILEVL=KLEV
    ENDIF

    DO JLEV=JLEVE-1,ILEVL
      DO JLON=KIDIA,KFDIA
        ZTRGO(JLON,JLEV)=ZTRG(JLON,JLEV)
      ENDDO
    ENDDO

    DO JLON=KIDIA,KFDIA
      ZTRG(JLON,JLEVE-1)=1._JPRB
      ZNTH(JLON)=0._JPRB
      ZNTC(JLON)=0._JPRB
      ZNTO(JLON)=0._JPRB
      ZRTH(JLON)=0._JPRB
      ZVTH(JLON)=0._JPRB
      ZCTH(JLON)=0._JPRB
      ZRTC(JLON)=0._JPRB
      ZVTC(JLON)=0._JPRB
      ZRTO(JLON)=0._JPRB
      ZVTO(JLON)=0._JPRB
    ENDDO

    ILEVL=MIN(KLEV,JLEVE+1)
    IF (NRAUTOEV == 1) THEN
      ILEVL=KLEV
    ENDIF

    DO JLEV=JLEVE,ILEVL
      LLILEV=(JLEV==JLEVE)
      DO JLON=KIDIA,KFDIA
        ZIRHOV=(PR(JLON,JLEV)*PT(JLON,JLEV))/PAPRSF(JLON,JLEV)
        ZQ=MAX(ZEPS2,PQ(JLON,JLEV))
        !ZRT=SQRT(PT(JLON,JLEV))
        ZRT=ZRT_VEC(JLON,JLEV)
        ZTI=1._JPRB/PT(JLON,JLEV)
        ZT2=PT(JLON,JLEV)*PT(JLON,JLEV)
        ZT2I=ZTI*ZTI
        ZT4I=ZT2I*ZT2I
        !ZTDETA=ZRT**49*EXP(GCE4/PT(JLON,JLEV))
        ZTDETA=ZTDETA_VEC(JLON,JLEV)
        ZT3=ZT2*PT(JLON,JLEV)
        ZDUH=(2._JPRB*PDELP(JLON,JLEV))*ZQ
        ZDUC=(2._JPRB*PDELP(JLON,JLEV))*PQCO2(JLON,JLEV)
        ZDUO=(2._JPRB*PDELP(JLON,JLEV))*PQO3(JLON,JLEV)
        ZNTH(JLON)=ZNTH(JLON)+ZDUH*ZTI
        ZNTC(JLON)=ZNTC(JLON)+ZDUC
        ZNTO(JLON)=ZNTO(JLON)+ZDUO*ZT2
        ZDUH=ZDUH*(ZMD*PAPRSF(JLON,JLEV))*ZRT
        ZDUC=ZDUC*(ZMD*PAPRSF(JLON,JLEV))*PT(JLON,JLEV)
        ZDUO=ZDUO*(ZMD*PAPRSF(JLON,JLEV))*ZRT
        ZRTH(JLON)=ZRTH(JLON)+(ZDUH*ZT2I*ZRT)
        ZVTH(JLON)=ZVTH(JLON)+(ZDUH*ZT2I*ZRT)*ZIRHOV
        ZCTH(JLON)=ZCTH(JLON)+(ZDUH*ZT2I*ZRT)*ZT4I*(1._JPRB+GCD4*ZQ&
         &*ZTDETA)
        ZRTC(JLON)=ZRTC(JLON)+ZDUC*PT(JLON,JLEV)
        ZVTC(JLON)=ZVTC(JLON)+ZDUC*PT(JLON,JLEV)*ZIRHOV
        ZRTO(JLON)=ZRTO(JLON)+ZDUO*ZT3*ZRT
        ZVTO(JLON)=ZVTO(JLON)+ZDUO*ZT3*ZRT*ZIRHOV
        ZVOIGT=ZRHOZ0V(4)*ZVTH(JLON)/ZRTH(JLON)
        ZBZV=ZG4B(4)*ZNTH(JLON)/(ZRTH(JLON)/ZNTH(JLON))
        ZAFVOI=1._JPRB
        IF (LVOIGT) THEN
          IF (LVFULL) THEN
            ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
          ELSE
            ZVOIEMP=ZVOISIM
          ENDIF
          ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
           &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
           &*ZVOIEMP)
        ENDIF
        ZWTH=(ZGAS2B(4)*(ZRTH(JLON)/ZNTH(JLON)))*(SQRT(1._JPRB+ZAFVOI&
         &*ZBZV)-1._JPRB)+GCC(4)*ZCTH(JLON)
        ZVOIGT=ZRHOZ0V(5)*ZVTC(JLON)/ZRTC(JLON)
        ZBZV=ZG4B(5)*ZNTC(JLON)/(ZRTC(JLON)/ZNTC(JLON))
        ZAFVOI=1._JPRB
        IF (LVOIGT) THEN
          IF (LVFULL) THEN
            ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
          ELSE
            ZVOIEMP=ZVOISIM
          ENDIF
          ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
           &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
           &*ZVOIEMP)
        ENDIF
        ZWTC=(ZGAS2B(5)*(ZRTC(JLON)/ZNTC(JLON)))*(SQRT(1._JPRB+ZAFVOI&
         &*ZBZV)-1._JPRB)+GCC(5)*ZRTC(JLON)
        ZVOIGT=ZRHOZ0V(6)*ZVTO(JLON)/ZRTO(JLON)
        ZBZV=ZG4B(6)*ZNTO(JLON)/(ZRTO(JLON)/ZNTO(JLON))
        ZAFVOI=1._JPRB
        IF (LVOIGT) THEN
          IF (LVFULL) THEN
            ZVOIEMP=EXP(ZEPSV*LOG(ZIBV0*(ZBZV/ZVOIGT))*LOG(ZIZV0*ZVOIGT))
          ELSE
            ZVOIEMP=ZVOISIM
          ENDIF
          ZAFVOI=1._JPRB+ZVOIGT/(RPI*(ZBZV/ZVOIGT)/(1.5_JPRB*SQRT(ZBZV))&
           &+4._JPRB/(ZBZV/ZVOIGT)+5._JPRB+ZGAMV*SQRT(ZBZV/ZVOIGT)&
           &*ZVOIEMP)
        ENDIF
        ZWTO=(ZGAS2B(6)*(ZRTO(JLON)/ZNTO(JLON)))*(SQRT(1._JPRB+ZAFVOI&
         &*ZBZV)-1._JPRB)+GCC(6)*ZRTO(JLON)
        ZEOT(JLON)=(ZWTH*(1._JPRB+ZWTH*(VNP(1,4)+ZWTH*(VNP(2,4)&
         &+ZWTH*(VNP(3,4)&
         &+ZWTH*(VNP(4,4)+ZWTH*VNP(5,4)))))))/(1._JPRB+ZWTH*(VDP(1,4)&
         &+ZWTH*(VDP(2,4)+ZWTH*(VDP(3,4)+ZWTH*(VDP(4,4)+ZWTH&
         &*VDP(5,4))))))+(ZWTC*(1._JPRB+ZWTC*(VNP(1,5)+ZWTC*(VNP(2,5)&
         &+ZWTC*(VNP(3,5)+ZWTC*(VNP(4,5)+ZWTC*VNP(5,5)))))))/(1._JPRB&
         &+ZWTC*(VDP(1,5)+ZWTC*(VDP(2,5)+ZWTC*(VDP(3,5)+ZWTC&
         &*(VDP(4,5)+ZWTC*VDP(5,5))))))+(ZWTO*(1._JPRB+ZWTO*(VNP(1,6)&
         &+ZWTO*(VNP(2,6)+ZWTO*(VNP(3,6)+ZWTO*(VNP(4,6)+ZWTO&
         &*VNP(5,6)))))))/(1._JPRB+ZWTO*(VDP(1,6)+ZWTO*(VDP(2,6)+ZWTO&
         &*(VDP(3,6)+ZWTO*(VDP(4,6)+ZWTO*VDP(5,6))))))

        ! GAS OVERLAP CORRECTION
        ZEOT(JLON)=ZEOT(JLON) &
         &+GOLCA(4)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(4))*ZRTC(JLON) &
         &/(ZRTC(JLON)+GOLCC(4))) &
         &+GOLCA(5)*SQRT(ZRTH(JLON)/(ZRTH(JLON)+GOLCB(5))*ZRTO(JLON) &
         &/(ZRTO(JLON)+GOLCC(5))) &
         &+GOLCA(6)*SQRT(ZRTC(JLON)/(ZRTC(JLON)+GOLCB(6))*ZRTO(JLON) &
         &/(ZRTO(JLON)+GOLCC(6)))

        ZTRG(JLON,JLEV)=EXP(MAX(-ZEOT(JLON),ZARGLI))

        IF (LLILEV) THEN
          ZEOLT(JLON,JLEV)=ZEOT(JLON)
        ENDIF

      ENDDO
    ENDDO

    ILEVL=JLEVE
    IF (NRAUTOEV == 1) THEN
      ILEVL=KLEV
    ENDIF

    DO JLEV=JLEVE,ILEVL
      DO JLON=KIDIA,KFDIA
        ZPNER(JLON,JLEV,JLEVE-1)=MAX(ZTRG(JLON,JLEV-1)-ZTRG(JLON,JLEV)&
         &-ZTRGO(JLON,JLEV-1)+ZTRGO(JLON,JLEV),0._JPRB)
      ENDDO
    ENDDO

    IF (LRTDL) THEN
      DO JLON=KIDIA,KFDIA
        ZRPROX(JLON,JLEVE-1)=ZTRGO(JLON,JLEVE)/MAX(ZTRG(JLON,JLEVE)&
         &*ZTRGO(JLON,JLEVE-1),ZTRLI)
      ENDDO
    ENDIF

  ENDDO

ENDIF

!*
!     ------------------------------------------------------------------
!     IV - CALCUL DES ELEMENTS DE GEOMETRIE NUAGEUSE. LES TERMES ZB
!     SERVENT DE STOCKAGE TEMPORAIRE POUR LES "ALPHA", "BETA", "GAMMA"
!     ET "DELTA" POUR LES CALCULS DE RECOUVREMENT MAXIMUM OU (POUR ZB1
!     SEUL) POUR LE COMPLEMENT A UN DE LA NEBULOSITE DANS LE CAS DE
!     RECOUVREMENT AU HASARD.

!          COMPUTATION OF THE CLOUD GEOMETRY ELEMENTS. THE TERMS ZB ARE
!     USED AS TEMPORARY STORAGE FOR THE "ALPHA", "BETA", "GAMMA" AND
!     "DELTA" COEFFICIENTS OF THE MAXIMUM OVERLAP CALCULATIONS OR (IN
!     THAT CASE ZB1 ALONE) FOR THE COMPLEMENT TO ONE OF CLOUDINESS IN
!     THE RANDOM OVERLAP CALCULATIONS.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZB1       : PREMIER STOCKAGE INTERMEDIAIRE DE GEOMETRIE NUAGEUSE.
!            : FIRST INTERMEDIATE STORAGE FOR CLOUD GEOMETRY.
! ZB2       : SECOND STOCKAGE INTERMEDIAIRE DE GEOMETRIE NUAGEUSE.
!            : SECOND INTERMEDIATE STORAGE FOR CLOUD GEOMETRY.
! ZB3       : TROISIEME STOCKAGE INTERMEDIAIRE DE GEOMETRIE NUAGEUSE.
!            : THIRD INTERMEDIATE STORAGE FOR CLOUD GEOMETRY.
! ZB4       : QUATRIEME STOCKAGE INTERMEDIAIRE DE GEOMETRIE NUAGEUSE.
!            : FOURTH INTERMEDIATE STORAGE FOR CLOUD GEOMETRY.

!     CALCUL OPTIONNEL (EN VERSION RECOUVREMENT MAXIMUM DES PARTIES
!     NUAGEUSES ADJACENTES) DES COEFFICIENTS MATRICIELS DE TRANSFERT
!     ENTRE PARTIES CLAIRES ET COUVERTES DE DEUX COUCHES. DANS LE CAS
!     CONTRAIRE (RECOUVREMENT ALEATOIRE DE TOUTES LES FRACTIONS
!     NUAGEUSES) ON NE CALCULE QUE LE COMPLEMENT A UN DE LA NEBULOSITE.
!     OPTIONAL CALCULATION (IN MAXIMUM OVERLAP VERSION FOR THE ADJACENT
!     CLOUDY PARTS) OF THE MATRIX COEFFICIENT FOR TRANSFER BETWEEN CLEAR
!     AND CLOUDY PARTS OF BOTH LAYERS. IN THE OPPOSITE CASE (RANDOM
!     OVERLAP OF ALL CLOUD FRACTIONS) ONE COMPUTES ONLY THE COMPLEMENT
!     TO ONE OF CLOUDINESS.

! - TEMPORAIRE(S) 1D .

! ZNMXB     : NEBULOSITE MAXIMUM ADJACENTE "VERS LE BAS".
!            : MAXIMUM ADJACENT CLOUDINESS "DOWNWARDS".
! ZNMNB     : NEBULOSITE MINIMUM ADJACENTE "VERS LE BAS".
!            : MINIMUM ADJACENT CLOUDINESS "DOWNWARDS".
! ZNMXH     : NEBULOSITE MAXIMUM ADJACENTE "VERS LE HAUT".
!            : MAXIMUM ADJACENT CLOUDINESS "UPWARDS".
! ZNMNH     : NEBULOSITE MINIMUM ADJACENTE "VERS LE HAUT".
!            : MINIMUM ADJACENT CLOUDINESS "UPWARDS".

IF (LRNUMX) THEN
  DO JLON=KIDIA,KFDIA
    ZB1(JLON,KTDIA)=1._JPRB-PNEB(JLON,KTDIA)
    ZB3(JLON,KTDIA)=1._JPRB
    ZNMXB(JLON)=MAX(PNEB(JLON,KTDIA),PNEB(JLON,KTDIA+1))
    ZNMNB(JLON)=MIN(PNEB(JLON,KTDIA),PNEB(JLON,KTDIA+1))
    ZB1(JLON,KTDIA+1)=(1._JPRB-ZNMXB(JLON))/(1._JPRB-PNEB(JLON,KTDIA))
    ZB3(JLON,KTDIA+1)=ZNMNB(JLON)/PNEB(JLON,KTDIA)
  ENDDO
!cdir unroll=8
  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZNMXH(JLON)=ZNMXB(JLON)
      ZNMNH(JLON)=ZNMNB(JLON)
    ENDDO
    DO JLON=KIDIA,KFDIA
      ZNMXB(JLON)=MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))
      ZNMNB(JLON)=MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))
      ZB1(JLON,JLEV+1)=(1._JPRB-ZNMXB(JLON))*(1._JPRB/(1._JPRB-PNEB(JLON,JLEV)))
      ZB2(JLON,JLEV-1)=(1._JPRB-ZNMXH(JLON))*(1._JPRB/(1._JPRB-PNEB(JLON,JLEV)))
      ZB3(JLON,JLEV+1)=ZNMNB(JLON)*(1._JPRB/PNEB(JLON,JLEV))
      ZB4(JLON,JLEV-1)=ZNMNH(JLON)*(1._JPRB/PNEB(JLON,JLEV))
    ENDDO
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZNMXH(JLON)=ZNMXB(JLON)
    ZNMNH(JLON)=ZNMNB(JLON)
    ZB2(JLON,KLEV-1)=(1._JPRB-ZNMXH(JLON))/(1._JPRB-PNEB(JLON,KLEV))
    ZB4(JLON,KLEV-1)=ZNMNH(JLON)/PNEB(JLON,KLEV)
    ZB2(JLON,KLEV)=1._JPRB
    ZB4(JLON,KLEV)=1._JPRB
  ENDDO
ELSE
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZB1(JLON,JLEV)=1._JPRB-PNEB(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!*
!     ------------------------------------------------------------------
!     V - CALCUL DES FLUX DE CORPS NOIR AU SOMMET DES COUCHES SUPPOSEES
!     ISOTHERMES ET DE L'EMISSIVITE EFFECTIVE DE SURFACE. LES FLUX DE
!     CORPS NOIR SONT DANS UN TABLEAU (0:KLEV) CAR LE SOL EST CONSIDERE
!     COMME UNE COUCHE D'EPAISSEUR OPTIQUE INFINIE.

!         COMPUTATION OF THE BLACK-BODY FLUXES AT THE TOP OF LAYERS
!     (ASSUMED ISOTHERMAL) AND OF THE EFFECTIVE SURFACE EMISSIVITY.
!     BLACK-BODY FLUXES ARE IN A (0:KLEV) ARRAY SINCE THE SOIL IS
!     CONSIDERED TO BE A LAYER OF INFINITE OPTICAL DEPTH.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZBB       : FLUX DE CORPS NOIR AU SOMMET DES COUCHES.
!            : BLACK-BODY FLUX AT THE TOP OF THE LAYERS.

!     FLUX DE CORPS NOIR.
!     BLACK-BODY FLUX.

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZBB(JLON,JLEV-1)=RSIGMA*(PT(JLON,JLEV)*PT(JLON,JLEV))&
     &*(PT(JLON,JLEV)*PT(JLON,JLEV))

!   ATMOSPHERIC-ONLY DAMPING COEFFICIENTS FOR THE TIME-SECURE HANDLING
!   OF FLUX DIVERGENCES

    ZDAMP(JLON,JLEV)=ZSECUR*PT(JLON,JLEV)*(PT(JLON,JLEV)*PT(JLON,JLEV))&
     &/(PDELP(JLON,JLEV)*PCP(JLON,JLEV))
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  ZBB(JLON,KLEV)=RSIGMA*(PTS(JLON)*PTS(JLON))*(PTS(JLON)*PTS(JLON))
ENDDO

IF (LRMIX.AND.NRAUTOEV == 0) THEN

!cdir unroll=8
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZMIXP(JLON,JLEV)=MIN(1._JPRB,PMAN(JLEV)+PMAK(JLEV)&
       &*MAX(0._JPRB,(PCP(JLON,JLEV+1)*PT(JLON,JLEV+1)&
       &*(PAPRS(JLON,KLEV)/PAPRSF(JLON,JLEV+1))**RKAPPA&
       &-PCP(JLON,JLEV)*PT(JLON,JLEV)*(PAPRS(JLON,KLEV)&
       &/PAPRSF(JLON,JLEV))**RKAPPA)/(PAPHIF(JLON,JLEV+1)&
       &-PAPHIF(JLON,JLEV))))
      ZFLUXC(JLON,JLEV)=0._JPRB
    ENDDO
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZMIXP(JLON,KTDIA-1)=ZMIXP(JLON,KTDIA)
    ZFLUXC(JLON,KTDIA-1)=0._JPRB
    ZMIXP(JLON,KLEV)=ZMIXP(JLON,KLEV-1)
    ZFLUXC(JLON,KLEV)=0._JPRB
  ENDDO

ELSE

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZMIXP(JLON,JLEV)=0._JPRB
      ZFLUXC(JLON,JLEV)=0._JPRB
    ENDDO
  ENDDO

ENDIF

! AUTO-EVALUATION COMPUTATIONS OF DIFFERENT FLUXES IN NO-SCATTERING CASE
! IN PRINCIPLE THIS SHOULD ONLY BE USED TOGETHER WITH LREWS=.T.

IF (NRAUTOEV == 1) THEN

! EXACT AUTO COMPUTATION OF EBL-FLUXES FOR HALF LEVELS FROM ZERO TO KLEV

  DO JLON=KIDIA,KFDIA
    ZFLUXR(JLON,KTDIA-1)=0._JPRB
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZFLUXR(JLON,KTDIA-1)=ZFLUXR(JLON,KTDIA-1)+(ZBB(JLON,KTDIA-1)&
       &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,KTDIA-1)
    ENDDO
  ENDDO

  DO JLEVE=KTDIA,KLEV

    DO JLON=KIDIA,KFDIA
      ZFLUXR(JLON,JLEVE)=ZFLUXR(JLON,JLEVE-1)+(ZBB(JLON,JLEVE-1)&
       &-ZBB(JLON,KTDIA-1))*ZPNER(JLON,JLEVE,KTDIA-1)
    ENDDO

    IF (JLEVE /= KTDIA) THEN
      DO JLEV=KTDIA,JLEVE-1
        DO JLON=KIDIA,KFDIA
          ZFLUXR(JLON,JLEVE)=ZFLUXR(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEVE,JLEV)
        ENDDO
      ENDDO
    ENDIF

    IF (JLEVE /= KLEV) THEN
      DO JLEV=JLEVE+1,KLEV
        DO JLON=KIDIA,KFDIA
          ZFLUXR(JLON,JLEVE)=ZFLUXR(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,JLEVE)
        ENDDO
      ENDDO
    ENDIF

  ENDDO

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZFLUXT(JLON,JLEV)=ZFLUXR(JLON,JLEV)
    ENDDO
  ENDDO

  IF (LRPROX) THEN
    DO JLEV=KTDIA,KLEV-1
      DO JLON=KIDIA,KFDIA
        ZFLUXR(JLON,JLEV)=ZFLUXR(JLON,JLEV)-(ZBB(JLON,JLEV-1)&
         &-ZBB(JLON,JLEV))*ZPNER(JLON,JLEV+1,JLEV)
      ENDDO
    ENDDO
  ENDIF

  DO JLON=KIDIA,KFDIA
    ZFLUX1(JLON)=-ZBB(JLON,KLEV)*ZTRT(JLON,KLEV)
    ZFLUXT(JLON,KLEV)=ZFLUXT(JLON,KLEV)+ZFLUX1(JLON)
  ENDDO

  DO JLEV=KLEV-1,KTDIA-1,-1
    DO JLON=KIDIA,KFDIA
      ZFLUX1(JLON)=ZFLUX1(JLON)+ZBB(JLON,JLEV)*(ZTRT(JLON,JLEV+1)&
       & -ZTRT(JLON,JLEV))
      ZFLUXT(JLON,JLEV)=ZFLUXT(JLON,JLEV)+ZFLUX1(JLON)
    ENDDO
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZFLUX1(JLON)=(ZBB(JLON,KTDIA-1)-ZBB(JLON,KLEV))*(ZTRW(JLON,KTDIA)&
     & -ZTRW(JLON,KTDIA-1))
    ZFLUXT(JLON,KTDIA-1)=ZFLUXT(JLON,KTDIA-1)+ZFLUX1(JLON)
  ENDDO

  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFLUX1(JLON)=ZFLUX1(JLON)+(ZBB(JLON,JLEV-1)-ZBB(JLON,KLEV))&
       &*(ZTRW(JLON,JLEV+1)-ZTRW(JLON,JLEV))
      ZFLUXT(JLON,JLEV)=ZFLUXT(JLON,JLEV)+ZFLUX1(JLON)
    ENDDO
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZFLUX1(JLON)=ZFLUX1(JLON)+(ZBB(JLON,KLEV-1)-ZBB(JLON,KLEV))&
     &*(1._JPRB-ZTRW(JLON,KLEV))
    ZFLUXT(JLON,KLEV)=ZFLUXT(JLON,KLEV)+ZFLUX1(JLON)
  ENDDO

! AUTO COMPUTATION OF EBL-FLUXES FOR 'GREY' MINIMUM OPTICAL DEPTHS

! LOCAL TRANSMISSIVITIES

  DO JLON=KIDIA,KFDIA
    ZTRMUL(JLON,KTDIA-1)=EXP(MAX(-ZUETS(JLON),ZARGLI))
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTRMUL(JLON,JLEV)=EXP(MAX(-ZUEOT(JLON,JLEV),ZARGLI))
    ENDDO
  ENDDO

! NET EXCHANGED POWER RATES

  DO JLEVE=KTDIA-1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZPNER(JLON,JLEVE+1,JLEVE)=1._JPRB
    ENDDO

    IF (JLEVE /= KLEV-1) THEN
      DO JLEV=JLEVE+2,KLEV
        DO JLON=KIDIA,KFDIA
          ZPNER(JLON,JLEV,JLEVE)=ZPNER(JLON,JLEV-1,JLEVE)&
           &*ZTRMUL(JLON,JLEV-1)
        ENDDO
      ENDDO
    ENDIF

    IF (LRPROX) THEN
      DO JLON=KIDIA,KFDIA
        ZPNER(JLON,JLEVE+1,JLEVE)=0._JPRB
      ENDDO
    ENDIF

  ENDDO

  DO JLEVE=KTDIA-1,KLEV-1
    DO JLEV=JLEVE+1,KLEV
      DO JLON=KIDIA,KFDIA
        ZPNER(JLON,JLEV,JLEVE)=ZPNER(JLON,JLEV,JLEVE)*(1._JPRB&
         &-ZTRMUL(JLON,JLEV))*(1._JPRB-ZTRMUL(JLON,JLEVE))
      ENDDO
    ENDDO
  ENDDO

! EBL FLUX COMPUTATION ITSELF

  DO JLON=KIDIA,KFDIA
    ZFLUXD(JLON,KTDIA-1)=0._JPRB
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZFLUXD(JLON,KTDIA-1)=ZFLUXD(JLON,KTDIA-1)+(ZBB(JLON,KTDIA-1)&
       &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,KTDIA-1)
    ENDDO
  ENDDO

  DO JLEVE=KTDIA,KLEV

    DO JLON=KIDIA,KFDIA
      ZFLUXD(JLON,JLEVE)=ZFLUXD(JLON,JLEVE-1)+(ZBB(JLON,JLEVE-1)&
       &-ZBB(JLON,KTDIA-1))*ZPNER(JLON,JLEVE,KTDIA-1)
    ENDDO

    IF (JLEVE /= KTDIA) THEN
      DO JLEV=KTDIA,JLEVE-1
        DO JLON=KIDIA,KFDIA
          ZFLUXD(JLON,JLEVE)=ZFLUXD(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEVE,JLEV)
        ENDDO
      ENDDO
    ENDIF

    IF (JLEVE /= KLEV) THEN
      DO JLEV=JLEVE+1,KLEV
        DO JLON=KIDIA,KFDIA
          ZFLUXD(JLON,JLEVE)=ZFLUXD(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,JLEVE)
        ENDDO
      ENDDO
    ENDIF

  ENDDO

! AUTO COMPUTATION OF EBL-FLUXES FOR 'GREY' MAXIMUM OPTICAL DEPTHS

! LOCAL TRANSMISSIVITIES

  DO JLON=KIDIA,KFDIA
    ZTRMUL(JLON,KTDIA-1)=EXP(MAX(-ZEOTS(JLON),ZARGLI))
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZTRMUL(JLON,JLEV)=EXP(MAX(-ZEOLT(JLON,JLEV),ZARGLI))
    ENDDO
  ENDDO

! NET EXCHANGED POWER RATES

  DO JLEVE=KTDIA-1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZPNER(JLON,JLEVE+1,JLEVE)=1._JPRB
    ENDDO

    IF (JLEVE /= KLEV-1) THEN
      DO JLEV=JLEVE+2,KLEV
        DO JLON=KIDIA,KFDIA
          ZPNER(JLON,JLEV,JLEVE)=ZPNER(JLON,JLEV-1,JLEVE)&
           &*ZTRMUL(JLON,JLEV-1)
        ENDDO
      ENDDO
    ENDIF

    IF (LRPROX) THEN
      DO JLON=KIDIA,KFDIA
        ZPNER(JLON,JLEVE+1,JLEVE)=0._JPRB
      ENDDO
    ENDIF

  ENDDO

  DO JLEVE=KTDIA-1,KLEV-1
    DO JLEV=JLEVE+1,KLEV
      DO JLON=KIDIA,KFDIA
        ZPNER(JLON,JLEV,JLEVE)=ZPNER(JLON,JLEV,JLEVE)*(1._JPRB&
         &-ZTRMUL(JLON,JLEV))*(1._JPRB-ZTRMUL(JLON,JLEVE))
      ENDDO
    ENDDO
  ENDDO

! EBL FLUX COMPUTATION ITSELF

  DO JLON=KIDIA,KFDIA
    ZFLUXL(JLON,KTDIA-1)=0._JPRB
  ENDDO

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZFLUXL(JLON,KTDIA-1)=ZFLUXL(JLON,KTDIA-1)+(ZBB(JLON,KTDIA-1)&
       &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,KTDIA-1)
    ENDDO
  ENDDO

  DO JLEVE=KTDIA,KLEV

    DO JLON=KIDIA,KFDIA
      ZFLUXL(JLON,JLEVE)=ZFLUXL(JLON,JLEVE-1)+(ZBB(JLON,JLEVE-1)&
       &-ZBB(JLON,KTDIA-1))*ZPNER(JLON,JLEVE,KTDIA-1)
    ENDDO

    IF (JLEVE /= KTDIA) THEN
      DO JLEV=KTDIA,JLEVE-1
        DO JLON=KIDIA,KFDIA
          ZFLUXL(JLON,JLEVE)=ZFLUXL(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEVE,JLEV)
        ENDDO
      ENDDO
    ENDIF

    IF (JLEVE /= KLEV) THEN
      DO JLEV=JLEVE+1,KLEV
        DO JLON=KIDIA,KFDIA
          ZFLUXL(JLON,JLEVE)=ZFLUXL(JLON,JLEVE)+(ZBB(JLON,JLEVE-1)&
           &-ZBB(JLON,JLEV-1))*ZPNER(JLON,JLEV,JLEVE)
        ENDDO
      ENDDO
    ENDIF

  ENDDO

  DO JLEV=KTDIA-1,KLEV
    DO JLON=KIDIA,KFDIA
      ZDEBL=ZFLUXL(JLON,JLEV)-ZFLUXD(JLON,JLEV)
      ZW   =RMIXD*RMIXD/(RMIXD*RMIXD+ZDEBL*ZDEBL)
      ZMIXP(JLON,JLEV)=MAX(0._JPRB,MIN(1._JPRB,                    &
       & ((1._JPRB-ZW)*ZFLUXR(JLON,JLEV)+                          &
       & ZW*(ZFLUXD(JLON,JLEV)+RMIXP0*(ZFLUXL(JLON,JLEV)-          &
       & ZFLUXD(JLON,JLEV)))-ZFLUXD(JLON,JLEV))/                   &
       & SIGN(MAX(ABS(ZFLUXL(JLON,JLEV)-ZFLUXD(JLON,JLEV)),ZEPS1), &
       & ZFLUXL(JLON,JLEV)-ZFLUXD(JLON,JLEV))))
      ZFLUXC(JLON,JLEV)=ZFLUXR(JLON,JLEV)-(ZFLUXD(JLON,JLEV)&
       &+ZMIXP(JLON,JLEV)*(ZFLUXL(JLON,JLEV)-ZFLUXD(JLON,JLEV)))
    ENDDO
  ENDDO

ENDIF

!*
!     ------------------------------------------------------------------
!     VI - CALCUL DES COMPLEMENTS A UN DES FLUX THERMIQUES EN ATMOSPHERE
!     ISOTHERME NORMALISEE PAR ELIMINATION/SUBSTITUTION PUIS UTILISATION
!     DES RESULTATS POUR LES CALCULS DE FLUX. DANS TOUTE LA SUITE LES
!     QUANTITES ZF (OU ZZF DANS LE CAS THERMIQUE AVEC TEMPERATURES
!     REELLES) DESIGNERONT LES SECONDS MEMBRES DES SYSTEMES LINEAIRES
!     "ADDING METHOD" ET LES QUANTITES ZTU LES ELEMENTS SURDIAGONAUX A
!     UTILISER DANS LA PARTIE SUBSTITUTION DE LA SOLUTION. IL FAUT
!     EGALEMENT SIGNALER QUE DANS LE CAS "RECOUVREMENT AU HASARD" LE
!     SUFFIXE "C" (CLAIR) REPRESENTE TOUTE LA COUCHE ET QUE LE SUFFIXE
!     "N" (NUAGEUX) REPRESENTE DES TABLEAUX INUTILISES.

!          COMPUTATION OF THE COMPLEMENTS TO ONE OF THE THERMAL FLUXES
!     IN A NORMALIZED ISOTHERMAL ATMOSPHERE VIA ELIMINATION/BACK-
!     SUBSTITUTION AND USE OF THE RESULT TO COMPUTE FLUXES. IN ALL THE
!     FOLLOWING THE QUANTITIES ZF (OR ZZF IN THE THERMAL CASE WITH
!     REAL TEMPERATURES) WILL INDICATE THE RIGHT HAND SIDES OF THE
!     "ADDING METHOD" LINEAR SYSTEMS AND THE QUANTITIES ZTU THE
!     SUPERDIAGONAL ELEMENTS TO BE USED IN THE BACK-SUBSTITUTION PART
!     OF THE ALGORITHM. ONE MUST ALSO MENTION THAT IN THE "RANDOM
!     OVERLAP" CASE THE SUFFIX "C" (CLEAR) REPRESENTS THE WHOLE LAYER
!     AND THAT THE SUFFIX "N" (NEBULOUS) REPRESENTS UNUSED ARRAYS.

! - TEMPORAIRE(S) 2D (0:KLEV) .

! ZFPC      : TERME "MEMBRE DE DROITE" POUR FLUX PARALLELE CLAIR.
!            : RIGHT HAND SIDE TERM FOR CLEAR-SKY PARALLEL FLUX.
! ZFPN      : TERME "MEMBRE DE DROITE" POUR FLUX PARALLELE NUAGEUX.
!            : RIGHT HAND SIDE TERM FOR CLOUDY PARALLEL FLUX.
! ZFDC      : TERME "MEMBRE DE DROITE" POUR FLUX DIFFUS "DOWN" CLAIR.
!            : RIGHT HAND SIDE TERM FOR CLEAR-SKY DOWNWARDS DIFF. FLUX.
! ZFDN      : TERME "MEMBRE DE DROITE" POUR FLUX DIFFUS "DOWN" NUAGEUX.
!            : RIGHT HAND SIDE TERM FOR CLOUDY DOWNWARDS DIFF. FLUX.
! ZFMC      : TERME "MEMBRE DE DROITE" POUR FLUX DIFFUS "UP" CLAIR.
!            : RIGHT HAND SIDE TERM FOR CLEAR-SKY UPWARDS DIFF. FLUX.
! ZFMN      : TERME "MEMBRE DE DROITE" POUR FLUX DIFFUS "UP" NUAGEUX.
!            : RIGHT HAND SIDE TERM FOR CLOUDY UPWARDS DIFF. FLUX.
! ZZFDC     : AUTRE TERME "M.D.D." POUR FLUX DIFFUS "DOWN" CLAIR.
!            : OTHER "R.H.S." TERM FOR CLEAR-SKY DOWNWARDS DIFF. FLUX.
! ZZFDN     : AUTRE TERME "M.D.D." POUR FLUX DIFFUS "DOWN" NUAGEUX.
!            : OTHER "R.H.S." TERM FOR CLOUDY DOWNWARDS DIFF. FLUX.
! ZZFMC     : AUTRE TERME "M.D.D." POUR FLUX DIFFUS "UP" CLAIR.
!            : OTHER "R.H.S." TERM FOR CLEAR-SKY UPWARDS DIFF. FLUX.
! ZZFMN     : AUTRE TERME "M.D.D." POUR FLUX DIFFUS "UP" NUAGEUX.
!            : OTHER "R.H.S." TERM FOR CLOUDY UPWARDS DIFF. FLUX.

! - TEMPORAIRE(S) 2D (1:KLEV) .

! ZTU1      : PREMIER COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : FIRST SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU2      : SECOND COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : SECOND SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU3      : TROISIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : THIRD SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU4      : QUATRIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : FOURTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU5      : CINQUIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : FIFTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU6      : SIXIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : SIXTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU7      : SEPTIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : SEVENTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU8      : HUITIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : EIGTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.
! ZTU9      : NEUVIEME COEFFICIENT SURDIAGONAL DU SYSTEME "ADDING".
!            : NINTH SUPERDIAGONAL COEFF. FOR THE "ADDING" SYSTEM.

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.1   SOMMET DE L'ATMOSPHERE.

!            TOP OF THE ATMOSPHERE.

DO JLON=KIDIA,KFDIA
  ZFDC(JLON,KTDIA-1)=EXP(MAX(-ZEOTS(JLON),ZARGLI))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFDN(JLON,KTDIA-1)=0._JPRB
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.2   PREMIERE COUCHE : CALCUL DES EPAISSEURS OPTIQUES, DES
!     TRANSMISSIVITES/REFLECTIVITES ET DES COEFFICIENTS LOCAUX DE
!     GEOMETRIE NUAGEUSE. POUR CES DERNIERS SEULES LES VALEURS 4 ET 5
!     SERONT UTILISEES DANS LA PARTIE THERMIQUE (PAS DE FLUX PARALLELE).

!            FIRST LAYER : COMPUTATION OF OPTICAL DEPTHS, OF
!     TRANSMISSIVITIES/REFLECTIVITIES AND OF LOCAL COEFFICIENTS FOR THE
!     CLOUD GEOMETRY. FOR THE LATTER ONLY THE 4 AND 5 VALUES WILL BE
!     USED IN THE THERMAL CASE (NO PARALLEL FLUX).

! - TEMPORAIRE(S) 1D .

! ZA1C      : TRANSMISSION PARALLELE CLAIRE.
!            : CLEAR-SKY PARALLEL TRANSMISSIVITY.
! ZA2C      : TRANSMISSION PARALLELE/DIFFUSE CLAIRE.
!            : CLEAR-SKY PARALLEL/DIFFUSE TRANSMISSIVITY.
! ZA3C      : REFLEXION PARALLELE/DIFFUSE CLAIRE.
!            : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.
! ZA4C      : TRANSMISSION DIFFUSE CLAIRE.
!            : CLEAR-SKY DIFFUSE TRANSMISSIVITY.
! ZA5C      : REFLEXION DIFFUSE CLAIRE.
!            : CLEAR-SKY DIFFUSE REFLECTIVITY.
! ZA1N      : TRANSMISSION PARALLELE NUAGEUSE.
!            : CLOUDY PARALLEL TRANSMISSIVITY.
! ZA2N      : TRANSMISSION PARALLELE/DIFFUSE NUAGEUSE.
!            : CLOUDY PARALLEL/DIFFUSE TRANSMISSIVITY.
! ZA3N      : REFLEXION PARALLELE/DIFFUSE NUAGEUSE.
!            : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.
! ZA4N      : TRANSMISSION DIFFUSE NUAGEUSE.
!            : CLOUDY DIFFUSE TRANSMISSIVITY.
! ZA5N      : REFLEXION DIFFUSE NUAGEUSE.
!            : CLOUDY DIFFUSE REFLECTIVITY.
! ZAL1      : POIDS DU FLUX M SORTANT C DANS LE FLUX M RENTRANT C.
!            : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING C FLUX.
! ZAL2      : POIDS DU FLUX D SORTANT C DANS LE FLUX D RENTRANT C.
!            : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING C FLUX.
! ZBE1      : POIDS DU FLUX M SORTANT C DANS LE FLUX M RENTRANT N.
!            : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING N FLUX.
! ZBE2      : POIDS DU FLUX D SORTANT C DANS LE FLUX D RENTRANT N.
!            : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING N FLUX.
! ZGA1      : POIDS DU FLUX M SORTANT N DANS LE FLUX M RENTRANT C.
!            : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING C FLUX.
! ZGA2      : POIDS DU FLUX D SORTANT N DANS LE FLUX D RENTRANT C.
!            : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING C FLUX.
! ZDE1      : POIDS DU FLUX M SORTANT N DANS LE FLUX M RENTRANT N.
!            : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING N FLUX.
! ZDE2      : POIDS DU FLUX D SORTANT N DANS LE FLUX D RENTRANT N.
!            : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING N FLUX.

DO JLON=KIDIA,KFDIA
  ZEO1=ZDEOT(JLON,KTDIA)+ZEO1TA(JLON,KTDIA)
  ZEO2=ZEO2TA(JLON,KTDIA)
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZZA4C1(JLON,KTDIA)=ZA4C(JLON)
  ZZA5C1(JLON,KTDIA)=ZA5C(JLON)
  ZEO1=ZEO1+ZEO1TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO1TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEO2=ZEO2+ZEO2TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO2TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZAL1(JLON)=ZB2(JLON,KTDIA)
    ZAL2(JLON)=ZB1(JLON,KTDIA)
    ZBE1(JLON)=1._JPRB-ZB2(JLON,KTDIA)
    ZBE2(JLON)=1._JPRB-ZB1(JLON,KTDIA)
    ZGA1(JLON)=1._JPRB-ZB4(JLON,KTDIA)
    ZGA2(JLON)=1._JPRB-ZB3(JLON,KTDIA)
    ZDE1(JLON)=ZB4(JLON,KTDIA)
    ZDE2(JLON)=ZB3(JLON,KTDIA)
  ELSE
    ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,KTDIA)+ZA4N(JLON)*PNEB(JLON,KTDIA)
    ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,KTDIA)+ZA5N(JLON)*PNEB(JLON,KTDIA)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.3   PREMIERE COUCHE, ELIMINATION (SANS PROBLEMES).

!            FIRST LEVEL, ELIMINATION (EASY).

DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFMC(JLON,KTDIA-1)=ZA5C(JLON)*(ZAL2(JLON)*ZFDC(JLON,KTDIA-1))
    ZFDC(JLON,KTDIA)=ZA4C(JLON)*(ZAL2(JLON)*ZFDC(JLON,KTDIA-1))
    ZFMN(JLON,KTDIA-1)=ZA5N(JLON)*(ZBE2(JLON)*ZFDC(JLON,KTDIA-1))
    ZFDN(JLON,KTDIA)=ZA4N(JLON)*(ZBE2(JLON)*ZFDC(JLON,KTDIA-1))
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1(JLON)*ZA4C(JLON)
    ZTU3(JLON,KTDIA)=ZGA1(JLON)*ZA4C(JLON)
    ZTU4(JLON,KTDIA)=ZBE1(JLON)*ZA4N(JLON)
    ZTU5(JLON,KTDIA)=ZDE1(JLON)*ZA4N(JLON)
    ZTU6(JLON,KTDIA)=ZAL1(JLON)*ZA5C(JLON)
    ZTU7(JLON,KTDIA)=ZGA1(JLON)*ZA5C(JLON)
    ZTU8(JLON,KTDIA)=ZBE1(JLON)*ZA5N(JLON)
    ZTU9(JLON,KTDIA)=ZDE1(JLON)*ZA5N(JLON)
  ELSE
    ZFMC(JLON,KTDIA-1)=ZA5C(JLON)*ZFDC(JLON,KTDIA-1)
    ZFDC(JLON,KTDIA)=ZA4C(JLON)*ZFDC(JLON,KTDIA-1)
    ZTU2(JLON,KTDIA)=ZA4C(JLON)
    ZTU6(JLON,KTDIA)=ZA5C(JLON)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.4   BOUCLE SUR LES NIVEAUX, CALCULS PRELIMINAIRES PUIS
!     ELIMINATION.

!            LOOP OVER LAYERS, PRELIMINARY COMPUTATIONS, THEN
!     ELIMINATION.

DO JLEV=KTDIA+1,KLEV
  DO JLON=KIDIA,KFDIA
    ZEO1=ZDEOT(JLON,JLEV)+ZEO1TA(JLON,JLEV)
    ZEO2=ZEO2TA(JLON,JLEV)
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZZA4C1(JLON,JLEV)=ZA4C(JLON)
    ZZA5C1(JLON,JLEV)=ZA5C(JLON)
    ZEO1=ZEO1+ZEO1TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO1TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEO2=ZEO2+ZEO2TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO2TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZAL1(JLON)=ZB2(JLON,JLEV)
      ZAL2(JLON)=ZB1(JLON,JLEV)
      ZBE1(JLON)=1._JPRB-ZB2(JLON,JLEV)
      ZBE2(JLON)=1._JPRB-ZB1(JLON,JLEV)
      ZGA1(JLON)=1._JPRB-ZB4(JLON,JLEV)
      ZGA2(JLON)=1._JPRB-ZB3(JLON,JLEV)
      ZDE1(JLON)=ZB4(JLON,JLEV)
      ZDE2(JLON)=ZB3(JLON,JLEV)
    ELSE
      ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,JLEV)+ZA4N(JLON)*PNEB(JLON,JLEV)
      ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,JLEV)+ZA5N(JLON)*PNEB(JLON,JLEV)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
      ZFMC(JLON,JLEV-1)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZFDC(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZFDN(JLON,JLEV-1))
      ZTU1(JLON,JLEV)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZAL1(JLON)
      ZTU3(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZGA1(JLON)
      ZTD2=ZA5N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-ZA5N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
      ZFMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
       &*ZFDC(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZFDN(JLON,JLEV-1))+ZTD2&
       &*ZFMC(JLON,JLEV-1))
      ZTU4(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZBE1(JLON)+ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZDE1(JLON)+ZTD2*ZTU3(JLON,JLEV))
    ELSE
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*ZTU6(JLON,JLEV-1))
      ZFMC(JLON,JLEV-1)=ZTD1*ZA5C(JLON)*ZFDC(JLON,JLEV-1)
      ZTU2(JLON,JLEV)=ZTD1*ZA4C(JLON)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD4=ZA4C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD5=ZA4C(JLON)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZFDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZFDC(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZFDN(JLON,JLEV-1))+ZTD4&
       &*ZFMC(JLON,JLEV-1)+ZTD5*ZFMN(JLON,JLEV-1)
      ZTU6(JLON,JLEV)=ZA5C(JLON)*ZAL1(JLON)+ZTD4*ZTU2(JLON,JLEV)&
       &+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=ZA5C(JLON)*ZGA1(JLON)+ZTD4*ZTU3(JLON,JLEV)&
       &+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=ZA4N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD7=ZA4N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZFDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZFDC(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZFDN(JLON,JLEV-1))+ZTD6&
       &*ZFMC(JLON,JLEV-1)+ZTD7*ZFMN(JLON,JLEV-1)
      ZTU8(JLON,JLEV)=ZA5N(JLON)*ZBE1(JLON)+ZTD6*ZTU2(JLON,JLEV)&
       &+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=ZA5N(JLON)*ZDE1(JLON)+ZTD6*ZTU3(JLON,JLEV)&
       &+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD4=ZA4C(JLON)*ZTU6(JLON,JLEV-1)
      ZFDC(JLON,JLEV)=ZA4C(JLON)*ZFDC(JLON,JLEV-1)+ZTD4*ZFMC(JLON,JLEV-1)
      ZTU6(JLON,JLEV)=ZA5C(JLON)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.5   TRAITEMENT EN SURFACE.

!            SURFACE TREATMENT.

DO JLON=KIDIA,KFDIA
  ZAL=1._JPRB-PEMIS(JLON)
  ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
  ZFMC(JLON,KLEV)=(ZTDS1*ZAL)*ZFDC(JLON,KLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    ZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZFDN(JLON,KLEV)+ZTDS2*ZFMC(JLON,KLEV))
    ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)+ZTUS1*ZFMN(JLON,KLEV)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.6   SUBSTITUTION NIVEAU PAR NIVEAU.

!            BACK-SUBSTITUTION LAYER BY LAYER.

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZFDN(JLON,JLEV)=ZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFMN(JLON,JLEV-1)=ZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZFMN(JLON,JLEV-1)
    ELSE
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZFMC(JLON,JLEV)
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZFMC(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.7   CALCUL DES FLUX AVEC L'HYPOTHESE "COOLING TO SPACE".

!            FLUX COMPUTATION IN THE COOLING TO SPACE CASE.

! - TEMPORAIRE(S) 1D .

! ZTRS      : FLUX "Z" NET AU SOMMET D'UNE COUCHE.
!            : "Z" NET FLUX AT THE TOP OF A LAYER.
! ZTRB      : FLUX "Z" NET A LA BASE D'UNE COUCHE.
!            : "Z" NET FLUX AT THE BOTTOM OF A LAYER.

DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZFDC(JLON,KLEV)-ZFMC(JLON,KLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZFDN(JLON,KLEV)-ZFMN(JLON,KLEV)
  ENDIF

  PFRTH(JLON,KLEV)=-ZBB(JLON,KLEV)*ZTRS(JLON)
ENDDO
!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZTRS(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZFDC(JLON,JLEV-1)-ZFMC(JLON,JLEV-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTRS(JLON)=ZTRS(JLON)+ZFDN(JLON,JLEV-1)-ZFMN(JLON,JLEV-1)
    ENDIF

!   FINAL COMPUTATION INCLUDING THE CTS SECURISATION FOR THE TIME-STEP

    PFRTH(JLON,JLEV-1)=PFRTH(JLON,JLEV)+ZBB(JLON,JLEV-1)&
     &*(ZTRB(JLON)-ZTRS(JLON))/(1._JPRB-(ZTRB(JLON)-ZTRS(JLON))&
     &*ZDAMP(JLON,JLEV))
  ENDDO
ENDDO

! NON-INDENTED SEQUENCE FOR THE CASE THE EWS SPECIFIC CALCULATION IS
! ACTIVATED

IF (LREWS) THEN

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.1   SOMMET DE L'ATMOSPHERE.

!            TOP OF THE ATMOSPHERE.

DO JLON=KIDIA,KFDIA
  ZFDC(JLON,KTDIA-1)=0._JPRB

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFDN(JLON,KTDIA-1)=0._JPRB
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.2   PREMIERE COUCHE : CALCUL DES EPAISSEURS OPTIQUES, DES
!     TRANSMISSIVITES/REFLECTIVITES ET DES COEFFICIENTS LOCAUX DE
!     GEOMETRIE NUAGEUSE. POUR CES DERNIERS SEULES LES VALEURS 4 ET 5
!     SERONT UTILISEES DANS LA PARTIE THERMIQUE (PAS DE FLUX PARALLELE).

!            FIRST LAYER : COMPUTATION OF OPTICAL DEPTHS, OF
!     TRANSMISSIVITIES/REFLECTIVITIES AND OF LOCAL COEFFICIENTS FOR THE
!     CLOUD GEOMETRY. FOR THE LATTER ONLY THE 4 AND 5 VALUES WILL BE
!     USED IN THE THERMAL CASE (NO PARALLEL FLUX).

! - TEMPORAIRE(S) 1D .

! ZA1C      : TRANSMISSION PARALLELE CLAIRE.
!            : CLEAR-SKY PARALLEL TRANSMISSIVITY.
! ZA2C      : TRANSMISSION PARALLELE/DIFFUSE CLAIRE.
!            : CLEAR-SKY PARALLEL/DIFFUSE TRANSMISSIVITY.
! ZA3C      : REFLEXION PARALLELE/DIFFUSE CLAIRE.
!            : CLEAR-SKY PARALLEL/DIFFUSE REFLECTIVITY.
! ZA4C      : TRANSMISSION DIFFUSE CLAIRE.
!            : CLEAR-SKY DIFFUSE TRANSMISSIVITY.
! ZA5C      : REFLEXION DIFFUSE CLAIRE.
!            : CLEAR-SKY DIFFUSE REFLECTIVITY.
! ZA1N      : TRANSMISSION PARALLELE NUAGEUSE.
!            : CLOUDY PARALLEL TRANSMISSIVITY.
! ZA2N      : TRANSMISSION PARALLELE/DIFFUSE NUAGEUSE.
!            : CLOUDY PARALLEL/DIFFUSE TRANSMISSIVITY.
! ZA3N      : REFLEXION PARALLELE/DIFFUSE NUAGEUSE.
!            : CLOUDY PARALLEL/DIFFUSE REFLECTIVITY.
! ZA4N      : TRANSMISSION DIFFUSE NUAGEUSE.
!            : CLOUDY DIFFUSE TRANSMISSIVITY.
! ZA5N      : REFLEXION DIFFUSE NUAGEUSE.
!            : CLOUDY DIFFUSE REFLECTIVITY.
! ZAL1      : POIDS DU FLUX M SORTANT C DANS LE FLUX M RENTRANT C.
!            : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING C FLUX.
! ZAL2      : POIDS DU FLUX D SORTANT C DANS LE FLUX D RENTRANT C.
!            : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING C FLUX.
! ZBE1      : POIDS DU FLUX M SORTANT C DANS LE FLUX M RENTRANT N.
!            : WEIGHT OF THE M OUTGOING C FLUX IN THE M INGOING N FLUX.
! ZBE2      : POIDS DU FLUX D SORTANT C DANS LE FLUX D RENTRANT N.
!            : WEIGHT OF THE D OUTGOING C FLUX IN THE D INGOING N FLUX.
! ZGA1      : POIDS DU FLUX M SORTANT N DANS LE FLUX M RENTRANT C.
!            : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING C FLUX.
! ZGA2      : POIDS DU FLUX D SORTANT N DANS LE FLUX D RENTRANT C.
!            : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING C FLUX.
! ZDE1      : POIDS DU FLUX M SORTANT N DANS LE FLUX M RENTRANT N.
!            : WEIGHT OF THE M OUTGOING N FLUX IN THE M INGOING N FLUX.
! ZDE2      : POIDS DU FLUX D SORTANT N DANS LE FLUX D RENTRANT N.
!            : WEIGHT OF THE D OUTGOING N FLUX IN THE D INGOING N FLUX.

DO JLON=KIDIA,KFDIA
  ZEO1=ZUUEOT(JLON,KTDIA)+ZEO1TA(JLON,KTDIA)
  ZEO2=ZEO2TA(JLON,KTDIA)
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZZA4C2(JLON,KTDIA)=ZA4C(JLON)
  ZZA5C2(JLON,KTDIA)=ZA5C(JLON)
  ZEO1=ZEO1+ZEO1TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO1TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEO2=ZEO2+ZEO2TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO2TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZAL1(JLON)=ZB2(JLON,KTDIA)
    ZAL2(JLON)=ZB1(JLON,KTDIA)
    ZBE1(JLON)=1._JPRB-ZB2(JLON,KTDIA)
    ZBE2(JLON)=1._JPRB-ZB1(JLON,KTDIA)
    ZGA1(JLON)=1._JPRB-ZB4(JLON,KTDIA)
    ZGA2(JLON)=1._JPRB-ZB3(JLON,KTDIA)
    ZDE1(JLON)=ZB4(JLON,KTDIA)
    ZDE2(JLON)=ZB3(JLON,KTDIA)
  ELSE
    ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,KTDIA)+ZA4N(JLON)*PNEB(JLON,KTDIA)
    ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,KTDIA)+ZA5N(JLON)*PNEB(JLON,KTDIA)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.3   PREMIERE COUCHE, ELIMINATION (SANS PROBLEMES).

!            FIRST LEVEL, ELIMINATION (EASY).

DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFMC(JLON,KTDIA-1)=0._JPRB
    ZFDC(JLON,KTDIA)=0._JPRB
    ZFMN(JLON,KTDIA-1)=0._JPRB
    ZFDN(JLON,KTDIA)=0._JPRB
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1(JLON)*ZA4C(JLON)
    ZTU3(JLON,KTDIA)=ZGA1(JLON)*ZA4C(JLON)
    ZTU4(JLON,KTDIA)=ZBE1(JLON)*ZA4N(JLON)
    ZTU5(JLON,KTDIA)=ZDE1(JLON)*ZA4N(JLON)
    ZTU6(JLON,KTDIA)=ZAL1(JLON)*ZA5C(JLON)
    ZTU7(JLON,KTDIA)=ZGA1(JLON)*ZA5C(JLON)
    ZTU8(JLON,KTDIA)=ZBE1(JLON)*ZA5N(JLON)
    ZTU9(JLON,KTDIA)=ZDE1(JLON)*ZA5N(JLON)
  ELSE
    ZFMC(JLON,KTDIA-1)=0._JPRB
    ZFDC(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZA4C(JLON)
    ZTU6(JLON,KTDIA)=ZA5C(JLON)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.4   BOUCLE SUR LES NIVEAUX, CALCULS PRELIMINAIRES PUIS
!     ELIMINATION.

!            LOOP OVER LAYERS, PRELIMINARY COMPUTATIONS, THEN
!     ELIMINATION.

DO JLEV=KTDIA+1,KLEV
  LLREWS=(JLEV==KLEV)
  DO JLON=KIDIA,KFDIA
    ZEO1=ZUUEOT(JLON,JLEV)+ZEO1TA(JLON,JLEV)
    ZEO2=ZEO2TA(JLON,JLEV)
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZZA4C2(JLON,JLEV)=ZA4C(JLON)
    ZZA5C2(JLON,JLEV)=ZA5C(JLON)
    ZEO1=ZEO1+ZEO1TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO1TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEO2=ZEO2+ZEO2TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO2TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZAL1(JLON)=ZB2(JLON,JLEV)
      ZAL2(JLON)=ZB1(JLON,JLEV)
      ZBE1(JLON)=1._JPRB-ZB2(JLON,JLEV)
      ZBE2(JLON)=1._JPRB-ZB1(JLON,JLEV)
      ZGA1(JLON)=1._JPRB-ZB4(JLON,JLEV)
      ZGA2(JLON)=1._JPRB-ZB3(JLON,JLEV)
      ZDE1(JLON)=ZB4(JLON,JLEV)
      ZDE2(JLON)=ZB3(JLON,JLEV)
    ELSE
      ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,JLEV)+ZA4N(JLON)*PNEB(JLON,JLEV)
      ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,JLEV)+ZA5N(JLON)*PNEB(JLON,JLEV)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
      IF (LLREWS) THEN
        ZFMC(JLON,JLEV-1)=-ZTD1*(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)
      ELSE
        ZFMC(JLON,JLEV-1)=0._JPRB
      ENDIF
      ZTU1(JLON,JLEV)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZAL1(JLON)
      ZTU3(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZGA1(JLON)
      ZTD2=ZA5N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-ZA5N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
      IF (LLREWS) THEN
        ZFMN(JLON,JLEV-1)=ZTD3*(ZTD2*ZFMC(JLON,JLEV-1)-PNEB(JLON,JLEV)&
         &*ZA4N(JLON))
      ELSE
        ZFMN(JLON,JLEV-1)=0._JPRB
      ENDIF
      ZTU4(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZBE1(JLON)+ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZDE1(JLON)+ZTD2*ZTU3(JLON,JLEV))
    ELSE
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*ZTU6(JLON,JLEV-1))
      IF (LLREWS) THEN
        ZFMC(JLON,JLEV-1)=-ZTD1*ZA4C(JLON)
      ELSE
        ZFMC(JLON,JLEV-1)=0._JPRB
      ENDIF
      ZTU2(JLON,JLEV)=ZTD1*ZA4C(JLON)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD4=ZA4C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD5=ZA4C(JLON)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      IF (LLREWS) THEN
        ZFDC(JLON,JLEV)=ZTD4*ZFMC(JLON,JLEV-1)+ZTD5*ZFMN(JLON,JLEV-1)&
         &-(1._JPRB-PNEB(JLON,JLEV))*ZA5C(JLON)
      ELSE
        ZFDC(JLON,JLEV)=0._JPRB
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)*ZAL1(JLON)+ZTD4*ZTU2(JLON,JLEV)&
       &+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=ZA5C(JLON)*ZGA1(JLON)+ZTD4*ZTU3(JLON,JLEV)&
       &+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=ZA4N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD7=ZA4N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      IF (LLREWS) THEN
        ZFDN(JLON,JLEV)=ZTD6*ZFMC(JLON,JLEV-1)+ZTD7&
         &*ZFMN(JLON,JLEV-1)-PNEB(JLON,JLEV)*ZA5N(JLON)
      ELSE
        ZFDN(JLON,JLEV)=0._JPRB
      ENDIF
      ZTU8(JLON,JLEV)=ZA5N(JLON)*ZBE1(JLON)+ZTD6*ZTU2(JLON,JLEV)&
       &+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=ZA5N(JLON)*ZDE1(JLON)+ZTD6*ZTU3(JLON,JLEV)&
       &+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD4=ZA4C(JLON)*ZTU6(JLON,JLEV-1)
      IF (LLREWS) THEN
        ZFDC(JLON,JLEV)=ZTD4*ZFMC(JLON,JLEV-1)-ZA5C(JLON)
      ELSE
        ZFDC(JLON,JLEV)=0._JPRB
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.5   TRAITEMENT EN SURFACE.

!            SURFACE TREATMENT.

DO JLON=KIDIA,KFDIA
  ZAL=1._JPRB-PEMIS(JLON)
  ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
  ZFMC(JLON,KLEV)=(ZTDS1*ZAL)*(ZFDC(JLON,KLEV)+1._JPRB)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)-(ZTDS1*ZAL)*PNEB(JLON,KLEV)
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    ZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZFDN(JLON,KLEV)+ZTDS2*ZFMC(JLON,KLEV))&
     &+ZTDS3*ZAL*PNEB(JLON,KLEV)
    ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)+ZTUS1*ZFMN(JLON,KLEV)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.6   SUBSTITUTION NIVEAU PAR NIVEAU.

!            BACK-SUBSTITUTION LAYER BY LAYER.

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZFDN(JLON,JLEV)=ZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFMN(JLON,JLEV-1)=ZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZFMN(JLON,JLEV-1)
    ELSE
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZFMC(JLON,JLEV)
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZFMC(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VI.7   CALCUL DES FLUX AVEC L'HYPOTHESE "EXCHANGE WITH SURFACE".

!            FLUX COMPUTATION IN THE EXCHANGE WITH SURFACE CASE.

! - TEMPORAIRE(S) 1D .

! ZTRS      : FLUX "Z" NET AU SOMMET D'UNE COUCHE.
!            : "Z" NET FLUX AT THE TOP OF A LAYER.
! ZTRB      : FLUX "Z" NET A LA BASE D'UNE COUCHE.
!            : "Z" NET FLUX AT THE BOTTOM OF A LAYER.

DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZFDC(JLON,KTDIA-1)-ZFMC(JLON,KTDIA-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTRB(JLON)=ZTRB(JLON)+ZFDN(JLON,KTDIA-1)-ZFMN(JLON,KTDIA-1)
  ENDIF

  ZFRTH(JLON,KTDIA-1)=0._JPRB
ENDDO
!cdir unroll=8
DO JLEV=KTDIA,KLEV
  LLREWS=(JLEV==KLEV)
  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTRB(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZFDC(JLON,JLEV)-ZFMC(JLON,JLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTRB(JLON)=ZTRB(JLON)+ZFDN(JLON,JLEV)-ZFMN(JLON,JLEV)
    ENDIF

    IF (LLREWS) THEN
      ZTRB(JLON)=ZTRB(JLON)+1._JPRB
    ENDIF

!   FINAL COMPUTATION INCLUDING THE EWS SECURISATION FOR THE TIME-STEP

    ZFRTH(JLON,JLEV)=ZFRTH(JLON,JLEV-1)+(ZBB(JLON,KLEV)&
     &-ZBB(JLON,JLEV-1))*(ZTRS(JLON)-ZTRB(JLON))&
     &/(1._JPRB-(ZTRS(JLON)-ZTRB(JLON))*ZDAMP(JLON,JLEV))
  ENDDO
ENDDO

ELSE

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    ZFRTH(JLON,JLEV)=0._JPRB
  ENDDO
ENDDO

ENDIF

!*
!     ------------------------------------------------------------------
!     VII - CALCUL DES COMPLEMENTS AU FLUX DU CORPS NOIR DES FLUX
!     THERMIQUES 'ANTI-SURESTIMATION' POUR LE PROFIL DE TEMPERATURE REEL
!     ET LE PROFIL ISOTHERME NORMALISE AFIN D'OBTENIR PAR DIFFERENCE LES
!     TERMES D'ECHANGE QUI, AJOUTES AUX RESULTATS "COOLING TO SPACE",
!     DONNERONT LE CHAMP DE RAYONNEMENT THERMIQUE FINAL.

!           COMPUTATION OF THE COMPLEMENTS TO THE BLACK-BODY FLUXES OF
!     "ANTI-SURESTIMATION" THERMAL FLUXES FOR THE REAL TEMPERATURE
!     PROFILE AND FOR THE NORMALIZED ISOTHERMAL PROFILE IN ORDER TO
!     OBTAIN BY DIFFERENCIATION THE EXCHANGE TERMS THAT, ADDED TO THE
!     COOLING TO SPACE RESULTS, WILL GIVE THE FINAL THERMAL RADIATIVE
!     FLUXES' ARRAY.

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.1   SOMMET DE L'ATMOSPHERE.

!             TOP OF THE ATMOSPHERE.

DO JLON=KIDIA,KFDIA
  ZTDC(JLON,KTDIA-1)=EXP(MAX(-ZUETS(JLON),ZARGLI))
  ZZTDC(JLON,KTDIA-1)=ZBB(JLON,KTDIA-1)*ZTDC(JLON,KTDIA-1)
  IF (LREWS) THEN
    ZZZTDC(JLON,KTDIA-1)=0._JPRB
  ENDIF

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTDN(JLON,KTDIA-1)=0._JPRB
    ZZTDN(JLON,KTDIA-1)=0._JPRB
    IF (LREWS) THEN
      ZZZTDN(JLON,KTDIA-1)=0._JPRB
    ENDIF
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.2   PREMIERE COUCHE: CALCUL DES EPAISSEURS OPTIQUES, DES
!     TRANSMISSIVITES/REFLECTIVITES ET DES COEFFICIENTS LOCAUX DE
!     GEOMETRIE NUAGEUSE.

!             FIRST LAYER : COMPUTATION OF THE OPTICAL DEPTHS, OF THE
!     TRANSMISSIVITIES/REFLECTIVITIES AND OF THE LOCAL COEFFICIENTS FOR
!     THE CLOUD GEOMETRY.

IF (LRPROX.AND.(.NOT.LRMIX)) THEN
  DO JLON=KIDIA,KFDIA
    ZEO1=ZEOLT(JLON,KTDIA)+ZEO1TA(JLON,KTDIA)
    ZEO2=ZEO2TA(JLON,KTDIA)
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
    ZZA4C4(JLON,KTDIA)=ZA4C(JLON)
    ZEO1=ZEO1+ZEO1TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
     &+ZEO1TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
    ZEO2=ZEO2+ZEO2TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
     &+ZEO2TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))

!     LOCAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

    ZLEPC(JLON,KTDIA)=(1._JPRB-PNEB(JLON,KTDIA))*ZA4C(JLON)&
     &+PNEB(JLON,KTDIA)*ZA4N(JLON)
    ZDA4L(JLON,KTDIA)=ZA4C(JLON)-ZA4N(JLON)
  ENDDO
ENDIF

DO JLON=KIDIA,KFDIA
  ZEO1=ZUEOT(JLON,KTDIA)+ZEO1TA(JLON,KTDIA)
  ZEO2=ZEO2TA(JLON,KTDIA)
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZZA4C3(JLON,KTDIA)=ZA4C(JLON)
  ZZA5C3(JLON,KTDIA)=ZA5C(JLON)
  ZEO1=ZEO1+ZEO1TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO1TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEO2=ZEO2+ZEO2TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO2TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))

!     GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

  ZGEPC(JLON,KTDIA)=(1._JPRB-PNEB(JLON,KTDIA))*ZA4C(JLON)&
   &+PNEB(JLON,KTDIA)*ZA4N(JLON)
  ZDA4G(JLON,KTDIA)=ZA4C(JLON)-ZA4N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZAL1(JLON)=ZB2(JLON,KTDIA)
    ZAL2(JLON)=ZB1(JLON,KTDIA)
    ZBE1(JLON)=1._JPRB-ZB2(JLON,KTDIA)
    ZBE2(JLON)=1._JPRB-ZB1(JLON,KTDIA)
    ZGA1(JLON)=1._JPRB-ZB4(JLON,KTDIA)
    ZGA2(JLON)=1._JPRB-ZB3(JLON,KTDIA)
    ZDE1(JLON)=ZB4(JLON,KTDIA)
    ZDE2(JLON)=ZB3(JLON,KTDIA)
  ELSE
    ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,KTDIA)+ZA4N(JLON)*PNEB(JLON,KTDIA)
    ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,KTDIA)+ZA5N(JLON)*PNEB(JLON,KTDIA)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.3   PREMIERE COUCHE, ELIMINATION (SANS PROBLEMES).

!             FIRST LAYER, ELIMINATION (EASY).

DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTMC(JLON,KTDIA-1)=ZA5C(JLON)*(ZAL2(JLON)*ZTDC(JLON,KTDIA-1))
    ZZTMC(JLON,KTDIA-1)=ZA5C(JLON)*(ZAL2(JLON)&
     &*ZZTDC(JLON,KTDIA-1))+ZA4C(JLON)*(1._JPRB&
     &-PNEB(JLON,KTDIA))*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTMC(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZTDC(JLON,KTDIA)=ZA4C(JLON)*(ZAL2(JLON)*ZTDC(JLON,KTDIA-1))
    ZZTDC(JLON,KTDIA)=ZA4C(JLON)*(ZAL2(JLON)&
     &*ZZTDC(JLON,KTDIA-1))+ZA5C(JLON)*(1._JPRB&
     &-PNEB(JLON,KTDIA))*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTDC(JLON,KTDIA)=0._JPRB
    ENDIF
    ZTMN(JLON,KTDIA-1)=ZA5N(JLON)*(ZBE2(JLON)*ZTDC(JLON,KTDIA-1))
    ZZTMN(JLON,KTDIA-1)=ZA5N(JLON)*(ZBE2(JLON)&
     &*ZZTDC(JLON,KTDIA-1))+ZA4N(JLON)&
     &*PNEB(JLON,KTDIA)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTMN(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZTDN(JLON,KTDIA)=ZA4N(JLON)*(ZBE2(JLON)*ZTDC(JLON,KTDIA-1))
    ZZTDN(JLON,KTDIA)=ZA4N(JLON)*(ZBE2(JLON)&
     &*ZZTDC(JLON,KTDIA-1))+ZA5N(JLON)&
     &*PNEB(JLON,KTDIA)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTDN(JLON,KTDIA)=0._JPRB
    ENDIF
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1(JLON)*ZA4C(JLON)
    ZTU3(JLON,KTDIA)=ZGA1(JLON)*ZA4C(JLON)
    ZTU4(JLON,KTDIA)=ZBE1(JLON)*ZA4N(JLON)
    ZTU5(JLON,KTDIA)=ZDE1(JLON)*ZA4N(JLON)
    ZTU6(JLON,KTDIA)=ZAL1(JLON)*ZA5C(JLON)
    ZTU7(JLON,KTDIA)=ZGA1(JLON)*ZA5C(JLON)
    ZTU8(JLON,KTDIA)=ZBE1(JLON)*ZA5N(JLON)
    ZTU9(JLON,KTDIA)=ZDE1(JLON)*ZA5N(JLON)
  ELSE
    ZTMC(JLON,KTDIA-1)=ZA5C(JLON)*ZTDC(JLON,KTDIA-1)
    ZZTMC(JLON,KTDIA-1)=ZA5C(JLON)*ZZTDC(JLON,KTDIA-1)&
     &+ZA4C(JLON)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTMC(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZTDC(JLON,KTDIA)=ZA4C(JLON)*ZTDC(JLON,KTDIA-1)
    ZZTDC(JLON,KTDIA)=ZA4C(JLON)*ZZTDC(JLON,KTDIA-1)+ZA5C(JLON)&
     &*(ZBB(JLON,KTDIA-1)-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZTDC(JLON,KTDIA)=0._JPRB
    ENDIF
    ZTU2(JLON,KTDIA)=ZA4C(JLON)
    ZTU6(JLON,KTDIA)=ZA5C(JLON)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.4   BOUCLE SUR LES NIVEAUX, CALCULS PRELIMINAIRES PUIS
!     ELIMINATION.

!             LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN
!     ELIMINATION.

DO JLEV=KTDIA+1,KLEV
  LLREWS=(JLEV==KLEV)

  IF (LRPROX.AND.(.NOT.LRMIX)) THEN
    DO JLON=KIDIA,KFDIA
      ZEO1=ZEOLT(JLON,JLEV)+ZEO1TA(JLON,JLEV)
      ZEO2=ZEO2TA(JLON,JLEV)
      ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
      ZTAU=EXP(MAX(-ZEPS,ZARGLI))
      ZRHO=ZEO2/(ZEO1+ZEPS)
      ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))
      ZZA4C4(JLON,JLEV)=ZA4C(JLON)
      ZEO1=ZEO1+ZEO1TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
       &+ZEO1TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
      ZEO2=ZEO2+ZEO2TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
       &+ZEO2TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
      ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
      ZTAU=EXP(MAX(-ZEPS,ZARGLI))
      ZRHO=ZEO2/(ZEO1+ZEPS)
      ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))

!     LOCAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

      ZLEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)&
       &+PNEB(JLON,JLEV)*ZA4N(JLON)
      ZDA4L(JLON,JLEV)=ZA4C(JLON)-ZA4N(JLON)
    ENDDO
  ENDIF

  DO JLON=KIDIA,KFDIA
    ZEO1=ZUEOT(JLON,JLEV)+ZEO1TA(JLON,JLEV)
    ZEO2=ZEO2TA(JLON,JLEV)
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZZA4C3(JLON,JLEV)=ZA4C(JLON)
    ZZA5C3(JLON,JLEV)=ZA5C(JLON)
    ZEO1=ZEO1+ZEO1TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO1TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEO2=ZEO2+ZEO2TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO2TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))

!     GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

    ZGEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)&
     &+PNEB(JLON,JLEV)*ZA4N(JLON)
    ZDA4G(JLON,JLEV)=ZA4C(JLON)-ZA4N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZAL1(JLON)=ZB2(JLON,JLEV)
      ZAL2(JLON)=ZB1(JLON,JLEV)
      ZBE1(JLON)=1._JPRB-ZB2(JLON,JLEV)
      ZBE2(JLON)=1._JPRB-ZB1(JLON,JLEV)
      ZGA1(JLON)=1._JPRB-ZB4(JLON,JLEV)
      ZGA2(JLON)=1._JPRB-ZB3(JLON,JLEV)
      ZDE1(JLON)=ZB4(JLON,JLEV)
      ZDE2(JLON)=ZB3(JLON,JLEV)
    ELSE
      ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,JLEV)+ZA4N(JLON)*PNEB(JLON,JLEV)
      ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,JLEV)+ZA5N(JLON)*PNEB(JLON,JLEV)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
      ZTMC(JLON,JLEV-1)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZTDC(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTDN(JLON,JLEV-1))
      ZZTMC(JLON,JLEV-1)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZZTDC(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZZTDN(JLON,JLEV-1))+ZTD1*(1._JPRB&
       &-PNEB(JLON,JLEV))*(ZA4C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA5C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZTMC(JLON,JLEV-1)=-ZTD1*(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)
        ELSE
          ZZZTMC(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU1(JLON,JLEV)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZAL1(JLON)
      ZTU3(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZGA1(JLON)
      ZTD2=ZA5N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-ZA5N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
      ZTMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
       &*ZTDC(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTDN(JLON,JLEV-1))+ZTD2&
       &*ZTMC(JLON,JLEV-1))
      ZZTMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
       &*ZZTDC(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZZTDN(JLON,JLEV-1))+ZTD2&
       &*ZZTMC(JLON,JLEV-1))+ZTD3*PNEB(JLON,JLEV)&
       &*(ZA4N(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV))+ZA5N(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZTMN(JLON,JLEV-1)=ZTD3*(ZTD2*ZZZTMC(JLON,JLEV-1)-PNEB(JLON,JLEV)&
           &*ZA4N(JLON))
        ELSE
          ZZZTMN(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU4(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZBE1(JLON)+ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZDE1(JLON)+ZTD2*ZTU3(JLON,JLEV))
    ELSE
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*ZTU6(JLON,JLEV-1))
      ZTMC(JLON,JLEV-1)=ZTD1*ZA5C(JLON)*ZTDC(JLON,JLEV-1)
      ZZTMC(JLON,JLEV-1)=ZTD1*ZA5C(JLON)*ZZTDC(JLON,JLEV-1)+ZTD1&
       &*(ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV))+ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZTMC(JLON,JLEV-1)=-ZTD1*ZA4C(JLON)
        ELSE
          ZZZTMC(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU2(JLON,JLEV)=ZTD1*ZA4C(JLON)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD4=ZA4C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD5=ZA4C(JLON)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZTDC(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZTDN(JLON,JLEV-1))+ZTD4&
       &*ZTMC(JLON,JLEV-1)+ZTD5*ZTMN(JLON,JLEV-1)
      ZZTDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZZTDC(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZZTDN(JLON,JLEV-1))+ZTD4&
       &*ZZTMC(JLON,JLEV-1)+ZTD5*ZZTMN(JLON,JLEV-1)&
       &+(1._JPRB-PNEB(JLON,JLEV))*(ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZTDC(JLON,JLEV)=ZTD4*ZZZTMC(JLON,JLEV-1)+ZTD5*ZZZTMN(JLON,JLEV-1)&
           &-(1._JPRB-PNEB(JLON,JLEV))*ZA5C(JLON)
        ELSE
          ZZZTDC(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)*ZAL1(JLON)+ZTD4*ZTU2(JLON,JLEV)&
       &+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=ZA5C(JLON)*ZGA1(JLON)+ZTD4*ZTU3(JLON,JLEV)&
       &+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=ZA4N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD7=ZA4N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZTDC(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZTDN(JLON,JLEV-1))+ZTD6&
       &*ZTMC(JLON,JLEV-1)+ZTD7*ZTMN(JLON,JLEV-1)
      ZZTDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZZTDC(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZZTDN(JLON,JLEV-1))+ZTD6&
       &*ZZTMC(JLON,JLEV-1)+ZTD7*ZZTMN(JLON,JLEV-1)&
       &+PNEB(JLON,JLEV)*(ZA5N(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4N(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZTDN(JLON,JLEV)=ZTD6*ZZZTMC(JLON,JLEV-1)+ZTD7*ZZZTMN(JLON,JLEV-1)&
           &-PNEB(JLON,JLEV)*ZA5N(JLON)
        ELSE
          ZZZTDN(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU8(JLON,JLEV)=ZA5N(JLON)*ZBE1(JLON)+ZTD6*ZTU2(JLON,JLEV)&
       &+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=ZA5N(JLON)*ZDE1(JLON)+ZTD6*ZTU3(JLON,JLEV)&
       &+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD4=ZA4C(JLON)*ZTU6(JLON,JLEV-1)
      ZTDC(JLON,JLEV)=ZA4C(JLON)*ZTDC(JLON,JLEV-1)+ZTD4*ZTMC(JLON,JLEV-1)
      ZZTDC(JLON,JLEV)=ZA4C(JLON)*ZZTDC(JLON,JLEV-1)+ZTD4&
       &*ZZTMC(JLON,JLEV-1)+ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2))
      IF (LREWS) THEN
        IF (LLREWS) THEN 
          ZZZTDC(JLON,JLEV)=ZTD4*ZZZTMC(JLON,JLEV-1)-ZA5C(JLON)
        ELSE
          ZZZTDC(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.5   TRAITEMENT EN SURFACE.

!             SURFACE TREATMENT.

DO JLON=KIDIA,KFDIA
  ZAL=1._JPRB-PEMIS(JLON)
  ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
  ZTMC(JLON,KLEV)=(ZTDS1*ZAL)*ZTDC(JLON,KLEV)
  ZZTMC(JLON,KLEV)=(ZTDS1*ZAL)*ZZTDC(JLON,KLEV)+(ZTDS1*ZAL)&
   &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
  IF (LREWS) THEN
    ZZZTMC(JLON,KLEV)=(ZTDS1*ZAL)*(ZZZTDC(JLON,KLEV)+1._JPRB)
  ENDIF

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZZTMC(JLON,KLEV)=ZZTMC(JLON,KLEV)-(ZTDS1*ZAL)*PNEB(JLON,KLEV)&
     &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
    IF (LREWS) THEN
      ZZZTMC(JLON,KLEV)=ZZZTMC(JLON,KLEV)-(ZTDS1*ZAL)*PNEB(JLON,KLEV)
    ENDIF
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    ZTMN(JLON,KLEV)=ZTDS3*(ZAL*ZTDN(JLON,KLEV)+ZTDS2*ZTMC(JLON,KLEV))
    ZZTMN(JLON,KLEV)=ZTDS3*(ZAL*ZZTDN(JLON,KLEV)+ZTDS2&
     &*ZZTMC(JLON,KLEV))+ZTDS3*ZAL*PNEB(JLON,KLEV)&
     &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
    IF (LREWS) THEN
      ZZZTMN(JLON,KLEV)=ZTDS3*(ZAL*ZZZTDN(JLON,KLEV)+ZTDS2&
       &*ZZZTMC(JLON,KLEV))+ZTDS3*ZAL*PNEB(JLON,KLEV)
    ENDIF
    ZTMC(JLON,KLEV)=ZTMC(JLON,KLEV)+ZTUS1*ZTMN(JLON,KLEV)
    ZZTMC(JLON,KLEV)=ZZTMC(JLON,KLEV)+ZTUS1*ZZTMN(JLON,KLEV)
    IF (LREWS) THEN
      ZZZTMC(JLON,KLEV)=ZZZTMC(JLON,KLEV)+ZTUS1*ZZZTMN(JLON,KLEV)
    ENDIF
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.6   SUBSTITUTION NIVEAU PAR NIVEAU.

!             BACK-SUBSTITUTION LAYER BY LAYER.

DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTDN(JLON,JLEV)=ZTDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZTMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZTMN(JLON,JLEV)
      ZZTDN(JLON,JLEV)=ZZTDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZZTMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZZTMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZTDN(JLON,JLEV)=ZZZTDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
         &*ZZZTMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
         &*ZZZTMN(JLON,JLEV)
      ENDIF
      ZTDC(JLON,JLEV)=ZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZTMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZTMN(JLON,JLEV)
      ZZTDC(JLON,JLEV)=ZZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZZTMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZZTMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZTDC(JLON,JLEV)=ZZZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
         &*ZZZTMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
         &*ZZZTMN(JLON,JLEV)
      ENDIF
      ZTMN(JLON,JLEV-1)=ZTMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZTMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZTMN(JLON,JLEV)
      ZZTMN(JLON,JLEV-1)=ZZTMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZZTMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZZTMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZTMN(JLON,JLEV-1)=ZZZTMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
         &*ZZZTMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
         &*ZZZTMN(JLON,JLEV)
      ENDIF
      ZTMC(JLON,JLEV-1)=ZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZTMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZTMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZTMN(JLON,JLEV-1)
      ZZTMC(JLON,JLEV-1)=ZZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZZTMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZZTMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZZTMN(JLON,JLEV-1)
      IF (LREWS) THEN
        ZZZTMC(JLON,JLEV-1)=ZZZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         &*ZZZTMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
         &*ZZZTMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
         &*ZZZTMN(JLON,JLEV-1)
      ENDIF
    ELSE
      ZTDC(JLON,JLEV)=ZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZTMC(JLON,JLEV)
      ZZTDC(JLON,JLEV)=ZZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZZTMC(JLON,JLEV)
      IF (LREWS) THEN
        ZZZTDC(JLON,JLEV)=ZZZTDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZZZTMC(JLON,JLEV)
      ENDIF
      ZTMC(JLON,JLEV-1)=ZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZTMC(JLON,JLEV)
      ZZTMC(JLON,JLEV-1)=ZZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZZTMC(JLON,JLEV)
      IF (LREWS) THEN
        ZZZTMC(JLON,JLEV-1)=ZZZTMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         &*ZZZTMC(JLON,JLEV)
      ENDIF
    ENDIF

  ENDDO
ENDDO

! DUPLICATION OF THE LOOP WITH THREE RIGHT-HAND SIDES WHEN REPLACING THE
! EBL MINIMUM VALUES FOR OPTICAL DEPTHS BY THE MAXIMUM (LOCAL) ONES.
! NON-INDENTED SEQUENCE

IF (LRMIX) THEN

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.1   SOMMET DE L'ATMOSPHERE.

!             TOP OF THE ATMOSPHERE.

DO JLON=KIDIA,KFDIA
  ZFDC(JLON,KTDIA-1)=EXP(MAX(-ZEOTS(JLON),ZARGLI))
  ZZFDC(JLON,KTDIA-1)=ZBB(JLON,KTDIA-1)*ZFDC(JLON,KTDIA-1)
  IF (LREWS) THEN
    ZZZFDC(JLON,KTDIA-1)=0._JPRB
  ENDIF

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFDN(JLON,KTDIA-1)=0._JPRB
    ZZFDN(JLON,KTDIA-1)=0._JPRB
    IF (LREWS) THEN
      ZZZFDN(JLON,KTDIA-1)=0._JPRB
    ENDIF
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.2   PREMIERE COUCHE: CALCUL DES EPAISSEURS OPTIQUES, DES
!     TRANSMISSIVITES/REFLECTIVITES ET DES COEFFICIENTS LOCAUX DE
!     GEOMETRIE NUAGEUSE.

!             FIRST LAYER : COMPUTATION OF THE OPTICAL DEPTHS, OF THE
!     TRANSMISSIVITIES/REFLECTIVITIES AND OF THE LOCAL COEFFICIENTS FOR
!     THE CLOUD GEOMETRY.

DO JLON=KIDIA,KFDIA
  ZEO1=ZEOLT(JLON,KTDIA)+ZEO1TA(JLON,KTDIA)
  ZEO2=ZEO2TA(JLON,KTDIA)
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZZA4C4(JLON,KTDIA)=ZA4C(JLON)
  ZZA5C4(JLON,KTDIA)=ZA5C(JLON)
  ZEO1=ZEO1+ZEO1TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO1TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEO2=ZEO2+ZEO2TN(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
   &+ZEO2TI(JLON,KTDIA)*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
  ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
  ZTAU=EXP(MAX(-ZEPS,ZARGLI))
  ZRHO=ZEO2/(ZEO1+ZEPS)
  ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))
  ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU*ZTAU)))

!     GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

  ZLEPC(JLON,KTDIA)=(1._JPRB-PNEB(JLON,KTDIA))*ZA4C(JLON)&
   &+PNEB(JLON,KTDIA)*ZA4N(JLON)
  ZDA4L(JLON,KTDIA)=ZA4C(JLON)-ZA4N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZAL1(JLON)=ZB2(JLON,KTDIA)
    ZAL2(JLON)=ZB1(JLON,KTDIA)
    ZBE1(JLON)=1._JPRB-ZB2(JLON,KTDIA)
    ZBE2(JLON)=1._JPRB-ZB1(JLON,KTDIA)
    ZGA1(JLON)=1._JPRB-ZB4(JLON,KTDIA)
    ZGA2(JLON)=1._JPRB-ZB3(JLON,KTDIA)
    ZDE1(JLON)=ZB4(JLON,KTDIA)
    ZDE2(JLON)=ZB3(JLON,KTDIA)
  ELSE
    ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,KTDIA)+ZA4N(JLON)*PNEB(JLON,KTDIA)
    ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,KTDIA)+ZA5N(JLON)*PNEB(JLON,KTDIA)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.3   PREMIERE COUCHE, ELIMINATION (SANS PROBLEMES).

!             FIRST LAYER, ELIMINATION (EASY).

DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZFMC(JLON,KTDIA-1)=ZA5C(JLON)*(ZAL2(JLON)*ZFDC(JLON,KTDIA-1))
    ZZFMC(JLON,KTDIA-1)=ZA5C(JLON)*(ZAL2(JLON)&
     &*ZZFDC(JLON,KTDIA-1))+ZA4C(JLON)*(1._JPRB&
     &-PNEB(JLON,KTDIA))*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFMC(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZFDC(JLON,KTDIA)=ZA4C(JLON)*(ZAL2(JLON)*ZFDC(JLON,KTDIA-1))
    ZZFDC(JLON,KTDIA)=ZA4C(JLON)*(ZAL2(JLON)&
     &*ZZFDC(JLON,KTDIA-1))+ZA5C(JLON)*(1._JPRB&
     &-PNEB(JLON,KTDIA))*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFDC(JLON,KTDIA)=0._JPRB
    ENDIF
    ZFMN(JLON,KTDIA-1)=ZA5N(JLON)*(ZBE2(JLON)*ZFDC(JLON,KTDIA-1))
    ZZFMN(JLON,KTDIA-1)=ZA5N(JLON)*(ZBE2(JLON)&
     &*ZZFDC(JLON,KTDIA-1))+ZA4N(JLON)&
     &*PNEB(JLON,KTDIA)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFMN(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZFDN(JLON,KTDIA)=ZA4N(JLON)*(ZBE2(JLON)*ZFDC(JLON,KTDIA-1))
    ZZFDN(JLON,KTDIA)=ZA4N(JLON)*(ZBE2(JLON)&
     &*ZZFDC(JLON,KTDIA-1))+ZA5N(JLON)&
     &*PNEB(JLON,KTDIA)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFDN(JLON,KTDIA)=0._JPRB
    ENDIF
    ZTU1(JLON,KTDIA)=0._JPRB
    ZTU2(JLON,KTDIA)=ZAL1(JLON)*ZA4C(JLON)
    ZTU3(JLON,KTDIA)=ZGA1(JLON)*ZA4C(JLON)
    ZTU4(JLON,KTDIA)=ZBE1(JLON)*ZA4N(JLON)
    ZTU5(JLON,KTDIA)=ZDE1(JLON)*ZA4N(JLON)
    ZTU6(JLON,KTDIA)=ZAL1(JLON)*ZA5C(JLON)
    ZTU7(JLON,KTDIA)=ZGA1(JLON)*ZA5C(JLON)
    ZTU8(JLON,KTDIA)=ZBE1(JLON)*ZA5N(JLON)
    ZTU9(JLON,KTDIA)=ZDE1(JLON)*ZA5N(JLON)
  ELSE
    ZFMC(JLON,KTDIA-1)=ZA5C(JLON)*ZFDC(JLON,KTDIA-1)
    ZZFMC(JLON,KTDIA-1)=ZA5C(JLON)*ZZFDC(JLON,KTDIA-1)&
     &+ZA4C(JLON)*(ZBB(JLON,KTDIA-1)&
     &-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFMC(JLON,KTDIA-1)=0._JPRB
    ENDIF
    ZFDC(JLON,KTDIA)=ZA4C(JLON)*ZFDC(JLON,KTDIA-1)
    ZZFDC(JLON,KTDIA)=ZA4C(JLON)*ZZFDC(JLON,KTDIA-1)+ZA5C(JLON)&
     &*(ZBB(JLON,KTDIA-1)-ZBB(JLON,KTDIA))
    IF (LREWS) THEN
      ZZZFDC(JLON,KTDIA)=0._JPRB
    ENDIF
    ZTU2(JLON,KTDIA)=ZA4C(JLON)
    ZTU6(JLON,KTDIA)=ZA5C(JLON)
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.4   BOUCLE SUR LES NIVEAUX, CALCULS PRELIMINAIRES PUIS
!     ELIMINATION.

!             LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN
!     ELIMINATION.

DO JLEV=KTDIA+1,KLEV
  LLREWS=(JLEV==KLEV)

  DO JLON=KIDIA,KFDIA
    ZEO1=ZEOLT(JLON,JLEV)+ZEO1TA(JLON,JLEV)
    ZEO2=ZEO2TA(JLON,JLEV)
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZZA4C4(JLON,JLEV)=ZA4C(JLON)
    ZZA5C4(JLON,JLEV)=ZA5C(JLON)
    ZEO1=ZEO1+ZEO1TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO1TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEO2=ZEO2+ZEO2TN(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
     &+ZEO2TI(JLON,JLEV)*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))

!     GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION

    ZLEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)&
     &+PNEB(JLON,JLEV)*ZA4N(JLON)
    ZDA4L(JLON,JLEV)=ZA4C(JLON)-ZA4N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZAL1(JLON)=ZB2(JLON,JLEV)
      ZAL2(JLON)=ZB1(JLON,JLEV)
      ZBE1(JLON)=1._JPRB-ZB2(JLON,JLEV)
      ZBE2(JLON)=1._JPRB-ZB1(JLON,JLEV)
      ZGA1(JLON)=1._JPRB-ZB4(JLON,JLEV)
      ZGA2(JLON)=1._JPRB-ZB3(JLON,JLEV)
      ZDE1(JLON)=ZB4(JLON,JLEV)
      ZDE2(JLON)=ZB3(JLON,JLEV)
    ELSE
      ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,JLEV)+ZA4N(JLON)*PNEB(JLON,JLEV)
      ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,JLEV)+ZA5N(JLON)*PNEB(JLON,JLEV)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
      ZFMC(JLON,JLEV-1)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZFDC(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZFDN(JLON,JLEV-1))
      ZZFMC(JLON,JLEV-1)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZZFDC(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZZFDN(JLON,JLEV-1))+ZTD1*(1._JPRB&
       &-PNEB(JLON,JLEV))*(ZA4C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA5C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZFMC(JLON,JLEV-1)=-ZTD1*(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON)
        ELSE
          ZZZFMC(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU1(JLON,JLEV)=(ZTD1*ZA5C(JLON))*(ZAL2(JLON)&
       &*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZTU2(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZAL1(JLON)
      ZTU3(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZGA1(JLON)
      ZTD2=ZA5N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD3=1._JPRB/(1._JPRB-ZA5N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
      ZFMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
       &*ZFDC(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZFDN(JLON,JLEV-1))+ZTD2&
       &*ZFMC(JLON,JLEV-1))
      ZZFMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
       &*ZZFDC(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZZFDN(JLON,JLEV-1))+ZTD2&
       &*ZZFMC(JLON,JLEV-1))+ZTD3*PNEB(JLON,JLEV)&
       &*(ZA4N(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV))+ZA5N(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZFMN(JLON,JLEV-1)=ZTD3*(ZTD2*ZZZFMC(JLON,JLEV-1)-PNEB(JLON,JLEV)&
           &*ZA4N(JLON))
        ELSE
          ZZZFMN(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU4(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZBE1(JLON)+ZTD2*ZTU2(JLON,JLEV))
      ZTU5(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZDE1(JLON)+ZTD2*ZTU3(JLON,JLEV))
    ELSE
      ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*ZTU6(JLON,JLEV-1))
      ZFMC(JLON,JLEV-1)=ZTD1*ZA5C(JLON)*ZFDC(JLON,JLEV-1)
      ZZFMC(JLON,JLEV-1)=ZTD1*ZA5C(JLON)*ZZFDC(JLON,JLEV-1)+ZTD1&
       &*(ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV))+ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZFMC(JLON,JLEV-1)=-ZTD1*ZA4C(JLON)
        ELSE
          ZZZFMC(JLON,JLEV-1)=0._JPRB
        ENDIF
      ENDIF
      ZTU2(JLON,JLEV)=ZTD1*ZA4C(JLON)
    ENDIF

  ENDDO
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTD4=ZA4C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD5=ZA4C(JLON)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZFDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZFDC(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZFDN(JLON,JLEV-1))+ZTD4&
       &*ZFMC(JLON,JLEV-1)+ZTD5*ZFMN(JLON,JLEV-1)
      ZZFDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZZFDC(JLON,JLEV-1)&
       &+ZGA2(JLON)*ZZFDN(JLON,JLEV-1))+ZTD4&
       &*ZZFMC(JLON,JLEV-1)+ZTD5*ZZFMN(JLON,JLEV-1)&
       &+(1._JPRB-PNEB(JLON,JLEV))*(ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZFDC(JLON,JLEV)=ZTD4*ZZZFMC(JLON,JLEV-1)+ZTD5*ZZZFMN(JLON,JLEV-1)&
           &-(1._JPRB-PNEB(JLON,JLEV))*ZA5C(JLON)
        ELSE
          ZZZFDC(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)*ZAL1(JLON)+ZTD4*ZTU2(JLON,JLEV)&
       &+ZTD5*ZTU4(JLON,JLEV)
      ZTU7(JLON,JLEV)=ZA5C(JLON)*ZGA1(JLON)+ZTD4*ZTU3(JLON,JLEV)&
       &+ZTD5*ZTU5(JLON,JLEV)
      ZTD6=ZA4N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU8(JLON,JLEV-1))
      ZTD7=ZA4N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)+ZDE2(JLON)&
       &*ZTU9(JLON,JLEV-1))
      ZFDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZFDC(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZFDN(JLON,JLEV-1))+ZTD6&
       &*ZFMC(JLON,JLEV-1)+ZTD7*ZFMN(JLON,JLEV-1)
      ZZFDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZZFDC(JLON,JLEV-1)&
       &+ZDE2(JLON)*ZZFDN(JLON,JLEV-1))+ZTD6&
       &*ZZFMC(JLON,JLEV-1)+ZTD7*ZZFMN(JLON,JLEV-1)&
       &+PNEB(JLON,JLEV)*(ZA5N(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4N(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2)))
      IF (LREWS) THEN
        IF (LLREWS) THEN
          ZZZFDN(JLON,JLEV)=ZTD6*ZZZFMC(JLON,JLEV-1)+ZTD7*ZZZFMN(JLON,JLEV-1)&
           &-PNEB(JLON,JLEV)*ZA5N(JLON)
        ELSE
          ZZZFDN(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU8(JLON,JLEV)=ZA5N(JLON)*ZBE1(JLON)+ZTD6*ZTU2(JLON,JLEV)&
       &+ZTD7*ZTU4(JLON,JLEV)
      ZTU9(JLON,JLEV)=ZA5N(JLON)*ZDE1(JLON)+ZTD6*ZTU3(JLON,JLEV)&
       &+ZTD7*ZTU5(JLON,JLEV)
    ELSE
      ZTD4=ZA4C(JLON)*ZTU6(JLON,JLEV-1)
      ZFDC(JLON,JLEV)=ZA4C(JLON)*ZFDC(JLON,JLEV-1)+ZTD4*ZFMC(JLON,JLEV-1)
      ZZFDC(JLON,JLEV)=ZA4C(JLON)*ZZFDC(JLON,JLEV-1)+ZTD4&
       &*ZZFMC(JLON,JLEV-1)+ZA5C(JLON)&
       &*(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))&
       &+ZA4C(JLON)*(ZBB(JLON,JLEV-1)&
       &-ZBB(JLON,JLEV-2))
      IF (LREWS) THEN
        IF (LLREWS) THEN 
          ZZZFDC(JLON,JLEV)=ZTD4*ZZZFMC(JLON,JLEV-1)-ZA5C(JLON)
        ELSE
          ZZZFDC(JLON,JLEV)=0._JPRB
        ENDIF
      ENDIF
      ZTU6(JLON,JLEV)=ZA5C(JLON)+ZTD4*ZTU2(JLON,JLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.5   TRAITEMENT EN SURFACE.

!             SURFACE TREATMENT.

DO JLON=KIDIA,KFDIA
  ZAL=1._JPRB-PEMIS(JLON)
  ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
  ZFMC(JLON,KLEV)=(ZTDS1*ZAL)*ZFDC(JLON,KLEV)
  ZZFMC(JLON,KLEV)=(ZTDS1*ZAL)*ZZFDC(JLON,KLEV)+(ZTDS1*ZAL)&
   &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
  IF (LREWS) THEN
    ZZZFMC(JLON,KLEV)=(ZTDS1*ZAL)*(ZZZFDC(JLON,KLEV)+1._JPRB)
  ENDIF

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZZFMC(JLON,KLEV)=ZZFMC(JLON,KLEV)-(ZTDS1*ZAL)*PNEB(JLON,KLEV)&
     &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
    IF (LREWS) THEN
      ZZZFMC(JLON,KLEV)=ZZZFMC(JLON,KLEV)-(ZTDS1*ZAL)*PNEB(JLON,KLEV)
    ENDIF
    ZTUS1=(ZTDS1*ZAL)*ZTU7(JLON,KLEV)
    ZTDS2=ZAL*ZTU8(JLON,KLEV)
    ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
    ZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZFDN(JLON,KLEV)+ZTDS2*ZFMC(JLON,KLEV))
    ZZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZZFDN(JLON,KLEV)+ZTDS2&
     &*ZZFMC(JLON,KLEV))+ZTDS3*ZAL*PNEB(JLON,KLEV)&
     &*(ZBB(JLON,KLEV)-ZBB(JLON,KLEV-1))
    IF (LREWS) THEN
      ZZZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZZZFDN(JLON,KLEV)+ZTDS2&
       &*ZZZFMC(JLON,KLEV))+ZTDS3*ZAL*PNEB(JLON,KLEV)
    ENDIF
    ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)+ZTUS1*ZFMN(JLON,KLEV)
    ZZFMC(JLON,KLEV)=ZZFMC(JLON,KLEV)+ZTUS1*ZZFMN(JLON,KLEV)
    IF (LREWS) THEN
      ZZZFMC(JLON,KLEV)=ZZZFMC(JLON,KLEV)+ZTUS1*ZZZFMN(JLON,KLEV)
    ENDIF
  ENDIF

ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.6   SUBSTITUTION NIVEAU PAR NIVEAU.

!             BACK-SUBSTITUTION LAYER BY LAYER.

DO JLEV=KLEV,KTDIA,-1
  DO JLON=KIDIA,KFDIA

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZFDN(JLON,JLEV)=ZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZZFDN(JLON,JLEV)=ZZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
       &*ZZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
       &*ZZFMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZFDN(JLON,JLEV)=ZZZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
         &*ZZZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
         &*ZZZFMN(JLON,JLEV)
      ENDIF
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZZFDC(JLON,JLEV)=ZZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
       &*ZZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
       &*ZZFMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZFDC(JLON,JLEV)=ZZZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
         &*ZZZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
         &*ZZZFMN(JLON,JLEV)
      ENDIF
      ZFMN(JLON,JLEV-1)=ZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)
      ZZFMN(JLON,JLEV-1)=ZZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
       &*ZZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
       &*ZZFMN(JLON,JLEV)
      IF (LREWS) THEN
        ZZZFMN(JLON,JLEV-1)=ZZZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
         &*ZZZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
         &*ZZZFMN(JLON,JLEV)
      ENDIF
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZFMN(JLON,JLEV-1)
      ZZFMC(JLON,JLEV-1)=ZZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
       &*ZZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
       &*ZZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
       &*ZZFMN(JLON,JLEV-1)
      IF (LREWS) THEN
        ZZZFMC(JLON,JLEV-1)=ZZZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         &*ZZZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
         &*ZZZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
         &*ZZZFMN(JLON,JLEV-1)
      ENDIF
    ELSE
      ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZFMC(JLON,JLEV)
      ZZFDC(JLON,JLEV)=ZZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZZFMC(JLON,JLEV)
      IF (LREWS) THEN
        ZZZFDC(JLON,JLEV)=ZZZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZZZFMC(JLON,JLEV)
      ENDIF
      ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZFMC(JLON,JLEV)
      ZZFMC(JLON,JLEV-1)=ZZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZZFMC(JLON,JLEV)
      IF (LREWS) THEN
        ZZZFMC(JLON,JLEV-1)=ZZZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         &*ZZZFMC(JLON,JLEV)
      ENDIF
    ENDIF

  ENDDO
ENDDO

ELSE

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    ZFMC(JLON,JLEV)=0._JPRB
    ZFDC(JLON,JLEV)=0._JPRB
    ZZFMC(JLON,JLEV)=0._JPRB
    ZZFDC(JLON,JLEV)=0._JPRB
    IF (LREWS) THEN
      ZZZFMC(JLON,JLEV)=0._JPRB
      ZZZFDC(JLON,JLEV)=0._JPRB
    ENDIF
    IF (LRNUMX) THEN
      ZFMN(JLON,JLEV)=0._JPRB
      ZFDN(JLON,JLEV)=0._JPRB
      ZZFMN(JLON,JLEV)=0._JPRB
      ZZFDN(JLON,JLEV)=0._JPRB
      IF (LREWS) THEN
        ZZZFMN(JLON,JLEV)=0._JPRB
        ZZZFDN(JLON,JLEV)=0._JPRB
      ENDIF
    ENDIF
  ENDDO
ENDDO

ENDIF

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     VII.7   CALCUL DES FLUX DEFINITIFS.

!             COMPUTATION OF THE FINAL FLUXES.

IF (LREWS) THEN

DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZZZTDC(JLON,KTDIA-1)-ZZZTMC(JLON,KTDIA-1)
  ZFRB(JLON)=ZZZFDC(JLON,KTDIA-1)-ZZZFMC(JLON,KTDIA-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTRB(JLON)=ZTRB(JLON)+ZZZTDN(JLON,KTDIA-1)-ZZZTMN(JLON,KTDIA-1)
    ZFRB(JLON)=ZFRB(JLON)+ZZZFDN(JLON,KTDIA-1)-ZZZFMN(JLON,KTDIA-1)
  ENDIF

  ZZTRTH(JLON,KTDIA-1)=0._JPRB
  ZZFRTH(JLON,KTDIA-1)=0._JPRB
ENDDO
!cdir unroll=8
DO JLEV=KTDIA,KLEV
  LLREWS=(JLEV==KLEV)
  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTRB(JLON)
    ZFRS(JLON)=ZFRB(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZZZTDC(JLON,JLEV)-ZZZTMC(JLON,JLEV)
    ZFRB(JLON)=ZZZFDC(JLON,JLEV)-ZZZFMC(JLON,JLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTRB(JLON)=ZTRB(JLON)+ZZZTDN(JLON,JLEV)-ZZZTMN(JLON,JLEV)
      ZFRB(JLON)=ZFRB(JLON)+ZZZFDN(JLON,JLEV)-ZZZFMN(JLON,JLEV)
    ENDIF

    IF (LLREWS) THEN
      ZTRB(JLON)=ZTRB(JLON)+1._JPRB
      ZFRB(JLON)=ZFRB(JLON)+1._JPRB
    ENDIF

    ZZTRTH(JLON,JLEV)=ZZTRTH(JLON,JLEV-1)+(ZBB(JLON,KLEV)&
     &-ZBB(JLON,JLEV-1))*(ZTRS(JLON)-ZTRB(JLON))
    ZZFRTH(JLON,JLEV)=ZZFRTH(JLON,JLEV-1)+(ZBB(JLON,KLEV)&
     &-ZBB(JLON,JLEV-1))*(ZFRS(JLON)-ZFRB(JLON))
    ZFRTH(JLON,JLEV)=ZFRTH(JLON,JLEV)-(ZZTRTH(JLON,JLEV)&
     &+ZMIXP(JLON,JLEV)*(ZZFRTH(JLON,JLEV)-ZZTRTH(JLON,JLEV)))
  ENDDO
ENDDO

ENDIF

! - TEMPORAIRE(S) 1D .

! ZZTRS     : FLUX "ZZ" NET AU SOMMET D'UNE COUCHE.
!            : "ZZ" NET FLUX AT THE TOP OF THE LAYER.
! ZDRS      : FLUX NET AU SOMMET "COOLING TO SPACE".
!            : NET TOP FLUX FOR COOLING TO SPACE.
! ZZTRB     : FLUX "ZZ" NET A LA BASE D'UNE COUCHE.
!            : "ZZ" NET FLUX AT THE BOTTOM OF THE LAYER.
! ZDRB      : FLUX NET A LA BASE "COOLING TO SPACE".
!            : NET BOTTOM FLUX FOR COOLING TO SPACE.

DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZTDC(JLON,KLEV)-ZTMC(JLON,KLEV)
  ZFRS(JLON)=ZFDC(JLON,KLEV)-ZFMC(JLON,KLEV)
  ZZTRS(JLON)=ZZTDC(JLON,KLEV)-ZZTMC(JLON,KLEV)+ZBB(JLON,KLEV)&
   &-ZBB(JLON,KLEV-1)
  ZZFRS(JLON)=ZZFDC(JLON,KLEV)-ZZFMC(JLON,KLEV)+ZBB(JLON,KLEV)&
   &-ZBB(JLON,KLEV-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,KLEV)-ZTMN(JLON,KLEV)
    ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,KLEV)-ZFMN(JLON,KLEV)
    ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,KLEV)-ZZTMN(JLON,KLEV)
    ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,KLEV)-ZZFMN(JLON,KLEV)
  ENDIF

  ZDRS(JLON)=PFRTH(JLON,KLEV)
  ZZTRTH(JLON,KLEV)=ZDRS(JLON)+ZBB(JLON,KLEV)*ZTRS(JLON)-ZZTRS(JLON)
  ZZFRTH(JLON,KLEV)=ZDRS(JLON)+ZBB(JLON,KLEV)*ZFRS(JLON)-ZZFRS(JLON)
  PFRTH(JLON,KLEV)=ZZTRTH(JLON,KLEV)+ZMIXP(JLON,KLEV)&
   &*(ZZFRTH(JLON,KLEV)-ZZTRTH(JLON,KLEV))
ENDDO
!cdir unroll=8
DO JLEV=KLEV,KTDIA+1,-1
  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZTRS(JLON)
    ZFRB(JLON)=ZFRS(JLON)
    ZZTRB(JLON)=ZZTRS(JLON)
    ZZFRB(JLON)=ZZFRS(JLON)
    ZDRB(JLON)=ZDRS(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTDC(JLON,JLEV-1)-ZTMC(JLON,JLEV-1)
    ZFRS(JLON)=ZFDC(JLON,JLEV-1)-ZFMC(JLON,JLEV-1)
    ZZTRS(JLON)=ZZTDC(JLON,JLEV-1)-ZZTMC(JLON,JLEV-1)&
     &+ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)
    ZZFRS(JLON)=ZZFDC(JLON,JLEV-1)-ZZFMC(JLON,JLEV-1)&
     &+ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV-2)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,JLEV-1)-ZTMN(JLON,JLEV-1)
      ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,JLEV-1)-ZFMN(JLON,JLEV-1)
      ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,JLEV-1)-ZZTMN(JLON,JLEV-1)
      ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,JLEV-1)-ZZFMN(JLON,JLEV-1)
    ENDIF

    ZDRS(JLON)=PFRTH(JLON,JLEV-1)
    ZZTRTH(JLON,JLEV-1)=ZZTRTH(JLON,JLEV)+ZDRS(JLON)-ZDRB(JLON)&
     &-ZBB(JLON,JLEV-1)*(ZTRB(JLON)-ZTRS(JLON))&
     &+ZZTRB(JLON)-ZZTRS(JLON)
    ZZFRTH(JLON,JLEV-1)=ZZFRTH(JLON,JLEV)+ZDRS(JLON)-ZDRB(JLON)&
     &-ZBB(JLON,JLEV-1)*(ZFRB(JLON)-ZFRS(JLON))&
     &+ZZFRB(JLON)-ZZFRS(JLON)
    PFRTH(JLON,JLEV-1)=ZZTRTH(JLON,JLEV-1)+ZMIXP(JLON,JLEV-1)&
     &*(ZZFRTH(JLON,JLEV-1)-ZZTRTH(JLON,JLEV-1))
  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZTRS(JLON)
  ZFRB(JLON)=ZFRS(JLON)
  ZZTRB(JLON)=ZZTRS(JLON)
  ZZFRB(JLON)=ZZFRS(JLON)
  ZDRB(JLON)=ZDRS(JLON)
ENDDO
DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZTDC(JLON,KTDIA-1)-ZTMC(JLON,KTDIA-1)
  ZFRS(JLON)=ZFDC(JLON,KTDIA-1)-ZFMC(JLON,KTDIA-1)
  ZZTRS(JLON)=ZZTDC(JLON,KTDIA-1)-ZZTMC(JLON,KTDIA-1)
  ZZFRS(JLON)=ZZFDC(JLON,KTDIA-1)-ZZFMC(JLON,KTDIA-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

  IF (LRNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,KTDIA-1)-ZTMN(JLON,KTDIA-1)
    ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,KTDIA-1)-ZFMN(JLON,KTDIA-1)
    ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,KTDIA-1)-ZZTMN(JLON,KTDIA-1)
    ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,KTDIA-1)-ZZFMN(JLON,KTDIA-1)
  ENDIF

  ZDRS(JLON)=PFRTH(JLON,KTDIA-1)
  ZZTRTH(JLON,KTDIA-1)=ZZTRTH(JLON,KTDIA)+ZDRS(JLON)-ZDRB(JLON)&
   &-ZBB(JLON,KTDIA-1)*(ZTRB(JLON)-ZTRS(JLON))&
   &+ZZTRB(JLON)-ZZTRS(JLON)
  ZZFRTH(JLON,KTDIA-1)=ZZFRTH(JLON,KTDIA)+ZDRS(JLON)-ZDRB(JLON)&
   &-ZBB(JLON,KTDIA-1)*(ZFRB(JLON)-ZFRS(JLON))&
   &+ZZFRB(JLON)-ZZFRS(JLON)
  PFRTH(JLON,KTDIA-1)=ZZTRTH(JLON,KTDIA-1)+ZMIXP(JLON,KTDIA-1)&
   &*(ZZFRTH(JLON,KTDIA-1)-ZZTRTH(JLON,KTDIA-1))
ENDDO

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)+ZFRTH(JLON,JLEV)
  ENDDO
ENDDO

! 'PROXIMITY CORRECTION' WITH THE INTERPOLATED EFFECT AS BASIS AND
! THE TIME-SECURED MAXIMUM EXCHANGE SITUATION AS TARGET

IF (LRPROX) THEN

!cdir unroll=8
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDEL1=-LOG(MAX(ZTRLI,ZLEPC(JLON,JLEV)))
      ZDEL2=-LOG(MAX(ZTRLI,ZLEPC(JLON,JLEV+1)))
      ZFTPP=1._JPRB
      IF (LRTPP) THEN
        ZFTPP=MAX(0._JPRB,1._JPRB-((2._JPRB-(1._JPRB-ZLEPC(JLON,JLEV))&
         &*(1._JPRB+2._JPRB/ZDEL1))+(2._JPRB-(1._JPRB-ZLEPC(JLON,JLEV+1))&
         &*(1._JPRB+2._JPRB/ZDEL2)))/((1._JPRB-ZLEPC(JLON,JLEV))+(1._JPRB&
         &-ZLEPC(JLON,JLEV+1))))
      ENDIF
      ZTARE2=(1._JPRB-ZLEPC(JLON,JLEV))*(1._JPRB-ZLEPC(JLON,JLEV+1))
      ZEFFE2=(1._JPRB-ZGEPC(JLON,JLEV))*(1._JPRB-ZGEPC(JLON,JLEV+1))
      IF (LRTDL) THEN
        ZTARSP=1._JPRB-MAX(0._JPRB,ZLEPC(JLON,JLEV)+ZLEPC(JLON,JLEV+1)&
         &-ZRPROX(JLON,JLEV)*ZLEPC(JLON,JLEV)*ZLEPC(JLON,JLEV+1))
      ENDIF
      IF (LRNUMX) THEN
        ZTARE2=ZTARE2&
         &+MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
         &-MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
         &*(ZDA4L(JLON,JLEV)*ZDA4L(JLON,JLEV+1))
        ZEFFE2=ZEFFE2&
         &+MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
         &-MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
         &*(ZDA4G(JLON,JLEV)*ZDA4G(JLON,JLEV+1))
        IF (LRTDL) THEN
          ZTARSP=MIN(1._JPRB,ZTARSP&
           &+MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
           &-MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
           &*(ZDA4L(JLON,JLEV)*ZDA4L(JLON,JLEV+1))*ZRPROX(JLON,JLEV))
        ENDIF
      ENDIF
      ZEFFE2=ZEFFE2*(1._JPRB-ZMIXP(JLON,JLEV))+ZTARE2*ZMIXP(JLON,JLEV)
      IF (LRTDL) THEN
        ZTARE2=ZTARSP
      ENDIF
      ZTARE2=ZTARE2*ZFTPP
      ZTARE2=ZTARE2/(1._JPRB+(ZDAMP(JLON,JLEV)+ZDAMP(JLON,JLEV+1))*ZTARE2)
      PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)&
       &+(ZBB(JLON,JLEV-1)-ZBB(JLON,JLEV))*(ZTARE2-ZEFFE2)
    ENDDO
  ENDDO

  IF (LREWS.AND.LRTPP) THEN
    DO JLON=KIDIA,KFDIA
      ZDEL1=-LOG(MAX(ZTRLI,ZLEPC(JLON,KLEV)))
      ZEFFE2=(1._JPRB-ZLEPC(JLON,KLEV))*PEMIS(JLON)
      ZTARE2=2._JPRB*PEMIS(JLON)*((1._JPRB-ZLEPC(JLON,KLEV))*(1._JPRB+1._JPRB&
       &/ZDEL1)-1._JPRB)
      PFRTH(JLON,KLEV)=PFRTH(JLON,KLEV)&
       &+(ZBB(JLON,KLEV-1)-ZBB(JLON,KLEV))*(ZTARE2-ZEFFE2)/(1._JPRB&
       &+ZDAMP(JLON,KLEV)*ZEFFE2)
    ENDDO
  ENDIF

  IF (.NOT.LREWS) THEN
    DO JLON=KIDIA,KFDIA
      ZTARE2=(1._JPRB-ZLEPC(JLON,KLEV))*PEMIS(JLON)
      ZEFFE2=(1._JPRB-ZGEPC(JLON,JLEV))*PEMIS(JLON)
      ZEFFE2=ZEFFE2*(1._JPRB-ZMIXP(JLON,KLEV))+ZTARE2*ZMIXP(JLON,KLEV)
      ZTARE2=ZTARE2/(1._JPRB+ZDAMP(JLON,KLEV)*ZTARE2)
      PFRTH(JLON,KLEV)=PFRTH(JLON,KLEV)&
       &+(ZBB(JLON,KLEV-1)-ZBB(JLON,KLEV))*(ZTARE2-ZEFFE2)
    ENDDO
  ENDIF
ENDIF

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)+ZFLUXC(JLON,JLEV)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  PFRTHDS(JLON)=ZBB(JLON,KLEV)+PFRTH(JLON,KLEV)/PEMIS(JLON)
ENDDO

!*
!     ------------------------------------------------------------------
!     VIII - CALCUL DES TABLEAUX DEPENDANTS DE L'ANGLE SOLAIRE ET DE
!     L'ALBEDO EFFECTIF DE SURFACE.

!            COMPUTATION OF THE SOLAR ANGLE DEPENDENT ARRAYS AND OF THE
!     EFFECTIVE SURFACE ALBEDO.

!     INVERSE DU COSINUS DE L'ANGLE ZENITHAL ET "UPSCATTERED FRACTIONS".
!     INVERSE OF THE COSINE OF THE ZENITH ANGLE AND UPSCATTERED
!     FRACTIONS.

! - TEMPORAIRE(S) 1D .

! ZMU0I     : INVERSE DU COSINUS MODIFIE DE L'ANGLE ZENITHAL.
!            : INVERSE OF THE MODIFIED COSINE OF THE ZENITH ANGLE.
! ZUSA      : "UPSCATTERED FRACTION" POUR LES AEROSOLS.
!            : AEROSOLS' UPSCATTERED FRACTION.

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    ZMU0I(JLON)=2._JPRB*ZDM0I(JLON)
  ENDDO
ENDDO

ZEO3SA=0._JPRB
ZEO4SA=0._JPRB

DO JAE=1,6
  DO JLEV=KTDIA,KLEV
    DO JN=1,IAUCR
      DO JLON=IIDIA(JN),IFDIA(JN)
        ZUSA=(0.5_JPRB+USAA(JAE)*ZMU0(JLON))/(1._JPRB+USBA*ZMU0(JLON))
        ZEO3SA(JLON,JLEV)=ZEO3SA(JLON,JLEV)+EODSA(JAE)*PDAER(JLON,JLEV,JAE)&
         & *ZUSA
        ZEO4SA(JLON,JLEV)=ZEO4SA(JLON,JLEV)+EODSA(JAE)*PDAER(JLON,JLEV,JAE)&
         & *(1._JPRB-ZUSA)
      ENDDO
    ENDDO
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     IX - CALCUL DES FLUX SOLAIRES PAR ELIMINATION/SUBSTITUTION.

!          SOLAR FLUXES' CALCULATION BY ELIMINATION/BACK-SUBSTITUTION.

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.1   SOMMET DE L'ATMOSPHERE.

!            TOP OF THE ATMOSPHERE.

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    ZFPC(JLON,KTDIA-1)=ZII0(JLON)*ZMU0(JLON)*EXP(MAX(-0.5_JPRB*ZMU0I(JLON)&
     &*ZEOSS(JLON),ZARGLI))
    ZFDC(JLON,KTDIA-1)=0._JPRB

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZFPN(JLON,KTDIA-1)=0._JPRB
      ZFDN(JLON,KTDIA-1)=0._JPRB
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.2   PREMIERE COUCHE : CALCUL DES EPAISSEURS OPTIQUES, DES
!     TRANSMISSIVITES/REFLECTIVITES ET DES COEFFICIENTS LOCAUX DE
!     GEOMETRIE NUAGEUSE.

!            FIRST LAYER : COMPUTATION OF THE OPTICAL DEPTHS, OF THE
!     TRANSMISSIVITIES/REFLECTIVITIES AND OF THE LOCAL COEFFICIENTS FOR
!     THE CLOUD GEOMETRY.

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    ZEO1=ZUEOS(JLON,KTDIA)+ZEO1SA(JLON,KTDIA)+(EORAY*PDELP(JLON,KTDIA))
    ZEO2=ZEO2SA(JLON,KTDIA)+(EORAY*PDELP(JLON,KTDIA))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZZA4C5(JLON,KTDIA)=ZA4C(JLON)
    ZZA5C5(JLON,KTDIA)=ZA5C(JLON)
    ZEO=0.5_JPRB*ZDEOS(JLON,KTDIA)+ZEOSA(JLON,KTDIA)+(EORAY*PDELP(JLON,KTDIA))
    ZMU0IC=(ZEPS/ZEO)+SIGN(MAX(ABS(ZMU0I(JLON)-(ZEPS&
     &/ZEO)),ZEPS3),(ZMU0I(JLON)-(ZEPS/ZEO)))
    ZEO3=(ZEO3SA(JLON,KTDIA)+(0.5_JPRB*(EORAY*PDELP(JLON,KTDIA))))*ZMU0IC
    ZEO4=(ZEO4SA(JLON,KTDIA)+(0.5_JPRB*(EORAY*PDELP(JLON,KTDIA))))*ZMU0IC
    ZEO5=ZEO*ZMU0IC
    ZA1C(JLON)=EXP(MAX(-ZEO5,ZARGLI))
    ZZA1C(JLON,KTDIA)=ZA1C(JLON)
    ZG1=(ZEO3*(ZEO5-ZEO1)-ZEO2*ZEO4)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
    ZG2=-(ZEO4*(ZEO5+ZEO1)+ZEO2*ZEO3)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
    ZA2C(JLON)=ZG2*(ZA1C(JLON)-ZA4C(JLON))-ZG1*ZA5C(JLON)*ZA1C(JLON)
    ZA3C(JLON)=ZG1*(1._JPRB-ZA4C(JLON)*ZA1C(JLON))-ZG2*ZA5C(JLON)
    ZZA2C(JLON,KTDIA)=ZA2C(JLON)
    ZZA3C(JLON,KTDIA)=ZA3C(JLON)
    ZEO2SN=2._JPRB*ZBSFSN(JLON,KTDIA)*ZEODSN(JLON,KTDIA)
    ZEO2SI=2._JPRB*ZBSFSI(JLON,KTDIA)*ZEODSI(JLON,KTDIA)
    ZEO1SN=ZEO2SN+2._JPRB*ZEOASN(JLON,KTDIA)
    ZEO1SI=ZEO2SI+2._JPRB*ZEOASI(JLON,KTDIA)
    ZEO1=ZEO1+ZEO1SN*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
     &+ZEO1SI*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
    ZEO2=ZEO2+ZEO2SN*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
     &+ZEO2SI*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
    ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
    ZTAU=EXP(MAX(-ZEPS,ZARGLI))
    ZRHO=ZEO2/(ZEO1+ZEPS)
    ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
     &*ZTAU)))
    ZEOSN=ZEODSN(JLON,KTDIA)+ZEOASN(JLON,KTDIA)
    ZEOSI=ZEODSI(JLON,KTDIA)+ZEOASI(JLON,KTDIA)
    ZEO=ZEO+ZEOSN*(PDELP(JLON,KTDIA)*ZQLI(JLON,KTDIA))&
     &+ZEOSI*(PDELP(JLON,KTDIA)*ZQICE(JLON,KTDIA))
    ZMU0IN=(ZEPS/ZEO)+SIGN(MAX(ABS(ZMU0I(JLON)-(ZEPS&
     &/ZEO)),ZEPS3),(ZMU0I(JLON)-(ZEPS/ZEO)))
    ZUSN=(0.5_JPRB+ZUSAN(JLON,KTDIA)*ZMU0(JLON))/&
     &(1._JPRB+ZUSBN(JLON,KTDIA)*ZMU0(JLON))
    ZUSI=(0.5_JPRB+ZUSAI(JLON,KTDIA)*ZMU0(JLON))/&
     &(1._JPRB+ZUSBI(JLON,KTDIA)*ZMU0(JLON))
    ZEO3=(ZEO3/ZMU0IC+ZUSN*((PDELP(JLON,KTDIA)&
     &*ZQLI(JLON,KTDIA))*ZEODSN(JLON,KTDIA))&
     &+ZUSI*((PDELP(JLON,KTDIA)&
     &*ZQICE(JLON,KTDIA))*ZEODSI(JLON,KTDIA)))*ZMU0IN
    ZEO4=(ZEO4/ZMU0IC+(1._JPRB-ZUSN)*((PDELP(JLON,KTDIA)&
     &*ZQLI(JLON,KTDIA))*ZEODSN(JLON,KTDIA))&
     &+(1._JPRB-ZUSI)*((PDELP(JLON,KTDIA)&
     &*ZQICE(JLON,KTDIA))*ZEODSI(JLON,KTDIA)))*ZMU0IN
    ZEO5=ZEO*ZMU0IN
    ZA1N(JLON)=EXP(MAX(-ZEO5,ZARGLI))
    ZG1=(ZEO3*(ZEO5-ZEO1)-ZEO2*ZEO4)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
    ZG2=-(ZEO4*(ZEO5+ZEO1)+ZEO2*ZEO3)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
    ZA2N(JLON)=ZG2*(ZA1N(JLON)-ZA4N(JLON))-ZG1*ZA5N(JLON)*ZA1N(JLON)
    ZA3N(JLON)=ZG1*(1._JPRB-ZA4N(JLON)*ZA1N(JLON))-ZG2*ZA5N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZAL1(JLON)=ZB2(JLON,KTDIA)
      ZAL2(JLON)=ZB1(JLON,KTDIA)
      ZBE1(JLON)=1._JPRB-ZB2(JLON,KTDIA)
      ZBE2(JLON)=1._JPRB-ZB1(JLON,KTDIA)
      ZGA1(JLON)=1._JPRB-ZB4(JLON,KTDIA)
      ZGA2(JLON)=1._JPRB-ZB3(JLON,KTDIA)
      ZDE1(JLON)=ZB4(JLON,KTDIA)
      ZDE2(JLON)=ZB3(JLON,KTDIA)
    ELSE
      ZA1C(JLON)=ZA1C(JLON)*ZB1(JLON,KTDIA)+ZA1N(JLON)*PNEB(JLON,KTDIA)
      ZA2C(JLON)=ZA2C(JLON)*ZB1(JLON,KTDIA)+ZA2N(JLON)*PNEB(JLON,KTDIA)
      ZA3C(JLON)=ZA3C(JLON)*ZB1(JLON,KTDIA)+ZA3N(JLON)*PNEB(JLON,KTDIA)
      ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,KTDIA)+ZA4N(JLON)*PNEB(JLON,KTDIA)
      ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,KTDIA)+ZA5N(JLON)*PNEB(JLON,KTDIA)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.3   PREMIERE COUCHE, ELIMINATION (SANS PROBLEMES).

!            FIRST LAYER, ELIMINATION (EASY).

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZFMC(JLON,KTDIA-1)=ZA3C(JLON)*(ZAL2(JLON)*ZFPC(JLON,KTDIA-1))
      ZFPC(JLON,KTDIA)=ZA1C(JLON)*(ZAL2(JLON)*ZFPC(JLON,KTDIA-1))
      ZFDC(JLON,KTDIA)=ZA2C(JLON)*(ZAL2(JLON)*ZFPC(JLON,KTDIA-1))
      ZFMN(JLON,KTDIA-1)=ZA3N(JLON)*(ZBE2(JLON)*ZFPC(JLON,KTDIA-1))
      ZFPN(JLON,KTDIA)=ZA1N(JLON)*(ZBE2(JLON)*ZFPC(JLON,KTDIA-1))
      ZFDN(JLON,KTDIA)=ZA2N(JLON)*(ZBE2(JLON)*ZFPC(JLON,KTDIA-1))
      ZTU1(JLON,KTDIA)=0._JPRB
      ZTU2(JLON,KTDIA)=ZAL1(JLON)*ZA4C(JLON)
      ZTU3(JLON,KTDIA)=ZGA1(JLON)*ZA4C(JLON)
      ZTU4(JLON,KTDIA)=ZBE1(JLON)*ZA4N(JLON)
      ZTU5(JLON,KTDIA)=ZDE1(JLON)*ZA4N(JLON)
      ZTU6(JLON,KTDIA)=ZAL1(JLON)*ZA5C(JLON)
      ZTU7(JLON,KTDIA)=ZGA1(JLON)*ZA5C(JLON)
      ZTU8(JLON,KTDIA)=ZBE1(JLON)*ZA5N(JLON)
      ZTU9(JLON,KTDIA)=ZDE1(JLON)*ZA5N(JLON)
    ELSE
      ZFMC(JLON,KTDIA-1)=ZA3C(JLON)*ZFPC(JLON,KTDIA-1)
      ZFPC(JLON,KTDIA)=ZA1C(JLON)*ZFPC(JLON,KTDIA-1)
      ZFDC(JLON,KTDIA)=ZA2C(JLON)*ZFPC(JLON,KTDIA-1)
      ZTU2(JLON,KTDIA)=ZA4C(JLON)
      ZTU6(JLON,KTDIA)=ZA5C(JLON)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.4   BOUCLE SUR LES NIVEAUX, CALCULS PRELIMINAIRES PUIS
!     ELIMINATION.

!            LOOP OVER THE LAYERS, PRELIMINARY COMPUTATIONS AND THEN
!     ELIMINATION.

DO JLEV=KTDIA+1,KLEV
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      ZEO1=ZUEOS(JLON,JLEV)+ZEO1SA(JLON,JLEV)+(EORAY*PDELP(JLON,JLEV))
      ZEO2=ZEO2SA(JLON,JLEV)+(EORAY*PDELP(JLON,JLEV))
      ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
      ZTAU=EXP(MAX(-ZEPS,ZARGLI))
      ZRHO=ZEO2/(ZEO1+ZEPS)
      ZA4C(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))
      ZA5C(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))
      ZZA4C5(JLON,JLEV)=ZA4C(JLON)
      ZZA5C5(JLON,JLEV)=ZA5C(JLON)
      ZEO=0.5_JPRB*ZDEOS(JLON,JLEV)+ZEOSA(JLON,JLEV)+(EORAY*PDELP(JLON,JLEV))
      ZMU0IC=(ZEPS/ZEO)+SIGN(MAX(ABS(ZMU0I(JLON)-(ZEPS&
       &/ZEO)),ZEPS3),(ZMU0I(JLON)-(ZEPS/ZEO)))
      ZEO3=(ZEO3SA(JLON,JLEV)+(0.5_JPRB*(EORAY*PDELP(JLON,JLEV))))*ZMU0IC
      ZEO4=(ZEO4SA(JLON,JLEV)+(0.5_JPRB*(EORAY*PDELP(JLON,JLEV))))*ZMU0IC
      ZEO5=ZEO*ZMU0IC
      ZA1C(JLON)=EXP(MAX(-ZEO5,ZARGLI))
      ZZA1C(JLON,JLEV)=ZA1C(JLON)
      ZG1=(ZEO3*(ZEO5-ZEO1)-ZEO2*ZEO4)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
      ZG2=-(ZEO4*(ZEO5+ZEO1)+ZEO2*ZEO3)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
      ZA2C(JLON)=ZG2*(ZA1C(JLON)-ZA4C(JLON))-ZG1*ZA5C(JLON)*ZA1C(JLON)
      ZA3C(JLON)=ZG1*(1._JPRB-ZA4C(JLON)*ZA1C(JLON))-ZG2*ZA5C(JLON)
      ZZA2C(JLON,JLEV)=ZA2C(JLON)
      ZZA3C(JLON,JLEV)=ZA3C(JLON)
      ZEO2SN=2._JPRB*ZBSFSN(JLON,JLEV)*ZEODSN(JLON,JLEV)
      ZEO2SI=2._JPRB*ZBSFSI(JLON,JLEV)*ZEODSI(JLON,JLEV)
      ZEO1SN=ZEO2SN+2._JPRB*ZEOASN(JLON,JLEV)
      ZEO1SI=ZEO2SI+2._JPRB*ZEOASI(JLON,JLEV)
      ZEO1=ZEO1+ZEO1SN*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
       &+ZEO1SI*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
      ZEO2=ZEO2+ZEO2SN*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
       &+ZEO2SI*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
      ZEPS=SQRT(ZEO1*ZEO1-ZEO2*ZEO2)
      ZTAU=EXP(MAX(-ZEPS,ZARGLI))
      ZRHO=ZEO2/(ZEO1+ZEPS)
      ZA4N(JLON)=ZTAU*(1._JPRB-(ZRHO*ZRHO))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))
      ZA5N(JLON)=ZRHO*(1._JPRB-(ZTAU*ZTAU))*(1._JPRB/(1._JPRB-(ZRHO*ZRHO)*(ZTAU&
       &*ZTAU)))
      ZEOSN=ZEODSN(JLON,JLEV)+ZEOASN(JLON,JLEV)
      ZEOSI=ZEODSI(JLON,JLEV)+ZEOASI(JLON,JLEV)
      ZEO=ZEO+ZEOSN*(PDELP(JLON,JLEV)*ZQLI(JLON,JLEV))&
       &+ZEOSI*(PDELP(JLON,JLEV)*ZQICE(JLON,JLEV))
      ZMU0IN=(ZEPS/ZEO)+SIGN(MAX(ABS(ZMU0I(JLON)-(ZEPS&
       &/ZEO)),ZEPS3),(ZMU0I(JLON)-(ZEPS/ZEO)))
      ZUSN=(0.5_JPRB+ZUSAN(JLON,JLEV)*ZMU0(JLON))/&
       &(1._JPRB+ZUSBN(JLON,JLEV)*ZMU0(JLON))
      ZUSI=(0.5_JPRB+ZUSAI(JLON,JLEV)*ZMU0(JLON))/&
       &(1._JPRB+ZUSBI(JLON,JLEV)*ZMU0(JLON))
      ZEO3=(ZEO3/ZMU0IC+ZUSN*((PDELP(JLON,JLEV)&
       &*ZQLI(JLON,JLEV))*ZEODSN(JLON,JLEV))&
       &+ZUSI*((PDELP(JLON,JLEV)&
       &*ZQICE(JLON,JLEV))*ZEODSI(JLON,JLEV)))*ZMU0IN
      ZEO4=(ZEO4/ZMU0IC+(1._JPRB-ZUSN)*((PDELP(JLON,JLEV)&
       &*ZQLI(JLON,JLEV))*ZEODSN(JLON,JLEV))&
       &+(1._JPRB-ZUSI)*((PDELP(JLON,JLEV)&
       &*ZQICE(JLON,JLEV))*ZEODSI(JLON,JLEV)))*ZMU0IN
      ZEO5=ZEO*ZMU0IN
      ZA1N(JLON)=EXP(MAX(-ZEO5,ZARGLI))
      ZG1=(ZEO3*(ZEO5-ZEO1)-ZEO2*ZEO4)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
      ZG2=-(ZEO4*(ZEO5+ZEO1)+ZEO2*ZEO3)*(1._JPRB/(ZEO5*ZEO5-ZEPS*ZEPS))
      ZA2N(JLON)=ZG2*(ZA1N(JLON)-ZA4N(JLON))-ZG1*ZA5N(JLON)*ZA1N(JLON)
      ZA3N(JLON)=ZG1*(1._JPRB-ZA4N(JLON)*ZA1N(JLON))-ZG2*ZA5N(JLON)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

      IF (LRNUMX) THEN
        ZAL1(JLON)=ZB2(JLON,JLEV)
        ZAL2(JLON)=ZB1(JLON,JLEV)
        ZBE1(JLON)=1._JPRB-ZB2(JLON,JLEV)
        ZBE2(JLON)=1._JPRB-ZB1(JLON,JLEV)
        ZGA1(JLON)=1._JPRB-ZB4(JLON,JLEV)
        ZGA2(JLON)=1._JPRB-ZB3(JLON,JLEV)
        ZDE1(JLON)=ZB4(JLON,JLEV)
        ZDE2(JLON)=ZB3(JLON,JLEV)
      ELSE
        ZA1C(JLON)=ZA1C(JLON)*ZB1(JLON,JLEV)+ZA1N(JLON)*PNEB(JLON,JLEV)
        ZA2C(JLON)=ZA2C(JLON)*ZB1(JLON,JLEV)+ZA2N(JLON)*PNEB(JLON,JLEV)
        ZA3C(JLON)=ZA3C(JLON)*ZB1(JLON,JLEV)+ZA3N(JLON)*PNEB(JLON,JLEV)
        ZA4C(JLON)=ZA4C(JLON)*ZB1(JLON,JLEV)+ZA4N(JLON)*PNEB(JLON,JLEV)
        ZA5C(JLON)=ZA5C(JLON)*ZB1(JLON,JLEV)+ZA5N(JLON)*PNEB(JLON,JLEV)
      ENDIF

    ENDDO
  ENDDO
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

      IF (LRNUMX) THEN
        ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)&
         &+ZGA2(JLON)*ZTU8(JLON,JLEV-1)))
        ZFMC(JLON,JLEV-1)=ZTD1*(ZA5C(JLON)*(ZAL2(JLON)&
         &*ZFDC(JLON,JLEV-1)+ZGA2(JLON)&
         &*ZFDN(JLON,JLEV-1))+ZA3C(JLON)&
         &*(ZAL2(JLON)*ZFPC(JLON,JLEV-1)+ZGA2(JLON)&
         &*ZFPN(JLON,JLEV-1)))
        ZTU1(JLON,JLEV)=ZTD1*ZA5C(JLON)*(ZAL2(JLON)&
         &*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
         &*ZTU9(JLON,JLEV-1))
        ZTU2(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZAL1(JLON)
        ZTU3(JLON,JLEV)=(ZTD1*ZA4C(JLON))*ZGA1(JLON)
        ZTD2=ZA5N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
         &*ZTU8(JLON,JLEV-1))
        ZTD3=1._JPRB/(1._JPRB-ZA5N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)&
         &+ZDE2(JLON)*ZTU9(JLON,JLEV-1))-ZTD2*ZTU1(JLON,JLEV))
        ZFMN(JLON,JLEV-1)=ZTD3*(ZA5N(JLON)*(ZBE2(JLON)&
         &*ZFDC(JLON,JLEV-1)+ZDE2(JLON)&
         &*ZFDN(JLON,JLEV-1))+ZTD2*ZFMC(JLON,JLEV-1)&
         &+ZA3N(JLON)*(ZBE2(JLON)*ZFPC(JLON,JLEV-1)&
         &+ZDE2(JLON)*ZFPN(JLON,JLEV-1)))
        ZTU4(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZBE1(JLON)+ZTD2*ZTU2(JLON,JLEV))
        ZTU5(JLON,JLEV)=ZTD3*(ZA4N(JLON)*ZDE1(JLON)+ZTD2*ZTU3(JLON,JLEV))
      ELSE
        ZTD1=1._JPRB/(1._JPRB-ZA5C(JLON)*ZTU6(JLON,JLEV-1))
        ZFMC(JLON,JLEV-1)=ZTD1*(ZA5C(JLON)*ZFDC(JLON,JLEV-1)&
         &+ZA3C(JLON)*ZFPC(JLON,JLEV-1))
        ZTU2(JLON,JLEV)=ZTD1*ZA4C(JLON)
      ENDIF

    ENDDO
  ENDDO
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

      IF (LRNUMX) THEN
        ZFPC(JLON,JLEV)=ZA1C(JLON)*(ZAL2(JLON)*ZFPC(JLON,JLEV-1)&
         &+ZGA2(JLON)*ZFPN(JLON,JLEV-1))
        ZFPN(JLON,JLEV)=ZA1N(JLON)*(ZBE2(JLON)*ZFPC(JLON,JLEV-1)&
         &+ZDE2(JLON)*ZFPN(JLON,JLEV-1))
        ZTD4=ZA4C(JLON)*(ZAL2(JLON)*ZTU6(JLON,JLEV-1)+ZGA2(JLON)&
         &*ZTU8(JLON,JLEV-1))
        ZTD5=ZA4C(JLON)*(ZAL2(JLON)*ZTU7(JLON,JLEV-1)+ZGA2(JLON)&
         &*ZTU9(JLON,JLEV-1))
        ZFDC(JLON,JLEV)=ZA4C(JLON)*(ZAL2(JLON)*ZFDC(JLON,JLEV-1)&
         &+ZGA2(JLON)*ZFDN(JLON,JLEV-1))+ZTD4&
         &*ZFMC(JLON,JLEV-1)+ZTD5*ZFMN(JLON,JLEV-1)&
         &+ZA2C(JLON)*(ZAL2(JLON)*ZFPC(JLON,JLEV-1)&
         &+ZGA2(JLON)*ZFPN(JLON,JLEV-1))
        ZTU6(JLON,JLEV)=ZA5C(JLON)*ZAL1(JLON)+ZTD4*ZTU2(JLON,JLEV)&
         &+ZTD5*ZTU4(JLON,JLEV)
        ZTU7(JLON,JLEV)=ZA5C(JLON)*ZGA1(JLON)+ZTD4*ZTU3(JLON,JLEV)&
         &+ZTD5*ZTU5(JLON,JLEV)
        ZTD6=ZA4N(JLON)*(ZBE2(JLON)*ZTU6(JLON,JLEV-1)+ZDE2(JLON)&
         &*ZTU8(JLON,JLEV-1))
        ZTD7=ZA4N(JLON)*(ZBE2(JLON)*ZTU7(JLON,JLEV-1)+ZDE2(JLON)&
         &*ZTU9(JLON,JLEV-1))
        ZFDN(JLON,JLEV)=ZA4N(JLON)*(ZBE2(JLON)*ZFDC(JLON,JLEV-1)&
         &+ZDE2(JLON)*ZFDN(JLON,JLEV-1))+ZTD6&
         &*ZFMC(JLON,JLEV-1)+ZTD7*ZFMN(JLON,JLEV-1)&
         &+ZA2N(JLON)*(ZBE2(JLON)*ZFPC(JLON,JLEV-1)&
         &+ZDE2(JLON)*ZFPN(JLON,JLEV-1))
        ZTU8(JLON,JLEV)=ZA5N(JLON)*ZBE1(JLON)+ZTD6*ZTU2(JLON,JLEV)&
         &+ZTD7*ZTU4(JLON,JLEV)
        ZTU9(JLON,JLEV)=ZA5N(JLON)*ZDE1(JLON)+ZTD6*ZTU3(JLON,JLEV)&
         &+ZTD7*ZTU5(JLON,JLEV)
      ELSE
        ZFPC(JLON,JLEV)=ZA1C(JLON)*ZFPC(JLON,JLEV-1)
        ZTD4=ZA4C(JLON)*ZTU6(JLON,JLEV-1)
        ZFDC(JLON,JLEV)=ZA4C(JLON)*ZFDC(JLON,JLEV-1)+ZTD4&
         &*ZFMC(JLON,JLEV-1)+ZA2C(JLON)&
         &*ZFPC(JLON,JLEV-1)
        ZTU6(JLON,JLEV)=ZA5C(JLON)+ZTD4*ZTU2(JLON,JLEV)
      ENDIF

    ENDDO
  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.5   TRAITEMENT EN SURFACE.

!            SURFACE TREATMENT.

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    ZAL =PALB   (JLON)
    ZALP=PALBDIR(JLON)
    ZTDS1=1._JPRB/(1._JPRB-ZAL*ZTU6(JLON,KLEV))
    ZFMC(JLON,KLEV)=ZTDS1*(ZAL*ZFDC(JLON,KLEV)+ZALP*ZFPC(JLON,KLEV))

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      ZTUS1=ZTDS1*ZAL*ZTU7(JLON,KLEV)
      ZTDS2=ZAL*ZTU8(JLON,KLEV)
      ZTDS3=1._JPRB/(1._JPRB-ZAL*ZTU9(JLON,KLEV)-ZTDS2*ZTUS1)
      ZFMN(JLON,KLEV)=ZTDS3*(ZAL*ZFDN(JLON,KLEV)+ZTDS2&
       &*ZFMC(JLON,KLEV)+ZALP*ZFPN(JLON,KLEV))
      ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)+ZTUS1*ZFMN(JLON,KLEV)
    ENDIF

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.6   SUBSTITUTION NIVEAU PAR NIVEAU.

!            BACK-SUBSTITUTION LAYER BY LAYER.

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1
  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

      IF (LRNUMX) THEN
        ZFDN(JLON,JLEV)=ZFDN(JLON,JLEV)+ZTU8(JLON,JLEV)&
         &*ZFMC(JLON,JLEV)+ZTU9(JLON,JLEV)&
         &*ZFMN(JLON,JLEV)
        ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)&
         &*ZFMC(JLON,JLEV)+ZTU7(JLON,JLEV)&
         &*ZFMN(JLON,JLEV)
        ZFMN(JLON,JLEV-1)=ZFMN(JLON,JLEV-1)+ZTU4(JLON,JLEV)&
         &*ZFMC(JLON,JLEV)+ZTU5(JLON,JLEV)&
         &*ZFMN(JLON,JLEV)
        ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)&
         &*ZFMC(JLON,JLEV)+ZTU3(JLON,JLEV)&
         &*ZFMN(JLON,JLEV)+ZTU1(JLON,JLEV)&
         &*ZFMN(JLON,JLEV-1)
      ELSE
        ZFDC(JLON,JLEV)=ZFDC(JLON,JLEV)+ZTU6(JLON,JLEV)*ZFMC(JLON,JLEV)
        ZFMC(JLON,JLEV-1)=ZFMC(JLON,JLEV-1)+ZTU2(JLON,JLEV)*ZFMC(JLON,JLEV)
      ENDIF

    ENDDO

  ENDDO
ENDDO

!*
!- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
!     IX.7   CALCUL DES FLUX.

!            FLUXES' COMPUTATION.

DO JLEV=KTDIA-1,KLEV

!     MISE A ZERO DES FLUX EN SECURITE POUR LE CAS NOCTURNE.
!     SETTING FLUXES TO ZERO FOR SECURITY IN THE NIGHT-TIME CASE.

  DO JLON=KIDIA,KFDIA
    PFRSO(JLON,JLEV)=0._JPRB
  ENDDO

!     CALCUL EFFECTIF.
!     ACTUAL COMPUTATION.

  DO JN=1,IAUCR
    DO JLON=IIDIA(JN),IFDIA(JN)
      PFRSO(JLON,JLEV)=ZFPC(JLON,JLEV)+ZFDC(JLON,JLEV)-ZFMC(JLON,JLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

      IF (LRNUMX) THEN
        PFRSO(JLON,JLEV)=PFRSO(JLON,JLEV)+ZFPN(JLON,JLEV)&
         &+ZFDN(JLON,JLEV)-ZFMN(JLON,JLEV)
      ENDIF

    ENDDO
  ENDDO
ENDDO

!     DIAGN. FLUX SOLAIRE PARALLELE ET DIFFUS VERS LE BAS EN SURFACE.
!     DIAGNOSTIC OF PARALLEL AND DIFFUSE DOWNWARD FLUXES AT THE SURFACE.

!     MISE A ZERO DES FLUX EN SECURITE POUR LE CAS NOCTURNE.
!     SETTING FLUXES TO ZERO FOR SECURITY IN THE NIGHT-TIME CASE.

DO JLON=KIDIA,KFDIA
  PFRSODS(JLON)=0._JPRB
  PFRSOPS(JLON)=0._JPRB
ENDDO

!     CALCUL EFFECTIF.
!     ACTUAL COMPUTATION.

DO JN=1,IAUCR
  DO JLON=IIDIA(JN),IFDIA(JN)
    PFRSODS(JLON)=ZFDC(JLON,KLEV)
    PFRSOPS(JLON)=ZFPC(JLON,KLEV)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LRNUMX) THEN
      PFRSODS(JLON)=PFRSODS(JLON)+ZFDN(JLON,KLEV)
      PFRSOPS(JLON)=PFRSOPS(JLON)+ZFPN(JLON,KLEV)
    ENDIF

  ENDDO
ENDDO

!     PAS DE DIVERGENCE DES FLUX AU DESSUS DE KTDIA.
!     NO FLUX DIVERGENCE ABOVE KTDIA.

DO JLEV=0,KTDIA-2
  DO JLON=KIDIA,KFDIA
    PFRSO(JLON,JLEV)=PFRSO(JLON,KTDIA-1)
    PFRTH(JLON,JLEV)=PFRTH(JLON,KTDIA-1)
  ENDDO
ENDDO

IF(LRAYLU) THEN

! CALCUL DU FLUX LUNAIRE DESCENDANT EN SURFACE.
! ATTENTION: ON VEUT LE SEUL FLUX LUNAIRE,
! AUSSI CE FLUX EST MIS A ZERO LA OU LE SOLEIL EST LEVE.

! COMPUTE LUNAR DOWNWARD FLUX AT SURFACE.
! WARNING: ONE COMPUTES THE MERE LUNAR FLUX,
! SO THIS FLUX IS PUT TO ZERO WHERE THE SUN IS UP.

  DO JLON=KIDIA,KFDIA
    PFRSOLU(JLON)=1._JPRB/(MAX(ZEPSAL,1._JPRB-PALB(JLON)))*PFRSO(JLON,KLEV)&
     &*0.5_JPRB*(1._JPRB+SIGN(1._JPRB,0._JPRB-PMU0(JLON)))
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    PFRSOLU(JLON)=0._JPRB
  ENDDO

ENDIF

!-----------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACRANEB',1,ZHOOK_HANDLE)
END SUBROUTINE ACRANEB
