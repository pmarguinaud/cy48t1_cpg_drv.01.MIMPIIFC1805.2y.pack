SUBROUTINE ACRANEB_SOLVT( &
! - INPUT
 & YDPHY, &
 & LDNUMX,KIDIA,KFDIA,KLON,KTDIA,KLEV, &
! - INPUT 2D
 & PQICE,PQLI,PNEB,PB1,PB2,PB3,PB4,PDELP,PBB, &
 & PDEOTI,PDEOTI2,PUEOTI,PUEOTI2,PEOLT,PEOXT,PRPROX,PMIXP,PFLUXC, &
 & PEO1TI,PEO2TI,PEO1TN,PEO2TN,PEO1TA,PEO2TA,PDAMP, &
! - INPUT 1D
 & PEMIS,PRSURF, &
! - OUTPUT 2D
 & PFRTH, &
! - OUTPUT 1D
 & PFRTHDS)

! Purpose:
! --------
!   ACRANEB_SOLVT - NER solver (thermal band).

! Interface:
! ----------
! INPUT:
!   LDNUMX  - former LRNUMX, false for random overlaps
!   KIDIA   - initial index for horizontal loops
!   KFDIA   - final index for horizontal loops
!   KLON    - horizontal dimension of arrays
!   KTDIA   - initial index for vertical loops (usually 1)
!   KLEV    - vertical dimension of full level arrays
!   PQICE   - specific mass of cloud ice INSIDE CLOUD
!   PQLI    - specific mass of cloud liquid INSIDE CLOUD
!   PNEB    - cloud fraction
!   PB1     - 1st intermediate storage for cloud geometry
!   PB2     - 2nd intermediate storage for cloud geometry
!   PB3     - 3rd intermediate storage for cloud geometry
!   PB4     - 4th intermediate storage for cloud geometry
!   PDELP   - pressure thickness of the layer
!   PBB     - blackbody flux at the top of the layer
!   PDEOTI  - incremental gaseous optical depth (thermal descending),
!             using dB/dT(T0) based weights
!   PDEOTI2 - incremental gaseous optical depth (thermal descending, CTS),
!             including linear correction in (T_emit - T_local)
!   PUEOTI  - incremental gaseous optical depth (thermal ascending)
!             using dB/dT(T0) based weights
!   PUEOTI2 - incremental gaseous optical depth (thermal ascending),
!             including linear correction in (T_emit - T_local)
!   PEOLT   - local gaseous optical depth, dB/dT(T0) weights
!   PEOXT   - maximum gaseous optical depth for EBL, resp. EBL-EAL flux,
!             dB/dT(T0) weights
!   PRPROX  - correction term for adjacent exchanges
!   PMIXP   - bracketing weight
!   PFLUXC  - bracketing offset
!   PEO1TI  - alpha1.d(delta) for ice clouds
!   PEO2TI  - alpha2.d(delta) for ice clouds
!   PEO1TN  - alpha1.d(delta) for liquid clouds
!   PEO2TN  - alpha2.d(delta) for liquid clouds
!   PEO1TA  - alpha1.d(delta) for aerosols
!   PEO2TA  - alpha2.d(delta) for aerosols
!   PDAMP   - damping coefficient for time-secure handling of flux divergences
!   PEMIS   - surface emissivity
!   PRSURF  - corrective ratio for surface CTS contribution
! OUTPUT:
!   PFRTH   - net thermal flux (positive downwards)
!   PFRTHDS - downward thermal flux at surface
 
! Externals:
! ----------
!   ACRANEB_COEFT
!   ACRANEB_SOLVT1
!   ACRANEB_SOLVT3

! Method:
! -------

! Reference:
! ----------

! Author:
! -------
!   1989-12, J.-F. Geleyn (original ACRANEB)

! Modifications:
! --------------
!   2019-09, J. Masek
!   NER solver externalized from ACRANEB2 as a stand-alone subroutine
!   ACRANEB_SOLVT. Former ACRANEB_SOLVT renamed to ACRANEB_SOLVT1.
! End Modifications
!-------------------------------------------------------------------------------

USE PARKIND1 ,ONLY : JPIM     ,JPRB
USE YOMPHY   ,ONLY : TPHY
USE YOMHOOK  ,ONLY : LHOOK    ,DR_HOOK

!-------------------------------------------------------------------------------

IMPLICIT NONE

TYPE(TPHY),INTENT(IN):: YDPHY

LOGICAL,INTENT(IN) :: LDNUMX

INTEGER(KIND=JPIM),INTENT(IN) :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA 
INTEGER(KIND=JPIM),INTENT(IN) :: KLON 
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA 
INTEGER(KIND=JPIM),INTENT(IN) :: KLEV 

REAL(KIND=JPRB),INTENT(IN)  :: PQICE(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PQLI(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PNEB(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PB1(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PB2(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PB3(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PB4(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PDELP(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PBB(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PDEOTI(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PDEOTI2(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PUEOTI(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PUEOTI2(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEOLT(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEOXT(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PRPROX(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PMIXP(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PFLUXC(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO1TI(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO2TI(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO1TN(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO2TN(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO1TA(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEO2TA(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PDAMP(KLON,KLEV)
REAL(KIND=JPRB),INTENT(IN)  :: PEMIS(KLON)
REAL(KIND=JPRB),INTENT(IN)  :: PRSURF(KLON)
REAL(KIND=JPRB),INTENT(OUT) :: PFRTH(KLON,0:KLEV)
REAL(KIND=JPRB),INTENT(OUT) :: PFRTHDS(KLON)

!-------------------------------------------------------------------------------

! minimum gaseous optical depth
REAL(KIND=JPRB) :: ZEONT(KLON,0:KLEV)

! transmissions and reflectivities
REAL(KIND=JPRB) :: ZA4C(KLON,KLEV),ZA5C(KLON,KLEV)&
  &,ZA4N(KLON,KLEV),ZA5N(KLON,KLEV)

! right hand sides (in) / fluxes (out)
REAL(KIND=JPRB) :: ZFDC(KLON,0:KLEV),ZFDN(KLON,0:KLEV)&
  &,ZFMC(KLON,0:KLEV),ZFMN(KLON,0:KLEV)&
  &,ZZFDC(KLON,0:KLEV),ZZFDN(KLON,0:KLEV)&
  &,ZZFMC(KLON,0:KLEV),ZZFMN(KLON,0:KLEV)&
  &,ZZZFDC(KLON,0:KLEV),ZZZFDN(KLON,0:KLEV)&
  &,ZZZFMC(KLON,0:KLEV),ZZZFMN(KLON,0:KLEV)&
  &,ZTDC(KLON,0:KLEV),ZTDN(KLON,0:KLEV)&
  &,ZTMC(KLON,0:KLEV),ZTMN(KLON,0:KLEV)&
  &,ZZTDC(KLON,0:KLEV),ZZTDN(KLON,0:KLEV)&
  &,ZZTMC(KLON,0:KLEV),ZZTMN(KLON,0:KLEV)&
  &,ZZZTDC(KLON,0:KLEV),ZZZTDN(KLON,0:KLEV)&
  &,ZZZTMC(KLON,0:KLEV),ZZZTMN(KLON,0:KLEV)

! net fluxes
REAL(KIND=JPRB) :: ZDRB(KLON),ZDRS(KLON)&
  &,ZTRB(KLON),ZTRS(KLON),ZZTRB(KLON),ZZTRS(KLON)&
  &,ZFRB(KLON),ZFRS(KLON),ZZFRB(KLON),ZZFRS(KLON)

! various auxiliary arrays
REAL(KIND=JPRB) :: ZFRTH(KLON,0:KLEV)&
  &,ZZFRTH(KLON,0:KLEV),ZZTRTH(KLON,0:KLEV)&
  &,ZGEPC(KLON,KLEV),ZLEPC(KLON,KLEV),ZXEPC(KLON,KLEV)&
  &,ZDA4G(KLON,KLEV),ZDA4L(KLON,KLEV),ZDA4X(KLON,KLEV)

! local integer scalars
INTEGER(KIND=JPIM) :: JLEV,JLON

! local logical scalars
LOGICAL :: LLREWS

! local real scalars
REAL(KIND=JPRB) :: ZARGLI,ZTRLI,ZEFFE2,ZTARE2,ZDEL1,ZDEL2,ZFTPP,ZTARSP
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-------------------------------------------------------------------------------

#include "acraneb_coeft.intfb.h"
#include "acraneb_solvt1.intfb.h"
#include "acraneb_solvt3.intfb.h"

!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ACRANEB_SOLVT',0,ZHOOK_HANDLE)

ASSOCIATE(LRTPP =>YDPHY%LRTPP,LRPROX=>YDPHY%LRPROX)

! security constants
ZARGLI=-250._JPRB
ZTRLI=EXP(ZARGLI)

! compute minimum gaseous optical depth
DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    ZEONT(JLON,JLEV)=MIN(PUEOTI(JLON,JLEV),PDEOTI(JLON,JLEV))
  ENDDO
ENDDO

!     I-III - CALCUL DES COMPLEMENTS A UN DES FLUX THERMIQUES EN ATMOSPHERE
!     ISOTHERME NORMALISEE PAR ELIMINATION/SUBSTITUTION PUIS UTILISATION
!     DES RESULTATS POUR LES CALCULS DE FLUX. DANS TOUTE LA SUITE LES
!     QUANTITES ZF (OU ZZF DANS LE CAS THERMIQUE AVEC TEMPERATURES
!     REELLES) DESIGNERONT LES SECONDS MEMBRES DES SYSTEMES LINEAIRES
!     "ADDING METHOD" ET LES QUANTITES ZTU LES ELEMENTS SURDIAGONAUX A
!     UTILISER DANS LA PARTIE SUBSTITUTION DE LA SOLUTION. IL FAUT
!     EGALEMENT SIGNALER QUE DANS LE CAS "RECOUVREMENT AU HASARD" LE
!     SUFFIXE "C" (CLAIR) REPRESENTE TOUTE LA COUCHE ET QUE LE SUFFIXE
!     "N" (NUAGEUX) REPRESENTE DES TABLEAUX INUTILISES.

!     I-III - COMPUTATION OF THE COMPLEMENTS TO ONE OF THE THERMAL FLUXES
!     IN A NORMALIZED ISOTHERMAL ATMOSPHERE VIA ELIMINATION/BACK-
!     SUBSTITUTION AND USE OF THE RESULT TO COMPUTE FLUXES. IN ALL THE
!     FOLLOWING THE QUANTITIES ZF (OR ZZF IN THE THERMAL CASE WITH
!     REAL TEMPERATURES) WILL INDICATE THE RIGHT HAND SIDES OF THE
!     "ADDING METHOD" LINEAR SYSTEMS AND THE QUANTITIES ZTU THE
!     SUPERDIAGONAL ELEMENTS TO BE USED IN THE BACK-SUBSTITUTION PART
!     OF THE ALGORITHM. ONE MUST ALSO MENTION THAT IN THE "RANDOM
!     OVERLAP" CASE THE SUFFIX "C" (CLEAR) REPRESENTS THE WHOLE LAYER
!     AND THAT THE SUFFIX "N" (NEBULOUS) REPRESENTS UNUSED ARRAYS.
!-------------------------------------------------------------------------------

!     I - CALCUL DES FLUX AVEC L'HYPOTHESE "COOLING TO SPACE".

!     I - FLUX COMPUTATION IN THE COOLING TO SPACE CASE.
!-------------------------------------------------------------------------------

!     I.1 - COMPUTE MATRIX COEFICIENTS OF ADDING METHOD LINEAR SYSTEM
!     FOR "DECENDING" GASEOUS OPTICAL DEPTH.
!-------------------------------------------------------------------------------

CALL ACRANEB_COEFT(KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PDELP,PDEOTI2(1,1),PEO1TI,PEO2TI,PEO1TN,PEO2TN,PQICE,&
 & PQLI,PEO1TA,PEO2TA,ZA4C,ZA5C,ZA4N,ZA5N)

!     I.2 - PREPARE RIGHT-HAND SIDE OF ADDING METHOD LINEAR SYSTEM
!     FOR IDEALIZED PROFILE A.
!-------------------------------------------------------------------------------

DO JLON=KIDIA,KFDIA
  ZFDC(JLON,KTDIA-1)=EXP(MAX(-PDEOTI2(JLON,KTDIA-1),ZARGLI))
  ZFMC(JLON,KTDIA-1)=0._JPRB
  IF (LDNUMX) THEN
    ZFDN(JLON,KTDIA-1)=0._JPRB
    ZFMN(JLON,KTDIA-1)=0._JPRB
  ENDIF
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZFMC(JLON,JLEV)=0._JPRB
    ZFDC(JLON,JLEV)=0._JPRB
    IF (LDNUMX) THEN
      ZFDN(JLON,JLEV)=0._JPRB
      ZFMN(JLON,JLEV)=0._JPRB
    ENDIF
  ENDDO
ENDDO

!     I.3 - SOLVE THE ADDING METHOD LINEAR SYSTEM BY GAUSSIAN
!     ELIMINATION BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------

CALL ACRANEB_SOLVT1(LDNUMX,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PEMIS,PNEB,PB1,PB2,PB3,PB4,ZA4C,ZA5C,ZA4N,ZA5N,&
 & ZFDC,ZFMC,ZFDN,ZFMN)

!     I.4 - COMPUTE THE FINAL FLUXES FOR THE CTS CASE.
!-------------------------------------------------------------------------------

! - TEMPORAIRE(S) 1D

! ZTRS      : FLUX "Z" NET AU SOMMET D'UNE COUCHE.
!           : "Z" NET FLUX AT THE TOP OF A LAYER.
! ZTRB      : FLUX "Z" NET A LA BASE D'UNE COUCHE.
!           : "Z" NET FLUX AT THE BOTTOM OF A LAYER.

DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZFDC(JLON,KLEV)-ZFMC(JLON,KLEV)

  ! CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
  ! OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.
  IF (LDNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZFDN(JLON,KLEV)-ZFMN(JLON,KLEV)
  ENDIF

  PFRTH(JLON,KLEV)=-PBB(JLON,KLEV)*MIN(1._JPRB,ZTRS(JLON)*PRSURF(JLON))
ENDDO

!cdir unroll=8
DO JLEV=KLEV,KTDIA,-1

  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZTRS(JLON)
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZFDC(JLON,JLEV-1)-ZFMC(JLON,JLEV-1)

!     CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
!     OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.

    IF (LDNUMX) THEN
      ZTRS(JLON)=ZTRS(JLON)+ZFDN(JLON,JLEV-1)-ZFMN(JLON,JLEV-1)
    ENDIF

!     FINAL COMPUTATION INCLUDING THE CTS SECURISATION FOR THE TIME-STEP.

    PFRTH(JLON,JLEV-1)=PFRTH(JLON,JLEV)+PBB(JLON,JLEV-1)&
     & *(ZTRB(JLON)-ZTRS(JLON))/(1._JPRB-(ZTRB(JLON)-ZTRS(JLON))&
     & *PDAMP(JLON,JLEV))
  ENDDO

ENDDO

!     II - CALCUL DES FLUX AVEC L'HYPOTHESE "EXCHANGE WITH SURFACE".

!     II - FLUX COMPUTATION IN THE EXCHANGE WITH SURFACE (EWS) CASE.
!-------------------------------------------------------------------------------


!     II.1 - COMPUTE MATRIX COEFICIENTS OF ADDING METHOD LINEAR SYSTEM
!     USING "ASCENDING" GASEOUS OPTICAL DEPTH.
!-------------------------------------------------------------------------------

CALL ACRANEB_COEFT(KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PDELP,PUEOTI2(1,1),PEO1TI,PEO2TI,PEO1TN,PEO2TN,PQICE,&
 & PQLI,PEO1TA,PEO2TA,ZA4C,ZA5C,ZA4N,ZA5N)

!     II.2 - PREPARE RIGHT-HAND SIDE OF ADDING METHOD LINEAR SYSTEM
!     FOR IDEALIZED PROFILE B.
!-------------------------------------------------------------------------------

IF (LDNUMX) THEN

  DO JLON=KIDIA,KFDIA
    ZFDC(JLON,KTDIA-1)=0._JPRB
    ZFMC(JLON,KTDIA-1)=0._JPRB
    ZFDC(JLON,KTDIA)=0._JPRB
    ZFMC(JLON,KLEV-1)=-ZA4C(JLON,KLEV)
    ZFDC(JLON,KLEV)=-ZA5C(JLON,KLEV)
    ZFMC(JLON,KLEV)=1._JPRB-PEMIS(JLON)

    ZFMC(JLON,KLEV-1)=ZFMC(JLON,KLEV-1)*(1._JPRB-PNEB(JLON,KLEV))
    ZFDC(JLON,KLEV)=ZFDC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZFMC(JLON,KLEV)=ZFMC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZFDN(JLON,KTDIA-1)=0._JPRB
    ZFMN(JLON,KTDIA-1)=0._JPRB
    ZFDN(JLON,KTDIA)=0._JPRB
    ZFMN(JLON,KLEV-1)=-ZA4N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZFDN(JLON,KLEV)=-ZA5N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZFMN(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*PNEB(JLON,KLEV)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFDC(JLON,JLEV)=0._JPRB
      ZFMC(JLON,JLEV-1)=0._JPRB
      ZFDN(JLON,JLEV)=0._JPRB
      ZFMN(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    ZFDC(JLON,KTDIA-1)=0._JPRB
    ZFMC(JLON,KTDIA-1)=0._JPRB
    ZFDC(JLON,KTDIA  )=0._JPRB
    ZFMC(JLON,KLEV-1 )=-(ZA4C(JLON,KLEV)*PB1(JLON,KLEV)+ &
     &                   ZA4N(JLON,KLEV)*PNEB(JLON,KLEV)) 
    ZFDC(JLON,KLEV   )=-(ZA5C(JLON,KLEV)*PB1(JLON,KLEV)+ &
     &                   ZA5N(JLON,KLEV)*PNEB(JLON,KLEV))
    ZFMC(JLON,KLEV   )=1._JPRB-PEMIS(JLON)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZFDC(JLON,JLEV)=0._JPRB
      ZFMC(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ENDIF ! LDNUMX

!     II.3 - SOLVE THE ADDING METHOD LINEAR SYSTEM BY GAUSSIAN
!     ELIMINATION BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------

CALL ACRANEB_SOLVT1(LDNUMX,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PEMIS,PNEB,PB1,PB2,PB3,PB4,ZA4C,ZA5C,ZA4N,ZA5N,&
 & ZFDC,ZFMC,ZFDN,ZFMN)

!     II.4 - COMPUTE THE FINAL FLUXES FOR EWS CASE.
!-------------------------------------------------------------------------------

! - TEMPORAIRE(S) 1D

! ZTRS      : FLUX "Z" NET AU SOMMET D'UNE COUCHE.
!           : "Z" NET FLUX AT THE TOP OF A LAYER.
! ZTRB      : FLUX "Z" NET A LA BASE D'UNE COUCHE.
!           : "Z" NET FLUX AT THE BOTTOM OF A LAYER.

DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZFDC(JLON,KTDIA-1)-ZFMC(JLON,KTDIA-1)

  ! CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
  ! OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.
  IF (LDNUMX) THEN
    ZTRB(JLON)=ZTRB(JLON)+ZFDN(JLON,KTDIA-1)-ZFMN(JLON,KTDIA-1)
  ENDIF

  ZFRTH(JLON,KTDIA-1)=0._JPRB
ENDDO

!cdir unroll=8
DO JLEV=KTDIA,KLEV
  LLREWS=(JLEV==KLEV)
  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTRB(JLON)
  ENDDO
  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZFDC(JLON,JLEV)-ZFMC(JLON,JLEV)

    ! CALCULS OPTIONNELS SUIVANT LA GEOMETRIE NUAGEUSE CHOISIE.
    ! OPTIONAL COMPUTATIONS ACCORDING TO THE CHOSEN GEOMETRY.
    IF (LDNUMX) THEN
      ZTRB(JLON)=ZTRB(JLON)+ZFDN(JLON,JLEV)-ZFMN(JLON,JLEV)
    ENDIF

    IF (LLREWS) THEN
      ZTRB(JLON)=ZTRB(JLON)+1._JPRB
    ENDIF

    ! FINAL COMPUTATION INCLUDING THE EWS SECURISATION FOR THE TIME-STEP.
    ZFRTH(JLON,JLEV)=ZFRTH(JLON,JLEV-1)+(PBB(JLON,KLEV)&
     & -PBB(JLON,JLEV-1))*(ZTRS(JLON)-ZTRB(JLON))&
     & /(1._JPRB-(ZTRS(JLON)-ZTRB(JLON))*PDAMP(JLON,JLEV))
  ENDDO
ENDDO

!     III - CALCUL DES COMPLEMENTS AU FLUX DU CORPS NOIR DES FLUX
!     THERMIQUES 'ANTI-SURESTIMATION' POUR LE PROFIL DE TEMPERATURE REEL
!     ET LE PROFIL ISOTHERME NORMALISE AFIN D'OBTENIR PAR DIFFERENCE LES
!     TERMES D'ECHANGE QUI, AJOUTES AUX RESULTATS "COOLING TO SPACE",
!     DONNERONT LE CHAMP DE RAYONNEMENT THERMIQUE FINAL.

!     III - COMPUTATION OF THE COMPLEMENTS TO THE BLACK-BODY FLUXES OF
!     "ANTI-SURESTIMATION" THERMAL FLUXES FOR THE REAL TEMPERATURE
!     PROFILE AND FOR THE NORMALIZED ISOTHERMAL PROFILE IN ORDER TO
!     OBTAIN BY DIFFERENCIATION THE EXCHANGE TERMS THAT, ADDED TO THE
!     COOLING TO SPACE AND EXCHANGE WITH SURFACE RESULTS, WILL GIVE
!     THE FINAL THERMAL RADIATIVE FLUXES' ARRAY.
!-------------------------------------------------------------------------------


!     III.1 - COMPUTE LINEAR SYSTEM COEFICIENTS FOR ADDING METHOD.
!-------------------------------------------------------------------------------

CALL ACRANEB_COEFT(KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PDELP,ZEONT(1,1),PEO1TI,PEO2TI,PEO1TN,PEO2TN,PQICE,&
 & PQLI,PEO1TA,PEO2TA,ZA4C,ZA5C,ZA4N,ZA5N)

! GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION.
IF (LRPROX) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZGEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON,JLEV)&
       & +PNEB(JLON,JLEV)*ZA4N(JLON,JLEV)
      ZDA4G(JLON,JLEV)=ZA4C(JLON,JLEV)-ZA4N(JLON,JLEV)
    ENDDO
  ENDDO
ENDIF

!     III.2 - PREPARE RIGHT-HAND SIDES OF ADDING METHOD LINEAR SYSTEM
!     FOR NORMALIZED ISOTHERMAL PROFILES A AND B AND THE REAL
!     TEMPERATURE PROFILE.
!-----------------------------------------------------------------------


! RIGHT-HAND SIDE FOR PROFILE A.

DO JLON=KIDIA,KFDIA
  ZTDC(JLON,KTDIA-1)=EXP(MAX(-ZEONT(JLON,KTDIA-1),ZARGLI))
  ZTMC(JLON,KTDIA-1)=0._JPRB
  IF (LDNUMX) THEN
    ZTDN(JLON,KTDIA-1)=0._JPRB
    ZTMN(JLON,KTDIA-1)=0._JPRB
  ENDIF
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZTMC(JLON,JLEV)=0._JPRB
    ZTDC(JLON,JLEV)=0._JPRB
    IF (LDNUMX) THEN
      ZTDN(JLON,JLEV)=0._JPRB
      ZTMN(JLON,JLEV)=0._JPRB
    ENDIF
  ENDDO
ENDDO

! RIGHT-HAND SIDE FOR PROFILE B.

IF (LDNUMX) THEN

  DO JLON=KIDIA,KFDIA
    ZZZTDC(JLON,KTDIA-1)=0._JPRB
    ZZZTMC(JLON,KTDIA-1)=0._JPRB
    ZZZTDC(JLON,KTDIA)=0._JPRB
    ZZZTMC(JLON,KLEV-1)=-ZA4C(JLON,KLEV)
    ZZZTDC(JLON,KLEV)=-ZA5C(JLON,KLEV)
    ZZZTMC(JLON,KLEV)=1._JPRB-PEMIS(JLON)
    ZZZTMC(JLON,KLEV-1)=ZZZTMC(JLON,KLEV-1)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZTDC(JLON,KLEV)=ZZZTDC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZTMC(JLON,KLEV)=ZZZTMC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZTDN(JLON,KTDIA-1)=0._JPRB
    ZZZTMN(JLON,KTDIA-1)=0._JPRB
    ZZZTDN(JLON,KTDIA)=0._JPRB
    ZZZTMN(JLON,KLEV-1)=-ZA4N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZZZTDN(JLON,KLEV)=-ZA5N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZZZTMN(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*PNEB(JLON,KLEV)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZZTDC(JLON,JLEV)=0._JPRB
      ZZZTMC(JLON,JLEV-1)=0._JPRB
      ZZZTDN(JLON,JLEV)=0._JPRB
      ZZZTMN(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    ZZZTDC(JLON,KTDIA-1)=0._JPRB
    ZZZTMC(JLON,KTDIA-1)=0._JPRB
    ZZZTDC(JLON,KTDIA)=0._JPRB
    ZZZTMC(JLON,KLEV-1)=-(ZA4C(JLON,KLEV)*PB1(JLON,KLEV)+ZA4N(JLON,KLEV)&
     & *PNEB(JLON,KLEV)) 
    ZZZTDC(JLON,KLEV)=-(ZA5C(JLON,KLEV)*PB1(JLON,KLEV)+ZA5N(JLON,KLEV)&
     & *PNEB(JLON,KLEV))
    ZZZTMC(JLON,KLEV)=1._JPRB-PEMIS(JLON)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZZTDC(JLON,JLEV)=0._JPRB
      ZZZTMC(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ENDIF ! LDNUMX

! RIGHT-HAND SIDE FOR A REAL TEMPERATURE PROFILE.

IF (LDNUMX) THEN

  DO JLON=KIDIA,KFDIA
    ZZTDC(JLON,KTDIA-1)=PBB(JLON,KTDIA-1)*ZTDC(JLON,KTDIA-1)
    ZZTMC(JLON,KTDIA-1)=ZA4C(JLON,KTDIA)*(PBB(JLON,KTDIA-1)&
     & -PBB(JLON,KTDIA)) 
    ZZTDC(JLON,KTDIA)=ZA5C(JLON,KTDIA)*(PBB(JLON,KTDIA-1)&
     & -PBB(JLON,KTDIA))
    ZZTMC(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*(PBB(JLON,KLEV)&
     & -PBB(JLON,KLEV-1))
    ZZTMC(JLON,KTDIA-1)=(1._JPRB-PNEB(JLON,KTDIA))*ZZTMC(JLON,KTDIA-1)
    ZZTDC(JLON,KTDIA)=(1._JPRB-PNEB(JLON,KTDIA))*ZZTDC(JLON,KTDIA)
    ZZTMC(JLON,KLEV)=(1._JPRB-PNEB(JLON,KLEV))*ZZTMC(JLON,KLEV)
    ZZTDN(JLON,KTDIA-1)=0._JPRB
    ZZTMN(JLON,KTDIA-1)=ZA4N(JLON,KTDIA)*PNEB(JLON,KTDIA)&
     & *(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZTDN(JLON,KTDIA)=ZA5N(JLON,KTDIA)*PNEB(JLON,KTDIA)&
     & *(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZTMN(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*PNEB(JLON,KLEV)&
     & *(PBB(JLON,KLEV)-PBB(JLON,KLEV-1))
  ENDDO

  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZZTMC(JLON,JLEV-1)=ZA4C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV))+ZA5C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV-2))
      ZZTDC(JLON,JLEV)=ZA5C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV))+ZA4C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV-2))
      ZZTMC(JLON,JLEV-1)=(1._JPRB-PNEB(JLON,JLEV))*ZZTMC(JLON,JLEV-1)
      ZZTDC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZZTDC(JLON,JLEV)
      ZZTMN(JLON,JLEV-1)=PNEB(JLON,JLEV)*(ZA4N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+ZA5N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2))) 
      ZZTDN(JLON,JLEV)=PNEB(JLON,JLEV)*(ZA5N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+ZA4N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2)))
   ENDDO
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    ZZTDC(JLON,KTDIA-1)=PBB(JLON,KTDIA-1)*ZTDC(JLON,KTDIA-1)
    ZZTMC(JLON,KTDIA-1)=(ZA4C(JLON,KTDIA)*PB1(JLON,KTDIA)+ZA4N(JLON,KTDIA)&
     & *PNEB(JLON,KTDIA))*(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA)) 
    ZZTDC(JLON,KTDIA)=(ZA5C(JLON,KTDIA)*PB1(JLON,KTDIA)+ZA5N(JLON,KTDIA)&
     & *PNEB(JLON,KTDIA))*(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZTMC(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*(PBB(JLON,KLEV)-PBB(JLON,KLEV-1))
  ENDDO

  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
    ZZTMC(JLON,JLEV-1)=(ZA4C(JLON,JLEV)*PB1(JLON,JLEV)+ZA4N(JLON,JLEV)&
     & *PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+(ZA5C(JLON,JLEV)&
     & *PB1(JLON,JLEV)+ZA5N(JLON,JLEV)*PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)&
     & -PBB(JLON,JLEV-2))
    ZZTDC(JLON,JLEV)=(ZA5C(JLON,JLEV)*PB1(JLON,JLEV)+ZA5N(JLON,JLEV)&
     & *PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+(ZA4C(JLON,JLEV)&
     & *PB1(JLON,JLEV)+ZA4N(JLON,JLEV)*PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)&
     & -PBB(JLON,JLEV-2))
   ENDDO
  ENDDO

ENDIF ! LDNUMX


!     III.3 - SOLVE THE ADDING METHOD LINEAR SYSTEM BY GAUSSIAN
!     ELIMINATION BACK-SUBSTITUTION.
!-------------------------------------------------------------------------------

CALL ACRANEB_SOLVT3(LDNUMX,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PEMIS,PNEB,PB1,PB2,PB3,PB4,ZA4C,ZA5C,ZA4N,ZA5N,&
 & ZTDC,ZTMC,ZTDN,ZTMN,ZZTDC,ZZTMC,ZZTDN,ZZTMN,&
 & ZZZTDC,ZZZTMC,ZZZTDN,ZZZTMN)

! DUPLICATION OF THE LOOP WITH THREE RIGHT-HAND SIDES
! WHEN REPLACING THE EBL MINIMUM VALUES FOR OPTICAL
! DEPTHS BY THE MAXIMUM (LOCAL) ONES.

! GLOBAL STORAGE FOR THE 'PROXIMITY' COMPUTATION
IF (LRPROX) THEN

  ! COMPUTE MATRIX COEFICIENTS USING LOCAL GAS. OPTICAL DEPTH PEOLT

  CALL ACRANEB_COEFT(KIDIA,KFDIA,KLON,KTDIA,KLEV,&
   & PDELP,PEOLT,PEO1TI,PEO2TI,PEO1TN,PEO2TN,PQICE,&
   & PQLI,PEO1TA,PEO2TA,ZA4C,ZA5C,ZA4N,ZA5N)

  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZLEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON,JLEV)&
       & +PNEB(JLON,JLEV)*ZA4N(JLON,JLEV)
      ZDA4L(JLON,JLEV)=ZA4C(JLON,JLEV)-ZA4N(JLON,JLEV)
    ENDDO
  ENDDO

ENDIF

! COMPUTE MATRIX COEFICIENTS USING MAXIMUM GAS. OPTICAL DEPTH PEOXT

CALL ACRANEB_COEFT(KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PDELP,PEOXT,PEO1TI,PEO2TI,PEO1TN,PEO2TN,PQICE,&
 & PQLI,PEO1TA,PEO2TA,ZA4C,ZA5C,ZA4N,ZA5N)

IF (LRPROX) THEN
  DO JLEV=KTDIA,KLEV
    DO JLON=KIDIA,KFDIA
      ZXEPC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZA4C(JLON,JLEV)&
       & +PNEB(JLON,JLEV)*ZA4N(JLON,JLEV)
      ZDA4X(JLON,JLEV)=ZA4C(JLON,JLEV)-ZA4N(JLON,JLEV)
    ENDDO
  ENDDO
ELSEIF (LRTPP) THEN
  DO JLON=KIDIA,KFDIA
    ZLEPC(JLON,KLEV)=(1._JPRB-PNEB(JLON,KLEV))*ZA4C(JLON,KLEV)&
     & +PNEB(JLON,KLEV)*ZA4N(JLON,KLEV)
  ENDDO
ENDIF

! RHS FOR PROFILE A

DO JLON=KIDIA,KFDIA
  ZFDC(JLON,KTDIA-1)=EXP(MAX(-PDEOTI(JLON,KTDIA-1),ZARGLI))
  ZFMC(JLON,KTDIA-1)=0._JPRB
  IF (LDNUMX) THEN
    ZFDN(JLON,KTDIA-1)=0._JPRB
    ZFMN(JLON,KTDIA-1)=0._JPRB
  ENDIF
ENDDO

DO JLEV=KTDIA,KLEV
  DO JLON=KIDIA,KFDIA
    ZFMC(JLON,JLEV)=0._JPRB
    ZFDC(JLON,JLEV)=0._JPRB
    IF (LDNUMX) THEN
      ZFDN(JLON,JLEV)=0._JPRB
      ZFMN(JLON,JLEV)=0._JPRB
    ENDIF
  ENDDO
ENDDO

! RHS FOR PROFILE C

IF (LDNUMX) THEN

  DO JLON=KIDIA,KFDIA
    ZZFDC(JLON,KTDIA-1)=PBB(JLON,KTDIA-1)*ZFDC(JLON,KTDIA-1)
    ZZFMC(JLON,KTDIA-1)=ZA4C(JLON,KTDIA)*(PBB(JLON,KTDIA-1)&
     & -PBB(JLON,KTDIA)) 
    ZZFDC(JLON,KTDIA)=ZA5C(JLON,KTDIA)*(PBB(JLON,KTDIA-1)&
     & -PBB(JLON,KTDIA))
    ZZFMC(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*(PBB(JLON,KLEV)&
     & -PBB(JLON,KLEV-1))
    ZZFMC(JLON,KTDIA-1)=(1._JPRB-PNEB(JLON,KTDIA))*ZZFMC(JLON,KTDIA-1)
    ZZFDC(JLON,KTDIA)=(1._JPRB-PNEB(JLON,KTDIA))*ZZFDC(JLON,KTDIA)
    ZZFMC(JLON,KLEV)=(1._JPRB-PNEB(JLON,KLEV))*ZZFMC(JLON,KLEV)
    ZZFDN(JLON,KTDIA-1)=0._JPRB
    ZZFMN(JLON,KTDIA-1)=ZA4N(JLON,KTDIA)*PNEB(JLON,KTDIA)&
     & *(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZFDN(JLON,KTDIA)=ZA5N(JLON,KTDIA)*PNEB(JLON,KTDIA)&
     & *(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZFMN(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*PNEB(JLON,KLEV)&
     & *(PBB(JLON,KLEV)-PBB(JLON,KLEV-1))
  ENDDO

  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
      ZZFMC(JLON,JLEV-1)=ZA4C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV))+ZA5C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV-2))
      ZZFDC(JLON,JLEV)=ZA5C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV))+ZA4C(JLON,JLEV)*(PBB(JLON,JLEV-1)&
       & -PBB(JLON,JLEV-2))
      ZZFMC(JLON,JLEV-1)=(1._JPRB-PNEB(JLON,JLEV))*ZZFMC(JLON,JLEV-1)
      ZZFDC(JLON,JLEV)=(1._JPRB-PNEB(JLON,JLEV))*ZZFDC(JLON,JLEV)
      ZZFMN(JLON,JLEV-1)=PNEB(JLON,JLEV)*(ZA4N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+ZA5N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2))) 
      ZZFDN(JLON,JLEV)=PNEB(JLON,JLEV)*(ZA5N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+ZA4N(JLON,JLEV)&
       & *(PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2)))
    ENDDO
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    ZZFDC(JLON,KTDIA-1)=PBB(JLON,KTDIA-1)*ZFDC(JLON,KTDIA-1)
    ZZFMC(JLON,KTDIA-1)=(ZA4C(JLON,KTDIA)*PB1(JLON,KTDIA)+ZA4N(JLON,KTDIA)&
     & *PNEB(JLON,KTDIA))*(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA)) 
    ZZFDC(JLON,KTDIA)=(ZA5C(JLON,KTDIA)*PB1(JLON,KTDIA)+ZA5N(JLON,KTDIA)&
     & *PNEB(JLON,KTDIA))*(PBB(JLON,KTDIA-1)-PBB(JLON,KTDIA))
    ZZFMC(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*(PBB(JLON,KLEV)-PBB(JLON,KLEV-1))
  ENDDO

  DO JLEV=KTDIA+1,KLEV
    DO JLON=KIDIA,KFDIA
    ZZFMC(JLON,JLEV-1)=(ZA4C(JLON,JLEV)*PB1(JLON,JLEV)+ZA4N(JLON,JLEV)&
     & *PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+(ZA5C(JLON,JLEV)&
     & *PB1(JLON,JLEV)+ZA5N(JLON,JLEV)*PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)&
     & -PBB(JLON,JLEV-2))
    ZZFDC(JLON,JLEV)=(ZA5C(JLON,JLEV)*PB1(JLON,JLEV)+ZA5N(JLON,JLEV)&
     & *PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))+(ZA4C(JLON,JLEV)&
     & *PB1(JLON,JLEV)+ZA4N(JLON,JLEV)*PNEB(JLON,JLEV))*(PBB(JLON,JLEV-1)&
     & -PBB(JLON,JLEV-2))
   ENDDO
  ENDDO

ENDIF ! LDNUMX

! RHS FOR PROFILE B

IF (LDNUMX) THEN

  DO JLON=KIDIA,KFDIA
    ZZZFDC(JLON,KTDIA-1)=0._JPRB
    ZZZFMC(JLON,KTDIA-1)=0._JPRB
    ZZZFDC(JLON,KTDIA)=0._JPRB
    ZZZFMC(JLON,KLEV-1)=-ZA4C(JLON,KLEV)
    ZZZFDC(JLON,KLEV)=-ZA5C(JLON,KLEV)
    ZZZFMC(JLON,KLEV)=1._JPRB-PEMIS(JLON)

    ZZZFMC(JLON,KLEV-1)=ZZZFMC(JLON,KLEV-1)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZFDC(JLON,KLEV)=ZZZFDC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZFMC(JLON,KLEV)=ZZZFMC(JLON,KLEV)*(1._JPRB-PNEB(JLON,KLEV))
    ZZZFDN(JLON,KTDIA-1)=0._JPRB
    ZZZFMN(JLON,KTDIA-1)=0._JPRB
    ZZZFDN(JLON,KTDIA)=0._JPRB
    ZZZFMN(JLON,KLEV-1)=-ZA4N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZZZFDN(JLON,KLEV)=-ZA5N(JLON,KLEV)*PNEB(JLON,KLEV)
    ZZZFMN(JLON,KLEV)=(1._JPRB-PEMIS(JLON))*PNEB(JLON,KLEV)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZZFDC(JLON,JLEV)=0._JPRB
      ZZZFMC(JLON,JLEV-1)=0._JPRB
      ZZZFDN(JLON,JLEV)=0._JPRB
      ZZZFMN(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ELSE

  DO JLON=KIDIA,KFDIA
    ZZZFDC(JLON,KTDIA-1)=0._JPRB
    ZZZFMC(JLON,KTDIA-1)=0._JPRB
    ZZZFDC(JLON,KTDIA)=0._JPRB
    ZZZFMC(JLON,KLEV-1)=-(ZA4C(JLON,KLEV)*PB1(JLON,KLEV)+ZA4N(JLON,KLEV)&
     & *PNEB(JLON,KLEV)) 
    ZZZFDC(JLON,KLEV)=-(ZA5C(JLON,KLEV)*PB1(JLON,KLEV)+ZA5N(JLON,KLEV)&
     & *PNEB(JLON,KLEV))
    ZZZFMC(JLON,KLEV)=1._JPRB-PEMIS(JLON)
  ENDDO

  DO JLEV=KTDIA+1,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZZZFDC(JLON,JLEV)=0._JPRB
      ZZZFMC(JLON,JLEV-1)=0._JPRB
    ENDDO
  ENDDO

ENDIF ! LDNUMX

! SOLVE ADDING SYSTEM FOR PROFILES A, B, C

CALL ACRANEB_SOLVT3(LDNUMX,KIDIA,KFDIA,KLON,KTDIA,KLEV,&
 & PEMIS,PNEB,PB1,PB2,PB3,PB4,ZA4C,ZA5C,ZA4N,ZA5N,&
 & ZFDC,ZFMC,ZFDN,ZFMN,ZZFDC,ZZFMC,ZZFDN,ZZFMN,&
 & ZZZFDC,ZZZFMC,ZZZFDN,ZZZFMN)

!     III.4 - CALCUL DES FLUX DEFINITIFS.

!     III.4 - COMPUTATION OF THE FINAL FLUXES.
!-------------------------------------------------------------------------------

DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZZZTDC(JLON,KTDIA-1)-ZZZTMC(JLON,KTDIA-1)
  ZFRB(JLON)=ZZZFDC(JLON,KTDIA-1)-ZZZFMC(JLON,KTDIA-1)

  IF (LDNUMX) THEN
    ZTRB(JLON)=ZTRB(JLON)+ZZZTDN(JLON,KTDIA-1)-ZZZTMN(JLON,KTDIA-1)
    ZFRB(JLON)=ZFRB(JLON)+ZZZFDN(JLON,KTDIA-1)-ZZZFMN(JLON,KTDIA-1)
  ENDIF

  ZZTRTH(JLON,KTDIA-1)=0._JPRB
  ZZFRTH(JLON,KTDIA-1)=0._JPRB
ENDDO

!cdir unroll=8
DO JLEV=KTDIA,KLEV
  LLREWS=(JLEV==KLEV)

  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTRB(JLON)
    ZFRS(JLON)=ZFRB(JLON)
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZZZTDC(JLON,JLEV)-ZZZTMC(JLON,JLEV)
    ZFRB(JLON)=ZZZFDC(JLON,JLEV)-ZZZFMC(JLON,JLEV)

    IF (LDNUMX) THEN
      ZTRB(JLON)=ZTRB(JLON)+ZZZTDN(JLON,JLEV)-ZZZTMN(JLON,JLEV)
      ZFRB(JLON)=ZFRB(JLON)+ZZZFDN(JLON,JLEV)-ZZZFMN(JLON,JLEV)
    ENDIF

    IF (LLREWS) THEN
      ZTRB(JLON)=ZTRB(JLON)+1._JPRB
      ZFRB(JLON)=ZFRB(JLON)+1._JPRB
    ENDIF

    ZZTRTH(JLON,JLEV)=ZZTRTH(JLON,JLEV-1)+(PBB(JLON,KLEV)&
     & -PBB(JLON,JLEV-1))*(ZTRS(JLON)-ZTRB(JLON))
    ZZFRTH(JLON,JLEV)=ZZFRTH(JLON,JLEV-1)+(PBB(JLON,KLEV)&
     & -PBB(JLON,JLEV-1))*(ZFRS(JLON)-ZFRB(JLON))
    ZFRTH(JLON,JLEV)=ZFRTH(JLON,JLEV)-(ZZTRTH(JLON,JLEV)&
     & +PMIXP(JLON,JLEV)*(ZZFRTH(JLON,JLEV)-ZZTRTH(JLON,JLEV)))
  ENDDO

ENDDO

! - TEMPORAIRE(S) 1D

! ZZTRS     : FLUX "ZZ" NET AU SOMMET D'UNE COUCHE.
!           : "ZZ" NET FLUX AT THE TOP OF THE LAYER.
! ZDRS      : FLUX NET AU SOMMET "COOLING TO SPACE".
!           : NET TOP FLUX FOR COOLING TO SPACE.
! ZZTRB     : FLUX "ZZ" NET A LA BASE D'UNE COUCHE.
!           : "ZZ" NET FLUX AT THE BOTTOM OF THE LAYER.
! ZDRB      : FLUX NET A LA BASE "COOLING TO SPACE".
!           : NET BOTTOM FLUX FOR COOLING TO SPACE.

DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZTDC(JLON,KLEV)-ZTMC(JLON,KLEV)
  ZFRS(JLON)=ZFDC(JLON,KLEV)-ZFMC(JLON,KLEV)
  ZZTRS(JLON)=ZZTDC(JLON,KLEV)-ZZTMC(JLON,KLEV)+PBB(JLON,KLEV)&
   & -PBB(JLON,KLEV-1)
  ZZFRS(JLON)=ZZFDC(JLON,KLEV)-ZZFMC(JLON,KLEV)+PBB(JLON,KLEV)&
   & -PBB(JLON,KLEV-1)

  IF (LDNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,KLEV)-ZTMN(JLON,KLEV)
    ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,KLEV)-ZFMN(JLON,KLEV)
    ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,KLEV)-ZZTMN(JLON,KLEV)
    ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,KLEV)-ZZFMN(JLON,KLEV)
  ENDIF

  ZDRS(JLON)=PFRTH(JLON,KLEV)
  ZZTRTH(JLON,KLEV)=ZDRS(JLON)+PBB(JLON,KLEV)*ZTRS(JLON)-ZZTRS(JLON)
  ZZFRTH(JLON,KLEV)=ZDRS(JLON)+PBB(JLON,KLEV)*ZFRS(JLON)-ZZFRS(JLON)
  PFRTH(JLON,KLEV)=ZZTRTH(JLON,KLEV)+PMIXP(JLON,KLEV)&
   & *(ZZFRTH(JLON,KLEV)-ZZTRTH(JLON,KLEV))
ENDDO

!cdir unroll=8
DO JLEV=KLEV,KTDIA+1,-1

  DO JLON=KIDIA,KFDIA
    ZTRB(JLON)=ZTRS(JLON)
    ZFRB(JLON)=ZFRS(JLON)
    ZZTRB(JLON)=ZZTRS(JLON)
    ZZFRB(JLON)=ZZFRS(JLON)
    ZDRB(JLON)=ZDRS(JLON)
  ENDDO

  DO JLON=KIDIA,KFDIA
    ZTRS(JLON)=ZTDC(JLON,JLEV-1)-ZTMC(JLON,JLEV-1)
    ZFRS(JLON)=ZFDC(JLON,JLEV-1)-ZFMC(JLON,JLEV-1)
    ZZTRS(JLON)=ZZTDC(JLON,JLEV-1)-ZZTMC(JLON,JLEV-1)&
     & +PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2)
    ZZFRS(JLON)=ZZFDC(JLON,JLEV-1)-ZZFMC(JLON,JLEV-1)&
     & +PBB(JLON,JLEV-1)-PBB(JLON,JLEV-2)

    IF (LDNUMX) THEN
      ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,JLEV-1)-ZTMN(JLON,JLEV-1)
      ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,JLEV-1)-ZFMN(JLON,JLEV-1)
      ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,JLEV-1)-ZZTMN(JLON,JLEV-1)
      ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,JLEV-1)-ZZFMN(JLON,JLEV-1)
    ENDIF

    ZDRS(JLON)=PFRTH(JLON,JLEV-1)
    ZZTRTH(JLON,JLEV-1)=ZZTRTH(JLON,JLEV)+ZDRS(JLON)-ZDRB(JLON)&
     & -PBB(JLON,JLEV-1)*(ZTRB(JLON)-ZTRS(JLON))&
     & +ZZTRB(JLON)-ZZTRS(JLON)
    ZZFRTH(JLON,JLEV-1)=ZZFRTH(JLON,JLEV)+ZDRS(JLON)-ZDRB(JLON)&
     & -PBB(JLON,JLEV-1)*(ZFRB(JLON)-ZFRS(JLON))&
     & +ZZFRB(JLON)-ZZFRS(JLON)
    PFRTH(JLON,JLEV-1)=ZZTRTH(JLON,JLEV-1)+PMIXP(JLON,JLEV-1)&
     & *(ZZFRTH(JLON,JLEV-1)-ZZTRTH(JLON,JLEV-1))
  ENDDO

ENDDO

DO JLON=KIDIA,KFDIA
  ZTRB(JLON)=ZTRS(JLON)
  ZFRB(JLON)=ZFRS(JLON)
  ZZTRB(JLON)=ZZTRS(JLON)
  ZZFRB(JLON)=ZZFRS(JLON)
  ZDRB(JLON)=ZDRS(JLON)
ENDDO

DO JLON=KIDIA,KFDIA
  ZTRS(JLON)=ZTDC(JLON,KTDIA-1)-ZTMC(JLON,KTDIA-1)
  ZFRS(JLON)=ZFDC(JLON,KTDIA-1)-ZFMC(JLON,KTDIA-1)
  ZZTRS(JLON)=ZZTDC(JLON,KTDIA-1)-ZZTMC(JLON,KTDIA-1)
  ZZFRS(JLON)=ZZFDC(JLON,KTDIA-1)-ZZFMC(JLON,KTDIA-1)

  IF (LDNUMX) THEN
    ZTRS(JLON)=ZTRS(JLON)+ZTDN(JLON,KTDIA-1)-ZTMN(JLON,KTDIA-1)
    ZFRS(JLON)=ZFRS(JLON)+ZFDN(JLON,KTDIA-1)-ZFMN(JLON,KTDIA-1)
    ZZTRS(JLON)=ZZTRS(JLON)+ZZTDN(JLON,KTDIA-1)-ZZTMN(JLON,KTDIA-1)
    ZZFRS(JLON)=ZZFRS(JLON)+ZZFDN(JLON,KTDIA-1)-ZZFMN(JLON,KTDIA-1)
  ENDIF

  ZDRS(JLON)=PFRTH(JLON,KTDIA-1)
  ZZTRTH(JLON,KTDIA-1)=ZZTRTH(JLON,KTDIA)+ZDRS(JLON)-ZDRB(JLON)&
   & -PBB(JLON,KTDIA-1)*(ZTRB(JLON)-ZTRS(JLON))&
   & +ZZTRB(JLON)-ZZTRS(JLON)
  ZZFRTH(JLON,KTDIA-1)=ZZFRTH(JLON,KTDIA)+ZDRS(JLON)-ZDRB(JLON)&
   & -PBB(JLON,KTDIA-1)*(ZFRB(JLON)-ZFRS(JLON))&
   & +ZZFRB(JLON)-ZZFRS(JLON)
  PFRTH(JLON,KTDIA-1)=ZZTRTH(JLON,KTDIA-1)+PMIXP(JLON,KTDIA-1)&
   & *(ZZFRTH(JLON,KTDIA-1)-ZZTRTH(JLON,KTDIA-1))
ENDDO

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)+ZFRTH(JLON,JLEV)
  ENDDO
ENDDO

! 'PROXIMITY CORRECTION' WITH THE INTERPOLATED EFFECT AS BASIS
! AND THE TIME-SECURED MAXIMUM EXCHANGE SITUATION AS TARGET.
IF (LRPROX) THEN

!cdir unroll=8
  DO JLEV=KTDIA,KLEV-1
    DO JLON=KIDIA,KFDIA
      ZDEL1=-LOG(MAX(ZTRLI,ZLEPC(JLON,JLEV)))
      ZDEL2=-LOG(MAX(ZTRLI,ZLEPC(JLON,JLEV+1)))
      ZFTPP=1._JPRB
      IF (LRTPP) THEN
        ZFTPP=MAX(0._JPRB,1._JPRB-((2._JPRB-(1._JPRB-ZLEPC(JLON,JLEV))&
         & *(1._JPRB+2._JPRB/ZDEL1))+(2._JPRB-(1._JPRB-ZLEPC(JLON,JLEV+1))&
         & *(1._JPRB+2._JPRB/ZDEL2)))/((1._JPRB-ZLEPC(JLON,JLEV))+(1._JPRB&
         & -ZLEPC(JLON,JLEV+1))))
      ENDIF
      ZTARE2=(1._JPRB-ZXEPC(JLON,JLEV))*(1._JPRB-ZXEPC(JLON,JLEV+1))
      ZEFFE2=(1._JPRB-ZGEPC(JLON,JLEV))*(1._JPRB-ZGEPC(JLON,JLEV+1))
      ZTARSP=1._JPRB-ZLEPC(JLON,JLEV)-ZLEPC(JLON,JLEV+1)+       &
       & PRPROX(JLON,JLEV)*ZLEPC(JLON,JLEV)*ZLEPC(JLON,JLEV+1)
      ZTARSP=MAX(0._JPRB,MIN(1._JPRB,ZTARSP))
      IF (LDNUMX) THEN
        ZTARE2=ZTARE2&
         & +MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
         & -MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
         & *(ZDA4X(JLON,JLEV)*ZDA4X(JLON,JLEV+1))
        ZEFFE2=ZEFFE2&
         & +MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
         & -MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
         & *(ZDA4G(JLON,JLEV)*ZDA4G(JLON,JLEV+1))
        ZTARSP=MIN(1._JPRB,ZTARSP&
         & +MIN(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1))*(1._JPRB&
         & -MAX(PNEB(JLON,JLEV),PNEB(JLON,JLEV+1)))&
         & *(ZDA4L(JLON,JLEV)*ZDA4L(JLON,JLEV+1))*PRPROX(JLON,JLEV))
      ENDIF
      ZEFFE2=ZEFFE2*(1._JPRB-PMIXP(JLON,JLEV))+ZTARE2*PMIXP(JLON,JLEV)
      ZTARE2=ZTARSP
      ZTARE2=ZTARE2*ZFTPP
      ZTARE2=ZTARE2/(1._JPRB+(PDAMP(JLON,JLEV)+PDAMP(JLON,JLEV+1))*ZTARE2)
      PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)&
       & +(PBB(JLON,JLEV-1)-PBB(JLON,JLEV))*(ZTARE2-ZEFFE2)
    ENDDO
  ENDDO

ENDIF ! LRPROX

! LRTPP correction for exchange between lowest model level and surface
IF (LRTPP) THEN
  DO JLON=KIDIA,KFDIA
    ZDEL1=-LOG(MAX(ZTRLI,ZLEPC(JLON,KLEV)))
    ZEFFE2=(1._JPRB-ZLEPC(JLON,KLEV))*PEMIS(JLON)
    ZTARE2=2._JPRB*PEMIS(JLON)*((1._JPRB-ZLEPC(JLON,KLEV))&
     & *(1._JPRB+1._JPRB/ZDEL1)-1._JPRB)
    PFRTH(JLON,KLEV)=PFRTH(JLON,KLEV)&
     & +(PBB(JLON,KLEV-1)-PBB(JLON,KLEV))*(ZTARE2-ZEFFE2)&
     & /(1._JPRB+PDAMP(JLON,KLEV)*ZEFFE2)
  ENDDO
ENDIF

DO JLEV=KTDIA-1,KLEV
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,JLEV)+PFLUXC(JLON,JLEV)
  ENDDO
ENDDO

! no net flux divergence above layer KTDIA
DO JLEV=0,KTDIA-2
  DO JLON=KIDIA,KFDIA
    PFRTH(JLON,JLEV)=PFRTH(JLON,KTDIA-1)
  ENDDO
ENDDO

DO JLON=KIDIA,KFDIA
  PFRTHDS(JLON)=PBB(JLON,KLEV)+PFRTH(JLON,KLEV)/PEMIS(JLON)
ENDDO

!-------------------------------------------------------------------------------
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ACRANEB_SOLVT',1,ZHOOK_HANDLE)
END SUBROUTINE ACRANEB_SOLVT
