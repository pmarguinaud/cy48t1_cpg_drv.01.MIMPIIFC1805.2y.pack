!OPTIONS XOPT(NOEVAL)
SUBROUTINE RADAER ( YDEAERD,YDERAD,YDPHY,KIDIA , KFDIA , KPROMA , KLEV,&
 & PAPRS , PAPRSF, PT    , PTS,&
 & PAESEA, PAELAN, PAESOO, PAEDES, PAESUL, PAEVOL,&
 & PAER, PAERINDS                        )  

! ======================================================================

!**** *RADAER  - COMPUTES DISTRIBUTION OF AEROSOLS

!     PURPOSE.
!     --------

!     - USE THE ECMWF CLIMATOLOGICAL AEROSOLS DISTRIBUTION
!                   SEE TANRE ET AL., 1984
!     - AVOID THE USE OF RADACT WHICH DOES NOT WORK ON A
!                   NON-ROTATED, UNSTRETCHED GRID.

!     WARNING.
!     --------

!     RADAER HAS BEEN TESTED FOR ARPEGE-CLIMAT ONLY. IN PARTICULAR
!     ONE SHOULD BE CAREFUL WHEN USING IT WITH SPACE INTERPOLATION
!     OR MEMORY PARTITION (SEE RADINT).

!**   INTERFACE.
!     ----------

!     CALL *RADAER* FROM *APLPAR*

!        EXPLICIT ARGUMENTS :
!        --------------------

!        ==== INPUTS ===

! KIDIA  : LATITUDE INDEX OF 1-ST LATITUDE BAND IN VECTOR
! KFDIA  : LATITUDE INDEX OF LAST LATITUDE BAND IN VECTOR
! KPROMA : VECTOR LENGTH
! KLEV   : NUMBER OF LEVELS
! PAPRS  : (KPROMA,KLEV+1)      ; HALF LEVEL PRESSURE
! PAPRSF : (KPROMA,KLEV )       ; FULL LEVEL PRESSURE
! PT     : (KPROMA,KLEV)        ; FULL LEVEL TEMPERATURE
! PTS    : (KPROMA)             ; SURFACE TEMPERATURE
! PAESOO : (KPROMA)             ; SOOT AEROSOLS
! PAELAN : (KPROMA)             ; LAND AEROSOLS
! PAESEA : (KPROMA)             ; SEA AEROSOLS
! PAEDES : (KPROMA)             ; DESERT AEROSOLS
! PAESUL : (KPROMA)             ; SULFAT AEROSOLS
! PAEVOL : (KPROMA)             ; VOLCANO AEROSOLS

!        ==== OUTPUT ===

! PAER   : (KPROMA,KLEV,6)      ; OPTICAL THICKNESS OF THE AEROSOLS
! PAERINDS : (KPROMA,KLEV)      ; OPTICAL THICKNESS OF THE SULFATE AEROSOL
!                               ; USED FOR COMPUTATION OF ITS INDIRECT EFFECT

!        IMPLICIT ARGUMENTS 
!        --------------------

!        NONE

!     METHOD.
!     -------

!     LAND, SEA, SOOT AND DESERT AEROSOLS DISTRIBUTIONS ARE RED
!     FROM A FILE CONTAINING VARIOUS CLIMATOLOGIES AND COMBINED
!     HERE WITH VERTICAL PROFILES TO GET THE 3D DISTRIBUTION.

!     EXTERNALS.
!     ----------

!     NONE

!     REFERENCE.
!     ----------

!     SEE RADIATION'S PART OF THE MODEL'S DOCUMENTATION AND
!     ECMWF RESEARCH DEPARTMENT DOCUMENTATION OF THE "I.F.S"
!     FOR RADACT
!     J.-J. MORCRETTE E.C.M.W.F.   91/03/15 ORIGINAL RADACT SUBROUTINE

!     AUTHOR.
!     -------

!     Ph. DANDIN      METEO-FRANCE 94/11/14 AEROSOLS OUT OF RADINT

!     MODIFICATIONS.
!     --------------
!     M.Hamrud      01-Oct-2003 CY28 Cleaning
!     04/09 : F. BOUYSSEL - USE OF TEGEN'S AEROSOLS (LNEWAER)
!     09/09 : A.Alias  - SULFAT AEROSOLS (combined to land) (J.F.Gueremy)
!                      - PAEVOL  put in stratospheric aerosols category 
!                                instead of  RCSTBGA=cste (A.Voldoire)
!     09/07 : A. VOLDOIRE  - Add a table for sulfate aerosols alone 
!                            used for indirect effect calculation only
!                            For the direct effect, sulfate aerosols are 
!                            mixed with Land aerosols
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOEAERD  , ONLY : TEAERD
USE YOERAD   , ONLY : TERAD
USE YOMPHY  , ONLY : TPHY

IMPLICIT NONE

TYPE(TEAERD)      ,INTENT(IN)    :: YDEAERD
TYPE(TERAD)       ,INTENT(IN)    :: YDERAD
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)    :: KPROMA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KPROMA,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(KPROMA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KPROMA,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAESEA(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAELAN(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAESOO(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAEDES(KPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAESUL(KPROMA)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAEVOL(KPROMA)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAER(KPROMA,KLEV,6) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERINDS(KPROMA,KLEV) 
!     -----------------------------------------------------------------

!*       0.1   ARGUMENTS.
!              ----------

!     -----------------------------------------------------------------

!*       0.2   LOCAL ARRAYS.
!              -------------

REAL(KIND=JPRB) :: ZTH(KPROMA,KLEV+1)

REAL(KIND=JPRB) :: ZDPN  (KPROMA),ZDPO  (KPROMA)
REAL(KIND=JPRB) :: ZAEQSN(KPROMA),ZAEQSO(KPROMA)
REAL(KIND=JPRB) :: ZAEQLN(KPROMA),ZAEQLO(KPROMA)
REAL(KIND=JPRB) :: ZAEQUN(KPROMA),ZAEQUO(KPROMA)
REAL(KIND=JPRB) :: ZAEQDN(KPROMA),ZAEQDO(KPROMA)
REAL(KIND=JPRB) :: ZAETRN(KPROMA),ZAETRO(KPROMA)
REAL(KIND=JPRB) :: ZAEQFN(KPROMA),ZAEQFO(KPROMA)
REAL(KIND=JPRB) :: ZTOT(KPROMA)

INTEGER(KIND=JPIM) :: JK, JL, JAER

REAL(KIND=JPRB) :: ZAETR, ZDPNMO, ZGRTH, ZREPAER
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

!*         1.     AEROSOL PARAMETERS COMPUTATIONS
!                 -------------------------------

! Aerosols come from files containing the climatologies.

!*         2.      VERTICAL DISTRIBUTION
!*                 ---------------------

!*         2.1    HALF-LEVEL TEMPERATURE
!*          =     TEMPERATURES AT LAYERS' BOUNDARIES.

! Half-level temperature interpolation weighted by pressure 
! just as in RADINT (loop 1222) but shortened...

IF (LHOOK) CALL DR_HOOK('RADAER',0,ZHOOK_HANDLE)
ASSOCIATE(CVDAED=>YDEAERD%CVDAED, CVDAEL=>YDEAERD%CVDAEL, &
 & CVDAES=>YDEAERD%CVDAES, CVDAEU=>YDEAERD%CVDAEU, RCAEOPD=>YDEAERD%RCAEOPD, &
 & RCAEOPL=>YDEAERD%RCAEOPL, RCAEOPS=>YDEAERD%RCAEOPS, RCAEOPU=>YDEAERD%RCAEOPU, &
 & RCSTBGA=>YDEAERD%RCSTBGA, RCTRBGA=>YDEAERD%RCTRBGA, RCTRPT=>YDEAERD%RCTRPT, &
 & RCVOBGA=>YDEAERD%RCVOBGA, &
 & LNEWAER=>YDERAD%LNEWAER, LAEROVOL=>YDPHY%LAEROVOL, LAEROSUL=>YDPHY%LAEROSUL)
ZREPAER=1.E-12_JPRB
DO JK=2,KLEV
  DO JL=KIDIA,KFDIA
    ZTH(JL,JK)=(PT(JL,JK-1)*PAPRSF(JL,JK-1)&
     & *(PAPRSF(JL,JK)-PAPRS(JL,JK))&
     & +PT(JL,JK)*PAPRSF(JL,JK)*(PAPRS(JL,JK)-PAPRSF(JL,JK-1)))&
     & *(1.0_JPRB/(PAPRS(JL,JK)*(PAPRSF(JL,JK)-PAPRSF(JL,JK-1))))  
  ENDDO
ENDDO

DO JL=KIDIA,KFDIA
  ZTH(JL,KLEV+1)=PTS(JL)
  ZTH(JL,1)=PT(JL,1)-PAPRSF(JL,1)*(PT(JL,1)-ZTH(JL,2))&
   & /(PAPRSF(JL,1)-PAPRS(JL,2))  
ENDDO

!*         2.2    VERTICAL DISTRIBUTIONS

DO JL=KIDIA,KFDIA
  ZDPO(JL)=PAPRS (JL,1)
  IF (LNEWAER) THEN
    ZAEQSO(JL)=PAESEA(JL)*CVDAES(1)
    IF (LAEROSUL) THEN
      ZAEQLO(JL)=(PAELAN(JL)+PAESUL(JL))*CVDAEL(1)
      ZAEQFO(JL)=PAESUL(JL)*CVDAEL(1)
    ELSE
      ZAEQLO(JL)=PAELAN(JL)*CVDAEL(1)
      ZAEQFO(JL)=0._JPRB
    ENDIF
    ZAEQUO(JL)=PAESOO(JL)*CVDAEU(1)
    ZAEQDO(JL)=PAEDES(JL)*CVDAED(1)
  ELSE
    ZAEQSO(JL)=RCAEOPS*PAESEA(JL)*CVDAES(1)
    ZAEQLO(JL)=RCAEOPL*PAELAN(JL)*CVDAEL(1)
    ZAEQUO(JL)=RCAEOPU*PAESOO(JL)*CVDAEU(1)
    ZAEQDO(JL)=RCAEOPD*PAEDES(JL)*CVDAED(1)
  ENDIF
  ZAETRO(JL)=1.0_JPRB
  ZTOT(JL)=0.0_JPRB
ENDDO

DO JK=1,KLEV
  DO JL=KIDIA,KFDIA
    ZGRTH= ZTH(JL,JK)/ZTH(JL,JK+1)
    ZDPN(JL)=PAPRS (JL,JK+1)

    IF (LNEWAER) THEN
      ZAEQSN(JL)=PAESEA(JL)*CVDAES(JK+1)
      IF (LAEROSUL) THEN
        ZAEQLN(JL)=(PAELAN(JL)+PAESUL(JL))*CVDAEL(JK+1)
        ZAEQFN(JL)=PAESUL(JL)*CVDAEL(JK+1)
      ELSE
        ZAEQLN(JL)=PAELAN(JL)*CVDAEL(JK+1)
        ZAEQFN(JL)=0._JPRB
      ENDIF
      ZAEQUN(JL)=PAESOO(JL)*CVDAEU(JK+1)
      ZAEQDN(JL)=PAEDES(JL)*CVDAED(JK+1)
    ELSE
      ZAEQSN(JL)=RCAEOPS*PAESEA(JL)*CVDAES(JK+1)
      ZAEQLN(JL)=RCAEOPL*PAELAN(JL)*CVDAEL(JK+1)
      ZAEQUN(JL)=RCAEOPU*PAESOO(JL)*CVDAEU(JK+1)
      ZAEQDN(JL)=RCAEOPD*PAEDES(JL)*CVDAED(JK+1)
    ENDIF

    IF (0.5_JPRB*(PAPRS(JL,JK)+PAPRS(JL,JK+1)) < 999._JPRB) THEN
! for models with top above 10hPa
      ZAETRN(JL)=1.0_JPRB
      ZAETRO(JL)=1.0_JPRB
    ELSE
      ZAETRN(JL)=ZAETRO(JL)*(MIN(1.0_JPRB,ZGRTH))**RCTRPT
    ENDIF

    ZAETR=SQRT  (ZAETRN(JL)*ZAETRO(JL))
    ZDPNMO    =ZDPN(JL)-ZDPO(JL)
    ZTOT(JL) = ZTOT(JL) + ZAETR * ZDPNMO
    IF (LNEWAER) THEN
      PAERINDS(JL,JK)=(1.0_JPRB-ZAETR)*(ZAEQFN(JL)-ZAEQFO(JL))
    ENDIF
    PAER(JL,JK,1)=(1.0_JPRB-ZAETR)*(RCTRBGA*ZDPNMO+ ZAEQLN(JL)-ZAEQLO(JL))
    PAER(JL,JK,2)=(1.0_JPRB-ZAETR)*(ZAEQSN(JL)-ZAEQSO(JL))
    PAER(JL,JK,3)=(1.0_JPRB-ZAETR)*(ZAEQDN(JL)-ZAEQDO(JL))
    PAER(JL,JK,4)=(1.0_JPRB-ZAETR)*(ZAEQUN(JL)-ZAEQUO(JL))
    PAER(JL,JK,5)= ZAETR * RCVOBGA*ZDPNMO
    IF (LAEROVOL) THEN
      PAER(JL,JK,6)= ZAETR * ZDPNMO * PAEVOL(JL)
    ELSE 
      PAER(JL,JK,6)= ZAETR * RCSTBGA*ZDPNMO
    ENDIF
  ENDDO
  DO JL=KIDIA,KFDIA
    ZDPO(JL)=ZDPN(JL)
    ZAEQSO(JL)=ZAEQSN(JL)
    ZAEQLO(JL)=ZAEQLN(JL)
    ZAEQUO(JL)=ZAEQUN(JL)
    ZAEQDO(JL)=ZAEQDN(JL)
    ZAETRO(JL)=ZAETRN(JL)
  ENDDO
  IF (LNEWAER) THEN
    DO JL=KIDIA,KFDIA
      ZAEQFO(JL)=ZAEQFN(JL)
    ENDDO
  ENDIF
ENDDO

IF (LAEROVOL) THEN
  DO JL=KIDIA,KFDIA
    PAER(JL,:,6) = PAER(JL,:,6) / ZTOT(JL)
  ENDDO
ENDIF

DO JK=1,KLEV
  DO JAER=1,6
    DO JL=KIDIA,KFDIA
      PAER(JL,JK,JAER)=MAX(PAER(JL,JK,JAER),ZREPAER)
    ENDDO
  ENDDO
ENDDO

IF (LNEWAER) THEN
  DO JK=1,KLEV
    DO JL=KIDIA,KFDIA
       PAERINDS(JL,JK)=MAX(PAERINDS(JL,JK),ZREPAER)
    ENDDO
  ENDDO
ENDIF
!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RADAER',1,ZHOOK_HANDLE)
END SUBROUTINE RADAER
