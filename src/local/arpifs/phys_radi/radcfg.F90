!OPTIONS XOPT(HSFUN)
#ifdef RS6K
@PROCESS NOCHECK
#endif
SUBROUTINE RADCFG(YDGEOMETRY,YDSURF,YDMODEL,KST,KEND,&
 & KSTGLO ,KFLDS,&
 & LDLAND,&
 & POROG,PB2,PGFL,&
 & PUT9,PVT9,PTT9,PSPT9,&
 & PSP_SB,PSP_SG,PSP_SL,PSP_RR,&
 & PSD_VF,PSD_VN,PSD_VD,&
 & PALBD  ,PALBP  ,PCCNL  ,PCCNO,&
 & PPRE9  ,PPRE9F ,PNEB,&
 & PCCC   ,PHCC   ,PLCC   ,PMCC  ,PTCC,&
 & PGELAM,&
 & PDELP9 ,PSPECTRALEMISS,PEMIT,&
 & PGEMU  ,PMU0M  ,PQSAT,&
 & PQICE  ,PQLI   ,PQRAIN ,PRAINT,&
 & PAERO&
 & )  

!**** *RADCFG* - Radiation calculations on fine grid

!     Purpose.
!     --------

!**   Interface.
!     ----------
!        *CALL* *RADCFG*

!        Explicit arguments :
!        --------------------
!        KST,KEND  : FIRST AND LAST GRID POINTS FOR COMPUTATIONS
!        KPROC     : AID TO DEBUG
!        POROG     : surface orography
!        PB2       : semilagrangian buffer 2
!        PGFL      : unified_treatment grid-point fields

!        LDLAND    : LAND MASK (.T. OVER LAND)
!        PALBD     : DIFFUSE ALBEDO IN THE NSW SW INTERVALS
!        PALBP     : PARALLEL ALBEDO IN THE NSW SW INTERVALS
!        PCCNL     : CCN CONCENTRATION OVER LAND
!        PCCNO     : CCN CONCENTRATION OVER OCEAN
!        PPRE9     : HALF LEVEL PRESSURE
!        PPRE9F    : FULL LEVEL PRESSURE
!        PNEB      : FRACTIONAL CLOUDINESS FOR RADIATION
!        PCCC      : CONVECTIVE CLOUD COVER DIAGNOSTIC
!        PHCC      : HIGH CLOUD COVER DIAGNOSTIC
!        PLCC      : LOW CLOUD COVER DIAGNOSTIC
!        PMCC      : MEDIUM CLOUD COVER DIAGNOSTIC
!        PTCC      : TOTAL CLOUD COVER DIAGNOSTIC
!        PDELP9    : PRESSURE DIFFERENCE ACROSS LAYERS
!        PSPECTRALEMISS : MODEL SURFACE LONGWAVE SPECTRAL EMISSIVITY
!        PEMIT     : DIAGNOSED TOTAL SURFACE LONGWAVE EMISSIVITY
!        PGEMU     : SINE OF LATITUDE ON THE REAL EARTH
!        PMU0M     : SOLAR ANGLE (BETWEEN RADIATION STEPS)
!        PQSAT     : SPECIFIC HUMIDITY AT SATURATION
!        PQICE     : SPECIFIC HUMIDITY OF SOLID WATER FOR RADIATION
!        PQLI      : SPECIFIC HUMIDITY OF LIQUID WATER FOR RADIATION
!        PQRAIN    : SPECIFIC HUMIDITY OF RAIN WATER FOR RADIATION
!        PRAINT    : RAIN RATE FOR RADIATION

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!        Called by RADDRV.

!     Reference.
!     ----------

!     Author.
!     -------
!      George Mozdzynski  *ECMWF*
!      Original : 94-12-5

!     Modifications.
!     --------------
!      N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!      S. Boussetta/G.Balsamo   May 2009   (Add variable LAI)
!      K. Yessad (Jan 2011): remove useless overdimension.
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM
!      K. Yessad (July 2014): Move some variables.
!      R. Hogan     (Nov 2014): Removed computation of ZMU0 - not used
!      R. Hogan     (Apr 2015): Solar zenith angle calculated in cos_sza
!      K. Yessad (Feb 2018): remove deep-layer formulations.
!      R. Hogan  (Jan 2019): Spectral longwave emissivity in NLWEMISS intervals
!     ------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF, GPPOPER
USE PARKIND1           , ONLY : JPIM, JPRB
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMCT0             , ONLY : LTWOTL
USE YOMCT3             , ONLY : NSTEP
USE INTDYN_MOD         , ONLY : YYTXYB

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)       ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLDS 
INTEGER(KIND=JPIM),INTENT(IN)    :: KST 
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTGLO 
LOGICAL           ,INTENT(OUT)   :: LDLAND(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PB2(YDGEOMETRY%YRDIM%NPROMA,KFLDS) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PUT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PVT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PTT9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSPT9(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SB(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS,YDSURF%YSP_SBD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SG(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SGD%NLEVS,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_SL(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SLD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSP_RR(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VF(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VFD%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VN(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VND%NDIM)
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PSD_VD(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VDD%NDIM)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PALBD(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PALBP(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCCNL(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCCNO(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PPRE9(YDGEOMETRY%YRDIM%NPROMA,0:YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PPRE9F(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PNEB(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCCC(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PHCC(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLCC(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMCC(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTCC(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGELAM(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDELP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSPECTRALEMISS(YDGEOMETRY%YRDIM%NPROMA,YDMODEL%YRML_PHY_RAD%YRERAD%NLWEMISS)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMIT(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PGEMU(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PMU0M(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQSAT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQICE(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQLI(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PQRAIN(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PRAINT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERO(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NAERO) 
!     ------------------------------------------------------------------
REAL(KIND=JPRB) :: ZXYB9  (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YYTXYB%NDIM)

! ZR9    : (NPROMA,NFLEVG)       ; R
! ZCP9   : (NPROMA,NFLEVG)       ; CP

REAL(KIND=JPRB) :: ZR9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG),ZCP9(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)

INTEGER(KIND=JPIM) ::  IEND, IENDC,  IST, ISTC, JAER, JK, JL, JROF
INTEGER(KIND=JPIM) ::  IBL
LOGICAL :: LLFSTEP

REAL(KIND=JPRB) :: ZDTPHY
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "gprcp_pgfl.intfb.h"
#include "radpar.intfb.h"
#include "cos_sza.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('RADCFG',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDVAB=>YDGEOMETRY%YRVAB, YDPTRSLB2=>YDMODEL%YRML_DYN%YRPTRSLB2,&
& YDRIP=>YDMODEL%YRML_GCONF%YRRIP,YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD,   YGFL=>YDMODEL%YRML_GCONF%YGFL                     &
& )

ASSOCIATE(NAERO=>YGFL%NAERO, YA=>YGFL%YA, YAERO=>YGFL%YAERO,   YI=>YGFL%YI, YL=>YGFL%YL, YQ=>YGFL%YQ,             &
& NPROMA=>YDDIM%NPROMA,   NFLEVG=>YDDIMV%NFLEVG,   MSLB2VVEL=>YDPTRSLB2%MSLB2VVEL,   NSTART=>YDRIP%NSTART,        &
& TDT=>YDRIP%TDT,   YSD_VD=>YDSURF%YSD_VD, YSD_VF=>YDSURF%YSD_VF,   YSD_VN=>YDSURF%YSD_VN, YSP_RR=>YDSURF%YSP_RR, &
& YSP_SB=>YDSURF%YSP_SB,   YSP_SBD=>YDSURF%YSP_SBD, YSP_SG=>YDSURF%YSP_SG, YSP_SGD=>YDSURF%YSP_SGD,               &
& YSP_SL=>YDSURF%YSP_SL)
!     ------------------------------------------------------------------

!*       1.    Interface to global arrays.
!              ---------------------------

!*       1.1   INITIAL COMPUTATIONS

IF(NSTEP > 0) THEN
  LLFSTEP=.FALSE.
ELSE
  LLFSTEP=.TRUE.
ENDIF

IST =KST
IEND=KEND
ISTC =KST
IENDC=KEND
IBL=(KSTGLO-1)/NPROMA+1

!*       1.5   SURFACE PRESSURE VARIABLES

DO JROF=IST,IEND
  PPRE9 (JROF,NFLEVG)=EXP(PSPT9(JROF))
ENDDO

!*       1.6   SURFACE FIELDS FOR PHYSICS

IF(LLFSTEP .AND. .NOT. LTWOTL) THEN
  CALL GPPOPER(YDMODEL%YRML_DYN%YRDYN,'SET9TO0',YDSURF,PSP_SB=PSP_SB,PSP_SG=PSP_SG,PSP_SL=PSP_SL,PSP_RR=PSP_RR)
ENDIF


!     ------------------------------------------------------------------

!*       3.   PREPARE FOR PHYSICS  COMPUTATIONS
!             ---------------------------------

!*       3.1   Compute pressure and ZXYB9
ZXYB9(:,:,YYTXYB%M_DELP)=PDELP9
!*       3.1   Compute pressure +
!*       3.2   Compute auxiliary arrays related to the vertical
CALL GPHPRE(NPROMA,NFLEVG,IST,IEND,YDVAB,PPRE9,PXYB=ZXYB9,PRESF=PPRE9F)
PDELP9=ZXYB9(:,:,YYTXYB%M_DELP)

!*       3.3   Compute R, Cp 
CALL GPRCP_PGFL(NPROMA,IST,IEND,NFLEVG,PGFL=PGFL,KGFLTYP=9,PCP=ZCP9,PR=ZR9)

!*       3.5   Astronomy.

! Compute solar zenith angle for a time half-way between calls to the
! radiation scheme, or as an average for the entire radiation timestep
CALL COS_SZA(YDERAD,YDMODEL%YRML_PHY_RAD%YRERIP,YDRIP,IST,IEND,NPROMA,PGEMU,PGELAM,.TRUE.,PMU0M)


!*       3.7  ADIABATIC TENDENCIES AS INPUT FOR PHYSICS
!             (SLB2T1 ETC. TEMPORALY USED FOR TENDENCIES)
ZDTPHY=TDT

!*       3.8  PROGNOSTIC AEROSOLS

DO JAER=1,NAERO
  DO JK=1,NFLEVG
    DO JL=IST,IEND
      PAERO(JL,JK,JAER)=PGFL(JL,JK,YAERO(JAER)%MP9_PH) 
    ENDDO
  ENDDO
ENDDO


!     ------------------------------------------------------------------

!*       4.   CALL PHYSICS
!             -------------

!*     4.1  CALL PHYSICS NECESSARY FOR RADINT

CALL RADPAR (YDMODEL, IST,IEND,NPROMA,YSP_SBD%NLEVS,YSP_SGD%NLEVS,KSTGLO,&
 & 1,NFLEVG,NSTART,NSTEP,&
 ! - INPUT
 & PPRE9   ,PPRE9F,&
 & PTT9 ,PGFL(1,1,YQ%MP9_PH),&
 & PUT9 ,PVT9,&
 & PGFL(1,1,YL%MP9_PH) ,PGFL(1,1,YI%MP9_PH) ,PGFL(1,1,YA%MP9_PH),&
 & PSP_SG(1,1,YSP_SG%YF%MP9 )     ,PSP_SG(1,1,YSP_SG%YR%MP9),&
 & PSP_RR(1,YSP_RR%YT%MP9 ),&
 & PSP_RR(1,YSP_RR%YW%MP9  )    ,PSP_SB(1,1,YSP_SB%YQ%MP9 ),PB2(1,MSLB2VVEL),&
 & PSP_SL(1,YSP_SL%YLICD%MP9), PSP_SL(1,YSP_SL%YLICT%MP9),&!LAKE
 & PSD_VF(1,YSD_VF%YCLK%MP)     ,&!LAKE
 & PSD_VF(1,YSD_VF%YALBF%MP)    ,&
 & PSD_VF(:,YSD_VF%IALSTART:YSD_VF%IALEND), & ! 4 or 6 albedo coefficients
 & PSP_SG(1,1,YSP_SG%YA%MP9)   ,PSD_VF(1,YSD_VF%YEMISF%MP),&
 & PSD_VF(1,YSD_VF%YLSM%MP)     ,PMU0M              ,PGEMU,&
 & PSD_VF(1,YSD_VF%YZ0F%MP),&
 & PSD_VF(1,YSD_VF%YTVL%MP)     ,PSD_VF(1,YSD_VF%YTVH%MP),&
 & PSD_VF(1,YSD_VF%YCVL%MP)     ,PSD_VF(1,YSD_VF%YCVH%MP),&
 & PSD_VF(1,YSD_VF%YLAIL%MP)    ,PSD_VF(1,YSD_VF%YLAIH%MP),&
 & PSD_VF(1,YSD_VF%YSOTY%MP)    ,PSD_VF(1,YSD_VF%YGETRL%MP),&
 & PSD_VF(1,YSD_VF%YCI%MP),&
! & PGFL(1,1,YAERO%MP9_PH),&
 ! - UPDATED
 & PSD_VN(1,YSD_VN%YTOP%MP)     ,PSD_VN(1,YSD_VN%YBAS%MP) ,PSD_VN(1,YSD_VN%YACPR%MP),&
 ! - OUTPUT
 & LDLAND  ,PALBD  ,PALBP  ,PCCNL  ,PCCNO,&
 & PNEB    ,PQICE  ,PQLI   ,PQSAT,&
 & PCCC    ,PHCC   ,PLCC   ,PMCC   ,PTCC,&
 & PSPECTRALEMISS,PEMIT,&
 & PSD_VD(1,YSD_VD%YZ0F%MP)&
! &,PAERO   )  
 & )

! -- temporary
DO JK=1,NFLEVG
  DO JL=IST,IEND
    PQRAIN(JL,JK)=0.0_JPRB
    PRAINT(JL,JK)=0.0_JPRB
  ENDDO
ENDDO

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RADCFG',1,ZHOOK_HANDLE)
END SUBROUTINE RADCFG
