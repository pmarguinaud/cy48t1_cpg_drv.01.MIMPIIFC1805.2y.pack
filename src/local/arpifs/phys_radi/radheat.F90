!OPTIONS XOPT(HSFUN)
SUBROUTINE RADHEAT &
 & (  YDERAD,YDERDI,YDML_PHY_MF,KIDIA  , KFDIA  , KLON   , KLEV,&
 & PAPHM1 ,&
 & PEMIS  , PEMTED , PMU0,&
 & PQM1,&
 & PTE    , PTRSOL , PTRSOD  , PTSM1M , PTSPHY,&
 & PTRSODIR, PTRSODIF, PALBD , PALBP,          &
 & PFRSO  , PFRTH  , PFRSODS , PFRTHDS,        &
 & PCEMTR , PCTRSO , PFRSOC  , PFRTHC,         &
 & PSUDU  , PSDUR  , PDSRP,                    &
 & PSFSWDIR , PSFSWDIF,                        &
 & PFRSOPS, PFRSOFS, PFRSOPT                   &
 & )  

!**** *RADHEAT* - COMPUTES TEMPERATURE CHANGES DUE TO RADIATION.

!     PURPOSE.
!     --------

!          THIS ROUTINE COMPUTES THE TENDENCIES OF THE ATMOSPHERE'S
!     TEMPERATURE DUE TO THE EFFECTS OF LONG WAVE AND SHORT WAVE
!     RADIATION. THE COMPUTATION IS DONE ON THE T-1 TIME LEVEL USING
!     VALUES OF ATMOSPHERIC TRANSMISIVITIES AND EMISSIVITIES THAT HAVE
!     BEEN STORED AT THE LAST FULL RADIATION TIME STEP. THE SURFACE
!     SOLAR FLUX LATER TO BE USED IN THE SOIL PROCESS CALCULATIONS IS
!     ALSO STORED.

!**   INTERFACE.
!     ----------

!          *RADHEAT* IS CALLED FROM *CALLPAR*.
!          THE ROUTINE TAKES ITS INPUT FROM THE LONG-TERM STORAGE: TS,
!     T AND Q AT T-1 AND P ON LEVEL BOUNDARIES AND IN THE MIDDLE OF THE
!     LAYERS AT THE SAME TIME. ALSO USED ARE THE FOUR PARAMERTERS
!     DESCRIBING THE SUN'S POSITION. THE ROUTINE RETURNS ITS OUTPUT TO
!     THE LONG TERM STORAGE: TENDENCIES OF T AND SURFACE SOLAR FLUX.

!     METHOD.
!     -------

!          A CALL TO SUBROUTINE *SOLANG* GIVES FIELDS OF SOLAR ZENITH
!     ANGLES AND RELATIVE DAY LENGTH FROM WHICH AN EFFECTIVE SOLAR
!     INFLUX IS COMPUTED. THE RESULTS ARE OF COURSE DIFFERENT DEPENDING
!     ON THE SWITCH ON OR OFF OF THE DIURNAL CYCLE. PRODUCT OF SOLAR
!     INFLUX BY TRANSMISSIVITIES LEADS TO SOLAR FLUXES. THEN THE
!     TEMPERATURES ARE INTERPOLATED/EXTRAPOLATED TO THE LAYER BOUNDARIES
!     (AT THE BOTTOM ONE TAKES THE SURFACE TEMPERATURE) AND A PRODUCT BY
!     EMISSIVITIES OF SIGMA*T**4 GIVES THERMAL FLUXES. THE TWO FLUXES
!     ARE ADDED AND DIVERGENCES COMPUTED TO GIVE HEATING RATES.

!          WITH THE METEO-FRANCE PHYSICS, THE CONSTANT EMISSIVITY
!     HYPOTHESIS LEADS TO SPURIOUS OSCILLATIONS IN THE STRATOSPHERE. A
!     CONSTANT THERMAL FLUX APPROACH IS THEREFORE USED UNTIL A NEW
!     SUBROUTINE RISES...

!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------

!          SEE RADIATION'S PART OF THE MODEL'S DOCUMENTATION FOR DETAILS
!     ABOUT THE MATHEMATICS OF THIS ROUTINE.

!     AUTHOR.
!     -------
!      J.-F. GELEYN     E.C.M.W.F.    82/06/03.

!     MODIFICATIONS.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!      JJMorcrette 20090408 bug-fix to solar zenith angle
!      M. Janiskova     03-Mar-2009 Code re-arrangement for adjoint comp.
!      A. Alias    Dec 2010 Call to RADHEAT modified to compute PSFSWDIF/PSFSWDIR outputs
!      Y. Bouteloup Nov 2014 : Correction of a bug in the computation of SW downward diagnostic
!                              in the case LMSE=TRUE
!      Y. Bouteloup Jun 2015 : Re-activation of LMSE protection
!-----------------------------------------------------------------------

USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMCST   , ONLY : RG       ,RSIGMA   ,RCPD
USE YOETHF   , ONLY : RVTMP2
USE YOERDI   , ONLY : TERDI
USE YOERAD   , ONLY : TERAD

IMPLICIT NONE

TYPE(TERAD)       ,INTENT(IN)            :: YDERAD
TYPE(TERDI)       ,INTENT(IN)            :: YDERDI
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN)   :: YDML_PHY_MF
INTEGER(KIND=JPIM),INTENT(IN)            :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)            :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)            :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)            :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PAPHM1(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PEMIS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PEMTED(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PMU0(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PQM1(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT)         :: PTE(KLON,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTRSOL(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTRSOD(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTSM1M(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTSPHY 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTRSODIR(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(IN)            :: PTRSODIF(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(IN)            :: PALBD(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(IN)            :: PALBP(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSO(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRTH(KLON,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSODS(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRTHDS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PCEMTR(KLON,2) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PCTRSO(KLON,2) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSOC(KLON,2) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRTHC(KLON,2) 
REAL(KIND=JPRB)   ,INTENT(IN)            :: PSUDU(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PSDUR(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PDSRP(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PSFSWDIR(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PSFSWDIF(KLON,YDERAD%NSW)
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSOPT(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSOFS(KLON)
REAL(KIND=JPRB)   ,INTENT(OUT)           :: PFRSOPS(KLON)

!-----------------------------------------------------------------------

REAL(KIND=JPRB) ::&
 & ZDTDT(KLON)   , ZDTDIV(KLON) &
 & ,  ZFLB(KLON) , ZFLT(KLON) , ZFSO(KLON) , ZFTE(KLON)&
 & ,  ZI0(KLON)  , ZTH4(KLON)  

INTEGER(KIND=JPIM) :: ILONS, JK, JL, JSW

REAL(KIND=JPRB) :: ZBUD, ZCONS1, ZCONS3, ZRII0, ZTMST
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

!*    COMPUTATIONAL CONSTANTS.
!     ------------- ----------

IF (LHOOK) CALL DR_HOOK('RADHEAT',0,ZHOOK_HANDLE)
ASSOCIATE(NSW=>YDERAD%NSW, &
 & RRAE=>YDERDI%RRAE, RSUNDUR=>YDERDI%RSUNDUR, &
 & LMSE=>YDML_PHY_MF%YRARPHY%LMSE, LHLRADUPD=>YDML_PHY_MF%YRPHY%LHLRADUPD,RII0=>YDML_PHY_MF%YRPHY3%RII0)
ZTMST=PTSPHY
ZCONS1=RRAE*(RRAE+2.0_JPRB)
ZCONS3=RG/RCPD
ILONS=KFDIA-KIDIA+1
ZBUD = 1.0_JPRB
ZRII0=RII0

!     ------------------------------------------------------------------

!*         1.     SECURITIES.
!                 -----------
DO JL=KIDIA,KFDIA
   PFRSODS  (JL)=0.0_JPRB
   PFRSOFS  (JL)=0.0_JPRB
   PFRSOPS  (JL)=0.0_JPRB
ENDDO

!     ------------------------------------------------------------------

!*         2.     INCIDENT SOLAR RADIATION
!                 ------------------------

DO JL=KIDIA,KFDIA
  IF (PMU0(JL) >= 1.E-10_JPRB) THEN
!!    ZI0(JL)=RRAE/(SQRT(PMU0(JL)**2+ZCONS1)-PMU0(JL)) * ZRII0
    ZI0(JL)=PMU0(JL)*ZRII0
  ELSE
    ZI0(JL)=0.0_JPRB
  ENDIF
ENDDO
!     ------------------------------------------------------------------

!*         3.     TEMPERATURES AT LAYERS' BOUNDARIES.
!                 -----------------------------------

!     ------------------------------------------------------------------

!*         4.     FLUXES AND THEIR DIVERGENCES.
!                 ------ --- ----- ------------

!*         4.1     TOP FLUX.

DO JL=KIDIA,KFDIA
  ZFSO(JL)=ZI0(JL)*PTRSOL(JL,1)
  ZFTE(JL)=PEMTED(JL,1)

  PFRTH(JL,1)=ZFTE(JL)
  PFRSO(JL,1)=ZFSO(JL)

  PFRTHC(JL,1)=PCEMTR(JL,1)
  PFRSOC(JL,1)=ZI0(JL)*PCTRSO(JL,1)
ENDDO

!*         4.2     VERTICAL LOOP, BOTTOM FLUX AND HEATING RATE.

!***
DO JK=1,KLEV
!***
  DO JL=KIDIA,KFDIA
    ZFLT(JL)=ZFSO(JL)+ZFTE(JL)

    ZFSO(JL)=ZI0(JL)*PTRSOL(JL,JK+1)
! Set surface solar flux to minimum value for stability in vertical diffusion
    IF (JK == KLEV .AND. ZFSO(JL) < 1.E-15_JPRB) THEN
      ZFSO(JL)=1.E-15_JPRB
    ENDIF
    ZFTE(JL)=PEMTED(JL,JK+1)
    ZFLB(JL)=ZFSO(JL)+ZFTE(JL)
    ZDTDIV(JL)=1.0_JPRB/((PAPHM1(JL,JK+1)&
     & -PAPHM1(JL,JK))*(1.0_JPRB+RVTMP2*PQM1(JL,JK))) 
    ZDTDT(JL)=-ZCONS3*(ZFLB(JL)-ZFLT(JL))*ZDTDIV(JL) 
    PTE(JL,JK)=PTE(JL,JK)+ZDTDT(JL)
  ENDDO

!*         4.4     FLUX SWAP, END OF THE LOOP AND SURFACE SOLAR FLUX.

  DO JL=KIDIA,KFDIA
    PFRTH(JL,JK+1)=ZFTE(JL)
    PFRSO(JL,JK+1)=ZFSO(JL)
  ENDDO
!***
ENDDO
!***

DO JL=KIDIA,KFDIA
  PFRTHC(JL,2)=PCEMTR(JL,2)
  PFRSOC(JL,2)=ZI0(JL)*PCTRSO(JL,2)
ENDDO

!*   4.5     SOLAR AND TERRESTRIAL DOWNWARD FLUXES AT SURFACE AND TOA

DO JL=KIDIA,KFDIA

  PDSRP(JL)=ZI0(JL)*PSUDU(JL)
  IF (PDSRP(JL) >= RSUNDUR) THEN
    PSDUR(JL)=1.0_JPRB
  ELSE
    PSDUR(JL)=0.0_JPRB
  ENDIF
  ZTH4(JL)=RSIGMA*PTSM1M(JL)**4
  PFRTHDS(JL)=(PEMTED(JL,KLEV+1)+PEMIS(JL)*ZTH4(JL))/PEMIS(JL)
  !Total downward diag top
  PFRSOPT(JL)=ZI0(JL)
ENDDO
IF (LMSE.OR.LHLRADUPD) THEN
   DO JSW=1,NSW
      DO JL=KIDIA,KFDIA
         ! Direct net
         ZFSO(JL)=MAX(ZI0(JL)*PTRSODIR(JL,JSW),1.E-15_JPRB)
         ! Direct Down
         PSFSWDIR(JL,JSW)=ZFSO(JL)/(1.0_JPRB-PALBP(JL,JSW))
         ! Diffu net
         ZFSO(JL)=MAX(ZI0(JL)*PTRSODIF(JL,JSW),1.E-15_JPRB)
         ! Diffu Down
         PSFSWDIF(JL,JSW)=ZFSO(JL)/(1.0_JPRB-PALBD(JL,JSW))
         !Direct downward diag
         PFRSOPS(JL)=PFRSOPS(JL)+PSFSWDIR(JL,JSW)
         !Diffuse downward diag
         PFRSOFS(JL)=PFRSOFS(JL)+PSFSWDIF(JL,JSW)
         !Global radiation
         PFRSODS(JL)=PFRSODS(JL)+PSFSWDIF(JL,JSW)+PSFSWDIR(JL,JSW) 
      ENDDO
   ENDDO
ELSE
   DO JL=KIDIA,KFDIA 
      PFRSODS(JL)=ZI0(JL)*PTRSOD(JL)
   ENDDO
ENDIF
!     ------------------------------------------------------------------

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RADHEAT',1,ZHOOK_HANDLE)
END SUBROUTINE RADHEAT
