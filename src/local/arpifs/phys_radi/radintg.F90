!OPTIONS XOPT(HSFUN)
#ifdef RS6K        
@PROCESS NOCHECK
#endif
SUBROUTINE RADINTG(YDGEOMETRY, YDMODEL,KLEV  , KMODE,&
 & PALBD , PALBP , PCCNL , PCCNO,&
 & PAPRS , PAPRSF, PCCO2 , PCLFR,&
 & PQO3  , PAER  , PAERMACCS,PAERMACCL,PAERMACCSJ,PAERMACCLJ,&
 & PAERTGS,PAERTGL, PAERO , PGHG,&
 & PDP   , PSPECTRALEMISS,&
 & PMU0  , PQ,&
 & PQS   , PQIWP , PQSWP , PQLWP , PFSDP , PQRAIN, PRAINT, PSLM  , PT,&
 & PECPO3, PTS,&
 & PEMTD , PEMTU , PTRSO , PTRSC , PEMTC , PNTOP , PNBAS,&
 & PPERTT,&
 & PEMIT , PTH   , PFRTED, PTRSOD, PTRSODC, PEMTDC,&
 & PSUDU , PUVDF , PPARF , PPARCF, PTINCF,&
 & PFDIR , PCDIR , PLWDERIVATIVE)  

!**** *RADINTG* - RADIATION INTERFACE TO ACTUAL RADIATION SCHEME

!     PURPOSE.
!     --------
!           CONTROLS THE SPACE INTERPOLATION FOR RADIATION FIELDS

!**   INTERFACE.
!     ----------

!        EXPLICIT ARGUMENTS :
!        --------------------
! KLEV   : NUMBER OF LEVELS

! PAER   : (NGPTOT,6,KLEV)      ; OPTICAL THICKNESS OF THE AEROSOLS
! PAERO  : (NGPTOT,KLEV,NAERO)  ; MASS MIXING RATIO OF PROGNOSTIC AEROSOLS
! PGHG   : (NGPTOT,8,KLEV)      ; MASS MIXING RATIO OF GREENHOUSE GASES
! PALBD  : (NGPTOT,NTSW)        ; DIFFUSE ALBEDO IN THE NSW SW INTERVALS
! PALBP  : (NGPTOT,NTSW)        ; PARALLEL ALBEDO IN THE NSW SW INTERVALS
! PCCNL  : (NGPTOT)             ; CCNs CONCENTRATION OVER LAND
! PCCNO  : (NGPTOT)             ; CCNs CONCENTRATION OVER OCEAN
! PAPRS  : (NGPTOT,KLEV+1)      ; HALF LEVEL PRESSURE
! PAPRSF : (NGPTOT,KLEV )       ; FULL LEVEL PRESSURE
! PCCO2  :                      ; CONCENTRATION IN CO2 (PA/PA)
! PCLFR  : (NGPTOT,KLEV )       ; CLOUD FRACTIONAL COVER
! PQO3   : (NGPTOT,KLEV )       ; OZONE MIXING RATIO (MASS)
! PDP    : (NGPTOT,KLEV)        ; LAYER PRESSURE THICKNESS
! PSPECTRALEMISS : (NGPTOT,NLWEMISS) ; LONGWAVE SPECTRAL SURFACE EMISSIVITY
! PMU0   : (NGPTOT)             ; SOLAR ANGLE
! PNBAS  : (NGPTOT)             ; INDEX OF BASE OF CONVECTIVE LAYER
! PNTOP  : (NGPTOT)             ; INDEX OF TOP OF CONVECTIVE LAYER
! PQ     : (NGPTOT,KLEV )       ; SPECIFIC HUMIDITY PA/PA
! PQS    : (NGPTOT,KLEV )       ; SATURATION SPECIFIC HUMIDITY PA/PA
! PQIWP  : (NGPTOT,KLEV )       ; ICE WATER KG/KG
! PQSWP  : (NGPTOT,KLEV )       ; SNOW WATER KG/KG
! PQLWP  : (NGPTOT,KLEV )       ; LIQUID WATER KG/KG
! PFSDP  : (NGPTOT,KLEV )       ; cloud heterogeneity FSD
! PQRAIN : (NGPTOT,KLEV )       ; RAIN   WATER KG/KG
! PRAINT : (NGPTOT,KLEV )       ; RAIN RATE M/S  
! PSLM   : (NGPTOT)             ; LAND-SEA MASK
! PT     : (NGPTOT,KLEV)        ; FULL LEVEL TEMPERATURE
! PECPO3 : (NGPTOT,KLEV)        ; FULL LEVEL PROGNOSTIC OZONE (ECMWF)
! PTS    : (NGPTOT)             ; SURFACE TEMPERATURE
! PPERTT : (NGPTOT,YSPP%N2DRAD) ; SPP random patterns for radiation
!     ==== OUTPUTS ===
! PEMTD (NGPTOT,KLEV+1)         ; TOTAL-SKY NET LONGWAVE FLUX
! PEMTU (NGPTOT,KLEV+1)         ; TOTAL-SKY UPWARD LONGWAVE FLUX (NOT USED IN CALLING FUNCTION!)
! PTRSO (NGPTOT,KLEV+1)         ; TOTAL-SKY SHORTWAVE TRANSMISSIVITY
! PEMTC (NGPTOT,KLEV+1)         ; CLEAR-SKY NET LONGWAVE FLUX
! PTRSC (NGPTOT,KLEV+1)         ; CLEAR-SKY SHORTWAVE TRANSMISSIVITY
! PTH   (NGPTOT,KLEV+1)         ; HALF LEVEL TEMPERATURE
! PFRTED(NGPTOT,NLWOUT)         ; Total-sky surface downward longwave flux
! PTRSOD(NGPTOT)                ; TOTAL-SKY SURFACE SW TRANSMISSIVITY
! PTRSODC(NGPTOT)               ; SURFACE DOWNWARD CLEAR SKY SW TRANSMISSIVITY
! PEMTDC(NGPTOT)                ; SURFACE DOWNWARD CLEAR SKY LW FLUX
! PEMIT (NGPTOT)                ; SURFACE TOTAL LONGWAVE EMISSIVITY
! PSUDU (NGPTOT)                ; SUNSHINE DURATION
! PUVDF (NGPTOT)                ; UV(-B) DOWNWARD AT SURFACE
! PPARF (NGPTOT)                ; PHOTOSYNTHETICALLY ACTIVE RADIATION
! PPARCF(NGPTOT)                ; CLEAR-SKY PHOTOSYNTHETICALLY ACTIVE RADIATION
! PTINCF(NGPTOT)                ; TOA INCIDENT SOLAR RADIATION
! PFDIR (NGPTOT)                ; TOTAL SKY SURFACE DIRECT SW RADIATION 
! PCDIR (NGPTOT)                ; CLEAR-SKY SURFACE DIRECT SW RADIATION 
! PLwDerivative(NGPTOT,KLEV+1)  ; Derivative of upwelling LW flux w.r.t. surface value

!        IMPLICIT ARGUMENTS :   NONE
!        --------------------

!     METHOD.
!     -------
!        SEE DOCUMENTATION

!     EXTERNALS.
!     ----------

!     REFERENCE.
!     ----------
!        ECMWF RESEARCH DEPARTMENT DOCUMENTATION OF THE IFS

!     AUTHORS.
!     --------
!        G. MOZDZYNSKI  *ECMWF*

!     MODIFICATIONS.
!     --------------
!        01-11-08 : GMozdzynski      Original
!        01-10-10 : JJMorcrette      CCNs
!        01-11-08 : JJMorcrette      safety check
!        01-12-07 : JJMorcrette      safety check on interactive O3
!        02-10-01 : GMozdzynski      support on-demand radiation comms
!        03-03-31 : GMozdzynski      use minimum halo for radiation comms
!        03-05-15 : JJMorcrette      PAR, UVB
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!        JJMorcrette 20060721 PP of clear-sky PAR and TOA incident solar radiation
!        JJMorcrette 20071015        3D climatological input fields for CO2, CH4, N2O, NO2, CFC11, 12, 22 and CCL4
!        JJMorcrette 20080422        prognostic aerosols
!        D.Salmond   20080603        CO2-CL4 moved to from input to local
!        F. Vana     27-Aug-2008  new interpolation data-flow
!        K. Yessad Dec 2008: merge SLCOMM+SLCOMM1 -> SLCOMM.
!        K. Yessad Dec 2008: merge the different (E)SLEXTPOL.. -> (E)SLEXTPOL.
!        JJMorcrette 20091201        Total and clear sky downward direct SW flux at surface
!        2011-02    A.Alias Change of call to RADLSW
!                             Auxiliary diagnostic radiation fields
!        G.Mozdzynski (Jan 2011): OOPS cleaning, use of derived type SL_STRUCT
!        G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM and TCSGLEG
!        HHersbach   20110401 CMIP5-recommended SPARC ozone; original from EC-EARTH
!        M Ahlgrimm   31 Oct 2011  Surface Downward clear-sky LW and SW fluxes
!        A Geer       29 May 2012  Simpler interface to interpolation routines: laiddiobs/laidliobs
!        G. Mozdzynski (May 2012): further cleaning
!        T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!        R. Hogan     May 2014  Interpolate LW partial derivatives and downwelling LW surface flux; 
!                               average 4th power of skin temperature
!        R. Hogan     June 2014 Correction of SW fluxes for local albedo
!        K. Yessad (July 2014): Move some variables.
!        JJMorcrette&ABozzo (2014/2015): MACC aerosol climatology
!        ABozzo 2015: extra diagnostics for debug. GRIB outputs for aerosol 
!                climatology (Tegen and MACC). Extra fields activated 
!                for LDIAGFORGING=T
!        R. Hogan     Sept 2015 Added call to new radiation scheme
!        F. Vana   17-Dec-2015:  Support for single precision
!        M. Leutbecher & S.-J. Lock (Jan 2016) Introduced SPP scheme (LSPP)
!        S.Remy    Sept 2016 Update IRADAER (3 more aerosol variables for nitrate and ammonium
!        R. Hogan  Sept 2016 Check for sensible fluxes before approx SW update
!        R. Hogan  Sept 2016 Pass SPP perturbations to RADIATION_SCHEME
!        Y. Bouteloup october 2016 : Add IFDIF (new output flux from radlswr)
!        C. Roberts/R. Senan Feb 2017 - Support for CMIP6 forcings
!        R. Hogan  Mar  2017 Add security that downwelling shortwave must not be less than net
!        R. Hogan  Mar  2017 Add security that skin temperature after interp must be positive
!        M Ahlgrimm 2017-11-11 cloud heterogeneity FSD
!        R. Hogan  July 2018 Optionally interpolate gridbox-mean rather than in-cloud water contents
!        R. Hogan  Jan  2019 Spectral longwave emissivity in NLWEMISS intervals
!        R. Hogan  Feb  2019 Pass out surface longwave downwelling in each emissivity interval
!-----------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE TYPE_MODEL   , ONLY : MODEL
USE PARKIND1     , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMCST       , ONLY : RMD, RMO3, RPI, YRCST
USE YOETHF       , ONLY : YRTHF
USE YOMLUN       , ONLY : NULOUT, NULERR, RESERVE_LUN, FREE_LUN
USE YOESW        , ONLY : RADJUST, RAER
USE YOERDU       , ONLY : NIMP, NOUT, REPSCQ, REPSCO
USE YOMCT0       , ONLY : NUNDEFLD
USE YOMCT3       , ONLY : NSTEP
USE YOMMP0       , ONLY : NPROC, NPRTRV, MYSETV, MYPROC, LSLDEBUG
USE YOECMIP      , ONLY : NO3CMIP
USE YOESRTAER    , ONLY : RSRTAUA
USE SPP_MOD      , ONLY : YSPP_CONFIG, YSPP

!   -------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL)       ,INTENT(INOUT) :: YDMODEL
INTEGER(KIND=JPIM),INTENT(IN)    :: KLEV 
INTEGER(KIND=JPIM),INTENT(IN)    :: KMODE 
! Note that even though albedo is dimensioned up to NTSW=14 spectral
! intervals, only the first NSW=6 are used following the older
! radiation scheme
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBD(YDGEOMETRY%YRGEM%NGPTOT,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PALBP(YDGEOMETRY%YRGEM%NGPTOT,YDMODEL%YRML_PHY_RAD%YRERAD%NTSW) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCCNL(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCCNO(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRSF(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCCO2 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PCLFR(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PQO3(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PAER(YDGEOMETRY%YRGEM%NGPTOT,6,KLEV) 

!extra diagnostics to debug new aerosols
!for debugging. macc aerosol climatology SW OD in output
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERMACCS(YDGEOMETRY%YRGEM%NGPTOT,14) 
!for debugging. macc aerosol climatology LW OD in output
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERMACCL(YDGEOMETRY%YRGEM%NGPTOT,16) 
!for debugging. macc aerosol climatology SW OD in output, EACH SPECIE/MODE AT 0.55UM
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERMACCSJ(YDGEOMETRY%YRGEM%NGPTOT,12) 
!for debugging. macc aerosol climatology LW OD in output, EACH SPECIE/MODE AT 11UM
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERMACCLJ(YDGEOMETRY%YRGEM%NGPTOT,12) 
!for debugging. Tegen aerosol climatology SW OD in output
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERTGS(YDGEOMETRY%YRGEM%NGPTOT,KLEV,14) 
!for debugging. Tegen aerosol climatology LW OD in output
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAERTGL(YDGEOMETRY%YRGEM%NGPTOT,KLEV,16) 
!extra diagnostics to debug new aerosols

REAL(KIND=JPRB)   ,INTENT(IN)    :: PAERO(YDGEOMETRY%YRGEM%NGPTOT,KLEV,YDMODEL%YRML_GCONF%YGFL%NAERO) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGHG(YDGEOMETRY%YRGEM%NGPTOT,8,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PDP(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSPECTRALEMISS(YDGEOMETRY%YRGEM%NGPTOT,YDMODEL%YRML_PHY_RAD%YRERAD%NLWEMISS) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PMU0(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQ(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQS(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQIWP(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQSWP(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQLWP(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PFSDP(YDGEOMETRY%YRGEM%NGPTOT,KLEV)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQRAIN(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRAINT(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSLM(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PECPO3(YDGEOMETRY%YRGEM%NGPTOT,KLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PTS(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMTD(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMTU(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTRSO(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTRSC(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMTC(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNTOP(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PNBAS(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMIT(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB)   ,INTENT(IN)    :: PPERTT(YDGEOMETRY%YRGEM%NGPTOT, YSPP%N2DRAD)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTH(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFRTED(YDGEOMETRY%YRGEM%NGPTOT, YDMODEL%YRML_PHY_RAD%YRERAD%NLWOUT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTRSOD(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PTRSODC(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PEMTDC(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PSUDU(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PUVDF(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PPARF(YDGEOMETRY%YRGEM%NGPTOT) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PPARCF(YDGEOMETRY%YRGEM%NGPTOT),PTINCF(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PFDIR(YDGEOMETRY%YRGEM%NGPTOT) ,PCDIR(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PLWDERIVATIVE(YDGEOMETRY%YRGEM%NGPTOT,KLEV+1)
!     ------------------------------------------------------------------

REAL(KIND=JPRB) :: ZAMU0  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZQAER  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,6,KLEV)
REAL(KIND=JPRB) :: ZQAERO (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,12)
REAL(KIND=JPRB) :: ZQOZ   (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQCO2  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQCH4  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQN2O  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQNO2  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQC11  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQC12  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQOZO  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQC22  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQCL4  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZLWFC  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZSWFC  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZFRTH  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZFRSO  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZLWDERIVATIVE(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZFRSOD (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
! Surface LW downwelling flux in each emissivity band (or broadband value)
REAL(KIND=JPRB) :: ZFRTED (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA, YDMODEL%YRML_PHY_RAD%YRERAD%NLWOUT)
REAL(KIND=JPRB) :: ZFRSODC(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZFRTEDC(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZPQO3  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZCAPH  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZCHTI  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZCLC   (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQLWP  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQIWP  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQRWP  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZQSWP  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZTRSO  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZEMTD  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZEMTU  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZTRSC  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZEMTC  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV+1)
REAL(KIND=JPRB) :: ZRHCL  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV) !FOR DEBUGGING MACC AER CLIM
REAL(KIND=JPRB) :: ZTRSOD (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZTRSODC(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZEMTDC(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA) 
REAL(KIND=JPRB) :: ZSUDU  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZUVDF  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZPARF  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZPARCF (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA), ZTINCF(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZFDIR  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA), ZCDIR (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZEMIT  (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZDUMMY (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV, YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: ZDUMMY2(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZECPO3(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
!for debugging macc aer clima
REAL(KIND=JPRB) :: ZQSAT(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV),ZDP(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV)
REAL(KIND=JPRB) :: ZTAUAER(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,5)

INTEGER(KIND=JPIM) :: INUMFLD(NPRTRV),IFLAG
INTEGER(KIND=JPIM), ALLOCATABLE :: IVSETSC(:), ISTALAT(:)

REAL(KIND=JPRB), ALLOCATABLE :: ZRGP (:,:,:)
REAL(KIND=JPRD), ALLOCATABLE :: ZDGEMU(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZRGPBUF (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPMOD(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZSPRAD(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZPARITY(:)
REAL(KIND=JPRB), ALLOCATABLE :: ZLSDEPI(:)

REAL(KIND=JPRB) :: ZDSLAT(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,1)
REAL(KIND=JPRB) :: ZCSLA (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,1,2:4)
REAL(KIND=JPRB) :: ZDSLON(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,1,0:3)
REAL(KIND=JPRB) :: ZCSLO (YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,1,3,2)
!FOR DEBUGGING MACC AEROSOL CLIMATOLOGY
REAL(KIND=JPRB) :: ZAERTAUL(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,16)
REAL(KIND=JPRB) :: ZAERASYL(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,16)
REAL(KIND=JPRB) :: ZAEROMGL(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,16)
REAL(KIND=JPRB) :: ZAERTAUS(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,14)
REAL(KIND=JPRB) :: ZAERASYS(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,14)
REAL(KIND=JPRB) :: ZAEROMGS(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,14)
REAL(KIND=JPRB) :: ZAERTAUSJ(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,12)
REAL(KIND=JPRB) :: ZAERTAULJ(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,12)
REAL(KIND=JPRB) :: ZAERMACCM(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,KLEV,12)

REAL(KIND=JPRB) :: ZSFSWDIR(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: ZSFSWDIF(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: ZFSDNN(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)
REAL(KIND=JPRB) :: ZFSDNV(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)

INTEGER(KIND=JPIM) :: IL0S(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,1,0:3)
INTEGER(KIND=JPIM) :: IL0ULI(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,2,2), IL0UDI(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA,0:3,0:3)

INTEGER(KIND=JPIM) :: I,IBEG,IEND,IB,IL,I1,I2,IM,IMD,IRD,ICNT,&
 & J,JAERO,JARP,JIR,JB,JK,JL,JM,JN,JLEV,JRL,JSW,JLW,JSTGLO,JFLD,JGL,&
 & INEXT,IINBEG,IINEND,IOUTBEG,IOUTEND,IFLD,IARP,&
 & IFLDSIN,IFLDSOUT,IFLDSTOT,IGPBLKS,IGPBLKS_RAD,IGI,IGIX,&
 & IWIS,IFLDN,IFLDX,IGLGLO , ISUPP

INTEGER(KIND=JPIM) :: IFIXRADFLD(2)

!-- debug
!INTEGER(KIND=JPIM) :: JAER
!-- debug  

INTEGER(KIND=JPIM) :: IMU0,IAMU0,IEMISS,ITS,ISLM,IBAS,ITOP,&
 & IFRSOD,IFRTED,IFRSODC,IFRTEDC,IEMIT,&
 & ISUDU,IUVDF,IPARF,IPARCF,ITINCF,IFDIR,IFDIF,ICDIR,&
 & ILWDERIVATIVE,ISWDIRECTBAND,ISWDIFFUSEBAND,&
 & IALD,IALP,ITI,IPR,IQS,IWV,ICLC,ILWA,IIWA,ISWA,IRWA,&
 & IRRA,IDP,IOZ,IHTI,IHPR,IFRSO,ISWFC,IFRTH,ILWFC,IAE,IAER,IAERO,IPERT,&
 & IAPRS,IGELAM,IGEMU,ICLON,ISLON,IECPO3,ICCNL,ICCNO,&
 & ICO2,ICH4,IN2O,INO2,IC11,IC12,IC22,ICL4,IFSD 

INTEGER(KIND=JPIM) :: IDUMARR(2)
INTEGER(KIND=JPIM) :: IULTMP
REAL(KIND=JPRB) :: ZCRAE, ZRII0, ZDEPI, ZPIS2, Z4JP,ZDUM,ZREPALB
LOGICAL, parameter :: LLDEBUG = .false.
LOGICAL, parameter :: LLDEBUGRW = .false.
LOGICAL :: LLINC, LLDOLAST, LLERROR, LLREAD, LLPRINT, LLACTAERO
! BEN - introduce flag to activate aerosols in the radiation code
LOGICAL :: LLAERORAD

LOGICAL :: LLSPPRAD     ! local switch for SPP scheme

INTEGER(KIND=JPIM) :: IRADAER, IRADAER2

CHARACTER (LEN = 32) ::  CL_DEBUGFN
REAL(KIND=JPRB) :: ZPERTAER,ZPERTOZ

! Diffuse and direct downwelling surface fluxes in each band of the
! shortwave spectrum (W m-2)
REAL(KIND=JPRB) :: ZSWDIRECTBAND(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA, 1:YDMODEL%YRML_PHY_RAD%YRERAD%NSW)
REAL(KIND=JPRB) :: ZSWDIFFUSEBAND(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA, 1:YDMODEL%YRML_PHY_RAD%YRERAD%NSW)

! Upwelling and downwelling fluxes at surface and top-of-atmosphere
! (TOA; W m-2), plus the "effective" top-of-atmosphere downwelling,
! which is scaled by ZSwTopScalingFactor
REAL(KIND=JPRB) :: ZSWDNTOP, ZSWUPTOP, ZSWDNSURF, ZSWUPSURF, ZSWDNTOPEFF

! Approximate shortwave transmittance and reflectance for the entire
! atmosphere
REAL(KIND=JPRB) :: ZSWTRANSMITTANCE
REAL(KIND=JPRB) :: ZSWREFLECTANCE

! Broadband shortwave surface albedo
REAL(KIND=JPRB) :: ZBROADBANDALBEDO

! New net surface flux (W m-2) after updating for the local albedo,
! normalized by the TOA downwelling flux
REAL(KIND=JPRB) :: ZNEWSWNETSURF

! The change required to the net shortwave at all levels and the
! downwelling at the surface only (W m-2)
REAL(KIND=JPRB) :: ZSWNETCHANGE(YDMODEL%YRML_PHY_RAD%YRERAD%NRPROMA)

! In computing the approximate transmittance and reflectance, the TOA
! downwelling is first multiplied by this value, representing the
! fraction of the solar radiation that is absorbed in the stratosphere
! and upper troposphere and is best to keep out of the computation of
! transmittance and reflectance.  The most recent offline calculations
! suggest no scaling is required (so the value is 1.0), but the
! capability to scale is retained.
REAL(KIND=JPRB), PARAMETER :: ZSWTOPSCALINGDEFAULT = 1.0_JPRB
REAL(KIND=JPRB)            :: ZSWTOPSCALING

REAL(KIND=JPRB) :: ZEPSILON

REAL(KIND=JPRB) :: ZHOOK_HANDLE

INTEGER(KIND=JPIM) :: IAUX

!   -------------------------------------------------------------------

#include "dir_trans.h"
#include "inv_trans.h"
#include "surf_inq.h"

#include "abor1.intfb.h"
#include "laiddi_rad.intfb.h"
#include "laidli_rad.intfb.h"
#include "radact.intfb.h"
#include "radiation_scheme.intfb.h"
#include "radlsw.intfb.h"
#include "radlswr.intfb.h"
#include "radozc.intfb.h"
#include "radozv.intfb.h"
#include "radghg.intfb.h"
#include "rdscaw.intfb.h"
#include "slcomm.intfb.h"
#include "slcomm2.intfb.h"
#include "slextpol.intfb.h"
#include "check_sl_struct.intfb.h"
#include "aer_rrtm.intfb.h"
#include "satur.intfb.h"
!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('RADINTG',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP, &
 &   YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, YDHSLMER=>YDGEOMETRY%YRHSLMER,  YDLAP=>YDGEOMETRY%YRLAP, &
 &  YDCSGLEG=>YDGEOMETRY%YRCSGLEG, YDVAB=>YDGEOMETRY%YRVAB, YDVETA=>YDGEOMETRY%YRVETA, &
 & YDVFE=>YDGEOMETRY%YRVFE, YDSTA=>YDGEOMETRY%YRSTA, YDVSPLIP=>YDGEOMETRY%YRVSPLIP, &
 & YDVSLETA=>YDGEOMETRY%YRVSLETA, YDCSGEOM=>YDGEOMETRY%YRCSGEOM,  &
 & YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB,  YDSPGEOM=>YDGEOMETRY%YSPGEOM, YDPHY=>YDMODEL%YRML_PHY_MF%YRPHY, &
 & YDEPHLI=>YDMODEL%YRML_PHY_SLIN%YREPHLI,  &
 & YDERDI=>YDMODEL%YRML_PHY_RAD%YRERDI,YDERAD=>YDMODEL%YRML_PHY_RAD%YRERAD,YDPHY3=>YDMODEL%YRML_PHY_MF%YRPHY3, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP, &
 & YDCOMPO=>YDMODEL%YRML_CHEM%YRCOMPO, &
 & YGFL=>YDMODEL%YRML_GCONF%YGFL,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY,YDEAERATM=>YDMODEL%YRML_PHY_RAD%YREAERATM, &
 & YDEAERSRC=>YDMODEL%YRML_PHY_AER%YREAERSRC, YDEAERD=>YDMODEL%YRML_PHY_RAD%YREAERD, &
 & RADGRID=>YDMODEL%YRML_PHY_RAD%RADGRID, YDRI=>YDMODEL%YRML_PHY_RAD%YRRI, YDRO=>YDMODEL%YRML_PHY_RAD%YRRO)

ASSOCIATE(NACTAERO=>YGFL%NACTAERO, NAERO=>YGFL%NAERO, YO3=>YGFL%YO3, &
 & NDGLG=>YDDIM%NDGLG, NRESOL=>YDDIM%NRESOL, NSMAX=>YDDIM%NSMAX, &
 & NSPEC2=>YDDIM%NSPEC2, NUMP=>YDDIM%NUMP, &
 & REPSCAER=>YDEAERATM%REPSCAER, &
 & RSS_RH80_MASSFAC=>YDEAERATM%RSS_RH80_MASSFAC, &
 & NTYPAER=>YDEAERATM%NTYPAER, &
 & LAERCHEM=>YGFL%LAERCHEM, &
 & RCAEROS=>YDEAERD%RCAEROS, &
 & LPHYLIN=>YDEPHLI%LPHYLIN, &
 & YSURF=>YDEPHY%YSURF, &
 & LAERNITRATE=>YDCOMPO%LAERNITRATE, &
 & LAERSOA=>YDCOMPO%LAERSOA, &
 & LAERVOL=>YDEAERATM%LAERVOL, &
 & LAPPROXLWUPDATE=>YDERAD%LAPPROXLWUPDATE, &
 & LAPPROXSWUPDATE=>YDERAD%LAPPROXSWUPDATE, LDIAGFORCING=>YDERAD%LDIAGFORCING, &
 & LEPO3RA=>YDERAD%LEPO3RA, LHVOLCA=>YDERAD%LHVOLCA, &
 & LNOTROAER=>YDERAD%LNOTROAER, LSRTM=>YDERAD%LSRTM, NAER=>YDERAD%NAER, &
 & NAERMACC=>YDERAD%NAERMACC, NGHGRAD=>YDERAD%NGHGRAD, NOZOCL=>YDERAD%NOZOCL, &
 & NPERTAER=>YDERAD%NPERTAER, NPERTOZ=>YDERAD%NPERTOZ, NRADINT=>YDERAD%NRADINT, &
 & NRADRES=>YDERAD%NRADRES, NRPROMA=>YDERAD%NRPROMA, NSW=>YDERAD%NSW, &
 & NTSW=>YDERAD%NTSW, NLWEMISS=>YDERAD%NLWEMISS, NLWOUT=>YDERAD%NLWOUT, &
 & RCCL4=>YDERDI%RCCL4, RCFC11=>YDERDI%RCFC11, RCFC12=>YDERDI%RCFC12, &
 & RCFC22=>YDERDI%RCFC22, RCH4=>YDERDI%RCH4, REPCLC=>YDERDI%REPCLC, &
 & REPH2O=>YDERDI%REPH2O, RN2O=>YDERDI%RN2O, RNO2=>YDERDI%RNO2, &
 & RRAE=>YDERDI%RRAE, &
 & NGPTOT=>YDGEM%NGPTOT, R4JP=>YDGEM%R4JP, &
 & MYMS=>YDLAP%MYMS, NASM0=>YDLAP%NASM0, &
 & NGLOBALINDEX=>YDMP%NGLOBALINDEX, &
 & LRAYFM=>YDPHY%LRAYFM, &
 & RII0=>YDPHY3%RII0, & 
 & NSTART=>YDRIP%NSTART, NFSD=>YGFL%NFSD)
!     ------------------------------------------------------------------

!*       1.    PREPARATORY WORK
!              ----------------

WRITE(NULOUT,'(a)') 'Calling radiation scheme (RADINTG)'

ZEPSILON=1000._JPRB*TINY(ZEPSILON)

!initialize to zero quantities only needed for extra
!diagnostics for MACC aerosol climatology
IF (.NOT.LDIAGFORCING) THEN

ZAERTAUL=0._JPRB
ZAERTAULJ=0._JPRB
ZAERASYL=0._JPRB
ZAEROMGL=0._JPRB
ZAERTAUS=0._JPRB
ZAERTAUSJ=0._JPRB
ZAERASYS=0._JPRB
ZAEROMGS=0._JPRB
ZAERMACCM=0._JPRB
ZRHCL=0._JPRB
ZQSAT=0._JPRB
ZDP=0.0_JPRB
ZTAUAER=0.0_JPRB
IFLAG=0

ENDIF

LLSPPRAD=YSPP_CONFIG%LSPP.AND.(YSPP%N2DRAD>0)


CALL SURF_INQ(YSURF,PREPALB=ZREPALB)
NIMP=0
NOUT=0
ZRII0=RII0*RADJUST

IGPBLKS    =(NGPTOT        -1)/NRPROMA+1
IGPBLKS_RAD=(RADGRID%NGPTOT-1)/NRPROMA+1

#if 0
! LLDEBUG & LLDEBUGRW are set to as constants = .false. --> better for compiler optimization & dead code elimination
IF( NSTEP == 0 )THEN
  LLDEBUG=.FALSE.
  LLDEBUGRW=.FALSE.
ELSE
  LLDEBUG=.FALSE.
  LLDEBUGRW=.FALSE.
ENDIF
#endif

!-- NAERO > 0 and <= 16 is GEMS/MACC active configuration
!-- YDERAD%NAERMACC = 1 is MACC-derived aerosol climatology
!-- NACTAERO is the number of aerosol active in the prognostic scheme
!-- that will be active if switched on in radiation
LLACTAERO=.FALSE.
IF (NAERO > 0 .AND. NAERO <= 21 .AND. NAERMACC == 0) THEN
  LLACTAERO=.TRUE.
  ISUPP=0
  IF (LAERNITRATE) THEN
    ISUPP=ISUPP+3
  ENDIF
  IF (LAERSOA) THEN
    ISUPP=ISUPP+3
  ENDIF
  IF (LAERVOL) THEN
    ISUPP=ISUPP+3
  ENDIF
  IRADAER=12+ISUPP
  IRADAER2=12
ELSEIF (NAERMACC == 1) THEN
  IRADAER=12
ELSE
  IRADAER=12 !dummy in this case, not used.
ENDIF
!BEN
LLAERORAD=.FALSE.

!*       1.01    PARAMETERS EXTRACTED FROM COMDECKS (NON-TRANSPORTABLE)
!                ------------------------------------------------------

!*       1.02    PARAMETERS FOR LOCAL DIAGNOSTICS
!                --------------------------------

ZPERTAER = FLOAT(NPERTAER)*1.E-2_JPRB
ZPERTOZ  = FLOAT(NPERTOZ )*1.E-2_JPRB

!*       1.03    LOCAL CONSTANTS
!                ---------------

ZCRAE=RRAE*(RRAE+2.0_JPRB)

! SET UP SOLAR ZENITH ANGLE, CORRECTED FOR EARTH'S CURVATURE

DO JL=1,NGPTOT
  IF (PMU0(JL) > 1.E-10_JPRB) THEN
    ZAMU0(JL)=RRAE/(SQRT(PMU0(JL)**2+ZCRAE)-PMU0(JL))
  ELSE
    ZAMU0(JL)= RRAE/SQRT(ZCRAE)
  ENDIF 
ENDDO

!  INITIALISE INDICES FOR VARIABLE

! INDRAD is a CONTAIN'd function (at end of this routine)

INEXT  =1
IINBEG =1                        ! start of input variables
IGI    =INDRAD(INEXT,1,LLDEBUG)
IMU0   =INDRAD(INEXT,1,.TRUE.)
IAMU0  =INDRAD(INEXT,1,.TRUE.)
IEMISS =INDRAD(INEXT,NLWEMISS,.TRUE.)
ITS    =INDRAD(INEXT,1,.TRUE.)
ISLM   =INDRAD(INEXT,1,.TRUE.)
ICCNL  =INDRAD(INEXT,1,.TRUE.)
ICCNO  =INDRAD(INEXT,1,.TRUE.)
IBAS   =INDRAD(INEXT,1,.TRUE.)
ITOP   =INDRAD(INEXT,1,.TRUE.)
IGELAM =INDRAD(INEXT,1,.TRUE.)
IGEMU  =INDRAD(INEXT,1,.TRUE.)
ICLON  =INDRAD(INEXT,1,.TRUE.)
ISLON  =INDRAD(INEXT,1,.TRUE.)
IALD   =INDRAD(INEXT,NSW,.TRUE.)
IALP   =INDRAD(INEXT,NSW,.TRUE.)
ITI    =INDRAD(INEXT,KLEV,.TRUE.)
IPR    =INDRAD(INEXT,KLEV,.TRUE.)
IQS    =INDRAD(INEXT,KLEV,.TRUE.)
IWV    =INDRAD(INEXT,KLEV,.TRUE.)
ICLC   =INDRAD(INEXT,KLEV,.TRUE.)
ILWA   =INDRAD(INEXT,KLEV,.TRUE.)
IIWA   =INDRAD(INEXT,KLEV,.TRUE.)
ISWA   =INDRAD(INEXT,KLEV,.TRUE.)
IRWA   =INDRAD(INEXT,KLEV,.TRUE.)
IRRA   =INDRAD(INEXT,KLEV,.TRUE.)
IDP    =INDRAD(INEXT,KLEV,.TRUE.)
IFSD   =INDRAD(INEXT,KLEV, NFSD > 0)
IOZ    =INDRAD(INEXT,KLEV,LRAYFM)
IECPO3 =INDRAD(INEXT,KLEV ,.NOT.LRAYFM.AND.LEPO3RA.AND.YO3%LGP)
IHPR   =INDRAD(INEXT,KLEV+1,.TRUE.)
IAPRS  =INDRAD(INEXT,KLEV+1,.TRUE.)
IHTI   =INDRAD(INEXT,KLEV+1,.TRUE.)
IAERO  =INDRAD(INEXT,IRADAER*KLEV,.NOT.LRAYFM .AND. LLACTAERO .AND. NAERMACC == 0)
IPERT  =INDRAD(INEXT,YSPP%N2DRAD, LLSPPRAD )

!ICO2   =INDRAD(INEXT,KLEV,.TRUE.)
!ICH4   =INDRAD(INEXT,KLEV,.TRUE.)
!IN2O   =INDRAD(INEXT,KLEV,.TRUE.)
!INO2   =INDRAD(INEXT,KLEV,.TRUE.)
!IC11   =INDRAD(INEXT,KLEV,.TRUE.)
!IC12   =INDRAD(INEXT,KLEV,.TRUE.)
!IC22   =INDRAD(INEXT,KLEV,.TRUE.)
!ICL4   =INDRAD(INEXT,KLEV,.TRUE.)

IINEND =INEXT-1                  ! end of input variables

IOUTBEG=INEXT                    ! start of output variables
IF (NAERMACC == 1) IAERO = INDRAD(INEXT,IRADAER*KLEV,LDIAGFORCING)
IFRSOD =INDRAD(INEXT,1,.TRUE.)
IFRTED =INDRAD(INEXT,NLWOUT,.TRUE.)
IFRSODC=INDRAD(INEXT,1,.TRUE.)
IFRTEDC=INDRAD(INEXT,1,.TRUE.)
IEMIT  =INDRAD(INEXT,1,.TRUE.)
ISUDU  =INDRAD(INEXT,1,.TRUE.)
IUVDF  =INDRAD(INEXT,1,.TRUE.)
IPARF  =INDRAD(INEXT,1,.TRUE.)
IPARCF =INDRAD(INEXT,1,.TRUE.)
ITINCF =INDRAD(INEXT,1,.TRUE.)
IFDIR  =INDRAD(INEXT,1,.TRUE.)
IFDIF  =INDRAD(INEXT,1,.TRUE.)
ICDIR  =INDRAD(INEXT,1,.TRUE.)
ILWDERIVATIVE =INDRAD(INEXT,KLEV+1, LAPPROXLWUPDATE)
ISWDIRECTBAND =INDRAD(INEXT,NSW,LAPPROXSWUPDATE)
ISWDIFFUSEBAND=INDRAD(INEXT,NSW,LAPPROXSWUPDATE)
IFRSO  =INDRAD(INEXT,KLEV+1,.TRUE.)
ISWFC  =INDRAD(INEXT,KLEV+1,.TRUE.)
IFRTH  =INDRAD(INEXT,KLEV+1,.TRUE.)
ILWFC  =INDRAD(INEXT,KLEV+1,.TRUE.)
IAER   =INDRAD(INEXT,6*KLEV,LDIAGFORCING)
IOZ    =INDRAD(INEXT,KLEV,LDIAGFORCING)
ICO2   =INDRAD(INEXT,KLEV,LDIAGFORCING)
ICH4   =INDRAD(INEXT,KLEV,LDIAGFORCING)
IN2O   =INDRAD(INEXT,KLEV,LDIAGFORCING)
INO2   =INDRAD(INEXT,KLEV,LDIAGFORCING)
IC11   =INDRAD(INEXT,KLEV,LDIAGFORCING)
IC12   =INDRAD(INEXT,KLEV,LDIAGFORCING)
IC22   =INDRAD(INEXT,KLEV,LDIAGFORCING)
ICL4   =INDRAD(INEXT,KLEV,LDIAGFORCING)
IGIX   =INDRAD(INEXT,1,LLDEBUG)

IOUTEND=INEXT-1                  ! end of output variables

                                 ! start of local variables
IF(.NOT.LDIAGFORCING) THEN
  IF (NAERO == 0 .OR. NAERMACC == 1)  IAERO = INDRAD(INEXT,IRADAER*KLEV,.TRUE.)
  IAER  =INDRAD(INEXT,KLEV*6,.TRUE.)
  IOZ   =INDRAD(INEXT,KLEV,.NOT.LRAYFM)
  ICO2   =INDRAD(INEXT,KLEV,.TRUE.)
  ICH4   =INDRAD(INEXT,KLEV,.TRUE.)
  IN2O   =INDRAD(INEXT,KLEV,.TRUE.)
  INO2   =INDRAD(INEXT,KLEV,.TRUE.)
  IC11   =INDRAD(INEXT,KLEV,.TRUE.)
  IC12   =INDRAD(INEXT,KLEV,.TRUE.)
  IC22   =INDRAD(INEXT,KLEV,.TRUE.)
  ICL4   =INDRAD(INEXT,KLEV,.TRUE.)
ENDIF
                                 ! end of local variables

IF( LLDEBUG )THEN
  WRITE(NULOUT,'("RADINTG: IMU0   =",I0)')IMU0
  WRITE(NULOUT,'("RADINTG: IAMU0  =",I0)')IAMU0
  WRITE(NULOUT,'("RADINTG: IEMISS =",I0)')IEMISS
  WRITE(NULOUT,'("RADINTG: ITS    =",I0)')ITS
  WRITE(NULOUT,'("RADINTG: ISLM   =",I0)')ISLM
  WRITE(NULOUT,'("RADINTG: ICCNL  =",I0)')ICCNL
  WRITE(NULOUT,'("RADINTG: ICCNO  =",I0)')ICCNO
  WRITE(NULOUT,'("RADINTG: IBAS   =",I0)')IBAS
  WRITE(NULOUT,'("RADINTG: ITOP   =",I0)')ITOP
  WRITE(NULOUT,'("RADINTG: IGELAM =",I0)')IGELAM
  WRITE(NULOUT,'("RADINTG: IGEMU  =",I0)')IGEMU
  WRITE(NULOUT,'("RADINTG: ICLON  =",I0)')ICLON
  WRITE(NULOUT,'("RADINTG: ISLON  =",I0)')ISLON
  WRITE(NULOUT,'("RADINTG: IALD   =",I0)')IALD
  WRITE(NULOUT,'("RADINTG: IALP   =",I0)')IALP
  WRITE(NULOUT,'("RADINTG: ITI    =",I0)')ITI
  WRITE(NULOUT,'("RADINTG: IPR    =",I0)')IPR
  WRITE(NULOUT,'("RADINTG: IQS    =",I0)')IQS
  WRITE(NULOUT,'("RADINTG: IWV    =",I0)')IWV
  WRITE(NULOUT,'("RADINTG: ICLC   =",I0)')ICLC
  WRITE(NULOUT,'("RADINTG: ILWA   =",I0)')ILWA
  WRITE(NULOUT,'("RADINTG: IIWA   =",I0)')IIWA
  WRITE(NULOUT,'("RADINTG: ISWA   =",I0)')ISWA
  WRITE(NULOUT,'("RADINTG: IRWA   =",I0)')IRWA
  WRITE(NULOUT,'("RADINTG: IRRA   =",I0)')IRRA
  WRITE(NULOUT,'("RADINTG: IDP    =",I0)')IDP
  WRITE(NULOUT,'("RADINTG: IFSD   =",I0)')IFSD
  WRITE(NULOUT,'("RADINTG: IOZ    =",I0)')IOZ
  WRITE(NULOUT,'("RADINTG: IECPO3 =",I0)')IECPO3
  WRITE(NULOUT,'("RADINTG: IHPR   =",I0)')IHPR
  WRITE(NULOUT,'("RADINTG: IAPRS  =",I0)')IAPRS
  WRITE(NULOUT,'("RADINTG: IHTI   =",I0)')IHTI
  WRITE(NULOUT,'("RADINTG: IFRSOD =",I0)')IFRSOD
  WRITE(NULOUT,'("RADINTG: IFRTED=",I0)')IFRTED 
  WRITE(NULOUT,'("RADINTG: IFRSODC=",I0)')IFRSODC 
  WRITE(NULOUT,'("RADINTG: IFRTEDC=",I0)')IFRTEDC 
  WRITE(NULOUT,'("RADINTG: IEMIT  =",I0)')IEMIT
  WRITE(NULOUT,'("RADINTG: ISUDU  =",I0)')ISUDU
  WRITE(NULOUT,'("RADINTG: IUVDF  =",I0)')IUVDF
  WRITE(NULOUT,'("RADINTG: IPARF  =",I0)')IPARF
  WRITE(NULOUT,'("RADINTG: IPARCF =",I0)')IPARCF
  WRITE(NULOUT,'("RADINTG: ITINCF =",I0)')ITINCF
  WRITE(NULOUT,'("RADINTG: IFDIR  =",I0)')IFDIR
  WRITE(NULOUT,'("RADINTG: IFDIF  =",I0)')IFDIF
  WRITE(NULOUT,'("RADINTG: ICDIR  =",I0)')ICDIR
  WRITE(NULOUT,'("RADINTG: ILwDerivative =",I0)')ILWDERIVATIVE
  WRITE(NULOUT,'("RADINTG: ISwDirectBand =",I0)')ISWDIRECTBAND
  WRITE(NULOUT,'("RADINTG: ISwDiffuseBand =",I0)')ISWDIFFUSEBAND
  WRITE(NULOUT,'("RADINTG: IFRSO  =",I0)')IFRSO
  WRITE(NULOUT,'("RADINTG: ISWFC  =",I0)')ISWFC
  WRITE(NULOUT,'("RADINTG: IFRTH  =",I0)')IFRTH
  WRITE(NULOUT,'("RADINTG: ILWFC  =",I0)')ILWFC
  WRITE(NULOUT,'("RADINTG: IGI    =",I0)')IGI
  WRITE(NULOUT,'("RADINTG: IAER   =",I0)')IAER
  WRITE(NULOUT,'("RADINTG: IAERO  =",I0)')IAERO
  WRITE(NULOUT,'("RADINTG: IPERT  =",I0)')IPERT
  WRITE(NULOUT,'("RADINTG: ICO2   =",I0)')ICO2
  WRITE(NULOUT,'("RADINTG: ICH4   =",I0)')ICH4
  WRITE(NULOUT,'("RADINTG: IN2O   =",I0)')IN2O
  WRITE(NULOUT,'("RADINTG: INO2   =",I0)')INO2
  WRITE(NULOUT,'("RADINTG: IC11   =",I0)')IC11
  WRITE(NULOUT,'("RADINTG: IC12   =",I0)')IC12
  WRITE(NULOUT,'("RADINTG: IC22   =",I0)')IC22
  WRITE(NULOUT,'("RADINTG: ICL4   =",I0)')ICL4

ENDIF

IFLDSIN = IINEND - IINBEG +1
IFLDSOUT= IOUTEND-IOUTBEG +1
IFLDSTOT= INEXT  - 1

!IF( .TRUE. )THEN
  WRITE(NULOUT,'("RADINTG: IFLDSIN   =",I12)')IFLDSIN
  WRITE(NULOUT,'("RADINTG: IFLDSOUT  =",I12)')IFLDSOUT
  WRITE(NULOUT,'("RADINTG: IFLDSTOT  =",I12)')IFLDSTOT
!ENDIF

IF( NRADRES /= NSMAX )THEN
  ALLOCATE(ZRGP(NRPROMA,IFLDSIN,IGPBLKS))
ELSE
  ALLOCATE(ZRGP(NRPROMA,IFLDSTOT,IGPBLKS))
ENDIF
ZRGP=0.0_JPRB
ALLOCATE(ZDGEMU(NRPROMA,IGPBLKS))
ZDGEMU=0.0_JPRB

!  INPUT LOOP

CALL GSTATS(1205,0)
!$OMP  PARALLEL DO SCHEDULE(STATIC)&
!$OMP& PRIVATE(JSTGLO,JK,JSW,JLW,JL,IAE,IBEG,IEND,IL,IB,IARP,JARP,&
!$OMP&         ZCLC,ZQLWP,ZQIWP,ZQRWP,ZQSWP,ZPQO3,ZCAPH,ZCHTI)
DO JSTGLO=1,NGPTOT,NRPROMA
  IBEG=JSTGLO
  IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
  IL=IEND-IBEG+1
  IB=(JSTGLO-1)/NRPROMA+1

!*         2.     FULL GRID COMPUTATIONS


!*         2.1    FULL-LEVEL QUANTITIES

  IF (YDERAD%LINTERPINCLOUDMEAN) THEN
    ! Original interpolation scheme interpolates in-cloud mean water
    ! mixing ratios, i.e. gridbox means divided by cloud fraction. The
    ! trouble with this is that it does not conserve gridbox mean
    ! cloud water.
    DO JK=1,KLEV
      ZCLC(1:IL,JK)=MIN(MAX(PCLFR(IBEG:IEND,JK), 0.0_JPRB ), 1.0_JPRB )
      DO JL=1,IL
        IF (ZCLC(JL,JK) > REPCLC) THEN
          ZQLWP(JL,JK)=PQLWP(IBEG+JL-1,JK)/ZCLC(JL,JK)
          ZQIWP(JL,JK)=PQIWP(IBEG+JL-1,JK)/ZCLC(JL,JK)
          ZQRWP(JL,JK)=PQRAIN(IBEG+JL-1,JK)/ZCLC(JL,JK)
          ZQSWP(JL,JK)=PQSWP(IBEG+JL-1,JK)/ZCLC(JL,JK)
        ELSE
          ZQLWP(JL,JK)=0.0_JPRB
          ZQIWP(JL,JK)=0.0_JPRB
          ZQRWP(JL,JK)=0.0_JPRB
          ZQSWP(JL,JK)=0.0_JPRB
        ENDIF
      ENDDO
    ENDDO
  ENDIF

  IF (LRAYFM) THEN
    DO JK=1,KLEV
! PQO3 (kg/kg) -> ZPQO3 (ppm Pa/Pa)
      ZPQO3(1:IL,JK)=PQO3(IBEG:IEND,JK)* PDP(IBEG:IEND,JK)* RMD/RMO3
    ENDDO
  ENDIF

!*         2.2    HALF-LEVEL QUANTITIES

  DO JK=2,KLEV
    ZCAPH(1:IL,JK)=PAPRS(IBEG:IEND,JK)
    PTH(IBEG:IEND,JK)=(PT(IBEG:IEND,JK-1)*PAPRSF(IBEG:IEND,JK-1)&
     & *(PAPRSF(IBEG:IEND,JK)-PAPRS(IBEG:IEND,JK))&
     & +PT(IBEG:IEND,JK)*PAPRSF(IBEG:IEND,JK)*(PAPRS(IBEG:IEND,JK)&
     & -PAPRSF(IBEG:IEND,JK-1)))&
     & *(1.0_JPRB/(PAPRS(IBEG:IEND,JK)*(PAPRSF(IBEG:IEND,JK)-PAPRSF(IBEG:IEND,JK-1))))  
    ZCHTI(1:IL,JK)=PTH(IBEG:IEND,JK)
  ENDDO

!*         2.3     QUANTITIES AT BOUNDARIES

  ZCAPH(1:IL,1)=PAPRS(IBEG:IEND,1)
  ZCAPH(1:IL,KLEV+1)=PAPRS(IBEG:IEND,KLEV+1)
  PTH(IBEG:IEND,1)=PT(IBEG:IEND,1)-PAPRSF(IBEG:IEND,1)*(PT(IBEG:IEND,1)&
   & -PTH(IBEG:IEND,2))/(PAPRSF(IBEG:IEND,1)-PAPRS(IBEG:IEND,2))  
  PTH(IBEG:IEND,KLEV+1)=PTS(IBEG:IEND)
  ZCHTI(1:IL,KLEV+1)=PTH(IBEG:IEND,KLEV+1)
  ZCHTI(1:IL,1)=PTH(IBEG:IEND,1)

!     ------------------------------------------------------------------

!*         3.      PREPARE INPUT ARRAYS

  IF( LLDEBUG )THEN
    ZRGP(1:IL,IGI               ,IB) =NGLOBALINDEX(IBEG:IEND)
  ENDIF
  ZRGP(1:IL,IMU0                ,IB) =PMU0   (IBEG:IEND)
  ZRGP(1:IL,IAMU0               ,IB) =ZAMU0  (IBEG:IEND)
  DO JLW = 1,NLWEMISS
    ZRGP(1:IL,IEMISS+JLW-1      ,IB) =PSPECTRALEMISS(IBEG:IEND,JLW)
  ENDDO
  ! In order for the upwelling longwave from the radiation scheme to
  ! be consistent, spatial averaging of skin temperature should be in
  ! the 4th power, and then we take the 4th root of the result to pass
  ! to radlswr. We neglect the fact that complete consistency would
  ! require consideration of how surface emissivity is averaged
  IF (LAPPROXLWUPDATE) THEN
    ZRGP(1:IL,ITS               ,IB) =PTS    (IBEG:IEND) ** 4
  ELSE
    ZRGP(1:IL,ITS               ,IB) =PTS    (IBEG:IEND)
  ENDIF
  ZRGP(1:IL,ISLM                ,IB) =PSLM   (IBEG:IEND)
  ZRGP(1:IL,ICCNL               ,IB) =PCCNL  (IBEG:IEND)
  ZRGP(1:IL,ICCNO               ,IB) =PCCNO  (IBEG:IEND)
  ZRGP(1:IL,IBAS                ,IB) =PNBAS  (IBEG:IEND)
  ZRGP(1:IL,ITOP                ,IB) =PNTOP  (IBEG:IEND)
  ZRGP(1:IL,IGELAM              ,IB) =YDGSGEOM_NB%GELAM(IBEG:IEND)
  ZRGP(1:IL,IGEMU               ,IB) =YDGSGEOM_NB%GEMU (IBEG:IEND)
  ZDGEMU(1:IL,IB)                    =YDGSGEOM_NB%GEMU (IBEG:IEND)
  ZRGP(1:IL,ICLON               ,IB) =YDGSGEOM_NB%GECLO(IBEG:IEND)
  ZRGP(1:IL,ISLON               ,IB) =YDGSGEOM_NB%GESLO(IBEG:IEND)
  ZRGP(1:IL,IALD:IALD+NSW-1     ,IB) =PALBD  (IBEG:IEND,1:NSW)
  ZRGP(1:IL,IALP:IALP+NSW-1     ,IB) =PALBP  (IBEG:IEND,1:NSW)
  ZRGP(1:IL,ITI:ITI  +KLEV-1    ,IB) =PT     (IBEG:IEND,1:KLEV)
  ZRGP(1:IL,IPR:IPR  +KLEV-1    ,IB) =PAPRSF (IBEG:IEND,1:KLEV)
  ZRGP(1:IL,IQS:IQS  +KLEV-1    ,IB) =PQS    (IBEG:IEND,1:KLEV)
  ZRGP(1:IL,IWV:IWV  +KLEV-1    ,IB) =PQ     (IBEG:IEND,1:KLEV)
  IF (YDERAD%LINTERPINCLOUDMEAN) THEN
    ZRGP(1:IL,ICLC:ICLC+KLEV-1    ,IB) =ZCLC   (1:IL,1:KLEV)
    ZRGP(1:IL,ILWA:ILWA+KLEV-1    ,IB) =ZQLWP  (1:IL,1:KLEV)
    ZRGP(1:IL,IIWA:IIWA+KLEV-1    ,IB) =ZQIWP  (1:IL,1:KLEV)
    ZRGP(1:IL,ISWA:ISWA+KLEV-1    ,IB) =ZQSWP  (1:IL,1:KLEV)
    ZRGP(1:IL,IRWA:IRWA+KLEV-1    ,IB) =ZQRWP  (1:IL,1:KLEV)
    !  ZRGP(1:IL,IRWA:IRWA+KLEV-1    ,IB) =PQRAIN (IBEG:IEND,1:KLEV)
  ELSE
    ! Now interpolate gridbox-mean rather than in-cloud mean quantities
    ZRGP(1:IL,ICLC:ICLC+KLEV-1    ,IB) &
         &  = MIN(MAX(PCLFR(IBEG:IEND,1:KLEV), 0.0_JPRB), 1.0_JPRB)
    ZRGP(1:IL,ILWA:ILWA+KLEV-1    ,IB) =PQLWP (IBEG:IEND,1:KLEV)
    ZRGP(1:IL,IIWA:IIWA+KLEV-1    ,IB) =PQIWP (IBEG:IEND,1:KLEV)
    ZRGP(1:IL,ISWA:ISWA+KLEV-1    ,IB) =PQRAIN(IBEG:IEND,1:KLEV)
    ZRGP(1:IL,IRWA:IRWA+KLEV-1    ,IB) =PQSWP (IBEG:IEND,1:KLEV)
  ENDIF
  ZRGP(1:IL,IRRA:IRRA+KLEV-1    ,IB) =PRAINT (IBEG:IEND,1:KLEV)
  ZRGP(1:IL,IDP:IDP  +KLEV-1    ,IB) =PDP    (IBEG:IEND,1:KLEV)

  IF (NFSD > 0) THEN
     ZRGP(1:IL,IFSD:IFSD+KLEV-1    ,IB) =PFSDP  (IBEG:IEND,1:KLEV)
  ENDIF

  IF( LRAYFM )THEN
    ZRGP(1:IL,IOZ:IOZ+KLEV-1    ,IB) =ZPQO3  (1:IL,1:KLEV)
  ENDIF
  IF( .NOT.LRAYFM.AND.LEPO3RA.AND.YO3%LGP )THEN
    ZRGP(1:IL,IECPO3:IECPO3+KLEV-1,IB) =PECPO3 (IBEG:IEND,1:KLEV)
  ENDIF

  LLPRINT=.FALSE.
!-- configuration with prognostic aerosols in radiation
  IF( .NOT.LRAYFM .AND. NACTAERO > 0 .AND. NAERMACC == 0) THEN

!-- debug
!    IF (NSTEP == YDRIP%NSTART) THEN
!      JL=(INT(IBEG+IEND)/2)
!      DO JK=1,KLEV
!        WRITE(NULOUT,FMT='(" radintg ",2I4,18E10.4)') JL,JK,(PAERO(JL,JK,JAERO),JAERO=1,18)
!      ENDDO
!    ENDIF
!-- debug

    DO JAERO=1,IRADAER2
      IAE=(JAERO-1)*KLEV
      ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) =MAX(REPSCAER, PAERO(IBEG:IEND,1:KLEV,JAERO))
      IF (JAERO <= NTYPAER(1)) THEN
        ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) = ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) * RSS_RH80_MASSFAC
      ENDIF
    ENDDO
    IF (.NOT. LAERCHEM .AND. (LAERSOA .OR. LAERNITRATE .OR. LAERVOL)) THEN
      DO JAERO=IRADAER2+1,IRADAER2+ISUPP
        IAE=(JAERO-1)*KLEV
        ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) =MAX(REPSCAER, PAERO(IBEG:IEND,1:KLEV,JAERO))
      ENDDO
    ENDIF
  ! SR 05 2018 if LAERCHEM, shift one variable
    IF (LAERCHEM .AND. (LAERSOA .OR. LAERNITRATE .OR. LAERVOL)) THEN
      DO JAERO=IRADAER2+1,IRADAER2+ISUPP
        IAE=(JAERO-1)*KLEV
        ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) =MAX(REPSCAER, PAERO(IBEG:IEND,1:KLEV,JAERO-1))
      ENDDO
    ENDIF
  ENDIF
  !SPP: compute and store SPP random patterns on radiation grid
  IF (LLSPPRAD) THEN
    DO JARP=1,YSPP%N2DRAD
      IARP=JARP-1
      ZRGP(1:IL,IPERT+IARP,IB) =PPERTT(IBEG:IEND,JARP)
    ENDDO
  ENDIF

  ZRGP(1:IL,IHPR:IHPR+KLEV      ,IB) =ZCAPH  (1:IL,1:KLEV+1)
  ZRGP(1:IL,IAPRS:IAPRS+KLEV    ,IB) =PAPRS  (IBEG:IEND,1:KLEV+1)
  ZRGP(1:IL,IHTI:IHTI+KLEV      ,IB) =ZCHTI  (1:IL,1:KLEV+1)

ENDDO
!$OMP END PARALLEL DO
CALL GSTATS(1205,1)

!  INPUT INTERPOLATION IF REQUIRED

IF( NRADRES /= NSMAX )THEN

  IF( NRADINT == 1 )THEN

!  INTERPOLATE FROM MODEL GRID TO RADIATION GRID USING A SPECTRAL TRANSFORM

    INUMFLD(:)=IFLDSIN/NPRTRV
    DO J=1,MOD(IFLDSIN,NPRTRV)
      INUMFLD(J)=INUMFLD(J)+1
    ENDDO

    ALLOCATE(IVSETSC(IFLDSIN))
    IFLD=0
    DO JB=1,NPRTRV
      DO JFLD=1,INUMFLD(JB)
        IFLD=IFLD+1
        IVSETSC(IFLD)=JB
      ENDDO
    ENDDO

    ALLOCATE(ZSPMOD(INUMFLD(MYSETV),NSPEC2))
    CALL DIR_TRANS(PSPSCALAR=ZSPMOD(:,:),KPROMA=NRPROMA,KVSETSC=IVSETSC(:),&
     & PGP=ZRGP,KRESOL=NRESOL)  
    DEALLOCATE(ZRGP)
    ALLOCATE(ZSPRAD(INUMFLD(MYSETV),RADGRID%NSPEC2))
    ZSPRAD(:,:)=0.0_JPRB
    DO JM=1,NUMP
      IM=MYMS(JM)
      IF( IM <= RADGRID%NSMAX )THEN
        IF( IM /= RADGRID%MYMS(JM) )THEN
          CALL ABOR1('SUECRAD: MYMS AND RADGRID%MYMS DIFFERENT')
        ENDIF
        DO JIR=0,1
          DO JN=IM,RADGRID%NSMAX
            IMD=NASM0(IM)+2*(JN-IM)+JIR
            IRD=RADGRID%NASM0(IM)+2*(JN-IM)+JIR
            ZSPRAD(:,IRD)=ZSPMOD(:,IMD)
          ENDDO
        ENDDO
      ENDIF
    ENDDO
    DEALLOCATE(ZSPMOD)
    ALLOCATE(ZRGP(NRPROMA,IFLDSTOT,IGPBLKS_RAD))
    ZRGP=0.0_JPRB
    CALL INV_TRANS(PSPSCALAR=ZSPRAD(:,:),KPROMA=NRPROMA,KVSETSC=IVSETSC(:),&
     & PGP=ZRGP,KRESOL=RADGRID%NRESOL_ID)  
    DEALLOCATE(ZSPRAD)
    DEALLOCATE(IVSETSC)
  
  ELSEIF( NRADINT == 2 .OR. NRADINT == 3 )THEN

    ALLOCATE(ZRGPBUF(YDRI%NASLB1,IFLDSIN))
    ZRGPBUF=0.0_JPRB
    CALL GSTATS(1206,0)
!$OMP  PARALLEL DO SCHEDULE(STATIC) PRIVATE(JSTGLO,IBEG,IEND,IL,IB,JL,JFLD)
    DO JSTGLO=1,NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      DO JFLD=1,IFLDSIN
        DO JL=1,IL
          ZRGPBUF(YDRI%NSLCORE(JL+JSTGLO-1),JFLD)=ZRGP(JL,JFLD,IB)
        ENDDO
      ENDDO
    ENDDO
!$OMP END PARALLEL DO
    CALL GSTATS(1206,1)
    DEALLOCATE(ZRGP)
    ALLOCATE(ZRGP(NRPROMA,IFLDSTOT,IGPBLKS_RAD))
    ZRGP=0.0_JPRB
    
    IF( YDRI%LSLONDEM .AND. .NOT.YDRI%LSLONDEM_ACTIVE )THEN
      YDRI%MASK_SL2(:)=0
    ENDIF
    IF (NPROC > 1)THEN
      LLINC=.FALSE.
      CALL GSTATS(66,0)
      IF( YDRI%LSLONDEM.AND.YDRI%LSLONDEM_ACTIVE )THEN
        IFIXRADFLD(1)=1
        IFIXRADFLD(2)=0
        CALL SLCOMM2(YDGEOMETRY%YRDIM,YDRI,IFIXRADFLD,YDRI%MASK_SL1,YDRI%MASK_SL2,&
         & IFLDSIN,LLINC,ZRGPBUF)
        IF( LLDEBUG )THEN
          WRITE(NULERR,'("RADINTG:YDRI%NASLB1,SUM(YDRI%MASK_SL2)=",2(2X,I12))')&
           & YDRI%NASLB1,SUM(YDRI%MASK_SL2)  
        ENDIF
      ELSE
        CALL SLCOMM(YDRI,IDUMARR,IFLDSIN,LLINC,0,ZRGPBUF)
      ENDIF
      CALL GSTATS(66,1)
    ENDIF
    ALLOCATE(ZPARITY(1:IFLDSIN))
    ZPARITY(1:IFLDSIN)=1
    IF( YDRI%LSLONDEM.AND..NOT.YDRI%LSLONDEM_ACTIVE )THEN
      IFIXRADFLD(1)=1
      IFIXRADFLD(2)=IFLDSIN
      CALL SLEXTPOL(YDGEOMETRY%YRDIM,YDRI,IFLDSIN,IFIXRADFLD,3,ZPARITY,ZRGPBUF,KMASK2=YDRI%MASK_SL2)  
    ELSE
      CALL SLEXTPOL(YDGEOMETRY%YRDIM,YDRI,IFLDSIN,IDUMARR,1,ZPARITY,ZRGPBUF)  
    ENDIF
    IF( LSLDEBUG) CALL CHECK_SL_STRUCT(YDRI,'SLEXTPOL')
    DEALLOCATE(ZPARITY)

    IF    ( NRADINT == 2 )THEN
      IWIS=201
    ELSEIF( NRADINT == 3 )THEN
      IWIS=203
    ENDIF
    IFLDN=1
    IFLDX=IFLDSIN
    ZDEPI=2.0_JPRB*RPI
    ZPIS2=0.5_JPRB*RPI
    ALLOCATE(ISTALAT(YDRI%NDGSAH:YDRI%NDGENH))
    ALLOCATE(ZLSDEPI(YDRI%NDGSAH:YDRI%NDGENH))
!DEC$ IVDEP
    DO JGL=MAX(YDRI%NDGSAG,YDRI%NDGSAL+YDRI%NFRSTLOFF-YDRI%NSLWIDEN)-YDRI%NFRSTLOFF,&
       & MIN(YDRI%NDGENG,YDRI%NDGENL+YDRI%NFRSTLOFF+YDRI%NSLWIDES)-YDRI%NFRSTLOFF  
      IGLGLO=JGL+YDRI%NFRSTLOFF
      ZLSDEPI(JGL)=REAL(YDRI%NLOENG(IGLGLO),JPRB)/ZDEPI
      ISTALAT(JGL)=YDRI%NSLOFF(JGL)
    ENDDO

    CALL GSTATS(1207,0)
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP&PRIVATE(JSTGLO,IBEG,IEND,IL,IB,J,ZDSLAT)&
!$OMP&PRIVATE(ZCSLA,ZDSLON,ZCSLO,IL0S,IL0ULI,IL0UDI,JLEV)
    DO JSTGLO=1,RADGRID%NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      CALL RDSCAW(YDRI,NRPROMA,1,IL,&
       & ISTALAT(YDRI%NDGSAH:YDRI%NDGENH),IWIS,YDRI%NFRSTLOFF,&
       & R4JP,ZPIS2,ZLSDEPI(YDRI%NDGSAH:YDRI%NDGENH),YDCSGLEG%RLATI(YDRI%NDGSAH:YDRI%NDGENH),&
       & YDHSLMER%RIPI(YDRI%NDGSAH:YDRI%NDGENH,1:3),&
       & RADGRID%GELAM(IBEG:IEND),RADGRID%GELAT(IBEG:IEND),&
       & ZDSLAT,ZCSLA,ZDSLON,ZCSLO,IL0S)

      IF    ( NRADINT == 2 )THEN
        DO J=1,IL
          IL0ULI(J,1,1) = IL0S(J,1,1)+1
          IL0ULI(J,1,2) = IL0S(J,1,1)+2
          IL0ULI(J,2,1) = IL0S(J,1,2)+1
          IL0ULI(J,2,2) = IL0S(J,1,2)+2
          CALL LAIDLI_RAD(YDRI%NASLB1,IFLDSIN,&
           & ZDSLAT(J,1),ZDSLON(J,1,1:2),IL0ULI(J,:,:),&
           & ZRGPBUF,ZRGP(J,1:IFLDSIN,IB))  
        ENDDO
      ELSEIF( NRADINT == 3 )THEN
        ! 12-point interpolation: note that this can lead to
        ! interpolated values out of bounds of the original data,
        ! which may lead to unphysical values (e.g. negative
        ! temperatures)
        DO J=1,IL
          IL0UDI(J,0,0) = 0
          IL0UDI(J,0,1) = IL0S(J,1,0)+1
          IL0UDI(J,0,2) = IL0S(J,1,0)+2
          IL0UDI(J,0,3) = 0
          IL0UDI(J,1,0) = IL0S(J,1,1)+0
          IL0UDI(J,1,1) = IL0S(J,1,1)+1
          IL0UDI(J,1,2) = IL0S(J,1,1)+2
          IL0UDI(J,1,3) = IL0S(J,1,1)+3
          IL0UDI(J,2,0) = IL0S(J,1,2)+0
          IL0UDI(J,2,1) = IL0S(J,1,2)+1
          IL0UDI(J,2,2) = IL0S(J,1,2)+2
          IL0UDI(J,2,3) = IL0S(J,1,2)+3
          IL0UDI(J,3,0) = 0
          IL0UDI(J,3,1) = IL0S(J,1,3)+1
          IL0UDI(J,3,2) = IL0S(J,1,3)+2
          IL0UDI(J,3,3) = 0
          CALL LAIDDI_RAD(YDRI%NASLB1,IFLDSIN,&
           & ZCSLA(J,1,:),ZDSLON(J,1,:),ZCSLO(J,1,:,:),IL0UDI(J,:,:),&
           & ZRGPBUF,ZRGP(J,1:IFLDSIN,IB))  
        ENDDO
      ENDIF

    ENDDO
!$OMP END PARALLEL DO
    CALL GSTATS(1207,1)

    DEALLOCATE(ISTALAT)
    DEALLOCATE(ZLSDEPI)
    DEALLOCATE(ZRGPBUF)

  ELSE
    CALL ABOR1('RADINTG:INPUT - INVALID INTERPOLATION METHOD')
  ENDIF
  
  CALL GSTATS(1208,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JSTGLO,IBEG,IEND,IL,IB,JL)
  DO JSTGLO=1,RADGRID%NGPTOT,NRPROMA
    IBEG=JSTGLO
    IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
    IL=IEND-IBEG+1
    IB=(JSTGLO-1)/NRPROMA+1
    ZRGP(1:IL,IGELAM,IB)=RADGRID%GELAM(IBEG:IEND)
    ZRGP(1:IL,ICLON ,IB)=RADGRID%GECLO(IBEG:IEND)
    ZRGP(1:IL,ISLON ,IB)=RADGRID%GESLO(IBEG:IEND)
    ZRGP(1:IL,IGEMU ,IB)=RADGRID%GEMU (IBEG:IEND)
    ZDGEMU(1:IL,IB)=RADGRID%GEMU (IBEG:IEND)
    DO JL=1,IL
      IF (ZRGP(JL,IMU0,IB) > 1.E-10_JPRB) THEN
        ZRGP(JL,IAMU0,IB)=RRAE/(SQRT(ZRGP(JL,IMU0,IB)**2+ZCRAE)-&
         & ZRGP(JL,IMU0,IB))  
      ELSE
        ZRGP(JL,IAMU0,IB)=RRAE/SQRT(ZCRAE)
      ENDIF 
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
  CALL GSTATS(1208,1)

  IF( LLDEBUGRW )THEN
    WRITE(CL_DEBUGFN,'("DEBUG",I3.3)')MYPROC
    IULTMP = RESERVE_LUN()
    OPEN(IULTMP,FILE=CL_DEBUGFN,FORM='unformatted')
    LLERROR=.FALSE.
    LLREAD=.TRUE.
    ICNT=0
    DO JSTGLO=1,RADGRID%NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      DO JL=1,IL
        ICNT=ICNT+1
        IF( LLREAD )THEN
          READ(IULTMP) ZDUM
          IF( ZDUM /= ZRGP(JL,IGI,IB) )THEN
            LLERROR=.TRUE.
            WRITE(NULERR,'("RADINTG: ICNT,ZDUM,ZRGP=",I12,2(2X,F20.5))')&
             & ICNT,ZDUM,ZRGP(JL,IGI,IB)  
            WRITE(NULOUT,'("RADINTG: ICNT,ZDUM,ZRGP=",I12,2(2X,F20.5))')&
             & ICNT,ZDUM,ZRGP(JL,IGI,IB)  
          ENDIF
        ELSE
          WRITE(IULTMP)ZRGP(JL,IGI,IB)
        ENDIF
      ENDDO
    ENDDO
    IF( LLREAD )THEN
      IF( LLERROR )THEN
        CALL ABOR1('RADINTG: INPUT INTERP PROBLEM')
      ENDIF
    ENDIF
  ENDIF

ENDIF

!  COMPUTATION OF AEROSOLS, OZONE ON RADIATION GRID
!  SECURITY CHECKS

CALL GSTATS(1209,0)
!$OMP  PARALLEL DO SCHEDULE(DYNAMIC,1)&
!$OMP& PRIVATE(JSTGLO,J,JLEV,JSW,JL,IAE,IBEG,IEND,I,I1,I2,IL,IB,LLDOLAST,&
!$OMP&         ZQAERO,ZQAER,ZQOZ,ZQCO2,ZQCH4,ZQN2O,ZQNO2,ZQC11,ZQC12,ZQOZO,ZQC22,ZQCL4,ZECPO3)
DO JSTGLO=1,RADGRID%NGPTOT,NRPROMA
  IBEG=JSTGLO
  IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
  IL=IEND-IBEG+1
  IB=(JSTGLO-1)/NRPROMA+1

!  SECURITY COMPUTATIONS FOR CRITICAL VARIABLES.

  DO JSW=1,NSW
    DO JL=1,IL
      ZRGP(JL,IALD+JSW-1,IB) = MIN( 1.0_JPRB,MAX(ZREPALB,ZRGP(JL,IALD+JSW-1,IB)))
      ZRGP(JL,IALP+JSW-1,IB) = MIN( 1.0_JPRB,MAX(ZREPALB,ZRGP(JL,IALP+JSW-1,IB)))
    ENDDO
  ENDDO
  DO JL=1,IL
!-- original bounds from Martin et al. (1994) are 375/1500 and 36/280
!-- bounds from the relationship between 10m wind and CCNs
    ZRGP(JL,ICCNL,IB) = MIN( 1743._JPRB,MAX(292._JPRB,ZRGP(JL,ICCNL,IB)))
    ZRGP(JL,ICCNO,IB) = MIN(  287._JPRB,MAX( 32._JPRB,ZRGP(JL,ICCNO,IB)))
  ENDDO

  DO JLEV=1,KLEV
    DO JL=1,IL
      ZRGP(JL,IQS+JLEV-1 ,IB) = MAX(REPSCQ,&
       & MAX(2.0_JPRB*REPH2O,ZRGP(JL,IQS+JLEV-1,IB)))  
      ZRGP(JL,IWV+JLEV-1 ,IB) = MAX(REPSCQ,&
       & MAX(REPH2O,MIN(ZRGP(JL,IWV+JLEV-1,IB),&
       & ZRGP(JL,IQS+JLEV-1,IB)*(1.0_JPRB-REPH2O))))  
      ZRGP(JL,ICLC+JLEV-1,IB) = MAX( 0.0_JPRB ,MIN( 1.0_JPRB ,&
       & ZRGP(JL,ICLC+JLEV-1,IB)))  
      IF (YDERAD%LINTERPINCLOUDMEAN) THEN
        ZRGP(JL,ILWA+JLEV-1,IB) = MAX(  0.0_JPRB, MAX(REPH2O,ZRGP(JL,ILWA+JLEV-1,IB))&
             & *ZRGP(JL,ICLC+JLEV-1,IB))  
        ZRGP(JL,IIWA+JLEV-1,IB) = MAX(  0.0_JPRB, MAX(REPH2O,ZRGP(JL,IIWA+JLEV-1,IB))&
             & *ZRGP(JL,ICLC+JLEV-1,IB))  
        ZRGP(JL,ISWA+JLEV-1,IB) = MAX(  0.0_JPRB, MAX(REPH2O,ZRGP(JL,ISWA+JLEV-1,IB))&
             & *ZRGP(JL,ICLC+JLEV-1,IB))  
        ZRGP(JL,IRWA+JLEV-1,IB) = MAX(  0.0_JPRB, MAX(REPH2O,ZRGP(JL,IRWA+JLEV-1,IB))&
             & *ZRGP(JL,ICLC+JLEV-1,IB))  
        !      ZRGP(JL,IRWA+JLEV-1,IB) =MAX(0.0_JPRB,ZRGP(JL,IRWA+JLEV-1,IB))
      ELSE
        ! Now just copy over interpolated gridbox-mean quantities
        ZRGP(JL,ILWA+JLEV-1,IB) = MAX(  0.0_JPRB, ZRGP(JL,ILWA+JLEV-1,IB))
        ZRGP(JL,IIWA+JLEV-1,IB) = MAX(  0.0_JPRB, ZRGP(JL,IIWA+JLEV-1,IB))
        ZRGP(JL,ISWA+JLEV-1,IB) = MAX(  0.0_JPRB, ZRGP(JL,ISWA+JLEV-1,IB))
        ZRGP(JL,IRWA+JLEV-1,IB) = MAX(  0.0_JPRB, ZRGP(JL,IRWA+JLEV-1,IB))
      ENDIF
      ZRGP(JL,IRRA+JLEV-1,IB) =MAX(0.0_JPRB,ZRGP(JL,IRRA+JLEV-1,IB))
    ENDDO
  ENDDO

!  COMPUTE DISTRIBUTION OF CLIMATOLOGICAL AEROSOLS AND OZONE DIRECTLY ON THE REDUCED RADIATION GRID

  IF(.NOT.LRAYFM) THEN

    IF( LEPO3RA.AND.YO3%LGP )THEN
      ZECPO3(1:IL,1:KLEV) = ZRGP(1:IL,IECPO3:IECPO3+KLEV-1,IB)
    ELSE
      ZECPO3(:,:) = 0.0_JPRB
    ENDIF
    I1=1
    I2=99999999
    DO JL=1,IL
      IF( ZDGEMU(JL,IB) /= ZDGEMU(I1,IB) .OR. JL == IL )THEN
        LLDOLAST=.FALSE.
        IF( JL == IL )THEN
          IF( ZDGEMU(JL,IB) == ZDGEMU(I1,IB) )THEN
            I2=IL
          ELSE
            I2=IL-1
            LLDOLAST=.TRUE.
          ENDIF
        ELSE
          I2=JL-1
        ENDIF
        1000    CONTINUE
        CALL  RADACT ( YDEAERD,YDERAD,YDMODEL%YRML_PHY_AER%YREAERSNK,YDRIP, &
         & I1 ,I2 ,NRPROMA ,KLEV,&
         & 1 ,NRPROMA  ,NRPROMA  ,I1-1 ,1,&
         & ZRGP(1,IAPRS,IB) , ZRGP(1,IGELAM,IB) , ZDGEMU(1,IB),&
         & ZRGP(1,ICLON,IB) , ZRGP(1,ISLON ,IB) , ZRGP(1,IHTI ,IB),&
         & ZRGP(1,IWV  ,IB) , ZRGP(1,IQS   ,IB) , ZECPO3,&
         & ZQAER, ZQAERO , ZQOZ , ZRGP(1,IPERT ,IB)  )  

        IF (NOZOCL == 1.AND..NOT.LEPO3RA.AND..NOT.LPHYLIN) THEN
          IF (NO3CMIP /= 0) THEN
            CALL RADOZV ( I1, I2, NRPROMA, KLEV,&
             & 1 , NRPROMA , I1-1,&
             & ZRGP(1,IAPRS,IB) , ZRGP(1,IGELAM,IB) , ZRGP(1,IGEMU,IB),&
             & ZQOZ   )
          ELSE
            CALL RADOZC ( I1, I2, NRPROMA, KLEV,&
             & 1 , NRPROMA , I1-1,&
             & ZRGP(1,IAPRS,IB) , ZRGP(1,IGEMU,IB),&
             & ZQOZ   )  
          ENDIF
        ENDIF

        IF (NGHGRAD /= 0 .AND. .NOT.LPHYLIN) THEN
          CALL RADGHG ( YDERAD,YDRIP, I1, I2, NRPROMA, KLEV,&
           & NRPROMA , &
           & ZRGP(1,IAPRS,IB) , ZRGP(1,IGEMU,IB),&
           & ZQCO2, ZQCH4, ZQN2O, ZQNO2, ZQC11, ZQC12, ZQOZO, ZQC22, ZQCL4 )  
        ENDIF

        I1=JL
        IF( LLDOLAST )THEN
          LLDOLAST=.FALSE.
          I2=IL
          GOTO 1000
        ENDIF
      ENDIF
    ENDDO

!-- Prognostic O3 with interaction with radiation is required (ECMWF)
    IF(LEPO3RA.AND.YO3%LGP) THEN
      ZRGP(1:IL,IOZ:IOZ+KLEV-1,IB) = MAX( REPSCO, ZRGP(1:IL,IECPO3:IECPO3+KLEV-1,IB)*&
       & ZRGP(1:IL,IDP:IDP+KLEV-1,IB) )  
    ELSE
!-- Climatological O3 possibly scaled is used for radiation   (ECMWF)
      IF (NPERTOZ /= 0) ZQOZ(1:IL,1:KLEV) = ZQOZ(1:IL,1:KLEV) * (1.0_JPRB+ZPERTOZ)
      ZRGP(1:IL,IOZ:IOZ+KLEV-1,IB) = ZQOZ(1:IL,1:KLEV)
    ENDIF

! -- by default, globally averaged concentrations (mmr)
    ZRGP(1:IL,ICO2:ICO2+KLEV-1,IB) = PCCO2
    ZRGP(1:IL,ICH4:ICH4+KLEV-1,IB) = RCH4
    ZRGP(1:IL,IN2O:IN2O+KLEV-1,IB) = RN2O
    ZRGP(1:IL,INO2:INO2+KLEV-1,IB) = RNO2
    ZRGP(1:IL,IC11:IC11+KLEV-1,IB) = RCFC11
    ZRGP(1:IL,IC12:IC12+KLEV-1,IB) = RCFC12
    ZRGP(1:IL,IC22:IC22+KLEV-1,IB) = RCFC22
    ZRGP(1:IL,ICL4:ICL4+KLEV-1,IB) = RCCL4

    IF(NGHGRAD == 1 .OR. NGHGRAD >= 10) THEN
      ZRGP(1:IL,ICO2:ICO2+KLEV-1,IB) = ZQCO2(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 2 .OR. NGHGRAD >= 11) THEN
      ZRGP(1:IL,ICH4:ICH4+KLEV-1,IB) = ZQCH4(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 3 .OR. NGHGRAD >= 12) THEN
      ZRGP(1:IL,IN2O:IN2O+KLEV-1,IB) = ZQN2O(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 4 .OR. NGHGRAD >= 20) THEN
      ZRGP(1:IL,INO2:INO2+KLEV-1,IB) = ZQNO2(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 5 .OR. NGHGRAD >= 15) THEN
      ZRGP(1:IL,IC11:IC11+KLEV-1,IB) = ZQC11(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 6 .OR. NGHGRAD >= 16) THEN
      ZRGP(1:IL,IC12:IC12+KLEV-1,IB) = ZQC12(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 7 .OR. NGHGRAD == 17 .OR. NGHGRAD >= 21) THEN
      ZRGP(1:IL, IOZ:IOZ+KLEV-1 ,IB) = ZQOZO(1:IL,1:KLEV) *&
        & ZRGP(1:IL, IDP:IDP+KLEV-1, IB)
    ENDIF
    IF(NGHGRAD == 8 .OR. NGHGRAD >= 18) THEN
      ZRGP(1:IL,IC22:IC22+KLEV-1,IB) = ZQC22(1:IL,1:KLEV)
    ENDIF
    IF(NGHGRAD == 9 .OR. NGHGRAD >= 19) THEN
      ZRGP(1:IL,ICL4:ICL4+KLEV-1,IB) = ZQCL4(1:IL,1:KLEV)
    ENDIF

!-- Whether interaction of radiation with ECMWF prognostic aerosols is required or not,
!   get the climatological aerosols, with climatological aerosols possibly scaled is 
!   used for radiation   (ECMWF) 
    DO JLEV=1,KLEV
      IF (NPERTAER /= 0) ZQAER(1:IL,1:6,JLEV) = ZQAER(1:IL,1:6,JLEV) * (1.0_JPRB+ZPERTAER)
      I=(JLEV-1)*6
      ZRGP(1:IL,IAER+I:IAER+I+6-1,IB) = ZQAER(1:IL,1:6,JLEV)
    ENDDO
  ENDIF

  IF ( NACTAERO == 0 .OR. NAERMACC == 1) THEN 
    IF(.NOT.LRAYFM) THEN
      DO JLEV=1,KLEV
        IF (NPERTAER /= 0) ZQAER(1:IL,1:6,JLEV) = ZQAER(1:IL,1:6,JLEV) * (1.0_JPRB+ZPERTAER)
        I=(JLEV-1)*6
        ZRGP(1:IL,IAER+I:IAER+I+6-1,IB) = ZQAER(1:IL,1:6,JLEV)
      ENDDO
!correction to wrong Geleyn's O3 climatology done above 
    ELSEIF (NAER /= 0  .AND. .NOT.LHVOLCA) THEN
!  Supported but no transforms are performed (model grid = radiation grid)
      DO JLEV=1,KLEV
        I=(JLEV-1)*6
        ZRGP(1:IL,IAER+I:IAER+I+6-1,IB) = PAER(IBEG:IEND,1:6,JLEV)
      ENDDO
      ZRGP(1:IL,IOZ:IOZ+KLEV-1,IB) = PQO3(IBEG:IEND,1:KLEV)
    ELSE
      DO JLEV=1,KLEV
        I=(JLEV-1)*6
        ZRGP(1:IL,IAER+I:IAER+I+6-1,IB) = ZQAER(1:IL,1:6,JLEV)
      ENDDO
    ENDIF
  ENDIF

  IF (NAER == 0) THEN
    DO JLEV=1,KLEV
      I=(JLEV-1)*6
      ZRGP(1:IL,IAER+I:IAER+I+6-1,IB) = RCAEROS
    ENDDO
  ELSE
    DO JLEV=1,KLEV
      I=(JLEV-1)*6
      ZRGP(1:IL,IAER+I+5-1,IB) = RCAEROS
    ENDDO
  ENDIF
 !--
  IF (NAER == 1 .AND. LNOTROAER) THEN
    DO JLEV=1,KLEV
      I=(JLEV-1)*6
      ZRGP(1:IL,IAER+I+1-1,IB) = RCAEROS
      ZRGP(1:IL,IAER+I+2-1,IB) = RCAEROS
      ZRGP(1:IL,IAER+I+3-1,IB) = RCAEROS
      ZRGP(1:IL,IAER+I+4-1,IB) = RCAEROS
!!! for testing: keep climatological stratospheric aerosols; set all others to "zero"
!!      ZRGP(1:IL,IAER+I+6-1,IB) = RCAEROS
    ENDDO
  ENDIF

!-- using the MACC-derived aerosol climatology of aerosol mass mixing ratio
  IF (NAERMACC == 1) THEN

    DO JAERO=1,IRADAER
      IAE=(JAERO-1)*KLEV
      ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) =ZQAERO(1:IL,1:KLEV,JAERO)
    ENDDO

  ELSEIF (NAERMACC==0.AND.NACTAERO==0 ) THEN
    DO JAERO=1,IRADAER
      IAE=(JAERO-1)*KLEV
      ZRGP(1:IL,IAERO+IAE:IAERO+IAE+KLEV-1,IB) =0._JPRB
    ENDDO
  ENDIF


  IF( LLDEBUG )THEN
    ZRGP(:,IGIX,:)=ZRGP(:,IGI,:)
  ENDIF

  IF (LAPPROXLWUPDATE) THEN
    ! Surface temperature was weighted by 4th power in interpolation;
    ! now we convert back, but accounting for the fact that the
    ! interpolation may have led to negative values (yes this has
    ! happened). Note that the radiation scheme uses a look-up table
    ! for the Planck function in each spectral band, and 0 K will be
    ! off the scale, but that the look-up does sensible things for
    ! input temperatures out of bounds.
    ZRGP(1:IL,ITS,IB) = MAX(ZRGP(1:IL,ITS,IB),0.0_JPRB)**0.25_JPRB
  ENDIF

ENDDO
!$OMP END PARALLEL DO

DEALLOCATE(ZDGEMU)
CALL GSTATS(1209,1)

!  PERFORM RADIATION COMPUTATIONS

CALL GSTATS(1210,0)

!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1)&
!$OMP&PRIVATE(JRL,IBEG,IEND,IL,IB)
DO JRL=1,RADGRID%NGPTOT,NRPROMA

  IBEG=JRL
  IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
  IL=IEND-IBEG+1
  IB=(JRL-1)/NRPROMA+1

!*         4.2     CALL TO ACTUAL RADIATION SCHEME
  IF (YDERAD%LUSEPRE2017RAD) THEN
    ! Use the pre-2017 "McRad" radiation scheme

    IF (.NOT.LSRTM) THEN 

      CALL RADLSW&
     & (YDGEOMETRY%YRDIMV, YDMODEL%YRML_PHY_RAD,YDEPHLI,YDMODEL%YRML_PHY_SLIN%YRPHNC,YDMODEL%YRML_PHY_EC%YRECLD, &
     & 1 , IL , NRPROMA , KLEV,&
     & KMODE ,NAER,&
     & ZRII0,&
     & ZRGP(1,IAER,IB)    ,ZRGP(1,IALD,IB)   ,ZRGP(1,IALP,IB),&
     & ZRGP(1,IHPR,IB)    ,ZRGP(1,IPR,IB),&
     & ZRGP(1,ICCNL,IB)   ,ZRGP(1,ICCNO,IB)  ,PCCO2,&
     & ZRGP(1,ICLC,IB)    ,ZRGP(1,IDP,IB)    ,ZRGP(1,IEMISS,IB),& ! Note that first two bands of spectral emissivity are assumed
     & ZRGP(1,IEMISS+1,IB),ZRGP(1,ISLM,IB)   ,ZRGP(1,IAMU0,IB),&  ! to be outside IR window (PEMIR) and inside IR window (PEMIW)
     & ZRGP(1,IOZ,IB)     ,ZRGP(1,IWV,IB)    ,ZRGP(1,IIWA,IB),&
     & ZRGP(1,ILWA,IB)    ,ZRGP(1,IQS,IB)    ,&
     & ZRGP(1,IHTI,IB)    ,ZRGP(1,ITI,IB),&
     & ZRGP(1,ITS,IB)     ,ZRGP(1,IBAS,IB)   ,ZRGP(1,ITOP,IB),&
     ! FLUX OUTPUTS
     & ZRGP(1,IEMIT,IB)   ,ZRGP(1,ILWFC,IB)  ,ZRGP(1,IFRTH,IB),&
     & ZRGP(1,ISWFC,IB)   ,ZRGP(1,IFRSO,IB)  ,ZRGP(1,IFRSOD,IB),&
     & ZRGP(1,ISUDU,IB)   ,ZRGP(1,IUVDF,IB)  ,ZRGP(1,IPARF,IB),&
     & ZRGP(1,IPARCF,IB)  ,ZRGP(1,ITINCF,IB) ,&
     & ZSFSWDIR, ZSFSWDIF,ZFSDNN,ZFSDNV,&
     & .FALSE., ZDUMMY, ZDUMMY, ZDUMMY,ZDUMMY2 )   

    ELSE

      CALL RADLSWR&
     & (YDGEOMETRY%YRDIMV, YDMODEL, 1 , IL , NRPROMA , KLEV , IRADAER ,&
     & ZRII0,&
     & ZRGP(1,IAER,IB)   ,ZRGP(1,IAERO,IB) ,&
     & ZRGP(1,IALD,IB)   ,ZRGP(1,IALP,IB)  ,&
     & ZRGP(1,IHPR,IB)   ,ZRGP(1,IPR,IB)   ,&
     & ZRGP(1,ICCNL,IB)  ,ZRGP(1,ICCNO,IB) ,&
     & ZRGP(1,IGELAM,IB) ,ZRGP(1,IGEMU,IB) ,&
     & ZRGP(1,ICO2,IB)   ,ZRGP(1,ICH4,IB)  ,ZRGP(1,IN2O,IB) ,&
     & ZRGP(1,INO2,IB)   ,ZRGP(1,IC11,IB)  ,ZRGP(1,IC12,IB) ,&
     & ZRGP(1,IC22,IB)   ,ZRGP(1,ICL4,IB)  ,&
     & ZRGP(1,ICLC,IB)   ,ZRGP(1,IDP,IB)   ,ZRGP(1,IEMISS,IB),& ! Note that first two bands of spectral emissivity are assumed
     & ZRGP(1,IEMISS+1,IB),ZRGP(1,ISLM,IB)  ,ZRGP(1,IAMU0,IB),& ! to be outside IR window (PEMIR) and inside IR window (PEMIW)
     & ZRGP(1,IOZ,IB)    ,ZRGP(1,IWV,IB)   ,ZRGP(1,IIWA,IB), ZRGP(1,ISWA,IB),&
     & ZRGP(1,ILWA,IB)   ,ZRGP(1,IRWA,IB)  ,ZRGP(1,IHTI,IB) ,ZRGP(1,ITI,IB),&
     & ZRGP(1,ITS,IB)    ,&
     ! FLUX OUTPUTS
     & ZRGP(1,IEMIT,IB)  ,ZRGP(1,ILWFC,IB) ,ZRGP(1,IFRTH,IB),&
     & ZRGP(1,ISWFC,IB)  ,ZRGP(1,IFRSO,IB) ,ZRGP(1,IFRSOD,IB),&
     & ZRGP(1,IFRTED,IB),ZRGP(1,IFRSODC,IB),ZRGP(1,IFRTEDC,IB),&
     & ZRGP(1,ISUDU,IB)  ,ZRGP(1,IUVDF,IB) ,ZRGP(1,IPARF,IB),&
     & ZRGP(1,IPARCF,IB) ,ZRGP(1,ITINCF,IB),&
     & ZRGP(1,IFDIR,IB)  ,ZRGP(1,IFDIF,IB),ZRGP(1,ICDIR,IB) ,&
     & ZRGP(1,ILwDerivative,IB),&
     & ZRGP(1,ISwDiffuseBand,IB), ZRGP(1,ISwDirectBand,IB),&
     ! OPTIONAL ARGUMENTS
     & PPERT=ZRGP(1,IPERT,IB) )
    ENDIF

  ELSE

    ! Use the modular radiation scheme "ecRad"
    CALL RADIATION_SCHEME &
     & (YDMODEL, 1, IL, NRPROMA, KLEV, IRADAER, &
     &  ZRII0, &
     &  ZRGP(1,IAMU0,IB), ZRGP(1,ITS,IB),   ZRGP(1,IALD,IB), ZRGP(1,IALP,IB), &
     &  ZRGP(1,IEMISS,IB), &
     &  ZRGP(1,ICCNL,IB), ZRGP(1,ICCNO,IB) ,&
     &  ZRGP(1,IGELAM,IB),ZRGP(1,IGEMU,IB), ZRGP(1,ISLM,IB), &
     &  ZRGP(1,IPR,IB),   ZRGP(1,ITI,IB), &
     &  ZRGP(1,IAPRS,IB), ZRGP(1,IHTI,IB), &
     &  ZRGP(1,IWV,IB),   ZRGP(1,ICO2,IB),  ZRGP(1,ICH4,IB), ZRGP(1,IN2O,IB), &
     &  ZRGP(1,INO2,IB),  ZRGP(1,IC11,IB),  ZRGP(1,IC12,IB), ZRGP(1,IC22,IB), &
     &  ZRGP(1,ICL4,IB),  ZRGP(1,IOZ,IB), &
     &  ZRGP(1,ICLC,IB),  ZRGP(1,ILWA,IB),  ZRGP(1,IIWA,IB), ZRGP(1,IRWA,IB), &
     &  ZRGP(1,ISWA,IB), &
     &  ZRGP(1,IAER,IB),  ZRGP(1,IAERO,IB), &
     ! Flux outputs
     &  ZRGP(1,IFRSO,IB), ZRGP(1,IFRTH,IB), ZRGP(1,ISWFC,IB),ZRGP(1,ILWFC,IB),&
     &  ZRGP(1,IFRSOD,IB),ZRGP(1,IFRTED,IB), &
     &  ZRGP(1,IFRSODC,IB),ZRGP(1,IFRTEDC,IB),& 
     &  ZRGP(1,IFDIR,IB), ZRGP(1,ICDIR,IB), ZRGP(1,ISUDU,IB), &
     &  ZRGP(1,IUVDF,IB), ZRGP(1,IPARF,IB), &
     &  ZRGP(1,IPARCF,IB),ZRGP(1,ITINCF,IB), &
     &  ZRGP(1,IEMIT,IB) ,ZRGP(1,ILwDerivative,IB), &
     &  ZRGP(1,ISwDiffuseBand,IB), ZRGP(1,ISwDirectBand,IB),&
     ! OPTIONAL ARGUMENTS
     & PPERT=ZRGP(1,IPERT,IB), PFSD=ZRGP(1,IFSD,IB) )
   ENDIF

ENDDO
!$OMP END PARALLEL DO

CALL GSTATS(1210,1)

!  OUTPUT INTERPOLATION IF REQUIRED

IF( NRADRES /= NSMAX )THEN

  IF( NRADINT == 1 )THEN

!  INTERPOLATE FROM RADIATION GRID TO MODEL GRID USING A SPECTRAL TRANSFORM

    INUMFLD(:)=IFLDSOUT/NPRTRV
    DO J=1,MOD(IFLDSOUT,NPRTRV)
      INUMFLD(J)=INUMFLD(J)+1
    ENDDO
  
    ALLOCATE(IVSETSC(IFLDSOUT))
    IFLD=0
    DO JB=1,NPRTRV
      DO JFLD=1,INUMFLD(JB)
        IFLD=IFLD+1
        IVSETSC(IFLD)=JB
      ENDDO
    ENDDO
  
    ALLOCATE(ZSPRAD(INUMFLD(MYSETV),RADGRID%NSPEC2))
    CALL DIR_TRANS(PSPSCALAR=ZSPRAD(:,:),KPROMA=NRPROMA,KVSETSC=IVSETSC(:),&
     & PGP=ZRGP(:,IOUTBEG:IOUTEND,:),KRESOL=RADGRID%NRESOL_ID)  
    DEALLOCATE(ZRGP)
    ALLOCATE(ZSPMOD(INUMFLD(MYSETV),NSPEC2))
    ZSPMOD(:,:)=0.0_JPRB
    DO JM=1,RADGRID%NUMP
      IM=MYMS(JM)
      IF( IM /= RADGRID%MYMS(JM) )THEN
        CALL ABOR1('SUECRAD: MYMS AND RADGRID%MYMS DIFFERENT')
      ENDIF
      DO JIR=0,1
        DO JN=IM,RADGRID%NSMAX
          IMD=NASM0(IM)+2*(JN-IM)+JIR
          IRD=RADGRID%NASM0(IM)+2*(JN-IM)+JIR
          ZSPMOD(:,IMD)=ZSPRAD(:,IRD)
        ENDDO
      ENDDO
    ENDDO
    DEALLOCATE(ZSPRAD)
    ALLOCATE(ZRGP(NRPROMA,IFLDSOUT,IGPBLKS))
    CALL INV_TRANS(PSPSCALAR=ZSPMOD(:,:),KPROMA=NRPROMA,KVSETSC=IVSETSC(:),&
     & PGP=ZRGP,KRESOL=NRESOL)  
    DEALLOCATE(ZSPMOD)
    DEALLOCATE(IVSETSC)

  ELSEIF( NRADINT == 2 .OR. NRADINT == 3 )THEN
  
    ALLOCATE(ZRGPBUF(YDRO%NASLB1,IFLDSOUT))
    ZRGPBUF=0.0_JPRB
    CALL GSTATS(1211,0)
!$OMP  PARALLEL DO SCHEDULE(STATIC) PRIVATE(JSTGLO,IBEG,IEND,IL,IB,JL,JFLD)
    DO JSTGLO=1,RADGRID%NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,RADGRID%NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      DO JFLD=1,IFLDSOUT
        DO JL=1,IL
          ZRGPBUF(YDRO%NSLCORE(JL+JSTGLO-1),JFLD)=ZRGP(JL,IOUTBEG+JFLD-1,IB)
        ENDDO
      ENDDO
    ENDDO
!$OMP END PARALLEL DO
    CALL GSTATS(1211,1)
    DEALLOCATE(ZRGP)
    ALLOCATE(ZRGP(NRPROMA,IFLDSOUT,IGPBLKS))
    ZRGP=0.0_JPRB
   
    IF( YDRO%LSLONDEM .AND. .NOT.YDRO%LSLONDEM_ACTIVE )THEN
      YDRO%MASK_SL2(:)=0
    ENDIF
    IF (NPROC > 1)THEN
      LLINC=.FALSE.
      CALL GSTATS(65,0)
      IF( YDRO%LSLONDEM.AND.YDRO%LSLONDEM_ACTIVE )THEN
        IFIXRADFLD(1)=1
        IFIXRADFLD(2)=0
        CALL SLCOMM2(YDGEOMETRY%YRDIM,YDRO,IFIXRADFLD,YDRO%MASK_SL1,YDRO%MASK_SL2,&
         & IFLDSOUT,LLINC,ZRGPBUF)
        IF( LLDEBUG )THEN
          WRITE(NULERR,'("RADINTG:YDRO%NASLB1,SUM(YDRO%MASK_SL2)=",2(2X,I12))')&
           & YDRO%NASLB1,SUM(YDRO%MASK_SL2)  
        ENDIF
      ELSE
        CALL SLCOMM(YDRO,IDUMARR,IFLDSOUT,LLINC,0,ZRGPBUF)
      ENDIF
      CALL GSTATS(65,1)
    ENDIF
    ALLOCATE(ZPARITY(1:IFLDSOUT))
    ZPARITY(1:IFLDSOUT)=1
    IF( YDRO%LSLONDEM.AND..NOT.YDRO%LSLONDEM_ACTIVE )THEN
      IFIXRADFLD(1)=1
      IFIXRADFLD(2)=IFLDSOUT
      CALL SLEXTPOL(YDGEOMETRY%YRDIM,YDRO,IFLDSOUT,IFIXRADFLD,3,ZPARITY,ZRGPBUF,KMASK2=YDRO%MASK_SL2)  
    ELSE
      CALL SLEXTPOL(YDGEOMETRY%YRDIM,YDRO,IFLDSOUT,IDUMARR,1,ZPARITY,ZRGPBUF)  
    ENDIF
    DEALLOCATE(ZPARITY)

    IF    ( NRADINT == 2 )THEN
      IWIS=201
    ELSEIF( NRADINT == 3 )THEN
      IWIS=203
    ENDIF
    IFLDN=1
    IFLDX=IFLDSOUT
    ZDEPI=2.0_JPRB*RPI
    ZPIS2=0.5_JPRB*RPI
    Z4JP=(4._JPRB*REAL(RADGRID%NDGLG,JPRB)+2)/(4._JPRB*RPI)
    ALLOCATE(ISTALAT(RADGRID%NDGSAH:RADGRID%NDGENH))
    ALLOCATE(ZLSDEPI(RADGRID%NDGSAH:RADGRID%NDGENH))
!DEC$ IVDEP
    DO JGL=MAX(RADGRID%NDGSAG,&
       & RADGRID%NDGSAL+RADGRID%NFRSTLOFF-YDRO%NSLWIDEN)-RADGRID%NFRSTLOFF,&
       & MIN(RADGRID%NDGENG,&
       & RADGRID%NDGENL+RADGRID%NFRSTLOFF+YDRO%NSLWIDES)-RADGRID%NFRSTLOFF  
      IGLGLO=JGL+RADGRID%NFRSTLOFF
      ZLSDEPI(JGL)=REAL(RADGRID%NLOENG(IGLGLO),JPRB)/ZDEPI
      ISTALAT(JGL)=YDRO%NSLOFF(JGL)
    ENDDO

    CALL GSTATS(1212,0)
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) &
!$OMP&PRIVATE(JSTGLO,IBEG,IEND,IL,IB,J,ZDSLAT) &
!$OMP&PRIVATE(ZCSLA,ZDSLON,ZCSLO,IL0S,IL0ULI,IL0UDI,JLEV)
    DO JSTGLO=1,NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      CALL RDSCAW(YDRO,NRPROMA,1,IL,&
       & ISTALAT(RADGRID%NDGSAH:RADGRID%NDGENH),IWIS,RADGRID%NFRSTLOFF,&
       & Z4JP,ZPIS2,ZLSDEPI(RADGRID%NDGSAH:RADGRID%NDGENH),&
       & RADGRID%RLATI(RADGRID%NDGSAH:RADGRID%NDGENH),&
       & RADGRID%RIPI(RADGRID%NDGSAH:RADGRID%NDGENH,1:3),&
       & YDGSGEOM_NB%GELAM(IBEG:IEND),YDGSGEOM_NB%GELAT(IBEG:IEND),&
       & ZDSLAT,ZCSLA,ZDSLON,ZCSLO,IL0S)

      IF    ( NRADINT == 2 )THEN
        DO J=1,IL
          IL0ULI(J,1,1) = IL0S(J,1,1)+1
          IL0ULI(J,1,2) = IL0S(J,1,1)+2
          IL0ULI(J,2,1) = IL0S(J,1,2)+1
          IL0ULI(J,2,2) = IL0S(J,1,2)+2
          CALL LAIDLI_RAD(YDRO%NASLB1,IFLDSOUT,&
           & ZDSLAT(J,1),ZDSLON(J,1,1:2),IL0ULI(J,:,:),&
           & ZRGPBUF,ZRGP(J,1:IFLDSOUT,IB))  
        ENDDO
      ELSEIF( NRADINT == 3 )THEN
        DO J=1,IL
          IL0UDI(J,0,0) = 0
          IL0UDI(J,0,1) = IL0S(J,1,0)+1
          IL0UDI(J,0,2) = IL0S(J,1,0)+2
          IL0UDI(J,0,3) = 0
          IL0UDI(J,1,0) = IL0S(J,1,1)+0
          IL0UDI(J,1,1) = IL0S(J,1,1)+1
          IL0UDI(J,1,2) = IL0S(J,1,1)+2
          IL0UDI(J,1,3) = IL0S(J,1,1)+3
          IL0UDI(J,2,0) = IL0S(J,1,2)+0
          IL0UDI(J,2,1) = IL0S(J,1,2)+1
          IL0UDI(J,2,2) = IL0S(J,1,2)+2
          IL0UDI(J,2,3) = IL0S(J,1,2)+3
          IL0UDI(J,3,0) = 0
          IL0UDI(J,3,1) = IL0S(J,1,3)+1
          IL0UDI(J,3,2) = IL0S(J,1,3)+2
          IL0UDI(J,3,3) = 0 
          CALL LAIDDI_RAD(YDRO%NASLB1,IFLDSOUT,&
           & ZCSLA(J,1,:),ZDSLON(J,1,:),ZCSLO(J,1,:,:),IL0UDI(J,:,:),&
           & ZRGPBUF,ZRGP(J,1:IFLDSOUT,IB))  
        ENDDO
      ENDIF

    ENDDO
!$OMP END PARALLEL DO
    CALL GSTATS(1212,1)

    IF(YDRI%LSLONDEM.AND..NOT.YDRI%LSLONDEM_ACTIVE)THEN
      YDRI%LSLONDEM_ACTIVE=.TRUE.
    ENDIF
    IF(YDRO%LSLONDEM.AND..NOT.YDRO%LSLONDEM_ACTIVE)THEN
      YDRO%LSLONDEM_ACTIVE=.TRUE.
    ENDIF
    DEALLOCATE(ISTALAT)
    DEALLOCATE(ZLSDEPI)
    DEALLOCATE(ZRGPBUF)

  ELSE
    CALL ABOR1('RADINTG:OUTPUT - INVALID INTERPOLATION METHOD')
  ENDIF
  
  IFRSOD=IFRSOD-IOUTBEG+1
  IFRTED=IFRTED-IOUTBEG+1
  IFRSODC=IFRSODC-IOUTBEG+1
  IFRTEDC=IFRTEDC-IOUTBEG+1
  IEMIT =IEMIT -IOUTBEG+1
  ISUDU =ISUDU -IOUTBEG+1
  IUVDF =IUVDF -IOUTBEG+1
  IPARF =IPARF -IOUTBEG+1
  IPARCF=IPARCF-IOUTBEG+1
  ITINCF=ITINCF-IOUTBEG+1
  IFDIR =IFDIR -IOUTBEG+1
  ICDIR =ICDIR -IOUTBEG+1
  IF (LAPPROXLWUPDATE) THEN
    ILWDERIVATIVE = ILWDERIVATIVE - IOUTBEG + 1
  ENDIF

  IF (LAPPROXSWUPDATE) THEN
    ISWDIFFUSEBAND = ISWDIFFUSEBAND - IOUTBEG + 1
    ISWDIRECTBAND  = ISWDIRECTBAND  - IOUTBEG + 1
 ENDIF

  IFRSO =IFRSO -IOUTBEG+1
  ISWFC =ISWFC -IOUTBEG+1
  IFRTH =IFRTH -IOUTBEG+1
  ILWFC =ILWFC -IOUTBEG+1
  IF (LDIAGFORCING) THEN
     IAERO= IAERO- IOUTBEG+1      
     IAER = IAER - IOUTBEG+1
     IOZ  = IOZ  - IOUTBEG+1
     ICO2 = ICO2 - IOUTBEG+1
     ICH4 = ICH4 - IOUTBEG+1
     IN2O = IN2O - IOUTBEG+1
     INO2 = INO2 - IOUTBEG+1
     IC11 = IC11 - IOUTBEG+1
     IC12 = IC12 - IOUTBEG+1
     IC22 = IC22 - IOUTBEG+1
     ICL4 = ICL4 - IOUTBEG+1
  ENDIF
  IF( LLDEBUG )THEN
    IGIX  =IGIX  -IOUTBEG+1
  ENDIF
  
  IF( LLDEBUG )THEN
    DO JSTGLO=1,NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      DO JL=1,IL
        WRITE(NULOUT,'("RADINTG3: GI=",I6,2X,E25.15)')&
         & NGLOBALINDEX(JSTGLO+JL-1),ZRGP(JL,IGIX,IB)  
      ENDDO
    ENDDO
    CALL FLUSH(NULOUT)
  ENDIF

  IF( LLDEBUGRW )THEN
    LLERROR=.FALSE.
    ICNT=0
    DO JSTGLO=1,NGPTOT,NRPROMA
      IBEG=JSTGLO
      IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
      IL=IEND-IBEG+1
      IB=(JSTGLO-1)/NRPROMA+1
      DO JL=1,IL
        ICNT=ICNT+1
        IF( LLREAD )THEN
          READ(IULTMP) ZDUM
          IF( ZDUM /= ZRGP(JL,IGIX,IB) )THEN
            LLERROR=.TRUE.
            WRITE(NULERR,'("RADINTG: ICNT,ZDUM,ZRGP=",I12,2(2X,F20.5))')&
             & ICNT,ZDUM,ZRGP(JL,IGIX,IB)  
            WRITE(NULOUT,'("RADINTG: ICNT,ZDUM,ZRGP=",I12,2(2X,F20.5))')&
             & ICNT,ZDUM,ZRGP(JL,IGIX,IB)  
          ENDIF
        ELSE
          WRITE(IULTMP)ZRGP(JL,IGIX,IB)
        ENDIF
      ENDDO
    ENDDO
    IF( LLREAD )THEN
      IF( LLERROR )THEN
        CALL ABOR1('RADINTG: OUTPUT INTERP PROBLEM')
      ENDIF
    ENDIF
    CLOSE(IULTMP)
    CALL FREE_LUN(IULTMP)
  ENDIF

ENDIF

!  OUTPUT LOOP

CALL GSTATS(1213,0)
!$OMP  PARALLEL DO SCHEDULE(STATIC) &
!$OMP& PRIVATE(JSTGLO,JK,IBEG,IEND,IL,IB,JSW,&
!$OMP&   ZTRSO,ZEMTD,ZEMTU,ZTRSC,ZEMTC,ZTRSOD,ZTRSODC,ZEMTDC,ZAERMACCM,ZRHCL,ZQSAT,IFLAG,&
!$OMP&   ZAERTAUL, ZAERASYL, ZAEROMGL, ZAERTAUS, ZAERASYS, ZAEROMGS,ZTAUAER,IAE,ZAERTAULJ,ZAERTAUSJ,&
!$OMP&   ZFRSOD,ZFRTED,ZFRSODC,ZFRTEDC,ZEMIT,ZSUDU,ZUVDF,ZPARF,&
!$OMP&   ZPARCF,ZTINCF,ZFDIR,ZCDIR,ZFRSO,ZSWFC,ZFRTH,ZLWFC,&
!$OMP&   ZLWDERIVATIVE,ZSWDIFFUSEBAND,ZSWDIRECTBAND,&
!$OMP&   ZBROADBANDALBEDO, ZSWDNTOP, ZSWUPTOP, ZSWDNSURF,&
!$OMP&   ZSWUPSURF, ZSWDNTOPEFF, ZSWTOPSCALING, &
!$OMP&   ZSWTRANSMITTANCE, ZSWREFLECTANCE, ZNEWSWNETSURF, ZSWNETCHANGE)
DO JSTGLO=1,NGPTOT,NRPROMA
  IBEG =JSTGLO
  IEND=MIN(IBEG+NRPROMA-1,NGPTOT)
  IL=IEND-IBEG+1
  IB=(JSTGLO-1)/NRPROMA+1

  ZFRSOD (1:IL)          = ZRGP(1:IL,IFRSOD          ,IB)
  ZFRTED (1:IL,1:NLWOUT) = ZRGP(1:IL,IFRTED:IFRTED+NLWOUT-1,IB)
  ZFRSODC(1:IL)          = ZRGP(1:IL,IFRSODC         ,IB)
  ZFRTEDC(1:IL)          = ZRGP(1:IL,IFRTEDC         ,IB)
  ZEMIT  (1:IL)          = ZRGP(1:IL,IEMIT           ,IB)
  ZSUDU  (1:IL)          = ZRGP(1:IL,ISUDU           ,IB)
  ZUVDF  (1:IL)          = ZRGP(1:IL,IUVDF           ,IB)
  ZPARF  (1:IL)          = ZRGP(1:IL,IPARF           ,IB)
  ZPARCF (1:IL)          = ZRGP(1:IL,IPARCF          ,IB)
  ZTINCF (1:IL)          = ZRGP(1:IL,ITINCF          ,IB)
  ZFDIR  (1:IL)          = ZRGP(1:IL,IFDIR           ,IB)
  ZCDIR  (1:IL)          = ZRGP(1:IL,ICDIR           ,IB)
  ZFRSO  (1:IL,1:KLEV+1) = ZRGP(1:IL,IFRSO:IFRSO+KLEV,IB)
  ZSWFC  (1:IL,1:KLEV+1) = ZRGP(1:IL,ISWFC:ISWFC+KLEV,IB)
  ZFRTH  (1:IL,1:KLEV+1) = ZRGP(1:IL,IFRTH:IFRTH+KLEV,IB)
  ZLWFC  (1:IL,1:KLEV+1) = ZRGP(1:IL,ILWFC:ILWFC+KLEV,IB)

  ! Interpolation noise can lead to downwelling being less than net,
  ! which can cause problems in the Manners scheme in radheatn as it
  ! uses the ratio net/downwelling: correct both total sky and
  ! clear-sky downwelling fluxes
  ZFRSOD(1:IL)  = MAX(ZFRSO(1:IL,KLEV+1), ZFRSOD(1:IL))
  ZFRSODC(1:IL) = MAX(ZSWFC(1:IL,KLEV+1), ZFRSODC(1:IL))

  IF (LAPPROXLWUPDATE) THEN
    ZLWDERIVATIVE(1:IL,1:KLEV+1)&
         & = ZRGP(1:IL,ILWDERIVATIVE:ILWDERIVATIVE+KLEV,IB)
  ENDIF   

  ! We make an approximate update to the shortwave net flux that
  ! accounts for the different albedo to that seen by the radiation
  ! scheme
  IF (LAPPROXSWUPDATE) THEN
    ZSWDIRECTBAND(1:IL,1:NSW)&
         & = ZRGP(1:IL,ISWDIRECTBAND:ISWDIRECTBAND+NSW-1,IB)
    ZSWDIFFUSEBAND(1:IL,1:NSW)&
         & = ZRGP(1:IL,ISWDIFFUSEBAND:ISWDIFFUSEBAND+NSW-1,IB)

    ! Check that broadband diffuse and direct fluxes match the sum of
    ! the contributions from the bands
    DO JL = 1,IL

      ! Very occasionally the spatial interpolation can overshoot
      ! giving negative upwelling flux or negative albedo: only
      ! perform approximate updates if the surface fluxes are sensible
      IF (ZFRSOD(JL) > 0.0_JPRB .AND. ZFRSO(JL,KLEV+1) > 0.0_JPRB &
           &  .AND. ZFRSOD(JL) > ZFRSO(JL,KLEV+1)) THEN

        ! The broadband albedo is the weighted average of the spectral
        ! diffuse and direct albedos, where the weighting is by the
        ! components of the diffuse and direct fluxes from each band
        ZBROADBANDALBEDO = ( SUM(ZSWDIRECTBAND(JL,1:NSW) *PALBP(IBEG+JL-1,1:NSW))&
             &             + SUM(ZSWDIFFUSEBAND(JL,1:NSW)*PALBD(IBEG+JL-1,1:NSW)) )&
             &           / SIGN(MAX(ABS(ZFRSOD(JL)),ZEPSILON),ZFRSOD(JL))

        ! In principle, noise from the interpolation could cause an
        ! albedo out of range
        ZBROADBANDALBEDO = MAX(0.0_JPRB, MIN(ZBROADBANDALBEDO, 0.9999_JPRB))

        ! Local values of up and down surface and TOA fluxes
        ZSWDNTOP  = ZRII0 * ZAMU0(IBEG+JL-1)
        ZSWUPTOP  = ZSWDNTOP - ZFRSO(JL,1)
        ZSWDNSURF = ZFRSOD(JL)
        ZSWUPSURF = ZSWDNSURF - ZFRSO(JL,KLEV+1)

        ! Normally the TOA scaling factor is equal to the default
        ! value of ZSwTopScalingDefault, but if the total absorption
        ! of the atmosphere is less than 1-ZSwTopScalingDefault then
        ! this would lead to unphysical transmittance and absorption,
        ! in which case we set it to one minus the total absorption.
        ZSWTOPSCALING = (ZSWUPTOP + ZSWDNSURF - ZSWUPSURF) / ZSWDNTOP
        IF (ZSWTOPSCALING < ZSWTOPSCALINGDEFAULT) THEN
          ZSWTOPSCALING = ZSWTOPSCALINGDEFAULT
        ENDIF
        ! Scale the TOA incoming radiation to get an effective value,
        ! approximately accounting for the fact that some is quickly
        ! absorbed
        ZSWDNTOPEFF = ZSWDNTOP * ZSWTOPSCALING
        ZSWDNTOPEFF = ZSWDNTOP

        ! Compute approximate transmittance and reflectance of entire
        ! atmosphere
        ZSWTRANSMITTANCE&
             & = (ZSWDNTOPEFF*ZSWDNSURF   - ZSWUPSURF*ZSWUPTOP)&
             & / (ZSWDNTOPEFF*ZSWDNTOPEFF - ZSWUPSURF*ZSWUPSURF)
        ZSWREFLECTANCE&
             & = (ZSWDNTOPEFF*ZSWUPTOP    - ZSWUPSURF*ZSWDNSURF)&
             & / (ZSWDNTOPEFF*ZSWDNTOPEFF - ZSWUPSURF*ZSWUPSURF)

        ! New net surface flux according to local broadband albedo
        ZNEWSWNETSURF = ZSWDNTOPEFF * ZSWTRANSMITTANCE&
             &        * (1.0_JPRB - ZBROADBANDALBEDO)&
             &        / (1.0_JPRB - ZBROADBANDALBEDO*ZSWREFLECTANCE)
        
        ! Changes required to net flux at all levels
        ZSWNETCHANGE(JL) = ZNEWSWNETSURF - ZFRSO(JL, KLEV+1)

        ! Update the downwelling flux since some of the changed
        ! upwelling gets reflected back down
        ZFRSOD(JL) = ZNEWSWNETSURF / (1.0_JPRB - ZBROADBANDALBEDO)

      ELSE
        ! Skipping profile due to unphysical fluxes
        ZSWNETCHANGE(JL) = 0.0_JPRB
      ENDIF

    ENDDO

    ! Modify net flux by the same amount at all heights. Note that
    ! this update assumes that the change in the upwelling flux at the
    ! surface goes straight out to space or is reflected by the
    ! atmosphere back to the surface but none is absorbed within the
    ! atmosphere so there is no modification to the atmospheric
    ! heating rate profile.
    DO JK=1,KLEV+1
      DO JL=1,IL
         ZFRSO(JL,JK) = ZFRSO(JL,JK) + ZSWNETCHANGE(JL)
      ENDDO
    ENDDO

 ENDIF

!*         4.3     TRANSFORM FLUXES TO MODEL HISTORICAL VARIABLES

 ! All shortwave fluxes are divided by the TOA incoming flux to get
 ! "transmissivity", allowing subsequent multiplication by the TOA
 ! incoming flux in RADHEATN that might be different due to the
 ! changed solar zenith angle at a different spatial point or
 ! timestep. Longwave fluxes are not changed.

  DO JK=1,KLEV+1
    ZTRSO (1:IL,JK)=ZFRSO(1:IL,JK)/(ZRII0*ZAMU0(IBEG:IEND))
    ZEMTD (1:IL,JK)=ZFRTH(1:IL,JK)
    ZEMTU (1:IL,JK)=ZFRTH(1:IL,JK) ! EMTU not used in calling function
!-- clear-sky          
    ZTRSC (1:IL,JK)=ZSWFC(1:IL,JK)/(ZRII0*ZAMU0(IBEG:IEND))
    ZEMTC (1:IL,JK)=ZLWFC(1:IL,JK)
  ENDDO
  ZTRSOD(1:IL)=ZFRSOD(1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZTRSODC(1:IL)=ZFRSODC(1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZEMTDC(1:IL)=ZFRTEDC(1:IL)                          
  ZEMIT (1:IL)=ZEMIT (1:IL)
  ZUVDF (1:IL)=ZUVDF (1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZPARF (1:IL)=ZPARF (1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZPARCF(1:IL)=ZPARCF(1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZTINCF(1:IL)=ZTINCF(1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZFDIR (1:IL)=ZFDIR (1:IL)/(ZRII0*ZAMU0(IBEG:IEND))
  ZCDIR (1:IL)=ZCDIR (1:IL)/(ZRII0*ZAMU0(IBEG:IEND))

!     ------------------------------------------------------------------

!*         5.     DISTRIBUTION OF RESULTS TO FULL GRID.
!                 ------------ -- ------- -- ---- -----

!***

!*         7.2    STORAGE OF LW FLUXES AND SW TRANSMISSIVITIES

  DO JK=1,KLEV+1
    PTRSO(IBEG:IEND,JK)=ZTRSO(1:IL,JK)
    PTRSO(IBEG:IEND,JK)=MAX( 0.0_JPRB,MIN(1.0_JPRB,PTRSO(IBEG:IEND,JK)))
    PEMTD(IBEG:IEND,JK)=ZEMTD(1:IL,JK)
    PEMTU(IBEG:IEND,JK)=ZEMTU(1:IL,JK)
!-- clear-sky      
    PTRSC(IBEG:IEND,JK)=ZTRSC(1:IL,JK)
    PTRSC(IBEG:IEND,JK)=MAX( 0.0_JPRB,MIN(1.0_JPRB,PTRSC(IBEG:IEND,JK)))
    PEMTC(IBEG:IEND,JK)=ZEMTC(1:IL,JK)
    ! Partial derivatives
    IF (LAPPROXLWUPDATE) THEN
      ! This is the partial derivative of the upwelling LW flux at
      ! each half-level with respect to surface value (for a constant
      ! atmosphere), and should be between 0 and 1.  Interpolation
      ! back from the radiation grid can cause small negative values.
      PLWDERIVATIVE(IBEG:IEND,JK) = MAX(0.0_JPRB,MIN(1.0_JPRB,ZLWDERIVATIVE(1:IL,JK)))
    ENDIF
  ENDDO

  PFRTED(IBEG:IEND,1:NLWOUT)=ZFRTED(1:IL,1:NLWOUT)
  PTRSOD(IBEG:IEND)=MAX( 0.0_JPRB,MIN(1.0_JPRB,ZTRSOD(1:IL)))
  PTRSODC(IBEG:IEND)=MAX( 0.0_JPRB,MIN(1.0_JPRB,ZTRSODC(1:IL)))
  PEMTDC(IBEG:IEND)=ZEMTDC(1:IL)                             
  PEMIT(IBEG:IEND) =MAX( 0.0_JPRB,MIN(1.0_JPRB,ZEMIT (1:IL)))
  PSUDU(IBEG:IEND) =ZSUDU (1:IL)
  PUVDF(IBEG:IEND) =MAX( 0.0_JPRB,MIN(1.0_JPRB,ZUVDF (1:IL)))
  PPARF(IBEG:IEND) =MAX( 0.0_JPRB,MIN(1.0_JPRB,ZPARF (1:IL)))
  PPARCF(IBEG:IEND)=MAX( 0.0_JPRB,MIN(1.0_JPRB,ZPARCF(1:IL)))
  PTINCF(IBEG:IEND)=MAX( 0.0_JPRB,MIN(1.0_JPRB,ZTINCF(1:IL)))
  PFDIR(IBEG:IEND) =MAX( 0.0_JPRB,MIN(1.0_JPRB,ZFDIR (1:IL)))
  PCDIR(IBEG:IEND) =MAX( 0.0_JPRB,MIN(1.0_JPRB,ZCDIR (1:IL)))

!-- AEROSOLS,OZONE,GHG FOR DEBUGGING 
!-- The output GRIB sits only in FDB. 
!-- (!!!!!!!!!!!RUN ONLY 1 TIME STEP BECAUSE THE OUTPUT GRIB ON FDB CAN BE HUGE!!!!!!!)
!-- When LDIAGFORCING=True, remember also to add the following variable to the
!-- right namelists and check that there are no conflicts with the number
!-- of extra variables associated to radiation
!-- NAMDPHY: NVEXTRRAD=15
!-- NAMPHYDS: NVEXRADGB=82,83,84,85,86,87,88,89,90,91,92,93,94,95,96
!-- NAMFPC: NFP3DFS=15, MFP3Dfs=82,83,84,85,86,87,88,89,90,91,92,93,94,95,96
  IF (LDIAGFORCING) THEN
    DO JK=1,KLEV
       DO IAUX=1,6
          I=IAER-1 + 6*(JK-1)+IAUX
          PAER(IBEG:IEND,IAUX,JK)=ZRGP(1:IL,I,IB)
       ENDDO
!-- reconstruct Tegen aer SW OD
       DO JSW=1,14
       PAERTGS(IBEG:IEND,JK,JSW)=RSRTAUA(JSW,1)*PAER(IBEG:IEND,1,JK)+RSRTAUA(JSW,2)*PAER(IBEG:IEND,2,JK)+&
   & RSRTAUA(JSW,3)*PAER(IBEG:IEND,3,JK)+RSRTAUA(JSW,4)*PAER(IBEG:IEND,4,JK)+RSRTAUA(JSW,5)*PAER(IBEG:IEND,5,JK)+&
   & RSRTAUA(JSW,6)*PAER(IBEG:IEND,6,JK)
       ENDDO
!-- reconstruct Tegen aer LW OD
    IAE=1
    ZTAUAER(1:IL,IAE) =&
   & RAER(IAE,1)*PAER(IBEG:IEND,1,JK)+RAER(IAE,2)*PAER(IBEG:IEND,2,JK)&
   & +RAER(IAE,3)*PAER(IBEG:IEND,3,JK)+RAER(IAE,4)*PAER(IBEG:IEND,4,JK)&
   & +RAER(IAE,5)*PAER(IBEG:IEND,5,JK)+RAER(IAE,6)*PAER(IBEG:IEND,6,JK)  
    PAERTGL(IBEG:IEND,JK, 1)=ZTAUAER(1:IL,1)
    PAERTGL(IBEG:IEND,JK, 2)=ZTAUAER(1:IL,1)
    IAE=2
    ZTAUAER(1:IL,IAE) =&
   & RAER(IAE,1)*PAER(IBEG:IEND,1,JK)+RAER(IAE,2)*PAER(IBEG:IEND,2,JK)&
   & +RAER(IAE,3)*PAER(IBEG:IEND,3,JK)+RAER(IAE,4)*PAER(IBEG:IEND,4,JK)&
   & +RAER(IAE,5)*PAER(IBEG:IEND,5,JK)+RAER(IAE,6)*PAER(IBEG:IEND,6,JK)  
    PAERTGL(IBEG:IEND,JK, 3)=ZTAUAER(1:IL,2)
    PAERTGL(IBEG:IEND,JK, 4)=ZTAUAER(1:IL,2)
    PAERTGL(IBEG:IEND,JK, 5)=ZTAUAER(1:IL,2)
    IAE=3
    ZTAUAER(1:IL,IAE) =&
   & RAER(IAE,1)*PAER(IBEG:IEND,1,JK)+RAER(IAE,2)*PAER(IBEG:IEND,2,JK)&
   & +RAER(IAE,3)*PAER(IBEG:IEND,3,JK)+RAER(IAE,4)*PAER(IBEG:IEND,4,JK)&
   & +RAER(IAE,5)*PAER(IBEG:IEND,5,JK)+RAER(IAE,6)*PAER(IBEG:IEND,6,JK)  
    PAERTGL(IBEG:IEND,JK, 6)=ZTAUAER(1:IL,3)
    PAERTGL(IBEG:IEND,JK, 8)=ZTAUAER(1:IL,3)
    PAERTGL(IBEG:IEND,JK, 9)=ZTAUAER(1:IL,3)
    IAE=4
    ZTAUAER(1:IL,IAE) =&
   & RAER(IAE,1)*PAER(IBEG:IEND,1,JK)+RAER(IAE,2)*PAER(IBEG:IEND,2,JK)&
   & +RAER(IAE,3)*PAER(IBEG:IEND,3,JK)+RAER(IAE,4)*PAER(IBEG:IEND,4,JK)&
   & +RAER(IAE,5)*PAER(IBEG:IEND,5,JK)+RAER(IAE,6)*PAER(IBEG:IEND,6,JK)  
    PAERTGL(IBEG:IEND,JK, 7)=ZTAUAER(1:IL,4)
    IAE=5
    ZTAUAER(1:IL,IAE) =&
   & RAER(IAE,1)*PAER(IBEG:IEND,1,JK)+RAER(IAE,2)*PAER(IBEG:IEND,2,JK)&
   & +RAER(IAE,3)*PAER(IBEG:IEND,3,JK)+RAER(IAE,4)*PAER(IBEG:IEND,4,JK)&
   & +RAER(IAE,5)*PAER(IBEG:IEND,5,JK)+RAER(IAE,6)*PAER(IBEG:IEND,6,JK)  
    PAERTGL(IBEG:IEND,JK,10)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,11)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,12)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,13)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,14)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,15)=ZTAUAER(1:IL,5)
    PAERTGL(IBEG:IEND,JK,16)=ZTAUAER(1:IL,5)

!-- OZONE AND ghg CLIMATOLOGIES
    PQO3(IBEG:IEND  ,JK)=ZRGP(1:IL,IOZ +JK-1,IB)
    PGHG(IBEG:IEND,1,JK)=ZRGP(1:IL,ICO2+JK-1,IB)
    PGHG(IBEG:IEND,2,JK)=ZRGP(1:IL,ICH4+JK-1,IB)
    PGHG(IBEG:IEND,3,JK)=ZRGP(1:IL,IN2O+JK-1,IB)
    PGHG(IBEG:IEND,4,JK)=ZRGP(1:IL,INO2+JK-1,IB)
    PGHG(IBEG:IEND,5,JK)=ZRGP(1:IL,IC11+JK-1,IB)
    PGHG(IBEG:IEND,6,JK)=ZRGP(1:IL,IC12+JK-1,IB)
    PGHG(IBEG:IEND,7,JK)=ZRGP(1:IL,IC22+JK-1,IB)
    PGHG(IBEG:IEND,8,JK)=ZRGP(1:IL,ICL4+JK-1,IB)
    ENDDO !klev

!-- computes the LW and SW optical properties for the GEMS/MACC 
!-- prognostic aerosols as done in radlswr
!-- for debugging only
      DO IAUX=1,12
         DO JK=1,KLEV
          I=IAERO-1 + KLEV*(IAUX-1)+JK
          ZAERMACCM(1:IL,JK,IAUX)=ZRGP(1:IL,I,IB)
          ZDP(1:IL,JK)=PAPRS(IBEG:IEND,JK+1)-PAPRS(IBEG:IEND,JK)
         ENDDO
      ENDDO

    IFLAG=2

    CALL SATUR (YRTHF, YRCST, 1, IL, IL  , 1, KLEV, YDMODEL%YRML_PHY_SLIN%YREPHLI%LPHYLIN, &
    & PAPRSF(IBEG:IEND,1:KLEV), PT(IBEG:IEND,1:KLEV)   , ZQSAT, IFLAG )  
 
    DO JK=1,KLEV
    ZRHCL(1:IL,JK)=PQ(IBEG:IEND,JK)/ZQSAT(1:IL,JK)
    ENDDO

    CALL AER_RRTM&
    & ( YDEAERATM,YDMODEL%YRML_PHY_AER,YDMODEL%YRML_GCONF, &
    &  1   , IL   , IL    , KLEV    , 12, NSTART  , NSTEP , 1,&
    & PAPRS  (IBEG:IEND,1:KLEV+1)    , ZAERMACCM(1:IL,1:KLEV,:) , &
    & PAER(IBEG:IEND,:,1:KLEV), ZRHCL(1:IL,1:KLEV) ,&
    & ZAERTAUL, ZAERASYL, ZAEROMGL, ZAERTAUS, ZAERASYS, ZAEROMGS,&
    &   ZAERTAULJ,ZAERTAUSJ)

    PAERMACCS(IBEG:IEND,:)=SUM(ZAERTAUS(1:IL,:,:),2)
    PAERMACCL(IBEG:IEND,:)=SUM(ZAERTAUL(1:IL,:,:),2)
!    SINGLE SPECIES/MODE tau at 0.5 and 11 um
    PAERMACCSJ(IBEG:IEND,:)=SUM(ZAERTAUSJ(1:IL,:,:),2)
    PAERMACCLJ(IBEG:IEND,:)=SUM(ZAERTAULJ(1:IL,:,:),2)
  ENDIF

ENDDO
!$OMP END PARALLEL DO
CALL GSTATS(1213,1)

DEALLOCATE(ZRGP)

!***********************************************************************

!     -----------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RADINTG',1,ZHOOK_HANDLE)

CONTAINS

INTEGER(KIND=JPIM) FUNCTION INDRAD(KNEXT,KFLDS,LDUSE)
INTEGER(KIND=JPIM),INTENT(INOUT) :: KNEXT
INTEGER(KIND=JPIM),INTENT(IN) :: KFLDS
LOGICAL,INTENT(IN) :: LDUSE
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('RADINTG:INDRAD',0,ZHOOK_HANDLE)

IF( LDUSE )THEN
  INDRAD=KNEXT
  KNEXT=KNEXT+KFLDS
ELSE
  INDRAD=NUNDEFLD
ENDIF

IF (LHOOK) CALL DR_HOOK('RADINTG:INDRAD',1,ZHOOK_HANDLE)
  
END FUNCTION INDRAD

END SUBROUTINE RADINTG
