SUBROUTINE PPWETPOINT(YDCST, YDPHY,KIDIA,KFDIA,KLON,PAPRS,PT,PQV,PQL,PQI,PWETPOINT)
!-----------------------------------------------------------------------
! - INPUT .
! - OUTPUT .

!****  *PPWETPOINT*  CALCUL DU "POINT BLEU".

!                COMPUTATION OF THE WETPOINT TEMPERATURE.

!     Sujet.
!     ------
!     - ROUTINE ABSOLUMENT GENERALE CALCULANT LA TEMPERATURE 
!       EXPLICITEE CI-DESSUS QUELQUE SOIT L'ETAT "INPUT" DE L'AIR (P,T,
!       QV,QL,QI SONT INDEPENDANTS) ET QUE L'OPTION "LNEIGE=.T." AIT ETE
!       RETENUE OU PAS.
!     - FULLY GENERAL ROUTINE COMPUTING THE ABOVE-MENTIONED 
!       TEMPERATURE FOR WHATEVER ATMOSPHERIC "INPUT" STATE (P,T,QV,QL,QI
!       ARE INDEPENDENT VARIABLES) AND WHETHER OR NOT THE "LNEIGE=.T."
!       OPTION HAS BEEN ACTIVATED.

!**   Interface.
!     ----------
!        *CALL* *PPWETPOINT*

!-----------------------------------------------------------------------

! -   ARGUMENTS D'ENTREE  -  INPUT ARGUMENTS.
!     ---------------------------------------

! - DIMENSIONNEMENT - DIMENSIONING.

! KIDIA      : INDICE DE DEPART DES BOUCLES.
!            : FIRST INDEX OF LOOPS.
! KFDIA      : INDICE DE FIN DES BOUCLES.
!            : LAST INDEX OF LOOPS.
! KLON       : PROFONDEUR DES CHAMPS "VECTEUR".
!            : DEPTH OF THE VECTORIZATION ARRAYS.

! - VARIABLES - VARIABLES.

! PAPRS      : PRESSION.
!            : PRESSURE.
! PT         : TEMPERATURE.
!            : TEMPERATURE.
! PQV        : HUMIDITE SPECIFIQUE VAPEUR.
!            : WATER VAPOUR SPECIFIC HUMIDITY.
! PQL        : HUMIDITE SPECIFIQUE LIQUIDE.
!            : LIQUID WATER SPECIFIC HUMIDITY.
! PQV        : HUMIDITE SPECIFIQUE GLACE.
!            : ICE WATER SPECIFIC HUMIDITY.

!-----------------------------------------------------------------------

! -   ARGUMENTS DE SORTIE  -  OUTPUT ARGUMENTS.
!     -----------------------------------------

! - VARIABLES - VARIABLES.

! PWETPOINT  : TEMPERATURE DU POINT BLEU.
!            : WETPOINT TEMPERATURE.

!-----------------------------------------------------------------------

! -   ARGUMENTS IMPLICITES  -  INPLICIT ARGUMENTS.
!     --------------------------------------------

! COMMON/YOMPHY /
! COMMON/YOMCST /
! COMMON/FCTTRM /

!-----------------------------------------------------------------------

!     Externes.
!     ---------

!     Methode.
!     --------

!        CETTE ROUTINE N'A QU'UNE DIMENSION POUR SES VARIABLES D'ENTREE
!     AFIN D'ETRE LA PLUS GENERALE POSSIBLE (UTILISATION SUR LES NIVEAUX
!     DU MODELE COMME DU POST-PROCESSING, PAR EXEMPLE). TOUT ETAT DE
!     L'AIR REALISTE EST ADMIS EN ENTREE ET L'ALGORITHME PREND EN COMPTE
!     AUTOMATIQUEMENT UNE POSSIBLE TRANSITION DE PHASE LIQUIDE/GLACE.
!        TROIS EQUATIONS IMPLICITES SONT RESOLUES PAR METHODE DE NEWTON:
!           - RECHERCHE DU POINT DE SATURATION D'ENTROPIE EGALE PAR
!     TRANSFORMATION REVERSIBLE ;
!           - RECHERCHE DU POINT DE TEMPERATURE EGALE A CELLE DU POINT
!     TRIPLE LE LONG DE L'ADIABATIQUE SATUREE IRREVERSIBLE ;
!           - RECHERCHE DU POINT DE PRESSION EGALE A LA REFERENCE
!     ATMOSPHERIQUE LE LONG D'UNE AUTRE (PARFOIS LA MEME) ADIABATIQUE
!     IRREVERSIBLE.
!        REMARQUES :
!           - POUR LA PREMIERE ETAPE LA FORME SYMETRIQUE DE L'ENTROPIE
!     HUMIDE PROPOSEE PAR P. MARQUET EST UTILISEE AFIN DE PERMETTRE UN
!     MELANGE DE PHASES LIQUIDE ET GLACE DANS L'ETAT DE L'AIR ;
!           - POUR LES DEUX DERNIERES ETAPES, PLUTOT QUE DE NEGLIGER
!     COMME DE COUTUME LE TERME CONTENANT LE CP DU CONDENSAT, L'AUTEUR
!     DE LA ROUTINE EN A DERIVE UNE APPROXIMATION QUASI-EXACTE ET PLUTOT
!     BON MARCHE ;
!           - POUR CES DEUX MEMES ETAPES, LES EBAUCHES DES BOUCLES DE
!     NEWTON SONT OBTENUES PAR EXTRAPOLATION D'UNE LINEARISATION LOCALE
!     APPROCHEE DES EQUATIONS ADIABATIQUE SATUREES.

!        THIS ROUTINE HAS ONLY ONE DIMENSIONAL INPUT/OUTPUT ARRAYS IN
!     ORDER TO BE THE MOST GENERAL POSSIBLE (USE ON MODEL OR ON POST-
!     PROCESSING LEVELS, FOR EXAMPLE). ALL POSSIBLE REALISTIC INPUT
!     STATES ARE ALLOWED AND THE ALGORITHM AUTOMATICALLY TAKES INTO
!     ACCOUNT THE POTENTIAL LIQUID/ICE WATER TRANSITION.
!        THREE IMPLICIT EQUATIONS ARE SOLVED BY NEWTON METHODS :
!           - SEARCH OF THE SATURATION POINT OF EQUAL ENTROPY UNDER A
!     REVERSIBLE TRANSFORM ;
!           - SEARCH OF THE POINT OF TEMPERATURE EQUAL TO THAT OF THE
!     TRIPLE POINT ALONG THE IRREVERSIBLE MOIST ADIABAT ;
!           - SEARCH OF THE POINT OF REFERENCE ATMOSPHERIC PRESSURE
!     ALONG ANOTHER (SOMETIMES IDENTICAL) IRREVERSIBLE MOIST ADIABAT.
!        REMARKS :
!           - FOR THE FIRST STEP THE SYMETRIC FORM OF THE MOIST ENTROPY
!     PROPOSED BY P. MARQUET IS USED IN ORDER TO ALLOW A MIX OF LIQUID
!     AND ICE WATER IN THE ATMOSPHERIC STATE ;
!           - FOR THE TWO LAST STEPS, RATHER THAN THE USUAL NEGLECTION
!     OF THE TERM MULTIPLIED BY CP OF THE CONDENSATE, THE ROUTINE'S
!     AUTHOR DERIVED A QUASI EXACT AND NOT TOO EXPENSIVE ANALYTICAL
!     APPROXIMATION FOR IT ;
!           - FOR THE SAME STEPS, THE GUESSES OF THE NEWTON LOOP ARE
!     OBTAINED BY VERTICAL EXTRAPOLATION OF A LINEAR LOCAL APPROXIMATION
!     OF THE MOIST ADIABATS.

!     AUTHOR.
!     -------
!      92-09, J.F. Geleyn.

!     MODIFICATIONS.
!     --------------
!      M.Hamrud      01-Oct-2003 CY28 Cleaning
!-----------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

USE YOMPHY   , ONLY : TPHY
USE YOMCST   , ONLY : TCST  

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(TPHY)        ,INTENT(IN)    :: YDPHY
INTEGER(KIND=JPIM),INTENT(IN)    :: KLON 
INTEGER(KIND=JPIM),INTENT(IN)    :: KIDIA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFDIA 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PAPRS(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PT(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQV(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQL(KLON) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PQI(KLON) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PWETPOINT(KLON) 

REAL(KIND=JPRB) :: ZRT(KLON),ZCPT(KLON),ZRRT(KLON),ZS1(KLON),ZCONS(KLON)&
 & ,ZTITER(KLON),ZDEL(KLON),ZS2(KLON),ZFUNCT(KLON),ZPITER(KLON)  

INTEGER(KIND=JPIM) :: JIT, JLON

REAL(KIND=JPRB) :: ZCPLNTT, ZCWS, ZDELTA, ZDF, ZDLEW, ZDLSTT,&
 & ZE, ZEPSP, ZEPSRH, ZEW, ZF, ZKAPPA, ZKDI, &
 & ZKDW, ZKNI, ZKNW, ZLH, ZLHZ, ZLHZI, ZLHZW, &
 & ZLSTC, ZLSTT, ZLSTTI, ZLSTTW, ZPRS, ZQI, &
 & ZQL, ZQV, ZQVSAT, ZRDSV, ZRI, ZRL, ZRS, ZRV, &
 & ZTIN, ZTMAX, ZTMIN  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "fcttrm.ycst.h"

!*
!     ------------------------------------------------------------------
!     I - CALCUL DES CONSTANTES DERIVEES DE CELLES ISSUES DE YOMCST ET
!     CONSTANTES DE SECURITE.

!         COMPUTATION OF CONSTANTS DERIVED FROM THOSE CONTAINED IN
!     YOMCST AND SECURITY CONSTANTS.

IF (LHOOK) CALL DR_HOOK('PPWETPOINT',0,ZHOOK_HANDLE)
ASSOCIATE(NBITER=>YDPHY%NBITER, LNEIGE=>YDPHY%LNEIGE)
ZRDSV=YDCST%RD/YDCST%RV
ZLSTTW= FOLH (YDCST%RTT,0.0_JPRB)/YDCST%RTT+YDCST%RCW*YDCST%RV/( FOLH (YDCST%RTT,0.0_JPRB)/YDCST%RTT)
ZLSTTI= FOLH (YDCST%RTT,1.0_JPRB)/YDCST%RTT+YDCST%RCS*YDCST%RV/( FOLH (YDCST%RTT,1.0_JPRB)/YDCST%RTT)
ZLHZW= FOLH (0.0_JPRB,0.0_JPRB)
ZLHZI= FOLH (0.0_JPRB,1.0_JPRB)
ZKNW=YDCST%RESTT* FOLH (YDCST%RTT,0.0_JPRB)/(YDCST%RV*YDCST%RTT)
ZKNI=YDCST%RESTT* FOLH (YDCST%RTT,1.0_JPRB)/(YDCST%RV*YDCST%RTT)
ZKDW=YDCST%RKAPPA*YDCST%RESTT*( FOLH (YDCST%RTT,0.0_JPRB)/(YDCST%RV*YDCST%RTT))**2
ZKDI=YDCST%RKAPPA*YDCST%RESTT*( FOLH (YDCST%RTT,1.0_JPRB)/(YDCST%RV*YDCST%RTT))**2
ZCPLNTT=YDCST%RCPD*LOG(YDCST%RTT)

ZEPSP=10._JPRB
ZEPSRH=0.001_JPRB
ZTMIN=155._JPRB
ZTMAX=355._JPRB

!*
!     ------------------------------------------------------------------
!     II - CALCUL DE LA TEMPERATURE DE SATURATION (EN GENERAL MAIS PAS
!     FORCEMENT TEMPERATURE DU POINT DE CONDENSATION). LE RAPPORT DE
!     MELANGE TOTAL ET LA TEMPERATURE POTENTIELLE HUMIDE -REVERSIBLE-
!     SONT GARDES CONSTANTS DURANT L'OPERATION.

!          COMPUTATION OF THE SATURATION TEMPERATURE (IN GENERAL BUT NOT
!     SYSTEMATICALLY LIFTING CONDENSATION TEMPERATURE). THE TOTAL MIXING
!     RATIO AND THE MOIST -REVERSIBLE- POTENTIAL TEMPERATURE ARE KEPT
!     CONSTANT DURING THE PROCESS.

! - TEMPORAIRES .

! ZRT        : RAPPORT DE MELANGE TOTAL DE L'EAU.
!            : TOTAL WATER MIXING RATIO.
! ZCPT       : PARTIE "CP" DU "KAPPA" IMPLICITE DE LA TEMP. CONSERVEE.
!            : "CP" PART OF THE IMPLICIT "KAPPA" OF THE CONSERVED TEMP..
! ZRRT       : PARTIE "R" DU "KAPPA" IMPLICITE DE LA TEMP. CONSERVEE.
!            : "R" PART OF THE IMPLICIT "KAPPA" OF THE CONSERVED TEMP..
! ZS1        : EXPRESSION DE L'ENTROPIE ASSOCIE A LA TEMP. CONSERVEE.
!            : ENTROPY'S EXPRESSION LINKED TO THE CONSERVED TEMPERATURE.
! ZCONS      : CONSTANTE AUXILIAIRE POUR LA BOUCLE DE NEWTON.
!            : AUXILIARY CONSTANT FOR THE NEWTON LOOP.
! ZTITER     : EBAUCHE POUR LA SOLUTION DE LA BOUCLE DE NEWTON EN TEMP..
!            : FIRST GUESS FOR THE SOLUTION OF THE NEWTON LOOP ON TEMP..
! ZQVSAT     :
!            : SATURATED SPECIFIC HUMIDITY                               

!     CALCULS PRELIMINAIRES.
!     PRELIMINARY COMPUTATIONS.

DO JLON=KIDIA,KFDIA

!     SECURITES.
!     SECURITIES.

!     QVSAT CALCULATION DEPENING ON THE SNOW OPTION.            

  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-PT(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZQL=MAX(0.0_JPRB,PQL(JLON))
  ZQI=MAX(0.0_JPRB,PQI(JLON))
  ZTIN=MAX(ZTMIN,MIN(ZTMAX,PT(JLON)))
  ZPRS=MAX(ZEPSP,PAPRS(JLON))
  ZQVSAT=FOQS(FOEW(ZTIN,ZDELTA)/ZPRS)
  ZQV=MAX(ZEPSRH*ZQVSAT,PQV(JLON))

  ZRV=ZQV/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRL=ZQL/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRI=ZQI/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRT(JLON)=ZRV+ZRL+ZRI
  ZCPT(JLON)=YDCST%RCPD+YDCST%RCPV*ZRT(JLON)
  ZRRT(JLON)=YDCST%RD+YDCST%RV*ZRT(JLON)
  ZE=(ZRV*ZPRS)/(ZRV+ZRDSV)
  ZS1(JLON)=ZCPT(JLON)*LOG(ZTIN)-YDCST%RD*LOG(ZPRS-ZE)-YDCST%RV*ZRT(JLON)&
   & *LOG(ZE)-( FOLH (ZTIN,0.0_JPRB)*ZRL+ FOLH (ZTIN,1.0_JPRB)*ZRI)/ZTIN  
  ZCONS(JLON)=ZS1(JLON)+YDCST%RD*LOG(ZRDSV/ZRT(JLON))
  ZTITER(JLON)=ZTIN
ENDDO

!     BOUCLE DE NEWTON.
!     NEWTON LOOP.

DO JIT=1,NBITER
  DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-ZTITER(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

    ZEW= FOEW (ZTITER(JLON),ZDELTA)
    ZDLEW= FODLEW (ZTITER(JLON),ZDELTA)
    ZF=ZCONS(JLON)+ZRRT(JLON)*LOG(ZEW)-ZCPT(JLON)*LOG(ZTITER(JLON))
    ZDF=ZRRT(JLON)*ZDLEW-ZCPT(JLON)/ZTITER(JLON)
    ZTITER(JLON)=ZTITER(JLON)-ZF/ZDF
  ENDDO
ENDDO

!*
!     ------------------------------------------------------------------
!     III - CALCUL DE LA PRESSION CORRESPONDANT AU POINT TRIPLE LE LONG
!     DE L'ADIABATIQUE SATUREE IRREVERSIBLE PASSANT PAR LE POINT CALCULE
!     PRECEDEMMENT. DANS LE CAS "LNEIGE=.T." LA TEMPERATURE DU POINT EN
!     QUESTION DETERMINE LE CHOIX DES PARAMETRES LIES A "L" ET "CP".

!           COMPUTATION OF PRESSURE CORRESPONDING TO THE TRIPLE POINT ON
!     THE IRREVERSIBLE SATURATED ADIABAT PASSING THROUGH THE PREVIOUSLY
!     OBTAINED POINT. IN THE "LNEIGE=.T." CASE THE LATTER'S TEMPERATURE
!     DETERMINES THE CHOICE OF THE PARAMETERS LINKED TO "L" AND "CP".

! - TEMPORAIRES .

! ZDEL       : "MEMOIRE" DE LA VALEUR ZDELTA (EAU 0 / GLACE 1).
!            : "MEMORY" OF THE ZDELTA (WATER 0 / ICE 1) VALUE.
! ZFUNCT     : EXPRESSION UTILISEE DANS LA BOUCLE DE NEWTON.
!            : FUNCTIONAL EXPRESSION USED IN THE NEWTON LOOP.
! ZS2        : EXPRESSION DE LA PSEUDO ENTROPIE DE L'AD. IRREVERSIBLE.
!            : PSEUDO ENTROPY'S EXPRESSION FOR THE IRREVERSIBLE AD..
! ZPITER     : EBAUCHE POUR LA SOLUTION DE LA BOUCLE DE NEWTON EN PRES..
!            : FIRST GUESS FOR THE SOLUTION OF THE NEWTON LOOP ON PRES..

!     CALCULS PRELIMINAIRES.
!     PRELIMINARY COMPUTATIONS.

DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,YDCST%RTT-ZTITER(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZDEL(JLON)=ZDELTA
  ZEW= FOEW (ZTITER(JLON),ZDELTA)
  ZLSTT=ZLSTTW+ZDELTA*(ZLSTTI-ZLSTTW)
  ZCWS=YDCST%RCW+ZDELTA*(YDCST%RCS-YDCST%RCW)
  ZLSTC= FOLH (ZTITER(JLON),ZDELTA)/ZTITER(JLON)+ZCWS*YDCST%RV&
   & /( FOLH (ZTITER(JLON),ZDELTA)/ZTITER(JLON))  
  ZFUNCT(JLON)=ZRDSV*YDCST%RESTT*ZLSTT
  ZS2(JLON)=ZS1(JLON)+ZRT(JLON)*(ZLSTC+YDCST%RV*LOG(ZEW)-YDCST%RCPV*LOG(ZTITER(JLON)))
  ZCONS(JLON)=ZS2(JLON)-ZCPLNTT
  ZKAPPA=YDCST%RKAPPA*(1.0_JPRB+ZRT(JLON)* FOLH (ZTITER(JLON),ZDELTA)/(YDCST%RD&
   & *ZTITER(JLON)))/(1.0_JPRB+YDCST%RKAPPA*ZRT(JLON)&
   & * FOLH (ZTITER(JLON),ZDELTA)**2/(YDCST%RD*YDCST%RV*ZTITER(JLON)**2))  
  ZPITER(JLON)=(ZRDSV*ZEW/ZRT(JLON))*(YDCST%RTT/ZTITER(JLON))**(1.0_JPRB/ZKAPPA)-YDCST%RESTT
  ZPITER(JLON)=MAX(ZPITER(JLON),ZEPSP)
ENDDO

!     BOUCLE DE NEWTON (UNE ITERATION DE PLUS POUR P QUE POUR T).
!     NEWTON LOOP (ONE MORE ITERATION FOR P THAN FOR T).

DO JIT=1,NBITER+1
  DO JLON=KIDIA,KFDIA
    ZF=ZCONS(JLON)+YDCST%RD*LOG(ZPITER(JLON))-ZFUNCT(JLON)/ZPITER(JLON)
    ZDF=(YDCST%RD*ZPITER(JLON)+ZFUNCT(JLON))/ZPITER(JLON)**2
    ZPITER(JLON)=ZPITER(JLON)-ZF/ZDF
  ENDDO
ENDDO

!     RETOUR A LA PRESSION REELLE.
!     RETURN TO THE REAL PRESSURE.

DO JLON=KIDIA,KFDIA
  ZPITER(JLON)=ZPITER(JLON)+YDCST%RESTT
ENDDO

!*
!     ------------------------------------------------------------------
!     IV - CALCUL DE LA TEMPERATURE CORRESPONDANT A P STANDARD LE LONG
!     DE L'ADIABATIQUE SATUREE IRREVERSIBLE PASSANT PAR LE POINT CALCULE
!     PRECEDEMMENT. DANS LE CAS "LNEIGE=.T." LA PRESSION DU POINT EN
!     QUESTION DETERMINE LE CHOIX DES PARAMETRES LIES A "L" ET "CP".

!          COMPUTATION OF THE TEMPERATURE CORRESPONDING TO THE STD. P ON
!     THE IRREVERSIBLE SATURATED ADIABAT PASSING THROUGH THE PREVIOUSLY
!     OBTAINED POINT. IN THE "LNEIGE=.T." CASE THE LATTER'S PRESSURE
!     DETERMINES THE CHOICE OF THE PARAMETERS LINKED TO "L" AND "CP".

!     CALCULS PRELIMINAIRES.
!     PRELIMINARY COMPUTATIONS.

DO JLON=KIDIA,KFDIA

!     CALCULS DEPENDANT DE L'OPTION NEIGE.
!     SNOW OPTION DEPENDENT CALCULATIONS.

  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZPITER(JLON)-PAPRS(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZDLSTT=(ZDELTA-ZDEL(JLON))*(ZLSTTI-ZLSTTW)
  ZDEL(JLON)=ZDELTA
  ZS2(JLON)=ZS2(JLON)+ZDLSTT*ZRDSV*YDCST%RESTT/(ZPITER(JLON)-YDCST%RESTT)
  ZKAPPA=YDCST%RKAPPA*(1.0_JPRB+(ZKNW+ZDELTA*(ZKNI-ZKNW))/ZPITER(JLON))/(1.0_JPRB&
   & +(ZKDW+ZDELTA*(ZKDI-ZKDW))/ZPITER(JLON))  
  ZTITER(JLON)=YDCST%RTT*(PAPRS(JLON)/ZPITER(JLON))**ZKAPPA
ENDDO

!     BOUCLE DE NEWTON.
!     NEWTON LOOP.

DO JIT=1,NBITER
  DO JLON=KIDIA,KFDIA
    ZEW= FOEW (ZTITER(JLON),ZDEL(JLON))
    ZCWS=YDCST%RCW+ZDEL(JLON)*(YDCST%RCS-YDCST%RCW)
    ZLHZ=ZLHZW+ZDEL(JLON)*(ZLHZI-ZLHZW)
    ZLH= FOLH (ZTITER(JLON),ZDEL(JLON))
    ZLSTC=ZLH/ZTITER(JLON)+ZCWS*YDCST%RV/(ZLH/ZTITER(JLON))
    ZRS=ZRDSV*ZEW/(PAPRS(JLON)-ZEW)
    ZF=ZS2(JLON)-YDCST%RCPD*LOG(ZTITER(JLON))+YDCST%RD*LOG(PAPRS(JLON)-ZEW)-ZRS*ZLSTC
    ZDF=-YDCST%RCPD/ZTITER(JLON)-ZRS*((PAPRS(JLON)/(PAPRS(JLON)-ZEW))*ZLSTC*ZLH/(YDCST%RV&
     & *ZTITER(JLON)**2)+ZCWS*YDCST%RV*ZLHZ/ZLH**2+(YDCST%RCPV-ZCWS)&
     & /ZTITER(JLON))  
    ZTITER(JLON)=ZTITER(JLON)-ZF/ZDF
  ENDDO
ENDDO

!     STOCKAGE DU RESULTAT.
!     RESULT'S STORAGE.

DO JLON=KIDIA,KFDIA
  PWETPOINT(JLON)=ZTITER(JLON)
ENDDO

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('PPWETPOINT',1,ZHOOK_HANDLE)
END SUBROUTINE PPWETPOINT
