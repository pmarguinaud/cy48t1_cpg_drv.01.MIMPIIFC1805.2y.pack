SUBROUTINE SUDYN_STABILITY(YDCST, YDGEOMETRY,YDDYN,KULOUT)

!**** *SUDYN_STABILITY*   - Set Up DYNamical core, perform analysis of STABILITY 
!                           of NH or HY model with respect to T or pi_s residual with
!                           isothermal atmosphere
! 
!     Purpose.
!     --------
!     Tool to test stability properties of time stepping procedure
!     when vertical discretization or number of vertical levels are changed.

!**   Interface.
!     ----------
!       *CALL* SUNH_VERTFESPLINE

!     Explicit arguments :
!     --------------------
!       None

!     Implicit arguments :
!     --------------------

!     Method.
!     -------

!     Externals.
!     ----------
!       see below

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Jozef Vivoda (SHMI) ECMWF stay.
!      Original : 2019-03

!     Modifications.
!     --------------
!     End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE SUVFE_HLP    , ONLY : PRINT_MATRIX
USE YOMCT0       , ONLY : LNHDYN
USE YOMCVER      , ONLY : NVFE_TYPE, RVFE_ALPHA, RVFE_BETA, LVERTFE
USE YOMDYN       , ONLY : TDYN
USE YOMDYNA      , ONLY : YRDYNA
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMMP0       , ONLY : NPRINTLEV
USE YOMCST       , ONLY : TCST

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE (TCST), INTENT (IN) :: YDCST
TYPE(GEOMETRY)    ,INTENT(IN) :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(INOUT) :: YDDYN
INTEGER(KIND=JPIM),INTENT(IN) :: KULOUT 

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: JDIM, JSTEP, ITRUNC, IRES, IER, JL1
REAL(KIND=JPRB)    :: ZKWAVE, ZSITR, ZSIPR, ZTAU, ZTSTEP

REAL(KIND=JPRB) :: ZSITRAM(YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB) :: ZLAPL(YDGEOMETRY%YRDIMV%NFLEVG,YDGEOMETRY%YRDIMV%NFLEVG)

REAL(KIND=JPRB), ALLOCATABLE   :: ZFR(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZFI(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZABS(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZMO(:,:)
INTEGER(KIND=JPIM), ALLOCATABLE:: IWO(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZWO(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZID(:,:)

! JDIM allocations
REAL(KIND=JPRB), ALLOCATABLE :: ZIDM  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZMTL  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZRES  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZA_COR(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZTMP  (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZB_COR(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZIMPL (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZIMPLI(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZEXPL (:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZMSI  (:,:)

! JSTEP allocations
REAL(KIND=JPRB), ALLOCATABLE   :: ZSTEP(:,:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZFRM(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZFIM(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZABSM(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZMOM(:,:)
INTEGER(KIND=JPIM), ALLOCATABLE:: IWOM(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZWOM(:)

! JDIM allocations
REAL(KIND=JPRB), ALLOCATABLE   :: ZFRS(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZFIS(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZABSS(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZMOS(:,:)
INTEGER(KIND=JPIM), ALLOCATABLE:: IWOS(:)
REAL(KIND=JPRB), ALLOCATABLE   :: ZWOS(:)

CHARACTER(LEN=240) :: CLTAG
LOGICAL            :: LLVERB

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "siseve.intfb.h"
#include "eigsol.h"
#include "minv_caller.h"
#include "scordo.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIMV=>YDGEOMETRY%YRDIMV)
ASSOCIATE(NFLEVG=>YDDIMV%NFLEVG, LDYN_STABAN=>YDDYN%LDYN_STABAN,   SIPR=>YDDYN%SIPR, SITR=>YDDYN%SITR&
& )

!     ------------------------------------------------------------------

LLVERB = (NPRINTLEV>1)

IF(LNHDYN)THEN
  JDIM    = 4 * NFLEVG + 1
  ZSITRAM = YDDYN%SITRAM
ELSE
  JDIM    = 2 * NFLEVG + 1
ENDIF

IF(YRDYNA%LNESC)THEN
  JSTEP = JDIM
ELSE
  JSTEP = 2 * JDIM
ENDIF

ZSITR   = SITR
ZSIPR   = SIPR

WRITE(KULOUT,*) "================================="
WRITE(KULOUT,*) "      ANALYSIS OF STABILITY      "
WRITE(KULOUT,*) "================================="

ALLOCATE(ZIDM  (JDIM,JDIM))
ALLOCATE(ZMSI  (JDIM,JDIM))
ALLOCATE(ZMTL  (JDIM,JDIM))
ALLOCATE(ZRES  (JDIM,JDIM))
ALLOCATE(ZA_COR(JDIM,JDIM))
ALLOCATE(ZB_COR(JDIM,JDIM))
ALLOCATE(ZTMP  (JDIM,JDIM))
ALLOCATE(ZIMPL (JDIM,JDIM))
ALLOCATE(ZIMPLI(JDIM,JDIM))
ALLOCATE(ZEXPL (JDIM,JDIM))

ALLOCATE(ZID  (NFLEVG,NFLEVG))

ALLOCATE(ZSTEP(JSTEP, JSTEP))
ALLOCATE(ZFRM (JSTEP))
ALLOCATE(ZFIM (JSTEP))
ALLOCATE(ZABSM(JSTEP))
ALLOCATE(ZMOM (JSTEP, JSTEP))
ALLOCATE(IWOM (JSTEP + 1))
ALLOCATE(ZWOM (JSTEP + 1))

ALLOCATE(ZFRS (JDIM))
ALLOCATE(ZFIS (JDIM))
ALLOCATE(ZABSS(JDIM))
ALLOCATE(ZMOS (JDIM, JDIM))
ALLOCATE(IWOS (JDIM + 1))
ALLOCATE(ZWOS (JDIM + 1))

ALLOCATE(ZFR (NFLEVG))
ALLOCATE(ZFI (NFLEVG))
ALLOCATE(ZABS(NFLEVG))
ALLOCATE(ZMO (NFLEVG, NFLEVG))
ALLOCATE(IWO (NFLEVG + 1))
ALLOCATE(ZWO (NFLEVG + 1))

ZTSTEP = 600.0
ZTAU = ZTSTEP  * 0.5_JPRB

! * ZID and ZIDM contain the identity matrix "I":
ZIDM = 0.0_JPRB
DO JL1=1,JDIM
  ZIDM(JL1,JL1)=1.0_JPRB
ENDDO

ZID = 0.0_JPRB
DO JL1=1,NFLEVG
    ZID(JL1,JL1)=1.0_JPRB
ENDDO

DO ITRUNC = 1000, 1000, 100

  ! ZKWAVE = 2.0_JPRB * RPI / (40000000.) * REAL(ITRUNC,JPRB)
  ZKWAVE = 1.0_JPRB

  IF(LNHDYN)THEN
    CALL SUNH_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSITRAM,ZSIPR,ZMSI)
  ELSE
    CALL SU_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSIPR,ZMSI)
  ENDIF

  IF (LLVERB) THEN
  ! print linear model matrix
    CLTAG = "ZMSI ::"
    CALL PRINT_MATRIX(CLTAG, JDIM, JDIM, ZMSI)
  ENDIF

  ZIMPL = ZIDM - ZTAU * ZMSI
  ZEXPL = ZIDM + ZTAU * ZMSI

  CALL MINV_CALLER(.TRUE.,JDIM,ZIMPL,ZIMPLI)

  WRITE(KULOUT,'("----------------------")')
  CALL FLUSH(KULOUT)

  ! temperature residual check
  DO IRES = 0, 10

    ! compute isothermal matrix with T profile different that SITR
    ! this represents full model and therefore only one T profile can be used
    ZSITR = (370.0_JPRB - 150.0_JPRB) * REAL(IRES,JPRB) / 10.00_JPRB +  150.0_JPRB
    ZSIPR   = SIPR

    IF(LNHDYN)THEN
      ZSITRAM = ZSITR
      CALL SUNH_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSITRAM,ZSIPR,ZMTL)
    ELSE
      CALL SU_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSIPR,ZMTL)
    ENDIF

    ZRES  = ZTAU * (ZMTL - ZMSI)

    IF(YRDYNA%LNESC)THEN
      ZSTEP  = MATMUL(ZIMPLI, ZEXPL + 2.0_JPRB * ZRES)
    ELSE
      ZSTEP(1:JDIM,      1:     JDIM) = MATMUL(ZIMPLI, ZEXPL + 3.0_JPRB/2.0_JPRB * ZRES)
      ZSTEP(1:JDIM, JDIM+1:JDIM+JDIM) = MATMUL(ZIMPLI,       - 1.0_JPRB/2.0_JPRB * ZRES)
      ZSTEP(JDIM+1:JDIM+JDIM,      1:     JDIM) = ZIDM
      ZSTEP(JDIM+1:JDIM+JDIM, JDIM+1:JDIM+JDIM) = 0.0_JPRB
    ENDIF  

    ZA_COR = MATMUL(ZIMPLI, ZRES)

    ZTMP = ZA_COR
    CALL EIGSOL(JDIM,JDIM,ZTMP,ZFRS,ZFIS,1,ZMOS,IWOS,ZWOS,IER)
    IF(IER /= 0)THEN
      CALL ABOR1('SUDYN_STABILITY: ABORT AFTER CALL TO EIGSOL')
    ENDIF
    CALL SCORDO(JDIM,ZFRS,ZFIS,ZMOS,ZTMP)

    DO JL1 = 1, JDIM
      ZABSS(JL1) = SQRT(ZFRS(JL1)**2 + ZFIS(JL1)**2)
    ENDDO

    IF(YDDYN%NSITER > 0)THEN
      ! corrector step 
      ZB_COR = MATMUL(ZIMPLI, ZEXPL + ZRES)

      IF(YRDYNA%LNESC)THEN
        ZSTEP  = MATMUL(ZA_COR, ZSTEP) + ZB_COR
      ELSE
        ZSTEP(     1:     JDIM,      1:     JDIM) = ZA_COR
        ZSTEP(     1:     JDIM, JDIM+1:JDIM+JDIM) = ZB_COR
        ZSTEP(JDIM+1:JDIM+JDIM,      1:     JDIM) = 0.0_JPRB
        ZSTEP(JDIM+1:JDIM+JDIM, JDIM+1:JDIM+JDIM) = ZIDM
      ENDIF
    ENDIF

    CALL EIGSOL(JSTEP,JSTEP,ZSTEP,ZFRM,ZFIM,1,ZMOM,IWOM,ZWOM,IER)
    IF(IER /= 0)THEN
      CALL ABOR1('SUDYN_STABILITY: ABORT AFTER CALL TO EIGSOL')
    ENDIF
    CALL SCORDO(JSTEP,ZFRM,ZFIM,ZMOM,ZSTEP)

    DO JL1 = 1, JSTEP
      ZABSM(JL1) = SQRT(ZFRM(JL1)**2 + ZFIM(JL1)**2)
    ENDDO

    WRITE(KULOUT,'("T_STABILITY",";",2(I5,";"),6(F15.10,";"),5(F15.2,";"))') &
     & NVFE_TYPE, ITRUNC, RVFE_ALPHA, RVFE_BETA, MAXVAL(ZABSM), MINVAL(ZABSM), &
     & MAXVAL(ZABSS), MINVAL(ZABSS), SITR, ZSITR, SIPR, ZSIPR
    CALL FLUSH(KULOUT)

  ENDDO

  WRITE(KULOUT,'("----------------------")')
  CALL FLUSH(KULOUT)

  DO IRES = 0,20
   
    ZSITR = SITR
    ZSITRAM = ZSITR
    ZSIPR = (110000.0_JPRB - 60000.0_JPRB) * REAL(IRES,JPRB)/20.00_JPRB +  60000.0_JPRB

    IF(LNHDYN)THEN
      CALL SUNH_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSITRAM,ZSIPR,ZMTL)
    ELSE
      CALL SU_LINMOD(YDGEOMETRY,YDDYN,LLVERB,NFLEVG,JDIM,ZKWAVE,ZSITR,ZSIPR,ZMTL)
    ENDIF

    ! NESC predictor
    ZRES  = ZTAU * (ZMTL - ZMSI)

    IF(YRDYNA%LNESC)THEN
      ZSTEP  = MATMUL(ZIMPLI, ZEXPL + 2.0_JPRB * ZRES)
    ELSE
      ZSTEP(1:JDIM,      1:     JDIM) = MATMUL(ZIMPLI, ZEXPL + 3.0_JPRB/2.0_JPRB * ZRES)
      ZSTEP(1:JDIM, JDIM+1:JDIM+JDIM) = MATMUL(ZIMPLI,       - 1.0_JPRB/2.0_JPRB * ZRES)
      ZSTEP(JDIM+1: JDIM+JDIM,      1:     JDIM) = ZIDM
      ZSTEP(JDIM+1: JDIM+JDIM, JDIM+1:JDIM+JDIM) = 0.0_JPRB
    ENDIF  

    ZA_COR = MATMUL(ZIMPLI, ZRES)

    ZTMP = ZA_COR
    CALL EIGSOL(JDIM,JDIM,ZTMP,ZFRS,ZFIS,1,ZMOS,IWOS,ZWOS,IER)
    IF(IER /= 0)THEN
      CALL ABOR1('SUDYN_STABILITY: ABORT AFTER CALL TO EIGSOL')
    ENDIF
    CALL SCORDO(JDIM,ZFRS,ZFIS,ZMOS,ZTMP)

    DO JL1 = 1, JDIM
      ZABSS(JL1) = SQRT(ZFRS(JL1)**2 + ZFIS(JL1)**2)
    ENDDO

    ! corrector step 
    IF(YDDYN%NSITER > 0)THEN
      ZB_COR = MATMUL(ZIMPLI, ZEXPL + ZRES)

      IF(YRDYNA%LNESC)THEN
        ZSTEP  = MATMUL(ZA_COR, ZSTEP) + ZB_COR
      ELSE
        ZSTEP(     1:     JDIM,      1:     JDIM) = ZA_COR
        ZSTEP(     1:     JDIM, JDIM+1:JDIM+JDIM) = ZB_COR
        ZSTEP(JDIM+1:JDIM+JDIM,      1:     JDIM) = 0.0_JPRB
        ZSTEP(JDIM+1:JDIM+JDIM, JDIM+1:JDIM+JDIM) = ZIDM
      ENDIF
    ENDIF

    CALL EIGSOL(JSTEP,JSTEP,ZSTEP,ZFRM,ZFIM,1,ZMOM,IWOM,ZWOM,IER)
    IF(IER /= 0)THEN
      CALL ABOR1('SUDYN_STABILITY: ABORT AFTER CALL TO EIGSOL')
    ENDIF
    CALL SCORDO(JSTEP,ZFRM,ZFIM,ZMOM,ZSTEP)

    DO JL1 = 1, JSTEP
      ZABSM(JL1) = SQRT(ZFRM(JL1)**2 + ZFIM(JL1)**2)
    ENDDO

    WRITE(KULOUT,'("P_STABILITY",";",2(I5,";"),6(F15.10,";"),5(F15.2,";"))') &
     & NVFE_TYPE, ITRUNC, RVFE_ALPHA, RVFE_BETA, MAXVAL(ZABSM), MINVAL(ZABSM), &
     & MAXVAL(ZABSS), MINVAL(ZABSS), SITR, ZSITR, SIPR, ZSIPR
    CALL FLUSH(KULOUT)

  ENDDO

ENDDO

IF (LNHDYN) THEN

  ! construct laplacian operator (it is wave number independent)
  CALL SISEVE(YRDYNA,YDGEOMETRY,YDDYN,1,NFLEVG,ZID,ZLAPL,NFLEVG,LD_LSTAR=.TRUE.)

  IF (LLVERB) THEN
  ! print laplacian operator
    CLTAG = "ZLAPL ::"
    CALL PRINT_MATRIX(CLTAG, NFLEVG, NFLEVG, ZLAPL)
  ENDIF

  ! eigenvalues of laplacian operator
  CALL EIGSOL(NFLEVG,NFLEVG,ZLAPL,ZFR,ZFI,1,ZMO,IWO,ZWO,IER)
  IF(IER /= 0)THEN
    CALL ABOR1('SUDYN_STABILITY: ABORT AFTER CALL TO EIGSOL')
  ENDIF
  CALL SCORDO(NFLEVG,ZFR,ZFI,ZMO,ZLAPL)

  WRITE(KULOUT,'("----------------------")')
  WRITE(KULOUT,'("LAPL_STABILITY",";",1(I5,";"),2(F15.10,";"),2(1X,G15.7,";"))') & 
     & NVFE_TYPE, RVFE_ALPHA, RVFE_BETA, MAXVAL(ZFRS), MAXVAL(ZFIS)
  CALL FLUSH(KULOUT)

ENDIF

! switch off analysis to avoid another call
LDYN_STABAN = .FALSE.

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY',1,ZHOOK_HANDLE)

CONTAINS

SUBROUTINE SU_LINMOD(YDGEOMETRY,YDDYN,LDVERB,KFLEV,KDIM,PKWAVE, &
                     & PSITR,PSIPR,PMM)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE INTDYN_MOD,ONLY : YYTXYB
USE PARKIND1 , ONLY : JPRB, JPIM
USE SUVFE_HLP, ONLY : PRINT_MATRIX
USE YOMCST   , ONLY : TCST
USE YOMDYN   , ONLY : TDYN
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMLUN   , ONLY : NULOUT

!     ------------------------------------------------------------------

IMPLICIT NONE

! * input arguments
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(INOUT) :: YDDYN
LOGICAL           ,INTENT(IN)    :: LDVERB
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KDIM
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKWAVE                   
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSITR 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSIPR
! * output arguments
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMM(KDIM,KDIM)

CHARACTER(LEN=240) :: CLTAG
INTEGER(KIND=JPIM) :: IPROMA, IPROFS
PARAMETER(IPROMA=1)
INTEGER(KIND=JPIM) :: JI, IST, IEND, IMM, JLEV
INTEGER(KIND=JPIM) :: INDX_D, INDX_T, INDX_PS
REAL(KIND=JPRB) :: ZWN2
REAL(KIND=JPRB) :: ZVECTOR(KDIM)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-------------------------------------
! aux. variables
!-------------------------------------
REAL(KIND=JPRB) :: ZSPDIV_SI(KFLEV)
REAL(KIND=JPRB) :: ZSPT_SI  (KFLEV)
REAL(KIND=JPRB) :: ZSPSP_SI (IPROMA) 

REAL(KIND=JPRB) :: ZSTORAGE_SIPR
REAL(KIND=JPRB) :: ZSTORAGE_SITR
REAL(KIND=JPRB) :: ZSTORAGE_SITLAH(0:KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SITLAF(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIDELP(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIDPHI(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIRDEL(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SILNPR(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIALPH(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIRPRN
REAL(KIND=JPRB) :: ZSTORAGE_SIRPRG

REAL(KIND=JPRB) :: ZMDIV (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMTL  (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMSPL (IPROMA)

REAL(KIND=JPRB) :: ZPRES(IPROMA,0:KFLEV)
REAL(KIND=JPRB) :: ZXYB(IPROMA,KFLEV,YYTXYB%NDIM)

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "sigam.intfb.h"
#include "sitnu.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY:SU_LINMOD',0,ZHOOK_HANDLE)

!     ------------------------------------------------------------------

ZWN2   = -PKWAVE**2

! Dimensions
IST    = 1
IEND   = 1
IPROFS = IEND-IST+1

! store values 
ZSTORAGE_SIPR   = YDDYN%SIPR
ZSTORAGE_SITR   = YDDYN%SITR
ZSTORAGE_SITLAH = YDDYN%SITLAH
ZSTORAGE_SITLAF = YDDYN%SITLAF
ZSTORAGE_SIDPHI = YDDYN%SIDPHI
ZSTORAGE_SIDELP = YDDYN%SIDELP
ZSTORAGE_SIRDEL = YDDYN%SIRDEL
ZSTORAGE_SILNPR = YDDYN%SILNPR
ZSTORAGE_SIALPH = YDDYN%SIALPH
ZSTORAGE_SIRPRN = YDDYN%SIRPRN
ZSTORAGE_SIRPRG = YDDYN%SIRPRG

! redefine values by new values from arguments
YDDYN%SIPR   = PSIPR
YDDYN%SITR   = PSITR

! recompute SI quantities with respect to this SITR a SIPR
DO JI=1,IPROMA
  ZPRES(JI,KFLEV)=YDDYN%SIPR
ENDDO

CALL GPHPRE(IPROMA,KFLEV,IPROMA,IPROMA,YDGEOMETRY%YRVAB,ZPRES,PXYB=ZXYB)

YDDYN%SITLAH(0) = ZPRES(1,0)
DO JLEV=1,KFLEV
  YDDYN%SITLAH(JLEV)=ZPRES(1,JLEV)
  YDDYN%SITLAF(JLEV)=ZXYB(1,JLEV,YYTXYB%M_DELP)/ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIDPHI(JLEV)=YDCST%RD*YDDYN%SITR*ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIDELP(JLEV)=ZXYB(1,JLEV,YYTXYB%M_DELP)
  YDDYN%SIRDEL(JLEV)=ZXYB(1,JLEV,YYTXYB%M_RDELP)
  YDDYN%SILNPR(JLEV)=ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIALPH(JLEV)=ZXYB(1,JLEV,YYTXYB%M_ALPH)
ENDDO
YDDYN%SIRPRN = 1.0_JPRB/YDDYN%SIPR
YDDYN%SIRPRG = YDCST%RD*YDDYN%SITR

IF (LDVERB) THEN
  DO JI = 1, KFLEV
    WRITE(NULOUT,'(" SU_LINMOD: VERTICAL DISCR. - SITLAH, SITLAF, SIDPHI, &
     & SIDELP, SIRDEL, SILNPR, SIALPH", 7(1X,F12.5))') &
     & YDDYN%SITLAH(JI), YDDYN%SITLAF(JI), YDDYN%SIDPHI(JI), YDDYN%SIDELP(JI), &
     & YDDYN%SIRDEL(JI), YDDYN%SILNPR(JI), YDDYN%SIALPH(JI)
  ENDDO
ENDIF

! pointers 
INDX_D  = 0*KFLEV
INDX_T  = 1*KFLEV
INDX_PS = 2*KFLEV

DO IMM = 1,KDIM

  ZVECTOR       = 0.0_JPRB
  ZVECTOR(IMM)  = 1.0_JPRB
  
  DO JI=1,IPROMA
    ZMDIV (JI, 1:KFLEV) = ZVECTOR(INDX_D +1:INDX_D +KFLEV)
    ZMTL  (JI, 1:KFLEV) = ZVECTOR(INDX_T +1:INDX_T +KFLEV)
    ZMSPL (JI )         = ZVECTOR(INDX_PS+1)
  ENDDO

  ! only for LSPRT (not used)
  ZSPDIV_SI = 0.0_JPRB
  ZSPT_SI   = 0.0_JPRB
  ZSPSP_SI  = 0.0_JPRB

  CALL SITNU(LVERTFE, YDCST, YDGEOMETRY,YDDYN,1,1,ZMDIV,ZSPT_SI,ZSPSP_SI,1)
  CALL SIGAM(LVERTFE, YDCST, YDGEOMETRY,YDDYN,1,1,ZSPDIV_SI,ZMTL,ZMSPL,1,KFLEV)

  PMM(INDX_D +1:INDX_D +KFLEV,IMM) = -ZWN2*ZSPDIV_SI(1:KFLEV)  
  PMM(INDX_T +1:INDX_T +KFLEV,IMM) = -ZSPT_SI  (1:KFLEV)  
  PMM(INDX_PS+1,IMM)               = -ZSPSP_SI(1)

ENDDO

IF (LDVERB) THEN
  WRITE(CLTAG,*) " SU_LINMOD: MODEL MATRIX "
  CALL PRINT_MATRIX(CLTAG, KDIM, KDIM, PMM)
ENDIF

! restore original values
YDDYN%SIPR  = ZSTORAGE_SIPR
YDDYN%SITR  = ZSTORAGE_SITR
YDDYN%SITLAH = ZSTORAGE_SITLAH
YDDYN%SITLAF = ZSTORAGE_SITLAF
YDDYN%SIDPHI = ZSTORAGE_SIDPHI
YDDYN%SIDELP = ZSTORAGE_SIDELP
YDDYN%SIRDEL = ZSTORAGE_SIRDEL
YDDYN%SILNPR = ZSTORAGE_SILNPR
YDDYN%SIALPH = ZSTORAGE_SIALPH
YDDYN%SIRPRN = ZSTORAGE_SIRPRN
YDDYN%SIRPRG = ZSTORAGE_SIRPRG

IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY:SU_LINMOD',1,ZHOOK_HANDLE)
END SUBROUTINE SU_LINMOD

SUBROUTINE SUNH_LINMOD(YDGEOMETRY,YDDYN,LDVERB,KFLEV,KDIM,PKWAVE, &
                       & PSITR,PSITRAM,PSIPR,PMM)

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE INTDYN_MOD,ONLY : YYTXYB
USE PARKIND1 , ONLY : JPRB, JPIM
USE SUVFE_HLP, ONLY : PRINT_MATRIX
USE YOMCST   , ONLY : RD
USE YOMDYN   , ONLY : TDYN
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMLUN   , ONLY : NULOUT

!     ------------------------------------------------------------------

IMPLICIT NONE

! * input arguments
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(INOUT) :: YDDYN
LOGICAL           ,INTENT(IN)    :: LDVERB
INTEGER(KIND=JPIM),INTENT(IN)    :: KFLEV
INTEGER(KIND=JPIM),INTENT(IN)    :: KDIM
REAL(KIND=JPRB)   ,INTENT(IN)    :: PKWAVE                   
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSITR 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSITRAM(KFLEV) 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PSIPR
! * output arguments
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PMM(KDIM,KDIM)

CHARACTER(LEN=240) :: CLTAG
INTEGER(KIND=JPIM) :: IPROMA, IPROFS
PARAMETER(IPROMA=1)
INTEGER(KIND=JPIM) :: JI, IST, IEND, IMM, JLEV
INTEGER(KIND=JPIM) :: INDX_D, INDX_VD, INDX_T, INDX_PD, INDX_PS
REAL(KIND=JPRB) :: ZWN2
REAL(KIND=JPRB) :: ZVECTOR(KDIM)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-------------------------------------
! aux. variables
!-------------------------------------
REAL(KIND=JPRB) :: ZDUM  (KFLEV)

REAL(KIND=JPRB) :: ZSPDIV_SI(KFLEV)
REAL(KIND=JPRB) :: ZSPSVD_SI(KFLEV)
REAL(KIND=JPRB) :: ZSPSPD_SI(KFLEV)
REAL(KIND=JPRB) :: ZSPT_SI  (KFLEV)
REAL(KIND=JPRB) :: ZSPSP_SI (IPROMA) 

REAL(KIND=JPRB) :: ZSTORAGE_SIPR
REAL(KIND=JPRB) :: ZSTORAGE_SITR
REAL(KIND=JPRB) :: ZSTORAGE_SITRAM(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SITLAH(0:KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SITLAF(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIDELP(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIDPHI(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIRDEL(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SILNPR(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIALPH(KFLEV)
REAL(KIND=JPRB) :: ZSTORAGE_SIRPRN
REAL(KIND=JPRB) :: ZSTORAGE_SIRPRG

REAL(KIND=JPRB) :: ZMDIV (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMSPD (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMSPDL(IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMTL  (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMSVD (IPROMA,KFLEV)
REAL(KIND=JPRB) :: ZMSPL (IPROMA)

REAL(KIND=JPRB) :: ZPRES(IPROMA,0:KFLEV)
REAL(KIND=JPRB) :: ZXYB(IPROMA,KFLEV,YYTXYB%NDIM)

!     ------------------------------------------------------------------

#include "gphpre.intfb.h"
#include "sidd.intfb.h"
#include "siptp.intfb.h"
#include "siseve.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY:SUNH_LINMOD',0,ZHOOK_HANDLE)

!     ------------------------------------------------------------------

ZWN2   = -PKWAVE**2

! Dimensions
IST    = 1
IEND   = 1
IPROFS = IEND-IST+1

! store values 
ZSTORAGE_SIPR   = YDDYN%SIPR
ZSTORAGE_SITR   = YDDYN%SITR
ZSTORAGE_SITRAM = YDDYN%SITRAM
ZSTORAGE_SITLAH = YDDYN%SITLAH
ZSTORAGE_SITLAF = YDDYN%SITLAF
ZSTORAGE_SIDPHI = YDDYN%SIDPHI
ZSTORAGE_SIDELP = YDDYN%SIDELP
ZSTORAGE_SIRDEL = YDDYN%SIRDEL
ZSTORAGE_SILNPR = YDDYN%SILNPR
ZSTORAGE_SIALPH = YDDYN%SIALPH
ZSTORAGE_SIRPRN = YDDYN%SIRPRN
ZSTORAGE_SIRPRG = YDDYN%SIRPRG

! redefine values by new values from arguments
YDDYN%SIPR   = PSIPR
YDDYN%SITR   = PSITR
YDDYN%SITRAM = PSITRAM

! recompute SI quantities with respect to this SITR a SIPR
DO JI=1,IPROMA
  ZPRES(JI,KFLEV)=YDDYN%SIPR
ENDDO

CALL GPHPRE(IPROMA,KFLEV,IPROMA,IPROMA,YDGEOMETRY%YRVAB,ZPRES,PXYB=ZXYB)

YDDYN%SITLAH(0) = ZPRES(1,0)
DO JLEV=1,KFLEV
  YDDYN%SITLAH(JLEV)=ZPRES(1,JLEV)
  YDDYN%SITLAF(JLEV)=ZXYB(1,JLEV,YYTXYB%M_DELP)/ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIDPHI(JLEV)=YDCST%RD*YDDYN%SITR*ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIDELP(JLEV)=ZXYB(1,JLEV,YYTXYB%M_DELP)
  YDDYN%SIRDEL(JLEV)=ZXYB(1,JLEV,YYTXYB%M_RDELP)
  YDDYN%SILNPR(JLEV)=ZXYB(1,JLEV,YYTXYB%M_LNPR)
  YDDYN%SIALPH(JLEV)=ZXYB(1,JLEV,YYTXYB%M_ALPH)
ENDDO
YDDYN%SIRPRN = 1.0_JPRB/YDDYN%SIPR
YDDYN%SIRPRG = YDCST%RD*YDDYN%SITR

IF (LDVERB) THEN
  DO JI = 1, KFLEV
    WRITE(NULOUT,'(" SUNH_LINMOD: VERTICAL DISCR. - SITLAH, SITLAF, SIDPHI, &
     & SIDELP, SIRDEL, SILNPR, SIALPH", 7(1X,F12.5))') &
     & YDDYN%SITLAH(JI), YDDYN%SITLAF(JI), YDDYN%SIDPHI(JI), YDDYN%SIDELP(JI), &
     & YDDYN%SIRDEL(JI), YDDYN%SILNPR(JI), YDDYN%SIALPH(JI)
  ENDDO
ENDIF

! pointers 
INDX_D  = 0*KFLEV
INDX_VD = 1*KFLEV
INDX_T  = 2*KFLEV
INDX_PD = 3*KFLEV
INDX_PS = 4*KFLEV

DO IMM = 1,KDIM

  ZVECTOR       = 0.0_JPRB
  ZVECTOR(IMM)  = 1.0_JPRB
  
  DO JI=1,IPROMA
     ZMDIV (JI, 1:KFLEV) = ZVECTOR(INDX_D +1:INDX_D +KFLEV)  
     ZMSVD (JI, 1:KFLEV) = ZVECTOR(INDX_VD+1:INDX_VD+KFLEV)
     ZMSPD (JI, 1:KFLEV) = ZVECTOR(INDX_PD+1:INDX_PD+KFLEV)
     ZMSPDL(JI, 1:KFLEV) = ZVECTOR(INDX_PD+1:INDX_PD+KFLEV)
     ZMTL  (JI, 1:KFLEV) = ZVECTOR(INDX_T +1:INDX_T +KFLEV)
     ZMSPL (JI )         = ZVECTOR(INDX_PS+1)
  ENDDO

  ! only for LSPRT (not used)
  ZSPDIV_SI = 0.0_JPRB
  ZSPSVD_SI = 0.0_JPRB
  ZSPT_SI   = 0.0_JPRB
  ZSPSPD_SI = 0.0_JPRB
  ZSPSP_SI  = 0.0_JPRB

  ! - Computation of SI terms for continuity, T and Pcha equations.
  CALL SIPTP(YDCST, YDGEOMETRY,YDDYN,IPROMA,1,ZMDIV,ZMSVD,ZSPSPD_SI,ZSPT_SI, &
   & ZSPSP_SI,IPROFS)

  ! - Computation of SI terms for momentum equation.
  CALL SIDD(YRDYNA, YDCST, YDGEOMETRY,YDDYN,IPROMA,1,ZSPDIV_SI,ZDUM,ZMSPDL,ZMTL,ZMSPL,IPROFS)

  ! - Computation of SI terms for rescaled "pseudo" vertical divergence eqn.
  CALL SISEVE(YRDYNA,YDGEOMETRY,YDDYN,IPROMA,1,ZMSPD,ZSPSVD_SI,IPROFS)

  PMM(INDX_D +1:INDX_D +KFLEV,IMM) = -ZWN2*ZSPDIV_SI(1:KFLEV)
  PMM(INDX_VD+1:INDX_VD+KFLEV,IMM) = -ZSPSVD_SI(1:KFLEV)
  PMM(INDX_T +1:INDX_T +KFLEV,IMM) = -ZSPT_SI  (1:KFLEV)
  PMM(INDX_PD+1:INDX_PD+KFLEV,IMM) = -ZSPSPD_SI(1:KFLEV)
  PMM(INDX_PS+1,IMM) = -ZSPSP_SI(1)

ENDDO

IF (LDVERB) THEN
  WRITE(CLTAG,*) " SUNH_LINMOD: MODEL MATRIX "
  CALL PRINT_MATRIX(CLTAG, KDIM, KDIM, PMM)
ENDIF

! restore original values
YDDYN%SIPR   = ZSTORAGE_SIPR
YDDYN%SITR   = ZSTORAGE_SITR
YDDYN%SITRAM = ZSTORAGE_SITRAM
YDDYN%SITLAH = ZSTORAGE_SITLAH
YDDYN%SITLAF = ZSTORAGE_SITLAF
YDDYN%SIDPHI = ZSTORAGE_SIDPHI
YDDYN%SIDELP = ZSTORAGE_SIDELP
YDDYN%SIRDEL = ZSTORAGE_SIRDEL
YDDYN%SILNPR = ZSTORAGE_SILNPR
YDDYN%SIALPH = ZSTORAGE_SIALPH
YDDYN%SIRPRN = ZSTORAGE_SIRPRN
YDDYN%SIRPRG = ZSTORAGE_SIRPRG

IF (LHOOK) CALL DR_HOOK('SUDYN_STABILITY:SUNH_LINMOD',1,ZHOOK_HANDLE)
END SUBROUTINE SUNH_LINMOD

END SUBROUTINE SUDYN_STABILITY
