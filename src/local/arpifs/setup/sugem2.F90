!OCL  NOPREEX
SUBROUTINE SUGEM2(YDGEOMETRY)

!**** *SUGEM2*  - Initialize geometry parameters - part 2

!     Purpose.
!     --------
!        Initialize geometry
!        In particuliar, fills YRGSGEOM and YRCSGEOM

!**   Interface.
!     ----------
!        *CALL* *SUGEM2

!        Explicit arguments :
!        --------------------
!        None

!        Implicit arguments :
!        --------------------
!        YOMGEM

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!        None

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      Mats Hamrud and Philippe Courtier  *ECMWF*
!      Original : 87-10-15

!     Modifications.
!     --------------
!      Modified 03-08-25 by F. Vana: Geographical correction for SLHD
!      Modified 06-06-08 by F. Vana: Consistent setting of SLHDP with LAM
!      Modified 05-09-12 by A. Alias : GMGEC modif. introduced
!      S. Tyteca  - new diagnostics
!      P. Marquet - Cloud Forcing diagnostics
!      G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived types TGSGEOM, TCSGEOM and TCSGLEG
!      K. Yessad (Jul 2011): all YRGSGEOM and YRCSGEOM attributes now set-up there.
!      K. Yessad (jul 2011): reorder calculations for better consistency LAM/global model.
!      K. Yessad (dec 2011): ensure consistency with LAM model (comments).
!      R. El Khatib 13-Mar-2012 : let YRCSGEOM(IBL)%RINDX and
!                                 YRCSGEOM(IBL)%RINDY point somewhere for Arpege 
!      K. Yessad (June 2013): YOMGCO arrays allocated in a new routine SUALGCO.
!      T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCST   , ONLY : RPI, ROMEGA, RA
USE YOMLUN   , ONLY : NULOUT
USE YOMMP0   , ONLY : LOUTPUT, MY_REGION_EW

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT) :: YDGEOMETRY
INTEGER(KIND=JPIM) :: IENDLAT, IGLG, IROF, ISTLAT, JGL, JLON
INTEGER(KIND=JPIM) :: JKGLO, IEND, IBL

REAL(KIND=JPRB) :: ZZRCORI   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRCORIC  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGEMU    (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGSQM2   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGELAT   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGELAM   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGESLO   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGECLO   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGM      (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGOMVRL  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGOMVRM  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDL  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDM  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDLCL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDMCL(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGNORDMCM(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZGAW     (YDGEOMETRY%YRGEM%NGPTOT)
INTEGER(KIND=JPIM) :: IIGPLAT   (YDGEOMETRY%YRGEM%NGPTOT)
INTEGER(KIND=JPIM) :: IIUNIQUEGP(YDGEOMETRY%YRGEM%NGPTOT)

REAL(KIND=JPRD) :: ZZRCOLON  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRD) :: ZZRSILON  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRINDX   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRINDY   (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRATATH  (YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZZRATATX  (YDGEOMETRY%YRGEM%NGPTOT)

REAL(KIND=JPRB) :: Z2C, Z2OMR, ZA, ZB, ZC2M1, ZC2P1, ZCA1, ZCLABNE, ZCLAC,&
 & ZCLAP, ZCLO, ZCLOC, ZCLOP, ZD, ZGEMU,&
 & ZGM, ZGOMVRL, ZGSQM2, ZINCR, ZMUDEN,&
 & ZMUNUM, ZQM2, ZRCORI, ZRCORIC, ZSA1, ZSLABNE, ZSLAC, ZSLAP,&
 & ZSLO, ZSLOC, ZSLOP, ZTLABNE, ZUS2C, ZUSA, ZUSCLABNE  

REAL(KIND=JPRD) :: ZLON

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "sugem2_yrgsgeom.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUGEM2',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,  &
 & YDCSGLEG=>YDGEOMETRY%YRCSGLEG, YDVAB=>YDGEOMETRY%YRVAB,  &
 & YDVETA=>YDGEOMETRY%YRVETA, YDVFE=>YDGEOMETRY%YRVFE, YDSTA=>YDGEOMETRY%YRSTA, &
 & YDLAP=>YDGEOMETRY%YRLAP, &
  & YDVSPLIP=>YDGEOMETRY%YRVSPLIP, YDVSLETA=>YDGEOMETRY%YRVSLETA, YDHSLMER=>YDGEOMETRY%YRHSLMER,  &
  & YDSPGEOM=>YDGEOMETRY%YSPGEOM)
ASSOCIATE(NDGENL=>YDDIM%NDGENL, NDGLG=>YDDIM%NDGLG, NDLON=>YDDIM%NDLON, &
 & NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA, NSMAX=>YDDIM%NSMAX, &
 & NGPTOT=>YDGEM%NGPTOT, NLOENG=>YDGEM%NLOENG, NSTAGP=>YDGEM%NSTAGP, &
 & NSTTYP=>YDGEM%NSTTYP, RC2M1=>YDGEM%RC2M1, RC2P1=>YDGEM%RC2P1, &
 & RLOCEN=>YDGEM%RLOCEN, RMUCEN=>YDGEM%RMUCEN, RSTRET=>YDGEM%RSTRET, &
 & SLHDP=>YDGEM%SLHDP, &
 & MYLATS=>YDMP%MYLATS, NONL=>YDMP%NONL, NPTRFLOFF=>YDMP%NPTRFLOFF, &
 & NSTA=>YDMP%NSTA)
!     ------------------------------------------------------------------

!*       2.    Initialize part of YOMGC: YRGSGEOM
!              ----------------------------------

! ky: the LAM counterpart of these calculations is in part 7 of SUEGEM2.
!     Note that in LAM models YRGSGEOM should be computed before YRCSGEOM.

ISTLAT=1
IENDLAT=NDGENL

IF(NSTTYP == 1)THEN

  !     2.1  Case NSTTYP=1.

  IF (LOUTPUT) WRITE(UNIT=NULOUT,FMT='('' ******** NSTTYP=1'')')
  ZUS2C = 0.5_JPRB / RSTRET
  ZUSA  = 1.0_JPRB/RA
  CALL GSTATS(1405,0)
!$OMP PARALLEL DO SCHEDULE(STATIC)&
!$OMP&PRIVATE(JGL,IGLG,ZINCR,ZLON,ZMUNUM,ZMUDEN,ZGEMU,&
!$OMP& ZGSQM2,ZGM,ZRCORI,ZRCORIC,ZGOMVRL,IROF,JLON)
  DO JGL = ISTLAT,IENDLAT
    IGLG=MYLATS(JGL)
    IF ((IGLG <= 0).OR.(IGLG >= NDGLG+1)) THEN
      ZINCR=RPI
    ELSE
      ZINCR=0._JPRB
    ENDIF
    ZMUNUM = RC2M1 + RC2P1 * YDCSGLEG%RMU(IGLG)
    ZMUDEN = RC2P1 + RC2M1 * YDCSGLEG%RMU(IGLG)
    ZGEMU  = ZMUNUM / ZMUDEN
    ZGSQM2 = SQRT(1.0_JPRB-ZGEMU*ZGEMU)
    ZGM    = ZMUDEN*ZUS2C
    ZRCORI = 2.0_JPRB*ROMEGA*ZGEMU
    ZRCORIC= 2.0_JPRB*ROMEGA*ZGSQM2
    ZGOMVRL= 2.0_JPRB*ROMEGA*RA*ZGSQM2
    IROF=NSTAGP(JGL)-1
    DO JLON = NSTA(NPTRFLOFF+JGL,MY_REGION_EW),&
       & NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1  
      IROF=IROF+1
      ZZRCORI(IROF)=ZRCORI
      ZZRCORIC(IROF)=ZRCORIC
      ZZGEMU (IROF)=ZGEMU
      ZZGSQM2(IROF)=ZGSQM2
      ZLON = REAL(ZINCR,JPRD)+REAL(RPI,JPRD)*REAL(2*(JLON-1),JPRD)/REAL(NLOENG(IGLG),JPRD) 
      ZZGELAM(IROF)=ZLON
      ZZGELAT(IROF)=ASIN(ZGEMU)
      ZZGECLO(IROF)=COS(ZLON)
      ZZGESLO(IROF)=SIN(ZLON)
      ZZGM   (IROF)=ZGM
      IIUNIQUEGP(IROF)=NDLON*(IGLG-1)+JLON
      IIGPLAT(IROF)=IGLG
      ZZGOMVRL(IROF)=ZGOMVRL
      ZZGOMVRM(IROF)=0.0_JPRB
      ZZGNORDL(IROF)=0.0_JPRB
      ZZGNORDM(IROF)=1.0_JPRB
      ZZGNORDLCL(IROF)=0.0_JPRB
      ZZGNORDMCL(IROF)=0.0_JPRB
      ZZGNORDMCM(IROF)=-ZUSA*ZGEMU
      ZZGAW(IROF)=YDCSGLEG%RW(IGLG)/(REAL(NLOENG(IGLG),JPRB)*ZZGM(IROF)**2)
    ENDDO
  ENDDO
!$OMP END PARALLEL DO
  CALL GSTATS(1405,1)

ELSEIF(NSTTYP == 2)THEN

  !     2.2  Case NSTTYP=2.

  IF (LOUTPUT) WRITE(UNIT=NULOUT,FMT='('' ******** NSTTYP=2'')')

  Z2C   = 2.0_JPRB * RSTRET
  ZC2P1 = RSTRET* RSTRET+1.0_JPRB
  ZC2M1 = RSTRET* RSTRET-1.0_JPRB
  ZSLAP = RMUCEN
  ZCLAP = SQRT(1.0_JPRB - ZSLAP*ZSLAP)
  ZSLOP = SIN(RLOCEN)
  ZCLOP = COS(RLOCEN)
  Z2OMR = 2.0_JPRB*ROMEGA*RA
  ZUSA  = 1.0_JPRB/RA

  DO  JGL = ISTLAT,IENDLAT
    IGLG  = MYLATS(JGL)
    IF ((IGLG <= 0).OR.(IGLG >= NDGLG+1)) THEN
      ZINCR=RPI
    ELSE
      ZINCR=0._JPRB
    ENDIF
    ZSLAC = YDCSGLEG%RMU(IGLG)
    ZCLAC = SQRT(1.0_JPRB-YDCSGLEG%RMU(IGLG)*YDCSGLEG%RMU(IGLG))
    ZA    = 1.0_JPRB / (ZC2P1 + ZC2M1 * ZSLAC)
    ZGM   = (ZC2P1 + ZC2M1 * ZSLAC) / Z2C
    ZB    = ZC2M1 + ZC2P1 * ZSLAC
    ! * "theta_bne" is the latitude on the tilted unstretched sphere associated
    !   with the computational sphere; one computes ZCLABNE=cos(theta_bne),
    !   ZSLABNE=sin(theta_bne), ZTLABNE=tan(theta_bne),
    !   ZUSCLABNE=1/cos(theta_bne).
    ZCLABNE=ZCLAC/ZGM
    ZUSCLABNE=1.0_JPRB/ZCLABNE
    ZSLABNE=ZB*ZA
    ZTLABNE=ZSLABNE/ZCLABNE

    IROF=NSTAGP(JGL)-1
    DO JLON = NSTA(NPTRFLOFF+JGL,MY_REGION_EW),&
       & NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1  
      IROF=IROF+1
      ZLON = REAL(ZINCR,JPRD)+REAL(RPI,JPRD)*REAL(2*(JLON-1),JPRD)/REAL(NLOENG(IGLG),JPRD)
      ZSLOC= SIN(ZLON)
      ZCLOC= COS(ZLON)
      ZZGEMU(IROF)  = ZA*(Z2C*ZCLAP*ZCLAC*ZCLOC+ZB*ZSLAP)
      ZZGELAT(IROF) = ASIN(ZZGEMU(IROF))
      ZZGSQM2(IROF) = SQRT(1.0_JPRB-ZZGEMU(IROF)*ZZGEMU(IROF))
      ZQM2        = 1.0_JPRB / ZZGSQM2(IROF)
      ZZRCORI(IROF) = 2.0_JPRB * ROMEGA * ZZGEMU(IROF)
      ZZRCORIC(IROF)= 2.0_JPRB * ROMEGA * ZZGSQM2(IROF)
      ZZGM   (IROF) = ZGM
      IIUNIQUEGP(IROF)=NDLON*(IGLG-1)+JLON
      IIGPLAT(IROF)= IGLG

      ZD = ZA * ZQM2
      ZCLO = ZD *( ZB*ZCLAP*ZCLOP - Z2C*ZCLAC*&
       & (ZSLAP*ZCLOC*ZCLOP-ZSLOP*ZSLOC) )  
      ZSLO = ZD *( ZB*ZCLAP*ZSLOP - Z2C*ZCLAC*&
       & (ZSLAP*ZCLOC*ZSLOP+ZCLOP*ZSLOC) )  
      ZSLO = MIN(1.0_JPRB,MAX(-1.0_JPRB,ZSLO))
      ZZGECLO(IROF) = ZCLO
      ZZGESLO(IROF) = ZSLO
      ZZGELAM(IROF) = SIGN(1.0_JPRB,ZCLO) * ASIN(ZSLO) - RPI *&
       & ( MIN(SIGN(1.0_JPRB,ZCLO),0.0_JPRB) + MIN(SIGN(2.0_JPRB,ZSLO),0.0_JPRB)&
       & +MIN(SIGN(1.0_JPRB,ZCLO),0.0_JPRB) * MIN(SIGN(2.0_JPRB,ZSLO),0.0_JPRB) )  

      ZCA1 = (Z2C*ZSLAP*ZCLAC-ZB*ZCLAP*ZCLOC) * ZA
      ZSA1 = -ZCLAP*ZSLOC
      ZZGOMVRL(IROF) =  Z2OMR * ZCA1
      ZZGOMVRM(IROF) = -Z2OMR * ZSA1
      ZZGNORDL(IROF) = ZSA1 * ZQM2
      ZZGNORDM(IROF) = ZCA1 * ZQM2
      ZZGAW(IROF)=YDCSGLEG%RW(IGLG)/(REAL(NLOENG(IGLG),JPRB)*ZZGM(IROF)**2)
      ZZGNORDLCL(IROF)=-(ZUSA*ZCLAP*ZUSCLABNE)*ZCLOC
      ZZGNORDMCL(IROF)=(ZUSA*ZCLAP*ZTLABNE)*ZSLOC
      ZZGNORDMCM(IROF)=-ZUSA*(ZSLAP*ZSLABNE+ZCLAP*ZCLABNE*ZCLOC)
    ENDDO
  ENDDO

ELSE

  !     2.3  Case NSTTYP >2 or <1.

  WRITE(UNIT=NULOUT,FMT='('' ERROR NSTTYP '')')
  CALL ABOR1(' SUGEM2 ')

ENDIF

CALL SUGEM2_YRGSGEOM (YDGEOMETRY, ZZRCORI, ZZRCORIC, ZZGEMU, ZZGSQM2, ZZGELAM, &
& ZZGELAT, ZZGECLO, ZZGESLO, ZZGM, ZZGOMVRL, ZZGOMVRM, ZZGNORDL, ZZGNORDM, ZZGNORDLCL, &
& ZZGNORDMCL, ZZGNORDMCM, ZZGAW, IIGPLAT, IIUNIQUEGP)

!     ------------------------------------------------------------------

!*       3.    Initialize YRCSGEOM
!              --------------------

! ky: the LAM counterpart of these calculations is in part 8 of SUEGEM2.
!     RINDX and RINDY are required only if LRPLANE=T.
! REK : unfortunately RINDX and RINDY are passed in subroutines as pointer, so
!       they have to point to a well-known address.
!       Therefore it is better to compute them anyway (it is cheap).

CALL GSTATS(1406,0)
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) PRIVATE(JGL,IGLG,ZINCR,IROF,JLON,ZLON)
DO JGL = ISTLAT,IENDLAT
  IGLG=MYLATS(JGL)
  IF ((IGLG <= 0).OR.(IGLG >= NDGLG+1)) THEN
    ZINCR=RPI
  ELSE
    ZINCR=0._JPRB
  ENDIF
  IROF=NSTAGP(JGL)-1
  DO JLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW),NSTA(NPTRFLOFF+JGL,MY_REGION_EW)&
     & +NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1  
    IROF=IROF+1
    ZLON = REAL(ZINCR,JPRD)+REAL(RPI,JPRD)*REAL(2*(JLON-1),JPRD)/REAL(NLOENG(IGLG),JPRD)
    ZZRCOLON(IROF)=COS(ZLON)
    ZZRSILON(IROF)=SIN(ZLON)
    ZZRINDX(IROF)=REAL(JLON,JPRB)
    ZZRINDY(IROF)=REAL(IGLG,JPRB)
    ZZRATATH(IROF)=(RC2M1+YDCSGLEG%RMU(IGLG)*RC2P1)/(RSTRET*RA*YDCSGLEG%RSQM2(IGLG))
    ZZRATATX(IROF)=0.0_JPRB
  ENDDO
ENDDO
!$OMP END PARALLEL DO
CALL GSTATS(1406,1)

! Allocate YRCSGEOM_NB:
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RCOLON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RSILON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDX(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDY(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATH(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATX(NGPTOT))

! Allocate natively NPROMA-blocked copy of geometry

ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RCOLON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RSILON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDX(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDY(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATH(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATX(NPROMA,NGPBLKS))

! Allocate YRCSGEOM:
ALLOCATE(YDGEOMETRY%YRCSGEOM(NGPBLKS))

! Copy items to non-blocked form of YRCSGEOM
YDGEOMETRY%YRCSGEOM_NB%RCOLON(:)=ZZRCOLON(:)
YDGEOMETRY%YRCSGEOM_NB%RSILON(:)=ZZRSILON(:)
YDGEOMETRY%YRCSGEOM_NB%RINDX(:)=ZZRINDX(:)
YDGEOMETRY%YRCSGEOM_NB%RINDY(:)=ZZRINDY(:)
YDGEOMETRY%YRCSGEOM_NB%RATATH(:)=ZZRATATH(:)
YDGEOMETRY%YRCSGEOM_NB%RATATX(:)=ZZRATATX(:)

! Point to items in YRCSGEOM
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM(IBL)%RCOLON=>YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RSILON=>YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDX =>YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDY =>YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATH=>YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATX=>YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

! Copy blocks into native NPROMA-allocated arrays
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM_B%RCOLON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RSILON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDX (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDY (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATH(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATX(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

!     ------------------------------------------------------------------

!*       4.    Resolution correction for SLHD.
!              -------------------------------

! * This formula should be 2*RPI*SQRT(RA*RA/(NSMAX*(NSMAX+1)))
!   For the consistency reasons with the existing LAM tuning
!   it has been multiplied by SQRT(2.)
!   The LAM counterpart of this calculation is in part 9 of SUEGEM2:

SLHDP=RPI*SQRT(8.0_JPRB*RA*RA/(NSMAX*(NSMAX+1)))

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUGEM2',1,ZHOOK_HANDLE)
END SUBROUTINE SUGEM2
