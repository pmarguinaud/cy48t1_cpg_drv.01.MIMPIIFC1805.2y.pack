SUBROUTINE SUGEM2_YRCSGEOM (YDGEOMETRY, PZRCOLON, PZRSILON, PZRINDX, PZRINDY, PZRATATH, PZRATATX)

USE GEOMETRY_MOD, ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK

IMPLICIT NONE

TYPE (GEOMETRY),     INTENT (INOUT) :: YDGEOMETRY
REAL (KIND=JPRB),    INTENT (IN)    :: PZRCOLON(YDGEOMETRY%YRGEM%NGPTOT)
REAL (KIND=JPRB),    INTENT (IN)    :: PZRSILON(YDGEOMETRY%YRGEM%NGPTOT)
REAL (KIND=JPRB),    INTENT (IN)    :: PZRINDX (YDGEOMETRY%YRGEM%NGPTOT)
REAL (KIND=JPRB),    INTENT (IN)    :: PZRINDY (YDGEOMETRY%YRGEM%NGPTOT)
REAL (KIND=JPRB),    INTENT (IN)    :: PZRATATH(YDGEOMETRY%YRGEM%NGPTOT)
REAL (KIND=JPRB),    INTENT (IN)    :: PZRATATX(YDGEOMETRY%YRGEM%NGPTOT)


INTEGER (KIND=JPIM) :: IBL, IEND, JKGLO

REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('SUGEM2_YRCSGEOM',0,ZHOOK_HANDLE)

ASSOCIATE (NGPTOT => YDGEOMETRY%YRGEM%NGPTOT, NPROMA => YDGEOMETRY%YRDIM%NPROMA, &
         & NGPBLKS => YDGEOMETRY%YRDIM%NGPBLKS)

! Allocate YRCSGEOM_NB:
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RCOLON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RSILON(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDX(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RINDY(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATH(NGPTOT))
ALLOCATE(YDGEOMETRY%YRCSGEOM_NB%RATATX(NGPTOT))

! Allocate natively NPROMA-blocked copy of geometry

ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RCOLON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RSILON(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDX(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RINDY(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATH(NPROMA,NGPBLKS))
ALLOCATE(YDGEOMETRY%YRCSGEOM_B%RATATX(NPROMA,NGPBLKS))

! Allocate YRCSGEOM:
ALLOCATE(YDGEOMETRY%YRCSGEOM(NGPBLKS))

! Copy items to non-blocked form of YRCSGEOM
YDGEOMETRY%YRCSGEOM_NB%RCOLON(:)=PZRCOLON(:)
YDGEOMETRY%YRCSGEOM_NB%RSILON(:)=PZRSILON(:)
YDGEOMETRY%YRCSGEOM_NB%RINDX (:)=PZRINDX (:)
YDGEOMETRY%YRCSGEOM_NB%RINDY (:)=PZRINDY (:)
YDGEOMETRY%YRCSGEOM_NB%RATATH(:)=PZRATATH(:)
YDGEOMETRY%YRCSGEOM_NB%RATATX(:)=PZRATATX(:)

! Point to items in YRCSGEOM
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM(IBL)%RCOLON=>YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RSILON=>YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDX =>YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RINDY =>YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATH=>YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM(IBL)%RATATX=>YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

! Copy blocks into native NPROMA-allocated arrays
DO JKGLO=1,NGPTOT,NPROMA
  IEND=MIN(NPROMA,NGPTOT-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRCSGEOM_B%RCOLON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RSILON(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RSILON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDX (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDX(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RINDY (1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RINDY(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATH(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATH(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOM_B%RATATX(1:IEND,IBL)  = YDGEOMETRY%YRCSGEOM_NB%RATATX(JKGLO:JKGLO+IEND-1)
ENDDO

END ASSOCIATE

IF (LHOOK) CALL DR_HOOK('SUGEM2_YRCSGEOM',1,ZHOOK_HANDLE)

END SUBROUTINE SUGEM2_YRCSGEOM


