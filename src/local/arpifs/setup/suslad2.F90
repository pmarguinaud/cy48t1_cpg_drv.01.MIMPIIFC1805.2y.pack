SUBROUTINE SUSLAD2(YDGEOMETRY,YDSLREP,YDAD)

!**** *SUSLAD2*  Initialise constant arrays for SL adjoint interpolation

!     Purpose.
!     -------
!           Initialise constant arrays for SL adjoint interpolation.
!           These arrays cover an area of a standard width halo around 
!           this processors core points.

!     Method.
!     -------
!        See documentation

!     Author.
!     ------
!        George Mozdzynski  * ECMWF *
!        Original  : 00-08-01

!     Modifications.
!     -------------
!        02-10-01  G.Mozdzynski    support for radiation on-demand comms
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        K. Yessad Dec 2008: merge SLCOMM+SLCOMM1 -> SLCOMM.
!        G. Mozdzynski (Jan 2011): OOPS cleaning, use of derived type SL_STRUCT
!        G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived types TGSGEOM, TCSGEOM
!        T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!        K. Yessad (July 2014): Move some variables.
!        F. Vana July 2018: RK4 scheme for trajectory research.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1     , ONLY : JPIM, JPRB
USE YOMHOOK      , ONLY : LHOOK, DR_HOOK
USE YOMLUN       , ONLY : NULOUT
USE YOMCT0       , ONLY : LALLOPR, LRPLANE
USE YOMMP0       , ONLY : NPRINTLEV, NPROC
USE YOMSLREP     , ONLY : TSLREP
USE EINT_MOD     , ONLY : SL_STRUCT

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT) :: YDGEOMETRY
TYPE(TSLREP)   ,INTENT(INOUT) :: YDSLREP
TYPE(SL_STRUCT),INTENT(INOUT) :: YDAD
INTEGER(KIND=JPIM) :: IU, IFLDS, JROF, IDUMARR(2)
INTEGER(KIND=JPIM) :: JKGLO, IEND, IBL, IGPBLKSAD

LOGICAL :: LLP, LLINC

REAL(KIND=JPRB), ALLOCATABLE :: ZSLBUF(:,:)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "slcomm.intfb.h"

!    -------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUSLAD2',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDGEM=>YDGEOMETRY%YRGEM, YDCSGEOM_NB=>YDGEOMETRY%YRCSGEOM_NB, YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB,   &
& YDCSGEOMAD_NB=>YDGEOMETRY%YRCSGEOMAD_NB, YDGSGEOMAD_NB=>YDGEOMETRY%YRGSGEOMAD_NB)
ASSOCIATE(NPROMA=>YDDIM%NPROMA,   NGPTOT=>YDGEM%NGPTOT,   NADCORE=>YDSLREP%NADCORE, NGPTOTAD=>YDSLREP%NGPTOTAD&
& )
!     ------------------------------------------------------------------

LLP = NPRINTLEV >= 1.OR. LALLOPR
IU = NULOUT

IFLDS=11
IF( LRPLANE ) IFLDS=IFLDS+2
ALLOCATE (ZSLBUF(YDAD%NASLB1,IFLDS))
DO JROF=1,NGPTOT
  ZSLBUF(YDAD%NSLCORE(JROF), 1)=YDGSGEOM_NB%GEMU  (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 2)=YDGSGEOM_NB%GESLO (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 3)=YDGSGEOM_NB%GECLO (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 4)=YDGSGEOM_NB%GM    (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 5)=YDGSGEOM_NB%GNORDL(JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 6)=YDGSGEOM_NB%GNORDM(JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 7)=YDGSGEOM_NB%GSQM2 (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 8)=YDCSGEOM_NB%RCOLON(JROF)
  ZSLBUF(YDAD%NSLCORE(JROF), 9)=YDCSGEOM_NB%RSILON(JROF)
  ZSLBUF(YDAD%NSLCORE(JROF),10)=YDGSGEOM_NB%GELAM (JROF)
  ZSLBUF(YDAD%NSLCORE(JROF),11)=YDGSGEOM_NB%GELAT (JROF)
  IF( LRPLANE )THEN
    ZSLBUF(YDAD%NSLCORE(JROF),12)=YDCSGEOM_NB%RINDX (JROF)
    ZSLBUF(YDAD%NSLCORE(JROF),13)=YDCSGEOM_NB%RINDY (JROF)
  ENDIF
ENDDO
IF (NPROC > 1) THEN
  LLINC=.FALSE.
  CALL SLCOMM(YDAD,IDUMARR,IFLDS,LLINC,0,ZSLBUF)
ENDIF

ALLOCATE(YDGSGEOMAD_NB%RCORI    (1))
ALLOCATE(YDGSGEOMAD_NB%RCORIC   (1))
ALLOCATE(YDGSGEOMAD_NB%GEMU     (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GSQM2    (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GELAM    (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GELAT    (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GECLO    (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GESLO    (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GM       (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GOMVRL   (1))
ALLOCATE(YDGSGEOMAD_NB%GOMVRM   (1))
ALLOCATE(YDGSGEOMAD_NB%GNORDL   (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GNORDM   (NGPTOTAD))
ALLOCATE(YDGSGEOMAD_NB%GNORDLCL (1))
ALLOCATE(YDGSGEOMAD_NB%GNORDMCL (1))
ALLOCATE(YDGSGEOMAD_NB%GNORDMCM (1))
ALLOCATE(YDGSGEOMAD_NB%GAW      (1))
ALLOCATE(YDGSGEOMAD_NB%NGPLAT   (1))
ALLOCATE(YDGSGEOMAD_NB%NUNIQUEGP(1))

ALLOCATE(YDCSGEOMAD_NB%RCOLON(NGPTOTAD))
ALLOCATE(YDCSGEOMAD_NB%RSILON(NGPTOTAD))
IF( LRPLANE )THEN
  ALLOCATE(YDCSGEOMAD_NB%RINDX(NGPTOTAD))
  ALLOCATE(YDCSGEOMAD_NB%RINDY(NGPTOTAD))
ELSE
  ALLOCATE(YDCSGEOMAD_NB%RINDX(1))
  ALLOCATE(YDCSGEOMAD_NB%RINDY(1))
ENDIF
ALLOCATE(YDCSGEOMAD_NB%RATATH(1))
ALLOCATE(YDCSGEOMAD_NB%RATATX(1))

DO JROF=1,NGPTOTAD
  YDGSGEOMAD_NB%GEMU  (JROF)=ZSLBUF(NADCORE(JROF), 1)
  YDGSGEOMAD_NB%GESLO (JROF)=ZSLBUF(NADCORE(JROF), 2)
  YDGSGEOMAD_NB%GECLO (JROF)=ZSLBUF(NADCORE(JROF), 3)
  YDGSGEOMAD_NB%GM    (JROF)=ZSLBUF(NADCORE(JROF), 4)
  YDGSGEOMAD_NB%GNORDL(JROF)=ZSLBUF(NADCORE(JROF), 5)
  YDGSGEOMAD_NB%GNORDM(JROF)=ZSLBUF(NADCORE(JROF), 6)
  YDGSGEOMAD_NB%GSQM2 (JROF)=ZSLBUF(NADCORE(JROF), 7)
  YDCSGEOMAD_NB%RCOLON(JROF)=ZSLBUF(NADCORE(JROF), 8)
  YDCSGEOMAD_NB%RSILON(JROF)=ZSLBUF(NADCORE(JROF), 9)
  YDGSGEOMAD_NB%GELAM (JROF)=ZSLBUF(NADCORE(JROF),10)
  YDGSGEOMAD_NB%GELAT (JROF)=ZSLBUF(NADCORE(JROF),11)
  IF( LRPLANE )THEN
    YDCSGEOMAD_NB%RINDX(JROF)=ZSLBUF(NADCORE(JROF),12)
    YDCSGEOMAD_NB%RINDY(JROF)=ZSLBUF(NADCORE(JROF),13)
  ENDIF
ENDDO

IGPBLKSAD=(NGPTOTAD-1)/NPROMA+1
ALLOCATE(YDGEOMETRY%YRGSGEOMAD(IGPBLKSAD))
ALLOCATE(YDGEOMETRY%YRCSGEOMAD(IGPBLKSAD))
DO JKGLO=1,NGPTOTAD,NPROMA
  IEND=MIN(NPROMA,NGPTOTAD-JKGLO+1)
  IBL=(JKGLO-1)/NPROMA+1
  YDGEOMETRY%YRGSGEOMAD(IBL)%RCORI    =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%RCORIC   =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GEMU  =>YDGSGEOMAD_NB%GEMU  (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GSQM2 =>YDGSGEOMAD_NB%GSQM2 (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GELAM =>YDGSGEOMAD_NB%GELAM (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GELAT =>YDGSGEOMAD_NB%GELAT (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GESLO =>YDGSGEOMAD_NB%GESLO (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GECLO =>YDGSGEOMAD_NB%GECLO (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GM    =>YDGSGEOMAD_NB%GM    (JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GOMVRL   =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GOMVRM   =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GNORDL=>YDGSGEOMAD_NB%GNORDL(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GNORDM=>YDGSGEOMAD_NB%GNORDM(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRGSGEOMAD(IBL)%GNORDLCL =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GNORDMCL =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GNORDMCM =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%GAW      =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%NGPLAT   =>NULL()
  YDGEOMETRY%YRGSGEOMAD(IBL)%NUNIQUEGP=>NULL()

  YDGEOMETRY%YRCSGEOMAD(IBL)%RCOLON=>YDCSGEOMAD_NB%RCOLON(JKGLO:JKGLO+IEND-1)
  YDGEOMETRY%YRCSGEOMAD(IBL)%RSILON=>YDCSGEOMAD_NB%RSILON(JKGLO:JKGLO+IEND-1)
  IF( LRPLANE )THEN
    YDGEOMETRY%YRCSGEOMAD(IBL)%RINDX=>YDCSGEOMAD_NB%RINDX(JKGLO:JKGLO+IEND-1)
    YDGEOMETRY%YRCSGEOMAD(IBL)%RINDY=>YDCSGEOMAD_NB%RINDY(JKGLO:JKGLO+IEND-1)
  ELSE
    YDGEOMETRY%YRCSGEOMAD(IBL)%RINDX=>NULL()
    YDGEOMETRY%YRCSGEOMAD(IBL)%RINDY=>NULL()
  ENDIF
ENDDO

YDGEOMETRY%AD_GEOM = .TRUE.

IF(ALLOCATED(ZSLBUF)) DEALLOCATE(ZSLBUF)

9 FORMAT(1X,'ARRAY ',A10,' ALLOCATED ',8I8)
!    -------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUSLAD2',1,ZHOOK_HANDLE)
END SUBROUTINE SUSLAD2
