SUBROUTINE SUSLB(YDDIMV,YDMODEL)

!   ------------------------------------------------------------

!**** *SUSLB   - Setup relative pointers for semi-lagrangian buffers

!     Purpose.
!     --------
!           Initialize PTRSLB1 and PTRSLB2, modules that contain
!           the relative pointers for the semi-lagrangian buffers.
!           These are used under GP_MODEL to compute the absolute
!           pointers required. Also computes the parities needed for
!           the extension of semi-lagrangian buffer 1 over the poles.
!           Compute total number of fields in semi-lagrangian buffers.

!**   Interface.
!     ----------
!        *CALL* *SUSLB*

!        Explicit arguments : none
!        --------------------

!        Implicit arguments :
!        --------------------
!        modules PTRSLB1, PTRSLB2

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------
!      None
!      Is called by SUSC2B.

!     Reference.
!     ----------

!     Author.
!     -------
!      Mats Hamrud *ECMWF*
!      Original : 92-08-01

! Modifications
! -------------
!   N. Wedi and K. Yessad (Jan 2008): different dev for NH model and PC scheme
!   F. Vana  13-Jan-2009 : MSLB2KAPPA5 for TL/AD of SLHD
!   F. Vana  16-Oct-2009 : option NSPLTHOI
!   K. Yessad (Nov 2009): prune lpc_old.
!   F. Vana  22-Feb-2011: Horiz. turbulence, phys. tend. diffusion, N[X]LAG=4
!   G. Mozdzynski (Aug 2011): support higher order interpolation
!   F. Vana  25-Sep-2013: Split MSLB2KAPPA to MSLB2KAPPA and MSLBKAPPAT
!   T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!   S. Malardel Nov 2013: pointers STDDISx coef. for COMAD SL weight computation
!   K. Yessad (July 2014): Move some variables.
!   K. Yessad (Dec 2016): Prune obsolete options.
!   K. Yessad (June 2017): Introduce NHQE model.
!   F. Vana    21-Nov-2017: Fixed parities for LLSLIN_SI quantities.
!   F. Vana    21-Nov-2017: Option LSLDP_CURV + bugfix
!   K. Yessad (Feb 2018): remove deep-layer formulations.
!   F. Vana July 2018: RK4 scheme for trajectory research.
! End Modifications
!------------------------------------------------------------------------------

USE TYPE_MODEL , ONLY : MODEL
USE YOMDIMV  , ONLY : TDIMV
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMLUN   , ONLY : NULOUT
USE YOMMP0   , ONLY : NPRINTLEV
USE YOMCT0   , ONLY : LSLAG, LSPRT, LNHDYN, LALLOPR, NCONF, NUNDEFLD, LNHEE

USE YOMCVER  , ONLY : LVERTFE, LVFE_GW

!   ------------------------------------------------------------

IMPLICIT NONE

TYPE(TDIMV), INTENT(IN) :: YDDIMV
 TYPE(MODEL),INTENT(INOUT):: YDMODEL

INTEGER(KIND=JPIM) :: IGFL9,IGFLF9
INTEGER(KIND=JPIM) :: IGFL9_SPL,IGFLF9_SPL
INTEGER(KIND=JPIM) :: IGFLP9
INTEGER(KIND=JPIM) :: IC0
INTEGER(KIND=JPIM) :: IC9,IC9_SI,IC9_NL
INTEGER(KIND=JPIM) :: IOFF
INTEGER(KIND=JPIM) :: IFLDSLB2
INTEGER(KIND=JPIM) :: ILVP2
INTEGER(KIND=JPIM) :: IPD0
INTEGER(KIND=JPIM) :: IPD9,IPD9_SI
INTEGER(KIND=JPIM) :: IPTR2
INTEGER(KIND=JPIM) :: IPTX
INTEGER(KIND=JPIM) :: ISP9
INTEGER(KIND=JPIM) :: IT0
INTEGER(KIND=JPIM) :: IT9,IT9_SI,ITF9
INTEGER(KIND=JPIM) :: ITP9
INTEGER(KIND=JPIM) :: IU
INTEGER(KIND=JPIM) :: IU0
INTEGER(KIND=JPIM) :: IU9,IU9_SI,IUF9
INTEGER(KIND=JPIM) :: IUP9
INTEGER(KIND=JPIM) :: IUR0,IUR00
INTEGER(KIND=JPIM) :: IUR9
INTEGER(KIND=JPIM) :: IV0
INTEGER(KIND=JPIM) :: IV9,IV9_SI,IVF9
INTEGER(KIND=JPIM) :: IVD0
INTEGER(KIND=JPIM) :: IVD9,IVD9_SI,IVDF9
INTEGER(KIND=JPIM) :: INHX9
INTEGER(KIND=JPIM) :: IVP9
INTEGER(KIND=JPIM) :: IVR0,IVR00
INTEGER(KIND=JPIM) :: IVR9
INTEGER(KIND=JPIM) :: IZR0,IZR00
INTEGER(KIND=JPIM) :: IWR0,IWR00
INTEGER(KIND=JPIM) :: IWRA
INTEGER(KIND=JPIM) :: JF
INTEGER(KIND=JPIM) :: IU95
INTEGER(KIND=JPIM) :: IV95
INTEGER(KIND=JPIM) :: IT95
INTEGER(KIND=JPIM) :: IGFL95
INTEGER(KIND=JPIM) :: IC95
INTEGER(KIND=JPIM) :: IU05
INTEGER(KIND=JPIM) :: IV05
INTEGER(KIND=JPIM) :: IT05
INTEGER(KIND=JPIM) :: IC05
INTEGER(KIND=JPIM) :: ISP95
INTEGER(KIND=JPIM) :: IUR05,IUR005
INTEGER(KIND=JPIM) :: IVR05,IVR005
INTEGER(KIND=JPIM) :: IZR05,IZR005
INTEGER(KIND=JPIM) :: IWR05,IWR005
INTEGER(KIND=JPIM) :: IUR95
INTEGER(KIND=JPIM) :: IVR95
INTEGER(KIND=JPIM) :: INUMGFL
INTEGER(KIND=JPIM) :: INUMGFL_SPL
INTEGER(KIND=JPIM) :: INUMGFL_SL1
INTEGER(KIND=JPIM) :: JGFL,IGFL
INTEGER(KIND=JPIM) :: IDBBC9
INTEGER(KIND=JPIM) :: IDPHI9

LOGICAL :: LL2TLFF1
LOGICAL :: LLP
LOGICAL :: LLSLAD, LLSETTLS
LOGICAL :: LLSLIN_SI

REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUSLB',0,ZHOOK_HANDLE)
ASSOCIATE(YGFL=>YDMODEL%YRML_GCONF%YGFL,YDDYN=>YDMODEL%YRML_DYN%YRDYN,YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY, &
 & YDPTRSLB1=>YDMODEL%YRML_DYN%YRPTRSLB1,YDPTRSLB15=>YDMODEL%YRML_DYN%YRPTRSLB15,YDPTRSLB2=>YDMODEL%YRML_DYN%YRPTRSLB2)
ASSOCIATE(NUMFLDS=>YGFL%NUMFLDS, NUMFLDS_SL1=>YGFL%NUMFLDS_SL1, &
 & NUMFLDS_SPL=>YGFL%NUMFLDS_SPL, NFLEVG=>YDDIMV%NFLEVG, &
 & L2TLFF=>YDDYN%L2TLFF, &
 & LADVF=>YDDYN%LADVF, &
 & LSPLTHOIGFL=>YDDYN%LSPLTHOIGFL, LSVTSM=>YDDYN%LSVTSM, &
 & NSPDLAG=>YDDYN%NSPDLAG, NSPLTHOI=>YDDYN%NSPLTHOI, NSVDLAG=>YDDYN%NSVDLAG, &
 & NTLAG=>YDDYN%NTLAG, NVLAG=>YDDYN%NVLAG, NWLAG=>YDDYN%NWLAG, &
 & LSLHDHEAT=>YDDYN%LSLHDHEAT, LAGPHY=>YDEPHY%LAGPHY, LEPHYS=>YDEPHY%LEPHYS,LSLPHY=>YDEPHY%LSLPHY, &
 & LRSIDDH=>YDMODEL%YRML_DIAG%YRLDDH%LRSIDDH, MSLB1C0=>YDPTRSLB1%MSLB1C0, &
 & MSLB1C9=>YDPTRSLB1%MSLB1C9, &
 & MSLB1C9_SI=>YDPTRSLB1%MSLB1C9_SI, MSLB1DBBC9=>YDPTRSLB1%MSLB1DBBC9, &
 & MSLB1DPHI9=>YDPTRSLB1%MSLB1DPHI9, MSLB1GFL9=>YDPTRSLB1%MSLB1GFL9, &
 & MSLB1GFLF9=>YDPTRSLB1%MSLB1GFLF9, MSLB1GFLP9=>YDPTRSLB1%MSLB1GFLP9, &
 & MSLB1GFLSP9=>YDPTRSLB1%MSLB1GFLSP9, MSLB1GFLSPF9=>YDPTRSLB1%MSLB1GFLSPF9, &
 & MSLB1GWS9=>YDPTRSLB1%MSLB1GWS9, MSLB1NHX9=>YDPTRSLB1%MSLB1NHX9, &
 & MSLB1PD0=>YDPTRSLB1%MSLB1PD0, MSLB1PD9=>YDPTRSLB1%MSLB1PD9, &
 & MSLB1PD9_SI=>YDPTRSLB1%MSLB1PD9_SI, &
 & MSLB1SP9=>YDPTRSLB1%MSLB1SP9, MSLB1T0=>YDPTRSLB1%MSLB1T0, &
 & MSLB1T9=>YDPTRSLB1%MSLB1T9, MSLB1T9_SI=>YDPTRSLB1%MSLB1T9_SI, &
 & MSLB1TF9=>YDPTRSLB1%MSLB1TF9, MSLB1TP9=>YDPTRSLB1%MSLB1TP9, &
 & MSLB1U0=>YDPTRSLB1%MSLB1U0, MSLB1U9=>YDPTRSLB1%MSLB1U9, &
 & MSLB1U9_SI=>YDPTRSLB1%MSLB1U9_SI, MSLB1UF9=>YDPTRSLB1%MSLB1UF9, &
 & MSLB1UP9=>YDPTRSLB1%MSLB1UP9, MSLB1UR0=>YDPTRSLB1%MSLB1UR0, &
 & MSLB1UR9=>YDPTRSLB1%MSLB1UR9, MSLB1V0=>YDPTRSLB1%MSLB1V0, &
 & MSLB1V9=>YDPTRSLB1%MSLB1V9, MSLB1V9_SI=>YDPTRSLB1%MSLB1V9_SI, &
 & MSLB1VD0=>YDPTRSLB1%MSLB1VD0, MSLB1VD9=>YDPTRSLB1%MSLB1VD9, &
 & MSLB1VD9_SI=>YDPTRSLB1%MSLB1VD9_SI, MSLB1VDF9=>YDPTRSLB1%MSLB1VDF9, &
 & MSLB1VF9=>YDPTRSLB1%MSLB1VF9, MSLB1VP9=>YDPTRSLB1%MSLB1VP9, &
 & MSLB1VR0=>YDPTRSLB1%MSLB1VR0, MSLB1VR9=>YDPTRSLB1%MSLB1VR9, &
 & MSLB1ZR0=>YDPTRSLB1%MSLB1ZR0, MSLB1ZR05=>YDPTRSLB15%MSLB1ZR05, &
 & MSLB1WR0=>YDPTRSLB1%MSLB1WR0, MSLB1WRA=>YDPTRSLB1%MSLB1WRA, &
 & MSLB1UR00=>YDPTRSLB1%MSLB1UR00, MSLB1VR00=>YDPTRSLB1%MSLB1VR00,&
 & MSLB1ZR00=>YDPTRSLB1%MSLB1ZR00, MSLB1WR00=>YDPTRSLB1%MSLB1WR00,&
 & MSLBUF1=>YDPTRSLB1%MSLBUF1, NFLDSLB1=>YDPTRSLB1%NFLDSLB1, &
 & MSLB1C05=>YDPTRSLB15%MSLB1C05, MSLB1C95=>YDPTRSLB15%MSLB1C95, &
 & MSLB1GFL95=>YDPTRSLB15%MSLB1GFL95, MSLB1SP95=>YDPTRSLB15%MSLB1SP95, &
 & MSLB1T05=>YDPTRSLB15%MSLB1T05, MSLB1T95=>YDPTRSLB15%MSLB1T95, &
 & MSLB1U05=>YDPTRSLB15%MSLB1U05, MSLB1U95=>YDPTRSLB15%MSLB1U95, &
 & MSLB1UR05=>YDPTRSLB15%MSLB1UR05, MSLB1UR95=>YDPTRSLB15%MSLB1UR95, &
 & MSLB1V05=>YDPTRSLB15%MSLB1V05, MSLB1V95=>YDPTRSLB15%MSLB1V95, &
 & MSLB1VR05=>YDPTRSLB15%MSLB1VR05, MSLB1VR95=>YDPTRSLB15%MSLB1VR95, &
 & MSLB1WR05=>YDPTRSLB15%MSLB1WR05, MSLBUF15=>YDPTRSLB15%MSLBUF15, &
 & MSLB1UR005=>YDPTRSLB15%MSLB1UR005, MSLB1VR005=>YDPTRSLB15%MSLB1VR005,&
 & MSLB1ZR005=>YDPTRSLB15%MSLB1ZR005, MSLB1WR005=>YDPTRSLB15%MSLB1WR005,&
 & NFLDSLB15=>YDPTRSLB15%NFLDSLB15, MSLB2DBBC1=>YDPTRSLB2%MSLB2DBBC1, &
 & MSLB2DPHI1=>YDPTRSLB2%MSLB2DPHI1, &
 & MSLB2GDW=>YDPTRSLB2%MSLB2GDW, MSLB2GWF=>YDPTRSLB2%MSLB2GWF, &
 & MSLB2GWS=>YDPTRSLB2%MSLB2GWS, MSLB2KAPPA=>YDPTRSLB2%MSLB2KAPPA, &
 & MSLB2KAPPA5=>YDPTRSLB2%MSLB2KAPPA5, MSLB2KAPPAH=>YDPTRSLB2%MSLB2KAPPAH, &
 & MSLB2KAPPAM=>YDPTRSLB2%MSLB2KAPPAM, MSLB2KAPPAT=>YDPTRSLB2%MSLB2KAPPAT, &
 & MSLB2KAPPAT5=>YDPTRSLB2%MSLB2KAPPAT5, MSLB2PDSI=>YDPTRSLB2%MSLB2PDSI, &
 & MSLB2Q15=>YDPTRSLB2%MSLB2Q15, MSLB2SPSI=>YDPTRSLB2%MSLB2SPSI, &
 & MSLB2STDDISU=>YDPTRSLB2%MSLB2STDDISU, MSLB2STDDISV=>YDPTRSLB2%MSLB2STDDISV, &
 & MSLB2STDDISW=>YDPTRSLB2%MSLB2STDDISW, MSLB2T15=>YDPTRSLB2%MSLB2T15, &
 & MSLB2TSI=>YDPTRSLB2%MSLB2TSI, MSLB2U15=>YDPTRSLB2%MSLB2U15, &
 & MSLB2URL=>YDPTRSLB2%MSLB2URL, MSLB2URL5=>YDPTRSLB2%MSLB2URL5, &
 & MSLB2USI=>YDPTRSLB2%MSLB2USI, MSLB2V15=>YDPTRSLB2%MSLB2V15, &
 & MSLB2VDSI=>YDPTRSLB2%MSLB2VDSI, &
 & MSLB2VRL=>YDPTRSLB2%MSLB2VRL, MSLB2VRL5=>YDPTRSLB2%MSLB2VRL5, &
 & MSLB2VSI=>YDPTRSLB2%MSLB2VSI, MSLB2VVEL=>YDPTRSLB2%MSLB2VVEL, &
 & MSLB2WRL=>YDPTRSLB2%MSLB2WRL, MSLB2WRL5=>YDPTRSLB2%MSLB2WRL5, &
 & MSLBUF2=>YDPTRSLB2%MSLBUF2, NFLDSLB2=>YDPTRSLB2%NFLDSLB2, &
 & LMPHYS=>YDMODEL%YRML_PHY_MF%YRPHY%LMPHYS, &
 & MSLB1PD9_NL=>YDPTRSLB1%MSLB1PD9_NL, MSLB1T9_NL=>YDPTRSLB1%MSLB1T9_NL, &
 & MSLB1U9_NL=>YDPTRSLB1%MSLB1U9_NL, MSLB1V9_NL=>YDPTRSLB1%MSLB1V9_NL, &
 & MSLB1VD9_NL=>YDPTRSLB1%MSLB1VD9_NL, MSLB1C9_NL=>YDPTRSLB1%MSLB1C9_NL, &
 & LSLDP_RK=>YDDYN%LSLDP_RK, LSLDP_CURV=>YDDYN%LSLDP_CURV)
!     ------------------------------------------------------------------

!*    1.   COMPUTE RELATIVE POINTERS FOR SEMILAGRANGIAN BUFFERS.
!          -----------------------------------------------------

LL2TLFF1=L2TLFF.AND.(LMPHYS.OR.LEPHYS).AND.(.NOT.LAGPHY)
LLP = NPRINTLEV > 1.OR. LALLOPR
LLSLIN_SI= LRSIDDH .OR. YDMODEL%YRML_DYN%YRDYNA%LSLINL
LLSETTLS = YDMODEL%YRML_DYN%YRDYNA%LSETTLS .AND. YDMODEL%YRML_DYN%YRDYNA%LPC_CHEAP
IU = NULOUT
IF (LLP) WRITE (IU,*)
IF (LLP) WRITE (IU,'(A,/)') ' SUSLB PRINTOUT : POINTERS,NFIELDS'
INUMGFL=NUMFLDS
INUMGFL_SL1=NUMFLDS_SL1
INUMGFL_SPL=NUMFLDS_SPL

!*       1.1  COMPUTE POINTERS FOR SLBUF2

!*       1.1.1 Preliminary set pointers to value for undefined/unused fields

! * MSLBUF2
MSLBUF2  =NUNDEFLD
! * MSLB2[X]1
MSLB2DBBC1=NUNDEFLD
MSLB2DPHI1=NUNDEFLD
! * MSLB2[X]SI
MSLB2USI =NUNDEFLD
MSLB2VSI =NUNDEFLD
MSLB2TSI =NUNDEFLD
MSLB2PDSI=NUNDEFLD
MSLB2VDSI=NUNDEFLD
MSLB2SPSI=NUNDEFLD
! * MSLB2VVEL
MSLB2VVEL=NUNDEFLD
! * MSLB2[X]RL
MSLB2URL =NUNDEFLD
MSLB2VRL =NUNDEFLD
MSLB2WRL =NUNDEFLD
! * MSLB2[X]15
MSLB2U15 =NUNDEFLD
MSLB2V15 =NUNDEFLD
MSLB2T15 =NUNDEFLD
MSLB2Q15 =NUNDEFLD
! * MSLB2[X]RL5
MSLB2URL5=NUNDEFLD
MSLB2VRL5=NUNDEFLD
MSLB2WRL5=NUNDEFLD
! * MSLB2KAPPA
MSLB2KAPPA=NUNDEFLD
MSLB2KAPPA5=NUNDEFLD
MSLB2KAPPAT=NUNDEFLD
MSLB2KAPPAT5=NUNDEFLD
MSLB2KAPPAM=NUNDEFLD
MSLB2KAPPAH=NUNDEFLD
! * MSLB2GWF, MSLB2GDW, MSLB2GWS
MSLB2GWF=NUNDEFLD
MSLB2GDW=NUNDEFLD
MSLB2GWS=NUNDEFLD
! * MSLB2STDDISx
MSLB2STDDISU=NUNDEFLD
MSLB2STDDISV=NUNDEFLD
MSLB2STDDISW=NUNDEFLD

!*       1.1.2 Definite computation of pointers.

IF (LLP) WRITE (IU,'(A)') ' 1) BUFFER SLBUF2:'
IPTX    = 1

! * MSLBUF2

MSLBUF2 = IPTX

! * MSLB2DBBC1 and MSLB2DPHI1

IF(YDMODEL%YRML_DYN%YRDYNA%LRDBBC) THEN
  MSLB2DBBC1= IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2DBBC1= ',IPTX,1
  IPTX  = IPTX + 1
  MSLB2DPHI1= IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2DPHI1= ',IPTX,1
  IPTX  = IPTX + 1
ENDIF

! * MSLB2[X]SI

MSLB2USI  = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2USI  = ',IPTX,NFLEVG
IPTX      = IPTX + NFLEVG
MSLB2VSI  = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2VSI  = ',IPTX,NFLEVG
IPTX      = IPTX + NFLEVG
MSLB2TSI  = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2TSI  = ',IPTX,NFLEVG
IPTX      = IPTX + NFLEVG
IF(LNHEE) THEN
  MSLB2PDSI = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2PDSI = ',IPTX,NFLEVG
  IPTX      = IPTX + NFLEVG
ENDIF
IF(LNHDYN) THEN
  MSLB2VDSI = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2VDSI = ',IPTX,NFLEVG
  IPTX      = IPTX + NFLEVG
ENDIF
MSLB2SPSI = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2SPSI = ',IPTX,1
IPTX      = IPTX + 1

! * MSLB2VVEL

MSLB2VVEL = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2VVEL = ',IPTX,NFLEVG
IPTX      = IPTX + NFLEVG

! * MSLB2[X]RL

MSLB2URL = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2URL  = ',IPTX,NFLEVG
IPTX    = IPTX + NFLEVG
MSLB2VRL = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2VRL  = ',IPTX,NFLEVG
IPTX    = IPTX + NFLEVG
MSLB2WRL = IPTX
IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2WRL  = ',IPTX,NFLEVG
IPTX    = IPTX + NFLEVG

IPTR2     = IPTX

LLSLAD=LSLAG.AND.(NCONF == 131.OR.NCONF == 401.OR.NCONF == 501&
 & .OR.NCONF == 601.OR.NCONF == 801)  

! * MSLB2[X]15 then MSLB2[X]RL5

IF (LLSLAD) THEN
  IPTX    = IPTR2
  IF (LADVF.AND.L2TLFF) THEN
    MSLB2U15 = IPTX
    IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2U15  = ',IPTX,NFLEVG
    IPTX    = IPTX + NFLEVG
    MSLB2V15 = IPTX
    IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2V15  = ',IPTX,NFLEVG
    IPTX    = IPTX + NFLEVG
  ENDIF
  MSLB2URL5 = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2URL5 = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  MSLB2VRL5 = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2VRL5 = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  MSLB2WRL5 = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2WRL5 = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2   = IPTX

ENDIF

IF (LSPRT.AND.(NCONF == 131.OR.NCONF == 401.&
 &OR.NCONF == 501.OR.NCONF == 601.OR.NCONF == 801)) THEN
  IPTX    = IPTR2
  MSLB2T15 = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2T15  = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  MSLB2Q15 = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2Q15  = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2   = IPTX
ENDIF

! * MSLB2KAPPA...

IF (YDMODEL%YRML_DYN%YRDYNA%LSLHD) THEN
  MSLB2KAPPA = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPA= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
  IF (LSLHDHEAT) THEN
    MSLB2KAPPAT = IPTX
    IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPAT= ',IPTX,NFLEVG
    IPTX    = IPTX + NFLEVG
    IPTR2    = MAX(IPTX,IPTR2)
  ENDIF
  IF (LLSLAD) THEN
    MSLB2KAPPA5 = IPTX
    IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPA5= ',IPTX,NFLEVG
    IPTX    = IPTX + NFLEVG
    IPTR2    = MAX(IPTX,IPTR2)
    IF (LSLHDHEAT) THEN
      MSLB2KAPPAT5 = IPTX
      IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPAT5= ',IPTX,NFLEVG
      IPTX    = IPTX + NFLEVG
      IPTR2    = MAX(IPTX,IPTR2)
    ENDIF
  ENDIF
ENDIF
IF (YDMODEL%YRML_DYN%YRDYNA%L3DTURB) THEN
  MSLB2KAPPAM = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPAM= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
  MSLB2KAPPAH = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2KAPPAH= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
ENDIF

! * MSLB2GWF, MSLB2GDW, MSLB2GWS

IF (LNHDYN.AND.YDMODEL%YRML_DYN%YRDYNA%LGWADV.AND.LVERTFE.AND.LVFE_GW) THEN
  MSLB2GWF = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2GWF  = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  MSLB2GDW = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2GDW  = ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  MSLB2GWS = IPTX
  IF (LLP) WRITE(IU,'(1X,A12,2I4)') 'MSLB2GWS  = ',IPTX,1
  IPTX    = IPTX + 1
  IPTR2   = MAX(IPTX,IPTR2)
ENDIF

! * MSLB2STDDISU/V/W if LCOMAD for COMAD SL weights

IF (YDMODEL%YRML_DYN%YRDYNA%LCOMAD) THEN
  MSLB2STDDISU = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB2STDDISU= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
  MSLB2STDDISV = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB2STDDISV= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
  MSLB2STDDISW = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB2STDDISW= ',IPTX,NFLEVG
  IPTX    = IPTX + NFLEVG
  IPTR2    = MAX(IPTX,IPTR2)
ENDIF

IFLDSLB2 = IPTR2-1
NFLDSLB2 = IFLDSLB2

!*       1.2  COMPUTE POINTERS FOR SLBUF1

!*       1.2.1 Preliminary set pointers to value for undefined/unused fields

! * MSLBUF1
MSLBUF1  =NUNDEFLD

! * MSLB1[X]9 (for high order interpolations)
MSLB1UR9 =NUNDEFLD
MSLB1VR9 =NUNDEFLD
MSLB1U9  =NUNDEFLD
MSLB1V9  =NUNDEFLD
MSLB1T9  =NUNDEFLD
MSLB1GFL9=NUNDEFLD
MSLB1GFLSP9 =NUNDEFLD
MSLB1SP9 =NUNDEFLD
MSLB1C9  =NUNDEFLD
MSLB1VD9 =NUNDEFLD
MSLB1NHX9=NUNDEFLD
MSLB1PD9 =NUNDEFLD

! * MSLB1[X]9_SI (additional separate linear terms)
MSLB1U9_SI=NUNDEFLD
MSLB1V9_SI=NUNDEFLD
MSLB1T9_SI=NUNDEFLD
MSLB1C9_SI=NUNDEFLD
MSLB1VD9_SI=NUNDEFLD
MSLB1PD9_SI=NUNDEFLD

! * MSLB1[X]9_NL (additional term for NL term in LSETTLS/LPC_CHEAP scheme)
MSLB1U9_NL=NUNDEFLD
MSLB1V9_NL=NUNDEFLD
MSLB1T9_NL=NUNDEFLD
MSLB1C9_NL=NUNDEFLD
MSLB1VD9_NL=NUNDEFLD
MSLB1PD9_NL=NUNDEFLD

! * MSLB1[X]F9 - second part of the quantity (typically physics) to be
!                interplated by high order operator 
MSLB1UF9  =NUNDEFLD
MSLB1VF9  =NUNDEFLD
MSLB1TF9  =NUNDEFLD
MSLB1GFLF9=NUNDEFLD
MSLB1VDF9 =NUNDEFLD
MSLB1GFLSPF9 =NUNDEFLD

! * Diagnostic BBC: MSLB1DBBC9, MSLB1DPHI9 and MSLB1GWS9.
MSLB1DBBC9=NUNDEFLD
MSLB1DPHI9=NUNDEFLD
MSLB1GWS9 =NUNDEFLD

! * MSLB1[X]0 (for linear interpolations)
MSLB1UR0 =NUNDEFLD
MSLB1VR0 =NUNDEFLD
MSLB1ZR0 =NUNDEFLD
MSLB1WR0 =NUNDEFLD
MSLB1UR00=NUNDEFLD
MSLB1VR00=NUNDEFLD
MSLB1ZR00=NUNDEFLD
MSLB1WR00=NUNDEFLD
MSLB1WRA =NUNDEFLD
MSLB1U0  =NUNDEFLD
MSLB1V0  =NUNDEFLD
MSLB1T0  =NUNDEFLD
MSLB1C0  =NUNDEFLD
MSLB1VD0 =NUNDEFLD
MSLB1PD0 =NUNDEFLD

! * MSLB1[X]P9
MSLB1UP9 =NUNDEFLD
MSLB1VP9 =NUNDEFLD
MSLB1TP9 =NUNDEFLD
MSLB1GFLP9 =NUNDEFLD

! * MSLB1[X]95 and MSLB1[X]05
MSLBUF15 =NUNDEFLD
MSLB1U95 =NUNDEFLD
MSLB1V95 =NUNDEFLD
MSLB1T95 =NUNDEFLD
MSLB1GFL95 =NUNDEFLD
MSLB1U05 =NUNDEFLD
MSLB1V05 =NUNDEFLD
MSLB1T05 =NUNDEFLD
MSLB1C95 =NUNDEFLD
MSLB1C05 =NUNDEFLD
MSLB1SP95=NUNDEFLD
MSLB1UR05=NUNDEFLD
MSLB1VR05=NUNDEFLD
MSLB1ZR05=NUNDEFLD
MSLB1WR05=NUNDEFLD
MSLB1UR005=NUNDEFLD
MSLB1VR005=NUNDEFLD
MSLB1ZR005=NUNDEFLD
MSLB1WR005=NUNDEFLD
MSLB1UR95=NUNDEFLD
MSLB1VR95=NUNDEFLD

!*       1.2.2 Definite computation of pointers.

IF(LSLAG) THEN

  IF (LLP) WRITE (IU,*)
  IF (LLP) WRITE (IU,'(A)') ' 2) BUFFER SLBUF1:'

  ILVP2   = NFLEVG+2
  IPTX    = 1
  MSLBUF1 = IPTX

!        * Buffers for high order interpolations:
!          MSLB1[X]9

  IF (LL2TLFF1) THEN
    MSLB1UR9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1VR9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  MSLB1U9  = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1U9     = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  MSLB1V9  = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1V9     = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  MSLB1T9  = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1T9     = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  IF(INUMGFL_SL1> 0) THEN
    MSLB1GFL9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFL9   = ',IPTX,ILVP2*INUMGFL_SL1
    IPTX     = IPTX + ILVP2*INUMGFL_SL1
    IF(INUMGFL_SPL > 0) THEN
      MSLB1GFLSP9 = IPTX
      IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFLSP9     = ' &
       & ,IPTX,ILVP2*INUMGFL_SPL
      IPTX    = IPTX + ILVP2*INUMGFL_SPL
    ENDIF
  ENDIF
  MSLB1SP9 = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1SP9    = ',IPTX,1
  IPTX     = IPTX + 1
  IF (NVLAG == 2) THEN
    MSLB1C9  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1C9     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHDYN) THEN
    MSLB1VD9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VD9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF (YDMODEL%YRML_DYN%YRDYNA%NVDVAR == 4 .OR. YDMODEL%YRML_DYN%YRDYNA%NVDVAR==5) THEN
      MSLB1NHX9 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1NHX9   = ',IPTX,ILVP2
      IPTX   = IPTX + ILVP2
    ENDIF
  ENDIF
  IF (LNHEE) THEN
    MSLB1PD9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1PD9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF

  !      * Buffers for high order interpolation when it is split
  !        into two parts
  IF (NSPLTHOI /= 0) THEN
    MSLB1UF9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UF9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1VF9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VF9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1TF9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1TF9    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF(INUMGFL_SL1> 0) THEN
      MSLB1GFLF9 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFLF9  = ',IPTX,ILVP2*INUMGFL_SL1
      IPTX     = IPTX + ILVP2*INUMGFL_SL1
      IF(INUMGFL_SPL > 0) THEN
        MSLB1GFLSPF9 = IPTX
        IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFLSPF9    = ' &
         & ,IPTX,ILVP2*INUMGFL_SPL
        IPTX    = IPTX + ILVP2*INUMGFL_SPL
      ENDIF
    ENDIF
    IF (LNHDYN) THEN
      MSLB1VDF9 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VDF9   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF
  ELSE
    ! * In case the LPHYLIN=.T. atribute is activated for any GFL,
    !   the MSLB1GFLF9 (and MSLB1GFLSPF9) buffers are used for this purpose.
    !   (The slots for LPHYLIN=.F. variables will remain empty.
    !    This can be still optimized one day...)
    IF((INUMGFL_SL1> 0).AND.LSPLTHOIGFL) THEN
      MSLB1GFLF9 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFLF9  = ',IPTX,ILVP2*INUMGFL_SL1
      IPTX     = IPTX + ILVP2*INUMGFL_SL1
      IF(INUMGFL_SPL > 0) THEN
        MSLB1GFLSPF9 = IPTX
        IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFLSPF9    = ' &
         & ,IPTX,ILVP2*INUMGFL_SPL
        IPTX    = IPTX + ILVP2*INUMGFL_SPL
      ENDIF
    ENDIF
  ENDIF

!        * Buffers for high order interpolations,
!          for terms specific to diagnostic BBC in NH

  IF (YDMODEL%YRML_DYN%YRDYNA%LRDBBC .AND. (NSVDLAG == 2)) THEN
    MSLB1DBBC9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1DBBC9   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1DPHI9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1DPHI9   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (YDMODEL%YRML_DYN%YRDYNA%LRDBBC) THEN
    MSLB1GWS9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1GWS9    = ',IPTX,1
    IPTX     = IPTX + 1
  ENDIF

!        * Buffers for separate linear terms
!          (linear interpolations if N(X)LAG=3 or 4). 

  IF (LLSLIN_SI .AND. NWLAG>=3) THEN
    MSLB1U9_SI = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1U9_SI  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1V9_SI = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1V9_SI  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LLSLIN_SI .AND. NTLAG>=3) THEN
    MSLB1T9_SI = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1T9_SI  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHDYN .AND. LLSLIN_SI .AND. NSVDLAG>=3) THEN
    MSLB1VD9_SI = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VD9_SI = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHEE .AND. LLSLIN_SI .AND. NSPDLAG==3) THEN
    MSLB1PD9_SI = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1PD9_SI = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF

!        * Buffers for separate non-linear terms LSETTLS/LPC_CHEAP
!          (linear interpolations if N(X)LAG=3 or 4). 

  IF (LLSETTLS .AND. NWLAG==3) THEN
    MSLB1U9_NL = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1U9_NL  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1V9_NL = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1V9_NL  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LLSETTLS .AND. NTLAG==3) THEN
    MSLB1T9_NL = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1T9_NL  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHDYN .AND. LLSETTLS .AND. NSVDLAG==3) THEN
    MSLB1VD9_NL = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VD9_NL = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHEE .AND. LLSETTLS .AND. NSPDLAG==3) THEN
    MSLB1PD9_NL = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1PD9_NL = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LLSETTLS .AND. NVLAG == 3) THEN
    MSLB1C9_NL= IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1C9_NL  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF

!        * Buffers for linear interpolations,
!          for terms specific to diagnostic BBC in NH

  IF (YDMODEL%YRML_DYN%YRDYNA%LRDBBC .AND. (NSVDLAG >= 3)) THEN
    MSLB1DBBC9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1DBBC9   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1DPHI9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1DPHI9   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF

!        * Buffers for (not necessarily) linear interpolations,
!          for terms other than split physics.
!          MSLB1[X]0 and MSLB1C9

  MSLB1UR0 = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR0    = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  MSLB1VR0 = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR0    = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  IF (LSLDP_CURV) THEN
    MSLB1ZR0 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1ZR0    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF(LSVTSM) THEN
    MSLB1WRA = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1WRA    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  MSLB1WR0 = IPTX
  IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1WR0    = ',IPTX,ILVP2
  IPTX     = IPTX + ILVP2
  IF (LSLDP_RK) THEN
    MSLB1UR00 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR00   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1VR00 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR00   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF (LSLDP_CURV) THEN
      MSLB1ZR00 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1ZR00   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF
    MSLB1WR00 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1WR00   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (NWLAG >= 3) THEN
    MSLB1U0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1U0     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1V0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1V0     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (NTLAG >= 3) THEN
    MSLB1T0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1T0     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (NVLAG == 3) THEN
    MSLB1C9  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1C9     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (NVLAG == 3 .AND. YDMODEL%YRML_DYN%YRDYNA%LSLINLC2) THEN
    MSLB1C9_SI= IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1C9_SI  = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  ! For improved vectorization in SL TL/AD applications:
  IF (LLSLAD) THEN
    MSLB1C0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1C0     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHDYN.AND.(NSVDLAG >= 3)) THEN
    MSLB1VD0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1VD0    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF
  IF (LNHEE.AND.(NSPDLAG == 3)) THEN
    MSLB1PD0  = IPTX
    IF (LLP)WRITE(IU,'(1X,A14,2I4)') 'MSLB1PD0    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
  ENDIF

!        * Buffers for linear interpolations,
!          for terms used in split physics.
!          MSLB1[X]P9

  IF( LSLPHY ) THEN
    MSLB1UP9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UP9     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1VP9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VP9     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1TP9  = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1TP9     = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1GFLP9 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,3I4)') 'MSLB1GFLP9   = ',IPTX,ILVP2,INUMGFL
    DO JGFL=1,INUMGFL
      ! Numbering of fields, and allocation in 'SLBUF1', should be consistent
      !  with what is done in GPADDSLPHY and LARCINB.
      ! For the time being all GFL fields are counted and allocated, even those 
      !  which are useless (i.e. contain 0 or not advected).
      ! Memory optimisation when some GFL are passive ones or not advected,
      !  must be studied later. 
      IPTX    = IPTX + ILVP2
    ENDDO
  ENDIF

  NFLDSLB1 = IPTX - 1

!        * Buffers used in tangent-linear/adjoint of semi-Lagrangian.
!          MSLB1[X]95 and MSLB1[X]05

  IF (LLSLAD) THEN
    IF (LLP) WRITE (IU,*)
    IF (LLP) WRITE (IU,'(A)') ' 2) BUFFER SLBUF15:'
    IPTX    = 1
    MSLBUF15 = IPTX

    MSLB1U95 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1U95    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1V95 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1V95    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1T95 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1T95    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF(INUMGFL_SL1 > 0) THEN
      MSLB1GFL95 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1GFL95    = ',IPTX,ILVP2*INUMGFL_SL1
      IPTX     = IPTX + ILVP2*INUMGFL_SL1
    ENDIF
    MSLB1U05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1U05    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1V05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1V05    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1T05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1T05    = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF (NVLAG == 2.OR.NVLAG == 3) THEN
      MSLB1C95 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1C95    = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
      MSLB1C05 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1C05    = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF
    MSLB1SP95 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1SP95   = ',IPTX,1
    IPTX     = IPTX + 1
    MSLB1UR05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR05   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    MSLB1VR05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR05   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF (LSLDP_CURV) THEN
      MSLB1ZR05 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1ZR05   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF
    MSLB1WR05 = IPTX
    IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1WR05   = ',IPTX,ILVP2
    IPTX     = IPTX + ILVP2
    IF (LSLDP_RK) THEN
      MSLB1UR005 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR005   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
      MSLB1VR005 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR005   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
      IF (LSLDP_CURV) THEN
        MSLB1ZR005 = IPTX
        IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1ZR005   = ',IPTX,ILVP2
        IPTX     = IPTX + ILVP2
      ENDIF
      MSLB1WR005 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1WR005   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF
    IF (LL2TLFF1) THEN
      MSLB1UR95 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1UR95   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
      MSLB1VR95 = IPTX
      IF (LLP) WRITE(IU,'(1X,A14,2I4)') 'MSLB1VR95   = ',IPTX,ILVP2
      IPTX     = IPTX + ILVP2
    ENDIF

    NFLDSLB15 = IPTX - 1
  ELSE
    NFLDSLB15 = 1
  ENDIF
   
ELSE

  NFLDSLB1 = 1
  NFLDSLB15 = 1

ENDIF

IF (LLP) WRITE (IU,*)

!     ------------------------------------------------------------------

!*    2.   COMPUTE PARITIES FOR EXTENSION OVER POLE.
!          -----------------------------------------

IF (LSLAG) THEN
  IF(ALLOCATED(YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1)) DEALLOCATE(YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1) ! To make SUSLB recallable
  ALLOCATE(YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(NFLDSLB1))
  IF(LLP)WRITE(IU,9) 'RPARSL1  ',SIZE(YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1),SHAPE(YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1)
  IF(ALLOCATED(YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15)) DEALLOCATE(YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15) ! To make SUSLB recallable
  ALLOCATE(YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(NFLDSLB15))
  IF(LLP)WRITE(IU,9) 'RPARSL15 ',SIZE(YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15),SHAPE(YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15)
ENDIF
9 FORMAT(1X,'ARRAY ',A10,' ALLOCATED ',8I8)

IF (LSLAG) THEN
!        2.11 Computation of RPARSL1
!             for U,V,T,gfl,dcha,Pcha equations terms.

  DO JF=1,ILVP2

    ! * MSLB1[X]9
    IF (LL2TLFF1) THEN
      IUR9=MSLB1UR9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUR9)=-1.0_JPRB
      IVR9=MSLB1VR9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVR9)=-1.0_JPRB
    ENDIF
    IU9=MSLB1U9-1
    YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IU9)=-1.0_JPRB
    IV9=MSLB1V9-1
    YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IV9)=-1.0_JPRB
    IT9=MSLB1T9-1
    YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IT9)=1.0_JPRB
    DO JGFL=1,INUMGFL_SL1
      IGFL9=MSLB1GFL9+(JGFL-1)*ILVP2-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IGFL9)=1.0_JPRB
    ENDDO
    DO JGFL=1,INUMGFL_SPL
      IGFL9_SPL=MSLB1GFLSP9+(JGFL-1)*ILVP2-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IGFL9_SPL)=1.0_JPRB
    ENDDO
    IF (LNHDYN) THEN
      IVD9=MSLB1VD9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVD9)=1.0_JPRB
      IF (YDMODEL%YRML_DYN%YRDYNA%NVDVAR == 4 .OR. YDMODEL%YRML_DYN%YRDYNA%NVDVAR==5) THEN
        INHX9=MSLB1NHX9-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+INHX9)=1.0_JPRB
      ENDIF
    ENDIF
    IF (LNHEE) THEN
      IPD9=MSLB1PD9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IPD9)=1.0_JPRB
    ENDIF

    ! * MSLB[x]F9
    IF (NSPLTHOI /= 0) THEN
      IUF9=MSLB1UF9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUF9)=-1.0_JPRB
      IVF9=MSLB1VF9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVF9)=-1.0_JPRB
      ITF9=MSLB1TF9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+ITF9)=1.0_JPRB
      DO JGFL=1,INUMGFL_SL1
        IGFLF9=MSLB1GFLF9+(JGFL-1)*ILVP2-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IGFLF9)=1.0_JPRB
      ENDDO
      DO JGFL=1,INUMGFL_SPL
        IGFLF9_SPL=MSLB1GFLSPF9+(JGFL-1)*ILVP2-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IGFLF9_SPL)=1.0_JPRB
      ENDDO
      IF (LNHDYN) THEN
        IVDF9=MSLB1VDF9-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVDF9)=1.0_JPRB
      ENDIF
    ENDIF

    ! * Diagnostic BBC: MSLB1DBBC9, MSLB1DPHI9 and MSLB1GWS9.
    IF (YDMODEL%YRML_DYN%YRDYNA%LRDBBC) THEN
      IDBBC9=MSLB1DBBC9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IDBBC9)=1.0_JPRB
      IDPHI9=MSLB1DPHI9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IDPHI9)=1.0_JPRB
    ENDIF

    ! * MSLB1[X]9_SI
    IF (LLSLIN_SI .AND. NWLAG>=3) THEN
      IU9_SI=MSLB1U9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IU9_SI)=-1.0_JPRB
      IV9_SI=MSLB1V9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IV9_SI)=-1.0_JPRB
    ENDIF
    IF (LLSLIN_SI .AND. NTLAG>=3) THEN
      IT9_SI=MSLB1T9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IT9_SI)=1.0_JPRB
    ENDIF
    IF (LNHDYN .AND. LLSLIN_SI .AND. NSVDLAG>=3) THEN
      IVD9_SI=MSLB1VD9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVD9_SI)=1.0_JPRB
    ENDIF
    IF (LNHEE .AND. LLSLIN_SI .AND. NSPDLAG==3) THEN
      IPD9_SI=MSLB1PD9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IPD9_SI)=1.0_JPRB
    ENDIF

    ! * MSLB1[X]9_NL
    IF (LLSETTLS .AND. NWLAG==3) THEN
      IOFF=MSLB1U9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IOFF)=-1.0_JPRB
      IOFF=MSLB1V9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IOFF)=-1.0_JPRB
    ENDIF
    IF (LLSETTLS .AND. NTLAG==3) THEN
      IOFF=MSLB1T9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IOFF)=-1.0_JPRB
    ENDIF
    IF (LNHDYN .AND. LLSETTLS .AND. NSVDLAG==3) THEN
      IOFF=MSLB1VD9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IOFF)=-1.0_JPRB
    ENDIF
    IF (LNHEE .AND. LLSETTLS .AND. NSPDLAG==3) THEN
      IOFF=MSLB1PD9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IOFF)=-1.0_JPRB
    ENDIF

    ! * MSLB1[X]0
    IUR0=MSLB1UR0-1
    IF(LSLDP_CURV) THEN
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUR0)=1.0_JPRB
    ELSE
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUR0)=-1.0_JPRB
    ENDIF
    IVR0=MSLB1VR0-1
    IF(LSLDP_CURV) THEN
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVR0)=1.0_JPRB
    ELSE
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVR0)=-1.0_JPRB
    ENDIF
    IF(LSLDP_CURV) THEN
      IZR0=MSLB1ZR0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IZR0)= 1.0_JPRB
    ENDIF
    IF(LSVTSM) THEN
      IWRA=MSLB1WRA-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IWRA)=1.0_JPRB
    ENDIF
    IWR0=MSLB1WR0-1
    YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IWR0)=1.0_JPRB
    IF (LSLDP_RK) THEN
      IUR00=MSLB1UR00-1
      IF (LSLDP_CURV) THEN
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUR00)= 1.0_JPRB
      ELSE
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUR00)=-1.0_JPRB
      ENDIF
      IVR00=MSLB1VR00-1
      IF (LSLDP_CURV) THEN
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVR00)=1.0_JPRB
      ELSE
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVR00)=-1.0_JPRB
      ENDIF
      IF (LSLDP_CURV) THEN
        IZR00=MSLB1ZR00-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IZR00)= 1.0_JPRB
      ENDIF
      IWR00=MSLB1WR00-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IWR00)=1.0_JPRB
    ENDIF
    IF (NWLAG >= 3) THEN
      IU0=MSLB1U0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IU0)=-1.0_JPRB
      IV0=MSLB1V0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IV0)=-1.0_JPRB
    ENDIF
    IF (NTLAG >= 3) THEN
      IT0=MSLB1T0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IT0)=1.0_JPRB
    ENDIF
    IF (LNHDYN.AND.(NSVDLAG >= 3)) THEN
      IVD0=MSLB1VD0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVD0)=1.0_JPRB
    ENDIF
    IF (LNHEE.AND.(NSPDLAG == 3)) THEN
      IPD0=MSLB1PD0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IPD0)=1.0_JPRB
    ENDIF

    ! * MSLB1[X]P9
    IF( LSLPHY ) THEN
      IUP9=MSLB1UP9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IUP9)=-1.0_JPRB
      IVP9=MSLB1VP9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IVP9)=-1.0_JPRB
      ITP9=MSLB1TP9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+ITP9)=1.0_JPRB
      ! Same remark as above (see MSLB1GFLP9 setting).
      IGFL=0
      DO JGFL=1,INUMGFL
        IGFL=IGFL+1
        IGFLP9=MSLB1GFLP9+(IGFL-1)*ILVP2-1
        YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IGFLP9)=1.0_JPRB
      ENDDO
    ENDIF
  ENDDO

!        2.12 Computation of RPARSL1 for 3D continuity equation terms.

  ! * MSLB1C9 and MSLB1C0
  DO JF=1,ILVP2
    IF (NVLAG == 2.OR.NVLAG == 3) THEN
      IC9=MSLB1C9-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IC9)=1.0_JPRB
    ENDIF
    IF (NVLAG == 3.AND.YDMODEL%YRML_DYN%YRDYNA%LSLINLC2) THEN
      IC9_SI=MSLB1C9_SI-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IC9_SI)=1.0_JPRB
    ENDIF
    IF (NVLAG == 3.AND. LLSETTLS) THEN
      IC9_NL=MSLB1C9_NL-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IC9_NL)=1.0_JPRB
    ENDIF
    IF (LLSLAD) THEN
      IC0=MSLB1C0-1
      YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(JF+IC0)=1.0_JPRB
    ENDIF
  ENDDO

!        2.13 Computation of RPARSL1 for 2D continuity equation terms.

  ! * MSLB1SP9
  ISP9=MSLB1SP9-1
  YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(1+ISP9)=1.0_JPRB

  ! diagnostic LRDBBC
  IF (YDMODEL%YRML_DYN%YRDYNA%LRDBBC) YDMODEL%YRML_DYN%YRPTRSLB1%RPARSL1(MSLB1GWS9)=1.0_JPRB

!        2.14 Computation of RPARSL1 for TL/AD of semi-Lagrangian.

  ! * MSLB1[X]95 and MSLB1[X]05
  IF (LLSLAD) THEN

    DO JF=1,ILVP2
      IU95=MSLB1U95-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IU95)=-1.0_JPRB
      IV95=MSLB1V95-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IV95)=-1.0_JPRB
      IT95=MSLB1T95-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IT95)=1.0_JPRB
      DO JGFL=1,INUMGFL_SL1
        IGFL95=MSLB1GFL95+(JGFL-1)*ILVP2-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IGFL95)=1.0_JPRB
      ENDDO
      IU05=MSLB1U05-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IU05)=-1.0_JPRB
      IV05=MSLB1V05-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IV05)=-1.0_JPRB
      IT05=MSLB1T05-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IT05)=1.0_JPRB
      IF (NVLAG == 2.OR.NVLAG == 3) THEN
        IC05=MSLB1C05-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IC05)=1.0_JPRB
        IC95=MSLB1C95-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IC95)=1.0_JPRB
      ENDIF
      IUR05=MSLB1UR05-1
      IF (LSLDP_CURV) THEN
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IUR05)=1.0_JPRB
      ELSE
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IUR05)=-1.0_JPRB
      ENDIF
      IVR05=MSLB1VR05-1
      IF (LSLDP_CURV) THEN
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IVR05)=1.0_JPRB
      ELSE
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IVR05)=-1.0_JPRB
      ENDIF
      IF (LSLDP_CURV) THEN
        IZR05=MSLB1ZR05-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IZR05)=1.0_JPRB
      ENDIF
      IWR05=MSLB1WR05-1
      YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IWR05)=1.0_JPRB
      IF (LSLDP_RK) THEN
        IUR005=MSLB1UR005-1
        IF (LSLDP_CURV) THEN
          YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IUR005)= 1.0_JPRB
        ELSE
          YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IUR005)=-1.0_JPRB
        ENDIF
        IVR005=MSLB1VR005-1
        IF (LSLDP_CURV) THEN
          YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IVR005)=1.0_JPRB
        ELSE
          YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IVR005)=-1.0_JPRB
        ENDIF
        IF (LSLDP_CURV) THEN
          IZR005=MSLB1ZR005-1
          YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IZR005)=1.0_JPRB
        ENDIF
        IWR005=MSLB1WR005-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IWR005)=1.0_JPRB
      ENDIF
      IF (LL2TLFF1) THEN
        IUR95=MSLB1UR95-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IUR95)=-1.0_JPRB
        IVR95=MSLB1VR95-1
        YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(JF+IVR95)=-1.0_JPRB
      ENDIF
    ENDDO

    ISP95=MSLB1SP95-1
    YDMODEL%YRML_DYN%YRPTRSLB15%RPARSL15(1+ISP95)=1.0_JPRB
  ENDIF


ENDIF

!     ------------------------------------------------------------------


END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUSLB',1,ZHOOK_HANDLE)
END SUBROUTINE SUSLB
