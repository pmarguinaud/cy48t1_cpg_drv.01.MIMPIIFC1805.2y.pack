#ifdef RS6K
@PROCESS NOOPTIMIZE
#endif
SUBROUTINE SUVERTFE3(YDGEOMETRY)

!**** *SUVERTFE3*  - Setup VERTical Finite Element scheme based on
!                    cubic (3) elements

!     Purpose.
!     --------
!           Compute matrix for the vertical integrations based on
!           cubic B-spline finite elements

!**   Interface.
!     ----------

!     *CALL* SUVERTFE3
!        Explicit arguments :
!        --------------------
!        None

!        Implicit arguments :
!        --------------------

!     Method.
!     -------

!     Externals.
!     ----------
!        MINV_8

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        Agathe Untch ECMWF

!     Modifications.
!     --------------
!        Original : 2000-09
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        K. Yessad (Sep 2008): use VFE_ETAH and VFE_ETAF
!        T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!        F. Vana    17-Dec-2015   Remains always double precision.
!        P.Smolikova (Sep 2020): VFE pruning.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCVER  , ONLY : LVFE_VERBOSE
USE YOMLUN   , ONLY : NULOUT   
USE YOMMP0   , ONLY : MYPROC

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT) :: YDGEOMETRY
INTEGER(KIND=JPIM) :: IDIM,JR,JC,JK,JKR,JKC, J,I_K,JD
REAL(KIND=JPRD) :: ZDELTA(-3:YDGEOMETRY%YRDIMV%NFLEVG+4),ZD1,ZD2,ZD3,ZD4,ZD0,ZD0H
REAL(KIND=JPRD) :: ZDEL, ZDEL2, ZDEL3, ZDNH
REAL(KIND=JPRD) :: XA,XB,XC,XD,YA,YB,YC,YD, XX
REAL(KIND=JPRD) :: ZW0,ZW1,ZW2, ZCONST
REAL(KIND=JPRD) :: ZAAA2,ZBBB2,ZCCC2,ZDDD2,ZAAA3,ZBBB3,ZCCC3,ZDDD3
REAL(KIND=JPRD) :: ZDDD1,ZDDD4
REAL(KIND=JPRD) :: ZFAA(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2),ZFBB(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZFCC(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2),ZFDD(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZIAA(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2),ZIBB(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZICC(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2),ZIDD(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZALPHA(4,4), ZBETA(2,2), ZRHS(2), ZBMAX, ZDETB
REAL(KIND=JPRD) :: ZHH(4,4)
REAL(KIND=JPRD) :: ZINTR(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2),ZINTC(4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZG(4,4,-1:YDGEOMETRY%YRDIMV%NFLEVG+2)
REAL(KIND=JPRD) :: ZSF(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3), &
 & ZSFINV(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZSI(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZA (YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3), &
 & ZAINV (YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZB (YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZPROD(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZINTE(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3), &
 & ZDERI(YDGEOMETRY%YRDIMV%NFLEVG+3,YDGEOMETRY%YRDIMV%NFLEVG+3)
REAL(KIND=JPRD) :: ZWORK(2*(YDGEOMETRY%YRDIMV%NFLEVG+3)), ZEPS, ZDET
REAL(KIND=JPRD) :: ZCCA, ZCCB

REAL(KIND=JPRD) :: FPOLY, FDERI, FINT, FHH, FGG
REAL(KIND=JPRB) :: ZHOOK_HANDLE

#include "minv_8.h"

FPOLY(XX,XA,XB,XC,XD)=XA+XX*(XB+XX*(XC+XD*XX))
FDERI(XX,XA,XB,XC,XD)=XB+XX*(2._JPRD*XC+3._JPRD*XD*XX)
FINT(XX,XA,XB,XC,XD)=XX*(XA+XX*(XB/2._JPRD+XX*(XC/3._JPRD+XD/4._JPRD*XX)))

FHH(XX,XA,XB,XC,XD,YA,YB,YC,YD)=&
 & XX*(  XA*YA&
 & + XX*( (XA*YB+XB*YA)/2._JPRD&
 & + XX*( (XA*YC+XB*YB+XC*YA)/3._JPRD&
 & + XX*( (XA*YD+XB*YC+XC*YB+XD*YA)/4._JPRD&
 & + XX*( (      XB*YD+XC*YC+XD*YB)/5._JPRD&
 & + XX*( (            XC*YD+XD*YC)/6._JPRD&
 & +XD*YD/7._JPRD*XX))))) )  

FGG(XX,XA,XB,XC,XD,YA,YB,YC,YD)=&
 & XX**2*(  XA*YA/2._JPRD&
 & + XX*( (XA*YB/2._JPRD+XB*YA)/3._JPRD&
 & + XX*( (XA*YC/3._JPRD+XB*YB/2._JPRD+XC*YA)/4._JPRD&
 & + XX*( (XA*YD/4._JPRD+XB*YC/3._JPRD+XC*YB/2._JPRD+XD*YA)/5._JPRD&
 & + XX*( (XB*YD/4._JPRD+XC*YC/3._JPRD+XD*YB/2._JPRD)/6._JPRD&
 & + XX*( (XC*YD/4._JPRD+XD*YC/3._JPRD)/7._JPRD&
 & + XD*YD/4._JPRD/8._JPRD*XX))))) )  

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUVERTFE3',0,ZHOOK_HANDLE)
ASSOCIATE(YDVETA=>YDGEOMETRY%YRVETA,YDVFE=>YDGEOMETRY%YRVFE)
ASSOCIATE(NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG)!     ------------------------------------------------------------------

IDIM=NFLEVG+3
ZEPS=100.0_JPRD*TINY(1.0_JPRD)

! compute distance between nodes (full eta levels)

ZDELTA( 0)=2._JPRD*(YDVETA%VFE_ETAF(1)-YDVETA%VFE_ETAH(0))
ZDELTA(-3)=ZDELTA( 0)
ZDELTA(-2)=ZDELTA( 0)
ZDELTA(-1)=ZDELTA( 0)
DO JK=1,NFLEVG-1
  ZDELTA(JK)=YDVETA%VFE_ETAF(JK+1)-YDVETA%VFE_ETAF(JK)
ENDDO
ZDELTA(NFLEVG  )=2._JPRD*(YDVETA%VFE_ETAH(NFLEVG) -YDVETA%VFE_ETAF(NFLEVG))
ZDELTA(NFLEVG+1)=ZDELTA(NFLEVG  )
ZDELTA(NFLEVG+2)=ZDELTA(NFLEVG  )
ZDELTA(NFLEVG+3)=ZDELTA(NFLEVG  )
ZDELTA(NFLEVG+4)=ZDELTA(NFLEVG  )

! compute basis functions    

ZFAA(:,:)=0.0_JPRD
ZFBB(:,:)=0.0_JPRD
ZFCC(:,:)=0.0_JPRD
ZFDD(:,:)=0.0_JPRD
ZIAA(:,:)=0.0_JPRD
ZIBB(:,:)=0.0_JPRD
ZICC(:,:)=0.0_JPRD
ZIDD(:,:)=0.0_JPRD

ZBMAX=4._JPRD/6._JPRD 
  
DO JK=-1,NFLEVG+2
  ZD1=ZDELTA(JK-2)
  ZD2=ZDELTA(JK-1)
  ZD3=ZDELTA(JK  )
  ZD4=ZDELTA(JK+1)

! matrix alpha
  ZALPHA(1,1)=0.0_JPRD     
  ZALPHA(1,2)=0.0_JPRD      
  ZALPHA(1,3)=ZD3**3      
  ZALPHA(1,4)=ZD4**3+3._JPRD*ZD3*ZD4**2+3._JPRD*ZD3**2*ZD4  
  ZALPHA(2,1)=ZD1**2+2._JPRD*ZD1*ZD2
  ZALPHA(2,2)=ZD2**2
  ZALPHA(2,3)=ZD3**2
  ZALPHA(2,4)=ZD4**2+2._JPRD*ZD3*ZD4
  ZALPHA(3,1)= ZD1
  ZALPHA(3,2)= ZD2
  ZALPHA(3,3)=-ZD3
  ZALPHA(3,4)=-ZD4
  ZALPHA(4,1)=ZD1**3+3._JPRD*ZD1**2*ZD2+3._JPRD*ZD1*ZD2**2
  ZALPHA(4,2)=ZD2**3
  ZALPHA(4,3)=0.0_JPRD
  ZALPHA(4,4)=0.0_JPRD

! matrix beta
  ZBETA(1,1)=ZALPHA(2,2)-ZALPHA(2,1)*ZALPHA(4,2)/ZALPHA(4,1)
  ZBETA(1,2)=ZALPHA(2,3)-ZALPHA(2,4)*ZALPHA(1,3)/ZALPHA(1,4)
  ZBETA(2,1)=ZALPHA(3,2)-ZALPHA(3,1)*ZALPHA(4,2)/ZALPHA(4,1)
  ZBETA(2,2)=ZALPHA(3,3)-ZALPHA(3,4)*ZALPHA(1,3)/ZALPHA(1,4)

! right-hand side vector
  ZRHS(1)=-ZBMAX*(ZALPHA(2,1)/ZALPHA(4,1)+ZALPHA(2,4)/ZALPHA(1,4))
  ZRHS(2)=-ZBMAX*(ZALPHA(3,1)/ZALPHA(4,1)+ZALPHA(3,4)/ZALPHA(1,4))

! determinant of beta 
  ZDETB=ZBETA(1,1)*ZBETA(2,2)-ZBETA(1,2)*ZBETA(2,1)

! coefficients 
  ZDDD2=(ZRHS(1)*ZBETA(2,2)-ZRHS(2)*ZBETA(1,2))/ZDETB
  ZDDD3=(ZRHS(2)*ZBETA(1,1)-ZRHS(1)*ZBETA(2,1))/ZDETB
  ZDDD1=(ZBMAX - ZALPHA(4,2)*ZDDD2)/ZALPHA(4,1)    
  ZDDD4=(ZBMAX - ZALPHA(1,3)*ZDDD3)/ZALPHA(1,4) 

  ZAAA2=        ZD1**3*ZDDD1
  ZBBB2=3._JPRD*ZD1**2*ZDDD1
  ZCCC2=3._JPRD*ZD1   *ZDDD1

  ZAAA3=        ZD4**3*ZDDD4
  ZBBB3=3._JPRD*ZD4**2*ZDDD4
  ZCCC3=3._JPRD*ZD4   *ZDDD4

! coefficients of explicit cubic polynomials in each interval
  
  ZFAA(1,JK)=0.0_JPRD
  ZFBB(1,JK)=0.0_JPRD
  ZFCC(1,JK)=0.0_JPRD
  ZFDD(1,JK)=ZDDD1
 
  ZFAA(2,JK)=ZAAA2
  ZFBB(2,JK)=ZBBB2
  ZFCC(2,JK)=ZCCC2
  ZFDD(2,JK)=ZDDD2  
  
  ZDEL=ZDELTA(JK)
  ZDEL2=ZDEL*ZDEL
  ZDEL3=ZDEL2*ZDEL
  ZFAA(3,JK)=ZAAA3+ZBBB3*ZDEL+        ZCCC3*ZDEL2        +ZDDD3*ZDEL3
  ZFBB(3,JK)=     -ZBBB3     -2._JPRD*ZCCC3*ZDEL -3._JPRD*ZDDD3*ZDEL2
  ZFCC(3,JK)=                         ZCCC3      +3._JPRD*ZDDD3*ZDEL
  ZFDD(3,JK)=                                            -ZDDD3

  ZDEL=ZDELTA(JK+1)
  ZDEL2=ZDEL*ZDEL
  ZDEL3=ZDEL2*ZDEL
  ZFAA(4,JK)=         ZDDD4*ZDEL3
  ZFBB(4,JK)=-3._JPRD*ZDDD4*ZDEL2
  ZFCC(4,JK)= 3._JPRD*ZDDD4*ZDEL
  ZFDD(4,JK)=        -ZDDD4 

  DO J=1,4
    ZIAA(J,JK)=ZFAA(J,JK)
    ZIBB(J,JK)=ZFBB(J,JK)
    ZICC(J,JK)=ZFCC(J,JK)
    ZIDD(J,JK)=ZFDD(J,JK)
  ENDDO

ENDDO !end loop over nodes

!compute the linear combination weights for the modified basis functions of
! the integral basis
! (B_HAT(0)=ZW0*B(-1)+B(0), B_HAT(1)=ZW1*B(-1)+B(1),B_HAT(1)=ZW2*B(-1)+B(2)

ZD0 =ZDELTA(0)
ZD0H=ZD0*0.5_JPRD
ZW0=-(ZFAA(3,0)+ZFBB(3,0)*ZD0H+ZFCC(3,0)*ZD0H**2+ZFDD(3,0)*ZD0H**3) &
 & /(ZFAA(4,-1)+ZFBB(4,-1)*ZD0H+ZFCC(4,-1)*ZD0H**2+ZFDD(4,-1)*ZD0H**3)  
ZW1=-(ZFAA(2,1)+ZFBB(2,1)*ZD0H+ZFCC(2,1)*ZD0H**2+ZFDD(2,1)*ZD0H**3) &
 & /(ZFAA(4,-1)+ZFBB(4,-1)*ZD0H+ZFCC(4,-1)*ZD0H**2+ZFDD(4,-1)*ZD0H**3)  
ZW2=-(ZFAA(1,2)+ZFBB(1,2)*ZD0H+ZFCC(1,2)*ZD0H**2+ZFDD(1,2)*ZD0H**3) &
 & /(ZFAA(4,-1)+ZFBB(4,-1)*ZD0H+ZFCC(4,-1)*ZD0H**2+ZFDD(4,-1)*ZD0H**3)  

DO J=1,4
  ZIAA(J,-1)=0.0_JPRD
  ZIBB(J,-1)=0.0_JPRD
  ZICC(J,-1)=0.0_JPRD
  ZIDD(J,-1)=0.0_JPRD
ENDDO
DO J=1,2
  ZIAA(J,0)=0.0_JPRD
  ZIBB(J,0)=0.0_JPRD
  ZICC(J,0)=0.0_JPRD
  ZIDD(J,0)=0.0_JPRD
ENDDO
ZIAA(3,0)=ZW0*ZFAA(4,-1)+ZFAA(3,0)
ZIBB(3,0)=ZW0*ZFBB(4,-1)+ZFBB(3,0)
ZICC(3,0)=ZW0*ZFCC(4,-1)+ZFCC(3,0)
ZIDD(3,0)=ZW0*ZFDD(4,-1)+ZFDD(3,0)

ZIAA(1,1)=0.0_JPRD
ZIBB(1,1)=0.0_JPRD
ZICC(1,1)=0.0_JPRD
ZIDD(1,1)=0.0_JPRD
ZIAA(2,1)=ZW1*ZFAA(4,-1)+ZFAA(2,1)
ZIBB(2,1)=ZW1*ZFBB(4,-1)+ZFBB(2,1)
ZICC(2,1)=ZW1*ZFCC(4,-1)+ZFCC(2,1)
ZIDD(2,1)=ZW1*ZFDD(4,-1)+ZFDD(2,1)

ZIAA(1,2)=ZW2*ZFAA(4,-1)+ZFAA(1,2)
ZIBB(1,2)=ZW2*ZFBB(4,-1)+ZFBB(1,2)
ZICC(1,2)=ZW2*ZFCC(4,-1)+ZFCC(1,2)
ZIDD(1,2)=ZW2*ZFDD(4,-1)+ZFDD(1,2)

! modify basis functions of the function basis

! boundary condition:
! (function to be integrated)=const. in interval [x_N,x_N+1]

ZCONST=FPOLY(0.0_JPRD&
 & ,ZFAA(4,NFLEVG-1),ZFBB(4,NFLEVG-1),ZFCC(4,NFLEVG-1),ZFDD(4,NFLEVG-1))  
ZFAA(4,NFLEVG-1)=ZCONST
ZFBB(4,NFLEVG-1)=0.0_JPRD
ZFCC(4,NFLEVG-1)=0.0_JPRD
ZFDD(4,NFLEVG-1)=0.0_JPRD
ZCONST=FPOLY(0.0_JPRD&
 & ,ZFAA(3,NFLEVG  ),ZFBB(3,NFLEVG  ),ZFCC(3,NFLEVG  ),ZFDD(3,NFLEVG  ))  
DO J=3,4
  ZFAA(J,NFLEVG)=ZCONST
  ZFBB(J,NFLEVG)=0.0_JPRD
  ZFCC(J,NFLEVG)=0.0_JPRD
  ZFDD(J,NFLEVG)=0.0_JPRD
ENDDO
ZCONST=FPOLY(0.0_JPRD&
 & ,ZFAA(2,NFLEVG+1),ZFBB(2,NFLEVG+1),ZFCC(2,NFLEVG+1),ZFDD(2,NFLEVG+1))  
DO J=2,4
  ZFAA(J,NFLEVG+1)=ZCONST
  ZFBB(J,NFLEVG+1)=0.0_JPRD
  ZFCC(J,NFLEVG+1)=0.0_JPRD
  ZFDD(J,NFLEVG+1)=0.0_JPRD
ENDDO
DO J=1,4
  ZFAA(J,NFLEVG+2)=0.0_JPRD
  ZFBB(J,NFLEVG+2)=0.0_JPRD
  ZFCC(J,NFLEVG+2)=0.0_JPRD
  ZFDD(J,NFLEVG+2)=0.0_JPRD
ENDDO

! COMPUTE MATRIX SF ( SF(I,J)=BASISFUNC_J(X_I) )
! ---------------------------------------------

ZSF(:,:)=0.0_JPRD
DO JR=2,IDIM-1
  ZD2=ZDELTA(JR-1)
  DO JC=1,IDIM
    JKC=JC-2
    IF(JC == JR-1) ZSF(JR,JC)=&
     & FPOLY(0.0_JPRD,ZFAA(4,JKC),ZFBB(4,JKC),ZFCC(4,JKC),ZFDD(4,JKC))  
    IF(JC == JR  ) ZSF(JR,JC)=&
     & FPOLY(0.0_JPRD,ZFAA(3,JKC),ZFBB(3,JKC),ZFCC(3,JKC),ZFDD(3,JKC))  
    IF(JC == JR+1) ZSF(JR,JC)=&
     & FPOLY(0.0_JPRD,ZFAA(2,JKC),ZFBB(2,JKC),ZFCC(2,JKC),ZFDD(2,JKC))  
  ENDDO
ENDDO

ZSF(1,1)=FDERI(0.0_JPRD,ZFAA(4,-1),ZFBB(4,-1),ZFCC(4,-1),ZFDD(4,-1))
ZSF(1,2)=FDERI(0.0_JPRD,ZFAA(3, 0),ZFBB(3, 0),ZFCC(3, 0),ZFDD(3, 0))
ZSF(1,3)=FDERI(0.0_JPRD,ZFAA(2, 1),ZFBB(2, 1),ZFCC(2, 1),ZFDD(2, 1))

ZSF(IDIM,IDIM-2)=FDERI(ZDELTA(NFLEVG-1) &
 & ,ZFAA(3,NFLEVG-1),ZFBB(3,NFLEVG-1),ZFCC(3,NFLEVG-1),ZFDD(3,NFLEVG-1))  
ZSF(IDIM,IDIM-1)=FDERI(ZDELTA(NFLEVG-1) &
 & ,ZFAA(2,NFLEVG  ),ZFBB(2,NFLEVG  ),ZFCC(2,NFLEVG  ),ZFDD(2,NFLEVG  ))  
ZSF(IDIM,IDIM  )=FDERI(ZDELTA(NFLEVG-1) &
 & ,ZFAA(1,NFLEVG+1),ZFBB(1,NFLEVG+1),ZFCC(1,NFLEVG+1),ZFDD(1,NFLEVG+1))  

! invert SF: 
ZDET=0.0_JPRD
ZSFINV(:,:)=ZSF(:,:)
CALL MINV_8(ZSFINV,IDIM,IDIM,ZWORK,ZDET,ZEPS,0,1)

!  print S and S^-1
IF(MYPROC == 1.AND.LVFE_VERBOSE) THEN
  WRITE(NULOUT,*) ' DETERMINANT OF SF =',ZDET
  WRITE(NULOUT,*) ' MATRIX SF :'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.3)') (ZSF(JR,JC),JC=1,IDIM)
  ENDDO
  WRITE(NULOUT,*) ' INVERSE OF SF :'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.3)') (ZSFINV(JR,JC),JC=1,IDIM)
  ENDDO
ENDIF

! COMPUTE MATRIX SI ( SF(I,J)=BASISI_J(X_I) )
! -------------------------------------------

ZSI(:,:)=0.0_JPRD
DO JR=2,IDIM-2
  DO JC=1,IDIM
    JKC=JC-1
    IF(JC == JR-1) ZSI(JR,JC)=&
     & FPOLY(0.0_JPRD,ZIAA(4,JKC),ZIBB(4,JKC),ZICC(4,JKC),ZIDD(4,JKC))  
    IF(JC == JR  ) ZSI(JR,JC)=&
     & FPOLY(0.0_JPRD,ZIAA(3,JKC),ZIBB(3,JKC),ZICC(3,JKC),ZIDD(3,JKC))  
    IF(JC == JR+1) ZSI(JR,JC)=&
     & FPOLY(0.0_JPRD,ZIAA(2,JKC),ZIBB(2,JKC),ZICC(2,JKC),ZIDD(2,JKC))  
  ENDDO
ENDDO

ZSI(1,1)=FDERI(ZD0H,ZIAA(3,0),ZIBB(3,0),ZICC(3,0),ZIDD(3,0))
ZSI(1,2)=FDERI(ZD0H,ZIAA(2,1),ZIBB(2,1),ZICC(2,1),ZIDD(2,1))
ZSI(1,3)=FDERI(ZD0H,ZIAA(1,2),ZIBB(1,2),ZICC(1,2),ZIDD(1,2))

! evaluate integral in the middle of the last interval not at the end!
ZDNH=ZDELTA(NFLEVG)*0.5_JPRD
ZSI(IDIM-1,IDIM-3)=FPOLY(ZDNH &
 & ,ZIAA(4,NFLEVG-1),ZIBB(4,NFLEVG-1),ZICC(4,NFLEVG-1),ZIDD(4,NFLEVG-1))  
ZSI(IDIM-1,IDIM-2)=FPOLY(ZDNH &
 & ,ZIAA(3,NFLEVG  ),ZIBB(3,NFLEVG  ),ZICC(3,NFLEVG  ),ZIDD(3,NFLEVG  ))  
ZSI(IDIM-1,IDIM-1)=FPOLY(ZDNH &
 & ,ZIAA(2,NFLEVG+1),ZIBB(2,NFLEVG+1),ZICC(2,NFLEVG+1),ZIDD(2,NFLEVG+1))  
ZSI(IDIM-1,IDIM  )=FPOLY(ZDNH &
 & ,ZIAA(1,NFLEVG+2),ZIBB(1,NFLEVG+2),ZICC(1,NFLEVG+2),ZIDD(1,NFLEVG+2))  

ZSI(IDIM,IDIM-2)=FDERI(0.0_JPRD &
 & ,ZIAA(4,NFLEVG  ),ZIBB(4,NFLEVG  ),ZICC(4,NFLEVG  ),ZIDD(4,NFLEVG  ))  
ZSI(IDIM,IDIM-1)=FDERI(0.0_JPRD &
 & ,ZIAA(3,NFLEVG+1),ZIBB(3,NFLEVG+1),ZICC(3,NFLEVG+1),ZIDD(3,NFLEVG+1))  
ZSI(IDIM,IDIM  )=FDERI(0.0_JPRD &
 & ,ZIAA(2,NFLEVG+2),ZIBB(2,NFLEVG+2),ZICC(2,NFLEVG+2),ZIDD(2,NFLEVG+2))  

!----------------------------------------------------------------------
! Before computing the matrices A and B set the basis function outside
! the integration domain to zero.

!  basis for function
DO J=1,3
  ZFAA(J,-1)=0.0_JPRD
  ZFBB(J,-1)=0.0_JPRD
  ZFCC(J,-1)=0.0_JPRD
  ZFDD(J,-1)=0.0_JPRD
ENDDO
DO J=1,2
  ZFAA(J,0)=0.0_JPRD
  ZFBB(J,0)=0.0_JPRD
  ZFCC(J,0)=0.0_JPRD
  ZFDD(J,0)=0.0_JPRD
ENDDO
ZFAA(1,1)=0.0_JPRD
ZFBB(1,1)=0.0_JPRD
ZFCC(1,1)=0.0_JPRD
ZFDD(1,1)=0.0_JPRD

DO J=1,4
  ZFAA(J,NFLEVG+2)=0.0_JPRD
  ZFBB(J,NFLEVG+2)=0.0_JPRD
  ZFCC(J,NFLEVG+2)=0.0_JPRD
  ZFDD(J,NFLEVG+2)=0.0_JPRD
ENDDO
DO J=3,4
  ZFAA(J,NFLEVG+1)=0.0_JPRD
  ZFBB(J,NFLEVG+1)=0.0_JPRD
  ZFCC(J,NFLEVG+1)=0.0_JPRD
  ZFDD(J,NFLEVG+1)=0.0_JPRD
ENDDO
ZFAA(4,NFLEVG)=0.0_JPRD
ZFBB(4,NFLEVG)=0.0_JPRD
ZFCC(4,NFLEVG)=0.0_JPRD
ZFDD(4,NFLEVG)=0.0_JPRD

!  basis for integral
DO J=1,4
  ZIAA(J,-1)=0.0_JPRD
  ZIBB(J,-1)=0.0_JPRD
  ZICC(J,-1)=0.0_JPRD
  ZIDD(J,-1)=0.0_JPRD
ENDDO
DO J=1,2
  ZIAA(J,0)=0.0_JPRD
  ZIBB(J,0)=0.0_JPRD
  ZICC(J,0)=0.0_JPRD
  ZIDD(J,0)=0.0_JPRD
ENDDO

ZIAA(1,1)=0.0_JPRD
ZIBB(1,1)=0.0_JPRD
ZICC(1,1)=0.0_JPRD
ZIDD(1,1)=0.0_JPRD

DO J=2,4
  ZIAA(J,NFLEVG+2)=0.0_JPRD
  ZIBB(J,NFLEVG+2)=0.0_JPRD
  ZICC(J,NFLEVG+2)=0.0_JPRD
  ZIDD(J,NFLEVG+2)=0.0_JPRD
ENDDO
DO J=3,4
  ZIAA(J,NFLEVG+1)=0.0_JPRD
  ZIBB(J,NFLEVG+1)=0.0_JPRD
  ZICC(J,NFLEVG+1)=0.0_JPRD
  ZIDD(J,NFLEVG+1)=0.0_JPRD
ENDDO
ZIAA(4,NFLEVG)=0.0_JPRD
ZIBB(4,NFLEVG)=0.0_JPRD
ZICC(4,NFLEVG)=0.0_JPRD
ZIDD(4,NFLEVG)=0.0_JPRD

! COMPUTE MATRIX A
! ----------------

ZA(:,:)=0.0_JPRD

DO JR=1,IDIM
  JKR=JR-1
  ZHH(:,:)=0.0_JPRD
  DO J=1,4
    JD=JKR-3+J
    DO I_K=1,J
      JKC=JKR+J-I_K
      IF((JKR == 0 .AND. J == 3) .OR. &
         & (JKR == 1 .AND. J == 2) .OR. &
         & (JKR == 2 .AND. J == 1) ) THEN  
! integration only over half the interval  [JK=0, JK=1]
        ZHH(J,I_K)=FHH(ZD0 ,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR) &
         & ,ZIAA(I_K,JKC),ZIBB(I_K,JKC),ZICC(I_K,JKC),ZIDD(I_K,JKC))&
         & -FHH(ZD0H,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR) &
         & ,ZIAA(I_K,JKC),ZIBB(I_K,JKC),ZICC(I_K,JKC),ZIDD(I_K,JKC))  
      ELSE
        IF(JKC < IDIM) ZHH(J,I_K)=FHH(ZDELTA(JD) &
         & ,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR)&
         & ,ZIAA(I_K,JKC),ZIBB(I_K,JKC),ZICC(I_K,JKC),ZIDD(I_K,JKC))  
      ENDIF
    ENDDO
  ENDDO
  ZA(JR,JR  )=ZHH(1,1)+ZHH(2,2)+ZHH(3,3)+ZHH(4,4)
  IF(JR+1 <= IDIM) &
   & ZA(JR,JR+1)=         ZHH(2,1)+ZHH(3,2)+ZHH(4,3)  
  IF(JR+2 <= IDIM) &
   & ZA(JR,JR+2)=                  ZHH(3,1)+ZHH(4,2)  
  IF(JR+3 <= IDIM) &
   & ZA(JR,JR+3)=                           ZHH(4,1)  

ENDDO
  
! A is symmetric
DO JR=1,IDIM
  DO JC=JR+1,IDIM
    ZA(JC,JR)=ZA(JR,JC)
  ENDDO
ENDDO

! invert A: 
ZDET=0.0_JPRD
ZAINV(:,:)=ZA(:,:)*IDIM
CALL MINV_8(ZAINV,IDIM,IDIM,ZWORK,ZDET,ZEPS,0,1)
ZAINV(:,:)=ZAINV(:,:)*IDIM

! print A and A^-1
IF(MYPROC == 1.AND.LVFE_VERBOSE) THEN
  ZCCA=6._JPRD*6._JPRD*4._JPRD*4._JPRD*5._JPRD*7._JPRD*2._JPRD*4._JPRD
  WRITE(NULOUT,*) ' DETERMINANT OF A =',ZDET
  WRITE(NULOUT,*) ' MATRIX A :'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.1)')&
     & (ZCCA*ZA(JR,JC),JC=1,IDIM)  
  ENDDO
  WRITE(NULOUT,*) ' INVERSE OF A :'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.3)') (ZAINV(JR,JC),JC=1,IDIM)
  ENDDO
ENDIF

! COMPUTE MATRIX B
! ----------------

!setup ZINTR AND ZINTC:

ZINTR(:,:)=0.0_JPRD
ZINTC(:,:)=0.0_JPRD

DO JK=-1,NFLEVG+2
  ZD1=ZDELTA(JK-2)
  ZD2=ZDELTA(JK-1)
  ZD3=ZDELTA(JK  )
  ZD4=ZDELTA(JK+1)
  ZINTR(1,JK)=FINT(ZD1,ZIAA(1,JK),ZIBB(1,JK),ZICC(1,JK),ZIDD(1,JK))
  ZINTR(2,JK)=FINT(ZD2,ZIAA(2,JK),ZIBB(2,JK),ZICC(2,JK),ZIDD(2,JK))
  ZINTR(3,JK)=FINT(ZD3,ZIAA(3,JK),ZIBB(3,JK),ZICC(3,JK),ZIDD(3,JK))
  ZINTR(4,JK)=FINT(ZD4,ZIAA(4,JK),ZIBB(4,JK),ZICC(4,JK),ZIDD(4,JK))

  ZINTC(1,JK)=FINT(ZD1,ZFAA(1,JK),ZFBB(1,JK),ZFCC(1,JK),ZFDD(1,JK))
  ZINTC(2,JK)=FINT(ZD2,ZFAA(2,JK),ZFBB(2,JK),ZFCC(2,JK),ZFDD(2,JK))
  ZINTC(3,JK)=FINT(ZD3,ZFAA(3,JK),ZFBB(3,JK),ZFCC(3,JK),ZFDD(3,JK))
  ZINTC(4,JK)=FINT(ZD4,ZFAA(4,JK),ZFBB(4,JK),ZFCC(4,JK),ZFDD(4,JK))
ENDDO
ZINTR(3,0)=FINT(ZD0,ZIAA(3,0),ZIBB(3,0),ZICC(3,0),ZIDD(3,0)) &
 & -FINT(ZD0H,ZIAA(3,0),ZIBB(3,0),ZICC(3,0),ZIDD(3,0))  
ZINTR(2,1)= FINT(ZD0,ZIAA(2,1),ZIBB(2,1),ZICC(2,1),ZIDD(2,1)) &
 & -FINT(ZD0H,ZIAA(2,1),ZIBB(2,1),ZICC(2,1),ZIDD(2,1))  
ZINTR(1,2)= FINT(ZD0,ZIAA(1,2),ZIBB(1,2),ZICC(1,2),ZIDD(1,2)) &
 & -FINT(ZD0H,ZIAA(1,2),ZIBB(1,2),ZICC(1,2),ZIDD(1,2))  

ZINTC(4,-1)=FINT(ZD0,ZFAA(4,-1),ZFBB(4,-1),ZFCC(4,-1),ZFDD(4,-1)) &
 & -FINT(ZD0H,ZFAA(4,-1),ZFBB(4,-1),ZFCC(4,-1),ZFDD(4,-1))  
ZINTC(3, 0)=FINT(ZD0,ZFAA(3,0),ZFBB(3,0),ZFCC(3,0),ZFDD(3,0)) &
 & -FINT(ZD0H,ZFAA(3,0),ZFBB(3,0),ZFCC(3,0),ZFDD(3,0))  
ZINTC(2, 1)=FINT(ZD0,ZFAA(2,1),ZFBB(2,1),ZFCC(2,1),ZFDD(2,1)) &
 & -FINT(ZD0H,ZFAA(2,1),ZFBB(2,1),ZFCC(2,1),ZFDD(2,1))  
ZINTC(1, 2)=FINT(ZD0,ZFAA(1,2),ZFBB(1,2),ZFCC(1,2),ZFDD(1,2)) &
 & -FINT(ZD0H,ZFAA(1,2),ZFBB(1,2),ZFCC(1,2),ZFDD(1,2))  

! setup ZG(4x4) matrix for all nodes JK:

ZG(:,:,:)=0.0_JPRD
DO JK=0,NFLEVG+2
  ZD1=ZDELTA(JK-2)
  ZD2=ZDELTA(JK-1)
  ZD3=ZDELTA(JK  )
  ZD4=ZDELTA(JK+1)
  ZG(1,1,JK)=FGG(ZD1,ZIAA(1,JK),ZIBB(1,JK),ZICC(1,JK),ZIDD(1,JK)  &
   & ,ZFAA(1,JK),ZFBB(1,JK),ZFCC(1,JK),ZFDD(1,JK))  
  ZG(1,2,JK)=FGG(ZD1,ZIAA(1,JK),ZIBB(1,JK),ZICC(1,JK),ZIDD(1,JK)  &
   & ,ZFAA(2,JK-1),ZFBB(2,JK-1),ZFCC(2,JK-1),ZFDD(2,JK-1))  
  IF(JK-2 >= -1)  &
   & ZG(1,3,JK)=FGG(ZD1,ZIAA(1,JK),ZIBB(1,JK),ZICC(1,JK),ZIDD(1,JK)  &
   & ,ZFAA(3,JK-2),ZFBB(3,JK-2),ZFCC(3,JK-2),ZFDD(3,JK-2))   
  IF(JK-3 >= -1)   &
   & ZG(1,4,JK)=FGG(ZD1,ZIAA(1,JK),ZIBB(1,JK),ZICC(1,JK),ZIDD(1,JK)  &
   & ,ZFAA(4,JK-3),ZFBB(4,JK-3),ZFCC(4,JK-3),ZFDD(4,JK-3))  

  IF(JK+1 <= NFLEVG+2)  &
   & ZG(2,1,JK)=FGG(ZD2,ZIAA(2,JK),ZIBB(2,JK),ZICC(2,JK),ZIDD(2,JK)  &
   & ,ZFAA(1,JK+1),ZFBB(1,JK+1),ZFCC(1,JK+1),ZFDD(1,JK+1))  
  ZG(2,2,JK)=FGG(ZD2,ZIAA(2,JK),ZIBB(2,JK),ZICC(2,JK),ZIDD(2,JK)  &
   & ,ZFAA(2,JK),ZFBB(2,JK),ZFCC(2,JK),ZFDD(2,JK))  
  ZG(2,3,JK)=FGG(ZD2,ZIAA(2,JK),ZIBB(2,JK),ZICC(2,JK),ZIDD(2,JK)  &
   & ,ZFAA(3,JK-1),ZFBB(3,JK-1),ZFCC(3,JK-1),ZFDD(3,JK-1))  
  IF(JK-2 >= -1)  &
   & ZG(2,4,JK)=FGG(ZD2,ZIAA(2,JK),ZIBB(2,JK),ZICC(2,JK),ZIDD(2,JK)  &
   & ,ZFAA(4,JK-2),ZFBB(4,JK-2),ZFCC(4,JK-2),ZFDD(4,JK-2))  

  IF(JK+2 <= NFLEVG+2) &
   & ZG(3,1,JK)=FGG(ZD3,ZIAA(3,JK),ZIBB(3,JK),ZICC(3,JK),ZIDD(3,JK)  &
   & ,ZFAA(1,JK+2),ZFBB(1,JK+2),ZFCC(1,JK+2),ZFDD(1,JK+2))  
  IF(JK+1 <= NFLEVG+2)  &
   & ZG(3,2,JK)=FGG(ZD3,ZIAA(3,JK),ZIBB(3,JK),ZICC(3,JK),ZIDD(3,JK)  &
   & ,ZFAA(2,JK+1),ZFBB(2,JK+1),ZFCC(2,JK+1),ZFDD(2,JK+1))  
  ZG(3,3,JK)=FGG(ZD3,ZIAA(3,JK),ZIBB(3,JK),ZICC(3,JK),ZIDD(3,JK)  &
   & ,ZFAA(3,JK),ZFBB(3,JK),ZFCC(3,JK),ZFDD(3,JK))  
  ZG(3,4,JK)=FGG(ZD3,ZIAA(3,JK),ZIBB(3,JK),ZICC(3,JK),ZIDD(3,JK)  &
   & ,ZFAA(4,JK-1),ZFBB(4,JK-1),ZFCC(4,JK-1),ZFDD(4,JK-1))  
 
  IF(JK+3 <= NFLEVG+2) &
   & ZG(4,1,JK)=FGG(ZD4,ZIAA(4,JK),ZIBB(4,JK),ZICC(4,JK),ZIDD(4,JK)  &
   & ,ZFAA(1,JK+3),ZFBB(1,JK+3),ZFCC(1,JK+3),ZFDD(1,JK+3))  
  IF(JK+2 <= NFLEVG+2) &
   & ZG(4,2,JK)=FGG(ZD4,ZIAA(4,JK),ZIBB(4,JK),ZICC(4,JK),ZIDD(4,JK)  &
   & ,ZFAA(2,JK+2),ZFBB(2,JK+2),ZFCC(2,JK+2),ZFDD(2,JK+2))  
  IF(JK+1 <= NFLEVG+2) &
   & ZG(4,3,JK)=FGG(ZD4,ZIAA(4,JK),ZIBB(4,JK),ZICC(4,JK),ZIDD(4,JK)  &
   & ,ZFAA(3,JK+1),ZFBB(3,JK+1),ZFCC(3,JK+1),ZFDD(3,JK+1))  
  ZG(4,4,JK)=FGG(ZD4,ZIAA(4,JK),ZIBB(4,JK),ZICC(4,JK),ZIDD(4,JK)  &
   & ,ZFAA(4,JK),ZFBB(4,JK),ZFCC(4,JK),ZFDD(4,JK))  
ENDDO

! special ZG's for interval between JK=0 and JK=1 where integration is
! only over half the interval (from ZD0H to ZD0):

DO JKR=0,2
  J=3-JKR
  DO JKC=-1,2
    I_K=4-(JKC+1)
    ZG(J,I_K,JKR)=FGG(ZD0,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR)  &
     & ,ZFAA(I_K,JKC),ZFBB(I_K,JKC),ZFCC(I_K,JKC),ZFDD(I_K,JKC)) &
     & -FGG(ZD0H,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR)  &
     & ,ZFAA(I_K,JKC),ZFBB(I_K,JKC),ZFCC(I_K,JKC),ZFDD(I_K,JKC))&
     & -FINT(ZD0H,ZFAA(I_K,JKC),ZFBB(I_K,JKC),ZFCC(I_K,JKC),ZFDD(I_K,JKC))&
     & *(FINT(ZD0,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR))&
     & -FINT(ZD0H,ZIAA(J,JKR),ZIBB(J,JKR),ZICC(J,JKR),ZIDD(J,JKR)))  
  ENDDO
ENDDO

! compute B matrix from ZINTR, ZINTC and ZG

ZB(:,:)=0.0_JPRD

DO JR=4,IDIM
  DO JC=1,JR-3
    JKR=JR-1
    JKC=JC-2
    ZB(JR,JC)= (ZINTR(1,JKR)+ZINTR(2,JKR)+ZINTR(3,JKR)+ZINTR(4,JKR))&
     & *(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC)+ZINTC(4,JKC))  
  ENDDO
ENDDO

DO JR=1,IDIM
  JKR=JR-1
  JC=JR-2
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)=&
   & ZINTR(1,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC)) +ZG(1,4,JKR)&
   & +(ZINTR(2,JKR)+ZINTR(3,JKR)+ZINTR(4,JKR))&
   & *(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC)+ZINTC(4,JKC))  
  JC=JR-1
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)=&
   & ZINTR(1,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC))+ZG(1,3,JKR)&
   & +ZINTR(2,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC))+ZG(2,4,JKR)&
   & +(ZINTR(3,JKR)+ZINTR(4,JKR))&
   & *(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC)+ZINTC(4,JKC))  
  JC=JR
  JKC=JC-2
  ZB(JR,JC)=&
   & ZINTR(1,JKR)* ZINTC(1,JKC)+ZG(1,2,JKR)&
   & +ZINTR(2,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC))+ZG(2,3,JKR)&
   & +ZINTR(3,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC))+ZG(3,4,JKR)&
   & +ZINTR(4,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC)+ZINTC(4,JKC))  
  JC=JR+1
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)= &
   & ZG(1,1,JKR)&
   & +ZINTR(2,JKR)* ZINTC(1,JKC)+ZG(2,2,JKR)&
   & +ZINTR(3,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC))+ZG(3,3,JKR)&
   & +ZINTR(4,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC)+ZINTC(3,JKC))+ZG(4,4,JKR)  
  JC=JR+2
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)= &
   & ZG(2,1,JKR)&
   & +ZINTR(3,JKR)* ZINTC(1,JKC)+ZG(3,2,JKR)&
   & +ZINTR(4,JKR)*(ZINTC(1,JKC)+ZINTC(2,JKC))+ZG(4,3,JKR)  
  JC=JR+3
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)= &
   & ZG(3,1,JKR)&
   & +ZINTR(4,JKR)* ZINTC(1,JKC)+ZG(4,2,JKR)  
  JC=JR+4
  JKC=JC-2
  IF(1 <= JC.AND.JC <= IDIM) ZB(JR,JC)= ZG(4,1,JKR)
ENDDO

IF(MYPROC == 1.AND.LVFE_VERBOSE) THEN
  ZCCB=6.0_JPRD*6.0_JPRD*4.0_JPRD*4.0_JPRD*70.0_JPRD
  WRITE(NULOUT,*)' B MATRIX:'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.1)') (ZCCB*ZB(JR,JC),JC=1,IDIM)
  ENDDO
ENDIF

! COMPUTE PRODUCT SI*A^-1*B*S^-1=ZINTE
! ------------------------------------

ZINTE(:,:)=0.0_JPRD
DO JC=1,IDIM
  DO JR=1,IDIM
    DO JK=1,IDIM
      ZINTE(JR,JC)=ZINTE(JR,JC)+ZB(JR,JK)*ZSFINV(JK,JC)
    ENDDO
  ENDDO
ENDDO

ZPROD(:,:)=0.0_JPRD
DO JC=1,IDIM
  DO JR=1,IDIM
    DO JK=1,IDIM
      ZPROD(JR,JC)=ZPROD(JR,JC)+ZAINV(JR,JK)*ZINTE(JK,JC)
    ENDDO
  ENDDO
ENDDO

ZINTE(:,:)=0.0_JPRD
DO JC=1,IDIM
  DO JR=1,IDIM
    DO JK=1,IDIM
      ZINTE(JR,JC)=ZINTE(JR,JC)+ZSI(JR,JK)*ZPROD(JK,JC)
    ENDDO
  ENDDO
ENDDO

! print integration matrix and check derivative of constant function

IF(MYPROC == 1.AND.LVFE_VERBOSE) THEN
  WRITE(NULOUT,*) ' MATRIX FOR INTEGRALS:'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.4)') (ZINTE(JR,JC),JC=1,IDIM)
  ENDDO
  ZDERI(:,:)= ZINTE(:,:)*IDIM
  ZDET=0.0_JPRD
  CALL MINV_8(ZDERI,IDIM,IDIM,ZWORK,ZDET,ZEPS,0,1)
  ZDERI(:,:)=ZDERI(:,:)* IDIM
  WRITE(NULOUT,*) ' DETERMINANT=',ZDET
  WRITE(NULOUT,*) ' INVERSE OF MATRIX FOR INTEGRALS:'
  DO JR=1,IDIM
    WRITE(NULOUT,'(100F9.3)') (ZDERI(JR,JC),JC=1,IDIM)
  ENDDO
  WRITE(NULOUT,*) ' DERIVATIVE OF FUNCTION f(x)=1'
  DO JR=1,IDIM
    ZD0=0.0_JPRD
    DO JK=1,IDIM-1
!      loop only to NFLEVG+2 because f(NFLEVG+3)==f'(x_NFLEVG+1)==0
      ZD0=ZD0+ZDERI(JR,JK)
    ENDDO
    WRITE(NULOUT,*) ' NODE ',JR,' DERIVATIVE=',ZD0
  ENDDO
ENDIF

!- skip first and last column of integration matrix ZINTE because 
! the derivatives of the function to be integrated are set to 0 at the 
! first and last node: f'(x_0)==0==f'(x_NFLEVG+1)
!- add columns 2 and 3 because the function is constant above the first
! full level ( f(x_0)==f(x_1))
!- skip first and last row of the integration matrix ZINTE because
! these give the derivatives of the integral at the end nodes, which are
! not used 
DO JC=1,IDIM-3
  DO JR=1,IDIM-2
    IF(JC == 1) THEN
      YDVFE%RINTE(JR,JC)=ZINTE(JR+1,JC+1) + ZINTE(JR+1,JC+2)
    ELSE
      YDVFE%RINTE(JR,JC)=ZINTE(JR+1,JC+2)
    ENDIF
  ENDDO
ENDDO

IF(MYPROC == 1.AND.LVFE_VERBOSE) THEN
  WRITE(NULOUT,*) ' MATRIX RINTE:'
  DO JR=1,IDIM-2
    WRITE(NULOUT,'(100F9.4)') (YDVFE%RINTE(JR,JC),JC=1,IDIM-3)
  ENDDO
ENDIF

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUVERTFE3',1,ZHOOK_HANDLE)
END SUBROUTINE SUVERTFE3
