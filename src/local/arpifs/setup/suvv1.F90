SUBROUTINE SUVV1(YDGEOMETRY,KSUPERSEDE)

!**** *SUVV1*  - Routine to initialize the vertical coordinate

!     Purpose.
!     --------
!           Read namelist NAMVV1 and control it.

!**   Interface.
!     ----------

!     *CALL* SUVV1

!        Explicit arguments :
!        --------------------

!        Implicit arguments :
!        --------------------

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!      R. El Khatib  *Meteo-France*
!      Original : 26-Jul-2012 from SUVERT

!     Modifications.
!     --------------
!      T. Wilhelmsson (Sept 2013) Geometry and setup refactoring.
!      R. El Khatib 04-Aug-2014 Pruning of the conf. 927/928
!      J. Vivoda and P. Smolikova (Sep 2017): new options for VFE-NH
!      P.Smolikova (Sep 2020): VFE pruning.
! End Modifications
!-------------------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE PARDIM   , ONLY : JPMXLE
USE YOMARG   , ONLY : NSUPERSEDE, UVALH, UVBH
USE YOMLUN   , ONLY : NULOUT, NULNAM, NULERR
USE YOMCT0   , ONLY : LECMWF, LNHDYN
USE YOMCVER  , ONLY : LVERTFE, LVFE_ECMWF, LVFE_GW, LVFE_GW_HALF, &
 &                    NVFE_ORDER, NVFE_TYPE, NVFE_INTERNALS
USE YOMDYNA  , ONLY : YRDYNA
USE YOMVERT  , ONLY : VP00, TOPPRES, ALLOC_INIT_TVAB
USE YOMVV1   , ONLY : VVP00, DVALH, DVBH

!-------------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT), TARGET :: YDGEOMETRY
INTEGER(KIND=JPIM), INTENT(IN), OPTIONAL :: KSUPERSEDE

REAL(KIND=JPRB) :: ZVALH(0:YDGEOMETRY%YRDIMV%NIOLEVG), ZVBH(0:YDGEOMETRY%YRDIMV%NIOLEVG)
INTEGER(KIND=JPIM), PARAMETER :: JPKS=KIND(ZVALH)

INTEGER(KIND=JPIM) :: I, JLEV

REAL(KIND=JPRB) :: ZABOT, ZATOP, ZBBOT, ZBTOP, ZEPS
LOGICAL :: LLFORCE_READ, LLSUPERSEDE
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

NAMELIST /NAMVV0/ LLFORCE_READ
#include "namvv1.nam.h"

#include "abor1.intfb.h"
#include "sudefo_vv1.intfb.h"
#include "posnam.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUVV1',0,ZHOOK_HANDLE)
ASSOCIATE(YDVERT_GEOM=>YDGEOMETRY%YRVERT_GEOM)
ASSOCIATE(NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG,   NIOLEVG=>YDGEOMETRY%YRDIMV%NIOLEVG)
!     ------------------------------------------------------------------

!*       1.    ALLOCATE SPACE FOR ARRAYS.
!              --------------------------

!*       1.2   YOMVERT.


ALLOCATE(YDVERT_GEOM%YRVAB%VALH (0:NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VAH  (0:NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VBH  (0:NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VC   (NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VDELB(NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VRATH(0:NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVAB%VRATF(NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVETA%VETAH(0:NFLEVG))
ALLOCATE(YDVERT_GEOM%YRVETA%VETAF(0:NFLEVG+1))

IF (LVERTFE) THEN
  ALLOCATE(YDVERT_GEOM%YRVETA%VFE_ETAH(0:NFLEVG))
  ALLOCATE(YDVERT_GEOM%YRVETA%VFE_ETAF(0:NFLEVG+1))
  ALLOCATE(YDVERT_GEOM%YRVAB%VAF  (NFLEVG))
  ALLOCATE(YDVERT_GEOM%YRVAB%VBF  (NFLEVG))
  ALLOCATE(YDVERT_GEOM%YRVAB%VDELA(NFLEVG))
  ALLOCATE(YDVERT_GEOM%YRVETA%VFE_RDETAH(NFLEVG))
  IF(.NOT.LVFE_ECMWF.AND.NVFE_TYPE>1)THEN
    NVFE_INTERNALS = NFLEVG + 2 - NVFE_ORDER
    ALLOCATE(YDVERT_GEOM%YRVFE%VFE_KNOT(NVFE_INTERNALS))
  ENDIF

!*       1.3   VFE INTEGRAL AND DERIVATIVE OPERATORS

  IF (LVFE_ECMWF) THEN
    ALLOCATE(YDVERT_GEOM%YRVFE%RINTE (NFLEVG+1,NFLEVG))
    ALLOCATE(YDVERT_GEOM%YRVFE%D_RINTE (NFLEVG+1,NFLEVG))
    ALLOCATE(YDVERT_GEOM%YRVFE%RDERI (NFLEVG,NFLEVG))
    ALLOCATE(YDVERT_GEOM%YRVFE%RDERB (NFLEVG,NFLEVG+2))
    ALLOCATE(YDVERT_GEOM%YRVFE%RDDERI (NFLEVG,NFLEVG))
  ELSE
    ALLOCATE(YDVERT_GEOM%YRVFE%RINTBF11 (NFLEVG+1,NFLEVG+2))
    ALLOCATE(YDVERT_GEOM%YRVFE%RDERBF11 (NFLEVG,NFLEVG+2))
    IF (LNHDYN) THEN
      ALLOCATE(YDVERT_GEOM%YRVFE%RDERBF00 (NFLEVG,NFLEVG+2))
      ALLOCATE(YDVERT_GEOM%YRVFE%RDERBF01 (NFLEVG,NFLEVG+2))
      ALLOCATE(YDVERT_GEOM%YRVFE%RDERBF10 (NFLEVG,NFLEVG+2))
      ALLOCATE(YDVERT_GEOM%YRVFE%RDDERBF01(NFLEVG,NFLEVG+2))
      ALLOCATE(YDVERT_GEOM%YRVFE%RDERBH00 (NFLEVG+1,NFLEVG+2))
      ALLOCATE(YDVERT_GEOM%YRVFE%RDERBH01 (NFLEVG+1,NFLEVG+2))
      IF (YRDYNA%LGWADV.AND.(LVFE_GW.OR.LVFE_GW_HALF)) THEN
        ALLOCATE(YDVERT_GEOM%YRVFE%RINTBF00(NFLEVG+1,NFLEVG+2))
        ALLOCATE(YDVERT_GEOM%YRVFE%RDERGW(NFLEVG+1,NFLEVG+1))
        ALLOCATE(YDVERT_GEOM%YRVFE%RINTGW(NFLEVG+1,NFLEVG+1))
      ENDIF
    ENDIF
  ENDIF
  IF ((NVFE_TYPE/=1.AND.NVFE_TYPE/=3) .AND. LVFE_ECMWF) THEN
    CALL ABOR1(' INVALID VALUE OF NVFE_TYPE FOR ECMWF VFE')
  ENDIF

ENDIF

!! POINT THE POINTERS IN GEOMETRY DERIVED TYPE TO THE RIGHT PLACE,
!! TO ALLOW "LEGACY" CODE: THIS SHOULD BE REMOVABLE IN THE FUTURE
YDGEOMETRY%YRVAB  => YDVERT_GEOM%YRVAB
YDGEOMETRY%YRVETA => YDVERT_GEOM%YRVETA
YDGEOMETRY%YRVFE  => YDVERT_GEOM%YRVFE


!*         2. READ NAMELISTS.
!          ------------------

!      2.1 Set default values and read namelist

IF (PRESENT(KSUPERSEDE)) THEN
  LLSUPERSEDE=(KSUPERSEDE == 1)
ELSE
  LLSUPERSEDE=(NSUPERSEDE == 1)
ENDIF
LLFORCE_READ=.FALSE.
ZVALH=0._JPRB
ZVBH=0._JPRB
IF(NIOLEVG > JPMXLE) THEN
  WRITE(NULOUT,*) '*** SUVV1: NIOLEVG BIGGER THAN EXPECTED LIMIT'
  WRITE(NULOUT,*) JPMXLE,' LEVELS. IF YOUR NIOLEVG IS O.K., INCREASE'
  WRITE(NULOUT,*) 'PARAMETER JPMXLE IN PARDIM'
  CALL ABOR1('SUVV1')
ENDIF
CALL SUDEFO_VV1(LECMWF,NIOLEVG,ZVALH,ZVBH,VVP00)
DO JLEV=0,NIOLEVG
  DVALH(JLEV)=ZVALH(JLEV)
  DVBH(JLEV)=ZVBH(JLEV)
ENDDO
VP00 = VVP00
TOPPRES=0.1_JPRB
CALL POSNAM(NULNAM,'NAMVV0')
READ(NULNAM,NAMVV0)
WRITE(NULOUT,*) 'LLFORCE_READ = ',LLFORCE_READ
IF(LLFORCE_READ) THEN
  CALL POSNAM(NULNAM,'NAMVV1')
  READ(NULNAM,NAMVV1)
ELSEIF (LLSUPERSEDE) THEN
  DO JLEV=0,NIOLEVG
    DVALH(JLEV)=UVALH(JLEV)
    DVBH(JLEV)=UVBH(JLEV)
  ENDDO
ELSE
  IF (NIOLEVG > 1) THEN
    CALL POSNAM(NULNAM,'NAMVV1')
    READ(NULNAM,NAMVV1)
  ENDIF
ENDIF

!        2.2 Controls 

ZEPS=EPSILON(1.0_JPRB)*100._JPRB
IF(NIOLEVG > 1) THEN
  IF ((LLSUPERSEDE) .AND. .NOT. LLFORCE_READ) THEN
    I=0
    DO JLEV=0,NIOLEVG
      IF (ABS(REAL(DVALH(JLEV),JPKS)-UVALH(JLEV)) > ZEPS) THEN
        I=1
        DVALH(JLEV)=UVALH(JLEV)
      ENDIF
    ENDDO
    IF(I == 1)WRITE(NULERR,*) 'DVALH OVERWRITTEN BY COMAND LINE'
    I=0
    DO JLEV=0,NIOLEVG
      IF (ABS(REAL(DVBH(JLEV),JPKS)-UVBH(JLEV)) > ZEPS) THEN
        I=1
        DVBH(JLEV)=UVBH(JLEV)
      ENDIF
    ENDDO
    IF(I == 1)WRITE(NULERR,*) ' DVBH OVERWRITTEN BY COMAND LINE'
  ENDIF

  ZATOP=REAL(DVALH(0),JPKS)
  ZABOT=REAL(DVALH(NIOLEVG),JPKS)
  ZBTOP=REAL(DVBH(0),JPKS)
  ZBBOT=REAL(DVBH(NIOLEVG),JPKS)
  IF ((ABS(ZABOT) > ZEPS).OR.&
     & (ABS(ZBTOP) > ZEPS).OR.(ABS(ZBBOT-1.0_JPRB) > ZEPS)) THEN  
    WRITE(NULOUT,*) '*** SUVV1: LIMIT VALUES FOR VERTICAL'
    WRITE(NULOUT,*) 'FUNCTIONS A OR B ARE WRONG. PLEASE CHECK'
    WRITE(NULOUT,*) 'THEM. MAYBE THEN NUMBER OF LEVELS'
    WRITE(NULOUT,*) 'NIOLEVG DOES NOT MATCH WITH THE VALUES OF'
    WRITE(NULOUT,*) 'THE FUNCTION A AND B IN NAMELIST'
    CALL ABOR1('SUVV1')
  ENDIF

  WRITE(UNIT=NULOUT,FMT='(1X,''VP00 = '',E13.7)') VP00
  ! Here below we consider only the lowest NFLEVG levels :
  !* The division by VP00 is here just for continuity with ECMWF archives
  DO JLEV=0,NFLEVG
    YDVERT_GEOM%YRVAB%VAH(JLEV)=REAL(DVALH(JLEV+NIOLEVG-NFLEVG),JPKS)
    YDVERT_GEOM%YRVAB%VALH(JLEV)=REAL(DVALH(JLEV+NIOLEVG-NFLEVG),JPKS)/VP00
    YDVERT_GEOM%YRVAB%VBH(JLEV)=REAL(DVBH(JLEV+NIOLEVG-NFLEVG),JPKS)
  ENDDO

ENDIF

!        2.3 Setup I/O levels

DO JLEV=0,NIOLEVG
  ZVALH(JLEV)=REAL(DVALH(JLEV),JPKS)
  ZVBH(JLEV)=REAL(DVBH(JLEV),JPKS)
ENDDO
CALL ALLOC_INIT_TVAB(NIOLEVG,ZVALH,ZVBH,YDGEOMETRY%YVABIO)
WRITE(NULOUT,*) 'SHAPE VALHIO/VBHIO : ', SHAPE(YDGEOMETRY%YVABIO%VALH)

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUVV1',1,ZHOOK_HANDLE)
END SUBROUTINE SUVV1
