SUBROUTINE RDRADCOEF(YDGEOMETRY,YDRCOEF,KSTA,KEND,KJUMP,KFIELDSS,&
& PCOR  , PRAB3C, PRAB3N, PRAB4C, PRAB4N, PRAB6C, PRAB6N, &
& PRAT1C, PRAT1N, PRAT2C, PRAT2N, PRAT3C, PRAT3N, PRAT4C, &
& PRAT4N, PRAT5C, PRAT5N, PAC)

!**** *RDRADCOEF* - Read out the constants for solar and thermal radiation
!                   MF physics-dynamics interface, level 1

!     Purpose.  Read out the constants for solar and thermal radiation
!     --------  (grid-point transmission coefficients) from work file:
!               they are used in the simplified physics.

!**   Interface.
!     ----------
!        *CALL* *RDRADCOEF(...)*

!        Explicit arguments :
!        --------------------
!          KSTA          : start adress               (input)
!          KEND          : end adress                 (input)
!          KJUMP         : offset into gp array       (input)
!          KFIELDSS      : number of fields           (input)
!          PRADTC and PAC: transmission coefficients  (output)

!        Implicit arguments :  None
!        --------------------

!     Method.
!     -------

!     Externals. EMPTB - interface to packing routines
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        M. Janiskova

!     Modifications.
!     --------------
!        Original : 99-02-09
!        Modified :
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!        K.Yessad (Feb 2010): use YM_RADTC and PRADTC.
!     ------------------------------------------------------------------

USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMRCOEF , ONLY : TRCOEF
USE MODULE_RADTC_MIX, ONLY : YM_RADTC

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TRCOEF)      ,INTENT(IN)    :: YDRCOEF
INTEGER(KIND=JPIM),INTENT(IN)    :: KSTA 
INTEGER(KIND=JPIM),INTENT(IN)    :: KEND 
INTEGER(KIND=JPIM),INTENT(IN)    :: KJUMP 
INTEGER(KIND=JPIM),INTENT(IN)    :: KFIELDSS 
REAL(KIND=JPRB),   INTENT(OUT)   :: PCOR   (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB3C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB3N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB4C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB4N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB6C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAB6N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT1C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT1N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT2C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT2N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT3C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT3N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT4C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT4N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT5C (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB),   INTENT(OUT)   :: PRAT5N (YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PAC(YDGEOMETRY%YRDIMV%NFLEVG+1,YDGEOMETRY%YRDIMV%NFLEVG+1) 

INTEGER(KIND=JPIM), SAVE :: NLATRD = 0

!     ------------------------------------------------------------------

INTEGER(KIND=JPIM) :: IJUMP, ISTAGPSB,&
 & ISTAGPTB, IWORDS, IWORDT, JLEV, JF 
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "abor1.intfb.h"
#include "emptb.intfb.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('RDRADCOEF',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP)
ASSOCIATE(NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & LGLOBRAD=>YDRCOEF%LGLOBRAD, NLATWR=>YDRCOEF%NLATWR, &
 & SOLRAD=>YDRCOEF%SOLRAD, THERRAD=>YDRCOEF%THERRAD)
!     ------------------------------------------------------------------

!*       1.    READ OUT GRIDPOINT ARRAY.
!              --------------------------

IF (KFIELDSS /= NFLEVG*YM_RADTC%NDIM) THEN
  ! YM_RADTC%NDIM should be equal to NG3SR if LRCOEF=T.
  CALL ABOR1('RDRADCOEF 1.1: YM_RADTC%NDIM should be equal to NG3SR')
ENDIF

NLATRD=NLATRD+1

!*       1.1       CALCULATE LENGTHS

IWORDS = KEND-KSTA+1
IWORDT = NFLEVG+1

!*       1.2       READ OUT BUFFER

IJUMP=KJUMP-1
ISTAGPSB = 1+IJUMP*KFIELDSS
ISTAGPTB = 1

!        1.3      SOLAR RADIATION COEFFICIENTS, CORRECTION FACTOR
!                 FOR THERMAL RADIATION AND MATRIX OF THERMAL
!                 RADIATION COEFFICIENTS

! * Radiation transmission coefficients other than Curtis matrix.
DO JLEV=1,NFLEVG
  CALL EMPTB (IWORDS,PRAB3C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAB4C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAB6C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAB3N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAB4N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAB6N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT1C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT2C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT3C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT4C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT5C(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT1N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT2N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT3N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT4N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PRAT5N(KSTA,JLEV),SOLRAD(:),ISTAGPSB)
  CALL EMPTB (IWORDS,PCOR  (KSTA,JLEV),SOLRAD(:),ISTAGPSB)
ENDDO

! * Curtis matrix for thermal radiation for clear sky.
IF (LGLOBRAD) THEN
  DO JLEV=1,NFLEVG+1
    CALL EMPTB(IWORDT,PAC(1,JLEV),THERRAD(1,:),ISTAGPTB)  
  ENDDO
ELSE
  DO JLEV=1,NFLEVG+1
    CALL EMPTB(IWORDT,PAC(1,JLEV),THERRAD(NLATRD,:),ISTAGPTB)  
  ENDDO
ENDIF

!       1.4  UPDATE FOR LATITUDE

IF (NLATRD == NLATWR) THEN
  NLATRD=0
ENDIF

!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('RDRADCOEF',1,ZHOOK_HANDLE)
END SUBROUTINE RDRADCOEF

