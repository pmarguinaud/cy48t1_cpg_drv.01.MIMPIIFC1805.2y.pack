SUBROUTINE SUSPVARIABLES(YDML_GCONF)

USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LNHDYN
USE YOMDYNA  , ONLY : YRDYNA
USE YOM_GRIB_CODES  , ONLY : NGRBVO, NGRBD, NGRBT, NGRBQ, NGRBO3, &
 & NGRBCLWC, NGRBCIWC, NGRB118, NGRB119, NGRBLNSP, NGRBZ
USE YOMSP_PTRS, ONLY : MSPTR_T, MSPTR_SP

!     Purpose.
!     --------
!       Split form SUALSPA and SUALSPA1

!     Author.
!     -------
!      Y. Tremolet  *ECMWF*
!      Original : 06-Aug-2014

!     Modifications.
!     --------------

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT):: YDML_GCONF

INTEGER(KIND=JPIM) :: IPT, IOFF, IZ
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUSPVARIABLES',0,ZHOOK_HANDLE)

! * Individual GMV SP(X), and SPHV:
IF (ALLOCATED(YDML_GCONF%YRDIMF%NGRBSP3)) DEALLOCATE(YDML_GCONF%YRDIMF%NGRBSP3)
ALLOCATE(YDML_GCONF%YRDIMF%NGRBSP3(YDML_GCONF%YRDIMF%NS3D))

IPT = 1
IF (YDML_GCONF%YRDIMF%NS3D >= 1) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IPT)=NGRBVO
  IPT = IPT+1
ENDIF
IF (YDML_GCONF%YRDIMF%NS3D >= 2) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IPT)=NGRBD
  IPT = IPT+1
ENDIF
IF (YDML_GCONF%YRDIMF%LSPT) THEN
  MSPTR_T = IPT
  YDML_GCONF%YRDIMF%NGRBSP3(IPT)=NGRBT
  IPT = IPT+1
ELSE
  MSPTR_T = 999999999
ENDIF
IF(LNHDYN) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IPT)=NGRB118 ! Temporary until proper grib code assigned
  IPT=IPT+1
  YDML_GCONF%YRDIMF%NGRBSP3(IPT)=NGRB119 ! Temporary until proper grib code assigned
  IPT=IPT+1
  IF(YRDYNA%LNHX) THEN
    YDML_GCONF%YRDIMF%NGRBSP3(IPT)=-99 ! There is no grib code.
    IPT=IPT+1
  ENDIF
ENDIF

! * SPGFL and individual GFL SP(X).
IOFF=2+YDML_GCONF%YRDIMF%NFTHER
IF(YDML_GCONF%YGFL%YQ%LSP) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IOFF+YDML_GCONF%YGFL%YQ%MPSP)=NGRBQ
ENDIF
IF(YDML_GCONF%YGFL%YO3%LSP) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IOFF+YDML_GCONF%YGFL%YO3%MPSP)=NGRBO3
ENDIF
IF(YDML_GCONF%YGFL%YL%LSP) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IOFF+YDML_GCONF%YGFL%YL%MPSP)=NGRBCLWC
ENDIF
IF(YDML_GCONF%YGFL%YI%LSP) THEN
  YDML_GCONF%YRDIMF%NGRBSP3(IOFF+YDML_GCONF%YGFL%YI%MPSP)=NGRBCIWC
ENDIF

! From SUALSPA1

IF (ALLOCATED(YDML_GCONF%YRDIMF%NGRBSP2)) DEALLOCATE(YDML_GCONF%YRDIMF%NGRBSP2)
ALLOCATE(YDML_GCONF%YRDIMF%NGRBSP2(YDML_GCONF%YRDIMF%NS2D))

IF(YDML_GCONF%YRDIMF%NFD2D > 0) THEN
  MSPTR_SP = 1
  YDML_GCONF%YRDIMF%NGRBSP2(1)=NGRBLNSP
ENDIF

IF(YDML_GCONF%YRDIMF%NFC2D > 0) THEN
  YDML_GCONF%YRDIMF%NGRBSP2(YDML_GCONF%YRDIMF%NFD2D+1)=NGRBZ
ENDIF

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUSPVARIABLES',1,ZHOOK_HANDLE)
END SUBROUTINE SUSPVARIABLES
