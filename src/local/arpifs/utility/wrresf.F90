SUBROUTINE WRRESF(YDGEOMETRY,YDFIELDS,YDMODEL)

!**** *WRRESF*  - write restart files

!     Purpose.
!     --------
!     Write a set of restart files:
!       (1) spectral fields
!       (2) upper-air grid-point fields (t-dt)
!       (3) surface fields
!       (4) upper-air grid-point fields (t)
!       (5) cumulated fluxes
!       (6) instantaneous fluxes
!       (7) restart control file

!**   Interface.
!     ----------
!        *CALL* *WRRESF

!        Explicit arguments :      None.
!        --------------------

!        Implicit arguments :      None.
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        David Dent *ECMWF*
!        Original : 92-04-06

!     Modifications.
!     --------------
!        Modified : 01-06-13 R. El Khatib ISP restart + fix
!        Modified : 01-08-07 by R. El Khatib - Pruning options
!        Modified 08-2002 C. Smith : use "w" as prognostic variable in the
!         semi-lag advection of vertical divergence in the NH model.
!        Modified : 02-06-10 by D.Dent - Additional file for GP fields and  LFASTRES option
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!        Modified : 04-08-12 R. El Khatib Bugfixes + COPY
!        Modified : 22-June-2005 R. El Khatib Fix LFASTRES for MF
!        JJMorcrette 20060721 PP of clear-sky PAR and TOA incident solar radiation
!        M.Hamrud      01-Jul-2006  Revised surface fields
!        JJMorcrette 20070321 Prognostic aerosols in rad and clouds
!        R. El Khatib  10-Apr-2007  Fix again LFASTRES for CFU/XFU
!        M.Hamrud      13-Oct-2008 Use IOSTREAM, one file per task, only FASTRES supported
!        JJMorcrette 20091201 Total and clear-sky direct SW radiation flux at surface 
!        M.Leutbecher  10-Nov-2009 Parametrization tendency perturbation pattern
!        M. Steinheimer Oct-2009  added output for stochastic physics options LSTOPH
!                                 and LSTOPH_SPBS
!        M Ahlgrimm 31 Oct 2011 Surface Downward clear-sky LW and SW fluxes
!        K. Mogensen   26-Jan-2012 Ocean fields when coupling to LIM2
!        T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!        K. Yessad (July 2014): Move some variables.
!        F. Vana  05-Mar-2015  Support for single precision
!        F. Vana & M. Kharoutdinov 09-Feb-2015 CRM fields restart.
!        SJ Lock :   Jan-2016  Removed LSTOPH option and 
!                              enabled multiple perturbation patterns for SPPT
!        R. El Khatib 07-Mar-2016 Pruning of ISP
!        O. Marsden  Aug 2016  Replaced use of SPA3 by YDFIELDS%YRSPEC
!     ------------------------------------------------------------------

USE TYPE_MODEL         , ONLY : MODEL
USE GEOMETRY_MOD       , ONLY : GEOMETRY
USE FIELDS_MOD         , ONLY : FIELDS
USE SURFACE_FIELDS_MIX , ONLY : GPOPER
USE PARKIND1           , ONLY : JPIM, JPRB, JPRD
USE YOMHOOK            , ONLY : LHOOK, DR_HOOK
USE YOMLUN             , ONLY : NULOUT, NULRCF
USE YOMCT0             , ONLY : LELAM
USE YOMCT3             , ONLY : NSTEP
USE YOMSP              , ONLY : SPVOR_FLT, SPDIV_FLT
USE YOMSPSDT           , ONLY : YSPPT_CONFIG, YSPPT
USE YOMIOS             , ONLY : CIOSPRF, CFRCF, LRCFTIME
USE YOMRIP0            , ONLY : NINDAT, NSSSSS

USE YOMRES             , ONLY : JPNRF, CRFTIME, CTIME, CSTEP, N1RFS, N2RFS, LDELRES
USE YOMGRIB            , ONLY : NSTEPLPP
USE YOMMP0             , ONLY : NPRGPNS, NPRGPEW, NPRTRW, NPRTRV, MYPROC
USE IOSTREAM_MIX       , ONLY : SETUP_IOSTREAM, SETUP_IOREQUEST, IO_PUT,&
 &                              CLOSE_IOSTREAM, TYPE_IOSTREAM , TYPE_IOREQUEST,CLOSE_IOREQUEST
USE RANDOM_NUMBERS_MIX , ONLY : RANDOM_NUMBER_RESTARTFILE
USE SPECTRAL_FIELDS_MOD, ONLY : SPECTRAL_FIELD, ASSIGNMENT(=)
USE YOMGPPB            , ONLY : GPARBUF

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(IN)    :: YDGEOMETRY
TYPE(FIELDS)  , INTENT(INOUT) :: YDFIELDS
TYPE(MODEL)    ,INTENT(INOUT), TARGET :: YDMODEL
CHARACTER (LEN = 256) ::  CLFN, CLFS
CHARACTER (LEN = 5)   ::  CL_CMYPROC
CHARACTER (LEN = 8 )  ::  CLDATE
CHARACTER (LEN = 6 )  ::  CLTIME

REAL(KIND=JPRB), ALLOCATABLE :: ZBUF(:),ZBUF2(:,:),ZBUF3(:,:,:)

INTEGER(KIND=JPIM) :: IDAY,IEND,IFCTIM,IHOUR,IINC,IMIN,INUM,&
 & IST,ITEMP,JLEV,JSTGLO,IBL,II,JBLK,ILEN_CPL,ILEN_IO,J2D,&
 & ISIZE,JFLD,JROF,IBUF(1),IFLDS,IOFF,IBLSZ1,IBLSZ2,IBLSZ3,IBLSZ, &
 & IINCS,IH1,IMN1,IS1,ID1,IM1,IY1,ILMO(12)

TYPE(TYPE_IOSTREAM) :: YL_IOSTREAM
TYPE(TYPE_IOREQUEST) :: YL_IOREQUEST
REAL(KIND=JPRB), ALLOCATABLE :: ZALBICE(:),ZTHKICE(:),ZSNTICE(:),&
  & ZTEMPSEA(:),ZTEMPICE(:),ZFRACICE(:)
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

REAL(KIND=JPRB), POINTER  :: GMASS0, GMASSI 

#include "namrcf.nam.h"

#include "fcttim.func.h"

!     ------------------------------------------------------------------

#include "ddhsnd.intfb.h"
#include "updcalsec.intfb.h"
#include "wrresf_time.intfb.h"
#include "wrresf_sfx.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('WRRESF',0,ZHOOK_HANDLE)
ASSOCIATE(YDGFL=>YDFIELDS%YRGFL,YDGMV=>YDFIELDS%YRGMV, YDSURF=>YDFIELDS%YRSURF, YDDIM=>YDGEOMETRY%YRDIM, &
 & YDCFU=>YDFIELDS%YRCFU, YDXFU=>YDFIELDS%YRXFU,&
 &  YDDIMV=>YDGEOMETRY%YRDIMV, YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP, &
 & YDTILEPROP=>YDFIELDS%YEC_PHYS_FIELDS%YRTILEPROP, YDSTOPH=>YDMODEL%YRML_PHY_STOCH%YRSTOPH, &
 & YDRIP=>YDMODEL%YRML_GCONF%YRRIP,YDMCC=>YDMODEL%YRML_AOC%YRMCC,YDTDDH=>YDMODEL%YRML_DIAG%YRTDDH, &
 & YDDPHY=>YDMODEL%YRML_PHY_G%YRDPHY, &
 & YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY,YDEAERSRC=>YDMODEL%YRML_PHY_AER%YREAERSRC,YGFL=>YDMODEL%YRML_GCONF%YGFL, &
 & YDRADF=>YDMODEL%YRML_PHY_RAD%YRRADF,YDLDDH=>YDMODEL%YRML_DIAG%YRLDDH, &
 & YDSLPHY=>YDMODEL%YRML_PHY_G%YRSLPHY)

ASSOCIATE(NDIM0=>YGFL%NDIM0, NDIM9=>YGFL%NDIM9, NUMGPFLDS=>YGFL%NUMGPFLDS, &
 & LCUMFU=>YDCFU%LCUMFU, NFDCFU=>YDCFU%NFDCFU, &
 & NGPBLKS=>YDDIM%NGPBLKS, NPROMA=>YDDIM%NPROMA, &
 & NFLEVG=>YDDIMV%NFLEVG, &
 & NTILES=>YDDPHY%NTILES, &
 & HDCS0=>YDTDDH%HDCS0, HDCS1=>YDTDDH%HDCS1, HDCVB0=>YDTDDH%HDCVB0, &
 & HDCVB1=>YDTDDH%HDCVB1, &
 & LEPAERO=>YDEAERSRC%LEPAERO, &
 & LEPHYS=>YDEPHY%LEPHYS, LSLPHY=>YDEPHY%LSLPHY,&
 & NGPTOT=>YDGEM%NGPTOT, &
 & GFL=>YDGFL%GFL, GFLSLP=>YDGFL%GFLSLP, &
 & GMV=>YDGMV%GMV, GMVS=>YDGMV%GMVS, YT0=>YDGMV%YT0, YT9=>YDGMV%YT9, &
 & LSDDH=>YDLDDH%LSDDH, &
 & LNEMOCOUP=>YDMCC%LNEMOCOUP, LNEMOLIMGET=>YDMCC%LNEMOLIMGET, &
 & CPLNG_FLD=>YDMCC%CPLNG_FLD, LNEMOPARTCOUP=>YDMCC%LNEMOPARTCOUP, &
 & EDRO=>YDRADF%EDRO, EMTC=>YDRADF%EMTC, EMTD=>YDRADF%EMTD, &
 & SRCDIR=>YDRADF%SRCDIR, SRFDIR=>YDRADF%SRFDIR, SRLWDC=>YDRADF%SRLWDC, &
 & SRSWD=>YDRADF%SRSWD, SRSWDC=>YDRADF%SRSWDC, SRSWDCS=>YDRADF%SRSWDCS, &
 & SRSWPAR=>YDRADF%SRSWPAR, SRSWPARC=>YDRADF%SRSWPARC, &
 & SRSWTINC=>YDRADF%SRSWTINC, SRSWUVB=>YDRADF%SRSWUVB, TAUAER=>YDRADF%TAUAER, &
 & TRSC=>YDRADF%TRSC, TRSW=>YDRADF%TRSW, &
 & TSTEP=>YDRIP%TSTEP, &
 & SAVTEND=>YDSLPHY%SAVTEND, &
 & GPSTREAM=>YDSTOPH%GPSTREAM, GPTEMP=>YDSTOPH%GPTEMP, &
 & GPTOTDISS=>YDSTOPH%GPTOTDISS, GPTOTDISS_SMOOTH=>YDSTOPH%GPTOTDISS_SMOOTH, &
 & GPVORTGRAD=>YDSTOPH%GPVORTGRAD, &
 & LSTOPH_SPBS=>YDSTOPH%LSTOPH_SPBS, LSTOPH_SPBS_T=>YDSTOPH%LSTOPH_SPBS_T, &
 & SPSTREAM=>YDSTOPH%SPSTREAM, &
 & SPSTREAM_FORC=>YDSTOPH%SPSTREAM_FORC, SPTEMP=>YDSTOPH%SPTEMP, &
 & SPTEMP_FORC=>YDSTOPH%SPTEMP_FORC, &
 & NDIMSURFL=>YDSURF%NDIMSURFL, &
 & RAHFSTI=>YDTILEPROP%RAHFSTI, REVAPTI=>YDTILEPROP%REVAPTI, &
 & RTSKTI=>YDTILEPROP%RTSKTI, RUSTRTI=>YDTILEPROP%RUSTRTI, &
 & RVSTRTI=>YDTILEPROP%RVSTRTI, &
 & LXFU=>YDXFU%LXFU, NFDXFU=>YDXFU%NFDXFU)
! Associate pointers for variables in namelist
GMASS0 => YDMODEL%YRML_DYN%YRDYN%GMASS0
GMASSI => YDMODEL%YRML_DYN%YRDYN%GMASSI


!     ------------------------------------------------------------------

!*       1.    PREAMBLE
!              --------

!        1.1   FIND TIME STAMP


CALL GSTATS(27,0)

CALL WRRESF_TIME (YDRIP, CTIME, CLFN, CL_CMYPROC)

WRITE(UNIT=NULOUT,FMT='('' CREATE RESTART FILE '',A)') CLFN

CALL SETUP_IOSTREAM(YL_IOSTREAM,'CIO',TRIM(CLFN),CDMODE='w')
CALL SETUP_IOREQUEST(YL_IOREQUEST,'RAW')

IBUF(1)=88801
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=YDFIELDS%YRSPEC%SP3D)
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=YDFIELDS%YRSPEC%SP2D)
IF (LELAM) THEN
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=YDFIELDS%YRSPEC%SP1D)
ENDIF

IF (YSPPT_CONFIG%LSPSDT) THEN
  DO J2D=1,YSPPT%N2D
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=YSPPT%YSPSDT_AR1(J2D)%SF%SP2D)
  ENDDO
ENDIF

IF (YDMODEL%YRML_DYN%YRDYNA%LGRADSP) THEN
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPVOR_FLT)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPDIV_FLT)
ENDIF

!     ------------------------------------------------------------------

!*       3.    WRITE UPPER-AIR AND SURFACE DATA
!              --------------------------------

IBUF(1)=88802
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
IBLSZ1=NPROMA*NFLEVG*YT9%NDIM
IBLSZ2=NPROMA*YT9%NDIMS
IBLSZ3=NPROMA*NFLEVG*NDIM9
IBLSZ =IBLSZ1+IBLSZ2+IBLSZ3
ALLOCATE(ZBUF(NGPBLKS*IBLSZ))
DO JSTGLO=1,NGPTOT,NPROMA
  IBL=(JSTGLO-1)/NPROMA+1
  IEND=MIN(NPROMA,NGPTOT-JSTGLO+1)
  DO JFLD=1,YT9%NDIM
    DO JLEV=1,NFLEVG
      DO JROF=1,IEND
        II=(IBL-1)*IBLSZ&
         & +(JFLD-1)*NPROMA*NFLEVG+(JLEV-1)*NPROMA+JROF
        ZBUF(II)=GMV(JROF,JLEV,YT0%NDIM+JFLD,IBL)
      ENDDO
    ENDDO
  ENDDO
  DO JFLD=1,YT9%NDIMS
    DO JROF=1,IEND
      II=IBLSZ1+(IBL-1)*IBLSZ&
       & +(JFLD-1)*NPROMA+JROF
      ZBUF(II)=GMVS(JROF,YT0%NDIMS+JFLD,IBL)
    ENDDO
  ENDDO
  DO JFLD=1,NDIM9
    DO JLEV=1,NFLEVG
      DO JROF=1,IEND
        II=IBLSZ1+IBLSZ2+(IBL-1)*IBLSZ&
         & +(JFLD-1)*NPROMA*NFLEVG+(JLEV-1)*NPROMA+JROF
        ZBUF(II)=GFL(JROF,JLEV,NDIM0+JFLD,IBL)
      ENDDO
    ENDDO
  ENDDO
ENDDO
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZBUF)
DEALLOCATE(ZBUF)

!*       3.2   WRITE SURFACE GRID-POINT DATA


IBUF(1)=88803
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
ALLOCATE(ZBUF3(NPROMA,NDIMSURFL,NGPBLKS))
DO JBLK=1,NGPBLKS
  CALL GPOPER(YDDIM,YDMODEL%YRML_DYN%YRDYN,'GETALLFLDS',YDSURF,KBL=JBLK,PFIELD=ZBUF3(:,:,JBLK))
ENDDO
CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=ZBUF3)
DEALLOCATE(ZBUF3)

!*       3.3   WRITE UPPER-AIR T GRID-POINT DATA

IF(NUMGPFLDS > 0) THEN

  IBUF(1)=88804
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  IF (YDEPHY%LSPCRM) THEN
    ! workaround to reduce memory requirements (being excessive in case of CRM)
    JFLD = YGFL%NUMGPFLDS/15
    IF(JFLD*15 < YGFL%NUMGPFLDS) JFLD=JFLD+1
    DO II = 1, JFLD
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR4=GFL(:,:,(II-1)*15+1:MIN(II*15,YGFL%NUMGPFLDS),:))
    ENDDO
  ELSE
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR4=GFL(:,:,1:NUMGPFLDS,:))
  ENDIF

ENDIF

!*       3.4  WRITE CUMULATED AND INSTANTANEOUS FLUXES GRID-POINT DATA

IF (LCUMFU) THEN
  IF (NFDCFU > 0) THEN
    IBUF(1)=88805
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    ALLOCATE(ZBUF2(NGPTOT,NFDCFU))
    DO JSTGLO=1,NGPTOT,NPROMA
      IBL=(JSTGLO-1)/YDDIM%NPROMA+1
      IEND=MIN(NPROMA,NGPTOT-JSTGLO+1)
      ZBUF2 (JSTGLO:JSTGLO+IEND-1, :) = YDFIELDS%YRCFU%GFUBUF (1:IEND, :, IBL)
    ENDDO
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=ZBUF2)
    DEALLOCATE(ZBUF2)
  ENDIF
ENDIF

IF (LXFU) THEN
  IF (NFDXFU > 0) THEN
    IBUF(1)=88806
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    ALLOCATE(ZBUF2(NGPTOT,NFDXFU))
    DO JSTGLO=1,NGPTOT,NPROMA
      IBL=(JSTGLO-1)/YDDIM%NPROMA+1
      IEND=MIN(NPROMA,NGPTOT-JSTGLO+1)
      ZBUF2 (JSTGLO:JSTGLO+IEND-1, :) = YDFIELDS%YRXFU%XFUBUF (1:IEND, :, IBL)
    ENDDO
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=ZBUF2)
    DEALLOCATE(ZBUF2)
  ENDIF
ENDIF

!*       3.41  WRITE ADDITIONAL TENDENCIES AND RADIATION FIELD DATA

IF (LEPHYS) THEN
  IF (LSLPHY) THEN
    IBUF(1)=88807
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR4=SAVTEND)
    IF(ALLOCATED(YDGFL%GFLSLP)) THEN
      IF(SIZE(GFLSLP) > 0) THEN
        CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR4=GFLSLP)
      ENDIF
    ENDIF
  ENDIF
  IBUF(1)=88808
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=EMTD)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=TRSW)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=EMTC)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=TRSC)

  IF (LEPAERO) THEN
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR4=TAUAER)
  ENDIF

  IBUF(1)=88809
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  IFLDS=11 
  ISIZE=NPROMA*NGPBLKS
  ALLOCATE(ZBUF(ISIZE*IFLDS))
  IOFF=0
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRLWDC,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWDCS,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWD,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWDC,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(EDRO,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWPAR,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWUVB,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWPARC,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRSWTINC,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRFDIR,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(SRCDIR,(/ISIZE/))
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZBUF)
  DEALLOCATE(ZBUF)
    
  IBUF(1)=88810
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  IFLDS=5
  ISIZE=NPROMA*NGPBLKS*NTILES
  ALLOCATE(ZBUF(ISIZE*IFLDS))
  IOFF=0
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(RUSTRTI,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(RVSTRTI,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(RAHFSTI,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(REVAPTI,(/ISIZE/))
  IOFF=IOFF+ISIZE
  ZBUF(IOFF+1:IOFF+ISIZE)=RESHAPE(RTSKTI ,(/ISIZE/))
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZBUF)
  DEALLOCATE(ZBUF)

ELSE

  IBUF(1)=88808
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
 
  IF (YDMODEL%YRML_PHY_MF%YRPHY%LMPHYS .AND. &
   & (YDMODEL%YRML_PHY_MF%YRPHY%LRAYFM .OR.  &
   &  YDMODEL%YRML_PHY_MF%YRPHY%LRAYFM15)) THEN
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=YDRADF%EMTD) 
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=YDRADF%TRSW)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=YDRADF%EMTU)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=YDRADF%RMOON)
  ENDIF
  IF (YDMODEL%YRML_PHY_MF%YRPARAR%NGPAR > 0) THEN
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPARBUF)
  ENDIF

ENDIF

!*       3.5  WRITE DDH DATA

IF (LSDDH ) THEN

  IBUF(1)=88811
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  IBUF(1)=NPRTRW
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  IBUF(1)=NPRTRV
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)

  CALL DDHSND(HDCVB0,SIZE(HDCVB0),YL_IOSTREAM,YL_IOREQUEST)
  CALL DDHSND(HDCVB1,SIZE(HDCVB1),YL_IOSTREAM,YL_IOREQUEST)
  CALL DDHSND(HDCS0,SIZE(HDCS0),YL_IOSTREAM,YL_IOREQUEST)
  CALL DDHSND(HDCS1,SIZE(HDCS1),YL_IOSTREAM,YL_IOREQUEST)

ENDIF

!*       3.6 WRITE NEMO DATA NEEDED FOR COUPLING TO LIM.

IF (LNEMOCOUP.AND.LNEMOLIMGET) THEN

  IBUF(1)=88812
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  ! The coupling fields are double precision but the arrays used by IO_PUT
  ! might be single precision. In this case we need to allocate a single
  ! precision variable that the same number of bytes of storage as the double
  ! precision coupling field.
  ILEN_CPL = SIZE(CPLNG_FLD(YDMCC%IP_A_ICE_ALBEDO)%D(:,1,1))
  ILEN_IO = ILEN_CPL * &
    & (STORAGE_SIZE(CPLNG_FLD(YDMCC%IP_A_ICE_ALBEDO)%D(:,1,1)) / &
    &  STORAGE_SIZE(ZALBICE))
  ALLOCATE(ZALBICE(ILEN_IO))
  ALLOCATE(ZTHKICE(ILEN_IO))
  ALLOCATE(ZSNTICE(ILEN_IO))
  ALLOCATE(ZTEMPSEA(ILEN_IO))
  ALLOCATE(ZTEMPICE(ILEN_IO))
  ALLOCATE(ZFRACICE(ILEN_IO))
  ZALBICE(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_ICE_ALBEDO)%D(:,1,1),ZALBICE)
  ZTHKICE(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_ICE_THICKNESS)%D(:,1,1),ZTHKICE)
  ZSNTICE(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_SNOW_THICKNESS)%D(:,1,1),ZSNTICE)
  ZTEMPSEA(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_SST)%D(:,1,1),ZTEMPSEA)
  ZTEMPICE(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_ICE_TEMP)%D(:,1,1),ZTEMPICE)
  ZFRACICE(:) = TRANSFER(CPLNG_FLD(YDMCC%IP_A_ICE_FRAC)%D(:,1,1),ZFRACICE)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZALBICE)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZTHKICE)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZSNTICE)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZTEMPSEA)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZTEMPICE)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=ZFRACICE)
  DEALLOCATE(ZALBICE,ZTHKICE,ZSNTICE,ZTEMPSEA,ZTEMPICE,ZFRACICE)
ENDIF

!*       3.6.1 WRITE NEMO DATA NEEDED FOR PARTIAL COUPLING.

IF (LNEMOCOUP.AND.LNEMOPARTCOUP) THEN

  IBUF(1)=88813
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=YDMCC%PARTSST0)
  CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR1=YDMCC%PARTMASK)

ENDIF

CALL CLOSE_IOREQUEST(YL_IOREQUEST)
CALL CLOSE_IOSTREAM(YL_IOSTREAM)


!     ------------------------------------------------------------------

!*       4.    WRITE RESTART CONTROL FILE
!              --------------------------

IF( MYPROC == 1) THEN

!        4.1   MODIFY CONTENTS OF NAMELIST

  WRITE(CSTEP,'(I8)') NSTEP
  IPRGPNSRES=NPRGPNS
  IPRGPEWRES=NPRGPEW
  IPRTRWRES =NPRTRW 
  IPRTRVRES =NPRTRV 

!        4.2   WRITE FILE

  OPEN  (NULRCF,FILE=CFRCF,DELIM="QUOTE")
  WRITE (NULRCF,NAMRCF)
  CLOSE (NULRCF,STATUS='KEEP')
  WRITE(UNIT=NULOUT,FMT='('' CREATE RESTART CONTROL FILE '',A)')CFRCF

!        4.3  OPTIONAL WRITE A COPY FILE WITH TIME STAMP EXTENSION

  IF(LRCFTIME) THEN
     IFCTIM=(NSTEP*TSTEP+0.5_JPRB)
     IINC=IFCTIM/86400
     IINCS=IFCTIM-IINC*86400
     CALL UPDCALSEC(NSSSSS/3600,NDD(NINDAT),NMM(NINDAT),NCCAA(NINDAT),&
        & IINC,IINCS,IH1,IMN1,IS1,ID1,IM1,IY1,ILMO,NULOUT)
     WRITE(CLDATE,'(I4.4,2I2.2)')IY1,IM1,ID1
     WRITE(CLTIME,'(3I2.2)')   IH1,IMN1,IS1
     OPEN  (NULRCF,FILE=TRIM(CFRCF)//'.'//CLDATE//'_'//CLTIME,DELIM="QUOTE")
     WRITE (NULRCF,NAMRCF)
     CLOSE (NULRCF,STATUS='KEEP')
     WRITE(UNIT=NULOUT,FMT='('' CREATE ADDITIONAL RESTART CONTROL FILE '',A)')&
       & TRIM(CFRCF)//'.'//CLDATE//'_'//CLTIME
  ENDIF

ENDIF

!     ---------------------------------------------------------------------
!*       4.4   WRITE STATE OF AUTOREGRESSIVE PROCESS FOR STOCHASTIC PHYSICS
!              ------------------------------------------------------------
IF (LSTOPH_SPBS) THEN
    CLFS='../stoph_spbs' // TRIM (CTIME) //CL_CMYPROC
    WRITE(UNIT=NULOUT,FMT='('' CREATE RESTART FILE FOR SPBS'',A)') CLFS

    CALL SETUP_IOSTREAM(YL_IOSTREAM,'CIO',TRIM(CLFS),CDMODE='w')
    CALL SETUP_IOREQUEST(YL_IOREQUEST,'RAW')

    IBUF(1)=88801
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPSTREAM)
    IBUF(1)=88802
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPSTREAM_FORC)
    IBUF(1)=88803
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPSTREAM)
    IBUF(1)=88804
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPTOTDISS)
    IBUF(1)=88805
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPTOTDISS_SMOOTH)
    IBUF(1)=88806
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
    CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPVORTGRAD)
    IF (LSTOPH_SPBS_T) THEN
      IBUF(1)=88807
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPTEMP)
      IBUF(1)=88808
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR2=SPTEMP_FORC)
      IBUF(1)=88809
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,KR1=IBUF)
      CALL IO_PUT(YL_IOSTREAM,YL_IOREQUEST,PR3=GPTEMP)
    ENDIF
    CALL CLOSE_IOREQUEST(YL_IOREQUEST)
    CALL CLOSE_IOSTREAM(YL_IOSTREAM)
    !write state of the random number generator
    CLFS='../stoph_spbs_rand_nbrs' // TRIM (CTIME) //CL_CMYPROC
    WRITE(UNIT=NULOUT,FMT='('' WRITE RANDOM NUMBER STATE FOR SPBS'',A)') CLFS
    CALL RANDOM_NUMBER_RESTARTFILE( CLFS, 'w',YDMODEL%YRML_PHY_STOCH%YR_RANDOM_STREAMS%STOCHPHYS_SPBS)
    CLFS='../stoph_rvp_rand_nbrs' // TRIM (CTIME) //CL_CMYPROC
    WRITE(UNIT=NULOUT,FMT='('' WRITE RANDOM NUMBER STATE FOR RVP'',A)') CLFS
    CALL RANDOM_NUMBER_RESTARTFILE( CLFS, 'w',YDMODEL%YRML_PHY_STOCH%YR_RANDOM_STREAMS%STOCHPHYS_RVP)
    IF (LSTOPH_SPBS_T) THEN
      CLFS='../stoph_spbs_T_rand_nbrs' // TRIM (CTIME) //CL_CMYPROC
      WRITE(UNIT=NULOUT,FMT='('' WRITE RANDOM NUMBER STATE FOR SPBS_T'',A)') CLFS
      CALL RANDOM_NUMBER_RESTARTFILE( CLFS, 'w',YDMODEL%YRML_PHY_STOCH%YR_RANDOM_STREAMS%STOPH_SPBS_T)
      CLFS='../stoph_rvp_T_rand_nbrs' // TRIM (CTIME) //CL_CMYPROC
      WRITE(UNIT=NULOUT,FMT='('' WRITE RANDOM NUMBER STATE FOR RVP_T'',A)') CLFS
      CALL RANDOM_NUMBER_RESTARTFILE( CLFS, 'w',YDMODEL%YRML_PHY_STOCH%YR_RANDOM_STREAMS%STOPH_RVP_T)
    ENDIF
ENDIF

IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMSE .AND. &
  & YDMODEL%YRML_PHY_MF%YRMSE%NSURFEXCTL > 0) THEN
  CALL WRRESF_SFX (YDGEOMETRY, YDFIELDS, YDMODEL, CLFN)
ENDIF

!     ------------------------------------------------------------------

!*       5.    CONTROL NUMBER OF RESTART FILES
!              -------------------------------

!        5.1   HOW MANY EXIST NOW?

IF(N2RFS >= N1RFS) THEN
  INUM=N2RFS-N1RFS
ELSE
  INUM=JPNRF+N2RFS-N1RFS
ENDIF
IF(LDELRES .AND.INUM >= 1) THEN

!           5.2   TIME TO DELETE THE OLDEST SET

  ITEMP=99
  OPEN (ITEMP,FILE=TRIM (CIOSPRF)//CRFTIME(N1RFS)//CL_CMYPROC)
  CLOSE(ITEMP,STATUS='DELETE')
  IF (LSTOPH_SPBS) THEN
    OPEN (ITEMP,FILE='../stoph_spbs' //CRFTIME(N1RFS)//CL_CMYPROC)
    CLOSE(ITEMP,STATUS='DELETE')
    OPEN (ITEMP,FILE='../stoph_spbs_rand_nbrs' //CRFTIME(N1RFS)//CL_CMYPROC)
    CLOSE(ITEMP,STATUS='DELETE')
    OPEN (ITEMP,FILE='../stoph_rvp_rand_nbrs' //CRFTIME(N1RFS)//CL_CMYPROC)
    CLOSE(ITEMP,STATUS='DELETE')
    IF (LSTOPH_SPBS_T) THEN
      OPEN (ITEMP,FILE='../stoph_spbs_T_rand_nbrs' //CRFTIME(N1RFS)//CL_CMYPROC)
      CLOSE(ITEMP,STATUS='DELETE')
      OPEN (ITEMP,FILE='../stoph_rvp_T_rand_nbrs' //CRFTIME(N1RFS)//CL_CMYPROC)
      CLOSE(ITEMP,STATUS='DELETE')
    ENDIF
  ENDIF
  IF(N1RFS == JPNRF) THEN
    N1RFS=1
  ELSE
    N1RFS=N1RFS+1
  ENDIF
  
ENDIF

!           5.3   ADJUST CIRCULAR BUFFER

CRFTIME(N2RFS)=CTIME
IF(N2RFS == JPNRF) THEN
  N2RFS=1
ELSE
  N2RFS=N2RFS+1
ENDIF

CALL GSTATS(27,1)
!     ------------------------------------------------------------------

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('WRRESF',1,ZHOOK_HANDLE)
END SUBROUTINE WRRESF

