SUBROUTINE COSSMQ(YDGEOMETRY,YDDYN,PGLOB,PDOM,PCOL)
!**** *COSSMQ*  - SURFACE OF LOCAL MASQ

!     Purpose.
!     --------
!     EVALUATE THE SURFACE OF THE LOCAL MASQ

!**   Interface.
!     ----------
!        *CALL* *COSSMQ

!        Explicit arguments :
!        --------------------
!        None

!        Implicit arguments :
!        --------------------
!        None

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------

!     Modifications.
!     --------------
!        Original : 98-02-15 
!        M.Hamrud      01-Oct-2003 CY28 Cleaning
!        G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM and TCSGLEG
!        T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!     ------------------------------------------------------------------
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMDYN   , ONLY : TDYN
USE YOMLUN   , ONLY : NULOUT
USE YOMCST   , ONLY : RPI
USE YOMLCZ   , ONLY : NLEVMIN, NLEVMAX, ALON1, ALON3, ALAT1, ALAT3
USE YOMMP0   , ONLY : NPRGPEW, MY_REGION_EW
USE MPL_MODULE
!     ------------------------------------------------------------------

IMPLICIT NONE
TYPE(GEOMETRY)    ,INTENT(IN)    :: YDGEOMETRY
TYPE(TDYN)        ,INTENT(INOUT) :: YDDYN
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PGLOB 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PDOM 
REAL(KIND=JPRB)   ,INTENT(OUT)   :: PCOL 
REAL(KIND=JPRB) :: ZGM,ZGELAM,ZGELAT
REAL(KIND=JPRB) :: ZSGLOBL,ZSDOML,ZDPL

INTEGER(KIND=JPIM) :: IENDLAT, IENDLON, IGLG, ILEV, IROF, ISTLAT,&
 & ISTLON, JGL, JLEV, JLON  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!     0. init part
!     ----------------      
IF (LHOOK) CALL DR_HOOK('COSSMQ',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDMP=>YDGEOMETRY%YRMP,    &
& YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB, YDCSGLEG=>YDGEOMETRY%YRCSGLEG)
ASSOCIATE(NDGENL=>YDDIM%NDGENL,   NFLEVL=>YDDIMV%NFLEVL,   SIDELP=>YDDYN%SIDELP,   NLOENG=>YDGEM%NLOENG,          &
& NSTAGP=>YDGEM%NSTAGP,   MYLATS=>YDMP%MYLATS, MYLEVS=>YDMP%MYLEVS, NONL=>YDMP%NONL,   NPTRFLOFF=>YDMP%NPTRFLOFF, &
& NSTA=>YDMP%NSTA)
ZGM=0.0_JPRB
ZGELAT=0.0_JPRB
ZGELAM=0.0_JPRB
ZSGLOBL=0.0_JPRB
ZSDOML=0.0_JPRB
ZDPL=0.0_JPRB

WRITE(*,*)ALAT1,ALAT3,ALON1,ALON3

!     1. vertical part
!     ----------------
DO JLEV=1,NFLEVL
  ILEV=MYLEVS(JLEV)
  IF((ILEV >= NLEVMIN).AND.(ILEV <= NLEVMAX)) THEN
    ZDPL=ZDPL+SIDELP(ILEV)
  ENDIF
ENDDO
WRITE(NULOUT,*)"COSSMQ ZDPL",ZDPL
WRITE(*,*)"COSSMQ ZDPL",ZDPL
IF(NPRGPEW > 1) THEN
  CALL MPL_ALLREDUCE(ZDPL,'SUM',LDREPROD=.FALSE.,CDSTRING='COSSMQ:')
ENDIF
PCOL=ZDPL

!     2. horizontal part
!     ------------------
ISTLAT=1
IENDLAT=NDGENL

DO JGL=ISTLAT,IENDLAT
  IGLG=MYLATS(JGL)
  IROF=NSTAGP(JGL)-1
  ISTLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)
  IENDLON=NSTA(NPTRFLOFF+JGL,MY_REGION_EW)+NONL(NPTRFLOFF+JGL,MY_REGION_EW)-1
  DO JLON=ISTLON,IENDLON
    IROF=IROF+1
    ZGM=YDGSGEOM_NB%GM(IROF)
    ZGELAT=YDGSGEOM_NB%GELAT(IROF)
    ZGELAM=YDGSGEOM_NB%GELAM(IROF)
    ZSGLOBL=ZSGLOBL+YDCSGLEG%RW(IGLG)/(ZGM**2*NLOENG(IGLG))

    IF(ALON1 < ALON3) THEN
      IF (ZGELAT >= ALAT3.AND.&
         & ZGELAT <= ALAT1.AND.&
         & (ZGELAM >= ALON1.AND.&
         & ZGELAM <= ALON3)    ) THEN  

        ZSDOML=ZSDOML+YDCSGLEG%RW(IGLG)/(ZGM**2*NLOENG(IGLG))

      ENDIF
    ELSE
      IF (ZGELAT >= ALAT3.AND.&
         & ZGELAT <= ALAT1.AND.&
         & ((ZGELAM >= ALON1.AND.&
         & ZGELAM <= 2.0_JPRB*RPI).OR.&
         & (ZGELAM >= 0.0_JPRB  .AND.&
         & ZGELAM <= ALON3))  ) THEN  

        ZSDOML=ZSDOML+YDCSGLEG%RW(IGLG)/(ZGM**2*NLOENG(IGLG))
      ENDIF
    ENDIF
  ENDDO
ENDDO

WRITE(NULOUT,*)"COSSMQ ZSGLOBL",ZSGLOBL
CALL MPL_ALLREDUCE(ZSGLOBL,'SUM',LDREPROD=.FALSE.,CDSTRING='COSSMQ:')
PGLOB = ZSGLOBL
CALL MPL_ALLREDUCE(ZSDOML,'SUM',LDREPROD=.FALSE.,CDSTRING='COSSMQ:')
PDOM = ZSDOML

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('COSSMQ',1,ZHOOK_HANDLE)
END SUBROUTINE COSSMQ
