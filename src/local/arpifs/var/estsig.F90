SUBROUTINE ESTSIG(YDGEOMETRY,YDML_GCONF)

!*** *ESTSIG*

!   Purpose.
!   --------
!      To estimate the standard deviations of forecast error from
!      the standard deviations of analysis error. The error growth
!      model is that recommended by Savijarvi, 1995. Specifically,
!      de/dt = (a*e+s)(1-e/emax), where e is standard deviation.

!   Interface.
!   ----------

!      CALL ESTSIG

!   Externals.
!   ----------

!   Reference.
!   ----------
!      RD Tech Memo 220.
!      Savijarvi, Monthly Weather Review, Dec 1995, pp212...

!   Author.
!   -------
!    Mike Fisher *ECMWF*
!    Original   95-11-21

!   Modifications.
!   --------------
!    R. El Khatib : 01-08-07 Pruning options
!    E. Holm    03-04-23 New humidity control variable std estimate
!    M.Hamrud      01-Oct-2003 CY28 Cleaning
!    E. Holm    03-12-10 Correction for old humidity analysis
!    E. Holm    04-01-23 New ozone control variable std estimate
!    M.Hamrud      01-Dec-2005 Generalized IO scheme
!    G.Mozdzynski (Feb 2011): OOPS cleaning, use of derived type TGSGEOM
!    A.Inness      24-Jan-2013 Add CHEM fields
!    T. Wilhelmsson and K. Yessad (Oct 2013) Geometry and setup refactoring.
!-----------------------------------------------------------------------

USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE PARKIND1               , ONLY : JPIM, JPRB
USE YOMHOOK                , ONLY : LHOOK, DR_HOOK
USE PARDIM                 , ONLY : JPMXLE, JPMXGL
USE YOMMP0                 , ONLY : NPROC
USE YOMVAR                 , ONLY : NOANEF, NFGFCLEN ,LBGM
USE YOMJG                  , ONLY : JB_STRUCT, JPAEROCV
USE YOMANEB                , ONLY : NANEBU, NANEBV, NANEBZ, NANEBR, NANEBQ, &
 & NANEBT, NANEBVO, NANEBO3, NANEBSP, NANEBRAD, NANEBTCW, ANEBUF, NANEBRH2, &
 & NANEBU10, NANEBV10, NANEBT2, NANEBTO3, NANERADS, NANEBGHG, NANEBAERO, &
 & NANEBCHEM, NGRBCHEM, NGRBGHGASSIM
USE YOMLUN                 , ONLY : NULERR
USE YOMCST                 , ONLY : RPI, RG
USE IOSTREAM_MIX           , ONLY : SETUP_IOSTREAM, SETUP_IOREQUEST, IO_GET,&
 & CLOSE_IOSTREAM, TYPE_IOSTREAM, TYPE_IOREQUEST, IO_INQUIRE, CLOSE_IOREQUEST
USE YOMFGER                , ONLY : JPMAXVAR
USE YOM_GRIB_CODES         , ONLY : NGRBLNSP, NGRBSP, NGRBT, NGRBU, NGRBVO, &
 & NGRBGH, NGRBQ, NGRBBTMP, NGRBO3, NGRBTCWV, NGRBR, NGRB10U, NGRBTCO3, NGRB2T, &
 & NGRBAERLG, NGRBAERSM
USE YOM_YGFL               , ONLY : JPGHG_ASSIM, JPCHEM_ASSIM
USE YOMSPJB                , ONLY : SPA7

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(IN)  :: YDGEOMETRY
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT):: YDML_GCONF

CHARACTER (16) :: CLIMFILE='stdev_of_climate'
INTEGER(KIND=JPIM) :: INROWS, INVARS
INTEGER(KIND=JPIM) :: ILONE(JPMXGL)
INTEGER(KIND=JPIM) :: IFIELDS(JPMAXVAR), INLEVS(JPMAXVAR)
INTEGER(KIND=JPIM) :: ILEVS(JPMXLE,JPMAXVAR)
INTEGER(KIND=JPIM) :: IGRIB2D(JPMXLE*JPMAXVAR),ILEVS2D(JPMXLE*JPMAXVAR)
REAL(KIND=JPRB) :: ZLATE(JPMXGL), ZLON0E(JPMXGL), ZDLONE(JPMXGL) 
REAL(KIND=JPRB) :: ZPFIELD(YDGEOMETRY%YRGEM%NGPTOT)
REAL(KIND=JPRB) :: ZA(JPMXLE,JPMAXVAR), ZB(JPMXLE,JPMAXVAR)
REAL(KIND=JPRB), ALLOCATABLE :: ZEGRID(:,:,:), ZIGRID(:,:), ZPRSFI(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZVALH(:,:), ZVALM(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZPRSHO(:,:), ZPRSFO(:,:)
REAL(KIND=JPRB), ALLOCATABLE :: ZLATM(:), ZLONM(:)
REAL(KIND=JPRB) :: ZANE(YDGEOMETRY%YRDIM%NPROMA,NOANEF), ZFCE(YDGEOMETRY%YRDIM%NPROMA,JB_STRUCT%JB_DATA%NOFCEF)
REAL(KIND=JPRB) ,DIMENSION(10) ::ZXT=(/&
 & 0.667_JPRB,0.714_JPRB,0.833_JPRB,1.00_JPRB,1.00_JPRB,1.00_JPRB,1.00_JPRB,&
 & 1.00_JPRB,1.00_JPRB,1.00_JPRB/)  
INTEGER(KIND=JPIM), PARAMETER :: JPVARS=16+JPGHG_ASSIM+JPAEROCV+JPCHEM_ASSIM
INTEGER(KIND=JPIM), DIMENSION(JPVARS) :: IOFF_L
INTEGER(KIND=JPIM), ALLOCATABLE :: ICODES(:)
INTEGER(KIND=JPIM) :: IOMASTER,INPTE,INFLDS,IOFF,I,ILAT,IX,INPTM&
 & ,ISTC,IENDC,ILEVX,IFLDOFF,IVAR&
 & ,JVAR,JLEV,JLAT,JLON,J,JKGLO,JJ,JK, JFIELD, IFCEBQ, IPTJBVO, IPTJBQ&
 & ,IPTJBO3,IPTJBGHG(YDML_GCONF%YGFL%NGHG_ASSIM),IPTJBCHEM(YDML_GCONF%YGFL%NCHEM_ASSIM)&
 & ,IPTJBAERO(JB_STRUCT%JB_DATA%NAEROCV),II
REAL(KIND=JPRB) :: ZGROW,ZDT,ZLIMFAC,ZLIMFAC_GHG,ZMODFAC&
 & ,ZLAT,Z10DEG,ZDX,ZFACHR,ZSIGMOD,ZSIGMODV,ZSIGLIM,ZSD
LOGICAL :: LLRAD,LLSLVAR,LLOBSV
TYPE(TYPE_IOSTREAM) :: YL_IOSTREAM
TYPE(TYPE_IOREQUEST) :: YL_IOREQUEST
REAL(KIND=JPRB) :: ZHOOK_HANDLE
INTEGER(KIND=JPIM) :: IGHG, JGHG, IAERO, ICHEM, JPCODES, JCHEM

!--- zgrow   = error growth rate time constant (per day) and
!--- zdt     = time between analysis cycles (days)
!--- zlimfac : forecast error asymptotes to zlimfac*FCEMN(jlev,jvar)
!--- zmodfac : model error contributes zmodfac*FCEMN(jlev,jvar) per day

#include "abor1.intfb.h"
#include "commfce2.intfb.h"
#include "gphpre.intfb.h"
#include "speree.intfb.h"
#include "suhifce.intfb.h"
#include "suvifce.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('ESTSIG',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,   YDDIMV=>YDGEOMETRY%YRDIMV,YDGEM=>YDGEOMETRY%YRGEM, YDGSGEOM_NB=>YDGEOMETRY%YRGSGEOM_NB,   &
& YDVAB=>YDGEOMETRY%YRVAB,  YGFL=>YDML_GCONF%YGFL)

ASSOCIATE(NCHEM_ASSIM=>YGFL%NCHEM_ASSIM, NGHG_ASSIM=>YGFL%NGHG_ASSIM,   NPROMA=>YDDIM%NPROMA,   NFLEVG=>YDDIMV%NFLEVG,   &
& NGPTOT=>YDGEM%NGPTOT)
! BEN - check that NAEROCV is equal to JPAEROCV (=1 by default)
IF(JB_STRUCT%JB_DATA%NAEROCV > 0) THEN
 IF(JB_STRUCT%JB_DATA%NAEROCV /= JPAEROCV) THEN
   WRITE(NULERR,*) ' ESTSIG: NAEROCV, JPAEROCV', JB_STRUCT%JB_DATA%NAEROCV, JPAEROCV
   CALL ABOR1('ESTSIG: NAEROCV /= JPAEROCV - please change JPAEROCV in yomjg.F90 and recompile')
 ENDIF
ENDIF

ZGROW     =  0.4_JPRB
ZDT       =  NFGFCLEN/24.0_JPRB
ZLIMFAC   = 10.0_JPRB
ZLIMFAC_GHG   = 3.0_JPRB
ZMODFAC   =  0.1_JPRB

IOFF_L=(/NANEBSP,NANEBT,NANEBU,NANEBV,NANEBVO&
 & ,NANEBZ,NANEBQ,NANEBR,NANEBO3,NANEBTO3,NANEBRAD,NANEBTCW&
 & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2,NANEBGHG,NANEBAERO,NANEBCHEM/)

IF(JPAEROCV == 2) THEN
JPCODES=16+JPGHG_ASSIM+JPAEROCV+JPCHEM_ASSIM
ALLOCATE(ICODES(JPCODES))
ICODES=(/NGRBSP,NGRBT,NGRBU,NGRBU,NGRBVO&
 & ,NGRBGH,NGRBQ,NGRBR,NGRBO3,NGRBTCO3,NGRBBTMP,NGRBTCWV&
 & ,NGRBR,NGRB10U,NGRB10U,NGRB2T,NGRBGHGASSIM&
 & ,NGRBAERLG, NGRBAERSM, NGRBCHEM/)
ELSE
JPCODES=16+JPGHG_ASSIM+JPAEROCV+JPCHEM_ASSIM
ALLOCATE(ICODES(JPCODES))
ICODES=(/NGRBSP,NGRBT,NGRBU,NGRBU,NGRBVO&
 & ,NGRBGH,NGRBQ,NGRBR,NGRBO3,NGRBTCO3,NGRBBTMP,NGRBTCWV&
 & ,NGRBR,NGRB10U,NGRB10U,NGRB2T,NGRBGHGASSIM&
 & ,NGRBAERLG,NGRBCHEM/)
ENDIF
  
ICODES(1)=NGRBLNSP

!--- look up varaibles in SPJB_VARS_INFO

IFCEBQ  = HUGE(IFCEBQ)
IPTJBVO = HUGE(IPTJBVO)
IPTJBQ  = HUGE(IPTJBQ)
IPTJBO3 = HUGE(IPTJBO3)
IF (NGHG_ASSIM > 0) IPTJBGHG(:) = HUGE(IPTJBGHG(:))
IF (JB_STRUCT%JB_DATA%NAEROCV> 0) IPTJBAERO(:) = HUGE(IPTJBAERO(:))
IF (NCHEM_ASSIM > 0) IPTJBCHEM(:) = HUGE(IPTJBCHEM(:))
IGHG=0
IAERO=0
ICHEM=0
DO JFIELD=1,JB_STRUCT%CONFIG%N_SPJB_VARS
  IF (JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBVO) THEN
    IPTJBVO=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBQ) THEN
    IPTJBQ=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
    IFCEBQ=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTFCE-1
  ELSEIF (JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBO3) THEN
    IPTJBO3=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (ANY(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBGHGASSIM)) THEN
    DO JGHG=1,JPGHG_ASSIM
      IF(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBGHGASSIM(JGHG)) THEN
        IGHG=IGHG+1
        IPTJBGHG(IGHG)=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
      ENDIF
    ENDDO
  ELSEIF (JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBAERLG) THEN
      IF(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBAERLG) THEN
        IAERO=IAERO+1
        IPTJBAERO(IAERO)=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
      ENDIF
  ELSEIF (JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBAERSM) THEN
      IF(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBAERSM) THEN
        IAERO=IAERO+1
        IPTJBAERO(IAERO)=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
      ENDIF
  ELSEIF (ANY(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBCHEM)) THEN
    DO JCHEM=1,JPCHEM_ASSIM
      IF(JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE_FCE==NGRBCHEM(JCHEM)) THEN
        ICHEM=ICHEM+1
        IPTJBCHEM(ICHEM)=JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
      ENDIF
    ENDDO
  ENDIF

ENDDO

!--- read climatological variances

IF (.NOT.JB_STRUCT%CONFIG%LCLMSFCE) THEN
  INVARS=0
ELSE
!         1.    Determine the forecast grid parameters
  IOMASTER=1
  CALL SETUP_IOSTREAM(YL_IOSTREAM,'CIO',TRIM(CLIMFILE),CDMODE='r',&
      &               KIOMASTER=IOMASTER)
  CALL SETUP_IOREQUEST(YL_IOREQUEST,'GRIDPOINT_FIELDS',LDGRIB=.TRUE.,&
   & LDLATLONG=.TRUE.,KCHUNKSIZE=1)
  CALL IO_INQUIRE(YL_IOSTREAM,YL_IOREQUEST,&
   & KLATS=INROWS,KLONS=ILONE,PLATS=ZLATE,&
   & KNVARS=INVARS,KPARAMS=IFIELDS,KNLEVS=INLEVS,&
   & PA=ZA,PB=ZB,KLEVS=ILEVS)
  CALL CLOSE_IOREQUEST(YL_IOREQUEST)
  CALL CLOSE_IOSTREAM(YL_IOSTREAM)
  ZLON0E(:) = 0.0_JPRB

  ZDLONE(1:INROWS) = 2.0_JPRB * RPI / ILONE(1:INROWS)

  INPTE = SUM(ILONE(1:INROWS))
  INFLDS = SUM(INLEVS(1:INVARS))

  II=0
  DO JVAR=1,INVARS
    DO JLEV=1,INLEVS(JVAR)
      II=II+1
      IGRIB2D(II)=IFIELDS(JVAR)
      ILEVS2D (II)=ILEVS(JLEV,JVAR)
    ENDDO
  ENDDO

  ALLOCATE (ZEGRID(INPTE,INFLDS,1))

  CALL SETUP_IOSTREAM(YL_IOSTREAM,'CIO',TRIM(CLIMFILE),CDMODE='r',KIOMASTER=1)
  CALL SETUP_IOREQUEST(YL_IOREQUEST,'GRIDPOINT_FIELDS',LDGRIB=.TRUE.,&
   & KGRIB2D=IGRIB2D(1:INFLDS),KLEVS2D=ILEVS2D(1:INFLDS),&
   & KPROMA=INPTE,LDISTRIBUTE=.FALSE.,LDLATLONG=.TRUE.,KCHUNKSIZE=1)
  CALL IO_GET(YL_IOSTREAM,YL_IOREQUEST,PR3=ZEGRID)
  CALL CLOSE_IOREQUEST(YL_IOREQUEST)
  CALL CLOSE_IOSTREAM(YL_IOSTREAM)
  IF (NPROC>1) CALL COMMFCE2(ZEGRID,INPTE,INFLDS)

!---   Construct std of difference of two fields.
  CALL GSTATS(1871,0)
  DO JVAR=1,INFLDS
    DO JLEV=1,INPTE
      ZEGRID(JLEV,JVAR,1)=SQRT(2.0_JPRB)*ZEGRID(JLEV,JVAR,1)
    ENDDO
  ENDDO
  CALL GSTATS(1871,1)

!---   Increase climatological errors in the tropics

  IF (JB_STRUCT%CONFIG%LFACHRO) THEN
    IOFF = 0
    CALL GSTATS(1887,0)
    DO JVAR=1,INVARS
      DO JLEV=1,INLEVS(JVAR)
        IOFF = IOFF+1
        IF (IFIELDS(JVAR) /= NGRBQ) THEN
          I = 0
          DO JLAT=1,INROWS
            ZLAT=ABS(ZLATE(JLAT))
            Z10DEG=RPI/18._JPRB
            ILAT=MIN(INT(ZLAT/Z10DEG),8)
            IX=ILAT+1
            ZDX=ZLAT/Z10DEG-ILAT
            ZFACHR=ZXT(6)/( ZXT(IX)+ZDX*(ZXT(IX+1)-ZXT(IX)) )

            DO JLON=1,ILONE(JLAT)
              I = I+1
              ZEGRID(I,IOFF,1) = ZEGRID(I,IOFF,1)*ZFACHR
            ENDDO
          ENDDO
        ENDIF
      ENDDO
    ENDDO
    CALL GSTATS(1887,1)
  ENDIF

!--- interpolate in the horizontal

  INPTM = NGPTOT

  ALLOCATE (ZLATM(INPTM),ZLONM(INPTM),ZIGRID(INPTM,INFLDS))

  DO J=1,NGPTOT
    ZLONM(J)=YDGSGEOM_NB%GELAM(J)
    ZLATM(J)=YDGSGEOM_NB%GELAT(J)
  ENDDO

  CALL SUHIFCE(ZLON0E,ZDLONE,ZLATE,INROWS,ILONE,ZLONM,ZLATM,INPTM,&
   & INPTE,INFLDS,ZEGRID(:,:,1),ZIGRID)  

  DEALLOCATE (ZEGRID,ZLONM,ZLATM)

!--- transform background surface pressure to gridpoint space

  CALL SPEREE(YDGEOMETRY,1,1,SPA7%SP,ZPFIELD)
ENDIF

!--- apply error growth model to standard deviations of analysis error

ALLOCATE (ZPRSFO(NPROMA,NFLEVG))
ALLOCATE (ZPRSHO(NPROMA,0:NFLEVG))

CALL GSTATS(1995,0)
MESSP_LOOP : DO JKGLO=1,NGPTOT,NPROMA
  ISTC=1
  IENDC=MIN(NPROMA,NGPTOT-JKGLO+1)
  ZANE = ANEBUF (:, :, 1+(JKGLO-1)/YDGEOMETRY%YRDIM%NPROMA)


! zfce needed only for the 'old' humidity analysis (q control variable).
  IF(.NOT. JB_STRUCT%CONFIG%LJB_NONLINEAR_CVHUM) THEN
    ISTC=1
    IENDC=MIN(NPROMA,NGPTOT-JKGLO+1)
    ZFCE = JB_STRUCT%JB_DATA%FCE%BUF (:, :, 1+(JKGLO-1)/YDGEOMETRY%YRDIM%NPROMA)

  ENDIF

  MESSP_VAR_LOOP : DO JVAR=1,JPVARS
    IOFF = IOFF_L(JVAR)
!--- Not allocated variables are characterized by negative offset.
    IF (IOFF < 0) CYCLE MESSP_VAR_LOOP
!--- If dynamic error growth, just VO needs adjusting.
    IF (LBGM .AND. IOFF /= NANEBVO) CYCLE MESSP_VAR_LOOP

!--- Single level variables, ps, integrated columns, radiances
!--- Add ozone and/or humidity for new control variable
    LLSLVAR=ANY(IOFF == (/NANEBSP,NANEBTCW,NANEBTO3&
     & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2/))  
    LLRAD=IOFF == NANEBRAD

    IF (JB_STRUCT%CONFIG%LJB_NONLINEAR_CVHUM .AND.&
      & JB_STRUCT%CONFIG%LJB_NONLINEAR_CVO3) THEN
      LLOBSV=ANY(IOFF == (/NANEBRAD,NANEBTCW,NANEBTO3&
       & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2,NANEBR,NANEBQ,NANEBO3/))  
    ELSEIF (JB_STRUCT%CONFIG%LJB_NONLINEAR_CVHUM .AND.&
          & .NOT. JB_STRUCT%CONFIG%LJB_NONLINEAR_CVO3) THEN
      LLOBSV=ANY(IOFF == (/NANEBRAD,NANEBTCW,NANEBTO3&
       & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2,NANEBR,NANEBQ/))  
    ELSEIF( .NOT. JB_STRUCT%CONFIG%LJB_NONLINEAR_CVHUM .AND.&
          & JB_STRUCT%CONFIG%LJB_NONLINEAR_CVO3) THEN
      LLOBSV=ANY(IOFF == (/NANEBRAD,NANEBTCW,NANEBTO3&
       & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2,NANEBR,NANEBO3/))  
    ELSE
      LLOBSV=ANY(IOFF == (/NANEBRAD,NANEBTCW,NANEBTO3&
       & ,NANEBRH2,NANEBU10,NANEBV10,NANEBT2,NANEBR/))  
    ENDIF

!--- Set number of levels/channels.
    IF (LLRAD) THEN
      ILEVX=NANERADS
    ELSEIF(LLSLVAR) THEN
      ILEVX=1
    ELSE
      ILEVX=NFLEVG
    ENDIF

    IFLDOFF = -1
    DO JJ=1,INVARS
      IF (IFIELDS(JJ) == ICODES(JVAR)) THEN
        IFLDOFF = SUM(INLEVS(1:JJ-1))
        IVAR    = JJ
        EXIT
      ENDIF
    ENDDO

    ALLOCATE (ZVALM (NPROMA,ILEVX))
    ZVALM(:,:)=0.0_JPRB

!--- if the field has been provided, interpolate it in the vertical
!--- Radiances and single level fields can just be copied.

    IF (IFLDOFF >= 0) THEN

!--- Radiances and single lev fields without vertical interpolation.
      IF(LLRAD .OR. LLSLVAR) THEN
        IF(INLEVS(IVAR) /= ILEVX) THEN
          WRITE(NULERR,*) ' ESTSIG: ioff,jvar,icodes(jvar)'&
           & ,IOFF,JVAR,ICODES(JVAR),INLEVS(IVAR),ILEVX  
          CALL ABOR1('ESTSIG: INLEVS(IVAR) /= ilevx')
        ENDIF
        DO JLEV=1,ILEVX
          DO JK=ISTC,IENDC
            ZVALM(JK,JLEV) = ZIGRID(JKGLO-1+JK,JLEV+IFLDOFF)
          ENDDO
        ENDDO
      ELSE
        DO JK=ISTC,IENDC
          ZPRSHO(JK,NFLEVG)= EXP(ZPFIELD(JKGLO-1+JK))
        ENDDO

        CALL GPHPRE(NPROMA,NFLEVG,ISTC,IENDC,YDVAB,ZPRSHO,PRESF=ZPRSFO)

        ALLOCATE (ZPRSFI(NPROMA,  INLEVS(IVAR)))
        ALLOCATE (ZVALH (NPROMA,0:INLEVS(IVAR)))

        DO JLEV=1,INLEVS(IVAR)
          DO JK=ISTC,IENDC
            ZPRSFI(JK,JLEV) =  ZA(JLEV,IVAR)&
             & + ZPRSHO(JK,NFLEVG)*ZB(JLEV,IVAR)  
            ZVALH (JK,JLEV) = ZIGRID(JKGLO-1+JK,JLEV+IFLDOFF)
          ENDDO
        ENDDO

        ZVALH(ISTC:IENDC,0) = ZVALH(ISTC:IENDC,1)

        IF (ISTC /= 1) THEN
          WRITE (NULERR,*) 'ESTSIG calls SUVIFCE with istc<>1'
          CALL ABOR1 ('ESTSIG calls SUVIFCE with istc /= 1')
        ENDIF

        CALL SUVIFCE(ZVALH,ZPRSFI,NPROMA,IENDC-ISTC+1,&
         & INLEVS(IVAR),ZVALM,ZPRSFO,NFLEVG)

        IF (ICODES(JVAR) == NGRBGH) THEN
          ZVALM(ISTC:IENDC,:) = RG*ZVALM(ISTC:IENDC,:)
        ENDIF

        DEALLOCATE (ZPRSFI)
        DEALLOCATE (ZVALH)
      ENDIF

!--- if the field was not provided, use the default

    ELSE
      DO JLEV=1,ILEVX
        IF(LLOBSV) THEN
!         The climate variance is not known for some fields.
!         We then simply set it large compared to zane, i.e. no assymtote.
          WHERE(ZANE(ISTC:IENDC,JLEV+IOFF) <= 0.0_JPRB)
            ZVALM(ISTC:IENDC,JLEV)=100._JPRB
          ELSEWHERE
            ZVALM(ISTC:IENDC,JLEV)=&
             & 100._JPRB*SQRT(ZANE(ISTC:IENDC,JLEV+IOFF))  
          ENDWHERE
        ELSEIF(IOFF == NANEBU .OR. IOFF == NANEBV) THEN
          ZVALM(ISTC:IENDC,JLEV) = ZLIMFAC*JB_STRUCT%FCEMN%U(JLEV)
        ELSEIF(IOFF == NANEBZ) THEN
          ZVALM(ISTC:IENDC,JLEV) = ZLIMFAC*JB_STRUCT%FCEMN%Z(JLEV)
        ELSEIF(IOFF == NANEBQ) THEN
!       If JB_STRUCT%CONFIG%LJB_NONLINEAR_CVHUM, Q is done within LLOBSV, above
          IF (IFCEBQ/=HUGE(IFCEBQ)) THEN
             ZVALM(ISTC:IENDC,JLEV) = ZLIMFAC*ZFCE(ISTC:IENDC,JLEV+IFCEBQ)
          ELSE
            CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR Q NOT IN JB: 1')
          ENDIF
        ELSEIF(IOFF == NANEBT) THEN
          ZVALM(ISTC:IENDC,JLEV) = ZLIMFAC*JB_STRUCT%FCEMN%T(JLEV)
        ELSEIF(IOFF == NANEBVO) THEN
          IF (IPTJBVO/=HUGE(IPTJBVO)) THEN
            ZVALM(ISTC:IENDC,JLEV) =&
                        & ZLIMFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBVO)
           ELSE
            CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR VORTICITY NOT IN JB: 1')
          ENDIF
        ELSEIF(IOFF == NANEBO3) THEN
!       If JB_STRUCT%CONFIG%LJB_NONLINEAR_CVO3, O3 is done within LLOBSV, above
          IF (IPTJBO3/=HUGE(IPTJBO3)) THEN
            ZVALM(ISTC:IENDC,JLEV) =&
                        & ZLIMFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBO3)
           ELSE
            CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR O3 NOT IN JB: 1')
          ENDIF
!stj
        ELSEIF(ANY(IOFF == NANEBGHG)) THEN
           DO IGHG=1,NGHG_ASSIM
            IF (IOFF == NANEBGHG(IGHG)) THEN
              IF (IPTJBGHG(IGHG)/=HUGE(IPTJBGHG(IGHG))) THEN
                ZVALM(ISTC:IENDC,JLEV) =&
                   & ZLIMFAC_GHG*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBGHG(IGHG))
               ELSE
                CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR GHG NOT IN JB: 1')
              ENDIF
            ENDIF
          ENDDO
        ELSEIF(ANY(IOFF == NANEBAERO)) THEN
          DO IAERO=1,JB_STRUCT%JB_DATA%NAEROCV
            IF (IOFF == NANEBAERO(IAERO)) THEN
              IF (IPTJBAERO(IAERO)/=HUGE(IPTJBAERO(IAERO))) THEN
                ZVALM(ISTC:IENDC,JLEV) =&
                   & ZLIMFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBAERO(IAERO))
               ELSE
                CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR AEROSOL NOT IN JB: 1')
              ENDIF
            ENDIF
          ENDDO
        ELSEIF(ANY(IOFF == NANEBCHEM)) THEN
          DO ICHEM=1,NCHEM_ASSIM
            IF (IOFF == NANEBCHEM(ICHEM)) THEN
              IF (IPTJBCHEM(ICHEM)/=HUGE(IPTJBCHEM(ICHEM))) THEN
                ZVALM(ISTC:IENDC,JLEV) =&
                   & ZLIMFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBCHEM(ICHEM))
               ELSE
                CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR CHEM NOT IN JB: 1')
              ENDIF
            ENDIF
          ENDDO
!stj
        ELSEIF(IOFF == NANEBSP) THEN
          ZVALM(ISTC:IENDC,JLEV) = ZLIMFAC*JB_STRUCT%FCEMN%PS
        ENDIF
      ENDDO
    ENDIF

    DO JLEV=1,ILEVX
      IF (IOFF == NANEBU .OR. IOFF == NANEBV) THEN
        ZSIGMODV = ZMODFAC * JB_STRUCT%FCEMN%U(JLEV)
      ELSEIF(IOFF == NANEBZ) THEN
        ZSIGMODV = ZMODFAC * JB_STRUCT%FCEMN%Z(JLEV)
      ELSEIF(IOFF == NANEBQ) THEN
        IF (IPTJBQ/=HUGE(IPTJBQ)) THEN
          ZSIGMODV = ZMODFAC * JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBQ)
        ELSE
          CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR Q NOT IN JB: 2')
        ENDIF
      ELSEIF(IOFF == NANEBT) THEN
        ZSIGMODV = ZMODFAC * JB_STRUCT%FCEMN%T(JLEV)
      ELSEIF(IOFF == NANEBVO) THEN
        IF (IPTJBVO/=HUGE(IPTJBVO)) THEN
          ZSIGMODV = ZMODFAC * JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBVO)
        ELSE
          CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR VORTICITY NOT IN JB: 2')
        ENDIF
      ELSEIF(IOFF == NANEBO3) THEN
        IF (IPTJBO3/=HUGE(IPTJBO3)) THEN
          ZSIGMODV = ZMODFAC * JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBO3)
        ELSE
          CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR O3 NOT IN JB: 2')
        ENDIF


!stj
      ELSEIF(ANY(IOFF == NANEBGHG)) THEN
        DO IGHG=1,NGHG_ASSIM
          IF (IOFF == NANEBGHG(IGHG)) THEN
            IF (IPTJBGHG(IGHG)/=HUGE(IPTJBGHG(IGHG))) THEN
              ZSIGMODV = ZMODFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBGHG(IGHG))
            ELSE
              CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR GHG NOT IN JB: 2')
            ENDIF
          ENDIF
        ENDDO
! BEN
      ELSEIF(ANY(IOFF == NANEBAERO)) THEN
        DO IAERO=1,JB_STRUCT%JB_DATA%NAEROCV
          IF (IOFF == NANEBAERO(IAERO)) THEN
            IF (IPTJBAERO(IAERO)/=HUGE(IPTJBAERO(IAERO))) THEN
              ZSIGMODV = ZMODFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBAERO(IAERO))
            ELSE
              CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR AERO NOT IN JB: 2')
            ENDIF
          ENDIF
        ENDDO
!stj
      ELSEIF(ANY(IOFF == NANEBCHEM)) THEN
        DO ICHEM=1,NCHEM_ASSIM
          IF (IOFF == NANEBCHEM(ICHEM)) THEN
            IF (IPTJBCHEM(ICHEM)/=HUGE(IPTJBCHEM(ICHEM))) THEN
              ZSIGMODV = ZMODFAC*JB_STRUCT%JB_DATA%FCEMN3D(JLEV,IPTJBCHEM(ICHEM))
            ELSE
              CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR CHEM NOT IN JB: 2')
            ENDIF
          ENDIF
        ENDDO


      ELSEIF(LLOBSV) THEN
        ZSIGMODV = 0.0_JPRB
      ELSEIF(IOFF == NANEBSP) THEN
        ZSIGMODV = ZMODFAC * JB_STRUCT%FCEMN%PS
      ELSE
        ZSIGMODV = 0.0_JPRB
      ENDIF

      DO JK=ISTC,IENDC
        ZSIGLIM = ZVALM(JK,JLEV)
        ZSIGMOD = MIN (ZSIGLIM/ZDT,ZSIGMODV)
        ZSD = SQRT(MAX(0.0_JPRB,ZANE(JK,JLEV+IOFF)))
        
        IF(LBGM) THEN
          ZSD=MIN(MAX(ZSD,ZSIGLIM*0.1_JPRB),ZSIGLIM*0.9_JPRB)
        ELSE
          ZSD =  ZSIGLIM&
           & - MAX(0.0_JPRB , (ZSIGLIM  + ZSIGMOD/ZGROW)&
           & /( 1.0_JPRB - (ZSD + ZSIGMOD/ZGROW)&
           & *EXP((ZGROW+ZSIGMOD/ZSIGLIM)*ZDT)&
           & /(ZSD - ZSIGLIM) ) )  
        ENDIF

        ZANE(JK,JLEV+IOFF) = ZSD*ZSD
      ENDDO
    ENDDO
    DEALLOCATE (ZVALM)
  ENDDO MESSP_VAR_LOOP

  ANEBUF (:, :, 1+(JKGLO-1)/YDGEOMETRY%YRDIM%NPROMA) = ZANE

ENDDO MESSP_LOOP
CALL GSTATS(1995,1)

DEALLOCATE(ICODES)
DEALLOCATE (ZPRSFO)
DEALLOCATE (ZPRSHO)

IF (JB_STRUCT%CONFIG%LCLMSFCE) DEALLOCATE (ZIGRID)

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('ESTSIG',1,ZHOOK_HANDLE)
END SUBROUTINE ESTSIG
