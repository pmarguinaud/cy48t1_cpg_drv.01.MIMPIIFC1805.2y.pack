SUBROUTINE SUJBWAVELET_STDEVS(YDGEOMETRY,YDML_GCONF,YDCHEM,YD_JB_STRUCT)
!DEC$ OPTIMIZE:1

!      SUJBWAVELET_STDEVS - Set up profiles of background-error standard deviation for wavelet Jb

!     Purpose: Set up profiles of background-error standard deviation for wavelet Jb
!     -------

!     Interface: called from SUJBCOV
!     ---------

!     Author: Mike Fisher 2015-09-09 (split out from sujbwavelet)
!     ------

!     Modifications:
!     -------------

!     -----------------------------------------------------------------------------

USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE PARKIND1,   ONLY : JPIM, JPRB, JPRD
USE YOMHOOK,    ONLY : LHOOK, DR_HOOK
USE YOMLUN,     ONLY : NULOUT
USE YOMVERT,    ONLY : VP00
USE YOMJG,      ONLY : TYPE_JB_STRUCT
USE YOM_GRIB_CODES,    ONLY : NGRBVO, NGRBD, NGRBT, NGRBQ, NGRBO3, NGRBLNSP, NGRBAERLG, NGRBAERSM
USE YOMVAR,     ONLY : LACV, RCOEFCO2, RCOEFCH4
! Parameters for volcano SO2 JB
USE YOMCHEM,    ONLY : TCHEM
USE YOMVOLCANO, ONLY : NVOCDATES, IVOCSTART, ILVOCJB
USE YOMRIP0,    ONLY : NINDAT   ,NSSSSS
USE YOMCST,     ONLY : RDAY
USE YOMJBACV,   ONLY : YRACVGRIB

!     -----------------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)               ,INTENT(IN)    :: YDGEOMETRY
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT) :: YDML_GCONF
TYPE(TCHEM)                  ,INTENT(INOUT) :: YDCHEM
TYPE(TYPE_JB_STRUCT)         ,INTENT(INOUT) :: YD_JB_STRUCT

INTEGER(KIND=JPIM) :: JN,JJ, ILV950, JL, IL,JFIELD, IPTJBVO, IPTJBD, IPTJBTU, IPTJBQ,IPTJBO3, IPTJBPSU, IPTJB,&
 &                    IPTJBGHG(YDML_GCONF%YGFL%NGHG_ASSIM),IGHG,IPTJBCHEM(YDML_GCONF%YGFL%NCHEM_ASSIM),&
 &                    ICHEM, ICHEM_ASSIM, IPTJBAERO(YD_JB_STRUCT%JB_DATA%NAEROCV), IAERO,JK, IGHG_ASSIM
INTEGER(KIND=JPIM), ALLOCATABLE ::  IPTJBALPHA2D(:), IPTJBALPHA3D(:)

CHARACTER(LEN=10) :: CLCHEM_NAME(YDML_GCONF%YGFL%NCHEM_ASSIM)

REAL(KIND=JPRB) :: ZPRESF(YDGEOMETRY%YRDIMV%NFLEVG+1), ZPRESH(0:YDGEOMETRY%YRDIMV%NFLEVG+1), ZFUDGE  ,&
                 & ZFCEMN_VO, ZFCEMN_D, ZFCEMN_TU, ZFCEMN_Q, ZFCEMN_O3,&
                 & ZFCEMN_GHG(YDML_GCONF%YGFL%NGHG_ASSIM), ZFCEMN_CHEM(YDML_GCONF%YGFL%NCHEM_ASSIM),&
                 & ZFCEMN_AERO(YD_JB_STRUCT%JB_DATA%NAEROCV)
INTEGER(KIND=JPIM)  :: IACV, ICHEM_SO2, ILSO2  
INTEGER(KIND=JPIM) :: ID0, IM0, IY0, IH0, IDATE0, IDINCR,IDD,IMM,IYY,ILMON(12), IT

REAL(KIND=JPRB) :: ZHOOK_HANDLE

REAL(KIND=JPRB) :: ZREDNMC_AERO,ZREDNMC_GHG(YDML_GCONF%YGFL%NGHG),ZREDNMC_CHEM

!     -----------------------------------------------------------------------------

#include "abor1.intfb.h"
#include "gphpre.intfb.h"
#include "updcal.intfb.h"
#include "fcttim.func.h"
#include "sujbvolc.intfb.h"

!     -----------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SUJBWAVELET_STDEVS',0,ZHOOK_HANDLE)
ASSOCIATE(YDVAB=>YDGEOMETRY%YRVAB,YDLAP=>YDGEOMETRY%YRLAP,   YGFL=>YDML_GCONF%YGFL,YDRIP=>YDML_GCONF%YRRIP,   &
& YDDIMACV=>YDML_GCONF%YRDIMACV)

ASSOCIATE(NCHEM=>YGFL%NCHEM, NCHEM_ASSIM=>YGFL%NCHEM_ASSIM, NGHG=>YGFL%NGHG,   NGHG_ASSIM=>YGFL%NGHG_ASSIM,              &
& YCHEM=>YGFL%YCHEM, YGHG=>YGFL%YGHG,   CHEM_SCHEME=>YDCHEM%CHEM_SCHEME,   NSMAX=>YDGEOMETRY%YRDIM%NSMAX,                &
& NFLEVG=>YDGEOMETRY%YRDIMV%NFLEVG,   RLAPIN=>YDLAP%RLAPIN,   RSTATI=>YDRIP%RSTATI,   LACV_2D=>YDDIMACV%LACV_2D,         &
& LACV_3D=>YDDIMACV%LACV_3D,   NACV_2D=>YDDIMACV%NACV_2D, NACV_3D=>YDDIMACV%NACV_3D,   MACVGRB_2D=>YRACVGRIB%MACVGRB_2D, &
& MACVGRB_3D=>YRACVGRIB%MACVGRB_3D)
!     -----------------------------------------------------------------------------

!       Estimate profiles of background error standard deviation for those
!       variables which are not used in Jb, but which are nevertheless
!       required for quality control (e.g. total temperature, height, wind).

IPTJBVO = HUGE(IPTJBVO)
IPTJBD  = HUGE(IPTJBD)
IPTJBTU = HUGE(IPTJBTU)
IPTJBQ  = HUGE(IPTJBQ)
IPTJBO3 = HUGE(IPTJBO3)
IPTJBPSU= HUGE(IPTJBPSU)
IF (LACV .AND. LACV_2D) THEN 
  ALLOCATE(IPTJBALPHA2D(NACV_2D))
  DO IACV = 1, NACV_2D
    IPTJBALPHA2D(IACV)= HUGE(IPTJBALPHA2D(IACV))
  ENDDO 
ENDIF  
IF (LACV .AND. LACV_3D) THEN 
  ALLOCATE(IPTJBALPHA3D(NACV_3D))
  DO IACV = 1, NACV_3D
    IPTJBALPHA3D(IACV)= HUGE(IPTJBALPHA3D(IACV))
  ENDDO 
ENDIF  
DO IGHG=1,NGHG_ASSIM
  IPTJBGHG(IGHG) = HUGE(IPTJBGHG(IGHG))
ENDDO
DO ICHEM=1,NCHEM_ASSIM
  IPTJBCHEM(ICHEM) = HUGE(IPTJBCHEM(ICHEM))
ENDDO
DO IAERO=1,YD_JB_STRUCT%JB_DATA%NAEROCV
  IPTJBAERO(IAERO) = HUGE(IPTJBAERO(IAERO))
ENDDO

ICHEM_ASSIM=0
IGHG_ASSIM=0

DO JFIELD=1,YD_JB_STRUCT%CONFIG%N_SPJB_VARS
  IF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBVO) THEN
    IPTJBVO = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBD) THEN
    IPTJBD = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBT) THEN
    IPTJBTU = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBQ) THEN
    IPTJBQ = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBO3) THEN
    IPTJBO3 = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBLNSP) THEN
    IPTJBPSU = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (LACV  .AND. ANY(MACVGRB_2D==YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE)) THEN
    IACV = 1_JPIM
    ALPHA_LOOP_2D: DO WHILE (MACVGRB_2D(IACV) /= YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE)
      IACV = IACV + 1_JPIM
      IF (IACV > NACV_2D) EXIT ALPHA_LOOP_2D
    ENDDO ALPHA_LOOP_2D
    IPTJBALPHA2D(IACV) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF (LACV .AND. ANY(MACVGRB_3D==YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE)) THEN
    IACV = 1_JPIM
    ALPHA_LOOP_3D: DO WHILE (MACVGRB_3D(IACV) /= YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE)
      IACV = IACV + 1_JPIM
      IF (IACV > NACV_3D) EXIT ALPHA_LOOP_3D
    ENDDO ALPHA_LOOP_3D
    IPTJBALPHA3D(IACV) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
  ELSEIF(NGHG_ASSIM > 0 .OR. NCHEM_ASSIM>0 .OR. YD_JB_STRUCT%JB_DATA%NAEROCV > 0) THEN
    DO IGHG=1,NGHG
      IF(YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==YGHG(IGHG)%IGRBCODE .AND. YGHG(IGHG)%LASSIM) THEN
        IGHG_ASSIM=IGHG_ASSIM+1
        IPTJBGHG(IGHG_ASSIM) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
      ENDIF
     ENDDO
    DO ICHEM=1,NCHEM
      IF(YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==YCHEM(ICHEM)%IGRBCODE .AND. YCHEM(ICHEM)%LASSIM) THEN 
        ICHEM_ASSIM=ICHEM_ASSIM+1
        IPTJBCHEM(ICHEM_ASSIM) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
        IF( YCHEM(ICHEM)%IGRBCODE == 210122 ) THEN
           ICHEM_SO2=ICHEM
        ENDIF
      ENDIF
    ENDDO
    SELECT CASE (YD_JB_STRUCT%JB_DATA%NAEROCV)
    CASE (2)
!BEN Coarse mode mixing ratio
      IF(YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBAERLG)&
       & IPTJBAERO(1) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
!BEN Fine mode aerosol mixing ratio
      IF(YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBAERSM)&
       & IPTJBAERO(2) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB 
    CASE (1)
!BEN Total aerosol mixing ratio
        IF(YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IGRIBCODE==NGRBAERLG)&
         & IPTJBAERO(1) = YD_JB_STRUCT%SPJB_VARS_INFO(JFIELD)%IPTJB
    END SELECT
  ENDIF
ENDDO

IF (IPTJBTU/=HUGE(IPTJBTU)) THEN
  YD_JB_STRUCT%FCEMN%T(:)  = YD_JB_STRUCT%JB_DATA%FCEMN3D(:,IPTJBTU) * 2.0_JPRB
ELSE
  CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR TEMPERATURE NOT IN JB')
ENDIF

IF (IPTJBPSU/=HUGE(IPTJBPSU)) THEN
  YD_JB_STRUCT%FCEMN%PS    = YD_JB_STRUCT%JB_DATA%FCEMN2D(IPTJBPSU) * 2.0_JPRB
ELSE
  CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR LNSP NOT IN JB')
ENDIF

IF (IPTJBVO/=HUGE(IPTJBVO)) THEN
  DO JJ=1,NFLEVG
    YD_JB_STRUCT%FCEMN%U(JJ) = 0.0_JPRB
    DO JN=0,NSMAX
      YD_JB_STRUCT%FCEMN%U(JJ)  = YD_JB_STRUCT%FCEMN%U(JJ) -0.5_JPRB * RLAPIN(JN)&
       & * (YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBVO)**2)&
       & * (2.0_JPRB * JN + 1.0_JPRB)          /&
       & (YD_JB_STRUCT%JB_DATA%FGSCNM3D(JJ,JN,IPTJBVO)*&
       & YD_JB_STRUCT%JB_DATA%FGSCNM3D(JJ,JN,IPTJBVO))  
    ENDDO
    YD_JB_STRUCT%FCEMN%U(JJ) = SQRT(YD_JB_STRUCT%FCEMN%U(JJ))
  ENDDO
ELSE
  CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR VORTICITY NOT IN JB')
ENDIF

ZPRESH(NFLEVG)=VP00
CALL GPHPRE(1,NFLEVG,1,1,YDVAB,ZPRESH,PRESF=ZPRESF)

ILV950=0
DO JL=1,NFLEVG
  IF(ZPRESF(JL)<95000._JPRB) ILV950=JL
ENDDO
IF (ILV950==0) CALL ABOR1 ('CANNOT FIND A LEVEL BELOW 950hP')

WRITE(NULOUT,*) 'height profile is extrapolated below level ',ILV950

IF (IPTJBVO/=HUGE(IPTJBVO)) THEN
  DO JJ=1,NFLEVG
    IL=MIN(JJ,ILV950)
!         Do not try to understand this formula
    ZFUDGE=  (1.6_JPRB+REAL(IL,JPRB) / REAL(NFLEVG,JPRB)&
     & *(1.3_JPRB-1.6_JPRB))* 0.00005_JPRB  
    YD_JB_STRUCT%FCEMN%Z(JJ) = 0.0_JPRB
    DO JN=0,NSMAX
      YD_JB_STRUCT%FCEMN%Z(JJ) = YD_JB_STRUCT%FCEMN%Z(JJ) + (RLAPIN(JN)**2)&
       & *(ZFUDGE**2)&
       & *(YD_JB_STRUCT%JB_DATA%FCEMN3D(IL,IPTJBVO)**2)&
       & *(2.0_JPRB * JN + 1.0_JPRB) /(&
       & YD_JB_STRUCT%JB_DATA%FGSCNM3D(IL,JN,IPTJBVO)*&
       & YD_JB_STRUCT%JB_DATA%FGSCNM3D(IL,JN,IPTJBVO))  
    ENDDO
    YD_JB_STRUCT%FCEMN%Z(JJ) = SQRT(YD_JB_STRUCT%FCEMN%Z(JJ))
  ENDDO
ELSE
  CALL ABOR1 ('BEHAVIOUR NOT DEFINED FOR VORTICITY NOT IN JB')
ENDIF

!       Scale the background errors by YD_JB_STRUCT%CONFIG%REDNMC

YD_JB_STRUCT%JB_DATA%FCEMN3D(:,:) = YD_JB_STRUCT%CONFIG%REDNMC*&
                               & YD_JB_STRUCT%JB_DATA%FCEMN3D(:,:)
YD_JB_STRUCT%JB_DATA%FCEMN2D(:)   = YD_JB_STRUCT%CONFIG%REDNMC*&
                               & YD_JB_STRUCT%JB_DATA%FCEMN2D(:)
YD_JB_STRUCT%FCEMN%U (:) = YD_JB_STRUCT%CONFIG%REDNMC*YD_JB_STRUCT%FCEMN%U (:)
YD_JB_STRUCT%FCEMN%Z (:) = YD_JB_STRUCT%CONFIG%REDNMC*YD_JB_STRUCT%FCEMN%Z (:)
YD_JB_STRUCT%FCEMN%T (:) = YD_JB_STRUCT%CONFIG%REDNMC*YD_JB_STRUCT%FCEMN%T (:)
YD_JB_STRUCT%FCEMN%PS    = YD_JB_STRUCT%CONFIG%REDNMC*YD_JB_STRUCT%FCEMN%PS

!       Print some diagnostics to the log file

WRITE(NULOUT,*) 'Background error standard deviations have been '//&
 & 'multiplied by YD_JB_STRUCT%CONFIG%REDNMC=',YD_JB_STRUCT%CONFIG%REDNMC  
WRITE(NULOUT,*) 'Jb preliminary vertical stdev profiles:'
WRITE(NULOUT,*) '(-999.0 indicates the variable is not in Jb)'
WRITE(NULOUT,*) ' unbal ps : ',YD_JB_STRUCT%JB_DATA%FCEMN2D(IPTJBPSU)*VP00
IF (IPTJBPSU/=HUGE(IPTJBPSU)) THEN
  WRITE(NULOUT,*) ' total ps : ',YD_JB_STRUCT%FCEMN%PS *VP00
ELSE
  WRITE(NULOUT,*) ' total ps : ',-999.0_JPRB
ENDIF
IF (LACV) THEN 
  WRITE(NULOUT,*)'ALPHA  1D not available'
  !
  WRITE(NULOUT,*) ' I  ACV LEV'
  DO IACV = 1, NACV_2D
    WRITE(NULOUT,'(I3,1X,G11.4)') &
      & IACV, YD_JB_STRUCT%JB_DATA%FCEMN2D(IPTJBALPHA2D(IACV))
  ENDDO
  !
  WRITE(NULOUT,*) ' I  ACV LEV'
  DO IACV = 1, NACV_3D
    DO JJ=1,NFLEVG
      WRITE(NULOUT,'(I3,1X,I3,1X,G11.4)') &
        & IACV, JJ, YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBALPHA3D(IACV))
    ENDDO
  ENDDO
ENDIF
IF (LACV .AND. LACV_2D) DEALLOCATE(IPTJBALPHA2D)
IF (LACV .AND. LACV_3D) DEALLOCATE(IPTJBALPHA3D)



WRITE(NULOUT,*) '       Vor      unbal Div  '//&
 & '    Wind        mass    '//&
 & '     T       unbal T    '//&
 & '     Q       unbal O3'  
DO JJ=1,NFLEVG
  IF (IPTJBVO/=HUGE(IPTJBVO)) THEN
    ZFCEMN_VO = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBVO)
  ELSE
    ZFCEMN_VO = -999.0_JPRB
  ENDIF
  IF (IPTJBD/=HUGE(IPTJBD)) THEN
    ZFCEMN_D = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBD)
  ELSE
    ZFCEMN_D = -999.0_JPRB
  ENDIF
  IF (IPTJBTU/=HUGE(IPTJBTU)) THEN
    ZFCEMN_TU = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBTU)
  ELSE
    ZFCEMN_TU = -999.0_JPRB
  ENDIF
  IF (IPTJBQ/=HUGE(IPTJBQ)) THEN
    ZFCEMN_Q = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBQ)
  ELSE
    ZFCEMN_Q = -999.0_JPRB
  ENDIF
  IF (IPTJBO3/=HUGE(IPTJBO3)) THEN
    ZFCEMN_O3 = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBO3)
  ELSE
    ZFCEMN_O3 = -999.0_JPRB
  ENDIF
  WRITE(NULOUT,'(I3,8(1X,G11.4))') JJ,ZFCEMN_VO,&
   & ZFCEMN_D,YD_JB_STRUCT%FCEMN%U(JJ) ,YD_JB_STRUCT%FCEMN%Z(JJ),&
   & YD_JB_STRUCT%FCEMN%T(JJ),ZFCEMN_TU,ZFCEMN_Q,&
   & ZFCEMN_O3  
ENDDO

! Change SO2 JB
IF (NCHEM_ASSIM > 0) THEN
   ICHEM_ASSIM=0
   IF(TRIM(CHEM_SCHEME) == 'decay') THEN
     CALL SUJBVOLC
     IY0=NCCAA(NINDAT)
     IM0=NMM(NINDAT)
     ID0=NDD(NINDAT)
     IDINCR=(NSSSSS+NINT(RSTATI))/NINT(RDAY)
     IH0=INT(MOD(NSSSSS+NINT(RSTATI),NINT(RDAY))/3600_JPRB)
     CALL UPDCAL(ID0,IM0,IY0,IDINCR,IDD,IMM,IYY,ILMON,-1)
     IDATE0=IYY*1000000 + IMM*10000 + IDD*100 + IH0
     WRITE(NULOUT,*) 'SUJBWAVELET_STDEVS: MODIFY SO2 stdv NVOCDATES,IDATE0 ', NVOCDATES,IDATE0
     IT=-1
     ! find date in list
     DO JK=1,NVOCDATES
        IF (IDATE0 >= IVOCSTART(JK) ) THEN
          IT=JK
        ENDIF
     ENDDO
     IF (IT > 0) THEN
       WRITE(NULOUT,*) ' SUJBWAVELET_STDEVS: SO2 VOLCANO EMISS DATE ',IT,IVOCSTART(IT)
       WRITE(NULOUT,*) ' SUJBWAVELET_STDEVS: SO2 VOLCANO EMISS LEVEL ', ILVOCJB(IT)
       DO JJ=1,NFLEVG
         ICHEM_ASSIM=0
         DO ICHEM=1,NCHEM
           IF (YCHEM(ICHEM)%LASSIM) THEN
             ICHEM_ASSIM=ICHEM_ASSIM+1
             IF (ICHEM == ICHEM_SO2) THEN
               YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-10_JPRB

               IF(ILVOCJB(IT) > 0.) THEN
                 IF(JJ >= ILVOCJB(IT)-1 .AND. JJ <= ILVOCJB(IT)+1 )  THEN
                   YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-7_JPRB
                 ELSEIF(JJ == ILVOCJB(IT)-2 .OR. JJ == ILVOCJB(IT)+2 )  THEN
                   YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-8_JPRB
                 ENDIF
               ENDIF
             ENDIF
           ENDIF
         ENDDO
       ENDDO
     ENDIF
   ELSE
     IF (NFLEVG == 137) THEN
       ILSO2=98
     ELSEIF(NFLEVG == 91) THEN
       ILSO2=65
     ELSE
       ILSO2=40
     ENDIF
     DO JJ=1,NFLEVG
       ICHEM_ASSIM=0
       DO ICHEM=1,NCHEM
         IF (YCHEM(ICHEM)%LASSIM) THEN
           ICHEM_ASSIM=ICHEM_ASSIM+1
           IF (ICHEM == ICHEM_SO2) THEN
             YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-10_JPRB
             IF(JJ >= ILSO2-1 .AND. JJ <= ILSO2+1 )  THEN
               YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-7_JPRB
             ELSEIF(JJ == ILSO2-2 .OR. JJ == ILSO2+2 )  THEN
               YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))=YD_JB_STRUCT%CONFIG%REDNMC*1.E-8_JPRB
             ENDIF
           ENDIF
         ENDIF
       ENDDO
     ENDDO
   ENDIF

   ZREDNMC_CHEM =1.4_JPRB
   ICHEM_ASSIM=0
   DO ICHEM=1,NCHEM
     IF(YCHEM(ICHEM)%LASSIM) THEN
       ICHEM_ASSIM=ICHEM_ASSIM+1
       CLCHEM_NAME(ICHEM_ASSIM)=YCHEM(ICHEM)%CNAME
     ENDIF
   ENDDO
   WRITE(NULOUT,*) '     CHEM variables'
   WRITE(NULOUT,'(8x,4(A,x),A)') CLCHEM_NAME(1:NCHEM_ASSIM)
   DO JJ=1,NFLEVG
     ICHEM_ASSIM=0
     DO ICHEM=1,NCHEM
       IF (YCHEM(ICHEM)%LASSIM) THEN
         ICHEM_ASSIM=ICHEM_ASSIM+1
         IF (IPTJBCHEM(ICHEM_ASSIM)/=HUGE(IPTJBCHEM(ICHEM_ASSIM))) THEN
           YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM)) = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM)) /&
                           & YD_JB_STRUCT%CONFIG%REDNMC * ZREDNMC_CHEM
           ZFCEMN_CHEM(ICHEM_ASSIM) = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBCHEM(ICHEM_ASSIM))
         ELSE
           ZFCEMN_CHEM(ICHEM_ASSIM) = -999.0_JPRB
         ENDIF
       ENDIF
     ENDDO
     WRITE(NULOUT,'(I3,5(1X,G11.4))') JJ,ZFCEMN_CHEM(1:NCHEM_ASSIM)
   ENDDO
 ENDIF
 IF (NGHG_ASSIM > 0) THEN
   ZREDNMC_GHG(:) =1.0_JPRB
   WRITE(NULOUT,*) '      GHG variables'
   DO IGHG=1,NGHG
    IF (TRIM(YGHG(IGHG)%CNAME) == 'CO2') ZREDNMC_GHG(IGHG) = RCOEFCO2
    IF (TRIM(YGHG(IGHG)%CNAME) == 'CH4') ZREDNMC_GHG(IGHG) = RCOEFCH4
    WRITE(NULOUT,'(8x,A,x,A)') YGHG(IGHG)%CNAME
    WRITE(NULOUT,*) 'ZREDNMC_GHG =',ZREDNMC_GHG(IGHG)
   ENDDO

   DO JJ=1,NFLEVG
     IGHG_ASSIM=0
     DO IGHG=1,NGHG
       IF (YGHG(IGHG)%LASSIM) THEN
         IGHG_ASSIM=IGHG_ASSIM+1
         IF (IPTJBGHG(IGHG_ASSIM)/=HUGE(IPTJBGHG(IGHG_ASSIM))) THEN
           YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBGHG(IGHG_ASSIM)) =&
              & ZREDNMC_GHG(IGHG) / YD_JB_STRUCT%CONFIG%REDNMC *&
              & YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBGHG(IGHG_ASSIM)) 
           ZFCEMN_GHG(IGHG_ASSIM) = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBGHG(IGHG_ASSIM))
         ELSE
           ZFCEMN_GHG(IGHG_ASSIM) = -999.0_JPRB
         ENDIF
         WRITE(NULOUT,'(I3,1X,G11.4)') JJ,ZFCEMN_GHG(IGHG_ASSIM)
       ENDIF  
     ENDDO
!     WRITE(NULOUT,'(I3,3(1X,G11.4))') JJ,ZFCEMN_GHG(1:NGHG_ASSIM)
   ENDDO
 ENDIF

 ZREDNMC_AERO =1.0_JPRB
 IF (YD_JB_STRUCT%JB_DATA%NAEROCV > 0) THEN
   WRITE(NULOUT,*) '      AEROSOL variables'
   DO JJ=1,NFLEVG
     DO IAERO=1,YD_JB_STRUCT%JB_DATA%NAEROCV
       IF (IPTJBAERO(IAERO)/=HUGE(IPTJBAERO(IAERO))) THEN
          YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBAERO(IAERO)) =&
                           & YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBAERO(IAERO)) /&
                           & YD_JB_STRUCT%CONFIG%REDNMC * ZREDNMC_AERO
          ZFCEMN_AERO(IAERO) = YD_JB_STRUCT%JB_DATA%FCEMN3D(JJ,IPTJBAERO(IAERO))
       ELSE
         ZFCEMN_AERO(IAERO) = -999.0_JPRB
       ENDIF
     ENDDO
     WRITE(NULOUT,'(I3,3(1X,G11.4))') JJ,ZFCEMN_AERO(1:YD_JB_STRUCT%JB_DATA%NAEROCV)
   ENDDO
 ENDIF


END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUJBWAVELET_STDEVS',1,ZHOOK_HANDLE)

END SUBROUTINE SUJBWAVELET_STDEVS
