SUBROUTINE SUJBWAVRENORM(YDGEOMETRY,YDFIELDS,YDMTRAJ,YDML_GCONF,YD_JB_STRUCT)
!      SUJBWAVRENORM - Compute the variance renormalisation needed in  wavelet Jb
!     Purpose: Compute the variance renormalisation needed in  wavelet Jb. 
!              Can compute the variance induced by a serie of specific scales. 
!     -------

!     Interface: 
!     ---------
!     Author: Vincent Chabot 2016-01-27 
!     ------
!     Modifications:
!     -------------
!     ------------------------------------------------------------------
USE MODEL_GENERAL_CONF_MOD , ONLY : MODEL_GENERAL_CONF_TYPE
USE GEOMETRY_MOD           , ONLY : GEOMETRY
USE FIELDS_MOD             , ONLY : FIELDS
USE MTRAJ_MOD              , ONLY : MTRAJ
USE PARKIND1               , ONLY : JPIM, JPRB
USE YOMHOOK                , ONLY : LHOOK,  DR_HOOK
USE YOMJG                  , ONLY : TYPE_JB_STRUCT
USE YOMCVA                 , ONLY : CVA_DATA
USE YOM_GRIB_CODES         , ONLY : NGRBVO, NGRBUCTP, NGRBUCLN, NGRBUCDV, NGRBQ
USE SPECTRAL_FIELDS_MOD
!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY), INTENT(INOUT) :: YDGEOMETRY
TYPE(FIELDS)  , INTENT(INOUT) :: YDFIELDS
TYPE(MTRAJ)   , INTENT(INOUT) :: YDMTRAJ
TYPE(MODEL_GENERAL_CONF_TYPE),INTENT(INOUT):: YDML_GCONF
TYPE(TYPE_JB_STRUCT), INTENT(INOUT) :: YD_JB_STRUCT


INTEGER(KIND=JPIM) ::  JSCALE, JKGLO,  IOFF, IBL, ICEND, IGPTOT, JFIELD, IG, IGPBLKS,IPTOT
INTEGER(KIND=JPIM),ALLOCATABLE :: IVSETSC2(:),IGRB3(:), IGRB2(:)
REAL(KIND=JPRB),ALLOCATABLE :: ZGP3VAR(:,:,:), ZGP2VAR(:,:,:), &
 & ZSP3VAR(:,:,:), ZSP2VAR(:,:), ZGT3(:,:), ZGT2(:,:), & 
 & ZGP3(:,:,:,:), ZGP2(:,:,:),ZSP3SUM(:,:,:), ZSP2SUM(:,:), ZSP2BIS(:,:)
REAL(KIND=JPRB) :: ZHOOK_HANDLE
INTEGER(KIND=JPIM) :: JFIRST,JLAST                 
CHARACTER(LEN=1024) :: CLFILE

!     ------------------------------------------------------------------

#include "transdir_wavelet.intfb.h"
#include "jb_wav_var.intfb.h"
#include "writestd.intfb.h"
#include "wav_var_comp.intfb.h"
#include "inv_trans.h"
#include "test_sdp_variances.intfb.h"

!     ------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SUJBWAVRENORM',0,ZHOOK_HANDLE)

ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM, YDDIMV=>YDGEOMETRY%YRDIMV, YDGEM=>YDGEOMETRY%YRGEM,   YDMP=>YDGEOMETRY%YRMP&
& )

ASSOCIATE(NFLSUR=>YDDIMV%NFLSUR, NFLEVG=>YDDIMV%NFLEVG,   NSPEC2=>YDDIM%NSPEC2, NPROMA=>YDDIM%NPROMA,        &
& NRESOL=>YDDIM%NRESOL, NPSP=>YDMP%NPSP, NGPTOT=>YDGEM%NGPTOT, NBSETSP=>YDMP%NBSETSP, NBSETLEV=>YDMP%NBSETLEV&
&  )
IGPTOT = YD_JB_STRUCT%GRID_DEFINITION(1)%NGPTOT
 
! Allocate field to sum the equivalent pixel variance
ALLOCATE (ZSP3SUM(NFLSUR,NSPEC2,CVA_DATA%NVA3D))
ALLOCATE (ZSP2SUM(NSPEC2,CVA_DATA%NVA2D))   
ALLOCATE (ZSP2BIS(CVA_DATA%NVA2D,NSPEC2))   
ZSP3SUM=0.0_JPRB
ZSP2SUM=0.0_JPRB 

! if the computation is performed only for some specific scales then chose them. 
 IF(YD_JB_STRUCT%WJBCONF%N_LASTSCALE > 0 .AND. YD_JB_STRUCT%WJBCONF%N_LASTSCALE >= YD_JB_STRUCT%WJBCONF%N_FIRSTSCALE)THEN 
    JFIRST=YD_JB_STRUCT%WJBCONF%N_FIRSTSCALE
    JLAST= YD_JB_STRUCT%WJBCONF%N_LASTSCALE
 ELSE 
   ! Else use every scales. 
    JFIRST= 1
    JLAST= YD_JB_STRUCT%WJBCONF%N_WAVELET_SCALES+1
 ENDIF  


DO JSCALE=JFIRST,JLAST
    IPTOT = YD_JB_STRUCT%GRID_DEFINITION(JSCALE)%NGPTOT
    IGPBLKS = (IPTOT+NPROMA-1)/NPROMA
  
   ! Allocate field for standard deviation of wavelet coefficients
   ALLOCATE (ZGP2VAR(NPROMA,CVA_DATA%NVA2D,IGPBLKS))
   ALLOCATE (ZGP3VAR(NPROMA,NFLEVG*CVA_DATA%NVA3D,IGPBLKS))
   ZGP2VAR=0.0_JPRB

   ! Get The variance in pixel space
  !!!!!!!! !$OMP PARALLEL PRIVATE(JKGLO,ICEND,IBL,IOFF,ZGT3,ZGT2)
   ALLOCATE (ZGT3(NPROMA,NFLEVG*CVA_DATA%NVA3D))
   ALLOCATE (ZGT2(NPROMA,CVA_DATA%NVA2D))
 !!!!!!!!!!   !$OMP DO SCHEDULE(DYNAMIC,1)
    DO JKGLO=1,IGPTOT,NPROMA
      ICEND=MIN(NPROMA,IPTOT-JKGLO+1)
      IBL=(JKGLO-1)/NPROMA+1
      IOFF=JKGLO
  
      CALL JB_WAV_VAR(YDGEOMETRY,'Q',JSCALE,ICEND,&
       & YD_JB_STRUCT%GRID_DEFINITION(JSCALE)%GELAM(IOFF:),&
       & YD_JB_STRUCT%GRID_DEFINITION(JSCALE)%GELAT(IOFF:),&
       & ZGT3(1,1),ZGT2(1,1),YD_JB_STRUCT)
      ZGP3VAR(1:ICEND,:,IBL) = ZGT3(1:ICEND,:)
      ZGP2VAR(1:ICEND,:,IBL) = ZGT2(1:ICEND,:)
   ENDDO
 !!!!!!!  !$OMP END DO 
   DEALLOCATE(ZGT3)
   DEALLOCATE(ZGT2)
 !!!!!!!!  !$OMP END PARALLEL

   ! Allocate spectral array containing variance in spectral space
    ALLOCATE (ZSP3VAR(NFLSUR,NSPEC2,CVA_DATA%NVA3D))
    ALLOCATE (ZSP2VAR(NSPEC2,CVA_DATA%NVA2D))

    ZSP2VAR=0.0_JPRB
   !!!! Transform it in spectral space
    CALL TRANSDIR_WAVELET(YDGEOMETRY,JSCALE,ZSP3VAR,ZSP2VAR,ZGP3VAR,ZGP2VAR,YD_JB_STRUCT)
   !! Multiply it with the good filter
    CALL WAV_VAR_COMP(YDGEOMETRY,JSCALE,ZSP3VAR,ZSP2VAR,YD_JB_STRUCT)
   ! Sum contributions over scales. 
    ZSP3SUM(:,:,:)=ZSP3SUM(:,:,:)+ZSP3VAR(:,:,:)
    IF (NPSP == 1)  ZSP2SUM(:,:)=ZSP2SUM(:,:)+ZSP2VAR(:,:)

    DEALLOCATE(ZGP3VAR)
    DEALLOCATE(ZGP2VAR)
    DEALLOCATE (ZSP3VAR)
    DEALLOCATE (ZSP2VAR)
ENDDO
CALL GSTATS(203,1)

!* Fill IVSETSC2 (distribution over B-sets)
ALLOCATE(IVSETSC2(CVA_DATA%NVA2D))
IG=0
DO JFIELD=1,CVA_DATA%NVA2D
   IVSETSC2(IG+1)=NBSETSP
   IG=IG+1
ENDDO

!* Back to grid space
IGPBLKS=(NGPTOT-1)/NPROMA+1
ALLOCATE(ZGP3(NPROMA,NFLEVG,CVA_DATA%NVA3D,IGPBLKS))
ALLOCATE(ZGP2(NPROMA,CVA_DATA%NVA2D,IGPBLKS))
DO JFIELD=1,CVA_DATA%NVA2D
  ZSP2BIS(JFIELD,:)=ZSP2SUM(:,JFIELD)
ENDDO  
CALL INV_TRANS(PSPSC3A=ZSP3SUM,PSPSC2=ZSP2BIS,KPROMA=NPROMA,KRESOL=NRESOL,&
& KVSETSC3A=NBSETLEV,KVSETSC2=IVSETSC2,PGP3A=ZGP3,PGP2=ZGP2)
DEALLOCATE(ZSP2BIS)
DEALLOCATE(ZSP3SUM)
DEALLOCATE(ZSP2SUM)

!* Setup GRIB indices
ALLOCATE(IGRB3(CVA_DATA%NVA3D))
ALLOCATE(IGRB2(CVA_DATA%NVA2D))
IGRB3(1)=NGRBVO
IGRB3(2)=NGRBUCDV
IGRB3(3)=NGRBUCTP
IGRB3(4)=NGRBQ
IGRB2(1)=NGRBUCLN

! Test for negative values
CALL TEST_SDP_VARIANCES(YDGEOMETRY,CVA_DATA%NVA3D,CVA_DATA%NVA2D,IGRB3,IGRB2,ZGP3,ZGP2)

! Apply hard-coded threshold (x5 ratio) and take square-root
ZGP3 = MAX(ZGP3,0.2_JPRB)
ZGP3 = MIN(ZGP3,5.0_JPRB)
ZGP3 = SQRT(ZGP3)
ZGP2 = MAX(ZGP2,0.2_JPRB)
ZGP2 = MIN(ZGP2,5.0_JPRB)
ZGP2 = SQRT(ZGP2)

!* Write coefficients
CLFILE='renormalisa_coef'
CALL WRITESTD(YDGEOMETRY,YDML_GCONF%YRRIP,CVA_DATA%NVA3D,CVA_DATA%NVA2D,IGRB3,IGRB2,ZGP3,ZGP2,TRIM(CLFILE))

END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('SUJBWAVRENORM',1,ZHOOK_HANDLE)

END SUBROUTINE SUJBWAVRENORM





