SUBROUTINE VEC2GP(YDGEOMETRY,YDSURF,YDMODEL,CDCONF,KNUMB,KSTGLO,&
 & PU ,PV ,PT ,PQ ,PDIV, PVOR ,PO3 ,PGFL,PSP,&
 & PU5,PV5,PT5,PQ5,PO35,PGFL5,PSP5,&
 & POROG,PGELAT,PGELAM,PPERTTS,YDGOM,YDGOM5,&
 & PTRAJ_SRFC,PTRAJ_CST)

!*** *VEC2GP*

!   Purpose.
!   --------
!      To transform a control vector to grid point space and accumulate
!      sums of squares of gridpoint values

!   Interface.
!   ----------

!      CALL VEC2GP

!   Explicit Arguments:
!                     cdconf   - 'A' => add squares of gridpoint values
!                                'B' => subtract squares of point values
!                     knumb    - number of values in buffer
!                     kstglo   - indexing for buffer: global start
!                     pu       - gridpoint values of zonal wind
!                     pv       - gridpoint values of meridional wind
!                     pt       - gridpoint values of temperature
!                     pq       - gridpoint values of relative humidity
!                     pdiv     - gridpoint values of divergence
!                     pvor     - gridpoint values of vorticity
!                     po3      - gridpoint values of ozone
!                     pgfl     - gridpoint values of GFL variables
!                     psp      - gridpoint values of LNSP
!                     porog    - gridpoint values of orography
!                     ppertts  - gridpoint values of Ts perturpations
!                     pgelat   - gridpoint values of latitude
!                     pgelam   - gridpoint values of longitude
!                     pxx5     - gridpoint trajectory values

!   Implicit Arguments:
!                     anebuf   - gridpoint buffer for analysis errors

!   Externals.
!   ----------

!   Reference.
!   ----------
!      RD Tech Memo 220.

!   Author.
!   -------
!    Mike Fisher *ECMWF*  Nov 95

!   Modifications.
!   --------------
!    M.Hamrud      01-Oct-2003 CY28 Cleaning
!    M.Hamrud      10-Jan-2004 CY28R1 Cleaning
!    G. Desroziers 16-Mar-2006 No computation for surface variables if LECMWF=.F.
!    H.Varella : 2010-04-20 Divergence included
!    N. Bormann    08-Mar-2011 RTTOV-10 
!    J Hague       02-May-2012 Updated for new_gom 
!    A. Inness     24-Jan-2013 Add CHEM fields     
!    F. Vana  28-Nov-2013 : Redesigned trajectory handling
!    E.Dutra/G.Arduini  Jan 2018: Changed ZSP_SG5(1,YSP_SG%YF%MP) to ZSP_SG5(1,1,YSP_SG%YF%MP)        
!-----------------------------------------------------------------------

USE TYPE_MODEL   , ONLY : MODEL
USE GEOMETRY_MOD , ONLY : GEOMETRY
USE SURFACE_FIELDS_MIX , ONLY : TSURF, GPOPER
USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK
USE YOMCT0   , ONLY : LECMWF
USE YOMVAR   , ONLY : NOANEF, LBGOBS  
USE YOMANEB  , ONLY : NANEBU, NANEBV, NANEBZ, NANEBR ,&
 & NANEBQ,   NANEBT,   NANEBDIV, NANEBVO, NANEBSP, ANEBUF   ,&
 & NANEBRAD, NANEBTCW, NANEBO3,  NANEBTO3,&
 & NANEBRH2, NANEBU10, NANEBV10, NANEBT2, NANERADS,&
 & NANERADL, NANEBGHG, NANEBAERO, NANEBCHEM
USE SUPERGOM_CLASS  , ONLY : CLASS_SUPERGOM
USE YOMJG    , ONLY : JB_STRUCT
USE YOM_GRIB_CODES  , ONLY : NGRBO3,  NGRBAERLG, NGRBAERSM,NGRBGHG
USE YOM_YGFL , ONLY : JPGHG
USE YOMTRAJ  , ONLY : TRAJ_SRFC_TYPE, TRAJ_CST_TYPE, NGP5, NTRAJ_CST,&
 & LPRTTRAJ
USE YOMLUN   , ONLY : NULOUT

!-----------------------------------------------------------------------

IMPLICIT NONE

TYPE(GEOMETRY)       ,INTENT(IN)    :: YDGEOMETRY
TYPE(TSURF)          ,INTENT(INOUT) :: YDSURF
TYPE(MODEL)          ,INTENT(INOUT) :: YDMODEL
CHARACTER(LEN=1)     ,INTENT(IN)    :: CDCONF 
INTEGER(KIND=JPIM)   ,INTENT(IN)    :: KNUMB 
INTEGER(KIND=JPIM)   ,INTENT(IN)    :: KSTGLO 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PU(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PT(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PQ(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PDIV(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PVOR(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PO3(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGFL(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSP(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PU5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PV5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PT5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PQ5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PO35(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGFL5(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,YDMODEL%YRML_GCONF%YGFL%NDIM5) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PSP5(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: POROG(YDGEOMETRY%YRDIM%NPROMA) 
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGELAT(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)      ,INTENT(IN)    :: PGELAM(YDGEOMETRY%YRDIM%NPROMA)
REAL(KIND=JPRB)      ,INTENT(INOUT) :: PPERTTS(YDGEOMETRY%YRDIM%NPROMA) 
TYPE(CLASS_SUPERGOM) ,INTENT(IN)    :: YDGOM
TYPE(CLASS_SUPERGOM) ,INTENT(IN)    :: YDGOM5
TYPE(TRAJ_SRFC_TYPE) ,INTENT(IN)    :: PTRAJ_SRFC
TYPE(TRAJ_CST_TYPE)  ,INTENT(IN)    :: PTRAJ_CST
!-----------------------------------------------------------------------
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG)  ::ZUPP,ZVPP,ZTPP,ZRPP,ZQPP,ZZPP,ZO3PP
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,MAX(1,YDMODEL%YRML_GCONF%YGFL%NGHG_ASSIM))  ::ZGHGPP
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,MAX(1,YDMODEL%YRML_GCONF%YGFL%NCHEM_ASSIM))  ::ZCHEMPP
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA,YDGEOMETRY%YRDIMV%NFLEVG,MAX(1,JB_STRUCT%JB_DATA%NAEROCV)) ::ZAEROPP
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA)         ::ZTCW,ZRH2M,ZU10M,ZV10M,ZT2M,ZTO3,ZCI,ZVEG
REAL(KIND=JPRB), DIMENSION(YDGEOMETRY%YRDIM%NPROMA,NANERADS) :: ZRADM
INTEGER(KIND=JPIM) :: ICH(NANERADS)
REAL(KIND=JPRB),TARGET  :: ZANE   (YDGEOMETRY%YRDIM%NPROMA,NOANEF)
REAL(KIND=JPRB),POINTER :: ZANEU  (:,:)
REAL(KIND=JPRB),POINTER :: ZANEV  (:,:)
REAL(KIND=JPRB),POINTER :: ZANEZ  (:,:)
REAL(KIND=JPRB),POINTER :: ZANER  (:,:)
REAL(KIND=JPRB),POINTER :: ZANEQ  (:,:)
REAL(KIND=JPRB),POINTER :: ZANET  (:,:)
REAL(KIND=JPRB),POINTER :: ZANEDIV(:,:)
REAL(KIND=JPRB),POINTER :: ZANEVOR(:,:)
REAL(KIND=JPRB),POINTER :: ZANEO3 (:,:)
REAL(KIND=JPRB),POINTER :: ZANERAD(:,:)
REAL(KIND=JPRB),POINTER :: ZANETCW(:)
REAL(KIND=JPRB),POINTER :: ZANETO3(:)
REAL(KIND=JPRB),POINTER :: ZANERH2(:)
REAL(KIND=JPRB),POINTER :: ZANEU10(:)
REAL(KIND=JPRB),POINTER :: ZANEV10(:)
REAL(KIND=JPRB),POINTER :: ZANET2 (:)
REAL(KIND=JPRB),POINTER :: ZANESP (:)
REAL(KIND=JPRB) :: ZSP_SB5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SBD%NLEVS,YDSURF%YSP_SBD%NDIM)
REAL(KIND=JPRB) :: ZSP_SG5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_SGD%NLEVS,YDSURF%YSP_SGD%NDIM)
REAL(KIND=JPRB) :: ZSP_RR5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSP_RRD%NDIM)
REAL(KIND=JPRB) :: ZSD_VF5(YDGEOMETRY%YRDIM%NPROMA,YDSURF%YSD_VFD%NDIM)

TYPE TYPE_ZANEGEMS
  REAL(KIND=JPRB), POINTER :: ZANE(:,:) 
END TYPE TYPE_ZANEGEMS
TYPE(TYPE_ZANEGEMS) :: YLANEGHG(YDMODEL%YRML_GCONF%YGFL%NGHG_ASSIM)
TYPE(TYPE_ZANEGEMS) :: YLANECHEM(YDMODEL%YRML_GCONF%YGFL%NCHEM_ASSIM)
TYPE(TYPE_ZANEGEMS) :: YLANEAERO(JB_STRUCT%JB_DATA%NAEROCV)

INTEGER(KIND=JPIM) :: IST,IEND,ISTC,IENDC,ILEN,ISOBT,INRCH&
 & ,JLEV,JROF,JCH,IVTYPES
REAL(KIND=JPRB) :: ZSIGN,ZRTFREEZSICE
REAL(KIND=JPRB),ALLOCATABLE :: ZRVCOV(:)
REAL(KIND=JPRB) :: ZTEMP(YDGEOMETRY%YRDIM%NPROMA,NGP5), ZTEMPC(YDGEOMETRY%YRDIM%NPROMA,NTRAJ_CST)
LOGICAL :: LLO3JB
INTEGER(KIND=JPIM) :: JGHG, IGHG, IAERO, JCHEM, ICHEM
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!-----------------------------------------------------------------------

#include "surf_inq.h"
#include "abor1.intfb.h"
#include "bgobs.intfb.h"
#include "sigam.intfb.h"

!-----------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('VEC2GP',0,ZHOOK_HANDLE)
ASSOCIATE(YDDIM=>YDGEOMETRY%YRDIM,YDDIMV=>YDGEOMETRY%YRDIMV,   YDEPHY=>YDMODEL%YRML_PHY_EC%YREPHY,    &
& YGFL=>YDMODEL%YRML_GCONF%YGFL)

ASSOCIATE(NCHEM=>YGFL%NCHEM, NCHEM_ASSIM=>YGFL%NCHEM_ASSIM,   YCHEM=>YGFL%YCHEM,   NGHG_ASSIM=>YGFL%NGHG_ASSIM,     &
& YGHG=>YGFL%YGHG,   NPROMA=>YDDIM%NPROMA,   NFLEVG=>YDDIMV%NFLEVG,   YSURF=>YDEPHY%YSURF,   YSD_VF=>YDSURF%YSD_VF, &
& YSP_RR=>YDSURF%YSP_RR,   YSP_SB=>YDSURF%YSP_SB, YSP_SG=>YDSURF%YSP_SG)
!-----------------------------------------------------------------------

LLO3JB=ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE==NGRBO3)

CALL SURF_INQ(YSURF,KNVTYPES=IVTYPES)
ALLOCATE(ZRVCOV(0:IVTYPES))
CALL SURF_INQ(YSURF,PRTFREEZSICE=ZRTFREEZSICE,PRVCOV=ZRVCOV)

ZANEU  => ZANE(:,NANEBU +1 : NANEBU +NFLEVG)
ZANEV  => ZANE(:,NANEBV +1 : NANEBV +NFLEVG)
ZANEZ  => ZANE(:,NANEBZ +1 : NANEBZ +NFLEVG)
ZANEQ  => ZANE(:,NANEBQ +1 : NANEBQ +NFLEVG)
ZANET  => ZANE(:,NANEBT +1 : NANEBT +NFLEVG)
ZANEDIV=> ZANE(:,NANEBDIV+1 : NANEBDIV+NFLEVG)
ZANEVOR=> ZANE(:,NANEBVO+1 : NANEBVO+NFLEVG)
ZANESP => ZANE(:,NANEBSP+1)
IF(LBGOBS) THEN
  ZANERAD=> ZANE(:,NANEBRAD+1: NANEBRAD+NANERADS)
  ZANER  => ZANE(:,NANEBR +1 : NANEBR +NFLEVG)
  ZANETCW=> ZANE(:,NANEBTCW+1)
  ZANERH2=> ZANE(:,NANEBRH2+1)
  ZANEU10=> ZANE(:,NANEBU10+1)
  ZANEV10=> ZANE(:,NANEBV10+1)
  ZANET2 => ZANE(:,NANEBT2 +1)
ENDIF
IF(LLO3JB) ZANEO3 => ZANE(:,NANEBO3+1 : NANEBO3+NFLEVG)
IF(LLO3JB.AND. LBGOBS)  ZANETO3=> ZANE(:,NANEBTO3+1)
IF(NGHG_ASSIM > 0) THEN
  IGHG=0
  DO JGHG=1,JPGHG
    IF(ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE==NGRBGHG(JGHG) .AND. YGHG(:)%LASSIM)) THEN
      IGHG=IGHG+1
      YLANEGHG(IGHG)%ZANE => ZANE(:,NANEBGHG(IGHG)+1 : NANEBGHG(IGHG)+NFLEVG)
    ENDIF
  ENDDO
  IF(IGHG /= NGHG_ASSIM) THEN
    CALL ABOR1 ('VEC2GP: Wrong number of GHG fields, IGHG/=NGHG_ASSIM')
  ENDIF
ENDIF

IF(NCHEM_ASSIM > 0) THEN
  ICHEM=0
  DO JCHEM=1,NCHEM
    IF(ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE==YCHEM(JCHEM)%IGRBCODE .AND. YCHEM(JCHEM)%LASSIM)) THEN
      ICHEM=ICHEM+1
      YLANECHEM(ICHEM)%ZANE => ZANE(:,NANEBCHEM(ICHEM)+1 : NANEBCHEM(ICHEM)+NFLEVG)
    ENDIF
  ENDDO
  IF(ICHEM /= NCHEM_ASSIM) THEN
    CALL ABOR1 ('VEC2GP: Wrong number of CHEM fields, ICHEM/=NCHEM_ASSIM')
  ENDIF
ENDIF

IF(JB_STRUCT%JB_DATA%NAEROCV > 0) THEN
 IAERO=0
 IF(JB_STRUCT%JB_DATA%NAEROCV == 2) THEN  
    IF(ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE== NGRBAERLG )) THEN
      IAERO=IAERO+1
      YLANEAERO(IAERO)%ZANE => ZANE(:,NANEBAERO(IAERO)+1 : NANEBAERO(IAERO)+NFLEVG)
    ENDIF
    IF(ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE== NGRBAERSM )) THEN
      IAERO=IAERO+1
      YLANEAERO(IAERO)%ZANE => ZANE(:,NANEBAERO(IAERO)+1 : NANEBAERO(IAERO)+NFLEVG)
    ENDIF
 ELSE
    IF(ANY(JB_STRUCT%SPJB_VARS_INFO(:)%IGRIBCODE== NGRBAERLG )) THEN
      IAERO=IAERO+1
      YLANEAERO(IAERO)%ZANE => ZANE(:,NANEBAERO(IAERO)+1 : NANEBAERO(IAERO)+NFLEVG)
    ENDIF
 ENDIF ! BEN - DUAL CV
ENDIF

!--- decide whether to add or subtract squares

IF (CDCONF == 'A') THEN
  ZSIGN =  1.0_JPRB
ELSE
  ZSIGN = -1.0_JPRB
ENDIF

!--- read analysis error buffer

IST   = 1
IEND  = KNUMB
ISTC  = 1
IENDC = KNUMB

ZANE = ANEBUF (:, :, 1+(KSTGLO-1)/YDGEOMETRY%YRDIM%NPROMA)


!--- read surface fields

! Have to set these to zero as GPOPER does not know about ist:iend
ZTEMP(:,:) =0.0_JPRB
ZTEMPC(:,:)=0.0_JPRB
ZTEMP(IST:IEND,:)=PTRAJ_SRFC%MIX_SRFC(IST:IEND,:)
ZTEMPC(IST:IEND,:)=PTRAJ_CST%MIX_CST (IST:IEND,:)
CALL GPOPER(YDGEOMETRY%YRDIM,YDMODEL%YRML_DYN%YRDYN,'GETTRAJ',YDSURF,PSP_SB=ZSP_SB5,PSP_SG=ZSP_SG5,PSP_RR=ZSP_RR5,PSD_VF=ZSD_VF5,&
 & PFIELD=ZTEMP,PFIELD2=ZTEMPC)
IF (LPRTTRAJ.AND.PTRAJ_SRFC%LASTCHUNK) WRITE(NULOUT,*)'GREPTRAJ READ TRAJ_CST and TRAJ_SRFC in VEC2GP'

!--- calculate observed quantites, at g.p.
!--- prepare request of a number of tovs channels and channel numbers

ILEN=1+IEND-IST
IF(LBGOBS) THEN
  ISOBT=1
  INRCH =NANERADS
  ICH(:)=NANERADL(:)
  DO JROF=IST,IEND
    IF (LECMWF) THEN
      ZCI(JROF)=ZSD_VF5(1,YSD_VF%YCI%MP)
      ZVEG(JROF)=&
       & ZSD_VF5(JROF,YSD_VF%YCVH%MP)*ZRVCOV(NINT(ZSD_VF5(JROF,YSD_VF%YTVH%MP)))+&
       & ZSD_VF5(JROF,YSD_VF%YCVL%MP)*ZRVCOV(NINT(ZSD_VF5(JROF,YSD_VF%YTVL%MP)))  
    ELSE
      ZVEG(JROF)=ZSD_VF5(1,YSD_VF%YVEG%MP)
      IF (ZSP_RR5(JROF,YSP_RR%YT%MP) < ZRTFREEZSICE) THEN
        ZCI(JROF)=1.
      ELSE
        ZCI(JROF)=0.
      ENDIF
    ENDIF
  ENDDO
  ZRADM(:,:)=0.0_JPRB              ! BGOBS might not perform Radiance calc.
  CALL BGOBS(YDGEOMETRY,YDMODEL,CDCONF,ILEN,ICH,INRCH,POROG,PGELAT,PGELAM,&
   & ZSD_VF5(IST,YSD_VF%YLSM%MP),ZSP_RR5(IST,YSP_RR%YT%MP),ZSP_SB5(IST,1,YSP_SB%YQ%MP),&
   & ZSP_SG5(1,1,YSP_SG%YF%MP),ZSD_VF5(IST,YSD_VF%YZ0F%MP),ZVEG ,ZCI,&
   & PU(IST,1),PV(IST,1),PT(IST,1),&
   & PQ(IST,1),PO3(IST,1),PGFL(IST,1,1),PSP(IST),PPERTTS(IST),&
   & PU5(IST,1),PV5(IST,1),PT5(IST,1),&
   & PQ5(IST,1),PO35(IST,1),PGFL5(IST,1,1),PSP5(IST),&
   & ZUPP(IST,1),ZVPP(IST,1),ZTPP(IST,1),&
   & ZRPP(IST,1),ZQPP(IST,1),ZO3PP(IST,1),ZGHGPP(IST,1,1),ZAEROPP(IST,1,1),&
   & ZCHEMPP(IST,1,1),ZRADM(IST,1),ZZPP(IST,1),ZTCW(IST),ZTO3(IST),&
   & ZRH2M(IST),ZU10M(IST),ZV10M(IST),ZT2M(IST),YDGOM,YDGOM5)
ELSE
!--- calculate linearized geopotential
  CALL SIGAM(YDMODEL%YRCST,YDGEOMETRY,YDMODEL%YRML_DYN%YRDYN,NPROMA,1,ZZPP(IST,1),PT(IST,1),PSP(IST),ILEN,NFLEVG)
  DO JLEV=1,NFLEVG
    DO JROF=IST,IEND
      ZUPP(JROF,JLEV)=PU(JROF,JLEV)
      ZVPP(JROF,JLEV)=PV(JROF,JLEV)
      ZTPP(JROF,JLEV)=PT(JROF,JLEV)
      ZQPP(JROF,JLEV)=PQ(JROF,JLEV)
      IF(LLO3JB) ZO3PP(JROF,JLEV)=PO3(JROF,JLEV)
      DO IGHG=1,NGHG_ASSIM
        ZGHGPP(JROF,JLEV,IGHG)=0.0 !stj  NEEDS PROPER VALUES
      ENDDO
      DO IAERO=1,JB_STRUCT%JB_DATA%NAEROCV
        ZAEROPP(JROF,JLEV,IAERO)=0.0 !stj  NEEDS PROPER VALUES
      ENDDO
      DO ICHEM=1,NCHEM_ASSIM
        ZCHEMPP(JROF,JLEV,ICHEM)=0.0 !std  NEEDS PROPER VALUES
      ENDDO
    ENDDO
  ENDDO
ENDIF

!--- accumulate model upper air variances

DO JLEV=1,NFLEVG
!OCL NOALIAS
  DO JROF=IST,IEND
    ZANEU  (JROF,JLEV) = ZANEU(JROF,JLEV)+ZSIGN*ZUPP(JROF,JLEV)**2
    ZANEV  (JROF,JLEV) = ZANEV(JROF,JLEV)+ZSIGN*ZVPP(JROF,JLEV)**2
    ZANET  (JROF,JLEV) = ZANET(JROF,JLEV)+ZSIGN*ZTPP(JROF,JLEV)**2
    ZANEZ  (JROF,JLEV) = ZANEZ(JROF,JLEV)+ZSIGN*ZZPP(JROF,JLEV)**2
    IF(LBGOBS) ZANER  (JROF,JLEV) = ZANER(JROF,JLEV)+ZSIGN*ZRPP(JROF,JLEV)**2
    ZANEQ  (JROF,JLEV) = ZANEQ(JROF,JLEV)+ZSIGN*ZQPP(JROF,JLEV)**2
    ZANEDIV(JROF,JLEV) = ZANEDIV(JROF,JLEV)+ZSIGN*PDIV(JROF,JLEV)**2
    ZANEVOR(JROF,JLEV) = ZANEVOR(JROF,JLEV)+ZSIGN*PVOR(JROF,JLEV)**2
    IF(LECMWF.AND.LLO3JB) ZANEO3(JROF,JLEV) = ZANEO3(JROF,JLEV)+ZSIGN*ZO3PP(JROF,JLEV)**2
    DO IGHG=1,NGHG_ASSIM
      YLANEGHG(IGHG)%ZANE(JROF,JLEV) = YLANEGHG(IGHG)%ZANE(JROF,JLEV)+ZSIGN*ZGHGPP(JROF,JLEV,IGHG)**2
    ENDDO
    DO ICHEM=1,NCHEM_ASSIM
      YLANECHEM(ICHEM)%ZANE(JROF,JLEV) = YLANECHEM(ICHEM)%ZANE(JROF,JLEV)+ZSIGN*ZCHEMPP(JROF,JLEV,ICHEM)**2
    ENDDO
    DO IAERO=1,JB_STRUCT%JB_DATA%NAEROCV
      YLANEAERO(IAERO)%ZANE(JROF,JLEV) = YLANEAERO(IAERO)%ZANE(JROF,JLEV)+ZSIGN*ZAEROPP(JROF,JLEV,IAERO)**2
    ENDDO
  ENDDO
ENDDO

!--- accumulate tovs radiance variances

IF(LBGOBS) THEN
  DO JCH=1,NANERADS
!OCL NOALIAS
    DO JROF=IST,IEND
      ZANERAD(JROF,JCH) = ZANERAD(JROF,JCH)+ZSIGN*ZRADM(JROF,JCH)**2
    ENDDO
  ENDDO
ENDIF

!--- accumulate model surface variances

!OCL NOALIAS
DO JROF=IST,IEND
  ZANESP(JROF)  = ZANESP(JROF)  + ZSIGN*PSP(JROF)**2
  IF(LBGOBS.AND.LECMWF) THEN
    ZANETCW(JROF) = ZANETCW(JROF) + ZSIGN*ZTCW(JROF)**2
    ZANERH2(JROF) = ZANERH2(JROF) + ZSIGN*ZRH2M(JROF)**2
    ZANEU10(JROF) = ZANEU10(JROF) + ZSIGN*ZU10M(JROF)**2
    ZANEV10(JROF) = ZANEV10(JROF) + ZSIGN*ZV10M(JROF)**2
    ZANET2 (JROF) = ZANET2 (JROF) + ZSIGN*ZT2M(JROF)**2
    IF(LLO3JB) ZANETO3(JROF) = ZANETO3(JROF) + ZSIGN*ZTO3(JROF)**2
  ENDIF
ENDDO

!--- write buffer

ANEBUF (:, :, 1+(KSTGLO-1)/YDGEOMETRY%YRDIM%NPROMA) = ZANE


!-----------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('VEC2GP',1,ZHOOK_HANDLE)
END SUBROUTINE VEC2GP
