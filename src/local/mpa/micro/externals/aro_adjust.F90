!     ######spl
      SUBROUTINE  ARO_ADJUST(KKA,KKU,KKL,KLON,KLEV,  KRR,  &
                                  NGFL_EZDIAG, &
                                  HFRAC_ICE, OSUBG_COND, OSIGMAS, &
                                  CMICRO, OCND2, &
                                  PTSTEP, PSIGQSAT, &
                                  PZZF, PRHODJ, PEXNREF,&
                                  PPABSM, PTHT, PRT, PSIGS, &
                                  PMFCONV, PRC_MF, PRI_MF, PCF_MF, &
                                  PTHS, PRS,  PSRCS, PCLDFR,&
                                  PGP2DSPP,PEZDIAG_POS, &
                                  YDDDH,YDLDDH,YDMDDH)
      USE PARKIND1, ONLY : JPRB
      USE YOMHOOK , ONLY : LHOOK, DR_HOOK
!     ##########################################################################
!
!!****  * -  compute the  resolved clouds and precipitation
!!
!!    PURPOSE
!!    -------
!!      The purpose of this routine is to compute the  microphysical sources
!!    related to the resolved clouds and precipitation
!!
!!
!!**  METHOD
!!    ------
!!      The main actions of this routine is to call the routines computing the
!!    microphysical sources. Before that:
!!        - it computes the real absolute pressure,
!!        - negative values of the current guess of all mixing ratio are removed.
!!          This is done by a global filling algorithm based on a multiplicative
!!          method (Rood, 1987), in order to conserved the total mass in the
!!          simulation domain.
!!        - Sources are transformed in physical tendencies, by removing the
!!          multiplicative term Rhod*J.
!!        - External points values are filled owing to the use of cyclic
!!          l.b.c., in order to performe computations on the full domain.
!!      After calling to microphysical routines, the physical tendencies are
!!    switched back to prognostic variables.
!!
!!
!!    EXTERNAL
!!    --------
!!      Subroutine FMLOOK: to recover the logical unit number linked to a FMfile
!!      Subroutine SLOW_TERMS: Computes the explicit microphysical sources
!!      Subroutine FAST_TERMS: Performs the saturation adjustment for l
!!      Subroutine RAIN_ICE  : Computes the explicit microphysical sources for i
!!      Subroutine ICE_ADJUST: Performs the saturation adjustment for i+l
!!      MIN_ll,SUM3D_ll : distributed functions equivalent to MIN and SUM
!!
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!      Module MODD_PARAMETERS : contains declarations of parameter variables
!!         JPHEXT       : Horizontal external points number
!!         JPVEXT       : Vertical external points number
!!      Module MODD_CST
!!          XP00               ! Reference pressure
!!          XRD                ! Gaz  constant for dry air
!!          XCPD               ! Cpd (dry air)
!!
!!    REFERENCE
!!    ---------
!!
!!      Documentation AROME
!!
!!    AUTHOR
!!    ------
!!    S.Malardel and Y.Seity
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    10/03/03
!!      T. Kovacic  11-05-05, Call to budgets for NEGA1_
!!      S. Riette ice for EDKF
!!      2012-02 Y. Seity,  add possibility to run with reversed vertical levels
!!      2016-11 S. Riette: new ice_adjust interface, add OLD3/OLD4 schemes
!!      2020-12 U. Andrae : Introduce SPP for HARMONIE-AROME
!!
!-------------------------------------------------------------------------------
!
!*       0.    DECLARATIONS
!              ------------
!
USE MODD_CONF
USE MODD_CST
USE MODD_PARAMETERS
USE MODD_RAIN_ICE_DESCR
USE MODD_BUDGET
USE MODD_SPP_TYPE
!
USE MODI_ICE_ADJUST
USE MODI_BUDGET
USE SPP_MOD, ONLY : YSPP_CONFIG,YSPP
!
USE DDH_MIX , ONLY : TYP_DDH
USE YOMLDDH , ONLY : TLDDH
USE YOMMDDH , ONLY : TMDDH
!
IMPLICIT NONE
!
!*       0.1   Declarations of dummy arguments :
!
!

!
INTEGER,                  INTENT(IN)   :: KKA    !near ground array index
INTEGER,                  INTENT(IN)   :: KKU    !uppest atmosphere array index
INTEGER,                  INTENT(IN)   :: KKL    !vert. levels type 1=MNH -1=ARO
INTEGER,                  INTENT(IN)   :: KLON     !NPROMA under CPG
INTEGER,                  INTENT(IN)   :: KLEV     !Number of vertical levels
INTEGER,                  INTENT(IN)   :: KRR      ! Number of moist variables
INTEGER,                  INTENT(IN)   :: NGFL_EZDIAG  ! Diagnostic array dimension
CHARACTER*1,              INTENT(IN)   :: HFRAC_ICE
LOGICAL,                  INTENT(IN)   :: OSUBG_COND ! Switch for Subgrid Cond.
LOGICAL,                  INTENT(IN)   :: OSIGMAS  ! Switch for Sigma_s:
                                        ! use values computed in CONDENSATION
                                        ! or that from turbulence scheme
CHARACTER (LEN=4),        INTENT(IN)   :: CMICRO  ! Microphysics scheme
LOGICAL,                  INTENT(IN)   :: OCND2
REAL,                     INTENT(IN)   :: PTSTEP   ! Time step
REAL,                     INTENT(IN)   :: PSIGQSAT ! coeff applied to qsat variance contribution
!
!
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PZZF     ! Height (z)
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PRHODJ  !Dry density * Jacobian
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PEXNREF ! Reference Exner function
!
!
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PPABSM  ! abs. pressure at time t-dt
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PTHT    ! Theta at time t
REAL, DIMENSION(KLON,1,KLEV,KRR), INTENT(INOUT) :: PRT     ! Moist variables at time t
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PSIGS   ! Sigma_s at time t
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PMFCONV ! convective mass flux
REAL, DIMENSION(KLON,1,KLEV),   INTENT(IN)   :: PRC_MF, PRI_MF, PCF_MF
!
!
REAL, DIMENSION(KLON,1,KLEV),   INTENT(INOUT) :: PTHS  ! Theta source
REAL, DIMENSION(KLON,1,KLEV,KRR), INTENT(INOUT) :: PRS   ! Moist  variable sources
!
!
REAL, DIMENSION(KLON,1,KLEV),   INTENT(OUT)   :: PSRCS ! Second-order flux
                                                 ! s'rc'/2Sigma_s2 at time t+1
                                                 ! multiplied by Lambda_3
REAL, DIMENSION(KLON,1,KLEV), INTENT(INOUT)   :: PCLDFR! Cloud fraction
REAL, DIMENSION(KLON,YSPP%N2D), TARGET, INTENT(INOUT) :: PGP2DSPP
REAL, DIMENSION(KLON,KLEV), INTENT(INOUT) :: PEZDIAG_POS
!
TYPE(TYP_DDH), INTENT(INOUT) :: YDDDH
TYPE(TLDDH), INTENT(IN) :: YDLDDH
TYPE(TMDDH), INTENT(IN) :: YDMDDH
!
!*       0.2   Declarations of local variables :

CHARACTER*4               :: HBUNAME  ! Name of the budget
!
INTEGER :: JRR           ! Loop index for the moist and scalar variables
!
REAL, DIMENSION(SIZE(PZZF,1),SIZE(PZZF,2),SIZE(PZZF,3)):: ZT,ZLV,ZLS,ZCPH
REAL, DIMENSION(SIZE(PZZF,1),SIZE(PZZF,2),SIZE(PZZF,3)):: ZCOR
                                    ! for the correction of negative rv
REAL, DIMENSION(SIZE(PZZF,1),SIZE(PZZF,2),SIZE(PZZF,3)):: ZZZ
                                    ! model layer height
REAL  :: ZMASSTOT                   ! total mass  for one water category
                                    ! including the negative values
REAL  :: ZMASSPOS                   ! total mass  for one water category
                                    ! after removing the negative values
REAL  :: ZRATIO                     ! ZMASSTOT / ZMASSCOR
!
TYPE(TSPP_CONFIG_MPA) :: YSPP_PSIGQSAT,YSPP_ICE_CLD_WGT
!
!
!------------------------------------------------------------------------------
!
!*       1.     PRELIMINARY COMPUTATIONS
!               ------------------------
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('ARO_ADJUST',0,ZHOOK_HANDLE)

! Copy SPP settings
IF ( YSPP_CONFIG%LSPP ) THEN

  IF ( YSPP_CONFIG%LPERT_PSIGQSAT ) &
  CALL SET_SPP_TYPE(YSPP_PSIGQSAT, &
   YSPP_CONFIG%LLNN_MEAN1, YSPP_CONFIG%LLNN_MEAN1_PSIGQSAT, &
   YSPP_CONFIG%CMPERT_PSIGQSAT, YSPP_CONFIG%SDEV, &
   YSPP_CONFIG%CLIP_PSIGQSAT, &
   YSPP%MP_PSIGQSAT, &
   KLON,KLEV,YSPP%N2D,NGFL_EZDIAG, &
   YSPP_CONFIG%IEZDIAG_POS, &
   PGP2DSPP,PSIGQSAT,PEZDIAG_POS)
                    
  IF ( YSPP_CONFIG%LPERT_ICE_CLD_WGT ) &
  CALL SET_SPP_TYPE(YSPP_ICE_CLD_WGT, &
   YSPP_CONFIG%LLNN_MEAN1, YSPP_CONFIG%LLNN_MEAN1_ICE_CLD_WGT, &
   YSPP_CONFIG%CMPERT_ICE_CLD_WGT, YSPP_CONFIG%SDEV, &
   YSPP_CONFIG%CLIP_ICE_CLD_WGT, &
   YSPP%MP_ICE_CLD_WGT, &
   KLON,KLEV,YSPP%N2D,NGFL_EZDIAG, &
   YSPP_CONFIG%IEZDIAG_POS, &
   PGP2DSPP,1.0_JPRB,PEZDIAG_POS)
   ! Awaiting merge of HARMONIE-AROME physics 
   !PGP2DSPP,XFRMIN(21),PEZDIAG)

ENDIF 

HBUNAME='DEPI'
!
!*       2.     TRANSFORMATION INTO PHYSICAL TENDENCIES
!               ---------------------------------------
!
!
! personal comment:  tranfering these variables to the
!                    microphysical routines would save
!                    computing time
!
ZT(:,:,:)= PTHT(:,:,:)*PEXNREF(:,:,:)
ZLV(:,:,:)=XLVTT +(XCPV-XCL) *(ZT(:,:,:)-XTT)
ZLS(:,:,:)=XLSTT +(XCPV-XCI) *(ZT(:,:,:)-XTT)
ZCPH(:,:,:)=XCPD +XCPV*2.*PTSTEP*PRS(:,:,:,1)
!
!
!*       3.     REMOVE NEGATIVE VALUES
!               ----------------------
!
!*       3.1    Non local correction for precipitating species (Rood 87)
!
IF (CMICRO == 'KESS' .OR. CMICRO == 'ICE3' .OR. CMICRO == 'ICE2' &
    .OR.  CMICRO == 'C2R2' .OR. CMICRO == 'C3R5'.OR. CMICRO == 'ICE4' &
    .OR. CMICRO == 'OLD3' .OR. CMICRO == 'OLD4') THEN

  DO JRR = 3,KRR
    SELECT CASE (JRR)
      CASE(3,5,6,7) ! rain, snow, graupel and hail

        IF ( MINVAL( PRS(:,:,:,JRR)) < 0.0 ) THEN
! For AROME, we cannot use MAX_ll so that according to JPP's advises
!  we only correct negative values but not the total mass
! compute the total water mass computation
!
!          ZMASSTOT = MAX( 0. , SUM( PRS(:,:,:,JRR) ))
!
! remove the negative values
!
          PRS(:,:,:,JRR) = MAX( 0., PRS(:,:,:,JRR) )
!
! compute the new total mass
!
!          ZMASSPOS = MAX(1.E-60,SUM( PRS(:,:,:,JRR) ))
!
! correct again in such a way to conserve the total mass
!
!          ZRATIO = ZMASSTOT / ZMASSPOS
!          PRS(:,:,:,JRR) = PRS(:,:,:,JRR) * ZRATIO

        END IF
    END SELECT
  END DO
END IF
!
!*       3.2    Adjustement for liquid and solid cloud
!
SELECT CASE ( CMICRO )
!
!
    CASE('ICE2','ICE3','ICE4', 'OLD3', 'OLD4')
    WHERE (PRS(:,:,:,4) < 0.)
      PRS(:,:,:,1) = PRS(:,:,:,1) + PRS(:,:,:,4)
      PTHS(:,:,:) = PTHS(:,:,:) - PRS(:,:,:,4) * ZLS(:,:,:) /  &
           ZCPH(:,:,:) / PEXNREF(:,:,:)
      PRS(:,:,:,4) = 0.
    END WHERE
!
!   cloud
    WHERE (PRS(:,:,:,2) < 0.)
      PRS(:,:,:,1) = PRS(:,:,:,1) + PRS(:,:,:,2)
      PTHS(:,:,:) = PTHS(:,:,:) - PRS(:,:,:,2) * ZLV(:,:,:) /  &
           ZCPH(:,:,:) / PEXNREF(:,:,:)
      PRS(:,:,:,2) = 0.
    END WHERE
!
! if rc or ri are positive, we can correct negative rv
!   cloud
    WHERE ((PRS(:,:,:,1) <0.) .AND. (PRS(:,:,:,2)> 0.) )
      PRS(:,:,:,1) = PRS(:,:,:,1) + PRS(:,:,:,2)
      PTHS(:,:,:) = PTHS(:,:,:) - PRS(:,:,:,2) * ZLV(:,:,:) /  &
           ZCPH(:,:,:) / PEXNREF(:,:,:)
      PRS(:,:,:,2) = 0.
    END WHERE
!   ice
    IF(KRR > 3) THEN
      WHERE ((PRS(:,:,:,1) < 0.).AND.(PRS(:,:,:,4) > 0.))
        ZCOR(:,:,:)=MIN(-PRS(:,:,:,1),PRS(:,:,:,4))
        PRS(:,:,:,1) = PRS(:,:,:,1) + ZCOR(:,:,:)
        PTHS(:,:,:) = PTHS(:,:,:) - ZCOR(:,:,:) * ZLS(:,:,:) /  &
             ZCPH(:,:,:) / PEXNREF(:,:,:)
        PRS(:,:,:,4) = PRS(:,:,:,4) -ZCOR(:,:,:)
      END WHERE
    END IF
!
END SELECT
!
!
!*       3.3  STORE THE BUDGET TERMS
!            ----------------------
!
!IF (LBUDGET_RV) CALL BUDGET (PRS(:,:,:,1) * PRHODJ(:,:,:), 6,'NEGA_BU_RRV',YDDDH)
!IF (LBUDGET_RC) CALL BUDGET (PRS(:,:,:,2) * PRHODJ(:,:,:), 7,'NEGA_BU_RRC',YDDDH)
!IF (LBUDGET_RR) CALL BUDGET (PRS(:,:,:,3) * PRHODJ(:,:,:), 8,'NEGA_BU_RRR',YDDDH)
!IF (LBUDGET_RI) CALL BUDGET (PRS(:,:,:,4) * PRHODJ(:,:,:) ,9,'NEGA_BU_RRI',YDDDH)
!IF (LBUDGET_RS) CALL BUDGET (PRS(:,:,:,5) * PRHODJ(:,:,:),10,'NEGA_BU_RRS',YDDDH)
!IF (LBUDGET_RG) CALL BUDGET (PRS(:,:,:,6) * PRHODJ(:,:,:),11,'NEGA_BU_RRG',YDDDH)
!IF (LBUDGET_RH) CALL BUDGET (PRS(:,:,:,7) * PRHODJ(:,:,:),12,'NEGA_BU_RRH',YDDDH)
!IF (LBUDGET_TH) CALL BUDGET (PTHS(:,:,:)  * PRHODJ(:,:,:),4,'NEGA_BU_RTH',YDDDH)


!
!-------------------------------------------------------------------------------
!

!*       9.     MIXED-PHASE MICROPHYSICAL SCHEME (WITH 3 ICE SPECIES)
!               -----------------------------------------------------
!
!
!*       9.2    Perform the saturation adjustment over cloud ice and cloud water
!
    ZZZ =  PZZF
    IF (KRR==6) THEN
      CALL ICE_ADJUST ( KKA=KKA,KKU=KKU,KKL=KKL,KRR=KRR,&
        & HFRAC_ICE=HFRAC_ICE, HBUNAME=HBUNAME, &
        & OSUBG_COND=OSUBG_COND, OSIGMAS=OSIGMAS, &
        & OCND2=OCND2, &
        & PTSTEP=2*PTSTEP,PSIGQSAT=PSIGQSAT, &
        & PRHODJ=PRHODJ ,PEXNREF=PEXNREF,   &
        & PSIGS=PSIGS, PMFCONV=PMFCONV, PPABST=PPABSM, PZZ=ZZZ,    &
        & PEXN=PEXNREF, PCF_MF=PCF_MF,PRC_MF=PRC_MF,PRI_MF=PRI_MF, &
        & PRV=PRS(:,:,:,1)*2*PTSTEP, PRC=PRS(:,:,:,2)*2*PTSTEP,  &
        & PRVS=PRS(:,:,:,1), PRCS=PRS(:,:,:,2), &
        & PTH=PTHS*2*PTSTEP, PTHS=PTHS,PSRCS=PSRCS, PCLDFR=PCLDFR, &
        & PRR=PRS(:,:,:,3)*2*PTSTEP, &
        & PRI=PRS(:,:,:,4)*2*PTSTEP, PRIS=PRS(:,:,:,4), &
        & PRS=PRS(:,:,:,5)*2*PTSTEP, &
        & PRG=PRS(:,:,:,6)*2*PTSTEP, &
        & YSPP_PSIGQSAT=YSPP_PSIGQSAT, &
        & YSPP_ICE_CLD_WGT=YSPP_ICE_CLD_WGT, &
        & YDDDH=YDDDH, YDLDDH=YDLDDH, YDMDDH=YDMDDH)
    ELSE
      CALL ICE_ADJUST ( KKA=KKA,KKU=KKU,KKL=KKL,KRR=KRR,&
        & HFRAC_ICE=HFRAC_ICE, HBUNAME=HBUNAME,    &
        & OSUBG_COND=OSUBG_COND, OSIGMAS=OSIGMAS, &
        & OCND2=OCND2, &
        & PTSTEP=2*PTSTEP,PSIGQSAT=PSIGQSAT, &
        & PRHODJ=PRHODJ ,PEXNREF=PEXNREF,   &
        & PSIGS=PSIGS, PMFCONV=PMFCONV, PPABST=PPABSM, PZZ=ZZZ,    &
        & PEXN=PEXNREF, PCF_MF=PCF_MF,PRC_MF=PRC_MF,PRI_MF=PRI_MF, &
        & PRV=PRS(:,:,:,1)*2*PTSTEP, PRC=PRS(:,:,:,2)*2*PTSTEP, &
        & PRVS=PRS(:,:,:,1), PRCS=PRS(:,:,:,2), &
        & PTH=PTHS*2*PTSTEP, PTHS=PTHS,PSRCS=PSRCS, PCLDFR=PCLDFR, &
        & PRR=PRS(:,:,:,3)*2*PTSTEP, &
        & PRI=PRS(:,:,:,4)*2*PTSTEP, PRIS=PRS(:,:,:,4), &
        & PRS=PRS(:,:,:,5)*2*PTSTEP, &
        & PRG=PRS(:,:,:,6)*2*PTSTEP, &
        & PRH=PRS(:,:,:,7)*2*PTSTEP, &
        & YSPP_PSIGQSAT=YSPP_PSIGQSAT, &
        & YSPP_ICE_CLD_WGT=YSPP_ICE_CLD_WGT, &
        & YDDDH=YDDDH, YDLDDH=YDLDDH, YDMDDH=YDMDDH)
    ENDIF

 CALL CLEAR_SPP_TYPE(YSPP_PSIGQSAT)
 CALL CLEAR_SPP_TYPE(YSPP_ICE_CLD_WGT)

!
!-------------------------------------------------------------------------------
!
IF (LHOOK) CALL DR_HOOK('ARO_ADJUST',1,ZHOOK_HANDLE)
END SUBROUTINE ARO_ADJUST
